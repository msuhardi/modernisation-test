       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C869.                                                  
      *AUTHOR.       LSB.                                                       
      *DATE-WRITTEN. 05/10/95.                                                  
      *DATE-COMPILED.                                                           
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *========================================================*                
      *                                                        *                
      *    AFTER DETERMINE THE SUB-TENANT BASED ON THE ADDRESS *                
      *    FROM MHA, BEFORE UPDATE THE TENANT PRIORITY SCHEME, *                
      *    THIS PROGRAM WILL MATCH THE NRIC WITH RDB RECORDS   *                
      *    IF THE NRIC IN SUB-TENANT FILE FOUND IN RDB AS      *                
      *    LESSEE/TENANT OF DIFFERENT NEW TOWN, THEN DO NOT    *                
      *    AWARD SUB-TENANT SCHEME, AS THE MHA ADDRESS MAY NOT *                
      *    BE UP TO DATE.                                      *                
      *                                                         *               
      *  INPUT FILE :- BP13F716                                 *               
      *                P13F716A                                 *               
      *  OUTPUT FILE:- P13F716B                                 *               
      *                                                         *               
      ***********************************************************               
      *                                                        *                
      * CHG-NO   BY   ON      DESCRIPTION                      *                
      * ---------------------------------                      *                
      * BP130022 LSB 951005 - CHECK NT-REQ WITH NT-RESIDE IN   *                
      *                       RDB                              *                
      * BP130630 NOR 990203 - CHGE F715 F716 EXPAND TO 350     *                
      * BP132538 SK  040527   INCLUDE BP13K767                 *                
      * BP133610 ESA1090511   TO REPLACE BP13K767 W/ BP13K816  *                
      * BP135254 SMR 140417   TO REPLACE BP13K816 W/ BP13K813  *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716 ASSIGN TO BP13F716.                                  
           SELECT P13F716A ASSIGN TO P13F716A.                                  
           SELECT P13F716B ASSIGN TO P13F716B.                                  
           SELECT BP13L869 ASSIGN TO BP13L869.                                  
                                                                                
           SELECT BP13K813 ASSIGN  TO  BP13K813                                 
                     ORGANIZATION  IS  INDEXED                                  
                     ACCESS MODE   IS  RANDOM                                   
                     RECORD KEY    IS  K813-KEY-FLD                             
                     FILE STATUS   IS  WS-K813-STATUS.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F716   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 350 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13F716.                                                           
                                                                                
                                                                                
       FD  P13F716A   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 350 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       01  P13F716A-REC.                                                        
           05  F716A-NUM-REGN          PIC X(8).                                
           05  F716A-NUM-NRIC          PIC X(9).                                
           05  FILLER                  PIC X(33).                               
           05  F716A-NUM-SCH-ACCNT     PIC X(09).                               
           05  FILLER                  PIC X(26).                               
           05  F716A-NT-RESIDE         PIC X(3).                                
           05  FILLER                  PIC X(12).                               
           05  FILLER                  PIC X(150).                              
           05  FILLER                  PIC X(100).                              
                                                                                
       FD  P13F716B   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 350 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       01  P13F716B-REC                 PIC X(350).                             
                                                                                
       FD   BP13L869                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORDS ARE OMITTED.                                          
                                                                                
       01   BP13L869-REC          PIC X(132).                                   
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-PAGE-CNT               PIC 9(4) VALUE ZEROS.                  
           05  WS-REC-CNT                PIC 9(2) VALUE 99.                     
           05  WS-SERNO                  PIC 9(6) VALUE ZEROS.                  
           05  WS-READ-CNT-F716          PIC 9(6) VALUE ZEROS.                  
           05  WS-READ-CNT-F716A         PIC 9(6) VALUE ZEROS.                  
           05  WS-WRITE-CNT-F716B        PIC 9(6) VALUE ZEROS.                  
           05  WS-DELETE-CNT             PIC 9(6) VALUE ZEROS.                  
           05  WS-K813-STATUS            PIC  9(02) VALUE ZEROES.               
           05  WS-NT                     PIC  X(03) VALUE SPACES.               
                                                                                
       01  WS-F716-KEY.                                                         
           05  WS-F716-REGN              PIC X(8).                              
           05  WS-F716-NRIC              PIC X(9).                              
                                                                                
       01  WS-F716A-KEY.                                                        
           05  WS-F716A-REGN              PIC X(8).                             
           05  WS-F716A-NRIC              PIC X(9).                             
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CURR-YY                       PIC 9999  VALUE ZEROES.         
           05  WS-CURR-MM                       PIC 99.                         
           05  WS-CHAR-MM  REDEFINES WS-CURR-MM PIC XX.                         
           05  WS-CURR-DD                       PIC 99    VALUE 00.             
                                                                                
       01  WS-EDIT-DATE.                                                        
           05  WS-EDIT-DD                       PIC 99    VALUE 00.             
           05  FILLER                           PIC X     VALUE '/'.            
           05  WS-EDIT-MM                       PIC 99    VALUE 00.             
           05  FILLER                           PIC X     VALUE '/'.            
           05  WS-EDIT-YY                       PIC 9999  VALUE 00.             
                                                                                
                                                                                
       01  L869-HDG1.                                                           
           05  PRT-REPORT-TITLE         PIC X(9)  VALUE 'BP13L869'.             
           05  FILLER                   PIC X(38) VALUE SPACES.                 
           05  FILLER                   PIC X(40) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(28) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE :'.               
           05  PRT-DATE                 PIC X(10).                              
                                                                                
       01  L869-HDG2.                                                           
           05  FILLER                   PIC X(30) VALUE SPACES.                 
           05  FILLER                   PIC X(47) VALUE                         
               'NRIC FOUND IN RDB AS LESSEE/TENANT NOT TREATED'.                
           05  FILLER                   PIC X(40) VALUE                         
               'AS SUB-TENANT PRIORITY SCHEME'.                                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE : '.              
           05  PRT-PAGE                 PIC ZZZ9.                               
                                                                                
                                                                                
       01  L869-HDG3.                                                           
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(07) VALUE                         
               'S/NO  '.                                                        
           05  FILLER                   PIC X(60) VALUE                         
              'REF-NO     NRIC     NT-REQ MHA-NT RDB-NT  HDB-REF-NO'.           
                                                                                
      *----------------------------------------------------------------*        
      *   DETAIL RECORD                                                *        
      *----------------------------------------------------------------*        
                                                                                
       01  L869-DTL.                                                            
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  L869-SERNO               PIC ZZZ9.                               
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  L869-REGN                PIC X(8).                               
           05  FILLER                   PIC X(03) VALUE SPACES.                 
           05  L869-NRIC                PIC X(8).                               
           05  FILLER                   PIC X(04) VALUE SPACES.                 
           05  L869-NT-REQ              PIC X(3).                               
           05  FILLER                   PIC X(04) VALUE SPACES.                 
           05  L869-NT-MHA              PIC X(3).                               
           05  FILLER                   PIC X(04) VALUE SPACES.                 
           05  L869-NT-RDB              PIC X(3).                               
           05  FILLER                   PIC X(03) VALUE SPACES.                 
           05  L869-SCH-ACC-NO          PIC X(11).                              
           05  FILLER                   PIC X(02) VALUE SPACES.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       000-MAIN-LOGIC.                                                          
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURR-DATE.                    
            MOVE    WS-CURR-YY       TO     WS-EDIT-YY.                         
            MOVE    WS-CURR-MM       TO     WS-EDIT-MM.                         
            MOVE    WS-CURR-DD       TO     WS-EDIT-DD.                         
            MOVE    WS-EDIT-DATE     TO     PRT-DATE.                           
                                                                                
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F716    THRU 200-EXIT.                             
            PERFORM 250-READ-F716A   THRU 250-EXIT.                             
            PERFORM 400-PROCESS-UPDATE THRU 400-EXIT                            
                    UNTIL WS-F716-KEY = HIGH-VALUES                             
                     AND  WS-F716A-KEY = HIGH-VALUES.                           
            PERFORM 999-CLOSE-FILES  THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
                                                                                
           OPEN INPUT  BP13F716                                                 
                       P13F716A                                                 
                       BP13K813                                                 
                OUTPUT P13F716B                                                 
                       BP13L869.                                                
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K813: ' WS-K813-STATUS                 
              MOVE WS-K813-STATUS      TO RETURN-CODE                           
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F716.                                                           
           READ BP13F716 AT END                                                 
                         MOVE HIGH-VALUES TO WS-F716-KEY                        
                         GO TO 200-EXIT.                                        
                                                                                
           ADD 1                         TO WS-READ-CNT-F716.                   
           MOVE F716-NUM-REGN            TO WS-F716-REGN.                       
           MOVE F716-NUM-NRIC            TO WS-F716-NRIC.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       250-READ-F716A.                                                          
           READ P13F716A AT END                                                 
                         MOVE HIGH-VALUES TO WS-F716A-KEY                       
           GO TO 250-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-READ-CNT-F716A.                  
           MOVE F716A-NUM-REGN            TO WS-F716A-REGN.                     
           MOVE F716A-NUM-NRIC            TO WS-F716A-NRIC.                     
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-PROCESS-UPDATE.                                                      
                                                                                
           MOVE F716-CDE-NT-REQ       TO WS-NT.                                 
           PERFORM 0550-READ-BP13K813 THRU 0550-EXIT.                           
                                                                                
           IF WS-F716-KEY = WS-F716A-KEY                                        
              IF WS-NT NOT = F716A-NT-RESIDE                                    
                  ADD 1 TO WS-DELETE-CNT                                        
                  PERFORM 500-PRINT-INELIG-CASE THRU 500-EXIT                   
              ELSE                                                              
                  MOVE BP13F716-REC TO P13F716B-REC                             
                  WRITE  P13F716B-REC                                           
                 ADD 1 TO WS-WRITE-CNT-F716B                                    
              END-IF                                                            
              PERFORM 200-READ-F716    THRU 200-EXIT                            
              PERFORM 250-READ-F716A   THRU 250-EXIT                            
           ELSE                                                                 
           IF WS-F716-KEY < WS-F716A-KEY                                        
              MOVE BP13F716-REC TO P13F716B-REC                                 
              WRITE  P13F716B-REC                                               
              ADD 1 TO WS-WRITE-CNT-F716B                                       
              PERFORM 200-READ-F716    THRU 200-EXIT                            
           ELSE                                                                 
              PERFORM 250-READ-F716A   THRU 250-EXIT.                           
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-PRINT-INELIG-CASE.                                                   
                                                                                
           IF WS-REC-CNT > 55                                                   
               PERFORM 510-PRINT-HEADER THRU 510-EXIT.                          
                                                                                
           ADD    1            TO     WS-REC-CNT.                               
           ADD    1            TO     WS-SERNO.                                 
           MOVE WS-SERNO       TO L869-SERNO.                                   
           MOVE F716-NUM-REGN  TO L869-REGN.                                    
           MOVE F716-NUM-NRIC  TO L869-NRIC.                                    
           MOVE WS-NT           TO L869-NT-REQ.                                 
           MOVE F716-CDE-NT-RESIDE TO L869-NT-MHA.                              
           MOVE F716A-NT-RESIDE TO L869-NT-RDB.                                 
           MOVE F716A-NUM-SCH-ACCNT TO L869-SCH-ACC-NO.                         
                                                                                
           WRITE  BP13L869-REC             FROM   L869-DTL AFTER 1.             
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       510-PRINT-HEADER.                                                        
                                                                                
           ADD    1                   TO     WS-PAGE-CNT.                       
           MOVE   WS-PAGE-CNT         TO     PRT-PAGE.                          
           WRITE  BP13L869-REC FROM L869-HDG1  AFTER PAGE.                      
           WRITE  BP13L869-REC FROM L869-HDG2  AFTER 1.                         
           WRITE  BP13L869-REC FROM L869-HDG3  AFTER 1.                         
           MOVE   SPACES      TO   BP13L869-REC.                                
           WRITE  BP13L869-REC.                                                 
           MOVE   ZEROES      TO   WS-REC-CNT.                                  
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
       0550-READ-BP13K813.                                                      
                                                                                
           MOVE SPACES                      TO BP13K813-REC.                    
           INITIALIZE                          BP13K813-REC.                    
           MOVE WS-NT                       TO K813-NUM-ZONE.                   
           MOVE F716-CDE-FLAT-TYPE-REQ      TO K813-NUM-FLAT-TYPE.              
           MOVE F716-DTE-BALLOT             TO K813-DTE-BALLOT.                 
                                                                                
           READ BP13K813.                                                       
           IF WS-K813-STATUS = 00                                               
              MOVE K813-CDE-NT              TO WS-NT                            
           ELSE                                                                 
              IF WS-K813-STATUS = 23                                            
                 CONTINUE                                                       
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K813,STATUS ' WS-K813-STATUS           
                 MOVE WS-K813-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       0550-EXIT.                                                               
           EXIT.                                                                
                                                                                
       999-CLOSE-FILES.                                                         
                                                                                
           CLOSE BP13F716                                                       
                 P13F716A                                                       
                 P13F716B                                                       
                 BP13K813                                                       
                 BP13L869.                                                      
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K813, STATUS ' WS-K813-STATUS          
              MOVE WS-K813-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           DISPLAY '*------------------ EDTK500 -------------------'.           
                                                                                
           DISPLAY 'NO OF REC READ      BP13F716     = '                        
                    WS-READ-CNT-F716.                                           
           DISPLAY 'NO OF REC READ      BP13F716A    = '                        
                    WS-READ-CNT-F716A.                                          
           DISPLAY 'NO OF REC   WRITTEN  P13F716B    = '                        
                    WS-WRITE-CNT-F716B.                                         
           DISPLAY 'NO OF REC DELETED FROM P13F716A  = '                        
                    WS-DELETE-CNT.                                              
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
