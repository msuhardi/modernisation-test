      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CZ2W.                                                 
      *AUTHOR.        GURU.                                                     
      *DATE-WRITTEN.  04/04/18.                                                 
      *****************************************************************         
      *OBJECTIVE - GENERATE REPORT ON DETAILS OF LEASE OPTIONS FOR    *         
      *            2-ROOM FLEXI FLATS.                                *         
      *            A) VALID LEASE OPTION                              *         
      *            B) INVALID LEASE OPTION                            *         
      *                                                               *         
      *     INPUT  FILE : 1. BM06F120                                 *         
      *                   2. BM06K100                                 *         
      *                   3. BP13F205                                 *         
      *                   4. BB14K500                                 *         
      *     OUTPUT FILE : 1. BP13LZ2W (REPORT)                        *         
      *---------------------------------------------------------------*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP137281 04042018   KR13 NEW PGM                              *         
      *****************************************************************         
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F120  ASSIGN      TO BM06F120.                            
                                                                                
           SELECT BP13F205  ASSIGN      TO BP13F205.                            
                                                                                
           SELECT BM06K100  ASSIGN      TO BM06K100                             
                            ACCESS MODE IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                            RECORD KEY  IS K100-KEY-FLD                         
                            FILE STATUS IS WS-K100-STATUS.                      
                                                                                
           SELECT BB14K500  ASSIGN      TO BB14K500                             
                            ACCESS MODE IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                            RECORD KEY  IS K500-KEY-FLD                         
                            FILE STATUS IS WS-K500-STATUS.                      
                                                                                
           SELECT BP13LZ2W  ASSIGN      TO BP13LZ2W.                            
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BM06F120.                                                           
       COPY BM06F120.                                                           
                                                                                
       FD   BP13F205.                                                           
       COPY BP13F205.                                                           
                                                                                
       FD   BM06K100.                                                           
       COPY BM06K100.                                                           
                                                                                
       FD   BB14K500.                                                           
       COPY BB14K500.                                                           
                                                                                
       FD   BP13LZ2W                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       01   BP13LZ2W-REC    PIC X(200).                                         
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-F120-EOF             PIC X(1) VALUE 'N'.                      
           05  WS-F205-EOF             PIC X(1) VALUE 'N'.                      
           05  WS-K100-READ            PIC X(1) VALUE 'N'.                      
           05  WS-K500-READ            PIC X(1) VALUE 'N'.                      
           05  WS-K100-STATUS          PIC 9(2).                                
           05  WS-K500-STATUS          PIC 9(2).                                
                                                                                
           05  WS-UNIT-NO.                                                      
              10 WS-HASH               PIC X(01) VALUE '#'.                     
              10 WS-LEVEL              PIC X(02) VALUE SPACES.                  
              10 WS-DASH               PIC X(01) VALUE '-'.                     
              10 WS-UNIT               PIC X(04) VALUE SPACES.                  
                                                                                
           05  WS-BAL-LEASE            PIC 9(2) VALUE ZEROES.                   
           05  WS-F120-LEASE-TENURE    PIC 9(2) VALUE ZEROES.                   
           05  WS-F205-DTE-END.                                                 
               10 WS-DTE-END-YY        PIC 9(4) VALUE ZEROES.                   
               10 WS-DTE-END-MM        PIC 9(2) VALUE ZEROES.                   
           05  WS-K500-LEASE-COMM-DT.                                           
               10 WS-LEASE-DT-YY       PIC 9(4) VALUE ZEROES.                   
               10 WS-LEASE-DT-MM       PIC 9(2) VALUE ZEROES.                   
           05  WS-LEASE-COMM-DT.                                                
               10 WS-LEASE-DA          PIC 9(2) VALUE ZEROES.                   
               10 WS-SLASH             PIC X(1) VALUE '/'.                      
               10 WS-LEASE-MM          PIC 9(2) VALUE ZEROES.                   
               10 WS-SLASH1            PIC X(1) VALUE '/'.                      
               10 WS-LEASE-YY          PIC 9(4) VALUE ZEROES.                   
           05  WS-DIFF-YY              PIC 9(3) VALUE ZEROES.                   
           05  WS-DIFF-MM              PIC 9(2) VALUE ZEROES.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F120-READ            PIC 9(07) VALUE ZEROES.                  
           05  WS-F205-READ            PIC 9(07) VALUE ZEROES.                  
           05  WS-K100-MTCH            PIC 9(07) VALUE ZEROES.                  
           05  WS-K500-MTCH            PIC 9(07) VALUE ZEROES.                  
           05  WS-K100-NMTCH           PIC 9(07) VALUE ZEROES.                  
           05  WS-K500-NMTCH           PIC 9(07) VALUE ZEROES.                  
           05  WS-LZ2W-SNO             PIC 9(07) VALUE ZEROES.                  
           05  CUR-DTE                 PIC 9(06) VALUE ZEROES.                  
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC       PIC 9(2).                                
                   15  WS-SYS-YY       PIC 9(2).                                
               10  WS-SYS-MM           PIC 9(2).                                
               10  WS-SYS-DD           PIC 9(2).                                
                                                                                
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-MM               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-YY               PIC 9(4).                         
                                                                                
                                                                                
      *============================================================*            
      *         BP13LZ2W  -  R E P O R T    L A Y O U T            *            
      *============================================================*            
                                                                                
       01  LZ2W-HEADER-01.                                                      
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE 'BP13LZ2W'.              
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE 'HDBCAT 3'.              
           05  FILLER                  PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(54) VALUE                          
               'REPORT ON DETAILS OF LEASE OPTIONS FOR 2-ROOM FLEXI FL'.        
           05  FILLER                  PIC X(15) VALUE                          
                                         'ATS IN AUG 2018'.                     
           05  FILLER                  PIC X(51) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE 'DATE :'.                
           05  LZ2W-RUN-DATE           PIC X(10) VALUE SPACES.                  
                                                                                
       01  LZ2W-HEADER-02.                                                      
           05  FILLER                  PIC X(05) VALUE ' S/N '.                 
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(13) VALUE 'SCHEME ACC NO'.         
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(26) VALUE                          
                                           'BLK    UNIT         STREET'.        
           05  FILLER                  PIC X(21) VALUE SPACES.                  
           05  FILLER                  PIC X(11) VALUE ' COMM DATE '.           
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(13) VALUE 'BALANCE LEASE'.         
           05  FILLER                  PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(11) VALUE 'VALID LEASE'.           
           05  FILLER                  PIC X(08) VALUE ' OPTIONS'.              
           05  FILLER                  PIC X(19) VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE 'NON-VALID LEAS'.        
           05  FILLER                  PIC X(09) VALUE 'E OPTIONS'.             
                                                                                
       01  LZ2W-HEADER-03.                                                      
           05  FILLER                  PIC X(163) VALUE ALL ' '.                
                                                                                
       01  LZ2W-DETAIL.                                                         
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  LZ2W-SNO                PIC ZZZZ9.                               
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LZ2W-SCH-ACC            PIC X(11).                               
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  LZ2W-BLOCK              PIC X(05).                               
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  LZ2W-UNIT-NO            PIC X(08).                               
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  LZ2W-STREET             PIC X(32).                               
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  LZ2W-COMM-DATE          PIC X(10).                               
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  LZ2W-BAL-LEASE          PIC ZZ9.                                 
           05  FILLER                  PIC X(08) VALUE SPACES.                  
           05  LZ2W-VALID              PIC X(46).                               
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  LZ2W-NON-VALID          PIC X(15).                               
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
                                                                                
           PERFORM 2000-READ-BM06F120       THRU 2000-EXIT.                     
                                                                                
           PERFORM 3000-READ-BP13F205       THRU 3000-EXIT.                     
                                                                                
           PERFORM 4000-PROCESS-RECORDS     THRU 4000-EXIT                      
                                    UNTIL WS-F120-EOF = 'Y'.                    
                                                                                
           PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BM06F120                                                 
                       BP13F205                                                 
                       BM06K100                                                 
                       BB14K500                                                 
                OUTPUT BP13LZ2W.                                                
                                                                                
           IF WS-K100-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BM06K100 OPEN ERROR ' WS-K100-STATUS                     
              MOVE WS-K100-STATUS           TO RETURN-CODE                      
              PERFORM 9999-CLOSE-ROUTINE    THRU 9999-EXIT                      
           END-IF                                                               
                                                                                
           IF WS-K500-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB14K500 OPEN ERROR ' WS-K500-STATUS                     
              MOVE WS-K500-STATUS           TO RETURN-CODE                      
              PERFORM 9999-CLOSE-ROUTINE    THRU 9999-EXIT                      
           END-IF                                                               
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-SYSTEM-DATE.                  
           MOVE WS-SYS-DD                   TO WS-EDITED-DD.                    
           MOVE WS-SYS-MM                   TO WS-EDITED-MM.                    
           MOVE WS-SYS-CCYY                 TO WS-EDITED-YY.                    
           MOVE WS-EDITED-DATE              TO LZ2W-RUN-DATE.                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BM06F120.                                                      
      ******************************************************************        
                                                                                
           READ BM06F120 AT END                                                 
                MOVE 'Y'                    TO WS-F120-EOF                      
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
           ADD +1                           TO WS-F120-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-READ-BP13F205.                                                      
      ******************************************************************        
                                                                                
           READ BP13F205 AT END                                                 
                MOVE 'Y'                    TO WS-F205-EOF                      
                GO TO 3000-EXIT                                                 
           END-READ.                                                            
      *    DISPLAY 'F205 END DATE: ' F205-DTE-END                               
           ADD +1                           TO WS-F205-READ.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 4100-READ-BM06K100       THRU 4100-EXIT                      
                                                                                
           IF WS-K100-READ = 'Y'                                                
                                                                                
              PERFORM 4200-READ-BB14K500    THRU 4200-EXIT                      
                                                                                
              IF WS-K500-READ = 'Y'                                             
                                                                                
                 PERFORM 4300-CALC-BALANCE  THRU 4300-EXIT                      
                                                                                
                 PERFORM 4400-WRITE-REPORT  THRU 4400-EXIT                      
              END-IF                                                            
           END-IF                                                               
                                                                                
           PERFORM 2000-READ-BM06F120       THRU 2000-EXIT.                     
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4100-READ-BM06K100.                                                      
      ****************************************************************          
                                                                                
           INITIALIZE K100-REC                                                  
                                                                                
           MOVE F120-AIX1                   TO K100-KEY-FLD                     
                                                                                
           READ BM06K100.                                                       
      **   DISPLAY 'K100-KEY-FLD: ' K100-KEY-FLD '/ CDE : ' K100-CDE-BLK        
                                                                                
           EVALUATE WS-K100-STATUS                                              
               WHEN 00                                                          
                  MOVE 'Y'                  TO WS-K100-READ                     
                  ADD 1                     TO WS-K100-MTCH                     
               WHEN 10                                                          
               WHEN 23                                                          
                  MOVE 'N'                  TO WS-K100-READ                     
                  ADD 1                     TO WS-K100-NMTCH                    
               WHEN OTHER                                                       
                  DISPLAY 'ERROR WHEN READING BM06K100. STATUS IS '             
                       WS-K100-STATUS                                           
                  MOVE WS-K100-STATUS       TO RETURN-CODE                      
                  PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4200-READ-BB14K500.                                                      
      ****************************************************************          
                                                                                
           INITIALIZE BB14K500-REC                                              
                                                                                
           MOVE  K100-CDE-BLK               TO K500-CDE-BLK                     
                                                                                
           READ BB14K500.                                                       
      *    DISPLAY 'K500 CDE: ' K500-CDE-BLK '/ COMM DATE: '                    
      *             K500-DTE-LEASE-COMM-FLAT                                    
           EVALUATE WS-K500-STATUS                                              
               WHEN 00                                                          
                  MOVE 'Y'                  TO WS-K500-READ                     
                  ADD 1                     TO WS-K500-MTCH                     
               WHEN 10                                                          
               WHEN 23                                                          
                  MOVE 'N'                  TO WS-K500-READ                     
                  ADD 1                     TO WS-K500-NMTCH                    
               WHEN OTHER                                                       
                  DISPLAY 'ERROR WHEN READING BB14K500. STATUS IS '             
                       WS-K500-STATUS                                           
                  MOVE WS-K500-STATUS       TO RETURN-CODE                      
                  PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4300-CALC-BALANCE.                                                       
      ****************************************************************          
           DISPLAY 'F120 :   ' F120-KEY-FLD                                     
                   ' R :   ' F120-REGN-NO                                       
                   ' LEASE:   ' F120-NUM-LEASE-TENURE                           
                   ' F205 DTE END: ' F205-DTE-END                               
                   ' K500 COMM DT: ' K500-DTE-LEASE-COMM-FLAT                   
           IF F120-NUM-LEASE-TENURE IS NOT NUMERIC                              
              MOVE ZEROS                    TO WS-F120-LEASE-TENURE             
           ELSE                                                                 
              MOVE F120-NUM-LEASE-TENURE    TO WS-F120-LEASE-TENURE             
           END-IF                                                               
           IF F205-DTE-END IS NOT NUMERIC                                       
              MOVE ZEROS                    TO WS-F205-DTE-END                  
           ELSE                                                                 
              MOVE F205-DTE-END(1:6)        TO WS-F205-DTE-END                  
           END-IF                                                               
           IF K500-DTE-LEASE-COMM-FLAT IS NOT NUMERIC                           
              MOVE ZEROS                    TO WS-K500-LEASE-COMM-DT            
                                               LZ2W-COMM-DATE                   
           ELSE                                                                 
              MOVE K500-DTE-LEASE-COMM-FLAT(1:6)                                
                                            TO WS-K500-LEASE-COMM-DT            
              MOVE K500-DTE-LEASE-COMM-FLAT(1:4) TO WS-LEASE-YY                 
              MOVE K500-DTE-LEASE-COMM-FLAT(5:2) TO WS-LEASE-MM                 
              MOVE K500-DTE-LEASE-COMM-FLAT(7:2) TO WS-LEASE-DA                 
              MOVE WS-LEASE-COMM-DT              TO LZ2W-COMM-DATE              
           END-IF                                                               
                                                                                
                                                                                
           COMPUTE WS-DIFF-YY = WS-DTE-END-YY - WS-LEASE-DT-YY                  
                                                                                
           COMPUTE WS-DIFF-MM = WS-DTE-END-MM - WS-LEASE-DT-MM                  
                                                                                
           IF WS-DIFF-MM >= 6                                                   
              ADD +1                        TO WS-DIFF-YY                       
           END-IF                                                               
                                                                                
      **   COMPUTE WS-BAL-LEASE = WS-F120-LEASE-TENURE - WS-DIFF-YY             
           COMPUTE WS-BAL-LEASE = 99 - WS-DIFF-YY                               
                                                                                
           DISPLAY 'BAL LEASE1: ' WS-BAL-LEASE                                  
                                                                                
           IF WS-BAL-LEASE < 65                                                 
              COMPUTE WS-BAL-LEASE = 99 - WS-BAL-LEASE                          
           END-IF                                                               
                                                                                
           DISPLAY 'BAL LEASE2: ' WS-BAL-LEASE                                  
           EVALUATE TRUE                                                        
               WHEN WS-BAL-LEASE >= 85                                          
                 MOVE '99, 15, 20, 25, 30, 35, 40, 45, 50, 55, 60, 65'          
                                            TO LZ2W-VALID                       
                    MOVE SPACES             TO LZ2W-NON-VALID                   
               WHEN WS-BAL-LEASE >= 80 AND WS-BAL-LEASE <= 84                   
                 MOVE '99, 15, 20, 25, 30, 35, 40, 45, 50, 55, 60    '          
                                            TO LZ2W-VALID                       
                    MOVE '65            '   TO LZ2W-NON-VALID                   
               WHEN WS-BAL-LEASE >= 75 AND WS-BAL-LEASE <= 79                   
                 MOVE '99, 15, 20, 25, 30, 35, 40, 45, 50, 55        '          
                                            TO LZ2W-VALID                       
                    MOVE '60, 65        '   TO LZ2W-NON-VALID                   
               WHEN WS-BAL-LEASE >= 70 AND WS-BAL-LEASE <= 74                   
                 MOVE '99, 15, 20, 25, 30, 35, 40, 45, 50            '          
                                            TO LZ2W-VALID                       
                    MOVE '55, 60, 65    '   TO LZ2W-NON-VALID                   
               WHEN WS-BAL-LEASE >= 65 AND WS-BAL-LEASE <= 69                   
                 MOVE '99, 15, 20, 25, 30, 35, 40, 45                '          
                                            TO LZ2W-VALID                       
                    MOVE '50, 55, 60, 65'   TO LZ2W-NON-VALID                   
               WHEN OTHER                                                       
                    MOVE SPACES             TO LZ2W-VALID                       
                                               LZ2W-NON-VALID                   
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4400-WRITE-REPORT.                                                       
      ****************************************************************          
           MOVE SPACES                      TO BP13LZ2W-REC.                    
           INITIALIZE BP13LZ2W-REC.                                             
                                                                                
           ADD 1                            TO WS-LZ2W-SNO                      
           MOVE WS-LZ2W-SNO                 TO LZ2W-SNO                         
           MOVE F120-SCH-ACC-NO             TO LZ2W-SCH-ACC                     
           MOVE F120-BLK-NO                 TO LZ2W-BLOCK                       
                                                                                
           MOVE F120-LEVEL-NO               TO WS-LEVEL                         
           MOVE F120-MAIN-UNIT              TO WS-UNIT                          
           MOVE WS-UNIT-NO                  TO LZ2W-UNIT-NO                     
                                                                                
           MOVE F120-STREET-NAME            TO LZ2W-STREET                      
           MOVE WS-BAL-LEASE                TO LZ2W-BAL-LEASE                   
                                                                                
           IF WS-LZ2W-SNO = 1                                                   
              WRITE BP13LZ2W-REC    FROM LZ2W-HEADER-01                         
              WRITE BP13LZ2W-REC    FROM LZ2W-HEADER-02                         
              WRITE BP13LZ2W-REC    FROM LZ2W-HEADER-03                         
           END-IF                                                               
                                                                                
           WRITE BP13LZ2W-REC       FROM LZ2W-DETAIL.                           
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CZ2W'                                      
           DISPLAY 'DATE    : ' WS-SYSTEM-DATE                                  
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F120 RECORDS READ ....... : '                 
                    WS-F120-READ.                                               
           DISPLAY '(2) NO OF BM06K100 RECORDS MATCH....... : '                 
                    WS-K100-MTCH.                                               
           DISPLAY '(3) NO OF BM06K100 RECORDS NO MATCH.... : '                 
                    WS-K100-NMTCH.                                              
           DISPLAY '(4) NO OF BB14K500 RECORDS MATCH....... : '                 
                    WS-K500-MTCH.                                               
           DISPLAY '(5) NO OF BB14K500 RECORDS NO MATCH.... : '                 
                    WS-K500-NMTCH.                                              
           DISPLAY '(6) NO OF RECORDS WRITTEN.............. : '                 
                    WS-LZ2W-SNO.                                                
                                                                                
           CLOSE BM06F120                                                       
                 BP13F205                                                       
                 BM06K100                                                       
                 BB14K500                                                       
                 BP13LZ2W.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
