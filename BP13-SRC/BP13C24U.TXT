       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C24U.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  06 MAY 2014.                                              
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *    OBJECTIVE   : TO UPDATE PPS TAG AFTER BOOKING       *                
      *                                                        *                
      *    INPUT FILES : BP13F310                              *                
      *                  BP13K820                              *                
      *                  BP13K813                              *                
      *    I-O   FILES : BP13K800                              *                
      *                  BP13K848                              *                
      * ====================================================== *                
      *  CHG REF  OIC   DATE        DESCRIPTION                   *             
      *  -------- ----  ----------  ----------------------------  *             
      *  BP135305 ESA1  17/06/2014  NEW PROGRAM                   *             
      * ========================================================= *             
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310 ASSIGN       TO BP13F310.                            
                                                                                
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
                                                                                
           SELECT BP13K820 ASSIGN       TO BP13K820                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K820-KEY-FLD                         
                           FILE STATUS  IS K820-STATUS.                         
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS K800-STATUS.                         
                                                                                
           SELECT BP13K813 ASSIGN       TO BP13K813                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K813-KEY-FLD                         
                           FILE STATUS  IS K813-STATUS.                         
                                                                                
           SELECT BP13K848 ASSIGN TO BP13K848                                   
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K848-KEY-FLD                         
                           FILE STATUS  IS K848-STATUS.                         
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F310                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13F310.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K813                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K848                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K848.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 K813-STATUS                 PIC 9(02) VALUE ZEROES.               
           05 K820-STATUS                 PIC 9(02) VALUE ZEROES.               
           05 K800-STATUS                 PIC 9(02) VALUE ZEROES.               
           05 K848-STATUS                 PIC 9(02) VALUE ZEROES.               
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F310-READ               PIC 9(05) VALUE ZEROES.               
           05  WS-K820-READ               PIC 9(05) VALUE ZEROES.               
           05  WS-K800-UPDATE             PIC 9(05) VALUE ZEROES.               
           05  WS-SNO                     PIC 9(05) VALUE ZEROES.               
           05  WS-NRIC-CNT                PIC 9(05) VALUE ZEROES.               
           05  WS-CNT-K848-WRITE          PIC 9(05) VALUE ZEROES.               
           05  WS-MARITAL                 PIC X(01) VALUE SPACES.               
           05  WS-CITIZEN                 PIC X(02) VALUE SPACES.               
           05  WS-EDITED-DATE             PIC X(10) VALUE SPACES.               
           05  WS-CHILD-UIN               PIC X(09) VALUE SPACES.               
           05  WS-CHILD-NRIC              PIC X(09) VALUE SPACES.               
           05  WS-EOF-F310                PIC X     VALUE 'N'.                  
           05  WS-EOF-K820                PIC X     VALUE 'N'.                  
           05  WS-EOF-F001                PIC X     VALUE 'N'.                  
           05  WS-PPS-TAG                 PIC X     VALUE 'N'.                  
           05 WS-RELATION                 PIC X(02).                            
               88 VALID-REL-CDE                     VALUE  '41'.                
           05  WS-NUM-AGE-CHILD           PIC 9(02) VALUE ZEROES.               
           05  WS-NUM-AGE                 PIC 9(08) VALUE ZEROES.               
           05  WS-CHILD-AGE REDEFINES WS-NUM-AGE.                               
               10  WS-AGE-CY              PIC 9(04).                            
               10  WS-AGE-MM              PIC 9(02).                            
               10  WS-AGE-DD              PIC 9(02).                            
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DTE-OPEN-BTO             PIC 9(08)   VALUE ZEROES.            
           05  WS-DTE-CHILD-BIRTH          PIC 9(08)   VALUE ZEROES.            
           05  WS-CHILD-BIRTH              PIC 9(08)   VALUE ZEROES.            
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY                PIC 9(4).                             
           05  WS-SYS-MM                  PIC 9(2).                             
           05  WS-SYS-DD                  PIC 9(2).                             
       01  WS-SYSTEM-TIME                 PIC 9(8).                             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM 100-INITIALIZATION    THRU 100-EXIT.                         
           PERFORM 150-READ-SY02F001     THRU 150-EXIT.                         
           PERFORM 200-READ-BP13F310     THRU 200-EXIT.                         
           PERFORM 300-PROCESS           THRU 300-EXIT                          
                   UNTIL WS-EOF-F310 = 'Y'.                                     
           PERFORM 999-CLOSE-FILES       THRU 999-EXIT.                         
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       100-INITIALIZATION.                                                      
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F310                                                 
                       SY02F001                                                 
                       BP13K820                                                 
                       BP13K813                                                 
                I-O    BP13K800                                                 
                       BP13K848.                                                
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K820 FILE, ' K820-STATUS               
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K800 FILE, ' K800-STATUS               
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           IF K848-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K848, STATUS = ' K848-STATUS           
              MOVE     K848-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-SYSTEM-DATE.                   
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       150-READ-SY02F001.                                                       
      *-------------------------------------------------------------            
           READ SY02F001 AT END                                                 
                MOVE 'Y' TO WS-EOF-F001                                         
                GO TO 150-EXIT.                                                 
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       200-READ-BP13F310.                                                       
      *-------------------------------------------------------------            
           READ BP13F310 AT END                                                 
                MOVE 'Y' TO WS-EOF-F310                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1 TO WS-F310-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       300-PROCESS.                                                             
      *-------------------------------------------------------------            
           IF F001-DTE-CURRENT = F310-DTE-TRANS                                 
              PERFORM 700-READ-BP13K800    THRU 700-EXIT                        
              IF K800-NUM-PPS-ELIG = 'E'                                        
                 PERFORM 350-PROCESS-BP13K820 THRU 350-EXIT                     
                 IF WS-CHILD-BIRTH NOT = ZEROES AND SPACES                      
                    IF WS-CHILD-BIRTH < K800-DTE-REGN                           
                       IF WS-NUM-AGE-CHILD < 16                                 
                          PERFORM 800-WRITE-K800-IMAGE   THRU 800-EXIT          
                          PERFORM 710-REWRITE-BP13K800   THRU 710-EXIT          
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-BP13F310     THRU 200-EXIT.                         
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       350-PROCESS-BP13K820.                                                    
      *-------------------------------------------------------------            
           MOVE 'N'    TO WS-EOF-K820                                           
                          WS-PPS-TAG.                                           
           MOVE ZEROES TO WS-NUM-AGE-CHILD                                      
                          WS-CHILD-BIRTH                                        
                          WS-NRIC-CNT.                                          
           MOVE SPACES        TO WS-CHILD-UIN.                                  
           MOVE SPACES        TO K820-KEY-FLD.                                  
           MOVE F310-REGN-NO  TO K820-KEY-FLD.                                  
           START BP13K820 KEY >= K820-KEY-FLD.                                  
                                                                                
           EVALUATE K820-STATUS                                                 
              WHEN 00                                                           
                ADD 1 TO WS-K820-READ                                           
                PERFORM 400-CHECK-SC-CHILD-AGE THRU 400-EXIT                    
                   UNTIL K820-NUM-REGN NOT = F310-REGN-NO                       
                         OR WS-EOF-K820 = 'Y'                                   
              WHEN 10                                                           
              WHEN 23                                                           
                DISPLAY 'RECORD NOT FOUND IN K820, '                            
                         F310-REGN-NO                                           
              WHEN OTHER                                                        
                DISPLAY 'ERROR READING BP13K820, STATUS '                       
                         K820-STATUS                                            
                PERFORM 999-CLOSE-FILES THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       400-CHECK-SC-CHILD-AGE.                                                  
      *-------------------------------------------------------------            
           READ BP13K820 NEXT AT END                                            
                MOVE 'Y' TO WS-EOF-K820                                         
                GO TO 400-EXIT.                                                 
                                                                                
           EVALUATE K820-STATUS                                                 
              WHEN 00                                                           
              WHEN 02                                                           
                IF K820-NUM-REGN = F310-REGN-NO                                 
                   ADD 1 TO WS-K820-READ                                        
                   MOVE K820-NUM-RELATIONSHIP    TO WS-RELATION                 
                   MOVE K820-NUM-MARITAL-STATUS  TO WS-MARITAL                  
                   MOVE K820-NUM-CITIZENSHIP     TO WS-CITIZEN                  
                   MOVE K820-DTE-BIRTH           TO WS-DTE-CHILD-BIRTH          
                   MOVE K820-NUM-NRIC            TO WS-CHILD-NRIC               
                   PERFORM 500-READ-BP13K813     THRU 500-EXIT                  
                   IF (VALID-REL-CDE AND (WS-MARITAL = '1' OR 'S') AND          
                      (WS-CITIZEN = '10' OR 'SC'))                              
                      PERFORM 600-COMPUTE-AGE       THRU 600-EXIT               
                   END-IF                                                       
                END-IF                                                          
              WHEN 10                                                           
              WHEN 23                                                           
                DISPLAY 'RECORD NOT FOUND IN K820, '                            
                         F310-REGN-NO                                           
              WHEN OTHER                                                        
                DISPLAY 'ERROR READING BP13K820, STATUS '                       
                         K820-STATUS                                            
                PERFORM 999-CLOSE-FILES THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------              
       500-READ-BP13K813.                                                       
      *-----------------------------------------------------------              
           MOVE LOW-VALUES           TO K813-KEY-FLD.                           
           IF K800-NUM-ALLO-CAT = 'BTO'                                         
              MOVE K800-NUM-BTO-ZONE    TO K813-NUM-ZONE                        
           ELSE                                                                 
              MOVE K800-NUM-NEW-TOWN    TO K813-NUM-ZONE                        
           END-IF.                                                              
           MOVE K800-NUM-BTO-FLAT-TYPE   TO K813-NUM-FLAT-TYPE.                 
           MOVE K800-DTE-BALLOT      TO K813-DTE-BALLOT.                        
                                                                                
           READ BP13K813.                                                       
           EVALUATE K813-STATUS                                                 
           WHEN 00                                                              
              MOVE K813-DTE-OPEN-BTO TO WS-DTE-OPEN-BTO                         
           WHEN 23                                                              
              MOVE ZEROES            TO WS-DTE-OPEN-BTO                         
                                                                                
              IF K813-NUM-ZONE = 'JW'                                           
                 MOVE 'JE'                 TO K813-NUM-ZONE                     
                 READ BP13K813                                                  
                 EVALUATE K813-STATUS                                           
                 WHEN 00                                                        
                    MOVE K813-DTE-OPEN-BTO TO WS-DTE-OPEN-BTO                   
                 WHEN 23                                                        
                    MOVE ZEROES            TO WS-DTE-OPEN-BTO                   
                    DISPLAY 'BP13K813 (JW->JE) REGN : ' K800-NUM-REGN           
                    DISPLAY 'RECORD NOT FOUND BP13K813 : ' K813-KEY-FLD         
                 WHEN OTHER                                                     
                    DISPLAY 'ERROR READING BP13K813 : ' K813-STATUS             
                    MOVE K813-STATUS            TO  RETURN-CODE                 
                    PERFORM 999-CLOSE-FILES         THRU 999-EXIT               
                 END-EVALUATE                                                   
               ELSE                                                             
                    DISPLAY 'NOT FOUND BP13K813 REGN : ' K800-NUM-REGN          
                    DISPLAY 'RECORD NOT FOUND BP13K813 : ' K813-KEY-FLD         
               END-IF                                                           
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K813 : ' K813-STATUS                   
              MOVE K813-STATUS            TO  RETURN-CODE                       
              PERFORM 999-CLOSE-FILES         THRU 999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       600-COMPUTE-AGE.                                                         
      *-------------------------------------------------------------            
           IF WS-DTE-OPEN-BTO > WS-DTE-CHILD-BIRTH                              
              COMPUTE WS-NUM-AGE = WS-DTE-OPEN-BTO -                            
                                   WS-DTE-CHILD-BIRTH                           
           ELSE                                                                 
      *** FOR CHILD THAT WAS BORN AFTER LAUNCH DATE ***                         
              MOVE 1                       TO WS-AGE-CY                         
           END-IF.                                                              
                                                                                
      *** FOR CHILD LESS THAN 1 YEAR OLD ***                                    
           IF (WS-AGE-CY = ZEROES) AND                                          
              (WS-AGE-MM > ZEROES OR WS-AGE-DD > ZEROES)                        
              COMPUTE WS-AGE-CY = WS-AGE-CY + 1                                 
           END-IF.                                                              
                                                                                
           IF WS-NUM-AGE-CHILD = ZEROES                                         
              MOVE WS-AGE-CY         TO WS-NUM-AGE-CHILD                        
              MOVE WS-DTE-CHILD-BIRTH TO WS-CHILD-BIRTH                         
              MOVE WS-CHILD-NRIC      TO WS-CHILD-UIN                           
           ELSE                                                                 
              IF WS-AGE-CY < WS-NUM-AGE-CHILD                                   
                 MOVE WS-AGE-CY          TO WS-NUM-AGE-CHILD                    
                 MOVE WS-DTE-CHILD-BIRTH TO WS-CHILD-BIRTH                      
                 MOVE WS-CHILD-NRIC      TO WS-CHILD-UIN                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       700-READ-BP13K800.                                                       
      *--------------------------------------------------------------*          
           MOVE SPACES                TO K800-NUM-REGN.                         
           MOVE F310-REGN-NO          TO K800-NUM-REGN.                         
                                                                                
           READ BP13K800.                                                       
           EVALUATE K800-STATUS                                                 
              WHEN 00                                                           
              WHEN 02                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   MOVE SPACES        TO BP13K800-MASTER                        
                   DISPLAY 'RECORD NOT FOUND IN BP13K800, '                     
                           K800-NUM-REGN                                        
                   PERFORM 999-CLOSE-FILES     THRU 999-EXIT                    
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13K800. STATUS IS '                 
                           K800-STATUS                                          
                   PERFORM 999-CLOSE-FILES     THRU 999-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       710-REWRITE-BP13K800.                                                    
      *--------------------------------------------------------------*          
           MOVE 'Y'                     TO K800-NUM-PPS-ELIG.                   
           MOVE SPACES                  TO K800-NUM-PPS-BIRTH-CERT              
                                           K800-DTE-PPS-EST-DLVRY.              
                                                                                
           MOVE 'BP13C24U'              TO K800-NUM-USERID.                     
                                                                                
           MOVE WS-SYSTEM-DATE          TO K800-DTE-UPDATE.                     
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
            EVALUATE K800-STATUS                                                
               WHEN 00                                                          
               WHEN 02                                                          
                    ADD 1    TO WS-K800-UPDATE                                  
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR UPDATING BP13K800. STATUS IS '               
                            K800-STATUS                                         
                    PERFORM 999-CLOSE-FILES     THRU 999-EXIT                   
            END-EVALUATE.                                                       
                                                                                
       710-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       800-WRITE-K800-IMAGE.                                                    
      *--------------------------------------------------------------*          
                                                                                
           MOVE BP13K800-MASTER TO BP13K848-MASTER.                             
           MOVE 'C24U'          TO K848-NUM-TRANS-BKIMAGE.                      
                                                                                
           MOVE 'BP13C24U'      TO K848-NUM-USERID.                             
           MOVE WS-SYSTEM-DATE  TO K848-DTE-BKIMAGE.                            
                                                                                
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-SYSTEM-TIME.                   
                                                                                
           MOVE WS-SYSTEM-TIME  TO K848-TME-BKIMAGE.                            
           MOVE K848-NUM-REGN   TO K848-NUM-REGN-BKIMAGE.                       
                                                                                
           WRITE BP13K848-MASTER.                                               
                                                                                
           EVALUATE K848-STATUS                                                 
               WHEN 00                                                          
                    ADD 1 TO WS-CNT-K848-WRITE                                  
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR WRITING BP13K848. STATUS IS '                
                            K848-STATUS                                         
                    PERFORM 999-CLOSE-FILES THRU 999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '----- BP13C24U ---------------------------'.                
           DISPLAY '  NO. OF F310 RECORDS READ    = ' WS-F310-READ.             
           DISPLAY '  NO. OF K820 RECORDS READ    = ' WS-K820-READ.             
           DISPLAY '  NO. OF K800 RECORDS UPDATED = ' WS-K800-UPDATE.           
           DISPLAY '  NO. OF K848 RECORDS WRITTEN = ' WS-CNT-K848-WRITE.        
           DISPLAY '------------------------------------------'.                
                                                                                
           CLOSE BP13F310                                                       
                 SY02F001                                                       
                 BP13K813                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K848.                                                      
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING ERROR K820-STATUS IS ' K820-STATUS               
              MOVE K820-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING ERROR K800-STATUS IS ' K800-STATUS               
              MOVE K800-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF K813-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING ERROR K813-STATUS IS ' K813-STATUS               
              MOVE K813-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF K848-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING ERROR K848-STATUS IS ' K848-STATUS               
              MOVE K848-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
            EXIT.                                                               
