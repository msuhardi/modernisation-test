*--------------------------------------------------------------;                
*===============================================================                
*                      PROGRAM PROFILE                                          
*===============================================================                
*===============================================================                
* APPLICATION        : BP13                                                     
* APPLICATION NAME   : INFORMATION CENTRE DATABASE PHASE 2                      
* PROGRAM ID         : BP13S002                                                 
* LANGUAGE           : SAS                                                      
* DEVT PROJ REQ NO   : N950034                                                  
*                                                                               
* AUTHOR (PROFILE)   : LDW1             DATE : 05.09.96                         
* MAN-DAYS ESTIMATED : 2                                                        
*                                                                               
* PROGRAMMER         : DAVID            START DATE : 05.09.96                   
* MAN-DAYS ACTUAL    :                  END DATE   : 07.09.96                   
*                                                                               
*----------------------------------------------------------------               
* PURPOSE OF PROGRAM :                                                          
* ------------------                                                            
*                                                                               
*    THIS PROGRAM EXTRACTS SALES APPLICANT DATA AND LOAD INTO                   
*    BP13FW02 MONTHLY.                                                          
*                                                                               
*----------------------------------------------------------------               
* MODIFICATION LOG                                                              
* ----------------                                                              
*  MODIFY   DATE    CHGE REQ  DESCRIPTION OF CHANGE                             
*  ------   ----    --------  ---------------------                             
* LSB      970110  BP130176 - NEW PGM                                           
* BA3      070918  BP133145 - INCLUDE NT, FT, ROLE (3 BYTES)                    
*                                                                               
*===============================================================                
*                     RELATED DOCUMENTS                                         
*===============================================================                
* SCREEN LAYOUT   : NIL                                                         
* REPORT LAYOUT   : NIL                                                         
*                                                                               
* VIEW STRUCTURE  : NIL                                                         
*                                                                               
* FILE LAYOUT     : FI06.BATCH.COPYCOB(BP13FWA2)                                
*                                                                               
* DATA DEFINITION : NIL                                                         
*                                                                               
* PROCESSING RULE : NIL                                                         
*                                                                               
* PHYSICAL FLOW   : NIL                                                         
*                                                                               
* MESSAGE FILE    : NIL                                                         
*                                                                               
* SUBROUTINE USED : NIL                                                         
*                                                                               
*===============================================================                
OPTIONS SORTPGM=BEST;                                                           
                                                                                
*------------------------------------------------------------*                  
*   READ IN CURRENT & HISTORY SALES APPLICANT DATA APPENDED  *                  
*   TOGETHER AND SORTED ON REGISTRATION NUMBER, IC NUMBER    *                  
*                              -  (ALREADY DONE BY JCL)      *                  
*------------------------------------------------------------*;                 
DATA DATAF820;                                                                  
     INFILE F820;                                                               
     INPUT @1   NUM_REGN             $CHAR8.                                    
           @9   NUM_NRIC             $CHAR9.                                    
           @18  NME_OCCP             $CHAR66.                                   
           @84  NUM_ETHN             $CHAR1.                                    
           @85  NUM_SEX              $CHAR1.                                    
           @86  MAR_STAT             $CHAR1.                                    
           @87  DTE_BIRT             $CHAR8.                                    
           @95  NUM_CITZ             $CHAR2.                                    
           @97  NUM_RELN             $CHAR2.                                    
           @99  INCOME               5.                                         
           @104 INCOMETP             5.                                         
           @109 LESSECHK             $CHAR1.                                    
           @110 NUM_EXIST_NT         $CHAR3.                                    
           @113 NUM_EXIST_FT         $CHAR2.                                    
           @115 NUM_EXIST_ROLE       $CHAR3.;                                   
*------------------------------------------------------------*                  
*   READ IN SALES APPLICATION FILE  (OUPUT OF BP13S001)      *                  
*   SORTED ON REGISTRATION NUMBER - (ALREADY DONE BY JCL)    *                  
*------------------------------------------------------------*;                 
DATA DATAFW01;                                                                  
     INFILE FW01;                                                               
     INPUT @1   NUM_REGN      $CHAR8.;                                          
                                                                                
*-------------------------------------------------------------*                 
*   ENSURE SALES APPLICANT FILE HAS REGN NUMBER FROM  SALES    *                
*   APPLICATION FILE.                                         *                 
*-------------------------------------------------------------*;                
DATA DATAF820;                                                                  
     MERGE DATAF820(IN=A) DATAFW01(IN=B);                                       
     BY NUM_REGN;                                                               
     IF (A AND B);                                                              
                                                                                
*------------------------------------------------------------*                  
*   TRANSFORMATION ON CITIZEN SHIP                           *                  
*------------------------------------------------------------*;                 
     IF NUM_CITZ ^= '00' AND                                                    
        NUM_CITZ ^= '10' AND                                                    
        NUM_CITZ ^= '20' AND                                                    
        NUM_CITZ ^= '30' THEN                                                   
        NUM_CITZ  = '00';                                                       
                                                                                
*-------------------------------------------------------------*                 
*   EXTRACTING  CODE TABLE DESCRIPTION                        *                 
*-------------------------------------------------------------*;                
DATA COD25 COD35;                                                               
     INFILE CODT;                                                               
     INPUT @1   S_NO              $2.                                           
           @3   CODE              $CHAR3.                                       
           @6   DESCRIP           $CHAR20.;                                     
     IF (S_NO = '25') OR (S_NO = '35');                                         
                                                                                
*-------------------------------------------------------------*                 
*   SERIAL NO 25 BELONGS TO CITIZENSHIP                       *                 
*-------------------------------------------------------------*;                
     IF (S_NO='25') THEN                                                        
     DO;                                                                        
        NUM_CITZ=SUBSTR(CODE,1,2);                                              
        OUTPUT COD25;                                                           
     END;                                                                       
                                                                                
*-------------------------------------------------------------*                 
*   SERIAL NO 35 BELONGS TO RELATIONSHIP                      *                 
*-------------------------------------------------------------*;                
     ELSE IF (S_NO='35') THEN                                                   
          DO;                                                                   
             NUM_RELN=SUBSTR(CODE,1,2);                                         
             OUTPUT COD35;                                                      
          END;                                                                  
                                                                                
PROC SORT DATA=COD25;                                                           
     BY NUM_CITZ;                                                               
                                                                                
PROC SORT DATA=COD35;                                                           
     BY NUM_RELN;                                                               
                                                                                
*-------------------------------------------------------------*                 
*   EXTRACTING  NME_CITZ (NAME CITIZENSHIP)                   *                 
*-------------------------------------------------------------*;                
PROC SORT DATA=DATAF820;                                                        
     BY NUM_CITZ;                                                               
                                                                                
DATA DATAF820;                                                                  
     MERGE COD25(IN=A) DATAF820(IN=B);                                          
     BY NUM_CITZ;                                                               
     IF B;                                                                      
     RENAME DESCRIP=NME_CITZ;                                                   
                                                                                
*-------------------------------------------------------------*                 
*   EXTRACTING  NME_RELN (NAME RELATIONSHIP)                  *                 
*-------------------------------------------------------------*;                
PROC SORT DATA=DATAF820;                                                        
     BY NUM_RELN;                                                               
                                                                                
DATA DATAF820;                                                                  
     MERGE COD35(IN=A) DATAF820(IN=B);                                          
     BY NUM_RELN;                                                               
     IF B;                                                                      
     RENAME DESCRIP=NME_RELN;                                                   
                                                                                
*-------------------------------------------------------------*                 
*   TO OUTPUT TO BP13FW02                                     *                 
*-------------------------------------------------------------*;                
DATA _NULL_;                                                                    
     FILE OUT;                                                                  
     SET DATAF820;                                                              
       PUT @1   NUM_REGN             $CHAR8.                                    
           @9   NUM_NRIC             $CHAR9.                                    
           @18  NME_OCCP             $CHAR66.                                   
           @84  NUM_ETHN             $CHAR1.                                    
           @85  NUM_SEX              $CHAR1.                                    
           @86  MAR_STAT             $CHAR1.                                    
           @87  DTE_BIRT             $CHAR8.                                    
           @95  NUM_CITZ             $CHAR2.                                    
           @97  NME_CITZ             $CHAR25.                                   
           @122 NUM_RELN             $CHAR2.                                    
           @124 NME_RELN             $CHAR25.                                   
           @149 INCOME               5.                                         
           @154 INCOMETP             5.                                         
           @159 LESSECHK             $CHAR1.                                    
           @160 NUM_EXIST_NT         $CHAR3.                                    
           @163 NUM_EXIST_FT         $CHAR2.                                    
           @165 NUM_EXIST_ROLE       $CHAR3.;                                   
RETURN;                                                                         
                                                                                
