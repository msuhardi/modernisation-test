       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF05.                                                 
      *AUTHOR.        ZDD1                                                      
      *DATE-WRITTEN.  23APR2010.                                                
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. TO CHECK NRIC IF EXISTING ON BB24K020            *                
      *                                                        *                
      *    INPUT FILES  : BP13F730                             *                
      *    I-O            BB24K020                             *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY  DATE      DETAILS                          *                
      *=======  === ========= =================================*                
      *BP133893 ZDD 20100423  NEW PGM                          *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
                                                                                
           SELECT BB24K020 ASSIGN TO BB24K020                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K020-KEY-FLD                               
                  ALTERNATE KEY   IS K020-NUM-UIN                               
                  FILE STATUS     IS WS-K020-STATUS.                            
                                                                                
           SELECT BP13K730 ASSIGN TO BP13K730                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K730-NUM-REGN                              
                  FILE STATUS     IS WS-K730-STATUS.                            
                                                                                
           SELECT P13F730A ASSIGN TO P13F730A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F730.                                                           
                                                                                
       FD   BB24K020                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BB24K020.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K730.                                                           
                                                                                
       FD   P13F730A                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 500 CHARACTERS.                                     
       01   P13F730A-REC    PIC X(500).                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F730-EOF                    PIC X(1)  VALUE 'N'.              
           05  WS-K020-EOF                    PIC X(1)  VALUE 'N'.              
           05  WS-K020-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-K730-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-CNT-READ-F730               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-FND-K730                PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-NOT-FND-K020            PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-FND-K020                PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-NOT-FND-K730            PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-WRTE-F730               PIC 9(6)  VALUE ZEROS.            
           05  WS-BYPASS                      PIC 9(6)  VALUE ZEROS.            
           05  WS-FOUND                       PIC X(1)  VALUE 'N'.              
           05  WS-K730-FOUND                  PIC X(1)  VALUE 'N'.              
           05  WS-PREV-REGN                   PIC X(8)  VALUE SPACES.           
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
                                                                                
           PERFORM 2000-READ-F730         THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-F730      THRU 3000-EXIT                        
                  UNTIL WS-F730-EOF    = 'Y'.                                   
                                                                                
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BB24K020                                                 
                I-O    BP13K730                                                 
                OUTPUT P13F730A.                                                
                                                                                
           IF WS-K020-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BB24K020 OPEN ERROR, STATUS IS ' WS-K020-STATUS          
              MOVE WS-K020-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K730 OPEN ERROR, STATUS IS ' WS-K730-STATUS          
              MOVE WS-K730-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-F730.                                                          
      ****************************************************************          
                                                                                
           READ BP13F730                                                        
              AT END MOVE 'Y' TO WS-F730-EOF                                    
              GO TO 2000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD   1                TO WS-CNT-READ-F730.                          
                                                                                
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2100-READ-K020.                                                          
      ****************************************************************          
                                                                                
           READ BB24K020 KEY IS K020-NUM-UIN.                                   
                                                                                
           EVALUATE  WS-K020-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                  ADD 1                      TO WS-CNT-FND-K020                 
                  MOVE F730-NUM-REGN         TO K730-NUM-REGN                   
                  MOVE 'N'                   TO WS-K730-FOUND                   
                  PERFORM 4000-READ-K730   THRU 4000-EXIT                       
                  IF WS-K730-FOUND = 'Y'                                        
                    MOVE 'Y'                 TO WS-FOUND                        
                    WRITE  P13F730A-REC FROM BP13K730-REC                       
                    ADD 1                    TO WS-CNT-WRTE-F730                
                  END-IF                                                        
               WHEN 23                                                          
                  ADD 1                      TO WS-CNT-NOT-FND-K020             
               WHEN OTHER                                                       
                 DISPLAY 'ERROR READING OF BB24K020. STATUS IS '                
                         WS-K020-STATUS                                         
                 MOVE WS-K020-STATUS              TO RETURN-CODE                
                 PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-F730.                                                       
      ****************************************************************          
                                                                                
                                                                                
           MOVE SPACES               TO K020-NUM-UIN                            
                                        P13F730A-REC.                           
                                                                                
           IF F730-NUM-REGN = WS-PREV-REGN                                      
             IF WS-FOUND = 'Y'                                                  
                ADD 1                     TO WS-BYPASS                          
                CONTINUE                                                        
             ELSE                                                               
                MOVE F730-NUM-NRIC1       TO K020-NUM-UIN                       
                PERFORM 2100-READ-K020  THRU 2100-EXIT                          
             END-IF                                                             
           ELSE                                                                 
                MOVE 'N'                  TO WS-FOUND                           
                MOVE F730-NUM-NRIC1       TO K020-NUM-UIN                       
                MOVE F730-NUM-REGN        TO WS-PREV-REGN                       
                PERFORM 2100-READ-K020  THRU 2100-EXIT                          
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F730    THRU 2000-EXIT.                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4000-READ-K730.                                                          
      ****************************************************************          
                                                                                
           READ BP13K730                                                        
                                                                                
           EVALUATE  WS-K730-STATUS                                             
               WHEN 00                                                          
                  MOVE 'Y'   TO WS-K730-FOUND                                   
                  ADD 1      TO WS-CNT-FND-K730                                 
               WHEN 23                                                          
                  ADD 1      TO WS-CNT-NOT-FND-K730                             
               WHEN OTHER                                                       
                 DISPLAY 'ERROR READING OF BP13K730. STATUS IS '                
                         WS-K730-STATUS                                         
                 MOVE WS-K730-STATUS              TO RETURN-CODE                
                 PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13CF05 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F730 RECORDS READ      = ' WS-CNT-READ-F730.            
           DISPLAY 'NO OF NOT FOUND ON K020    = ' WS-CNT-NOT-FND-K020.         
           DISPLAY 'NO OF CASES FND ON K020    = ' WS-CNT-FND-K020.             
           DISPLAY 'NO OF BYPASS CASES         = ' WS-BYPASS.                   
           DISPLAY 'NO OF CASES FND ON K730    = ' WS-CNT-FND-K730.             
           DISPLAY 'NO OF NOT FOUND ON K730    = ' WS-CNT-NOT-FND-K730.         
           DISPLAY 'NO OF CASES WRITTEN        = ' WS-CNT-WRTE-F730.            
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           CLOSE BP13F730                                                       
                 BB24K020                                                       
                 BP13K730                                                       
                 P13F730A.                                                      
                                                                                
           IF WS-K020-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BB24K020 CLOSE ERROR, STATUS IS ' WS-K020-STATUS         
              MOVE WS-K020-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K730 CLOSE ERROR, STATUS IS ' WS-K730-STATUS         
              MOVE WS-K730-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
