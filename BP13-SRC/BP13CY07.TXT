       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY07.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  13/07/2011.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO MATCH K595 WITH F595                            *        
      *                                                                *        
      * F595 -                                                         *        
      * K595 -                                                         *        
      ******************************************************************        
      * CHG REQ   BY   DATE        DETAIL                              *        
      * --------  ---- ----------  ----------------------------------- *        
      * BP134300  JB8  13/07/2011  NEW PROGRAM                         *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT   BP13F595      ASSIGN TO BP13F595.                           
           SELECT   BP13K595      ASSIGN TO BP13K595.                           
           SELECT   P13F595A      ASSIGN TO P13F595A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13F595                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F595.                                                           
                                                                                
       FD   BP13K595                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K595.                                                           
                                                                                
       FD   P13F595A                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   P13F595A-REC PIC X(500).                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  CNT-F595-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-K595-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-F595A-WRITE              PIC 9(7)  VALUE ZEROS.              
           05  CNT-F595-NOTMAT              PIC 9(7)  VALUE ZEROS.              
           05  WS-1ST-NRIC                  PIC X(9)  VALUE SPACES.             
           05  WS-1ST-BALLOT                PIC X(6)  VALUE SPACES.             
           05  WS-1ST-BALLOT-BF             PIC X(6)  VALUE SPACES.             
           05  WS-1ST-BALLOT-LF             PIC X(6)  VALUE SPACES.             
           05  WS-1ST-BALLOT-WF             PIC X(6)  VALUE SPACES.             
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F595       THRU 2000-EXIT.                     
           PERFORM 2010-READ-BP13K595       THRU 2010-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL F595-NUM-NRIC1 = HIGH-VALUES AND                             
                   K595-NUM-NRIC1 = HIGH-VALUES.                                
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT BP13F595                                                  
                      BP13K595                                                  
               OUTPUT P13F595A.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F595.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F595                                                        
             AT END                                                             
                MOVE HIGH-VALUES     TO F595-NUM-NRIC1                          
                GO                   TO 2000-EXIT.                              
                                                                                
           MOVE SPACES               TO BP13F595-REC(470:31).                   
                                                                                
           ADD  1                    TO CNT-F595-READ.                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2010-READ-BP13K595.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K595                                                        
             AT END                                                             
                MOVE HIGH-VALUES     TO K595-NUM-NRIC1                          
                GO                   TO 2010-EXIT.                              
                                                                                
           ADD  1                    TO CNT-K595-READ.                          
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
           IF F595-NUM-NRIC1 = K595-NUM-NRIC1                                   
              IF F595-NUM-NRIC1 NOT = WS-1ST-NRIC                               
                 MOVE F595-NUM-NRIC1            TO WS-1ST-NRIC                  
                 MOVE F595-DTE-BALLOT           TO WS-1ST-BALLOT                
                 MOVE SPACES                    TO WS-1ST-BALLOT-BF             
                                                   WS-1ST-BALLOT-LF             
                                                   WS-1ST-BALLOT-WF             
              END-IF                                                            
              IF F595-DTE-BALLOT > '200906'                                     
                 EVALUATE F595-NUM-BOOK-STATUS                                  
                     WHEN 'BF'                                                  
                          IF WS-1ST-BALLOT-BF = SPACES                          
                             MOVE F595-DTE-BALLOT TO WS-1ST-BALLOT-BF           
                          END-IF                                                
                     WHEN 'LF'                                                  
                          IF WS-1ST-BALLOT-LF = SPACES                          
                             MOVE F595-DTE-BALLOT TO WS-1ST-BALLOT-LF           
                          END-IF                                                
                     WHEN 'WF'                                                  
                          IF WS-1ST-BALLOT-WF = SPACES                          
                             MOVE F595-DTE-BALLOT TO WS-1ST-BALLOT-WF           
                          END-IF                                                
                 END-EVALUATE                                                   
              END-IF                                                            
              MOVE K595-CDE-HOUSEHOLD           TO F595-CDE-HOUSEHOLD           
              MOVE BP13K595-REC(470:3)          TO BP13F595-REC(470:3)          
              MOVE WS-1ST-BALLOT                TO BP13F595-REC(473:6)          
              IF WS-1ST-BALLOT-BF NOT = SPACES                                  
                 MOVE 'BF'                      TO BP13F595-REC(479:3)          
                 MOVE WS-1ST-BALLOT-BF          TO BP13F595-REC(482:6)          
              ELSE                                                              
                 IF WS-1ST-BALLOT-LF NOT = SPACES                               
                    MOVE 'LF'                   TO BP13F595-REC(479:3)          
                    MOVE WS-1ST-BALLOT-LF       TO BP13F595-REC(482:6)          
                 ELSE                                                           
                    IF WS-1ST-BALLOT-WF NOT = SPACES                            
                       MOVE 'WF'                TO BP13F595-REC(479:3)          
                       MOVE WS-1ST-BALLOT-WF    TO BP13F595-REC(482:6)          
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
              WRITE P13F595A-REC FROM BP13F595-REC                              
              ADD 1                             TO CNT-F595A-WRITE              
              PERFORM 2000-READ-BP13F595      THRU 2000-EXIT                    
           ELSE                                                                 
              IF F595-NUM-NRIC1 < K595-NUM-NRIC1                                
                 ADD 1                          TO CNT-F595-NOTMAT              
                 PERFORM 2000-READ-BP13F595   THRU 2000-EXIT                    
              ELSE                                                              
                 PERFORM 2010-READ-BP13K595   THRU 2010-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CY07'.                                
           DISPLAY '***************************************'.                   
           DISPLAY 'CNT-F595-READ            : ' CNT-F595-READ.                 
           DISPLAY 'CNT-K595-READ            : ' CNT-K595-READ.                 
           DISPLAY 'CNT-F595A-WRITE          : ' CNT-F595A-WRITE.               
           DISPLAY 'CNT-F595-NOTMAT          : ' CNT-F595-NOTMAT.               
                                                                                
           CLOSE BP13F595                                                       
                 BP13K595                                                       
                 P13F595A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
