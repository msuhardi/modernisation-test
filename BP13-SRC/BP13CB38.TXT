       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB38.                                                 
      *DATE-WRITTEN.  04/09/2007.                                               
      *AUTHOR.        EDEN ROSE DINGAL.                                         
      * ========================================================== *            
      *  SYSTEM OF COMMITMENT (BP13)                               *            
      * ---------------------------------------------------------- *            
      *   OBJECTIVE :  MATCH BP13F595 WITH BP13F580 BY NRIC. IF    *            
      *                FOUND ACCEPT REC, STOP OUTPUT.              *            
      *                                                            *            
      *   INPUT   : BP13F595                                       *            
      *             BP13F580                                       *            
      *   OUTPUT  : P13F595A - MATCHED, W/ DTE-BALLOT > DTE-ACC-REJ*            
      *                        AND F595 UNMATCHED WITH F580        *            
      *   REPORT  : BP13LB38 - MATCHED, W/ DTE-BALLOT < DTE-ACC-REJ*            
      *                                                            *            
      * CHG-NO    CHG-DATE     BY   DESCRIPTION                    *            
      * ------    --------     --   -----------                    *            
      * BP133111  04/09/2007   ED1  NEW PROGRAM                    *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F580 ASSIGN TO BP13F580.                                  
           SELECT BP13F595 ASSIGN TO BP13F595.                                  
           SELECT P13F595A ASSIGN TO P13F595A.                                  
           SELECT BP13LB38 ASSIGN TO BP13LB38.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F595                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F595.                                                           
                                                                                
       FD  BP13F580                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F580.                                                           
                                                                                
       FD  P13F595A                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  WS-F595A-REC PIC X(500).                                             
                                                                                
       FD   BP13LB38                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   LB38-PRTREC  PIC X(132).                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNT.                                                            
           05  WS-F580-READ             PIC 9(7)    VALUE ZERO.                 
           05  WS-F595-READ             PIC 9(7)    VALUE ZERO.                 
           05  WS-F595A-WRITE           PIC 9(7)    VALUE ZERO.                 
           05  WS-F595B-WRITE           PIC 9(7)    VALUE ZERO.                 
           05  WS-F595-MATCH            PIC 9(7)    VALUE ZERO.                 
           05  WS-F595-NOTMATCH         PIC 9(7)    VALUE ZERO.                 
           05  WS-F580-NOTMATCH         PIC 9(7)    VALUE ZERO.                 
                                                                                
       01  WS-CTRS.                                                             
           05 WS-SNO                    PIC 9(05) VALUE ZEROS.                  
           05 WS-PAGE-CTR               PIC 9(04) VALUE ZEROS.                  
           05 WS-PRNT-CTR               PIC 9(05) VALUE ZEROS.                  
           05 WS-LINE-CTR               PIC 9(02) VALUE ZEROS.                  
           05 WS-CTR                    PIC 9     VALUE ZERO.                   
                                                                                
       01  WS-MAX-LINE                   PIC 9(02) VALUE 58.                    
                                                                                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE                  PIC X(08) VALUE SPACES.             
           05  WS-SYS-TIME                  PIC X(08) VALUE SPACES.             
           05  FILLER                       PIC X(05) VALUE SPACES.             
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CUR-DD                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-MM                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-CCYY                 PIC 9(04) VALUE ZEROES.              
                                                                                
       01  WS-F580-NRIC                 PIC X(9)    VALUE SPACES.               
       01  WS-F595-NRIC                 PIC X(9)    VALUE SPACES.               
       01  WS-F595-EOF                  PIC X VALUE 'N'.                        
       01  WS-PRINTED                   PIC X(8) VALUE SPACES.                  
                                                                                
      *---------------------------                                              
      * REPORT LAYOUT                                                           
      *---------------------------                                              
                                                                                
       01  WS-BLANK-LINE  PIC X(132)             VALUE SPACES.                  
                                                                                
       01  WS-DASH-LINE   PIC X(132)             VALUE ALL '-'.                 
                                                                                
       01  LB38-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13LB38'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LB38-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LB38-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
                                                                                
       01  LB38-HEADER02.                                                       
           05  FILLER              PIC X(44)     VALUE SPACES.                  
           05  FILLER              PIC X(46)     VALUE                          
               'REPORT ON DATE BALLOT <= DATE ACCEPTANCE'.                      
                                                                                
       01  LB38-HEADER03.                                                       
           05  FILLER              PIC X(03)     VALUE 'S/N'.                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REGN NO'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'SCH-ACC'.               
           05  FILLER              PIC X(11)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'NRIC'.                  
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'DTE-ACPT'.              
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'TRANS'.                 
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'DTE-BALLOT'.            
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'ALLO-CAT'.              
                                                                                
       01 LB38-DETAILS.                                                         
          05 LB38-DTL-SERIAL      PIC ZZZZ9     VALUE ZEROES.                   
          05 FILLER               PIC X(01)     VALUE SPACES.                   
          05 LB38-REGNO           PIC X(08)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LB38-SCH-ACC         PIC X(14)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LB38-NRIC            PIC X(09)     VALUE SPACES.                   
          05 FILLER               PIC X(04)     VALUE SPACES.                   
          05 LB38-DTE-ACPT        PIC X(07)     VALUE SPACES.                   
          05 FILLER               PIC X(07)     VALUE SPACES.                   
          05 LB38-TRANS           PIC X(02)     VALUE SPACES.                   
          05 FILLER               PIC X(08)     VALUE SPACES.                   
          05 LB38-DTE-BAL         PIC X(07)     VALUE SPACES.                   
          05 FILLER               PIC X(09)     VALUE SPACES.                   
          05 LB38-ALLO-CAT        PIC X(03)     VALUE SPACES.                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILE THRU 1000-EXIT.                               
           PERFORM 2000-READ-F595 THRU 2000-EXIT.                               
           PERFORM 3000-READ-F580 THRU 3000-EXIT.                               
                                                                                
           PERFORM 4000-PROCESS   THRU 4000-EXIT                                
              UNTIL WS-F580-NRIC = HIGH-VALUES                                  
               AND  WS-F595-NRIC = HIGH-VALUES.                                 
                                                                                
           PERFORM 9000-END-PROGRAM THRU 9000-EXIT.                             
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILE.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F580                                                 
                       BP13F595                                                 
                OUTPUT P13F595A                                                 
                       BP13LB38.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
           MOVE WS-SYS-DATE(1:4)      TO WS-CUR-CCYY.                           
           MOVE WS-SYS-DATE(5:2)      TO WS-CUR-MM.                             
           MOVE WS-SYS-DATE(7:2)      TO WS-CUR-DD.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-F595.                                                          
      *-------------------------------------------------------------            
           READ BP13F595 AT END MOVE HIGH-VALUES TO WS-F595-NRIC                
                                GO TO 2000-EXIT.                                
                                                                                
           ADD 1 TO WS-F595-READ.                                               
           MOVE BP13F595-REC(484:9) TO WS-F595-NRIC.                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-READ-F580.                                                          
      *-------------------------------------------------------------            
           READ BP13F580 AT END MOVE HIGH-VALUES TO WS-F580-NRIC                
                                GO TO 3000-EXIT.                                
                                                                                
           ADD 1 TO WS-F580-READ.                                               
           MOVE BP13F580-REC(74:9)  TO WS-F580-NRIC.                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-PROCESS.                                                            
      *-------------------------------------------------------------            
           IF WS-F595-NRIC = WS-F580-NRIC                                       
              ADD 1    TO WS-F595-MATCH                                         
              PERFORM  5000-CHECK-F595 THRU 5000-EXIT                           
                      UNTIL WS-F595-NRIC NOT = WS-F580-NRIC                     
                         OR WS-F595-EOF      = 'Y'                              
                                                                                
              PERFORM 3000-READ-F580       THRU 3000-EXIT                       
                                                                                
           ELSE                                                                 
           IF WS-F595-NRIC   <  WS-F580-NRIC                                    
              ADD 1 TO WS-F595-NOTMATCH                                         
              PERFORM 6000-WRITE-F595A  THRU 6000-EXIT                          
              PERFORM 2000-READ-F595    THRU 2000-EXIT                          
                                                                                
           ELSE                                                                 
              ADD 1 TO WS-F580-NOTMATCH                                         
              PERFORM 3000-READ-F580         THRU 3000-EXIT                     
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-CHECK-F595.                                                         
      *-------------------------------------------------------------            
                                                                                
           IF WS-F595-NRIC    = WS-F580-NRIC                                    
              IF F595-NUM-REGN   = F580-NUM-REGN   AND                          
                 F595-DTE-BALLOT = F580-DTE-BALLOT AND                          
                 F595-CDE-REQUEST-STATUS = 'S'                                  
                    PERFORM 6000-WRITE-F595A  THRU 6000-EXIT                    
              ELSE                                                              
                                                                                
                 IF F595-DTE-BALLOT > F580-DTE-ACC-REJ(1:6)                     
                    PERFORM 6000-WRITE-F595A  THRU 6000-EXIT                    
                 ELSE                                                           
                    PERFORM 7000-WRITE-REPORT THRU 7000-EXIT                    
                 END-IF                                                         
                                                                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F595       THRU 2000-EXIT.                         
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-WRITE-F595A.                                                        
      *-------------------------------------------------------------            
           MOVE SPACES                TO WS-F595A-REC.                          
           MOVE BP13F595-REC          TO WS-F595A-REC.                          
                                                                                
           WRITE       WS-F595A-REC.                                            
           ADD 1    TO WS-F595A-WRITE.                                          
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       7000-WRITE-REPORT.                                                       
      *-------------------------------------------------------------            
           IF WS-PRINTED = F580-NUM-REGN                                        
              GO TO 7000-EXIT                                                   
           ELSE                                                                 
              MOVE F580-NUM-REGN TO WS-PRINTED                                  
           END-IF.                                                              
                                                                                
           MOVE SPACES  TO LB38-DETAILS.                                        
           INITIALIZE      LB38-DETAILS.                                        
                                                                                
           IF F580-SCH-ACC NOT = SPACES AND LOW-VALUES AND ZEROS                
              STRING F580-SCH-ACC(1:4) '-'                                      
                     F580-SCH-ACC(5:4) '-'                                      
                     F580-SCH-ACC(9:1) '-'                                      
                     F580-SCH-ACC(10:2)                                         
                     DELIMITED BY SIZE INTO LB38-SCH-ACC                        
           END-IF.                                                              
                                                                                
           MOVE F580-NUM-REGN               TO LB38-REGNO.                      
           MOVE F580-NUM-REGN               TO WS-PRINTED.                      
           MOVE WS-F580-NRIC                TO LB38-NRIC.                       
           MOVE F580-NUM-ACC-REJ            TO LB38-TRANS.                      
           MOVE F580-NUM-ALLO-CAT           TO LB38-ALLO-CAT.                   
                                                                                
           IF F580-DTE-ACC-REJ(1:6) NOT = SPACES AND LOW-VALUES                 
                                                 AND ZEROS                      
              STRING F580-DTE-ACC-REJ(1:4) '/'                                  
                     F580-DTE-ACC-REJ(5:2) '/'                                  
                     DELIMITED BY SIZE INTO LB38-DTE-ACPT                       
           END-IF.                                                              
                                                                                
           IF F595-DTE-BALLOT NOT = SPACES AND LOW-VALUES AND ZEROS             
              STRING F595-DTE-BALLOT(1:4) '/'                                   
                     F595-DTE-BALLOT(5:2) '/'                                   
                     DELIMITED BY SIZE INTO LB38-DTE-BAL                        
           END-IF.                                                              
                                                                                
           ADD   1                TO  WS-SNO.                                   
           MOVE  WS-SNO           TO  LB38-DTL-SERIAL.                          
           MOVE  WS-CURRENT-DATE  TO  LB38-DATE-HDR.                            
                                                                                
           IF WS-LINE-CTR > WS-MAX-LINE OR                                      
              WS-SNO     = 1                                                    
              ADD          1   TO WS-PAGE-CTR                                   
              MOVE ZEROES      TO LB38-PAGE-HDR                                 
              MOVE WS-PAGE-CTR TO LB38-PAGE-HDR                                 
              PERFORM 7050-PRINT-HEADING THRU 7050-EXIT                         
           END-IF.                                                              
                                                                                
           WRITE LB38-PRTREC FROM LB38-DETAILS.                                 
           ADD   1   TO   WS-LINE-CTR.                                          
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       7050-PRINT-HEADING.                                                      
      *-------------------------------------------------------------            
           WRITE LB38-PRTREC FROM WS-BLANK-LINE AFTER PAGE.                     
           WRITE LB38-PRTREC FROM WS-BLANK-LINE.                                
           WRITE LB38-PRTREC FROM WS-BLANK-LINE.                                
           WRITE LB38-PRTREC FROM LB38-HEADER01.                                
           WRITE LB38-PRTREC FROM LB38-HEADER02.                                
           WRITE LB38-PRTREC FROM WS-BLANK-LINE.                                
           WRITE LB38-PRTREC FROM LB38-HEADER03.                                
           WRITE LB38-PRTREC FROM WS-DASH-LINE.                                 
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE  8 TO WS-LINE-CTR.                                              
                                                                                
       7050-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
       9000-END-PROGRAM.                                                        
      *-------------------------------------------------------------            
           DISPLAY '*------- BP13CB38 -------*'.                                
           DISPLAY ' TOTAL RECORD OF BP13F595   : ' WS-F595-READ.               
           DISPLAY ' TOTAL RECORD OF BP13F580   : ' WS-F580-READ.               
           DISPLAY ' MATCHED CASES              : ' WS-F595-MATCH.              
           DISPLAY ' TOTAL WRITTEN TO P13F595A  : ' WS-F595A-WRITE.             
           DISPLAY ' TOTAL DTE-BAL < DTE-ACPT   : ' WS-SNO.                     
                                                                                
           CLOSE      BP13F580                                                  
                      BP13F595                                                  
                      P13F595A                                                  
                      BP13LB38.                                                 
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
