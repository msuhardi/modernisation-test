      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.   BP13C342.                                                  
      *AUTHOR.       MCC.                                                       
      *DTE-WRITTEN.  10DEC2002.                                                 
                                                                                
      * ======================================================== *              
      * SYSTEM OF COMMITMENT  (BP13)                             *              
      * ======================================================== *              
      *    OBJECTIVE  :                                          *              
      *            PROGRAM TO EXTRACT DATA & CREATE FILE FOR     *              
      *            NEW ACCEPTED FLAT CASES                       *              
      *                                                          *              
      *     INPUT FILES:                                         *              
      *         1. BP13F310 - TRANS FILE                         *              
      *         2. BP13K800 - SOC MASTER                         *              
      *         3. BP13K670 - BO INFO                            *              
      *         4. BP13K820 - OCCUP FILE                         *              
      *         5. BM06K110 - FIS FILE                           *              
      *         6. BP13K440 - S&P FILE                           *              
      *         7. BB14K580 - UNTLEASE FILE                      *              
      *         8. BB14K500 - BLKLEASE FILE                      *              
      *                                                          *              
      *     OUTPUT FILES:                                        *              
      *         1. BP13F445  -                                   *              
      *                                                          *              
      * -------------------------------------------------------- *              
      * CHG NO   DATE     BY   DESCRIPTION                       *              
      * -------  -------- ---  -----------                       *              
      * BP132249 10122002 MCC  NEW-PROGRAM                       *              
      * BP132441 20040106 CLT  MOVE BUYER LAWYER INFO            *              
      * BP132660 20050131 JF4  TO GET PREMIUM FROM BP13K800      *              
      * BP132672 20050202 JF4  MOVE NEW TOWN INFO                *              
      * BP132814 20060218 LSB  ADD DBSS ACCEPTANCE               *              
      * BP133137 20080313 ED1  TO CHECK FOR LEAP YEARS           *              
      * BP133209 20080326 ED1  TO CHANGE WS-K110-DTE-MM & DD INTO*              
      *                        2 BYTES EACH                      *              
      * BP133729 20091013 PCL3 TO BYPASS NPL CASES               *              
      * BP134608 20120817 AB9  CATER FOR RESALE LEVY CASE        *              
      * BP134872 20130411 ESD1 TO ADD LEASE COMMENCEMENT DATE,   *              
      *                        TENURE PERIOD AND STUDIO APT TAG. *              
      * BP134833 20130725 ESD1 TO MOVE 99 ON TENURE PERIOD FOR   *              
      *                        NORMAL FLAT AND 30 FOR STUDIO.    *              
      * BP136088 15012016 KSJ3 TO CATER FOR 2-ROOM FLEXI CASES   *              
      * BP136161 10032016 KSJ3 TO TAG SA/SLF FOR SERS LBS CASES  *              
      * BP136161 22032016 KSJ3 TO INCL BP17K000 TO GET SCH ACCT  *              
      * BP136161 18042016 KSJ3 TO REMOVE SERS ANNOUNCEMENT DATE  *              
      *                        CHECKING                          *              
      * BP136161 23052016 KSJ3 TO REMOVE 30 YEARS LEASE TENURE   *              
      *                        LIMIT FOR LBS STUDIO TAGGING      *              
      * BP138702 26042021 ZDD3 TO CATER FOR CCA FLAT 1F CASES    *              
      ************************************************************              
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310 ASSIGN TO BP13F310.                                  
                                                                                
           SELECT BP13K440 ASSIGN TO BP13K440                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K440-NUM-REGN                              
                  FILE STATUS     IS WS-K440-STATUS.                            
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS WS-K820-STATUS.                            
                                                                                
           SELECT BP13K670 ASSIGN TO BP13K670                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K670-KEY-FLD                               
                  FILE STATUS     IS WS-K670-STATUS.                            
                                                                                
           SELECT BM06K100 ASSIGN TO BM06K100                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K100-KEY-FLD OF K100-REC                   
                  FILE STATUS     IS WS-K100-STATUS.                            
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K110-KEY-FLD OF K110-REC                   
                  FILE STATUS     IS WS-K110-STATUS.                            
                                                                                
           SELECT BB14K500 ASSIGN TO BB14K500                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K500-KEY-FLD                               
                  FILE STATUS     IS WS-K500-STATUS.                            
                                                                                
           SELECT BB14K580 ASSIGN TO BB14K580                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K580-KEY-FLD                               
                  FILE STATUS     IS WS-K580-STATUS.                            
                                                                                
           SELECT BP13F445 ASSIGN TO BP13F445.                                  
                                                                                
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       FILE SECTION.                                                            
                                                                                
       FD   BP13F310                                                            
            RECORDING MODE IS F.                                                
       COPY BP13F310.                                                           
                                                                                
       FD   BP13K440.                                                           
       COPY BP13K440.                                                           
                                                                                
       FD   BP13K800.                                                           
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820.                                                           
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K670.                                                           
       COPY BP13K670.                                                           
                                                                                
       FD   BM06K100.                                                           
       COPY BM06K100.                                                           
                                                                                
       FD   BM06K110.                                                           
       COPY BM06K110.                                                           
                                                                                
       FD   BB14K500.                                                           
       COPY BB14K500.                                                           
                                                                                
       FD   BB14K580.                                                           
       COPY BB14K580.                                                           
                                                                                
       FD   BP13F445                                                            
            RECORDING MODE IS F.                                                
       COPY BP13F445.                                                           
                                                                                
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
       01  WS-VARIABLES.                                                        
           05 WS-CURRENT-DATE        PIC X(8) VALUE SPACES.                     
           05 WS-SQL-CODE            PIC 9(4) VALUE ZEROS.                      
           05 WS-NUM-NRIC            PIC X(9) VALUE ZEROS.                      
           05 WS-SELL-PRICE          PIC 9(8) VALUE ZEROS.                      
           05 WS-AMT-PREM            PIC S9(7)V99 VALUE ZEROS.                  
           05 WS-K110-DTE-ACCEPTANCE.                                           
              10 WS-K110-DTE-CCYY    PIC 9(4) VALUE ZEROES.                     
              10 WS-K110-DTE-MM      PIC 9(2) VALUE ZEROES.                     
              10 WS-K110-DTE-DD      PIC 9(2) VALUE ZEROES.                     
                                                                                
       01  WS-FLAGS-AND-FILE-STATUS.                                            
           05 WS-F310-EOF            PIC X(1) VALUE 'N'.                        
           05 WS-K440-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K800-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K820-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K670-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K100-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K110-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K500-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K580-STATUS         PIC 9(2) VALUE ZEROES.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F310-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-F445-WRITE          PIC 9(5) VALUE ZEROES.                     
           05 WS-K440-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-K440-NOTFD          PIC 9(5) VALUE ZEROES.                     
           05 WS-K440-WRITE          PIC 9(5) VALUE ZEROES.                     
           05 WS-K800-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-K800-NOTFD          PIC 9(5) VALUE ZEROES.                     
           05 WS-K800-NPL            PIC 9(5) VALUE ZEROES.                     
           05 WS-K820-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-K820-NOTFD          PIC 9(5) VALUE ZEROES.                     
           05 WS-K670-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-K670-NOTFD          PIC 9(5) VALUE ZEROES.                     
           05 WS-K110-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-K110-NOTFD          PIC 9(5) VALUE ZEROES.                     
           05 WS-K100-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-K100-NOTFD          PIC 9(5) VALUE ZEROES.                     
           05 WS-K500-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-K500-NOTFD          PIC 9(5) VALUE ZEROES.                     
           05 WS-K580-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-K580-NOTFD          PIC 9(5) VALUE ZEROES.                     
           05 WS-SKIP-RECS           PIC 9(5) VALUE ZEROES.                     
           05 WS-SKIP-K440-FND       PIC 9(5) VALUE ZEROES.                     
           05 WS-SKIP-STUDIO         PIC 9(5) VALUE ZEROES.                     
           05 WS-SKIP-NOTBANK        PIC 9(5) VALUE ZEROES.                     
                                                                                
       01  WS-BP13C913                   PIC X(08) VALUE 'BP13C913'.    01190014
       01  WS-REMAINDER                  PIC 9     VALUE ZERO.          01190014
       01  WS-QUOTIENT                   PIC 99    VALUE ZEROS.         01200014
       01  WS-DATE.                                                     3029    
           10  WS-CC1                    PIC 99    VALUE ZEROS.         0028    
           10  WS-YY1                    PIC 99    VALUE ZEROS.         0028    
           10  WS-MM1                    PIC 99    VALUE ZEROS.         0028    
           10  WS-DD1                    PIC 99    VALUE ZEROS.         0028    
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    LINKAGE VARIABLES USED BY THE CALLED SUB-PROGRAM            *        
      *            (BP13C913)  TO RETRIEVE ADDRESS                     *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH      PIC X(4).                               
               10  WS-LINK-NUM-ACC      PIC X(5).                               
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *----------------------------------------------------------------*        
      *    SERS ALLOC SCHEME CHECKING VARIABLES                        *        
      *----------------------------------------------------------------*        
       COPY SERSCH.                                                             
       01  WS-SERS-VARIABLES.                                                   
           05  WS-ALLOC-SCH             PIC X(03) VALUE SPACES.                 
           05  WS-SCH-FND               PIC X(01) VALUE 'N'.                    
           05  WS-DTE-SERS-ANNOUNCE     PIC X(08) VALUE SPACES.                 
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *---------------*                                                         
       000-MAIN-LOGIC.                                                          
      *---------------*                                                         
            PERFORM 100-OPEN-FILES      THRU 100-EXIT.                          
            PERFORM 200-READ-BP13F310   THRU 200-EXIT.                          
            PERFORM 300-PROCESS         THRU 300-EXIT                           
                    UNTIL WS-F310-EOF = 'Y'.                                    
            PERFORM 999-CLOSE-FILES     THRU 999-EXIT.                          
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------*                                                         
       100-OPEN-FILES.                                                          
      *---------------*                                                         
           OPEN INPUT  BP13F310                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K670                                                 
                       BM06K100                                                 
                       BM06K110                                                 
                       BB14K500                                                 
                       BB14K580                                                 
                I-O    BP13K440                                                 
                OUTPUT BP13F445.                                                
                                                                                
           IF WS-K440-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K440-STATUS ' WS-K440-STATUS              
              MOVE WS-K440-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K820-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K820-STATUS ' WS-K820-STATUS              
              MOVE WS-K820-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K670-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K670-STATUS ' WS-K670-STATUS              
              MOVE WS-K670-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K110-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K110-STATUS ' WS-K110-STATUS              
              MOVE WS-K110-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K100-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K100-STATUS ' WS-K100-STATUS              
              MOVE WS-K100-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K500-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K500-STATUS ' WS-K500-STATUS              
              MOVE WS-K500-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K580-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K580-STATUS ' WS-K580-STATUS              
              MOVE WS-K580-STATUS       TO RETURN-CODE.                         
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURRENT-DATE.                  
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       200-READ-BP13F310.                                                       
      *-----------------*                                                       
           READ BP13F310 AT END                                                 
           MOVE 'Y'                      TO WS-F310-EOF                         
           GO TO 200-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-F310-READ.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------*                                                            
       300-PROCESS.                                                             
      *------------*                                                            
           IF F310-REGN-NO = SPACES OR LOW-VALUES                               
              PERFORM 200-READ-BP13F310 THRU 200-EXIT                           
              GO TO 300-EXIT.                                                   
                                                                                
      *-------------------------------------------------------------*           
      * READ BP13K440 TO CHECK IF EXISTING CASE OR NOT                          
      * IF     FOUND, SKIP RECORD.                                              
      * IF REC NOTFD, WRITE BP13K440 FROM NEWLY CREATED BP13F445-REC.           
      *-------------------------------------------------------------*           
           MOVE SPACES           TO BP13K440-REC.                               
           INITIALIZE               BP13K440-REC.                               
                                                                                
           MOVE F310-REGN-NO     TO K440-NUM-REGN.                              
           READ BP13K440.                                                       
                                                                                
           IF WS-K440-STATUS = 00                                               
              ADD 1               TO WS-K440-READ                               
                                     WS-SKIP-RECS                               
                                     WS-SKIP-K440-FND                           
           ELSE                                                                 
              IF WS-K440-STATUS = 23                                            
                 ADD 1            TO WS-K440-NOTFD                              
                 PERFORM 350-PROCESS-NEW-SNP THRU 350-EXIT                      
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K440,STATUS ' WS-K440-STATUS           
                 MOVE WS-K440-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
           PERFORM 200-READ-BP13F310 THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       350-PROCESS-NEW-SNP.                                                     
      *--------------------*                                                    
      *----------------------------------------------------------------*        
      * STUDIO APARTMENT CASES ARE NOT REQUIRED TO SEND S&P DETAILS             
      * SINCE IT IS NOT ELIGIBLE TO USE CPF.                                    
      * ALSO, IF NOT BANK LOAN CASE, SKIP RECORD.                               
      *----------------------------------------------------------------*        
           MOVE SPACES           TO BP13K800-MASTER.                            
           MOVE F310-REGN-NO     TO K800-NUM-REGN.                              
                                                                                
           READ BP13K800.                                                       
           IF WS-K800-STATUS = 00                                               
              ADD 1               TO WS-K800-READ                               
              IF K800-NUM-LEASE-TENURE NOT NUMERIC                              
                 MOVE ZEROES      TO K800-NUM-LEASE-TENURE                      
              END-IF                                                            
              IF (K800-NUM-ALLO-CAT NOT = 'NPL')                                
                  PERFORM 400-WRITE-BP13F445 THRU 400-EXIT                      
              ELSE                                                              
                  IF (K800-NUM-ALLO-CAT    = 'NPL') AND                         
                     ((K800-NUM-FLAT-TYPE  = '1A' OR '2A') OR                   
                      (K800-NUM-IDP-SCHEME = 'SA '))                            
                     PERFORM 400-WRITE-BP13F445 THRU 400-EXIT                   
                  ELSE                                                          
                     ADD 1         TO WS-K800-NPL                               
                  END-IF                                                        
              END-IF                                                            
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 ADD 1               TO WS-K800-NOTFD                           
                 DISPLAY 'REC NOTFD BP13K800 ' WS-K800-STATUS ' '               
                                   F310-REGN-NO                                 
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K800,STATUS ' WS-K800-STATUS           
                 MOVE WS-K800-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       400-WRITE-BP13F445.                                                      
      *------------------*                                                      
      *----------------------*                                                  
      * CREATE BP13F445-REC                                                     
      *----------------------*                                                  
           MOVE SPACES TO BP13F445-REC.                                         
           INITIALIZE     BP13F445-REC.                                         
                                                                                
           MOVE F310-REGN-NO          TO   F445-NUM-REGN.                       
           MOVE 'N'                   TO   F445-NUM-ADJ-FLAT                    
           MOVE SPACES                TO   F445-NUM-COMB-FLAT                   
                                           F445-NUM-BUYER-RET-HDB-REF.          
           MOVE ZEROES                TO   F445-AMT-CASH-DOWNPAYMENT.           
                                                                                
      *-----------------------------*                                           
      * EXTRACT INFO FROM BP13K800                                              
      *-----------------------------*                                           
           MOVE K800-NUM-STATUS            TO   F445-NUM-STATUS.                
           MOVE K800-NUM-SCH-ACC           TO   F445-NUM-HDB-REF.               
           MOVE K800-NUM-HOLDING-TYPE      TO   F445-NUM-HOLDING-TYPE.          
           MOVE K800-NUM-LOAN-TAG          TO   F445-CDE-BUYER-LOAN-TAG.        
           MOVE K800-NUM-NEW-TOWN          TO   F445-NUM-TOWN.                  
           IF K800-NUM-ALLO-CAT = 'DBS'                                         
              MOVE 'Y'                     TO   F445-NUM-DBSS                   
              MOVE K800-NUM-PAYMENT-SCHEME TO   F445-NUM-PAYMENT-SCHEME         
           END-IF.                                                              
                                                                                
           IF K800-DTE-SO-APPT = SPACES OR LOW-VALUES                           
              MOVE ZEROES                  TO   F445-DTE-SO-APPT                
           ELSE                                                                 
              MOVE K800-DTE-SO-APPT        TO   F445-DTE-SO-APPT                
           END-IF.                                                              
                                                                                
           IF K800-DTE-REGN = SPACES OR LOW-VALUES                              
              MOVE ZEROES                  TO   F445-DTE-APPL-RECEIVED          
           ELSE                                                                 
              MOVE K800-DTE-REGN           TO   F445-DTE-APPL-RECEIVED          
           END-IF.                                                              
                                                                                
           EVALUATE K800-NUM-ROOM                                               
           WHEN '1'                                                             
              MOVE '01'                    TO   F445-NUM-FLAT-TYPE              
           WHEN '2'                                                             
              MOVE '02'                    TO   F445-NUM-FLAT-TYPE              
           WHEN '3'                                                             
              MOVE '03'                    TO   F445-NUM-FLAT-TYPE              
           WHEN '4'                                                             
              MOVE '04'                    TO   F445-NUM-FLAT-TYPE              
           WHEN '5'                                                             
              MOVE '05'                    TO   F445-NUM-FLAT-TYPE              
           WHEN '6'                                                             
              MOVE '06'                    TO   F445-NUM-FLAT-TYPE              
           WHEN '7'                                                             
              MOVE '07'                    TO   F445-NUM-FLAT-TYPE              
           WHEN '8'                                                             
              MOVE '08'                    TO   F445-NUM-FLAT-TYPE              
           WHEN 'E'                                                             
              MOVE '06'                    TO   F445-NUM-FLAT-TYPE              
           WHEN OTHER                                                           
              MOVE SPACES                  TO   F445-NUM-FLAT-TYPE              
           END-EVALUATE.                                                        
                                                                                
      *-------------------------*                                               
      * EXTRACT INFO FROM PIDB                                                  
      *-------------------------*                                               
           PERFORM 500-CALL-BP13C913       THRU 500-EXIT.                       
           MOVE COMM8-NUM-BLK              TO   F445-NUM-BLOCK.                 
           MOVE COMM8-NME-STREET           TO   F445-NME-STREET.                
           MOVE COMM8-NUM-LEVEL            TO   F445-NUM-HSELEVEL.              
           STRING COMM8-NUM-UNIT-MAIN    COMM8-NUM-UNIT-SUB                     
                  DELIMITED BY SIZE      INTO   F445-NUM-MAIN.                  
           MOVE COMM8-NUM-POSTAL-CODE      TO   F445-NUM-POSTAL.                
           MOVE COMM8-CDE-ST               TO   F445-CDE-STREET.                
           MOVE COMM8-NUM-MGMT             TO   F445-NUM-BO.                    
                                                                                
      *------------------------*                                                
      * EXTRACT INFO FROM FIS                                                   
      *------------------------*                                                
           PERFORM 600-READ-BM06K110       THRU 600-EXIT.                       
           IF K110-DTE-ACCEPTANCE = SPACES OR LOW-VALUES                        
              MOVE ZEROES                  TO   F445-DTE-BOOK                   
           ELSE                                                                 
              MOVE K110-DTE-ACCEPTANCE     TO   F445-DTE-BOOK                   
           END-IF.                                                              
                                                                                
           IF K110-DTE-KEY-ISSUED = SPACES OR LOW-VALUES                        
              MOVE ZEROES                  TO   F445-DTE-KEY-ISSUE              
           ELSE                                                                 
              MOVE K110-DTE-KEY-ISSUED     TO   F445-DTE-KEY-ISSUE              
           END-IF.                                                              
                                                                                
           IF K110-CURR-SELLING-PRICE NOT  NUMERIC                              
              MOVE ZEROES             TO   K110-CURR-SELLING-PRICE.             
                                                                                
           IF K800-NUM-PREMIUM-RATE   NOT  NUMERIC                              
              MOVE ZEROES             TO   K800-NUM-PREMIUM-RATE.               
                                                                                
           IF K800-AMT-PREMIUM        NOT  NUMERIC                              
              MOVE ZEROES             TO   K800-AMT-PREMIUM.                    
                                                                                
           MOVE K110-CURR-SELLING-PRICE TO WS-SELL-PRICE.                       
                                                                                
           MOVE K110-AIX1  TO   K100-KEY-FLD OF K100-REC                        
           PERFORM 601-READ-BM06K100       THRU 601-EXIT                        
           MOVE K100-CDE-BLK         TO K500-KEY-FLD                            
           MOVE K110-SCH-ACC-NO(1:9) TO K580-KEY-FLD                            
           PERFORM 650-READ-BB14K580 THRU 650-EXIT                              
           PERFORM 660-READ-BB14K500 THRU 660-EXIT                              
           IF (K580-DTE-LEASE-COMM = SPACES OR LOW-VALUES) OR                   
              (K580-DTE-LEASE-COMM IS NOT NUMERIC)                              
               MOVE '00000000'  TO K580-DTE-LEASE-COMM                          
           END-IF                                                               
           IF (K500-DTE-LEASE-COMM-FLAT = SPACES OR LOW-VALUES) OR              
              (K500-DTE-LEASE-COMM-FLAT IS NOT NUMERIC)                         
               MOVE '00000000'  TO K500-DTE-LEASE-COMM-FLAT                     
           END-IF                                                               
           IF (K580-NUM-LEASE-PERIOD NOT NUMERIC) OR                            
              (K580-NUM-LEASE-PERIOD = 0)                                       
               MOVE 30         TO K580-NUM-LEASE-PERIOD                         
           END-IF                                                               
           IF (K500-NUM-LEASE-PERIOD-FLAT NOT NUMERIC) OR                       
              (K500-NUM-LEASE-PERIOD-FLAT = 0)                                  
               MOVE 99         TO K500-NUM-LEASE-PERIOD-FLAT                    
           END-IF                                                               
           PERFORM 420-COMP-SA-SLF-TAG   THRU 420-EXIT                          
                                                                                
           IF (K110-FLAT-STATUS = 'NW') AND                                     
              (K110-DTE-HANDOVER = ZEROES OR SPACES) AND                        
              (K110-DTE-KEY-AVAIL = ZEROES OR SPACES)                           
               MOVE 'U'               TO F445-NUM-STATUS-COMPLETION             
               IF K580-DTE-UNIT-DELIVERY NOT = ZEROES AND SPACES                
                  MOVE K580-DTE-UNIT-DELIVERY                                   
                                      TO F445-DTE-EXPECTED-TOP                  
               ELSE                                                             
                  MOVE K110-DTE-ACCEPTANCE  TO WS-K110-DTE-ACCEPTANCE           
                  COMPUTE WS-K110-DTE-CCYY = WS-K110-DTE-CCYY + 2               
                  IF WS-K110-DTE-MM = 02 AND WS-K110-DTE-DD = 29                
                     PERFORM 410-CHECK-LEAP-YR THRU 410-EXIT                    
                  END-IF                                                        
                  MOVE WS-K110-DTE-ACCEPTANCE                                   
                                         TO F445-DTE-EXPECTED-TOP               
               END-IF                                                           
           ELSE                                                                 
               MOVE 'C'               TO F445-NUM-STATUS-COMPLETION             
               MOVE ZEROS             TO F445-DTE-EXPECTED-TOP                  
           END-IF.                                                              
                                                                                
           IF K800-NUM-PREMIUM-RATE > ZEROES                                    
              MOVE K800-AMT-PREMIUM   TO   WS-AMT-PREM                          
              ADD WS-AMT-PREM         TO   WS-SELL-PRICE                        
           ELSE                                                                 
               IF K800-NUM-RESALE-LEVY-TAG = 'Y'     AND                        
                  K800-NUM-PREMIUM-RATE    = ZEROES  AND                        
                  K800-AMT-PREMIUM         > ZEROS                              
                     MOVE K800-AMT-PREMIUM   TO   WS-AMT-PREM                   
                     ADD WS-AMT-PREM         TO   WS-SELL-PRICE                 
               END-IF                                                           
           END-IF.                                                              
           MOVE WS-SELL-PRICE         TO   F445-AMT-PURCHASE-PRICE              
                                           F445-AMT-VALUATION.                  
                                                                                
      *------------------------------*                                          
      * EXTRACT INFO FROM OCCUP FILE                                            
      *------------------------------*                                          
           PERFORM 700-GET-APPL-DETAILS    THRU 700-EXIT.                       
                                                                                
      *---------------------*                                                   
      * INITIALIZE CPFGRANT.                                                    
      *---------------------*                                                   
           MOVE ZEROES        TO F445-AMT-CPF-GRANT-BUYER1                      
                                 F445-AMT-CPF-GRANT-BUYER2                      
                                 F445-AMT-CPF-GRANT-BUYER3                      
                                 F445-AMT-CPF-GRANT-BUYER4.                     
                                                                                
      *-------------------*                                                     
      * EXTRACT BO MASTER                                                       
      *-------------------*                                                     
           IF K800-NUM-LOAN-TAG = 'B'                                           
              PERFORM 800-READ-BP13K670 THRU 800-EXIT                           
              MOVE K670-NME-BUYER-LAWYER TO                                     
                   F445-NME-BUYER-SOLICITOR                                     
              MOVE K670-NME-BUYER-LAWYER-ADDR TO                                
                   F445-NUM-BUYER-SOLICITOR-ADDR                                
           END-IF.                                                              
                                                                                
           MOVE '1'                         TO F445-CDE-ACTION-TAG.             
           MOVE WS-CURRENT-DATE             TO F445-DTE-HDB-SEND                
                                               F445-DTE-UPDATE.                 
           MOVE 'BP13C342'                  TO F445-NUM-USERID.                 
                                                                                
           MOVE SPACES       TO BP13K440-REC.                                   
           INITIALIZE           BP13K440-REC.                                   
           MOVE BP13F445-REC TO BP13K440-REC.                                   
                                                                                
      *----------------------------------------------------*                    
      * CREATE AN F445-REC WITH ACTION TAG = '1'(NEW CASE)                      
      *----------------------------------------------------*                    
           WRITE BP13F445-REC.                                                  
           ADD 01  TO WS-F445-WRITE.                                            
                                                                                
           PERFORM 450-WRITE-BP13K440 THRU 450-EXIT.                            
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       410-CHECK-LEAP-YR.                                                       
      *------------------*                                                      
           MOVE WS-K110-DTE-ACCEPTANCE     TO WS-DATE.                  03390017
                                                                        03390017
           DIVIDE WS-YY1 BY 4 GIVING    WS-QUOTIENT                     03390017
                              REMAINDER WS-REMAINDER.                   03400017
           IF WS-REMAINDER = 0                                          03410017
              MOVE 29  TO WS-K110-DTE-DD                                03420017
           ELSE                                                         03410017
              MOVE 28  TO WS-K110-DTE-DD                                03420017
           END-IF.                                                      03430017
                                                                        03430017
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       420-COMP-SA-SLF-TAG.                                                     
      *--------------------*                                                    
           MOVE SPACES                   TO F445-NUM-STUDIO.                    
                                                                                
      *---------------------------------------------------------------          
      *    TAG FOR 2 ROOM FLEXI (2F) AND CCA (1F)                               
      *---------------------------------------------------------------          
           IF (K800-NUM-FLAT-TYPE    = '2F' OR '1F')  AND                       
              (K800-NUM-LEASE-TENURE > ZEROES AND                               
               K800-NUM-LEASE-TENURE <= 45)                                     
              MOVE K800-NUM-FLAT-TYPE    TO F445-NUM-FLAT-TYPE                  
              MOVE K800-NUM-LEASE-TENURE TO F445-NUM-LEASE-TENURE               
              MOVE ZEROES                TO F445-DTE-LEASE-COMMENCEMENT         
              MOVE 'Y'                   TO F445-NUM-STUDIO                     
              GO TO 420-EXIT                                                    
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------          
      *    TAG FOR STUDIO APT (1A AND 2A)                                       
      *---------------------------------------------------------------          
           IF (K800-NUM-FLAT-TYPE = '1A' OR '2A') OR                            
              (K800-NUM-IDP-SCHEME = 'SA ')                                     
              MOVE 'Y'                   TO F445-NUM-STUDIO                     
              MOVE K580-DTE-LEASE-COMM   TO F445-DTE-LEASE-COMMENCEMENT         
                                                                                
              MOVE K580-NUM-LEASE-PERIOD TO F445-NUM-LEASE-TENURE               
              GO TO 420-EXIT                                                    
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------          
      *    TAG FOR SERS LEASE BUY BACK SCHEME                                   
      *---------------------------------------------------------------          
           IF (K800-NUM-LEASE-BUY-BACK = 'Y')   AND                             
              (K800-NUM-LEASE-TENURE   > ZEROES)                                
              MOVE K800-NUM-ALLO-SCHEME  TO WS-ALLOC-SCH                        
              PERFORM 421-CHECK-SERS     THRU 421-EXIT                          
                                                                                
              IF WS-SCH-FND             = 'Y' OR                                
                 K800-NUM-PORT-ELIG-TAG = 'Y'                                   
                 MOVE 'Y'                TO F445-NUM-STUDIO                     
                 MOVE K800-NUM-LEASE-TENURE                                     
                                         TO F445-NUM-LEASE-TENURE               
                 IF F445-DTE-KEY-ISSUE = SPACES OR LOW-VALUES OR                
                                         ZEROES                                 
                    MOVE ZEROES          TO F445-DTE-LEASE-COMMENCEMENT         
                 ELSE                                                           
                    MOVE F445-DTE-KEY-ISSUE                                     
                                         TO F445-DTE-LEASE-COMMENCEMENT         
                 END-IF                                                         
                 GO TO 420-EXIT                                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------          
      *    FOR ALL OTHER CASES                                                  
      *---------------------------------------------------------------          
           IF F445-NUM-STUDIO = SPACES                                          
              MOVE K500-DTE-LEASE-COMM-FLAT                                     
                                         TO F445-DTE-LEASE-COMMENCEMENT         
              MOVE K500-NUM-LEASE-PERIOD-FLAT                                   
                                         TO F445-NUM-LEASE-TENURE               
           END-IF.                                                              
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       421-CHECK-SERS.                                                          
      *---------------*                                                         
                                                                                
           IF WS-ALLOC-SCH  NOT =  LOW-VALUES AND SPACES                        
              SET WS-SERS-PTR  TO 1                                             
              SEARCH WS-SERS-SCH                                                
                 AT END                                                         
                    MOVE 'N'               TO   WS-SCH-FND                      
                 WHEN WS-SERS-SCHEME(WS-SERS-PTR) = WS-ALLOC-SCH                
                    MOVE 'Y'               TO   WS-SCH-FND                      
              END-SEARCH                                                        
           ELSE                                                                 
              MOVE 'N'                     TO   WS-SCH-FND                      
           END-IF.                                                              
                                                                                
       421-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       450-WRITE-BP13K440.                                                      
      *-------------------*                                                     
      *---------------------------------------------------------------*         
      * WRITE INFORMATION BASED ON FILES                                        
      *---------------------------------------------------------------*         
           WRITE BP13K440-REC.                                                  
                                                                                
           IF WS-K440-STATUS = 00 OR 02 OR 22                                   
              ADD 1          TO WS-K440-WRITE                                   
           ELSE                                                                 
              DISPLAY 'ERROR WRITING BP13K440,STATUS ' WS-K440-STATUS           
              MOVE WS-K440-STATUS     TO RETURN-CODE                            
              GO TO 999-CLOSE-FILES.                                            
                                                                                
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       500-CALL-BP13C913.                                                       
      *------------------*                                                      
           MOVE SPACES            TO BP13COMM8-REC.                             
           INITIALIZE                BP13COMM8-REC.                             
                                                                                
           MOVE K800-NUM-SCH-ACC  TO WS-LINK-REC.                               
                                                                                
      **** REMOVE AFTER TESTING ****                                            
      *    MOVE '020400372'       TO WS-LINK-REC.                               
      **** REMOVE AFTER TESTING ****                                            
                                                                                
           CALL WS-BP13C913  USING WS-LINK-REC,                                 
                                  BP13COMM8-REC.                                
                                                                                
           IF COMM8-CDE-SYSERR NOT = ZEROES                                     
              MOVE COMM8-CDE-SYSERR TO WS-SQL-CODE                              
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE   ' WS-SQL-CODE        
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE   ' WS-SQL-CODE.              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       600-READ-BM06K110.                                                       
      *------------------*                                                      
           MOVE K800-NUM-SCH-ACC TO K110-SCH-ACC-NO.                            
                                                                                
           READ BM06K110.                                                       
           IF WS-K110-STATUS = 00                                               
              ADD 1              TO WS-K110-READ                                
           ELSE                                                                 
              MOVE SPACES TO K110-REC                                           
              IF WS-K110-STATUS = 23                                            
                 ADD 1           TO WS-K110-NOTFD                               
                 DISPLAY 'REC NOTFD BM06K110 ' WS-K110-STATUS ' '               
                                               K800-NUM-SCH-ACC                 
              ELSE                                                              
                 DISPLAY 'ERROR READ BM06K110,STATUS ' WS-K110-STATUS           
                 MOVE WS-K110-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       601-READ-BM06K100.                                                       
      *------------------*                                                      
                                                                                
           READ BM06K100.                                                       
           IF WS-K100-STATUS = 00                                               
              ADD 1              TO WS-K100-READ                                
           ELSE                                                                 
              MOVE SPACES TO K100-REC                                           
              IF WS-K100-STATUS = 23                                            
                 ADD 1           TO WS-K100-NOTFD                               
                 DISPLAY 'REC NOTFD BM06K100 ' WS-K100-STATUS ' '               
                                               K100-KEY-FLD OF K100-REC         
              ELSE                                                              
                 DISPLAY 'ERROR READ BM06K100,STATUS ' WS-K100-STATUS           
                 MOVE WS-K100-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       601-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       650-READ-BB14K580.                                                       
      *------------------*                                                      
                                                                                
           READ BB14K580.                                                       
           IF WS-K580-STATUS = 00                                               
              ADD 1              TO WS-K580-READ                                
           ELSE                                                                 
              MOVE SPACES TO BB14K580-REC                                       
              IF WS-K580-STATUS = 23                                            
                 ADD 1           TO WS-K580-NOTFD                               
                 DISPLAY 'REC NOTFD BB14K580 ' WS-K580-STATUS ' '               
                                               K580-NUM-SCH-ACCNT               
              ELSE                                                              
                 DISPLAY 'ERROR READ BB14K580,STATUS ' WS-K580-STATUS           
                 MOVE WS-K580-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       660-READ-BB14K500.                                                       
      *------------------*                                                      
                                                                                
           READ BB14K500.                                                       
           IF WS-K500-STATUS = 00                                               
              ADD 1              TO WS-K500-READ                                
           ELSE                                                                 
              MOVE SPACES TO BB14K500-REC                                       
              IF WS-K500-STATUS = 23                                            
                 ADD 1           TO WS-K500-NOTFD                               
                 DISPLAY 'REC NOTFD BB14K500 ' WS-K500-STATUS ' '               
                                               K100-CDE-BLK                     
              ELSE                                                              
                 DISPLAY 'ERROR READ BB14K500,STATUS ' WS-K500-STATUS           
                 MOVE WS-K500-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       660-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       700-GET-APPL-DETAILS.                                                    
      *---------------------*                                                   
           MOVE SPACES                  TO   BP13K820-REC.                      
           MOVE F310-REGN-NO            TO   K820-NUM-REGN.                     
                                                                                
           MOVE K800-NUM-NRIC1          TO   K820-NUM-NRIC                      
                                             WS-NUM-NRIC                        
                                             F445-NUM-BUYER1-NRIC.              
                                                                                
           PERFORM 750-READ-BP13K820    THRU 750-EXIT.                          
           MOVE K820-NME-OCCP           TO   F445-NME-BUYER1.                   
                                                                                
           IF K820-NME-OCCP = 'JTC' OR 'ADM'                                    
              MOVE SPACES               TO   F445-NUM-BUYER1-CPF                
           ELSE                                                                 
              MOVE K820-NUM-CPF         TO   F445-NUM-BUYER1-CPF                
           END-IF.                                                              
                                                                                
           STRING K800-NUM-SHR1 '/' K800-NUM-SHR-BASE                           
                  DELIMITED BY SIZE   INTO  F445-NUM-BUYER1-PCT-HOLDING.        
                                                                                
           IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-NRIC2       TO   K820-NUM-NRIC WS-NUM-NRIC          
              PERFORM 750-READ-BP13K820 THRU 750-EXIT                           
              IF WS-K820-STATUS = 00                                            
                 MOVE K800-NUM-NRIC2    TO   F445-NUM-BUYER2-NRIC               
                 MOVE K820-NME-OCCP     TO   F445-NME-BUYER2                    
                 MOVE K820-NUM-CPF      TO   F445-NUM-BUYER2-CPF                
                 STRING K800-NUM-SHR2 '/' K800-NUM-SHR-BASE                     
                    DELIMITED BY SIZE INTO  F445-NUM-BUYER2-PCT-HOLDING         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-NRIC3       TO   K820-NUM-NRIC WS-NUM-NRIC          
              PERFORM 750-READ-BP13K820 THRU 750-EXIT                           
              IF WS-K820-STATUS = 00                                            
                 MOVE K800-NUM-NRIC3    TO   F445-NUM-BUYER3-NRIC               
                 MOVE K820-NME-OCCP     TO   F445-NME-BUYER3                    
                 MOVE K820-NUM-CPF      TO   F445-NUM-BUYER3-CPF                
                 STRING K800-NUM-SHR3 '/' K800-NUM-SHR-BASE                     
                    DELIMITED BY SIZE INTO  F445-NUM-BUYER3-PCT-HOLDING         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-NRIC4       TO   K820-NUM-NRIC WS-NUM-NRIC          
              PERFORM 750-READ-BP13K820 THRU 750-EXIT                           
              IF WS-K820-STATUS = 00                                            
                 MOVE K800-NUM-NRIC4    TO   F445-NUM-BUYER4-NRIC               
                 MOVE K820-NME-OCCP     TO   F445-NME-BUYER4                    
                 MOVE K820-NUM-CPF      TO   F445-NUM-BUYER4-CPF                
                 STRING K800-NUM-SHR4 '/' K800-NUM-SHR-BASE                     
                    DELIMITED BY SIZE INTO  F445-NUM-BUYER4-PCT-HOLDING         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       750-READ-BP13K820.                                                       
      *------------------*                                                      
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              ADD 1       TO WS-K820-READ                                       
           ELSE                                                                 
              MOVE SPACES TO BP13K820-REC                                       
              IF WS-K820-STATUS = 23                                            
                 ADD 1    TO WS-K820-NOTFD                                      
                 DISPLAY 'REC NOTFD BP13K820 ' WS-K820-STATUS ' '               
                                   F310-REGN-NO  WS-NUM-NRIC                    
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K820,STATUS ' WS-K820-STATUS           
                 MOVE WS-K820-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       750-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       800-READ-BP13K670.                                                       
      *------------------*                                                      
           MOVE SPACES           TO BP13K670-REC.                               
           MOVE F310-REGN-NO     TO K670-NUM-REGN.                              
                                                                                
           READ BP13K670.                                                       
           IF WS-K670-STATUS = 00                                               
              ADD 1              TO WS-K670-READ                                
           ELSE                                                                 
              MOVE SPACES        TO BP13K670-REC                                
              IF WS-K670-STATUS = 23                                            
                 ADD 1           TO WS-K670-NOTFD                               
                 DISPLAY 'REC NOTFD BP13K670 ' WS-K670-STATUS ' '               
                                               F310-REGN-NO                     
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K670,STATUS ' WS-K670-STATUS           
                 MOVE WS-K670-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *----------------*                                                        
       999-CLOSE-FILES.                                                         
      *----------------*                                                        
           DISPLAY ' '.                                                         
           DISPLAY '*----------------- BP13C342 -------------------'.           
                                                                                
           DISPLAY 'NO OF REC READ      BP13F310     = '                        
                    WS-F310-READ.                                               
           DISPLAY 'NO OF REC SKIPPED   BP13F310     = '                        
                    WS-SKIP-RECS.                                               
           DISPLAY '              (FND IN K440)      = '                        
                    WS-SKIP-K440-FND.                                           
           DISPLAY '              (STUDIO APT)       = '                        
                    WS-SKIP-STUDIO.                                             
           DISPLAY '              (NOT BANK LOAN)    = '                        
                    WS-SKIP-NOTBANK.                                            
           DISPLAY 'NO OF REC READ      BP13K440     = '                        
                    WS-K440-READ.                                               
           DISPLAY 'NO OF REC NOTFD     BP13K440     = '                        
                    WS-K440-NOTFD.                                              
           DISPLAY 'NO OF REC WRITTEN   BP13K440     = '                        
                    WS-K440-WRITE.                                              
           DISPLAY 'NO OF REC READ      BP13K800     = '                        
                    WS-K800-READ.                                               
           DISPLAY 'NO OF REC NOTFD     BP13K800     = '                        
                    WS-K800-NOTFD.                                              
           DISPLAY 'NO OF NPL CASES     BP13K800     = '                        
                    WS-K800-NPL.                                                
           DISPLAY 'NO OF REC READ      BP13K820     = '                        
                    WS-K820-READ.                                               
           DISPLAY 'NO OF REC NOTFD     BP13K820     = '                        
                    WS-K820-NOTFD                                               
           DISPLAY 'NO OF REC READ      BP13K670     = '                        
                    WS-K670-READ.                                               
           DISPLAY 'NO OF REC NOTFD     BP13K670     = '                        
                    WS-K670-NOTFD.                                              
           DISPLAY 'NO OF REC READ      BM06K110     = '                        
                    WS-K110-READ.                                               
           DISPLAY 'NO OF REC NOTFD     BM06K110     = '                        
                    WS-K110-NOTFD.                                              
           DISPLAY 'NO OF REC READ      BM06K100     = '                        
                    WS-K100-READ.                                               
           DISPLAY 'NO OF REC NOTFD     BM06K100     = '                        
                    WS-K100-NOTFD.                                              
           DISPLAY 'NO OF REC READ      BB14K500     = '                        
                    WS-K500-READ.                                               
           DISPLAY 'NO OF REC NOTFD     BB14K500     = '                        
                    WS-K500-NOTFD.                                              
           DISPLAY 'NO OF REC READ      BB14K580     = '                        
                    WS-K580-READ.                                               
           DISPLAY 'NO OF REC NOTFD     BB14K580     = '                        
                    WS-K580-NOTFD.                                              
           DISPLAY 'NO OF RECS WRITTEN (BP13F445)    = '                        
                    WS-F445-WRITE.                                              
                                                                                
           CLOSE BP13F310                                                       
                 BP13K440                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K670                                                       
                 BM06K110                                                       
                 BM06K100                                                       
                 BB14K500                                                       
                 BB14K580                                                       
                 BP13F445.                                                      
                                                                                
           IF WS-K440-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'CLOSING ERROR, K440-STATUS ' WS-K440-STATUS              
              MOVE WS-K440-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'CLOSING ERROR, K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K820-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'CLOSING ERROR, K820-STATUS ' WS-K820-STATUS              
              MOVE WS-K820-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K670-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'CLOSING ERROR, K670-STATUS ' WS-K670-STATUS              
              MOVE WS-K670-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K110-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'CLOSING ERROR, K110-STATUS ' WS-K110-STATUS              
              MOVE WS-K110-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K100-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'CLOSING ERROR, K110-STATUS ' WS-K100-STATUS              
              MOVE WS-K100-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K500-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'CLOSING ERROR, K500-STATUS ' WS-K500-STATUS              
              MOVE WS-K500-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K580-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'CLOSING ERROR, K580-STATUS ' WS-K580-STATUS              
              MOVE WS-K580-STATUS       TO RETURN-CODE.                         
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
