       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C532.                                                 
      *AUTHOR.        GD.                                                       
      *DATE-WRITTEN.  07/03/95.                                                 
      * ====================================================== *                
      *    SYSTEM OF COMMITMENT (BP13)                         *                
      * ====================================================== *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *    1.  TO UPDATE HUDC LESSEE TO SOC BALLOT FILE FOR    *                
      *        MATURE NT AS '07' TO EXISTING FLAT TYPE.        *                
      *                                                        *                
      *    INPUT  FILES : BP13F558                             *                
      *                                                        *                
      *    I/O   FILE   : BP13K740                             *                
      *                                                        *                
      *    LISTING      : BP13L532                             *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE    DETAILS                          *                
      * -------- --- -------- -------                          *                
      * BP130445 SSS 19980205 EXPAND F558 RECL FROM 150 TO 400 *                
      * BP130652 NOR 19990222 EXPAND K740 RECL FROM 300 TO 500 *                
      * BP130897 AKT 20000801 EXPAND F558 RECL FROM 400 TO 600 *                
      * BP132810 CKK 20060329 CHANGE REF NO FROM 6 TO 8 BYTES  *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K740 ASSIGN TO BP13K740                                   
              ORGANIZATION IS INDEXED                                           
              ACCESS MODE  IS RANDOM                                            
              RECORD KEY   IS K740-KEY-FLD                                      
              ALTERNATE RECORD KEY                                              
                           IS K740-ALT1-KEY                                     
              FILE STATUS  IS WS-K740-STATUS.                                   
                                                                                
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
                                                                                
           SELECT BP13L532 ASSIGN TO BP13L532.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13K740                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K740.                                                           
                                                                                
       FD  BP13F558                                                             
           RECORDING MODE    F                                                  
           BLOCK CONTAINS    0 RECORDS                                          
           LABEL RECORDS   ARE STANDARD                                         
           RECORD CONTAINS 600 CHARACTERS.                                      
       COPY BP13F558.                                                           
                                                                                
       FD  BP13L532                                                             
           RECORDING MODE    F                                                  
           BLOCK CONTAINS    0 RECORDS                                          
           LABEL RECORDS   ARE OMITTED                                          
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13L532-REC    PIC X(132).                                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-K740-STATUS                 PIC 9(2).                         
           05  WS-F558-EOF-SW                 PIC X(1)  VALUE 'N'.              
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-K740-FOUND-CTR              PIC 9(7)  VALUE ZEROS.            
           05  WS-K740-NOTFND-CTR             PIC 9(7)  VALUE ZEROS.            
           05  WS-K740-UPDATE-CTR             PIC 9(7)  VALUE ZEROS.            
           05  WS-K740-BYPASS-CTR             PIC 9(7)  VALUE ZEROS.            
           05  WS-F558-READ-CTR               PIC 9(7)  VALUE ZEROS.            
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-YY                  PIC 9(4).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-MM               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-YY               PIC 9(4).                         
                                                                                
       01  WS-BP13L532-CONTROLS.                                                
           05  WS-L532-LINECTR               PIC 9(2) VALUE 99.                 
           05  WS-L532-PAGECTR               PIC 9(5) VALUE ZEROS.              
           05  WS-L532-S-NO                  PIC 9(7) VALUE ZEROS.              
           05  WS-L532-LINE-SPACING          PIC 9(1) VALUE 1.                  
                                                                                
       01  WS-OTHER-VARS.                                                       
           05  WS-PREV-REGN                  PIC X(8) VALUE SPACES.             
                                                                                
      *============================================================*            
      *         BP13L532 -   R E P O R T    L A Y O U T            *            
      *============================================================*            
       01  L532-HEADER-01.                                                      
           05  FILLER              PIC X(08)     VALUE 'BP13L532'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F   C O M M I T M E N T'.                      
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  L532-RUN-DATE      PIC X(10)     VALUE SPACES.                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L532-PAGE-NO       PIC ZZZZ9 .                                   
                                                                                
       01  L532-HEADER-02.                                                      
           05  FILLER              PIC X(41)     VALUE SPACES.                  
           05  FILLER              PIC X(50) VALUE                              
               'REPORT ON APPLICANTS FOUND AS LESSEE OF HUDC FLATS'.            
                                                                                
       01  L532-HEADER-03.                                                      
           05  FILLER          PIC X(04)     VALUE SPACES.                      
           05  FILLER          PIC X(07)     VALUE '  S/N '.                    
           05  FILLER          PIC X(05)     VALUE SPACES.                      
           05  FILLER          PIC X(06)     VALUE 'REF NO'.                    
           05  FILLER          PIC X(05)     VALUE SPACES.                      
           05  FILLER          PIC X(09)     VALUE '  NRIC'.                    
           05  FILLER          PIC X(05)     VALUE SPACES.                      
           05  FILLER          PIC X(14)     VALUE ' HDB-REF-SMS'.              
           05  FILLER          PIC X(05)     VALUE SPACES.                      
           05  FILLER          PIC X(13)     VALUE 'FLAT-TYPE-SMS'.             
           05  FILLER          PIC X(05)     VALUE SPACES.                      
           05  FILLER          PIC X(15)     VALUE 'FILE-STATUS-SMS'.           
           05  FILLER          PIC X(05)     VALUE SPACES.                      
           05  FILLER          PIC X(06)     VALUE 'NT-REQ'.                    
           05  FILLER          PIC X(05)     VALUE SPACES.                      
           05  FILLER          PIC X(06)     VALUE 'FT-REQ'.                    
           05  FILLER          PIC X(05)     VALUE SPACES.                      
           05  FILLER          PIC X(07)     VALUE 'SUCC-NT'.                   
                                                                                
       01  L532-HEADER-04.                                                      
           05  FILLER          PIC X(04)     VALUE SPACES.                      
           05  FILLER          PIC X(07)     VALUE ALL '_'.                     
           05  FILLER          PIC X(05)     VALUE SPACES.                      
           05  FILLER          PIC X(06)     VALUE ALL '_'.                     
           05  FILLER          PIC X(05)     VALUE SPACES.                      
           05  FILLER          PIC X(09)     VALUE ALL '_'.                     
           05  FILLER          PIC X(05)     VALUE SPACES.                      
           05  FILLER          PIC X(14)     VALUE ALL '_'.                     
           05  FILLER          PIC X(05)     VALUE SPACES.                      
           05  FILLER          PIC X(13)     VALUE ALL '_'.                     
           05  FILLER          PIC X(05)     VALUE SPACES.                      
           05  FILLER          PIC X(15)     VALUE ALL '_'.                     
           05  FILLER          PIC X(05)     VALUE SPACES.                      
           05  FILLER          PIC X(06)     VALUE ALL '_'.                     
           05  FILLER          PIC X(05)     VALUE SPACES.                      
           05  FILLER          PIC X(06)     VALUE ALL '_'.                     
           05  FILLER          PIC X(05)     VALUE SPACES.                      
           05  FILLER          PIC X(07)     VALUE ALL '_'.                     
                                                                                
       01  L532-DETAIL-01.                                                      
           05  FILLER          PIC X(04)     VALUE SPACES.                      
           05  L532-S-NO       PIC ZZZZZZZ.                                     
           05  FILLER          PIC X(04)     VALUE SPACES.                      
           05  L532-REF-NO     PIC X(08).                                       
           05  FILLER          PIC X(04)     VALUE SPACES.                      
           05  L532-NRIC       PIC X(09).                                       
           05  FILLER          PIC X(05)     VALUE SPACES.                      
           05  L532-HDB-REF-SMS PIC X(14).                                      
           05  FILLER          PIC X(11)     VALUE SPACES.                      
           05  L532-FT-SMS     PIC X(02).                                       
           05  FILLER          PIC X(17)     VALUE SPACES.                      
           05  L532-FS-SMS     PIC X(02).                                       
           05  FILLER          PIC X(12)     VALUE SPACES.                      
           05  L532-NT-REQ     PIC X(03).                                       
           05  FILLER          PIC X(08)     VALUE SPACES.                      
           05  L532-FT-REQ     PIC X(02).                                       
           05  FILLER          PIC X(09)     VALUE SPACES.                      
           05  L532-SUCC-NT    PIC X(03).                                       
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
                                                                                
           PERFORM 2000-PROCESS-F558      THRU 2000-EXIT                        
              UNTIL WS-F558-EOF-SW = 'Y'.                                       
                                                                                
           GO TO   9999-CLOSE-ROUTINE.                                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-INITIALIZATION.                                                     
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F558                                                 
                I-O    BP13K740                                                 
                OUTPUT BP13L532.                                                
                                                                                
           ACCEPT WS-SYSTEM-DATE      FROM DATE YYYYMMDD.                       
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-YY           TO WS-EDITED-YY.                          
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K740 OPEN ERROR, STATUS IS ' WS-K740-STATUS          
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       2000-PROCESS-F558.                                                       
      ****************************************************************          
                                                                                
           READ BP13F558                                                        
              AT END      MOVE 'Y'    TO   WS-F558-EOF-SW                       
              GO TO       2000-EXIT                                             
           END-READ.                                                            
                                                                                
           ADD     +1                 TO   WS-F558-READ-CTR.                    
                                                                                
           IF F558-NUM-REGN = WS-PREV-REGN                                      
              MOVE    1                  TO   WS-L532-LINE-SPACING              
              PERFORM 5000-PRINT-L532    THRU 5000-EXIT                         
           ELSE                                                                 
              PERFORM 3000-PROCESS-K740  THRU 3000-EXIT                         
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       3000-PROCESS-K740.                                                       
      *************************************************************             
                                                                                
           MOVE F558-NUM-REGN     TO K740-ALT1-KEY.                             
                                                                                
           READ BP13K740   KEY IS K740-ALT1-KEY.                                
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROS                                                           
              MOVE    F558-NUM-REGN        TO   WS-PREV-REGN                    
              MOVE    2                    TO   WS-L532-LINE-SPACING            
              PERFORM 4000-CHECK-FLAT-TYPE THRU 4000-EXIT                       
              PERFORM 5000-PRINT-L532      THRU 5000-EXIT                       
              ADD     +1   TO WS-K740-FOUND-CTR                                 
           WHEN 23                                                              
              DISPLAY 'BP13K740 RECORD NOT FOUND, KEY IS ' K740-NUM-REGN        
              ADD     +1   TO WS-K740-NOTFND-CTR                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K740 READ ERROR, STATUS IS ' WS-K740-STATUS          
              DISPLAY 'BP13K740 RECORD KEY IS ' K740-NUM-REGN                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       4000-CHECK-FLAT-TYPE.                                                    
      *************************************************************             
                                                                                
           MOVE '07'        TO K740-CDE-EXIST-FLAT-TYPE.                        
                                                                                
           REWRITE BP13K740-REC.                                                
                                                                                
           IF WS-K740-STATUS = ZEROS OR 02                                      
              ADD  +1          TO WS-K740-UPDATE-CTR                            
           ELSE                                                                 
              DISPLAY 'BP13K740 UPDATE ERROR, STATUS IS ' WS-K740-STATUS        
              DISPLAY 'BP13K740 RECORD KEY IS ' K740-NUM-REGN.                  
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       5000-PRINT-L532.                                                         
      *************************************************************             
                                                                                
           IF WS-L532-LINECTR > 52                                              
              PERFORM 5100-PRINT-L532-HEADING THRU 5100-EXIT                    
           END-IF.                                                              
                                                                                
           IF WS-L532-LINE-SPACING = 2                                          
              ADD   +1                          TO  WS-L532-S-NO                
              MOVE  WS-L532-S-NO                TO  L532-S-NO                   
           ELSE                                                                 
              MOVE  ZERO                        TO  L532-S-NO                   
           END-IF.                                                              
                                                                                
           MOVE  F558-NUM-REGN               TO   L532-REF-NO.                  
           MOVE  F558-NUM-NRIC               TO   L532-NRIC.                    
                                                                                
           STRING F558-NUM-HDB-REF-SMS (01:4) '-'                               
                  F558-NUM-HDB-REF-SMS (05:4) '-'                               
                  F558-NUM-HDB-REF-SMS (09:1) '-'                               
                  F558-NUM-HDB-REF-SMS (10:2) DELIMITED BY SIZE                 
                                           INTO   L532-HDB-REF-SMS.             
                                                                                
           MOVE  F558-CDE-FLAT-TYPE-SMS      TO   L532-FT-SMS.                  
           MOVE  F558-CDE-FILE-STATUS-SMS    TO   L532-FS-SMS.                  
           MOVE  K740-CDE-NT1                TO   L532-NT-REQ.                  
           MOVE  K740-CDE-FLAT-TYPE          TO   L532-FT-REQ.                  
           MOVE  K740-CDE-SUCC-NT-ZONE       TO   L532-SUCC-NT.                 
                                                                                
           WRITE BP13L532-REC       FROM L532-DETAIL-01                         
                                   AFTER ADVANCING WS-L532-LINE-SPACING.        
                                                                                
           ADD   WS-L532-LINE-SPACING        TO   WS-L532-LINECTR.              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       5100-PRINT-L532-HEADING.                                                 
      *************************************************************             
                                                                                
           ADD +1     TO WS-L532-PAGECTR.                                       
           MOVE ZERO  TO WS-L532-LINECTR.                                       
                                                                                
           MOVE WS-L532-PAGECTR       TO L532-PAGE-NO.                          
           MOVE WS-EDITED-DATE        TO L532-RUN-DATE.                         
                                                                                
           WRITE BP13L532-REC    FROM L532-HEADER-01                            
                                AFTER ADVANCING PAGE.                           
                                                                                
           WRITE BP13L532-REC    FROM L532-HEADER-02                            
                                AFTER ADVANCING 1 LINE.                         
                                                                                
           WRITE BP13L532-REC    FROM L532-HEADER-03                            
                                AFTER ADVANCING 2 LINES.                        
                                                                                
           WRITE BP13L532-REC    FROM L532-HEADER-04                            
                               BEFORE ADVANCING 1 LINE.                         
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           CLOSE BP13F558                                                       
                 BP13K740                                                       
                 BP13L532.                                                      
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K740 CLOSE ERROR, STATUS IS ' WS-K740-STATUS         
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-EDITED-DATE.                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C532 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F558 RECORDS READ        = ' WS-F558-READ-CTR.          
           DISPLAY 'BP13K740 RECORDS FOUND       = ' WS-K740-FOUND-CTR.         
           DISPLAY 'BP13K740 RECORDS NOT FOUND   = ' WS-K740-NOTFND-CTR.        
           DISPLAY 'BP13K740 RECORDS UPDATED     = ' WS-K740-UPDATE-CTR.        
           DISPLAY 'BP13K740 RECORDS NOT UPDATED = ' WS-K740-BYPASS-CTR.        
           DISPLAY SPACES.                                                      
           DISPLAY '*======================================*'.                  
                                                                                
           STOP RUN.                                                            
