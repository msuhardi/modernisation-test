       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C577.                                                 
       AUTHOR.        RACHEL G MENDOZA / GD.                                    
      *DATE-WRITTEN : 22 OCT 1994.                                              
      *----------------------------------------------------------------*        
      *                                                                *        
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                            *        
      *                                                                *        
      *  SYSTEM ID   : BP13                                            *        
      *                                                                *        
      *  OBJECTIVE   : PROGRAM TO MATCH WITH THE BALLOT FILE TO EXTRACT*        
      *                THE WAITING LIST OCCUPIER INFORMATION FOR HOUSE-*        
      *                HOLD CHECK                                      *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *  REQ NO   OIC   DATE   DESCRIPTION                             *        
      *  ======   ===   ====   ===========                             *        
      * C950199   YCH2 060295  EXTRACT DATE BALLOT IF F500-CDE-NT1 NOT *        
      *                        SPACES AND 21ST AS THE DAY              *        
      * BP130410  LSB  211197  REMOVE F558-DTE-UPDATE                  *        
      * BP130445  LSB  050298  EXPAND F558 TO X(400), OUTPUT NUM-APPLN *        
      *                        ZONE/NT, FT, DTE-REGN                   *        
      * BP130418  LSB  201098  Y2K EXPAND F820 TO X400                 *        
      * BP130652  NOR  190299  EXPAND F500 RECL FROM 300 TO 500        *        
      * BP130897  AKT  010800  EXPAND F558 RECL FROM 400 TO 600        *        
      * BP132626  SK   241104  CHANGE F500-DTE-REGN TO F500-DTE-REQUEST*        
      *----------------------------------------------------------------*        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
           SELECT BP13F820 ASSIGN TO BP13F820.                                  
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F500                RECORD CONTAINS 500 CHARACTERS               
                                   RECORDING MODE IS F.                         
       COPY BP13F500.                                                           
                                                                                
       FD  BP13F820                RECORD CONTAINS 400 CHARACTERS               
                                   RECORDING MODE IS F.                         
       COPY BP13F820.                                                           
                                                                                
       FD  BP13F558                RECORD CONTAINS 600 CHARACTERS               
                                   RECORDING MODE IS F                          
                                   BLOCK CONTAINS  0 RECORDS                    
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F558.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F500-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F820-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F500-NMR             PIC 9(8)    VALUE ZERO.                      
       01  WS-F820-NMR             PIC 9(8)    VALUE ZERO.                      
       01  WS-F558-WRITE           PIC 9(8)    VALUE ZERO.                      
       01  WS-F820-BYPASS          PIC 9(8)    VALUE ZERO.                      
       01  WS-MATCH-CTR            PIC 9(8)    VALUE ZERO.                      
                                                                                
       01  WS-RUN-DATE.                                                         
           05  WS-YY               PIC X(4).                                    
           05  WS-MM               PIC X(2).                                    
           05  WS-DD               PIC X(2).                                    
                                                                                
       01  WS-EOF-F500             PIC X(3).                                    
           88  EOF-OF-F500                     VALUE 'YES'.                     
       01  WS-EOF-F820             PIC X(3).                                    
           88  EOF-OF-F820                     VALUE 'YES'.                     
                                                                                
       01  WS-DTE-REGN.                                                         
           05  WS-REGN-YYMM        PIC X(6).                                    
           05  WS-REGN-DD          PIC X(2).                                    
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       000-MAIN-LOGIC.                                                          
           PERFORM 100-INITIALIZATION                                           
              THRU 100-EXIT.                                                    
           PERFORM 200-READ-F500                                                
              THRU 200-EXIT.                                                    
           PERFORM 300-READ-F820                                                
              THRU 300-EXIT.                                                    
           PERFORM 400-PROCESS                                                  
              THRU 400-EXIT                                                     
             UNTIL F500-NUM-REGN = HIGH-VALUE                                   
              AND  F820-NUM-REGN = HIGH-VALUE.                                  
           PERFORM 900-CLOSE-ROUTINE                                            
              THRU 900-EXIT.                                                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
                                                                                
           OPEN INPUT  BP13F500                                                 
                       BP13F820                                                 
                OUTPUT BP13F558.                                                
                                                                                
           MOVE 'NO' TO WS-EOF-F500.                                            
           MOVE 'NO' TO WS-EOF-F820.                                            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-RUN-DATE.                    
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F500.                                                           
                                                                                
           READ BP13F500 AT END MOVE 'YES' TO WS-EOF-F500                       
                                MOVE HIGH-VALUE TO F500-NUM-REGN                
               GO TO 200-EXIT.                                                  
            ADD 1 TO WS-F500-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-F820.                                                           
                                                                                
           READ BP13F820 AT END MOVE 'YES' TO WS-EOF-F820                       
                                MOVE HIGH-VALUE TO F820-NUM-REGN                
               GO TO 300-EXIT.                                                  
            ADD 1 TO WS-F820-READ.                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *  MATCH  BY REF-NO                                             *         
      *****************************************************************         
                                                                                
       400-PROCESS.                                                             
                                                                                
           IF F500-NUM-REGN = F820-NUM-REGN                                     
              ADD  +1          TO WS-MATCH-CTR                                  
              PERFORM 500-WRITE-F558  THRU 500-EXIT                             
                UNTIL F820-NUM-REGN NOT = F500-NUM-REGN                         
                   OR WS-EOF-F820 = 'YES'                                       
              PERFORM 200-READ-F500 THRU 200-EXIT                               
           ELSE                                                                 
              IF F500-NUM-REGN   < F820-NUM-REGN                                
                 DISPLAY 'F500-NUM-REGN : ' F500-NUM-REGN                       
                 ADD 1  TO WS-F500-NMR                                          
                 PERFORM 200-READ-F500 THRU 200-EXIT                            
              ELSE                                                              
                 ADD 1  TO WS-F820-NMR                                          
                 PERFORM 300-READ-F820 THRU 300-EXIT.                           
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-WRITE-F558.                                                          
                                                                                
           IF F820-NUM-LESSEE-CHECK = 'Y'                                       
              MOVE SPACES                   TO BP13F558-REC                     
              MOVE F820-NUM-REGN            TO F558-NUM-REGN                    
              MOVE F820-NUM-NRIC            TO F558-NUM-NRIC                    
              MOVE F820-NUM-ETHNIC          TO F558-NUM-ETHNIC                  
              MOVE F820-NUM-SEX             TO F558-NUM-SEX                     
              MOVE F820-NUM-MARITAL-STATUS  TO F558-NUM-MARITAL-STATUS          
              MOVE F820-DTE-BIRTH           TO F558-DTE-BIRTH                   
              MOVE F820-NUM-CITIZENSHIP     TO F558-NUM-CITIZENSHIP             
              MOVE F820-NUM-RELATIONSHIP    TO F558-CDE-RELATIONSHIP            
              MOVE F820-NUM-LESSEE-CHECK    TO F558-CDE-LESSEE-CHECK            
                                                                                
              MOVE F500-CDE-FLAT-TYPE TO F558-NUM-FLAT-TY-SOC                   
              IF  F500-CDE-NT1 = SPACES OR LOW-VALUES                           
                  MOVE F500-CDE-ZONE        TO F558-NUM-ZONE-NT-SOC             
              ELSE                                                              
                  MOVE F500-CDE-NT1         TO F558-NUM-ZONE-NT-SOC             
              END-IF                                                            
                                                                                
              IF F500-DTE-REQUEST NOT = SPACES AND LOW-VALUES                   
                                               AND ALL '0'                      
                 MOVE F500-DTE-REQUEST         TO F558-DTE-ISSUE-REGN           
              ELSE                                                              
                 MOVE F500-DTE-BALLOT          TO WS-REGN-YYMM                  
                 MOVE '21'                     TO WS-REGN-DD                    
                 MOVE WS-DTE-REGN              TO F558-DTE-ISSUE-REGN           
              END-IF                                                            
                                                                                
              IF  F820-NUM-NRIC = F500-NUM-NRIC1 OR                             
                                  F500-NUM-NRIC2 OR                             
                                  F500-NUM-NRIC3 OR                             
                                  F500-NUM-NRIC4                                
                  MOVE 'Y'                  TO F558-NUM-APPLN                   
              END-IF                                                            
                                                                                
              IF F820-NUM-NRIC = 'X88888888' OR 'X77777777'                     
                ADD 1                         TO WS-F820-BYPASS                 
              ELSE                                                              
                WRITE BP13F558-REC                                              
                ADD +1                        TO WS-F558-WRITE                  
              END-IF                                                            
           ELSE                                                                 
              ADD 1                         TO WS-F820-BYPASS.                  
                                                                                
           PERFORM 300-READ-F820  THRU 300-EXIT.                                
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
                                                                                
             DISPLAY '*--------BP13C577  -----------*'.                         
             DISPLAY ' '.                                                       
             DISPLAY ' RUN DATE          : ' WS-DD '/' WS-MM '/' WS-YY.         
             DISPLAY ' '.                                                       
             DISPLAY ' BP13F500-READ     : ' WS-F500-READ.                      
             DISPLAY ' BP13F820-READ     : ' WS-F820-READ.                      
             DISPLAY ' MATCHED RECORDS   : ' WS-MATCH-CTR.                      
             DISPLAY ' BP13F558-WRITTEN  : ' WS-F558-WRITE.                     
             DISPLAY ' BP13F820-BYPASS   : ' WS-F820-BYPASS.                    
             DISPLAY ' BP13F820-NOMATCH  : ' WS-F820-NMR.                       
             DISPLAY ' BP13F500-NOMATCH  : ' WS-F500-NMR.                       
                                                                                
      *----> WS-MATCH-CTR - WS-F558-WRITE = NUMBER OF MATCHED RECORDS           
      *                                     WITH LESSEE-CHECK NOT = 'Y'         
                                                                                
           CLOSE      BP13F500                                                  
                      BP13F820                                                  
                      BP13F558.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
