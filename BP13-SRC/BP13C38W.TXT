      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C38W.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  SEPTEMBER 28, 2016.                                       
      *===============================================================*         
      * OBJECTIVE  :  PRINT NOTIFICATION/WAIVE CASES OF CASH PROCEED  *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F800                                     *         
      *               2. BP27K025                                     *         
      *               3. BP27K800                                     *         
      *                                                               *         
      * OUTPUT FILE:  1. BP13L38W                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP136461 28/08/2016 PCL4 NEW PROGRAM.                         *         
      * BP136548 19/12/2016 PCL4 TO CATER FOR BC/BL                   *         
      * BP138047 25/11/2019 KAC1 RECOMPILE FOR BP27 COPYBOOK UPDATE   *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F800  ASSIGN        TO BP13F800.                         
                                                                                
           SELECT  BP27K800  ASSIGN        TO BP27K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                     RECORD KEY IS K800-NUM-HLA OF BP27K800-MASTER              
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
           SELECT  BM06K110  ASSIGN        TO BM06K110                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                     RECORD KEY IS K110-KEY-FLD OF K110-REC                     
                             FILE STATUS   IS WS-K110-STATUS.                   
                                                                                
           SELECT  BP27K025  ASSIGN        TO BP27K025                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K025-KEY-FLD                      
                             ALTERNATE RECORD KEY IS K025-NUM-HLA               
                             FILE STATUS   IS WS-K025-STATUS.                   
                                                                                
                                                                                
           SELECT  BP13L38W  ASSIGN        TO BP13L38W.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP27K800                                                            
            RECORD CONTAINS 1200 CHARACTERS.                                    
       COPY BP27K800.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP27K025                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP27K025.                                                           
                                                                                
                                                                                
       FD   BP13L38W                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 50 CHARACTERS                                       
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   BP13L38W-REC.                                                       
            05 L38W-OIC    PIC X(5).                                            
            05 L38W-REGN   PIC X(8).                                            
            05 FILLER      PIC X(33).                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F800             PIC X(1)  VALUE 'N'.                     
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K025-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K110-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(1)  VALUE ZEROES.                  
           05  WS-HA                   PIC 9(1)  VALUE ZEROES.                  
           05  WS-F800-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-L38W-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(1)  VALUE 2.                       
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-PRIOR-DTE             PIC 9(08) VALUE ZEROES.                  
           05 WS-NUM-DTE-APPT          PIC 9(07) VALUE ZEROES.                  
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-NUM-RELN              PIC X(20) VALUE SPACES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
           05 WS-FIRST-WRITE           PIC X(1)  VALUE 'Y'.                     
           05 WS-K800-FOUND            PIC X(1)  VALUE 'N'.                     
                                                                                
       01  WS-DTE-REGN.                                                         
           05 WS-DTE-CCYY                   PIC 9(4).                           
           05 WS-DTE-MM                     PIC 9(2).                           
           05 WS-DTE-DD                     PIC 9(2).                           
                                                                                
       01 WS-PRIO-DTE.                                                          
          05  WS-PRIO-CCYY                 PIC 9(04).                           
          05  WS-PRIO-MM                   PIC 9(02).                           
          05  WS-PRIO-DD                   PIC 9(02).                           
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F800      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F800 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP27K800                                                 
                       BM06K110                                                 
                       BP27K025                                                 
              OUTPUT   BP13L38W.                                                
                                                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP27K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR OPENING : ' WS-K110-STATUS              
              MOVE WS-K110-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K025-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP27K025 - ERROR OPENING : ' WS-K025-STATUS              
              MOVE WS-K025-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 1100-CHECK-PRIOR-DATE   THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1100-CHECK-PRIOR-DATE.                                                   
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)      TO WS-SYSTEM-DATE               
                                                   WS-CURR-DATE                 
                                                   WS-PRIOR-DTE.                
           COMPUTE WS-NUM-DTE-APPT =                                            
                   FUNCTION INTEGER-OF-DATE(WS-PRIOR-DTE).                      
                                                                                
           SUBTRACT 1 FROM  WS-NUM-DTE-APPT.                                    
                                                                                
           COMPUTE  WS-PRIOR-DTE =                                              
                    FUNCTION DATE-OF-INTEGER(WS-NUM-DTE-APPT).                  
                                                                                
           MOVE WS-PRIOR-DTE        TO WS-PRIO-DTE.                             
                                                                                
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F800.                                                      
      ******************************************************************        
                                                                                
           READ BP13F800           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F800                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F800-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       2100-READ-BP27K800.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP27K800-MASTER.                    
           INITIALIZE                       BP27K800-MASTER.                    
                                                                                
           MOVE SPACES            TO WS-K800-FOUND.                             
           MOVE F800-NUM-HLA      TO K800-NUM-HLA OF BP27K800-MASTER.           
                                                                                
           READ BP27K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'    TO WS-K800-FOUND                                
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'N'    TO WS-K800-FOUND                                
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP27K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' F800-NUM-HLA                           
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       2200-READ-BM06K110.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO K110-REC.                           
           INITIALIZE                       K110-REC.                           
                                                                                
           MOVE F800-NUM-SCH-ACC         TO K110-KEY-FLD OF K110-REC.           
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES  TO K110-REC                                    
                    INITIALIZE      K110-REC                                    
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BM06K110 READ ERROR ' WS-K110-STATUS               
                    DISPLAY 'K110 KEY: ' F800-NUM-SCH-ACC                       
                    MOVE WS-K110-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF (F800-DTE-UPDATE-SUBTAG = WS-SYSTEM-DATE OR WS-PRIO-DTE)          
              PERFORM 2100-READ-BP27K800        THRU 2100-EXIT                  
                                                                                
              IF WS-K800-FOUND = 'Y'                                            
                 IF F800-TAG-SUB-APPRV = 'BC' OR 'BL'                           
                    PERFORM 2200-READ-BM06K110  THRU 2200-EXIT                  
                                                                                
                    IF (F800-NUM-BUY-SELL-TAG  = 'S' )  AND                     
                       (K110-DTE-KEY-AVAIL NOT = SPACES AND                     
                                 LOW-VALUES AND ZEROES) AND                     
                       (F800-DTE-2ND-CA-SENT = SPACES OR                        
                                     ZEROES OR LOW-VALUES)                      
                       PERFORM 4000-PROCESS-RECORDS   THRU 4000-EXIT            
                    END-IF                                                      
                 ELSE                                                           
                    PERFORM 4000-PROCESS-RECORDS      THRU 4000-EXIT            
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F800           THRU 2000-EXIT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-GET-CRM-OIC.                                                        
      ******************************************************************        
                                                                                
            MOVE SPACES                           TO BP27K025-REC.              
            INITIALIZE                               BP27K025-REC.              
                                                                                
            MOVE F800-NUM-HLA                     TO K025-NUM-HLA.              
                                                                                
            READ BP27K025 KEY IS K025-NUM-HLA.                                  
                                                                                
            EVALUATE WS-K025-STATUS                                             
                WHEN 00                                                         
                WHEN 02                                                         
                     CONTINUE                                                   
                                                                                
                WHEN 23                                                         
                     MOVE SPACES                  TO BP27K025-REC               
                     INITIALIZE                      BP27K025-REC               
                                                                                
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP27K025 : ' WS-K025-STATUS         
                             ' HLE NO = ' K025-NUM-HLA                          
                     PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT            
            END-EVALUATE.                                                       
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE SPACES                      TO BP13L38W-REC.                    
           INITIALIZE                          BP13L38W-REC.                    
                                                                                
           PERFORM 3100-GET-CRM-OIC       THRU 3100-EXIT.                       
                                                                                
           IF K025-NUM-OIC NOT = SPACES AND LOW-VALUES                          
              MOVE K025-NUM-OIC             TO L38W-OIC                         
              MOVE F800-NUM-REGN            TO L38W-REGN                        
                                                                                
              WRITE BP13L38W-REC                                                
              ADD 1                         TO WS-CNT-L38W-WRITE                
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C38W'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F800 RECORDS READ............. : '            
                    WS-F800-READ.                                               
           DISPLAY '(2) NO OF BP13L38W RECORDS WRITTEN.......... : '            
                    WS-CNT-L38W-WRITE.                                          
                                                                                
           CLOSE    BP13F800                                                    
                    BM06K110                                                    
                    BP27K800                                                    
                    BP27K025                                                    
                    BP13L38W.                                                   
                                                                                
           IF WS-K025-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP27K025 - ERROR CLOSING : ' WS-K025-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR CLOSING : ' WS-K110-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP27K800 - ERROR CLOSING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
