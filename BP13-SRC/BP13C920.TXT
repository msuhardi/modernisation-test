       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C920.                                                 
       AUTHOR.        YCH2.                                                     
      *DATE-WRITTEN.  10/11/2004                                                
      ***************************************************************           
      *                      SOC SYSTEM                             *           
      *=============================================================*           
      *                                                             *           
      *  OBJECTIVE  :  READ BP13F824 TO GET DATE OF ANNOUNCEMENT    *           
      *  I/O FILE                                                   *           
      *  --------                                                   *           
      *         I   :  BP13F824                                     *           
      *                BP17K100                                     *           
      *                BP17K110                                     *           
      *                BP17K120                                     *           
      *                BP13K800                                     *           
      *                BP13K820                                     *           
      *                BP13K825                                     *           
      *                BP13K830                                     *           
      *                BP13K730                                     *           
      *                BP13K740                                     *           
      *         O   :  P13F824A                                     *           
      *                                                             *           
      *-------------------------------------------------------------*           
      *  MODIFICATIONS                                              *           
      *  -------------                                              *           
      *  CHGREQ   OIC  DATE     DESCRIPTION                         *           
      *  -------- ---- -------- ----------------------------------- *           
      *  BP132684 YCH2 10112004 NEW PROGRAM TO EXTRACT DATE OF      *           
      *                         ANNOUNCEMENT                        *           
      *  BP135254 SMR2 30042014 CHANGE INPUT FILE FROM F824 TO F825 *           
      *                         / READ K010 BY NRIC / REMOVE READING*           
      *                         OF K740, K800, K820 / ADD NEW FIELDS*           
      *                         & INCREASE OUTPUT FILE LENGTH FROM  *           
      *                         250 TO 500                          *           
      *  BP135414 SMR2 31072014 CATER FOR DEBARMENT CODE '4J'       *           
      *  BP135485 SMR2 02102014 WRITE ALL RECORDS WITH REASON-CODE  *           
      *                         EA/EB/ET/R1/R2/4J                   *           
      *  BP136505 SMR2 03112016 CATER FOR DEBARMENT CODE 'SN'       *           
      *  BP136609 SMR2 02032017 CATER FOR DEBARMENT CODE FP/FK/FL & *           
      *                         NEW SERS OUTPUT FILE (EA/EB/ET/4J)  *           
      *  BP136902 SMR2 08062017 REMOVE DEB CODE 4J IN SERS &        *           
      *                         ADD DEB CODE R4 IN P13F824A         *           
      ***************************************************************           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F825 ASSIGN        TO BP13F825.                           
                                                                                
           SELECT P04K010  ASSIGN        TO P04K010                             
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K010-PRIME-KEY                      
                           FILE STATUS   IS WS-K010-STATUS.                     
                                                                                
           SELECT BP13K825 ASSIGN        TO BP13K825                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K825-KEY-FLD                        
                           FILE STATUS   IS WS-K825-STATUS.                     
                                                                                
           SELECT BP13K830 ASSIGN        TO BP13K830                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K830-KEY-FLD                        
                           ALTERNATE RECORD KEY IS K830-NUM-REGN                
                           FILE STATUS   IS WS-K830-STATUS.                     
                                                                                
           SELECT BP13K730 ASSIGN        TO BP13K730                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K730-KEY-NEW                        
                           FILE STATUS   IS WS-K730-STATUS.                     
                                                                                
           SELECT BP17K120 ASSIGN        TO BP17K120                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K120-KEY-FLD                        
                           FILE STATUS   IS WS-K120-STATUS.                     
                                                                                
           SELECT BP17K110 ASSIGN        TO BP17K110                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K110-KEY-FLD                        
                           ALTERNATE KEY IS K110-NUM-BLDNG-GL                   
                           WITH DUPLICATES                                      
                           FILE STATUS   IS WS-K110-STATUS.                     
                                                                                
           SELECT BP17K100 ASSIGN        TO BP17K100                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K100-KEY-FLD                        
                           FILE STATUS   IS WS-K100-STATUS.                     
                                                                                
           SELECT P13F824A ASSIGN        TO P13F824A.                           
                                                                                
           SELECT P13F824B ASSIGN        TO P13F824B.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F825                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  200 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORDS    ARE STANDARD.                                      
       COPY BP13F825.                                                           
                                                                                
       FD   P04K010                                                             
            RECORD CONTAINS  300 CHARACTERS.                                    
       COPY P04K010.                                                            
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13K825.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS  250 CHARACTERS.                                    
       COPY BP13K830.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13K730.                                                           
                                                                                
       FD   BP17K120                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP17K120.                                                           
                                                                                
       FD   BP17K110                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP17K110.                                                           
                                                                                
       FD   BP17K100                                                            
            RECORD CONTAINS  400 CHARACTERS.                                    
       COPY BP17K100.                                                           
                                                                                
       FD   P13F824A                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORDS    ARE STANDARD.                                      
       01   P13F824A-REC.                                                       
            05  F824A-NUM-REF              PIC X(08).                           
            05  FILLER1                    PIC X(01).                           
            05  F824A-NUM-STATUS           PIC X(01).                           
            05  FILLER2                    PIC X(01).                           
            05  F824A-NUM-NRIC             PIC X(09).                           
            05  FILLER3                    PIC X(01).                           
            05  F824A-NME-OCCP             PIC X(66).                           
            05  FILLER4                    PIC X(01).                           
            05  F824A-DTE-SERS             PIC X(08).                           
            05  FILLER5                    PIC X(01).                           
            05  F824A-CDE-DEBAR            PIC X(02).                           
            05  FILLER6                    PIC X(01).                           
            05  F824A-SER-SCH-ACC          PIC X(09).                           
            05  FILLER7                    PIC X(01).                           
            05  F824A-SER-ADDR             PIC X(99).                           
            05  FILLER8                    PIC X(01).                           
            05  F824A-NUM-REGN             PIC X(08).                           
            05  FILLER9                    PIC X(01).                           
            05  F824A-NUM-HHTY             PIC X(01).                           
            05  FILLER10                   PIC X(01).                           
            05  F824A-NUM-ALLO-SCH         PIC X(03).                           
            05  FILLER11                   PIC X(01).                           
            05  F824A-NUM-PORT-TAG         PIC X(01).                           
            05  FILLER12                   PIC X(01).                           
            05  F824A-NUM-FT               PIC X(02).                           
            05  FILLER13                   PIC X(01).                           
            05  F824A-NUM-NT               PIC X(03).                           
            05  FILLER14                   PIC X(01).                           
            05  F824A-DTE-APPT             PIC X(08).                           
            05  FILLER15                   PIC X(01).                           
            05  F824A-TME-APPT             PIC X(04).                           
            05  FILLER16                   PIC X(01).                           
            05  F824A-NUM-BLOCK            PIC X(05).                           
            05  FILLER17                   PIC X(01).                           
            05  F824A-NUM-HOUSE-NO         PIC X(11).                           
            05  FILLER18                   PIC X(01).                           
            05  F824A-NUM-STREETNAME       PIC X(65).                           
            05  FILLER19                   PIC X(01).                           
            05  F824A-NUM-POSTCODE         PIC X(06).                           
            05  FILLER20                   PIC X(01).                           
            05  F824A-NUM-APPS             PIC X(01).                           
            05  FILLER21                   PIC X(01).                           
            05  F824A-NUM-MGPS             PIC X(01).                           
            05  FILLER22                   PIC X(01).                           
            05  FILLER                     PIC X(157).                          
                                                                                
       FD   P13F824B                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORDS    ARE STANDARD.                                      
       01   P13F824B-REC.                                                       
            05  F824B-NUM-REF              PIC X(08).                           
            05  F824B-FIL1                 PIC X(01).                           
            05  F824B-NUM-STATUS           PIC X(01).                           
            05  F824B-FIL2                 PIC X(01).                           
            05  F824B-NUM-NRIC             PIC X(09).                           
            05  F824B-FIL3                 PIC X(01).                           
            05  F824B-NME-OCCP             PIC X(66).                           
            05  F824B-FIL4                 PIC X(01).                           
            05  F824B-DTE-SERS             PIC X(08).                           
            05  F824B-FIL5                 PIC X(01).                           
            05  F824B-CDE-DEBAR            PIC X(02).                           
            05  F824B-FIL6                 PIC X(01).                           
            05  F824B-SER-SCH-ACC          PIC X(09).                           
            05  F824B-FIL7                 PIC X(01).                           
            05  F824B-SER-ADDR             PIC X(99).                           
            05  F824B-FIL8                 PIC X(01).                           
            05  F824B-NUM-REGN             PIC X(08).                           
            05  F824B-FIL9                 PIC X(01).                           
            05  F824B-NUM-HHTY             PIC X(01).                           
            05  F824B-FIL10                PIC X(01).                           
            05  F824B-NUM-ALLO-SCH         PIC X(03).                           
            05  F824B-FIL11                PIC X(01).                           
            05  F824B-NUM-PORT-TAG         PIC X(01).                           
            05  F824B-FIL12                PIC X(01).                           
            05  F824B-NUM-FT               PIC X(02).                           
            05  F824B-FIL13                PIC X(01).                           
            05  F824B-NUM-NT               PIC X(03).                           
            05  F824B-FIL14                PIC X(01).                           
            05  F824B-DTE-APPT             PIC X(08).                           
            05  F824B-FIL15                PIC X(01).                           
            05  F824B-TME-APPT             PIC X(04).                           
            05  F824B-FIL16                PIC X(01).                           
            05  F824B-NUM-BLOCK            PIC X(05).                           
            05  F824B-FIL17                PIC X(01).                           
            05  F824B-NUM-HOUSE-NO         PIC X(11).                           
            05  F824B-FIL18                PIC X(01).                           
            05  F824B-NUM-STREETNAME       PIC X(65).                           
            05  F824B-FIL19                PIC X(01).                           
            05  F824B-NUM-POSTCODE         PIC X(06).                           
            05  F824B-FIL20                PIC X(01).                           
            05  F824B-NUM-APPS             PIC X(01).                           
            05  F824B-FIL21                PIC X(01).                           
            05  F824B-NUM-MGPS             PIC X(01).                           
            05  F824B-FIL22                PIC X(01).                           
            05  FILLER                     PIC X(157).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K010-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K825-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K830-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K730-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K120-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K110-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K100-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F825-READ                PIC 9(08)   VALUE ZEROES.            
           05  WS-K010-READ                PIC 9(08)   VALUE ZEROES.            
           05  WS-K825-READ                PIC 9(08)   VALUE ZEROES.            
           05  WS-K830-READ                PIC 9(08)   VALUE ZEROES.            
           05  WS-K730-READ                PIC 9(08)   VALUE ZEROES.            
           05  WS-K120-READ                PIC 9(08)   VALUE ZEROES.            
           05  WS-K110-READ                PIC 9(08)   VALUE ZEROES.            
           05  WS-K100-READ                PIC 9(08)   VALUE ZEROES.            
           05  WS-F824A-WRITE              PIC 9(08)   VALUE ZEROES.            
           05  WS-F824B-WRITE              PIC 9(08)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F825-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K010-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K110-FLAG                PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-RET-CODE                 PIC 9(02)   VALUE ZEROES.            
           05  WS-TEMP-REC                 PIC X(500)  VALUE SPACES.            
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *===========================================================*             
       0000-MAIN-ROUTINE.                                                       
      *===========================================================*             
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F825       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECS        THRU 3000-EXIT                      
             UNTIL WS-F825-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===========================================================*             
       1000-OPEN-FILES.                                                         
      *===========================================================*             
                                                                                
           OPEN INPUT  BP13F825                                                 
                       P04K010                                                  
                       BP13K825                                                 
                       BP13K830                                                 
                       BP13K730                                                 
                       BP17K120                                                 
                       BP17K110                                                 
                       BP17K100                                                 
                OUTPUT P13F824A                                                 
                       P13F824B.                                                
                                                                                
           IF WS-K010-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR IN OPEN P04K010 : ' WS-K010-STATUS                 
              MOVE WS-K010-STATUS           TO WS-RET-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR IN OPEN BP13K825 : ' WS-K825-STATUS                
              MOVE WS-K825-STATUS           TO WS-RET-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR IN OPEN BP13K830 : ' WS-K830-STATUS                
              MOVE WS-K830-STATUS           TO WS-RET-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR IN OPEN BP13K730 : ' WS-K730-STATUS                
              MOVE WS-K730-STATUS           TO WS-RET-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K120-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR IN OPEN BP17K120 : ' WS-K120-STATUS                
              MOVE WS-K120-STATUS           TO WS-RET-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR IN OPEN BP17K110 : ' WS-K110-STATUS                
              MOVE WS-K110-STATUS           TO WS-RET-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR IN OPEN BP17K100 : ' WS-K100-STATUS                
              MOVE WS-K100-STATUS           TO WS-RET-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===========================================================*             
       2000-READ-BP13F825.                                                      
      *===========================================================*             
                                                                                
           READ BP13F825 AT END                                                 
                MOVE 'Y'                    TO WS-F825-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F825-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===========================================================*             
       3000-PROCESS-RECS.                                                       
      *===========================================================*             
                                                                                
           PERFORM 4000-STARTBR-K010        THRU 4000-EXIT.                     
           PERFORM 2000-READ-BP13F825       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===========================================================*             
       4000-STARTBR-K010.                                                       
      *===========================================================*             
                                                                                
           MOVE SPACES                      TO P04K010-REC.                     
           INITIALIZE                          P04K010-REC.                     
           MOVE 'N'                         TO WS-K010-EOF.                     
           MOVE F825-NUM-NRIC               TO K010-NUM-UINFIN.                 
                                                                                
           START P04K010 KEY >= K010-NUM-UINFIN.                                
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN ZEROES                                                          
              PERFORM 4100-READNEXT-K010    THRU 4100-EXIT                      
                UNTIL K010-NUM-UINFIN NOT = F825-NUM-NRIC                       
                   OR WS-K010-EOF = 'Y'                                         
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN STARTBR P04K010 : ' WS-K010-STATUS              
              MOVE WS-K010-STATUS           TO WS-RET-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===========================================================*             
       4100-READNEXT-K010.                                                      
      *===========================================================*             
                                                                                
           READ P04K010 NEXT RECORD                                             
             AT END MOVE 'Y'                TO WS-K010-EOF.                     
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN ZEROES                                                          
              IF K010-NUM-UINFIN = F825-NUM-NRIC                                
                 IF K010-REASON-CODE = 'EA' OR 'EB' OR 'ET' OR                  
                                       'R1' OR 'R2' OR '4J' OR                  
                                       'SN' OR 'FP' OR 'FK' OR                  
                                       'FL' OR 'R4'                             
                    PERFORM 4200-WRITE-REC  THRU 4200-EXIT                      
                    IF K010-REASON-CODE = 'EA' OR 'EB' OR 'ET'                  
                       MOVE SPACES          TO P13F824B-REC                     
                       WRITE P13F824B-REC   FROM WS-TEMP-REC                    
                       ADD 1                TO WS-F824B-WRITE                   
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'Y'                   TO WS-K010-EOF                      
              END-IF                                                            
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              MOVE 'Y'                      TO WS-K010-EOF                      
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN READNEXT P04K010 : ' WS-K010-STATUS             
              MOVE WS-K010-STATUS           TO WS-RET-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===========================================================*             
       4200-WRITE-REC.                                                          
      *===========================================================*             
                                                                                
           MOVE SPACES                      TO P13F824A-REC.                    
           MOVE ';'                         TO FILLER1  FILLER2                 
                                               FILLER3  FILLER4                 
                                               FILLER5  FILLER6                 
                                               FILLER7  FILLER8                 
                                               FILLER9  FILLER10                
                                               FILLER11 FILLER12                
                                               FILLER13 FILLER14                
                                               FILLER15 FILLER16                
                                               FILLER17 FILLER18                
                                               FILLER19 FILLER20                
                                               FILLER21 FILLER22.               
                                                                                
           MOVE K010-EX-SCH-ACCTNO          TO F824A-SER-SCH-ACC.               
           MOVE K010-REASON-CODE            TO F824A-CDE-DEBAR.                 
           MOVE K010-BLOCK                  TO F824A-NUM-BLOCK.                 
           MOVE K010-HOUSE-NO               TO F824A-NUM-HOUSE-NO.              
           MOVE K010-STREETNAME             TO F824A-NUM-STREETNAME.            
           MOVE K010-POSTCODE               TO F824A-NUM-POSTCODE.              
                                                                                
           ADD 1                            TO WS-K010-READ.                    
                                                                                
           PERFORM 5000-READ-BP13K825       THRU 5000-EXIT.                     
           PERFORM 6000-READ-BP13K830       THRU 6000-EXIT.                     
           PERFORM 7000-READ-BP13K730       THRU 7000-EXIT.                     
           PERFORM 8000-READ-BP17K120       THRU 8000-EXIT.                     
                                                                                
           MOVE P13F824A-REC                TO WS-TEMP-REC.                     
                                                                                
           WRITE P13F824A-REC.                                                  
           ADD 1                            TO WS-F824A-WRITE.                  
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===========================================================*             
       5000-READ-BP13K825.                                                      
      *===========================================================*             
                                                                                
           MOVE SPACES                      TO BP13K825-REC.                    
           INITIALIZE                          BP13K825-REC.                    
           MOVE F825-KEY-FLD                TO K825-KEY-FLD.                    
                                                                                
           READ BP13K825.                                                       
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              MOVE K825-NUM-NRIC            TO F824A-NUM-NRIC                   
              MOVE K825-NME-OCCP            TO F824A-NME-OCCP                   
              ADD 1                         TO WS-K825-READ                     
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN READ BP13K825 : ' WS-K825-STATUS                
              MOVE WS-K825-STATUS           TO WS-RET-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===========================================================*             
       6000-READ-BP13K830.                                                      
      *===========================================================*             
                                                                                
           MOVE SPACES                      TO BP13K830-REC.                    
           INITIALIZE                          BP13K830-REC.                    
           MOVE F825-NUM-REGN               TO K830-KEY-FLD.                    
                                                                                
           READ BP13K830.                                                       
                                                                                
           EVALUATE WS-K830-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              IF K830-NUM-POSTAL NOT = SPACES AND LOW-VALUES                    
                 MOVE K830-NUM-RESIDENT-ADDR   TO F824A-SER-ADDR                
              ELSE                                                              
                 MOVE K830-NUM-MAIL-ADDR    TO F824A-SER-ADDR                   
              END-IF                                                            
              ADD 1                         TO WS-K830-READ                     
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN READ BP13K830 : ' WS-K830-STATUS                
              MOVE WS-K830-STATUS           TO WS-RET-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===========================================================*             
       7000-READ-BP13K730.                                                      
      *===========================================================*             
                                                                                
           MOVE SPACES                      TO BP13K730-REC.                    
           INITIALIZE                          BP13K730-REC.                    
           MOVE F825-NUM-REGN               TO K730-NUM-REGN.                   
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE WS-K730-STATUS                                              
           WHEN ZEROES                                                          
              MOVE K730-NUM-REF             TO F824A-NUM-REF                    
              MOVE K730-NUM-REGN            TO F824A-NUM-REGN                   
              MOVE K730-CDE-REQUEST-STATUS  TO F824A-NUM-STATUS                 
              MOVE K730-CDE-HOUSEHOLD       TO F824A-NUM-HHTY                   
              MOVE K730-CDE-FLAT-TYPE       TO F824A-NUM-FT                     
              MOVE K730-CDE-NT1             TO F824A-NUM-NT                     
              IF K730-NUM-MCPS = SPACES                                         
                 MOVE K730-CDE-ALLOC-SCH    TO F824A-NUM-ALLO-SCH               
              ELSE                                                              
                 IF K730-CDE-RESIDENT-HA1 = 'H' OR 'P' OR 'M'                   
                    MOVE K730-CDE-ALLOC-SCH TO F824A-NUM-ALLO-SCH               
                 ELSE                                                           
                    MOVE 'MCP'              TO F824A-NUM-ALLO-SCH               
                 END-IF                                                         
              END-IF                                                            
              MOVE K730-NUM-PORT-ELIG-TAG   TO F824A-NUM-PORT-TAG               
              MOVE K730-NUM-APPS            TO F824A-NUM-APPS                   
              MOVE K730-NUM-MGPS            TO F824A-NUM-MGPS                   
              MOVE K730-DTE-BK-APPT         TO F824A-DTE-APPT                   
              MOVE K730-TME-BK-APPT         TO F824A-TME-APPT                   
              ADD 1                         TO WS-K730-READ                     
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN READ BP13K730 : ' WS-K730-STATUS                
              MOVE WS-K730-STATUS           TO WS-RET-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===========================================================*             
       8000-READ-BP17K120.                                                      
      *===========================================================*             
                                                                                
           MOVE SPACES                      TO BP17K120-REC.                    
           INITIALIZE                          BP17K120-REC.                    
           MOVE F824A-SER-SCH-ACC           TO K120-NUM-SCH-ACC.                
                                                                                
           READ BP17K120.                                                       
                                                                                
           EVALUATE WS-K120-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-K120-READ                     
              PERFORM 8100-STARTBR-K110     THRU 8100-EXIT                      
                                                                                
           WHEN 23                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN READ BP17K120 : ' WS-K120-STATUS                
              MOVE WS-K120-STATUS           TO WS-RET-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===========================================================*             
       8100-STARTBR-K110.                                                       
      *===========================================================*             
                                                                                
           MOVE SPACES                      TO BP17K110-REC.                    
           INITIALIZE                          BP17K110-REC.                    
           MOVE 'N'                         TO WS-K110-FLAG.                    
           MOVE K120-NUM-BLDNG-GL           TO K110-NUM-BLDNG-GL.               
                                                                                
           START BP17K110 KEY IS >= K110-NUM-BLDNG-GL.                          
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              PERFORM 8200-READNEXT-K110    THRU 8200-EXIT                      
                UNTIL K110-NUM-BLDNG-GL NOT = K120-NUM-BLDNG-GL                 
                   OR WS-K110-FLAG = 'Y'                                        
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN STARTBR BP17K110 : ' WS-K110-STATUS             
              MOVE WS-K110-STATUS           TO WS-RET-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===========================================================*             
       8200-READNEXT-K110.                                                      
      *===========================================================*             
                                                                                
           READ BP17K110 NEXT RECORD                                            
             AT END MOVE 'Y'                TO WS-K110-FLAG.                    
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              IF K110-NUM-BLDNG-GL = K120-NUM-BLDNG-GL                          
                 MOVE 'Y'                   TO WS-K110-FLAG                     
                 ADD 1                      TO WS-K110-READ                     
                 PERFORM 8300-READ-BP17K100 THRU 8300-EXIT                      
              ELSE                                                              
                 MOVE 'Y'                   TO WS-K110-FLAG                     
              END-IF                                                            
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              MOVE 'Y'                      TO WS-K110-FLAG                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN READ BP17K110 : ' WS-K110-STATUS                
              MOVE WS-K110-STATUS           TO WS-RET-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       8200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===========================================================*             
       8300-READ-BP17K100.                                                      
      *===========================================================*             
                                                                                
           MOVE SPACES                      TO BP17K100-REC.                    
           INITIALIZE                          BP17K100-REC.                    
           MOVE K110-NUM-SERSREF            TO K100-NUM-SERSREF.                
                                                                                
           READ BP17K100.                                                       
                                                                                
           EVALUATE WS-K100-STATUS                                              
           WHEN ZEROES                                                          
              MOVE K100-DTE-ANNOUNCE        TO F824A-DTE-SERS                   
              ADD 1                         TO WS-K100-READ                     
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN READ BP17K100 : ' WS-K100-STATUS                
              MOVE WS-K100-STATUS           TO WS-RET-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       8300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===========================================================*             
       9000-CLOSE-FILES.                                                        
      *===========================================================*             
                                                                                
           DISPLAY '     ***** BP13C920 TOTAL *****     '.                      
           DISPLAY 'NO OF BP13F825 READ       : ' WS-F825-READ.                 
           DISPLAY 'NO OF P04K010 W/ VALID RC : ' WS-K010-READ.                 
           DISPLAY 'NO OF BP13K825 FOUND      : ' WS-K825-READ.                 
           DISPLAY 'NO OF BP13K830 FOUND      : ' WS-K830-READ.                 
           DISPLAY 'NO OF BP13K730 FOUND      : ' WS-K730-READ.                 
           DISPLAY 'NO OF BP17K120 FOUND      : ' WS-K120-READ.                 
           DISPLAY 'NO OF BP17K110 FOUND      : ' WS-K110-READ.                 
           DISPLAY 'NO OF BP17K100 FOUND      : ' WS-K100-READ.                 
           DISPLAY 'NO OF P13F824A WRITTEN    : ' WS-F824A-WRITE.               
           DISPLAY 'NO OF P13F824B WRITTEN    : ' WS-F824B-WRITE.               
                                                                                
           CLOSE BP13F825                                                       
                 P04K010                                                        
                 BP13K825                                                       
                 BP13K830                                                       
                 BP13K730                                                       
                 BP17K120                                                       
                 BP17K110                                                       
                 BP17K100                                                       
                 P13F824A                                                       
                 P13F824B.                                                      
                                                                                
           IF WS-K010-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR IN CLOSE P04K010 : ' WS-K010-STATUS                
              MOVE WS-K010-STATUS           TO WS-RET-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR IN CLOSE BP13K825 : ' WS-K825-STATUS               
              MOVE WS-K825-STATUS           TO WS-RET-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR IN CLOSE BP13K830 : ' WS-K830-STATUS               
              MOVE WS-K830-STATUS           TO WS-RET-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR IN CLOSE BP13K730 : ' WS-K730-STATUS               
              MOVE WS-K730-STATUS           TO WS-RET-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K120-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR IN CLOSE BP17K120 : ' WS-K120-STATUS               
              MOVE WS-K120-STATUS           TO WS-RET-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR IN CLOSE BP17K110 : ' WS-K110-STATUS               
              MOVE WS-K110-STATUS           TO WS-RET-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR IN CLOSE BP17K100 : ' WS-K100-STATUS               
              MOVE WS-K100-STATUS           TO WS-RET-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-RET-CODE NOT = ZEROES                                          
              MOVE WS-RET-CODE              TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
