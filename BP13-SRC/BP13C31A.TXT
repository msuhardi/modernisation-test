      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID. BP13C31A.                                                    
       AUTHOR. AJITH KUMAR T.                                                   
       DATE-WRITTEN. 26TH JULY 1999.                                            
                                                                                
      * =========================================================== *           
      *              SYSTEM OF COMMITMENT (BP13)                    *           
      * =========================================================== *           
      *  OBJECTIVE :                                                *           
      *    1.   TO READ AG07F640 FOR VALUE DATE, IBG REF AND CREATE *           
      *         BB14F140 FOR PASSING TO IRSA.                       *           
      *    2.   UPDATE BP13K625 ON PAYMENT INFO.                    *           
      *                                                             *           
      *  FILES :                                                    *           
      *  INPUT  -    AG07F640                                       *           
      *  I-O    -    BP13K625  (BP13.K625.STAMPFEE)                 *           
      *    O    -    BB14F140  (BP13.BB14.F140.SENTIRAS)            *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG-NO   DATE     BY   DESCRIPTION                          *           
      * -------- -------- ---  -----------                          *           
      * BP130696 26071999 AKT  CREATE STAMPING FILE FOR IRAS        *           
      * BP130806 20032000 CLT  CONVERT LEASE TERMS TO MONTHS.       *           
      * BP130883 07082000 RAS  TO CHANGE BP13C909 TO BP13C913       *           
      * BP131058 27072001 MCC  TO UPD DATE OF PRINCIPLE AGREEMENT   *           
      * BP132185 30072002 MALC UPDTE F140-DTE-DOC-PREP BASE ON K625 *           
      * BP132729 28102005 JF4  TO SKIP AG07 REJECTED CASES          *           
      * BP133881 28042010 JB8  TO AMEND DAILY E-STAMP FILE SEND VIA *           
      *                        LECS TO IRAS FOR STAMPING OF LEGAL DOCS *        
      * BP133884 03092010 JB8  TO CATER FOR TRANSFEREE              *           
      * BP133884 28092010 ESD1 TO CATER FOR TRANSFEREE PARTY TYPE,  *           
      *                        AND REMOVE DBDS TO SEND TO IRAS.     *           
      * BP133884 28102010 ESD1 TO READ BP13K330 TO GET THE TRANFOR  *           
      *                        INFO AND WRITE TO IRAS FILE.         *           
      * BP133934 02112010 ESD1 REMOVE PARTY NA FOR SUBTRANSFER      *           
      * BP133781 11112010 ESD1 CORRECTED THE PARTY COUNT AND WRITING*           
      *                        OF TRANSFEROR.                       *           
      * BP133884 11152010 ESD1 EDITED THE LOGIC TO REMOVE DBDS ON   *           
      *                        THE FILE SENT TO IRAS.               *           
      * BP133781 14022011 ESD1 CATER TO CORRECT THE LOGIC FOR TRANS-*           
      *                        FEROR.                               *           
      * BP134218 11052011 JB8  CATER FOR ZERO STAMP DUTY DUPLICATE  *           
      * BP133884 05072011 ESD1 CATER FOR NOMINAL DUTIES FEE   *                 
      * BP134809 26032013 JGO1 DO NOT ALLOW ZEROES ON STAMP DUTY    *           
      *                        FOR DOC NUM 'TRA'                    *           
      * BP134829 13082013 JGO1 WRITE F640 RECORD TO NEW OUTPUT FILE *           
      *                        FOR REGN NUM NOT IN K330             *           
      * BP135096 16102013 JGO1 UPDATE VOUCHER FROM 'SU' TO 'SS'     *           
      * BP135017 22012014 JGO1 UPDATE TO INCLUDE SUA AND TOL        *           
      * BP135309 03092014 JGO1 EXCLUDE DBST VOUCHERS                *           
      *=============================================================*           
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
                                                                                
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BB14F140     ASSIGN   TO       BB14F140                       
                  ORGANIZATION     IS  SEQUENTIAL.                              
                                                                                
           SELECT AG07F640     ASSIGN   TO       AG07F640                       
                  ORGANIZATION     IS  SEQUENTIAL.                              
                                                                                
           SELECT BP13F640     ASSIGN   TO       BP13F640                       
                  ORGANIZATION     IS  SEQUENTIAL.                              
                                                                                
           SELECT SY02F001     ASSIGN   TO SY02F001.                            
                                                                                
           SELECT BP13K625     ASSIGN   TO      BP13K625                        
                  ORGANIZATION     IS  INDEXED                                  
                  ACCESS MODE      IS  DYNAMIC                                  
                  RECORD KEY       IS  K625-KEY-FLD                             
                  ALTERNATE RECORD KEY IS K625-ALT-KEY1                         
                                   WITH DUPLICATES                              
                  ALTERNATE RECORD KEY IS K625-ALT-KEY2                         
                                   WITH DUPLICATES                              
                  FILE STATUS      IS  WS-K625-STATUS.                          
                                                                                
           SELECT BP13K800     ASSIGN   TO      BP13K800                        
                  ORGANIZATION     IS  INDEXED                                  
                  ACCESS MODE      IS  DYNAMIC                                  
                  RECORD KEY       IS  K800-NUM-REGN                            
                  FILE STATUS      IS  WS-K800-STATUS.                          
                                                                                
           SELECT BP13K330     ASSIGN   TO      BP13K330                        
                  ORGANIZATION     IS  INDEXED                                  
                  ACCESS MODE      IS  DYNAMIC                                  
                  RECORD KEY       IS  K330-KEY-FLD                             
                  FILE STATUS      IS  WS-K330-STATUS.                          
                                                                                
           SELECT BP13K820     ASSIGN   TO      BP13K820                        
                  ORGANIZATION     IS  INDEXED                                  
                  ACCESS MODE      IS  DYNAMIC                                  
                  RECORD KEY       IS  K820-KEY-FLD                             
                  FILE STATUS      IS  WS-K820-STATUS.                          
                                                                                
           SELECT BP13K410     ASSIGN   TO      BP13K410                        
                  ORGANIZATION     IS  INDEXED                                  
                  ACCESS MODE      IS  DYNAMIC                                  
                  RECORD KEY       IS  K410-KEY-FLD                             
                  FILE STATUS      IS  WS-K410-STATUS.                          
                                                                                
      *---------------*                                                         
       DATA DIVISION.                                                           
      *---------------*                                                         
                                                                                
      *---------------*                                                         
       FILE SECTION.                                                            
      *---------------*                                                         
                                                                                
       FD   BB14F140                                                            
            BLOCK  CONTAINS  0   RECORDS                                        
            RECORD CONTAINS  300 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BB14F140.                                                           
                                                                                
       FD   AG07F640                                                            
            BLOCK  CONTAINS  0   RECORDS                                        
            RECORD CONTAINS  100 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY AG07F640.                                                           
                                                                                
       FD   BP13F640                                                            
            BLOCK  CONTAINS  0   RECORDS                                        
            RECORD CONTAINS  100 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BP13F640-REC.                                                       
            05 F640-RECORD          PIC X(100).                                 
                                                                                
       FD   BP13K625                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13K625.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K330                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K330.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS  400 CHARACTERS.                                    
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K625-STATUS         PIC X(02).                                
           05  WS-K820-STATUS         PIC X(02).                                
           05  WS-K410-STATUS         PIC X(02).                                
           05  WS-K800-STATUS         PIC X(02).                                
           05  WS-K330-STATUS         PIC X(02).                                
                                                                                
       01  WS-EOF-VARIABLES.                                                    
           05  WS-K625-EOF            PIC X VALUE 'N'.                          
           05  WS-F640-EOF            PIC X VALUE 'N'.                          
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F140-TOT-WRITE      PIC 9(08)    VALUE ZEROES.                
           05  WS-F140-BAT-WRITE      PIC 9(08)    VALUE ZEROES.                
           05  WS-F140-DET-WRITE      PIC 9(08)    VALUE ZEROES.                
           05  WS-F140-PTY-WRITE      PIC 9(08)    VALUE ZEROES.                
           05  WS-K625-READ           PIC 9(08)    VALUE ZEROES.                
           05  WS-K625-WRITE          PIC 9(08)    VALUE ZEROES.                
           05  WS-K625-FND            PIC 9(08)    VALUE ZEROES.                
           05  WS-K625-NOT-FND        PIC 9(08)    VALUE ZEROES.                
           05  WS-F640-WRITE          PIC 9(08)    VALUE ZEROES.                
           05  WS-F640-READ           PIC 9(08)    VALUE ZEROES.                
           05  WS-F640-SKIP           PIC 9(08)    VALUE ZEROES.                
           05  CNT-K800-FND           PIC 9(08)    VALUE ZEROES.                
           05  CNT-K800-NOTFND        PIC 9(08)    VALUE ZEROES.                
           05  CNT-K820-FND           PIC 9(08)    VALUE ZEROES.                
           05  CNT-K820-NOTFND        PIC 9(08)    VALUE ZEROES.                
           05  CNT-K410-FND           PIC 9(08)    VALUE ZEROES.                
           05  CNT-K410-NOTFND        PIC 9(08)    VALUE ZEROES.                
           05  WS-CTR                 PIC 9(03)    VALUE ZEROES.                
           05  CNT-ROW                PIC 9(03)    VALUE ZEROES.                
           05  WS-CTR1                PIC 9(03)    VALUE ZEROES.                
           05  WS-CTR2                PIC 9(03)    VALUE ZEROES.                
           05  WS-I                   PIC 9(03)    VALUE ZEROES.                
           05  WS-NUM-LEASE-TERM1     PIC 9(04)V99 VALUE ZEROES.                
                                                                                
       01  WS-DATE.                                                             
           05  WS-CCYY                PIC 9(04).                                
           05  WS-MM                  PIC 9(02).                                
           05  WS-DD                  PIC 9(02).                                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-BATCH           PIC X(20).                                
           05  WS-NUM-FIRST           PIC X VALUE SPACES.                       
           05  WS-TOT-AMT-STAMP       PIC 9(16)V99 VALUE ZEROES.                
                                                                                
           05  WS-F140-HSEMAIN.                                                 
               10  WS-F140-HSE-MAIN       PIC X(4).                             
               10  WS-F140-HSE-SUB.                                             
                   15  WS-F140-HSE-SUB1   PIC X.                                
                   15  WS-F140-HSE-SUB2   PIC XX.                               
           05  WS-K800-FND            PIC X(01) VALUE SPACE.                    
               88 K800-FND                      VALUE 'Y'.                      
           05  WS-K330-FND            PIC X(01) VALUE SPACE.                    
               88 K330-FND                      VALUE 'Y'.                      
           05  WS-K820-FND            PIC X(01) VALUE SPACE.                    
               88 K820-FND                      VALUE 'Y'.                      
           05  WS-K410-FND            PIC X(01) VALUE SPACE.                    
               88 K410-FND                      VALUE 'Y'.                      
                                                                                
           05  WS-LESSEE-DTL.                                                   
               10  WS-NRIC OCCURS 4 TIMES PIC X(09).                            
               10  WS-NAME OCCURS 4 TIMES PIC X(66).                            
                                                                                
           05  WS-TRNFOR-DTL.                                                   
               10  WS-NRIC-TRNFOR OCCURS 4 TIMES PIC X(09).                     
               10  WS-NAME-TRNFOR OCCURS 4 TIMES PIC X(66).                     
                                                                                
           05  WS-TRNFOR-ALL-1.                                                 
               10  WS-TRNFOR-ALL OCCURS    200   PIC X(300).                    
                                                                                
                                                                                
       01  WS-DETAIL-TABLE.                                                     
           05  WS-DETAIL       OCCURS    200          TIMES.                    
               10  WS-NUM-HDB-REF          PIC X(11).                           
               10  WS-NUM-DOC              PIC X(03).                           
               10  WS-DTE-DOC-PREPARE      PIC X(08).                           
               10  WS-NUM-DOC-TYP          PIC X(01).                           
               10  WS-AMT-PURCHASE-PRICE   PIC 9(13)V99.                        
               10  WS-AMT-ANNUAL-RENT      PIC 9(13)V99.                        
               10  WS-AMT-SSD              PIC 9(13)V99.                        
               10  WS-NUM-INCREASE-SCCC    PIC X(1).                            
               10  WS-AMT-LOAN             PIC 9(13)V99.                        
               10  WS-AMT-STAMP-DUTY       PIC 9(13)V99.                        
               10  WS-AMT-STAMP-DUTY-DUP   PIC 9(13)V99.                        
               10  WS-NUM-LEASE-TERM       PIC 9(04)V99.                        
               10  WS-NUM-REGN             PIC X(08).                           
               10  WS-LESSEE-ALL           PIC X(300).                          
                                                                                
                                                                                
       01  SQL-CODE                    PIC 9(4).                                
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13C913                        *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH     PIC X(4).                                
               10  WS-LINK-NUM-ACC     PIC X(5).                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
                                                                                
      *================================================================*        
       0000-CONTROL.                                                            
      *================================================================*        
      *---------------------------------                                        
      *   OPENS PROCESSS AND CLOSES FILES                                       
      *----------------------------------                                       
            PERFORM  1000-OPEN-FILES    THRU     1000-EXIT.                     
                                                                                
            PERFORM  2000-READ-AG07F640 THRU     2000-EXIT                      
                     UNTIL WS-F640-EOF = 'Y'.                                   
                                                                                
            PERFORM  9000-CLOSE-FILES   THRU     9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       1000-OPEN-FILES.                                                         
      *================================================================*        
      *------------------------------------                                     
      *   OPENS FILES & CHECKS FILE STATUS                                      
      *------------------------------------                                     
           OPEN                                                                 
                   INPUT       AG07F640                                         
                               SY02F001                                         
                               BP13K820                                         
                               BP13K410                                         
                               BP13K800                                         
                               BP13K330                                         
                   OUTPUT      BB14F140                                         
                               BP13F640                                         
                   I-O         BP13K625.                                        
                                                                                
           IF    WS-K625-STATUS = '00' OR '97'                                  
                 CONTINUE                                                       
           ELSE                                                                 
                 DISPLAY 'ERROR WHILE OPENING BP13K625' WS-K625-STATUS          
                 PERFORM  9000-CLOSE-FILES THRU     9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF    WS-K800-STATUS = '00' OR '97'                                  
                 CONTINUE                                                       
           ELSE                                                                 
                 DISPLAY 'ERROR WHILE OPENING BP13K800' WS-K800-STATUS          
                 PERFORM  9000-CLOSE-FILES THRU     9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF    WS-K330-STATUS = '00' OR '97'                                  
                 CONTINUE                                                       
           ELSE                                                                 
                 DISPLAY 'ERROR WHILE OPENING BP13K330' WS-K330-STATUS          
                 PERFORM  9000-CLOSE-FILES THRU     9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF    WS-K820-STATUS = '00' OR '97'                                  
                 CONTINUE                                                       
           ELSE                                                                 
                 DISPLAY 'ERROR WHILE OPENING BP13K820' WS-K820-STATUS          
                 PERFORM  9000-CLOSE-FILES THRU     9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF    WS-K410-STATUS = '00' OR '97'                                  
                 CONTINUE                                                       
           ELSE                                                                 
                 DISPLAY 'ERROR WHILE OPENING BP13K410' WS-K410-STATUS          
                 PERFORM  9000-CLOSE-FILES THRU     9000-EXIT                   
           END-IF.                                                              
                                                                                
      *----------------------*                                                  
      *   GETTING SYSTEM DATE                                                   
      *----------------------*                                                  
           READ SY02F001                                                        
           AT END                                                               
              DISPLAY 'EMPTY FILE - SY02F001 '                                  
              PERFORM  9000-CLOSE-FILES THRU 9000-EXIT.                         
                                                                                
           MOVE F001-DTE-CURRENT       TO WS-DATE.                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2000-READ-AG07F640.                                                      
      *================================================================*        
      *-------------------*                                                     
      *   READS INPUT FILE                                                      
      *-------------------*                                                     
            READ  AG07F640 AT END MOVE 'Y' TO    WS-F640-EOF                    
                  GO TO    2000-EXIT.                                           
                                                                                
            ADD   1  TO    WS-F640-READ.                                        
                                                                                
            IF F640-NUM-REJECT NOT = SPACES                                     
               ADD   1           TO WS-F640-SKIP                                
               CONTINUE                                                         
            ELSE                                                                
               MOVE F640-NUM-VR  TO K625-ALT-KEY1                               
               MOVE 'N'          TO WS-K625-EOF                                 
               PERFORM 2100-START-BP13K625   THRU 2100-EXIT                     
            END-IF.                                                             
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2100-START-BP13K625.                                                     
      *================================================================*        
            START BP13K625 KEY = K625-ALT-KEY1.                                 
                                                                                
            EVALUATE  WS-K625-STATUS                                            
               WHEN '00'                                                        
                     PERFORM 2200-READNEXT-BP13K625 THRU 2200-EXIT              
               WHEN '10'                                                        
               WHEN '23'                                                        
                     DISPLAY 'VOUCHER NUMBER NOT FOUND IN BP13K625 '            
                              F640-NUM-VR                                       
                     ADD 1     TO WS-K625-NOT-FND                               
                     GO TO 2100-EXIT                                            
               WHEN OTHER                                                       
                    DISPLAY 'ERROR WHILE STARTING BROWSE-BP13K625 '             
                    MOVE WS-K625-STATUS TO RETURN-CODE                          
                    PERFORM 9000-CLOSE-FILES                                    
            END-EVALUATE.                                                       
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2200-READNEXT-BP13K625.                                                  
      *================================================================*        
            MOVE ZEROS                      TO WS-CTR CNT-ROW WS-CTR2           
            MOVE SPACES                     TO WS-DETAIL-TABLE                  
            INITIALIZE                         WS-DETAIL-TABLE.                 
                                                                                
            PERFORM 2500-READ-BP13K625    THRU 2500-EXIT                        
                    UNTIL WS-K625-EOF = 'Y'.                                    
                                                                                
            IF WS-NUM-BATCH(1:4) NOT = 'DBDS' AND WS-CTR > 0 AND                
               WS-NUM-BATCH(1:4) NOT = 'DBST'                                   
               PERFORM 2600-BB14F140-BATCH-WRITE  THRU 2600-EXIT                
            END-IF.                                                             
                                                                                
            MOVE ZEROES TO WS-CTR1.                                             
                                                                                
            IF WS-NUM-BATCH(1:4) NOT = 'DBDS' AND                               
               WS-NUM-BATCH(1:4) NOT = 'DBST'                                   
               PERFORM 2700-BB14F140-DETAIL-WRITE THRU 2700-EXIT                
                    UNTIL WS-CTR1 = WS-CTR                                      
            END-IF.                                                             
                                                                                
            MOVE ZEROES TO WS-TOT-AMT-STAMP.                                    
                                                                                
            PERFORM 2950-INITIALIZE-TABLE    THRU 2950-EXIT                     
                    VARYING WS-CTR   FROM 1 BY 1                                
                    UNTIL WS-CTR > 200.                                         
                                                                                
            MOVE ZEROES TO WS-CTR.                                              
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2500-READ-BP13K625.                                                      
      *================================================================*        
            READ BP13K625 NEXT AT END MOVE 'Y' TO WS-K625-EOF                   
               GO TO 2500-EXIT.                                                 
                                                                                
            IF K625-ALT-KEY1 NOT = F640-NUM-VR OR                               
               K625-NUM-BATCH-TYPE = 'DBDS' OR                                  
               K625-NUM-BATCH-TYPE = 'DBST'                                     
               MOVE 'Y'  TO WS-K625-EOF                                         
                                                                                
               GO TO 2500-EXIT                                                  
            END-IF.                                                             
                                                                                
      *------------------------------------                                     
      *   ACCUMULATES STAMP DUTY                                                
      *------------------------------------                                     
            IF  K625-AMT-STAMP-DUTY NOT NUMERIC MOVE ZEROES TO                  
                K625-AMT-STAMP-DUTY                                             
            END-IF.                                                             
                                                                                
            IF  K625-AMT-STAMP-DUTY-DUP NOT NUMERIC MOVE ZEROES TO              
                K625-AMT-STAMP-DUTY-DUP                                         
            END-IF.                                                             
                                                                                
            IF (K625-AMT-STAMP-DUTY = ZEROES) AND                               
               (K625-AMT-STAMP-DUTY-DUP = ZEROES) AND                           
      *        (K625-NUM-DOC  NOT = 'TRA' AND 'TWF' AND 'RMP')                  
               (K625-NUM-DOC  NOT = 'TWF' AND 'RMP')                            
               GO TO 2500-EXIT                                                  
            END-IF.                                                             
                                                                                
            ADD 1 TO WS-K625-READ.                                              
            ADD 1 TO WS-CTR.                                                    
                                                                                
            PERFORM 2550-GET-PARTY            THRU 2550-EXIT                    
                                                                                
            ADD K625-AMT-STAMP-DUTY      TO WS-TOT-AMT-STAMP.                   
            ADD K625-AMT-STAMP-DUTY-DUP  TO WS-TOT-AMT-STAMP.                   
            IF   K625-AMT-SELLER-DUTY IS NOT NUMERIC                            
            ADD  ZEROES                  TO WS-TOT-AMT-STAMP                    
            ELSE                                                                
            ADD K625-AMT-SELLER-DUTY     TO WS-TOT-AMT-STAMP                    
            END-IF.                                                             
                                                                                
      *------------------------------------------------------                   
      *   GETTING THE VALUE OF BATCH-NUM TO WRITE TO 'BATCH'                    
      *------------------------------------------------------                   
                                                                                
            MOVE K625-NUM-BATCH          TO WS-NUM-BATCH.                       
                                                                                
      *------------------------------------                                     
      *   TO WRITE TO K640 'DETAIL'                                             
      *------------------------------------                                     
                                                                                
            MOVE K625-NUM-HDB-REF        TO WS-NUM-HDB-REF(WS-CTR).             
            MOVE K625-NUM-DOC            TO WS-NUM-DOC(WS-CTR).                 
                                                                                
      *-----------------------------------------------------------------        
      *   K625-DTE-DOC-PREPARE IS DERIVED FROM                                  
      *  ++ SOCA ++                                                             
      *   1) AO RAISED ==> CREATE BP13K300                                      
      *   2) RUN BP13C301  MOVES F300-DTE-AGRM-ORDER TO                         
      *                    K800-DTE-PRINCIPAL-SALES-AGMT                        
      *   3) BP13B320  ==> MOVES K800-DTE-AGRMT-FOR-LEASE TO                    
      *                    COMM1-DTE-AGREEMENT-FOR-LEASE.                       
      *   4) BP13B321  ==> MOVES COMM1-DTE-AGREEMENT-FOR-LEASE TO SCREEN        
      *                                                     (PROTECTED)         
      *  ++ SUBTRANSFER ++                                                      
      *     DTE-SUB-TRANSFER IS SAME AS DTE-AGREEMENT-FOR-LEASE                 
      *   1) BP13B800  ==> MOVES K800-DTE-AGRMT-FOR-LEASE TO                    
      *                    COM12-DTE-ORIG-AFL.                                  
      *   2) BP13B805  ==> MOVES COM12-DTE-ORIG-AFL TO SCREEN(ORIG AFL)         
      *                    MOVES CURRENT DATE       TO SCREEN(AFL DTE)          
      *                    MOVES COM12-REC TO K880-REC       (PROTECTED)        
      *   3) RUN BP13C350  MOVES F880-DTE-AGRMT-FOR-LEASE TO                    
      *                    K800-DTE-AGREEMENT-FOR-LEASE                         
      *   4) RUN BP13C356  MOVES F880-DTE-SUB-TRANSFER TO                       
      *                    K330-DTE-AGRM-ORDER                                  
      *   5) RUN BP13C355  MOVES K330-DTE-AGRM-ORDER TO                         
      *                    K625-DTE-DOC-PREPARE                                 
      *  ++ TP ++                                                               
      *   1) BP13B401  ==> MOVES K800-DTE-AGRMT-FOR-LEASE TO                    
      *      BP13B641      COMM3-DTE-PRINCIPAL-SALES-AGMT                       
      *                    LAST PANEL MOVES COMM3-REC TO BP13K400               
      *  ++ SA/TP ++                                                            
      *   1) BP13B612  ==> MOVES COMM3-DTE-SO-EXECUTION TO                      
      *                    K400-DTE-AGRMT-FOR-LEASE                             
      *   2) BP13B608  ==> MOVES COMM3-DTE-SO-EXECUTION TO                      
      *      BP13B685      K400-DTE-AGRMT-FOR-LEASE                             
      *-----------------------------------------------------------------        
                                                                                
            MOVE K625-DTE-DOC-PREPARE    TO WS-DTE-DOC-PREPARE(WS-CTR).         
            MOVE K625-NUM-DOC-TYP        TO WS-NUM-DOC-TYP(WS-CTR).             
            MOVE K625-AMT-PURCHASE-PRICE TO                                     
                                  WS-AMT-PURCHASE-PRICE(WS-CTR).                
            MOVE K625-AMT-ANNUAL-RENT    TO WS-AMT-ANNUAL-RENT(WS-CTR).         
            IF   K625-AMT-SELLER-DUTY IS NOT NUMERIC                            
            MOVE ZEROES                  TO WS-AMT-SSD(WS-CTR)                  
            ELSE                                                                
            MOVE K625-AMT-SELLER-DUTY    TO WS-AMT-SSD(WS-CTR)                  
            END-IF.                                                             
            MOVE K625-NUM-INCREASE-SCCC  TO                                     
                                  WS-NUM-INCREASE-SCCC(WS-CTR).                 
            MOVE K625-AMT-LOAN           TO WS-AMT-LOAN(WS-CTR).                
            MOVE K625-AMT-STAMP-DUTY     TO WS-AMT-STAMP-DUTY(WS-CTR).          
            MOVE K625-AMT-STAMP-DUTY-DUP TO                                     
                                         WS-AMT-STAMP-DUTY-DUP(WS-CTR).         
                                                                                
            MOVE K625-NUM-LEASE-TERM TO WS-NUM-LEASE-TERM1.                     
                                                                                
            IF WS-NUM-LEASE-TERM1 = 9900.00                                     
               MOVE 1188.00              TO WS-NUM-LEASE-TERM(WS-CTR)           
            ELSE                                                                
            IF WS-NUM-LEASE-TERM1 = 3000.00                                     
               MOVE 360.00               TO WS-NUM-LEASE-TERM(WS-CTR)           
            ELSE                                                                
               MOVE WS-NUM-LEASE-TERM1   TO WS-NUM-LEASE-TERM(WS-CTR)           
            END-IF.                                                             
                                                                                
            MOVE K625-NUM-REGN           TO WS-NUM-REGN(WS-CTR).                
                                                                                
      *----------------------------------------                                 
      *   TO UPDATE BP13K625 REC WITH AG07 INFO                                 
      *----------------------------------------                                 
      *     STRING F640-NUM-BANK-PAY-FROM,  F640-NUM-IBG-SERIAL                 
      *            DELIMITED BY SIZE INTO   K625-NUM-IBG-REF.                   
                                                                                
            MOVE F640-TXT-IBG-SERIAL     TO K625-NUM-IBG-REF.                   
            MOVE F640-DTE-IBG-VALUE      TO K625-DTE-IBG-VALUE.                 
            MOVE WS-DATE                 TO K625-DTE-BATCH-SENT.                
                                                                                
            REWRITE      BP13K625-REC.                                          
                                                                                
            ADD 1 TO WS-K625-WRITE.                                             
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       2550-GET-PARTY.                                                          
      *================================================================*        
                                                                                
           MOVE SPACES   TO WS-LESSEE-ALL(WS-CTR)                               
                            WS-TRNFOR-ALL(WS-CTR)                               
                            WS-LESSEE-DTL                                       
                            WS-TRNFOR-DTL.                                      
                                                                                
           MOVE K625-NUM-REGN             TO K800-NUM-REGN                      
           PERFORM 3100-READ-BP13K800   THRU 3100-EXIT                          
                                                                                
      * FOR 'PARTY NA'                                                          
           IF K625-NUM-DOC = 'TWF' OR 'TRA' OR 'RMP'                            
              CONTINUE                                                          
           ELSE                                                                 
              ADD 1                          TO CNT-ROW                         
           END-IF                                                               
      * FOR 'DETAIL'                                                            
           ADD 1                          TO CNT-ROW                            
      * FOR TRANSFEROR                                                          
           IF  K625-NUM-DOC  = 'TRA' OR 'TWF' OR 'RMP'                          
               MOVE K800-NUM-REGN   TO  K330-CURR-REG                           
               MOVE '1'             TO  K330-SEQ-NO                             
               PERFORM 3210-READ-BP13K330 THRU 3210-EXIT                        
               IF  K330-NRIC1 NOT = SPACES AND LOW-VALUES                       
                   MOVE K330-NRIC1  TO  WS-NRIC-TRNFOR(1)                       
                   MOVE K330-NAME1  TO  WS-NAME-TRNFOR(1)                       
                   ADD  1           TO  CNT-ROW                                 
               END-IF                                                           
               IF  K330-NRIC2 NOT = SPACES AND LOW-VALUES                       
                   MOVE K330-NRIC2  TO  WS-NRIC-TRNFOR(2)                       
                   MOVE K330-NAME2  TO  WS-NAME-TRNFOR(2)                       
                   ADD  1           TO  CNT-ROW                                 
               END-IF                                                           
               IF  K330-NRIC3 NOT = SPACES AND LOW-VALUES                       
                   MOVE K330-NRIC3  TO  WS-NRIC-TRNFOR(3)                       
                   MOVE K330-NAME3  TO  WS-NAME-TRNFOR(3)                       
                   ADD  1           TO  CNT-ROW                                 
               END-IF                                                           
               IF  K330-NRIC4 NOT = SPACES AND LOW-VALUES                       
                   MOVE K330-NRIC4  TO  WS-NRIC-TRNFOR(4)                       
                   MOVE K330-NAME4  TO  WS-NAME-TRNFOR(4)                       
                   ADD  1           TO  CNT-ROW                                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
      * FOR INDIVIDUAL                                                          
           IF K800-FND                                                          
              ADD 1                       TO CNT-K800-FND                       
              MOVE K800-NUM-REGN          TO K820-NUM-REGN                      
              MOVE K800-NUM-NRIC1         TO K820-NUM-NRIC                      
              PERFORM 3200-READ-BP13K820 THRU 3200-EXIT                         
              IF K820-FND                                                       
                 ADD 1                    TO CNT-K820-FND                       
                 ADD 1                    TO CNT-ROW                            
                 MOVE K820-NUM-NRIC       TO WS-NRIC(1)                         
                 MOVE K820-NME-OCCP       TO WS-NAME(1)                         
              ELSE                                                              
                 ADD 1                    TO CNT-K820-NOTFND                    
              END-IF                                                            
                                                                                
              MOVE K800-NUM-REGN          TO K820-NUM-REGN                      
              IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                     
                 MOVE K800-NUM-NRIC2      TO K820-NUM-NRIC                      
                 PERFORM 3200-READ-BP13K820 THRU 3200-EXIT                      
                 IF K820-FND                                                    
                    ADD 1                 TO CNT-K820-FND                       
                    ADD 1                 TO CNT-ROW                            
                    MOVE K820-NUM-NRIC    TO WS-NRIC(2)                         
                    MOVE K820-NME-OCCP    TO WS-NAME(2)                         
                 ELSE                                                           
                    ADD 1                 TO CNT-K820-NOTFND                    
                 END-IF                                                         
              END-IF                                                            
                                                                                
              MOVE K800-NUM-REGN          TO K820-NUM-REGN                      
              IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                     
                 MOVE K800-NUM-NRIC3      TO K820-NUM-NRIC                      
                 PERFORM 3200-READ-BP13K820 THRU 3200-EXIT                      
                 IF K820-FND                                                    
                    ADD 1                 TO CNT-K820-FND                       
                    ADD 1                 TO CNT-ROW                            
                    MOVE K820-NUM-NRIC    TO WS-NRIC(3)                         
                    MOVE K820-NME-OCCP    TO WS-NAME(3)                         
                 ELSE                                                           
                    ADD 1                 TO CNT-K820-NOTFND                    
                 END-IF                                                         
              END-IF                                                            
                                                                                
              MOVE K800-NUM-REGN          TO K820-NUM-REGN                      
              IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                     
                 MOVE K800-NUM-NRIC4      TO K820-NUM-NRIC                      
                 PERFORM 3200-READ-BP13K820 THRU 3200-EXIT                      
                 IF K820-FND                                                    
                    ADD 1                 TO CNT-K820-FND                       
                    ADD 1                 TO CNT-ROW                            
                    MOVE K820-NUM-NRIC    TO WS-NRIC(4)                         
                    MOVE K820-NME-OCCP    TO WS-NAME(4)                         
                 ELSE                                                           
                    ADD 1                 TO CNT-K820-NOTFND                    
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              ADD 1                       TO CNT-K800-NOTFND                    
              MOVE K625-NUM-REGN          TO K410-NUM-REGN                      
              PERFORM 3300-READ-BP13K410  THRU 3300-EXIT                        
              IF K410-FND                                                       
                 ADD 1                    TO CNT-K410-FND                       
                 IF K410-NRIC1 NOT = SPACES AND LOW-VALUES                      
                    ADD 1                 TO CNT-ROW                            
                 END-IF                                                         
                 IF K410-NRIC2 NOT = SPACES AND LOW-VALUES                      
                    ADD 1                 TO CNT-ROW                            
                 END-IF                                                         
                 IF K410-NRIC3 NOT = SPACES AND LOW-VALUES                      
                    ADD 1                 TO CNT-ROW                            
                 END-IF                                                         
                 IF K410-NRIC4 NOT = SPACES AND LOW-VALUES                      
                    ADD 1                 TO CNT-ROW                            
                 END-IF                                                         
                 MOVE K410-NAME1          TO WS-NAME(1)                         
                 MOVE K410-NRIC1          TO WS-NRIC(1)                         
                 MOVE K410-NAME2          TO WS-NAME(2)                         
                 MOVE K410-NRIC2          TO WS-NRIC(2)                         
                 MOVE K410-NAME3          TO WS-NAME(3)                         
                 MOVE K410-NRIC3          TO WS-NRIC(3)                         
                 MOVE K410-NAME4          TO WS-NAME(4)                         
                 MOVE K410-NRIC4          TO WS-NRIC(4)                         
              ELSE                                                              
                 ADD 1                    TO CNT-K410-NOTFND                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE WS-LESSEE-DTL             TO WS-LESSEE-ALL(WS-CTR).             
           MOVE WS-TRNFOR-DTL             TO WS-TRNFOR-ALL(WS-CTR).             
                                                                                
       2550-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       2600-BB14F140-BATCH-WRITE.                                               
      *================================================================*        
            MOVE SPACES                 TO BB14F140-BATCH.                      
            INITIALIZE                     BB14F140-BATCH.                      
            MOVE 'BATCH'                TO F140-NUM-REC-TYP OF                  
                                           BB14F140-BATCH.                      
            MOVE  WS-NUM-BATCH          TO F140-NUM-BATCH.                      
                                                                                
      *     STRING F640-NUM-BANK-PAY-FROM,  F640-NUM-IBG-SERIAL                 
      *            DELIMITED BY SIZE INTO   F140-NUM-IBG-REF.                   
                                                                                
            MOVE  F640-TXT-IBG-SERIAL   TO F140-NUM-IBG-REF.                    
            MOVE  F640-DTE-IBG-VALUE    TO F140-DTE-VALUE.                      
            MOVE  WS-TOT-AMT-STAMP      TO F140-AMT-STMP-TOTAL.                 
      *     MOVE 'SU'                   TO F140-NUM-DEPT-SYS.                   
            MOVE 'SS'                   TO F140-NUM-DEPT-SYS.                   
            MOVE  CNT-ROW               TO F140-QTY-ROW-TOTAL.                  
                                                                                
            COMPUTE F140-QTY-ROW-TOTAL = F140-QTY-ROW-TOTAL + 1.                
                                                                                
            WRITE BB14F140-BATCH.                                               
                                                                                
            ADD 1 TO WS-F140-TOT-WRITE.                                         
            ADD 1 TO WS-F140-BAT-WRITE.                                         
                                                                                
       2600-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2700-BB14F140-DETAIL-WRITE.                                              
      *================================================================*        
            MOVE SPACES                          TO BB14F140-DETAIL.            
            INITIALIZE                              BB14F140-DETAIL             
            ADD 1 TO WS-CTR1.                                                   
            MOVE 'DETAIL'                        TO F140-NUM-REC-TYP            
                                 OF BB14F140-DETAIL.                            
            MOVE  WS-NUM-HDB-REF(WS-CTR1)        TO F140-NUM-BATCH.             
            MOVE  WS-NUM-DOC(WS-CTR1)            TO F140-NUM-DOC-TYPE.          
            MOVE  WS-DTE-DOC-PREPARE(WS-CTR1)    TO F140-DTE-DOC-PREP.          
            MOVE  WS-NUM-DOC-TYP(WS-CTR1)        TO F140-NUM-DOC-CAT.           
            MOVE  'B'                            TO F140-NUM-PPTY-TYPE.         
            MOVE  WS-AMT-PURCHASE-PRICE(WS-CTR1)                                
                                                 TO F140-AMT-CONSID.            
            MOVE  WS-AMT-ANNUAL-RENT(WS-CTR1) TO                                
                                                 F140-AMT-RENT-ANNUAL.          
            MOVE  WS-AMT-SSD(WS-CTR1)       TO F140-AMT-SELLER-DUTY.            
            MOVE  WS-NUM-INCREASE-SCCC(WS-CTR1)  TO F140-NUM-SCCC-INCR.         
            MOVE  WS-AMT-LOAN(WS-CTR1)           TO                             
                                                 F140-AMT-LOAN-GRANTED.         
                                                                                
            IF WS-AMT-STAMP-DUTY(WS-CTR1) = ZEROES                              
               MOVE WS-AMT-STAMP-DUTY-DUP(WS-CTR1)  TO F140-AMT-STAMP           
            ELSE                                                                
               MOVE WS-AMT-STAMP-DUTY(WS-CTR1)      TO F140-AMT-STAMP           
            END-IF.                                                             
                                                                                
            MOVE WS-NUM-LEASE-TERM(WS-CTR1)      TO F140-NUM-LEASE-TERM.        
                                                                                
      *------------------------------------                                     
      *      CALL BP13C913 TO GET ADDRESS                                       
      *------------------------------------                                     
                                                                                
            MOVE  SPACES                  TO  BP13COMM8-REC.                    
            INITIALIZE                        BP13COMM8-REC.                    
                                                                                
            MOVE  WS-NUM-HDB-REF(WS-CTR1) TO  WS-LINK-REC.                      
                                                                                
            PERFORM 2800-CALL-BP13C913 THRU  2800-EXIT.                         
                                                                                
            IF COMM8-CDE-SYSERR = 0                                             
               PERFORM 2900-MOVE-ADDR-DETLS THRU 2900-EXIT                      
            END-IF.                                                             
                                                                                
            WRITE BB14F140-DETAIL.                                              
                                                                                
            ADD 1 TO WS-F140-TOT-WRITE.                                         
            ADD 1 TO WS-F140-DET-WRITE.                                         
                                                                                
            MOVE WS-LESSEE-ALL(WS-CTR1)        TO WS-LESSEE-DTL.                
            PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 4                     
               IF WS-NRIC(WS-I) NOT = SPACES AND LOW-VALUES                     
                  IF (WS-NUM-FIRST NOT  = 'Y') AND                              
                     (WS-NUM-DOC(WS-CTR1) = 'TRA' OR 'TWF' OR 'RMP')            
                     MOVE 'Y' TO  WS-NUM-FIRST                                  
                     MOVE WS-TRNFOR-ALL(WS-CTR1) TO WS-TRNFOR-DTL               
                     PERFORM 2710-BB14F140-TRNFOR-WRITE THRU 2710-EXIT          
                     VARYING WS-CTR2 FROM 1 BY 1 UNTIL WS-CTR2 > 4              
                  END-IF                                                        
                  MOVE SPACES                  TO BB14F140-PARTY                
                  INITIALIZE                      BB14F140-PARTY                
                  MOVE 'PARTY '                TO F140-PTY-REC-TYP OF           
                                                  BB14F140-PARTY                
                  EVALUATE WS-NUM-DOC(WS-CTR1)                                  
                      WHEN 'DAS'                                                
                           MOVE 'MORGOR'       TO F140-PARTY-TYPE               
                      WHEN 'ALE'                                                
                           MOVE 'LESSEE'       TO F140-PARTY-TYPE               
                      WHEN 'TWF'                                                
                      WHEN 'TRA'                                                
                      WHEN 'RMP'                                                
                           MOVE 'TRNFRE'       TO F140-PARTY-TYPE               
                      WHEN 'TOL'                                                
                           MOVE 'LICNSE'       TO F140-PARTY-TYPE               
                      WHEN 'SUA'                                                
                           MOVE 'LESSEE'       TO F140-PARTY-TYPE               
                      WHEN OTHER                                                
                           MOVE 'LESSEE'       TO F140-PARTY-TYPE               
                  END-EVALUATE                                                  
                  MOVE WS-NAME(WS-I)           TO F140-NME-PARTY                
                  MOVE 'NRIC      '            TO F140-ENTITY-TYPE              
                  MOVE WS-NRIC(WS-I)           TO F140-NRIC-PARTY               
                                                                                
                  WRITE BB14F140-PARTY                                          
                                                                                
                  ADD 1 TO WS-F140-TOT-WRITE                                    
                  ADD 1 TO WS-F140-PTY-WRITE                                    
               END-IF                                                           
            END-PERFORM.                                                        
            MOVE 'N' TO  WS-NUM-FIRST                                           
            IF WS-NUM-DOC(WS-CTR1) = 'TWF' OR 'TRA' OR 'RMP'                    
               CONTINUE                                                         
            ELSE                                                                
               MOVE SPACES                        TO BB14F140-PARTY             
               INITIALIZE                            BB14F140-PARTY             
               MOVE 'PARTY '                    TO F140-PTY-REC-TYP OF          
                                                  BB14F140-PARTY                
               MOVE 'NA    '                      TO F140-PARTY-TYPE            
                                                                                
               WRITE BB14F140-PARTY                                             
                                                                                
               ADD 1 TO WS-F140-TOT-WRITE                                       
               ADD 1 TO WS-F140-PTY-WRITE                                       
            END-IF.                                                             
                                                                                
       2700-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       2710-BB14F140-TRNFOR-WRITE.                                              
      *================================================================*        
                                                                                
            IF  WS-NRIC-TRNFOR(WS-CTR2) = SPACES OR LOW-VALUES                  
                MOVE 5 TO WS-CTR2                                               
                GO TO 2710-EXIT                                                 
            END-IF.                                                             
                                                                                
            MOVE SPACES                  TO BB14F140-DETAIL.                    
            INITIALIZE                      BB14F140-DETAIL.                    
            MOVE SPACES                  TO BB14F140-PARTY.                     
            INITIALIZE                      BB14F140-PARTY.                     
            MOVE 'PARTY '                TO F140-PTY-REC-TYP OF                 
                                            BB14F140-PARTY.                     
            MOVE 'TRNFOR'                TO F140-PARTY-TYPE.                    
            MOVE WS-NAME-TRNFOR(WS-CTR2) TO F140-NME-PARTY.                     
            MOVE 'NRIC      '            TO F140-ENTITY-TYPE.                   
            MOVE WS-NRIC-TRNFOR(WS-CTR2) TO F140-NRIC-PARTY.                    
                                                                                
            WRITE BB14F140-PARTY.                                               
                                                                                
            ADD 1 TO WS-F140-TOT-WRITE.                                         
            ADD 1 TO WS-F140-PTY-WRITE.                                         
                                                                                
       2710-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       2800-CALL-BP13C913.                                                      
      *================================================================*        
            CALL 'BP13C913'     USING  WS-LINK-REC,                             
                                       BP13COMM8-REC.                           
                                                                                
            IF COMM8-CDE-SYSERR NOT = 0                                         
               MOVE COMM8-CDE-SYSERR TO SQL-CODE                                
               IF COMM8-CDE-SYSERR = 100                                        
                  DISPLAY 'SCHEME-ACCT-NO = ' WS-LINK-REC                       
                  DISPLAY 'SCH-ACC NOT FOUND INF PIDB TABLE   ' SQL-CODE        
               ELSE                                                             
                  DISPLAY 'ERROR IN READING PIDB TABLE   ' SQL-CODE.            
                                                                                
       2800-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2900-MOVE-ADDR-DETLS.                                                    
      *================================================================*        
               MOVE COMM8-NUM-UNIT-MAIN   TO  WS-F140-HSE-MAIN.                 
               MOVE COMM8-NUM-UNIT-SUB    TO  WS-F140-HSE-SUB.                  
               MOVE WS-F140-HSEMAIN       TO  F140-NUM-UNIT.                    
               MOVE COMM8-NUM-BLK         TO  F140-NUM-BLK-HSE.                 
               MOVE COMM8-NME-STREET      TO  F140-NME-STREET.                  
               MOVE COMM8-NUM-LEVEL       TO  F140-NUM-LVL.                     
               MOVE COMM8-NUM-POSTAL-CODE TO  F140-NUM-POSTAL.                  
                                                                                
       2900-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2950-INITIALIZE-TABLE.                                                   
      *================================================================*        
            MOVE SPACES TO   WS-NUM-HDB-REF(WS-CTR).                            
            MOVE SPACES TO   WS-NUM-DOC(WS-CTR).                                
            MOVE SPACES TO   WS-DTE-DOC-PREPARE(WS-CTR).                        
            MOVE SPACES TO   WS-NUM-DOC-TYP(WS-CTR).                            
            MOVE ZEROES TO   WS-AMT-PURCHASE-PRICE(WS-CTR).                     
            MOVE ZEROES TO   WS-AMT-ANNUAL-RENT(WS-CTR).                        
            MOVE SPACES TO   WS-NUM-INCREASE-SCCC(WS-CTR).                      
            MOVE ZEROES TO   WS-AMT-LOAN(WS-CTR)                                
            MOVE ZEROES TO   WS-AMT-STAMP-DUTY(WS-CTR)                          
            MOVE ZEROES TO   WS-AMT-STAMP-DUTY-DUP(WS-CTR)                      
            MOVE ZEROES TO   WS-NUM-LEASE-TERM(WS-CTR)                          
            MOVE SPACES TO   WS-NUM-REGN(WS-CTR).                               
                                                                                
       2950-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3100-READ-BP13K800.                                                      
      ******************************************************************        
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00 OR 02                                         
              MOVE 'Y'                   TO WS-K800-FND                         
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 MOVE 'N'                TO WS-K800-FND                         
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K800 - ' WS-K800-STATUS             
                 MOVE WS-K800-STATUS     TO RETURN-CODE                         
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-READ-BP13K820.                                                      
      ******************************************************************        
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00 OR 02                                         
              MOVE 'Y'                   TO WS-K820-FND                         
           ELSE                                                                 
              IF WS-K820-STATUS = 23                                            
                 MOVE 'N'                TO WS-K820-FND                         
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K820 - ' WS-K820-STATUS             
                 MOVE WS-K820-STATUS     TO RETURN-CODE                         
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3210-READ-BP13K330.                                                      
      ******************************************************************        
                                                                                
           READ BP13K330.                                                       
                                                                                
           IF WS-K330-STATUS = 00 OR 02                                         
              MOVE 'Y'                   TO WS-K330-FND                         
           ELSE                                                                 
              IF WS-K330-STATUS = 23                                            
                 DISPLAY 'NO RECORD ON BP13K330 - ' WS-K330-STATUS              
                 K800-NUM-REGN                                                  
                 PERFORM 3220-NOT-FOUND-K330 THRU 3220-EXIT                     
                 GO TO 2000-EXIT                                                
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K330 - ' WS-K330-STATUS             
                 MOVE WS-K330-STATUS     TO RETURN-CODE                         
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3220-NOT-FOUND-K330.                                                     
      ******************************************************************        
                                                                                
            INITIALIZE F640-RECORD.                                             
            MOVE AG07F640-REC TO F640-RECORD.                                   
            WRITE BP13F640-REC.                                                 
                                                                                
            ADD 1 TO WS-F640-WRITE                                              
                                                                                
            PERFORM 2950-INITIALIZE-TABLE    THRU 2950-EXIT                     
               VARYING WS-CTR   FROM 1 BY 1                                     
               UNTIL WS-CTR > 200.                                              
                                                                                
            MOVE ZEROES TO WS-CTR.                                              
                                                                                
       3220-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3300-READ-BP13K410.                                                      
      ******************************************************************        
                                                                                
           READ BP13K410.                                                       
                                                                                
           IF WS-K410-STATUS = 00 OR 02                                         
              MOVE 'Y'                   TO WS-K410-FND                         
           ELSE                                                                 
              IF WS-K410-STATUS = 23                                            
                 MOVE 'N'                TO WS-K410-FND                         
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K410 - ' WS-K410-STATUS             
                 MOVE WS-K410-STATUS     TO RETURN-CODE                         
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-FILES.                                                        
      *================================================================*        
           CLOSE     BP13K625                                                   
                     BB14F140                                                   
                     BP13K330                                                   
                     BP13K800                                                   
                     BP13K820                                                   
                     BP13K410                                                   
                     SY02F001                                                   
                     BP13F640                                                   
                     AG07F640.                                                  
                                                                                
           DISPLAY    '**** BP13C31A ****'.                                     
           DISPLAY    'AG07F640 TOT REC READ    : ' WS-F640-READ.               
           DISPLAY    'AG07F640 TOT REC SKIP    : ' WS-F640-SKIP.               
           DISPLAY    'BP13F640 TOT REC WRITE   : ' WS-F640-WRITE.              
           DISPLAY    'BP13K625 TOT REC READ    : ' WS-K625-READ.               
           DISPLAY    'BP13K625 RECORDS WRITTEN : ' WS-K625-WRITE.              
           DISPLAY    'BP13K625 RECORDS NOT FND : ' WS-K625-NOT-FND.            
           DISPLAY    'BB14F140 TOT REC WRITTEN : ' WS-F140-TOT-WRITE.          
           DISPLAY    'BB14F140 BAT REC WRITTEN : ' WS-F140-BAT-WRITE.          
           DISPLAY    'BB14F140 DET REC WRITTEN : ' WS-F140-DET-WRITE.          
           DISPLAY    'BB14F140 PTY REC WRITTEN : ' WS-F140-PTY-WRITE.          
           DISPLAY    'BP13K800 TOT REC READ    : ' CNT-K800-FND.               
           DISPLAY    'BP13K800 TOT REC NOT FND : ' CNT-K800-NOTFND.            
           DISPLAY    'BP13K820 TOT REC READ    : ' CNT-K820-FND.               
           DISPLAY    'BP13K820 TOT REC NOT FND : ' CNT-K820-NOTFND.            
           DISPLAY    'BP13K410 TOT REC READ    : ' CNT-K410-FND.               
           DISPLAY    'BP13K410 TOT REC NOT FND : ' CNT-K410-NOTFND.            
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
