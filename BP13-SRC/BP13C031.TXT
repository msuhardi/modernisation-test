       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C031.                                                 
      *AUTHOR.        SATHAPPAN.                                                
      *DATE-WRITTEN.  07/03/02.                                                 
      *===============================================================*         
      *               SYSTEM OF COMMITMENT (BP13) *                   *         
      *===============================================================*         
      *                                                               *         
      * OBJECTIVE :  THIS PROGRAM IS USED TO WRITE P13F853A FOR ONLY  *         
      *              THOSE FOUND IN F517 AND NOT FOUND IN F853 CASES  *         
      *                                                               *         
      *1. FILE DETAILS :                                              *         
      *    INPUT   :  BP13F517  ( OCCUPIER FILE  )                    *         
      *            :  BP13F853  ( MCPS OCCUPIER FILE )                *         
      *            :  BP13K800  ( MASTER FILE )                       *         
      *    OUTPUT  :  P13F853A  ( MCPS OCCUPIER FILE )                *         
      *                                                               *         
      * --------------------------------------------------------------*         
      * CHG-NO    CHG-DATE     BY   DESCRIPTION                       *         
      * ------    --------     --   -----------                       *         
      * BP132113  07/03/2002   AS1  NEW PROGRAM                       *         
      * BP132293  20/02/2003   CKK  TO WRITE DTE-BALLOT               *         
      * BP132313  28/04/2003   MJ16 INCLUDED SUBROUTINE AV02C013      *         
      *****************************************************************         
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
      *----------------------*                                                  
       CONFIGURATION SECTION.                                                   
      *----------------------*                                                  
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *----------------------*                                                  
       INPUT-OUTPUT SECTION.                                                    
      *----------------------*                                                  
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT    BP13F517   ASSIGN        TO   BP13F517.                    
                                                                                
           SELECT    BP13F853   ASSIGN        TO   BP13F853.                    
                                                                                
           SELECT    BP13K800   ASSIGN        TO   BP13K800                     
                                ORGANIZATION  IS   INDEXED                      
                                ACCESS MODE   IS   DYNAMIC                      
                                RECORD KEY    IS   K800-NUM-REGN                
                                FILE STATUS   IS   WS-K800-STATUS.              
                                                                                
           SELECT    P13F853A   ASSIGN        TO   P13F853A.                    
           SELECT    P13L031A   ASSIGN        TO   P13L031A.                    
           SELECT    P13L031B   ASSIGN        TO   P13L031B.                    
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
      *-------------*                                                           
       FILE SECTION.                                                            
      *-------------*                                                           
                                                                                
       FD  BP13F517                                                             
           RECORD    CONTAINS 250 CHARACTERS                                    
           BLOCK     CONTAINS   0 RECORDS                                       
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       COPY BP13F517.                                                           
                                                                                
       FD  BP13F853                                                             
           RECORD    CONTAINS 250 CHARACTERS                                    
           BLOCK     CONTAINS   0 RECORDS                                       
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       COPY BP13F853.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD    CONTAINS 2000 CHARACTERS.                                  
       COPY BP13K800.                                                           
                                                                                
       FD P13L031A    RECORD CONTAINS 132 CHARACTERS                            
                      RECORDING MODE IS F.                                      
       01 P13L031A-REC                   PIC X(132).                            
                                                                                
       FD P13L031B    RECORD CONTAINS 132 CHARACTERS                            
                      RECORDING MODE IS F.                                      
       01 P13L031B-REC                   PIC X(132).                            
                                                                                
       FD  P13F853A                                                             
           RECORD    CONTAINS 250 CHARACTERS                                    
           BLOCK     CONTAINS   0 RECORDS                                       
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       01  WS-P13F853A-REC.                                                     
           05 WS-F853A-NUM-REGN             PIC X(08).                          
           05 WS-F853A-NUM-NRIC             PIC X(09).                          
           05 FILLER                        PIC X(01).                          
           05 WS-F853A-NME-OCCP             PIC X(66).                          
           05 WS-F853A-NUM-NRIC-SPOUSE      PIC X(09).                          
           05 WS-F853A-NME-SPOUSE           PIC X(66).                          
           05 WS-F853A-NUN-NEW-TOWN         PIC X(03).                          
           05 WS-F853A-NUN-FLAT-TYPE        PIC X(02).                          
           05 WS-F853A-DTE-BALLOT           PIC X(06).                          
           05 WS-F853A-NUM-OCCP-MT          PIC X(01).                          
           05 WS-F853A-NUM-SPOUSE-MT        PIC X(01).                          
           05 FILLER                        PIC X(62).                          
           05 WS-F853A-DTE-UPDATE           PIC X(08).                          
           05 WS-F853A-NUM-USERID           PIC X(08).                          
                                                                                
      *-------------------------*                                               
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-ACCUMULATORS.                                                     
           03  WS-NO-READ-F517         PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-READ-F853         PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-READ-K800         PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-WRT-F853A         PIC 9(6)     VALUE ZEROES.               
                                                                                
           03  WS-ED-READ-F517         PIC Z,ZZ,ZZZ VALUE ZEROES.               
           03  WS-ED-READ-F853         PIC Z,ZZ,ZZZ VALUE ZEROES.               
           03  WS-ED-READ-K800         PIC Z,ZZ,ZZZ VALUE ZEROES.               
           03  WS-ED-WRT-F853A         PIC Z,ZZ,ZZZ VALUE ZEROES.               
                                                                                
       01  WS-STATUS.                                                           
           03  WS-K800-STATUS          PIC 9(2)     VALUE ZEROES.               
                                                                                
       01  WS-TEMP.                                                             
           05  WS-SYS-DATE             PIC X(08)    VALUE SPACES.               
           05  WS-DATE-RUN             PIC X(10)    VALUE SPACES.               
           05  WS-NUM-NRIC             PIC X(09)    VALUE SPACES.               
           05  WS-LINE-CTR1            PIC 9(6)     VALUE 60.                   
           05  WS-PAGE-CTR1            PIC 9(6)     VALUE ZEROS.                
           05  WS-SEQ-NO1              PIC 9(6)     VALUE 1.                    
           05  WS-LINE-CTR2            PIC 9(6)     VALUE 60.                   
           05  WS-PAGE-CTR2            PIC 9(6)     VALUE ZEROS.                
           05  WS-SEQ-NO2              PIC 9(6)     VALUE 1.                    
           05  WS-NUM-NRIC-TYPE        PIC X(3)     VALUE SPACES.               
           05  WS-NUM-CSTMR            PIC X(9)     VALUE SPACES.               
                                                                                
      ***************************************************************           
      *   AV02C013 SUBROUTINE VARIABLES                             *           
      ***************************************************************           
                                                                                
       01  WS-NUM-UINFIN            PIC  X(09).                                 
       01  WS-VALIDATION-FLAG       PIC  X(01).                                 
       01  WS-NUM-UFIN-TYPE         PIC  X(03).                                 
                                                                                
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01  L031A-PR-HEAD-01.                                                    
           05  FILLER              PIC X(8)      VALUE 'P13L031A'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L031A-DATE          PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L031A-PAGENO        PIC ZZZZ9 .                                  
                                                                                
       01  L031A-PR-HEAD-02.                                                    
           05  FILLER              PIC X(30)     VALUE SPACES .                 
           05  FILLER              PIC X(50)     VALUE                          
               'REPORT ON DATA FROM ROM - FOUND IN ROM'.                        
           05  FILLER              PIC X(30)     VALUE                          
               'SALES UNIT - APPLICATION TEAM '.                                
                                                                                
       01  L031A-PR-HEAD-04.                                                    
           05  FILLER              PIC X(42)     VALUE                          
               'SEQ.    REGN      APPL NRIC    APPL NAME  '.                    
           05  FILLER              PIC X(20)     VALUE SPACES .                 
           05  FILLER              PIC X(40)     VALUE                          
               ' NT    FLAT  SPOUSE NRIC    SPOUSE NAME '.                      
           05  FILLER              PIC X(20)     VALUE SPACES.                  
                                                                                
       01  L031A-PR-DTL-01.                                                     
           05  L031A-SEQ-NO         PIC ZZZZ.                                   
           05  FILLER               PIC X(02)     VALUE SPACES.                 
           05  L031A-NUM-REGN       PIC X(08).                                  
           05  FILLER               PIC X(04)     VALUE SPACES.                 
           05  L031A-NUM-NRIC1      PIC X(09).                                  
           05  FILLER               PIC X(04)     VALUE SPACES.                 
           05  L031A-NME-OCCP       PIC X(30).                                  
           05  FILLER               PIC X(02)     VALUE SPACES.                 
           05  L031A-NME-NT         PIC X(04).                                  
           05  FILLER               PIC X(02)     VALUE SPACES.                 
           05  L031A-NUM-FT         PIC X(04).                                  
           05  FILLER               PIC X(02)     VALUE SPACES.                 
           05  L031A-NUM-NRIC2      PIC X(11).                                  
           05  FILLER               PIC X(04)     VALUE SPACES.                 
           05  L031A-NME-SPOUSE     PIC X(30).                                  
                                                                                
       01  L031A-PR-LINE            PIC X(132)    VALUE ALL '-'.                
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01  L031B-PR-HEAD-01.                                                    
           05  FILLER              PIC X(8)      VALUE 'P13L031B'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L031B-DATE          PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L031B-PAGENO        PIC ZZZZ9 .                                  
                                                                                
       01  L031B-PR-HEAD-02.                                                    
           05  FILLER              PIC X(30)     VALUE SPACES .                 
           05  FILLER              PIC X(50)     VALUE                          
               'REPORT ON DATA FROM ROM - NOT FOUND IN ROM'.                    
           05  FILLER              PIC X(30)     VALUE                          
               'SALES UNIT - APPLICATION TEAM '.                                
                                                                                
       01  L031B-PR-HEAD-04.                                                    
           05  FILLER              PIC X(42)     VALUE                          
               'SEQ.    REGN      APPL NRIC    APPL NAME  '.                    
           05  FILLER              PIC X(20)     VALUE SPACES .                 
           05  FILLER              PIC X(40)     VALUE                          
               ' NT    FLAT  SPOUSE NRIC    SPOUSE NAME '.                      
           05  FILLER              PIC X(20)     VALUE SPACES.                  
                                                                                
       01  L031B-PR-DTL-01.                                                     
           05  L031B-SEQ-NO         PIC ZZZZ.                                   
           05  FILLER               PIC X(02)     VALUE SPACES.                 
           05  L031B-NUM-REGN       PIC X(08).                                  
           05  FILLER               PIC X(04)     VALUE SPACES.                 
           05  L031B-NUM-NRIC1      PIC X(09).                                  
           05  FILLER               PIC X(04)     VALUE SPACES.                 
           05  L031B-NME-OCCP       PIC X(30).                                  
           05  FILLER               PIC X(02)     VALUE SPACES.                 
           05  L031B-NME-NT         PIC X(04).                                  
           05  FILLER               PIC X(02)     VALUE SPACES.                 
           05  L031B-NUM-FT         PIC X(04).                                  
           05  FILLER               PIC X(02)     VALUE SPACES.                 
           05  L031B-NUM-NRIC2      PIC X(11).                                  
           05  FILLER               PIC X(04)     VALUE SPACES.                 
           05  L031B-NME-SPOUSE     PIC X(30).                                  
                                                                                
       01  L031B-PR-LINE            PIC X(132)    VALUE ALL '-'.                
                                                                                
      *COPY BE01W902.                                                           
                                                                                
           EXEC SQL  INCLUDE SQLCA      END-EXEC.                               
           EXEC SQL  INCLUDE P13PERSN   END-EXEC.                               
           EXEC SQL  INCLUDE P13IDNO    END-EXEC.                               
                                                                                
      *----------------------*                                                  
      *   ERROR REPORT LAYOUT                                                   
      *----------------------*                                                  
                                                                                
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *----------------*                                                        
       0000-MAIN-PROCESS.                                                       
      *----------------*                                                        
                                                                                
             PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                   
             PERFORM 2000-READ-BP13F517       THRU 2000-EXIT.                   
             PERFORM 3000-READ-BP13F853       THRU 3000-EXIT.                   
             PERFORM 4000-MAIN-PROCESS        THRU 4000-EXIT                    
               UNTIL F517-NUM-NRIC = HIGH-VALUES AND                            
                     F853-NUM-NRIC = HIGH-VALUES                                
             PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-ROUTINE.                                                       
      *----------------*                                                        
                                                                                
           OPEN       INPUT   BP13F517                                          
                              BP13F853                                          
                              BP13K800                                          
                      OUTPUT  P13F853A                                          
                              P13L031A                                          
                              P13L031B.                                         
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR IN BP13K800'                               
              DISPLAY 'WS-K800-STATUS  : '  WS-K800-STATUS                      
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE   FUNCTION CURRENT-DATE(1:8) TO   WS-SYS-DATE.                  
           MOVE   FUNCTION CURRENT-DATE(1:8) TO   WS-F853A-DTE-UPDATE.          
           STRING WS-SYS-DATE(7:2) '/'                                          
                  WS-SYS-DATE(5:2) '/'                                          
                  WS-SYS-DATE(1:4)                                              
                  DELIMITED BY SIZE          INTO WS-DATE-RUN.                  
           MOVE WS-DATE-RUN TO                    L031A-DATE.                   
           MOVE WS-DATE-RUN TO                    L031B-DATE.                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2000-READ-BP13F517.                                                      
      *-----------------*                                                       
                                                                                
           READ BP13F517 AT END  MOVE HIGH-VALUES TO F517-NUM-NRIC              
                         GO TO 2000-EXIT.                                       
                                                                                
           ADD 1 TO WS-NO-READ-F517.                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-READ-BP13F853.                                                      
      *-----------------*                                                       
                                                                                
           READ BP13F853 AT END  MOVE HIGH-VALUES TO F853-NUM-NRIC              
                         GO TO 3000-EXIT.                                       
                                                                                
           ADD 1 TO WS-NO-READ-F853.                                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       4000-MAIN-PROCESS.                                                       
      *-----------------*                                                       
                                                                                
           IF F853-NUM-NRIC = F517-NUM-NRIC                                     
              PERFORM    2000-READ-BP13F517    THRU 2000-EXIT                   
              PERFORM    3000-READ-BP13F853    THRU 3000-EXIT                   
           ELSE                                                                 
             IF F853-NUM-NRIC < F517-NUM-NRIC                                   
                PERFORM  3000-READ-BP13F853    THRU 3000-EXIT                   
             ELSE                                                               
               IF F853-NUM-NRIC > F517-NUM-NRIC                                 
                  MOVE     SPACES              TO   WS-P13F853A-REC             
                  PERFORM  6000-READ-BP13K800  THRU 6000-EXIT                   
                  PERFORM  7000-WRITE-P13F853A THRU 7000-EXIT                   
                  IF F517-NUM-NRIC-SPOUSE = SPACES                              
                     PERFORM  7600-PRINT-L031B THRU 7600-EXIT                   
                  ELSE                                                          
                     PERFORM  7500-PRINT-L031A THRU 7500-EXIT                   
                  END-IF                                                        
                  PERFORM  2000-READ-BP13F517  THRU 2000-EXIT                   
               END-IF                                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       6000-READ-BP13K800.                                                      
      *-----------------*                                                       
                                                                                
            MOVE F517-NUM-REGN       TO    K800-NUM-REGN.                       
            MOVE F517-NUM-REGN       TO    WS-F853A-NUM-REGN.                   
                                                                                
            READ BP13K800.                                                      
            EVALUATE WS-K800-STATUS                                             
                WHEN 00                                                         
                     ADD   1                  TO WS-NO-READ-K800                
                     MOVE  K800-NUM-NEW-TOWN  TO WS-F853A-NUN-NEW-TOWN          
                     MOVE  K800-NUM-FLAT-TYPE TO WS-F853A-NUN-FLAT-TYPE         
                     MOVE  K800-DTE-BALLOT    TO WS-F853A-DTE-BALLOT            
                WHEN 23                                                         
                     DISPLAY 'RECORD NOT FOUND IN K800' ' '                     
                             'K800-NUM-REGN :' K800-NUM-REGN                    
                     DISPLAY 'WS-K800-STATUS : ' WS-K800-STATUS                 
                WHEN OTHER                                                      
                     DISPLAY 'ERROR IN READING FILE K800  '  ' '                
                             'K800-NUM-REGN :' K800-NUM-REGN                    
                     DISPLAY 'WS-K800-STATUS : ' WS-K800-STATUS                 
                     PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                  
            END-EVALUATE.                                                       
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------*                                                     
       7000-WRITE-P13F853A.                                                     
      *-------------------*                                                     
                                                                                
            PERFORM 7100-GET-INFO-CDB  THRU  7100-EXIT                          
                                                                                
            IF SQLCODE = 000                                                    
               IF F517-NUM-NRIC-SPOUSE NOT = SPACES                             
                  MOVE F517-NME        TO    WS-F853A-NME-OCCP                  
                  CONTINUE                                                      
               ELSE                                                             
                   MOVE NME-PERSON OF DCLPERSONS  TO WS-F853A-NME-OCCP          
               END-IF                                                           
              MOVE NUM-MRTL-STATUS OF DCLPERSONS TO WS-F853A-NUM-OCCP-MT        
            END-IF                                                              
                                                                                
            MOVE F517-NUM-NRIC         TO    WS-F853A-NUM-NRIC.                 
            MOVE F517-NME-SPOUSE       TO    WS-F853A-NME-SPOUSE.               
            MOVE F517-NUM-NRIC-SPOUSE  TO    WS-F853A-NUM-NRIC-SPOUSE.          
            MOVE 'BP13C031'            TO    WS-F853A-NUM-USERID.               
                                                                                
            WRITE WS-P13F853A-REC.                                              
            ADD     1   TO WS-NO-WRT-F853A.                                     
                                                                                
            IF F517-NUM-NRIC-SPOUSE NOT = SPACES                                
             PERFORM  6000-READ-BP13K800  THRU 6000-EXIT                        
             MOVE F517-NUM-NRIC-SPOUSE  TO    WS-F853A-NUM-NRIC                 
             MOVE F517-NME-SPOUSE       TO    WS-F853A-NME-OCCP                 
             MOVE F517-NME              TO    WS-F853A-NME-SPOUSE               
             MOVE F517-NUM-NRIC         TO    WS-F853A-NUM-NRIC-SPOUSE          
             MOVE 'BP13C031'            TO    WS-F853A-NUM-USERID               
                                                                                
             WRITE WS-P13F853A-REC                                              
             ADD     1   TO WS-NO-WRT-F853A                                     
            END-IF.                                                             
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       7100-GET-INFO-CDB.                                                       
      *-----------------*                                                       
                                                                                
      *    MOVE   SPACES              TO    BE01W902-COMMAREA.                  
      *    INITIALIZE BE01W902-COMMAREA.                                        
                                                                                
      *    MOVE F517-NUM-NRIC         TO   BE01W902-IN-NUM-IDNTY-NO             
      *                                    WS-NUM-NRIC                          
                                                                                
           MOVE SPACES TO WS-NUM-UINFIN.                                        
           MOVE SPACES TO WS-VALIDATION-FLAG.                                   
           MOVE SPACES TO WS-NUM-UFIN-TYPE.                                     
                                                                                
           MOVE F517-NUM-NRIC         TO   WS-NUM-UINFIN                        
                                           WS-NUM-NRIC.                         
           PERFORM 7200-CONVERT-IDNTY-TYPE THRU 7200-EXIT.                      
                                                                                
            EXEC SQL                                                            
                 SELECT NUM_CSTMR                                               
                  INTO :DCLIDNTY-NO.NUM-CSTMR                                   
                  FROM  IDNTY_NO                                                
                  WHERE NUM_IDNTY_NO = :WS-NUM-NRIC                             
                    AND NUM_IDNTY_NO_TYPE = :WS-NUM-NRIC-TYPE                   
                                                                                
            END-EXEC.                                                           
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 000                                                         
                    PERFORM  7300-RETRIEVE-PERSON-TBL THRU 7300-EXIT            
               WHEN 100                                                         
                    DISPLAY  "REC. NOT FOUND IN IDNTY_NO TABLE  : "             
                              F517-NUM-NRIC                                     
               WHEN OTHER                                                       
                    DISPLAY 'SQL ERROR IN IDNTY_NO TABLE '                      
                    DISPLAY 'F517-NUM-NRIC   :  '  F517-NUM-NRIC                
                    DISPLAY 'SQL ERROR CODE  :  '  SQLCODE                      
                    DISPLAY SPACES                                              
                                                                                
                    PERFORM 9000-SQL-ERROR  THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       7100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------*                                                 
       7200-CONVERT-IDNTY-TYPE.                                                 
      *-----------------------*                                                 
                                                                                
      *     CALL 'BE01C902' USING BE01W902-COMMAREA                             
                                                                                
            IF WS-NUM-UINFIN NOT = SPACES AND LOW-VALUES                        
               CALL 'AV02C013' USING WS-NUM-UINFIN  WS-VALIDATION-FLAG          
                                 WS-NUM-UFIN-TYPE                               
                                                                                
               PERFORM  7250-EVALUATE-UFIN-TYPE  THRU 7250-EXIT                 
            ELSE                                                                
               DISPLAY 'INVALID NUM-NRIC'                                       
            END-IF.                                                             
                                                                                
      *     IF BE01W902-OUT-RTN-MSGNO = SPACES                                  
      *        MOVE BE01W902-OUT-NUM-IDNTY-NO-TYPE  TO WS-NUM-NRIC-TYPE         
      *     ELSE                                                                
      *      IF BE01W902-OUT-RTN-MSGNO  = '0127'                                
      *        MOVE        'XIN'                    TO WS-NUM-NRIC-TYPE         
      *      ELSE                                                               
      *        DISPLAY ' ERROR IN BE01C902' ' ' 'NRIC : ' F517-NUM-NRIC         
      *        DISPLAY 'BE01W902-OUT-RTN-MSGNO'                                 
      *      END-IF                                                             
      *     END-IF.                                                             
                                                                                
       7200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------*                                                
       7250-EVALUATE-UFIN-TYPE.                                                 
      *------------------------*                                                
                                                                                
           EVALUATE WS-NUM-UFIN-TYPE                                            
            WHEN 'UIN'                                                          
      *        MOVE WS-NUM-UFIN-TYPE TO BE01W902-OUT-NUM-IDNTY-NO-TYPE          
               MOVE WS-NUM-UFIN-TYPE TO WS-NUM-NRIC-TYPE                        
            WHEN 'FIN'                                                          
      *        MOVE WS-NUM-UFIN-TYPE TO BE01W902-OUT-NUM-IDNTY-NO-TYPE          
               MOVE WS-NUM-UFIN-TYPE TO WS-NUM-NRIC-TYPE                        
            WHEN OTHER                                                          
      *        MOVE WS-NUM-UFIN-TYPE TO BE01W902-OUT-NUM-IDNTY-NO-TYPE          
               MOVE 'XIN'            TO WS-NUM-NRIC-TYPE                        
      *        MOVE '0127'           TO BE01W902-OUT-RTN-MSGNO                  
      *        MOVE 'AV02C013'       TO BE01W902-OUT-RTN-PGMID                  
           END-EVALUATE.                                                        
                                                                                
                                                                                
       7250-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------*                                                
       7300-RETRIEVE-PERSON-TBL.                                                
      *------------------------*                                                
                                                                                
            MOVE    NUM-CSTMR OF DCLIDNTY-NO TO   WS-NUM-CSTMR                  
                                                                                
            PERFORM 7400-READ-PERSON-TABLE   THRU 7400-EXIT                     
                                                                                
            EVALUATE SQLCODE                                                    
                WHEN 000                                                        
                     CONTINUE                                                   
                WHEN 100                                                        
                     DISPLAY 'REC NOT IN PERSONS TABLE FOR '                    
                            ' ' F517-NUM-NRIC                                   
                WHEN OTHER                                                      
                     DISPLAY 'SQL ERROR IN PERSON   TABLE '                     
                     DISPLAY 'F517-NUM-NRIC   :  '  F517-NUM-NRIC               
                     DISPLAY 'SQL ERROR CODE  :  '  SQLCODE                     
                     DISPLAY SPACES                                             
                                                                                
                     PERFORM  9000-SQL-ERROR  THRU  9000-EXIT                   
            END-EVALUATE.                                                       
                                                                                
       7300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------*                                                   
       7400-READ-PERSON-TABLE.                                                  
      *---------------------*                                                   
                                                                                
            EXEC SQL                                                            
                 SELECT   NME_PERSON ,                                          
                          NUM_CSTMR  ,                                          
                          NUM_MRTL_STATUS                                       
                   INTO  :DCLPERSONS.NME-PERSON ,                               
                         :DCLPERSONS.NUM-CSTMR ,                                
                         :DCLPERSONS.NUM-MRTL-STATUS                            
                   FROM   PERSONS                                               
                   WHERE  NUM_CSTMR = :WS-NUM-CSTMR                             
                                                                                
            END-EXEC.                                                           
                                                                                
       7400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------*                                                         
       7500-PRINT-L031A.                                                        
      *---------------*                                                         
                                                                                
           MOVE WS-SEQ-NO1               TO L031A-SEQ-NO                        
           MOVE F517-NUM-NRIC            TO L031A-NUM-NRIC1.                    
           MOVE F517-NME                 TO L031A-NME-OCCP.                     
           MOVE F517-NUM-NRIC-SPOUSE     TO L031A-NUM-NRIC2.                    
           MOVE F517-NME-SPOUSE          TO L031A-NME-SPOUSE.                   
           MOVE K800-NUM-NEW-TOWN        TO L031A-NME-NT.                       
           MOVE K800-NUM-FLAT-TYPE       TO L031A-NUM-FT.                       
           MOVE F517-NUM-REGN            TO L031A-NUM-REGN                      
                                                                                
           ADD  1                        TO WS-SEQ-NO1                          
           IF WS-LINE-CTR1 > 55                                                 
              ADD  1              TO   WS-PAGE-CTR1                             
              MOVE 5              TO   WS-LINE-CTR1                             
              MOVE WS-PAGE-CTR1   TO   L031A-PAGENO                             
              WRITE P13L031A-REC  FROM L031A-PR-HEAD-01 AFTER PAGE              
              WRITE P13L031A-REC  FROM L031A-PR-HEAD-02                         
              WRITE P13L031A-REC  FROM L031A-PR-LINE    AFTER 2                 
              WRITE P13L031A-REC  FROM L031A-PR-HEAD-04                         
              WRITE P13L031A-REC  FROM L031A-PR-LINE                            
           END-IF.                                                              
                                                                                
           WRITE P13L031A-REC  FROM L031A-PR-DTL-01.                            
           ADD   1             TO   WS-LINE-CTR1.                               
                                                                                
       7500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       7600-PRINT-L031B.                                                        
      *---------------*                                                         
                                                                                
           MOVE WS-SEQ-NO2               TO L031B-SEQ-NO                        
           MOVE F517-NUM-NRIC            TO L031B-NUM-NRIC1.                    
           MOVE F517-NME                 TO L031B-NME-OCCP.                     
           MOVE F517-NUM-NRIC-SPOUSE     TO L031B-NUM-NRIC2.                    
           MOVE F517-NME-SPOUSE          TO L031B-NME-SPOUSE.                   
           MOVE K800-NUM-NEW-TOWN        TO L031B-NME-NT.                       
           MOVE K800-NUM-FLAT-TYPE       TO L031B-NUM-FT.                       
           MOVE F517-NUM-REGN            TO L031B-NUM-REGN                      
                                                                                
           ADD  1                        TO WS-SEQ-NO2                          
           IF WS-LINE-CTR2 > 55                                                 
              ADD  1              TO   WS-PAGE-CTR2                             
              MOVE 5              TO   WS-LINE-CTR2                             
              MOVE WS-PAGE-CTR2   TO   L031B-PAGENO                             
              WRITE P13L031B-REC  FROM L031B-PR-HEAD-01 AFTER PAGE              
              WRITE P13L031B-REC  FROM L031B-PR-HEAD-02                         
              WRITE P13L031B-REC  FROM L031B-PR-LINE    AFTER 2                 
              WRITE P13L031B-REC  FROM L031B-PR-HEAD-04                         
              WRITE P13L031B-REC  FROM L031B-PR-LINE                            
           END-IF.                                                              
                                                                                
           WRITE P13L031B-REC  FROM L031B-PR-DTL-01.                            
           ADD   1             TO   WS-LINE-CTR2.                               
                                                                                
       7600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *   ABORT PROCESSING DUE TO SQL ERRORS                          *         
      *****************************************************************         
      *--------------*                                                          
       9000-SQL-ERROR.                                                          
      *--------------*                                                          
                                                                                
           DISPLAY ' *****   BP13C008 ABEND   *****'.                           
                                                                                
           CALL 'DBATIAR'  USING SQLCA.                                         
           EXEC SQL   ROLLBACK   END-EXEC.                                      
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ROLLBACK FAILED, SQLCODE IS : ' SQLCODE            
           END-EVALUATE.                                                        
                                                                                
           MOVE 12                        TO   RETURN-CODE.                     
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       9999-CLOSE-ROUTINE.                                                      
      *------------------*                                                      
                                                                                
           MOVE   WS-NO-READ-F517     TO WS-ED-READ-F517.                       
           MOVE   WS-NO-READ-F853     TO WS-ED-READ-F853.                       
           MOVE   WS-NO-READ-K800     TO WS-ED-READ-K800.                       
           MOVE   WS-NO-WRT-F853A     TO WS-ED-WRT-F853A.                       
                                                                                
           DISPLAY  SPACES.                                                     
                                                                                
           DISPLAY  '**************************************************'        
           DISPLAY  'DATE : ' WS-DATE-RUN '       ' 'BP13C031'.                 
           DISPLAY  '**************************************************'        
           DISPLAY  SPACES.                                                     
                                                                                
           DISPLAY  ' 1. NO. OF REC. READ  FROM BP13F517            : '         
                     WS-ED-READ-F517.                                           
           DISPLAY  ' 2. NO. OF REC. READ  FROM BP13F853            : '         
                     WS-ED-READ-F853.                                           
           DISPLAY  ' 3. NO. OF REC. READ  FROM BP13K800            : '         
                     WS-ED-READ-K800.                                           
           DISPLAY  ' 4. NO. OF REC. WRIITEN INTO P13F853A          : '         
                     WS-ED-WRT-F853A.                                           
                                                                                
           CLOSE BP13F517                                                       
                 BP13F853                                                       
                 BP13K800                                                       
                 P13F853A                                                       
                 P13L031A                                                       
                 P13L031B.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
