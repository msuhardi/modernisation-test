       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C168.                                                  
      *AUTHOR.       LSB.                                                       
      *DATE-WRITTEN. 17/08/1998.                                                
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *                                                        *                
      *      1.  PROGRAM TO MATCH BP13F520 WITH BP13F558 BY    *                
      *          REGISTRATION NO. OUTPUT 2 FILES               *                
      *          P13F558A - FOUND AS NEW HOUSEHOLD             *                
      *          P13F558B - FOUND AS UPGRADER                  *                
      *                     FIRST TIME WITH PARENT/CHILD       *                
      *                                                        *                
      * NOTE : USE IN BP13A534 JCL                             *                
      *                                                        *                
      * ------------------------------------------------------ *                
      *  CHG ID  OIC   DATE   DESCRIPTION                      *                
      *  ------  ---   ----   -----------                      *                
      *BP130542  LSB  170898  NEW PROGRAM                      *                
      *BP130573  LSB  121198  OUTPUT PARENT/CHILD TO P13F558C  *                
      *BP130652  NOR  200299  EXPAND F520 RECL FROM 300 TO 500 *                
      *BP130897  AKT  310700  EXPAND F558 RECL FROM 400 TO 600 *                
      * ====================================================== *                
           SKIP1                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F558   ASSIGN TO BP13F558.                                
           SELECT BP13F520   ASSIGN TO BP13F520.                                
                                                                                
           SELECT P13F558A   ASSIGN TO P13F558A.                                
           SELECT P13F558B   ASSIGN TO P13F558B.                                
           SELECT P13F558C   ASSIGN TO P13F558C.                                
                                                                                
           EJECT                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F558                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F558.                                                           
                                                                                
       FD   BP13F520                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F520.                                                           
                                                                                
       FD   P13F558A                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01   P13F558A-REC.                                                       
           05 FILLER             PIC X(600).                                    
                                                                                
       FD   P13F558B                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01   P13F558B-REC.                                                       
           05 FILLER             PIC X(600).                                    
                                                                                
       FD   P13F558C                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01   P13F558C-REC.                                                       
           05 FILLER             PIC X(600).                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F520-READ            PIC 9(8)    VALUE 0.                         
       01  WS-F558-READ            PIC 9(8)    VALUE 0.                         
       01  WS-OUTPUT-NHH-CNT       PIC 9(8)    VALUE 0.                         
       01  WS-OUTPUT-UPG-CNT       PIC 9(8)    VALUE 0.                         
       01  WS-OUTPUT-PAT-CNT       PIC 9(8)    VALUE 0.                         
       01  WS-NOTFND-F520-CNT      PIC 9(8)    VALUE 0.                         
       01  WS-NOTFND-F558-CNT      PIC 9(8)    VALUE 0.                         
       01  WS-MATCH-CNT            PIC 9(8)    VALUE 0.                         
       01  WS-REC-CNT              PIC 9(8)    VALUE 0.                         
       01  WS-F558-REGN            PIC X(8)    VALUE SPACES.                    
                                                                                
       01  WS-DATE.                                                             
           05  WS-CC               PIC 99.                                      
           05  WS-YY               PIC 99.                                      
           05  WS-MM               PIC 99.                                      
           05  WS-DD               PIC 99.                                      
                                                                                
                                                                                
       01  WS-F520-EOF             PIC X(1).                                    
           88  F520-EOF                          VALUE 'Y'.                     
       01  WS-F558-EOF             PIC X(1).                                    
           88  F558-EOF                          VALUE 'Y'.                     
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
      *================================================================*        
       000-MAIN-ROUTINE.                                                        
      *================================================================*        
           OPEN INPUT  BP13F520                                                 
                       BP13F558                                                 
                OUTPUT P13F558A                                                 
                       P13F558B                                                 
                       P13F558C.                                                
                                                                                
           ACCEPT WS-DATE FROM DATE YYYYMMDD.                                   
                                                                                
           PERFORM 100-READ-F520-REC THRU 100-EXIT.                             
           PERFORM 110-READ-F558-REC THRU 110-EXIT.                             
           PERFORM 200-PROCESS-MATCH THRU 200-EXIT                              
                   UNTIL F520-EOF AND F558-EOF.                                 
           PERFORM 900-CLOSE-ROUTINE.                                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       100-READ-F520-REC.                                                       
      *================================================================*        
           READ BP13F520 AT END                                                 
                MOVE HIGH-VALUE TO F520-NUM-REGN                                
                MOVE 'Y'        TO WS-F520-EOF                                  
                GO TO 100-EXIT.                                                 
           ADD 1 TO WS-F520-READ.                                               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       110-READ-F558-REC.                                                       
      *================================================================*        
           READ BP13F558 AT END                                                 
                MOVE HIGH-VALUE TO F558-NUM-REGN                                
                MOVE 'Y'        TO WS-F558-EOF                                  
                GO TO 110-EXIT.                                                 
           ADD 1 TO WS-F558-READ.                                               
                                                                                
       110-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       200-PROCESS-MATCH.                                                       
      *================================================================*        
           IF F520-NUM-REGN = F558-NUM-REGN                                     
              ADD 1                       TO WS-MATCH-CNT                       
              PERFORM 250-PROCESS-MATCHED THRU 250-EXIT                         
                     UNTIL F558-NUM-REGN NOT = F520-NUM-REGN                    
                     OR F558-EOF                                                
              PERFORM 100-READ-F520-REC   THRU 100-EXIT                         
           ELSE                                                                 
              IF F520-NUM-REGN < F558-NUM-REGN                                  
                 ADD 1              TO WS-NOTFND-F520-CNT                       
                 PERFORM 100-READ-F520-REC THRU 100-EXIT                        
              ELSE                                                              
                 MOVE F558-NUM-REGN TO WS-F558-REGN                             
                 ADD 1 TO WS-NOTFND-F558-CNT                                    
                 PERFORM 250-PROCESS-MATCHED THRU 250-EXIT                      
                     UNTIL F558-NUM-REGN NOT = WS-F558-REGN                     
                     OR F558-EOF.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       250-PROCESS-MATCHED.                                                     
      *================================================================*        
                                                                                
           IF F520-CDE-HOUSEHOLD = 'H'                                          
              IF F520-CDE-PARENT-CHILD = 'P'                                    
                  WRITE P13F558C-REC FROM BP13F558-REC                          
                  ADD 1              TO WS-OUTPUT-PAT-CNT                       
              ELSE                                                              
                  WRITE P13F558A-REC FROM BP13F558-REC                          
                  ADD 1              TO WS-OUTPUT-NHH-CNT                       
           ELSE                                                                 
              WRITE P13F558B-REC FROM BP13F558-REC                              
              ADD 1              TO WS-OUTPUT-UPG-CNT.                          
                                                                                
           PERFORM 110-READ-F558-REC   THRU 110-EXIT.                           
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-UNMATCH-BP13F558.                                                    
                                                                                
            WRITE P13F558B-REC FROM BP13F558-REC                                
            ADD 1              TO WS-OUTPUT-UPG-CNT.                            
                                                                                
            PERFORM 110-READ-F558-REC   THRU 110-EXIT.                          
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       900-CLOSE-ROUTINE.                                                       
      *================================================================*        
           DISPLAY 'BP13C168                 DATE : ' WS-DATE.                  
           DISPLAY 'UPDATE  HOUSEHOLD TYPE TO REQUEST FILE'.                    
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM F520 :' WS-F520-READ.               
           DISPLAY 'NO OF RECORDS READ FROM F558 :' WS-F558-READ.               
           DISPLAY 'NO OF REORDS  WRITE OUT -NHH :' WS-OUTPUT-NHH-CNT.          
           DISPLAY 'NO OF REORDS  WRITE OUT -UPG :' WS-OUTPUT-UPG-CNT.          
           DISPLAY 'NO OF REORDS  WRITE OUT -PAT :' WS-OUTPUT-PAT-CNT.          
           DISPLAY 'NO OF RECORDS MATCH          :' WS-MATCH-CNT.               
           DISPLAY 'NO OF RECORDS NOT MATCH-F520 :' WS-NOTFND-F520-CNT.         
           DISPLAY 'NO OF RECORDS NOT MATCH-F558 :' WS-NOTFND-F558-CNT.         
                                                                                
           CLOSE BP13F558                                                       
                 BP13F520                                                       
                 P13F558A                                                       
                 P13F558B                                                       
                 P13F558C.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
