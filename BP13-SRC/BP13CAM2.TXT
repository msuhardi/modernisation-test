       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CAM2.                                                 
       AUTHOR.        JGO1.                                                     
       DATE-WRITTEN.  23 MAY 2013.                                              
      ************************************************************              
      *   SYSTEM NAME : SOC - AMR REPORT                         *              
      *                                                          *              
      *   OBJECTIVE:                                             *              
      *      CREATE REPORT CONTAINING AMR CASES WITH KEY         *              
      *           COLLECTED                                      *              
      *                                                          *              
      * CHG-NO  BY   ON       DESCRIPTION                        *              
      *-------  ---  ------   ----------                         *              
      *BP135158 JGO1 29/11/13 INITIAL CODING                     *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06K115  ASSIGN       TO BM06K115.                           
                                                                                
           SELECT BP13K893  ASSIGN       TO BP13K893                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K893-KEY-FLD                        
                            FILE STATUS  IS BP13K893-STATUS.                    
                                                                                
           SELECT BB24K510  ASSIGN       TO BB24K510                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K510-KEY-FLD                        
                            FILE STATUS  IS BB24K510-STATUS.                    
                                                                                
           SELECT BB24K500  ASSIGN       TO BB24K500                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K500-KEY-FLD                        
                            FILE STATUS  IS BB24K500-STATUS.                    
                                                                                
           SELECT BP13K350  ASSIGN       TO BP13K350                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K350-KEY-FLD                        
                            FILE STATUS  IS BP13K350-STATUS.                    
                                                                                
           SELECT BP13K352  ASSIGN       TO BP13K352                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K352-KEY-FLD                        
                            FILE STATUS  IS BP13K352-STATUS.                    
                                                                                
           SELECT BP13R24A  ASSIGN       TO BP13R24A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BM06K115                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K115.                                                           
                                                                                
       FD   BB24K510                                                            
            RECORD CONTAINS 50 CHARACTERS.                                      
       COPY BB24K510.                                                           
                                                                                
       FD   BB24K500                                                            
            RECORD CONTAINS 1700 CHARACTERS.                                    
       COPY BB24K500.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BP13K350                                                            
            RECORD CONTAINS 600 CHARACTERS.                                     
       COPY BP13K350.                                                           
                                                                                
       FD   BP13K352                                                            
            RECORD CONTAINS 600 CHARACTERS.                                     
       COPY BP13K352.                                                           
                                                                                
       FD   BP13R24A                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   R24A-PRINT-REC                    PIC X(200).                       
                                                                                
      *-------------------------                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------                                                
       01  BB24K510-STATUS              PIC 99     VALUE 00.                    
       01  BB24K500-STATUS              PIC 99     VALUE 00.                    
       01  BP13K893-STATUS              PIC 99     VALUE 00.                    
       01  BP13K350-STATUS              PIC 99     VALUE 00.                    
       01  BP13K352-STATUS              PIC 99     VALUE 00.                    
       01  WS-FLAGS.                                                            
           05 WS-K115-EOF               PIC X      VALUE 'N'.                   
           05 WS-K510-EOF               PIC X      VALUE 'N'.                   
           05 WS-K500-EOF               PIC X      VALUE 'N'.                   
           05 WS-K893-EOF               PIC X      VALUE 'N'.                   
           05 WS-K893-FND               PIC X      VALUE 'N'.                   
           05 WS-WRITE-FLAG             PIC X      VALUE 'N'.                   
           05 WS-ADV-COLLECTED          PIC X      VALUE 'N'.                   
       01  WS-COUNTERS.                                                         
           05 WS-CNT-K115-RD            PIC 9(4)   VALUE ZEROES.                
           05 WS-CNT-WITHADV            PIC 9(4)   VALUE ZEROES.                
           05 WS-CNT-WOADV              PIC 9(4)   VALUE ZEROES.                
           05 WS-CNT-24A-WRITE          PIC 9(4)   VALUE ZEROES.                
           05 WS-CNT-24A-DTL            PIC 9(4)   VALUE ZEROES.                
           05 WS-CNT-24A-PAGE           PIC 9(4)   VALUE 1.                     
       01  WS-K502-KEY.                                                         
           05 WS-HDB-REF                PIC X(11)  VALUE SPACES.                
           05 WS-DTE-APPROVAL           PIC X(08)  VALUE SPACES.                
       01  WS-FMT-SCH-ACC.                                                      
           05 WS-FMT-NUM-SCH            PIC X(04)  VALUE SPACES.                
           05 FILLER                    PIC X      VALUE '-'.                   
           05 WS-FMT-NUM-ACC            PIC X(04)  VALUE SPACES.                
           05 FILLER                    PIC X      VALUE '-'.                   
           05 WS-FMT-NUM-ACC-CK         PIC X(01)  VALUE SPACES.                
           05 FILLER                    PIC X      VALUE '-'.                   
           05 WS-FMT-NUM-LESSEE         PIC X(02)  VALUE SPACES.                
       01  WS-VARIABLES.                                                        
           05  WS-NRIC                  PIC X(09)  VALUE SPACES.                
           05  WS-HDBREF                PIC X(11)  VALUE SPACES.                
           05  WS-PR-CUR-DATE.                                                  
               10 WS-PC-DD             PIC 99        VALUE 00.                  
               10 FILLER               PIC X         VALUE '/'.                 
               10 WS-PC-MM             PIC 99        VALUE 00.                  
               10 FILLER               PIC X         VALUE '/'.                 
               10 WS-PC-CCYY           PIC 9999      VALUE ZERO.                
           05  WS-CURRENT-CCYYMMDD.                                             
                   15 WS-CUR-CCYY      PIC 9999      VALUE ZERO.                
                   15 WS-CUR-MM        PIC 99        VALUE 00.                  
                   15 WS-CUR-DD        PIC 99        VALUE 00.                  
           05  WS-F140-HSEMAIN.                                                 
               10  WS-F140-HSE-MAIN       PIC X(4).                             
               10  WS-F140-HSE-SUB.                                             
                   15  WS-F140-HSE-SUB1   PIC X.                                
                   15  WS-F140-HSE-SUB2   PIC XX.                               
                                                                                
                                                                                
       01  SQL-CODE                    PIC 9(4).                                
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13C913                        *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH     PIC X(4).                                
               10  WS-LINK-NUM-ACC     PIC X(5).                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *----------------------------------------------------------*              
      *        WS FOR REPORT LAYOUT VARIABLES                    *              
      *----------------------------------------------------------*              
       01  RA24-PR-HDG-01.                                                      
           05  FILLER              PIC X(8)    VALUE 'BP13RAM2'.                
           05  FILLER              PIC X(10)   VALUE '      HDB3'.              
           05  FILLER              PIC X(58)   VALUE SPACES.                    
           05  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(12)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'DATE : '.                 
           05  RA24-CURRENT-DATE   PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(43)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'PAGE : '.                 
           05  RA24-PAGE           PIC ZZZ9.                                    
                                                                                
       01  RA24-PR-HDG-02.                                                      
           05  FILLER              PIC X(30)   VALUE                            
               'HOUSING AND DEVELOPMENT BOARD'.                                 
           05  FILLER              PIC X(40)   VALUE SPACES.                    
           05  FILLER              PIC X(55)   VALUE                            
               '      KEY ISSUED AMR CASES WITH NPL FLAT SOLD     '.            
           05  FILLER              PIC X(70)   VALUE SPACES.                    
                                                                                
       01  RA24-PR-HDG-03A.                                                     
           05  FILLER              PIC X(80)   VALUE SPACES.                    
           05  FILLER              PIC X(30)   VALUE                            
               '                            '.                                  
           05  FILLER              PIC X(90)   VALUE SPACES.                    
                                                                                
       01  RA24-PR-HDG-DASH.                                                    
           05  FILLER              PIC X(200)  VALUE ALL '-'.                   
                                                                                
       01  RA24-PR-HDG-SPACE.                                                   
           05  FILLER              PIC X(200)  VALUE SPACES.                    
                                                                                
                                                                                
       01  RA24-PR-HDG-04.                                                      
           05  FILLER              PIC X(04)   VALUE 'S/N.'.                    
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(08)   VALUE 'REGN NO.'.                
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(14)   VALUE ' NPL SCH A/C '.           
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(14)   VALUE '   SCH A/C   '.           
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(05)   VALUE 'BLK  '.                   
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(32)   VALUE 'STREET'.                  
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(08)   VALUE '  UNIT  '.                
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(11)   VALUE 'ACCEPT DATE'.             
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(12)   VALUE 'KEY AVAIL DT'.            
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(13)   VALUE 'KEY ISSUE DT '.           
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(13)   VALUE 'FIRST APPT DT'.           
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(03)   VALUE SPACES.                    
           05  FILLER              PIC X(10)   VALUE '     CASH'.               
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(03)   VALUE SPACES.                    
           05  FILLER              PIC X(10)   VALUE 'CASH USED'.               
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(12)   VALUE 'CASH REFUND'.             
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
                                                                                
       01  RA24-PR-DTL-01.                                                      
           05  DTL-SER-NUM         PIC ZZZ9.                                    
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  DTL-NUM-REGN        PIC X(08).                                   
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  DTL-NPL-SCH-ACC     PIC X(14).                                   
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  DTL-SCH-ACC         PIC X(14).                                   
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  DTL-BLK             PIC X(05).                                   
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  DTL-STREET          PIC X(32).                                   
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  DTL-UNIT            PIC X(08).                                   
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  DTL-ACCEPT-DTE      PIC X(10).                                   
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(04)   VALUE SPACES.                    
           05  DTL-KEY-AVAIL-DTE   PIC X(10).                                   
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(04)   VALUE SPACES.                    
           05  DTL-ISSUE-DTE       PIC X(10).                                   
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(04)   VALUE SPACES.                    
           05  DTL-FIRST-APPT-DTE  PIC X(10).                                   
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  DTL-CASH-COLL       PIC Z,ZZZ,ZZ9.99.                            
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  DTL-CASH-USED       PIC Z,ZZZ,ZZ9.99.                            
           05  FILLER              PIC X(01)   VALUE ';'.                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  DTL-CASH-REFUND     PIC Z,ZZZ,ZZ9.99.                            
           05  FILLER              PIC X(01)   VALUE ';'.                       
                                                                                
                                                                                
      *-------------------------                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------                                                
       0000-MAIN.                                                               
                                                                                
            PERFORM 0500-INITIALIZE     THRU 0500-EXIT.                         
            PERFORM 1000-PROCESS        THRU 1000-EXIT                          
                    UNTIL WS-K115-EOF = 'Y'.                                    
            PERFORM 9999-CLOSING        THRU 9999-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       0500-INITIALIZE.                                                         
      *-------------------------                                                
                                                                                
            OPEN INPUT BP13K893                                                 
                       BP13K350                                                 
                       BP13K352                                                 
                       BM06K115                                                 
                OUTPUT BP13R24A.                                                
                                                                                
           IF BP13K893-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K893-STATUS ' BP13K893-STATUS         
              MOVE     BP13K893-STATUS TO RETURN-CODE                           
              PERFORM 9999-CLOSING        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K350-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K350-STATUS ' BP13K350-STATUS         
              MOVE     BP13K350-STATUS TO RETURN-CODE                           
              PERFORM 9999-CLOSING        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K352-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K352-STATUS ' BP13K352-STATUS         
              MOVE     BP13K352-STATUS TO RETURN-CODE                           
              PERFORM 9999-CLOSING        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE   FUNCTION CURRENT-DATE(1:8)                                    
                                    TO    WS-CURRENT-CCYYMMDD.                  
           MOVE   WS-CUR-DD         TO    WS-PC-DD.                             
           MOVE   WS-CUR-MM         TO    WS-PC-MM.                             
           MOVE   WS-CUR-CCYY       TO    WS-PC-CCYY.                           
           MOVE   WS-PR-CUR-DATE    TO    RA24-CURRENT-DATE.                    
                                                                                
           PERFORM 3000-WRITE-HEADER    THRU 3000-EXIT.                         
                                                                                
       0500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       1000-PROCESS.                                                            
      *-------------------------                                                
                                                                                
            MOVE 'N'                TO WS-ADV-COLLECTED.                        
            PERFORM 1500-READ-K115  THRU 1500-EXIT.                             
            PERFORM 2000-CHECK-REC  THRU 2000-EXIT.                             
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       1500-READ-K115.                                                          
      *-------------------------                                                
                                                                                
            READ BM06K115                                                       
                 AT END MOVE 'Y' TO WS-K115-EOF                                 
                 GO TO 1000-EXIT.                                               
            ADD 1 TO WS-CNT-K115-RD.                                            
                                                                                
       1500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       2000-CHECK-REC.                                                          
      *-------------------------                                                
                                                                                
            PERFORM 2100-READ-BP13K893  THRU 2100-EXIT.                         
                                                                                
            IF WS-K893-FND = 'Y'                                                
               IF K893-NUM-NRIC1 NOT = SPACES AND LOW-VALUES                    
                  MOVE LOW-VALUES       TO K510-KEY-FLD                         
                  MOVE K893-NUM-NRIC1   TO WS-NRIC                              
                  MOVE 'N' TO WS-WRITE-FLAG                                     
                  PERFORM 2500-READ-BB24 THRU 2500-EXIT                         
                  IF WS-WRITE-FLAG = 'Y'                                        
                    PERFORM 3100-WRITE-DETAIL THRU 3100-EXIT                    
                  END-IF                                                        
               END-IF                                                           
                                                                                
               IF ( K893-NUM-NRIC2 NOT = SPACES AND LOW-VALUES )                
                  AND WS-WRITE-FLAG NOT = 'Y'                                   
                  MOVE LOW-VALUES       TO K510-KEY-FLD                         
                  MOVE K893-NUM-NRIC2   TO WS-NRIC                              
                  MOVE 'N' TO WS-WRITE-FLAG                                     
                  PERFORM 2500-READ-BB24 THRU 2500-EXIT                         
                  IF WS-WRITE-FLAG = 'Y'                                        
                    PERFORM 3100-WRITE-DETAIL THRU 3100-EXIT                    
                  END-IF                                                        
               END-IF                                                           
                                                                                
                                                                                
               IF ( K893-NUM-NRIC3 NOT = SPACES AND LOW-VALUES )                
                  AND WS-WRITE-FLAG NOT = 'Y'                                   
                  MOVE LOW-VALUES       TO K510-KEY-FLD                         
                  MOVE K893-NUM-NRIC3   TO WS-NRIC                              
                  MOVE 'N' TO WS-WRITE-FLAG                                     
                  PERFORM 2500-READ-BB24 THRU 2500-EXIT                         
                  IF WS-WRITE-FLAG = 'Y'                                        
                    PERFORM 3100-WRITE-DETAIL THRU 3100-EXIT                    
                  END-IF                                                        
               END-IF                                                           
                                                                                
                                                                                
               IF ( K893-NUM-NRIC4 NOT = SPACES AND LOW-VALUES )                
                  AND WS-WRITE-FLAG NOT = 'Y'                                   
                  MOVE LOW-VALUES       TO K510-KEY-FLD                         
                  MOVE K893-NUM-NRIC4   TO WS-NRIC                              
                  MOVE 'N' TO WS-WRITE-FLAG                                     
                  PERFORM 2500-READ-BB24 THRU 2500-EXIT                         
                  IF WS-WRITE-FLAG = 'Y'                                        
                    PERFORM 3100-WRITE-DETAIL THRU 3100-EXIT                    
                  END-IF                                                        
               END-IF                                                           
                                                                                
            END-IF.                                                             
                                                                                
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       2100-READ-BP13K893.                                                      
      *-------------------------------------------------------------*           
                                                                                
           MOVE SPACES                        TO BP13K893-MASTER.               
           INITIALIZE                            BP13K893-MASTER.               
                                                                                
           MOVE K115-REGN-NO                  TO K893-NUM-REGN-HIST             
           MOVE 'N'                              TO WS-K893-EOF                 
                                                    WS-K893-FND                 
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K893-STATUS                                             
              WHEN 00                                                           
                 PERFORM 2200-READNEXT-BP13K893 THRU 2200-EXIT                  
                   UNTIL K893-NUM-REGN-HIST NOT = K115-REGN-NO                  
                      OR WS-K893-EOF = 'Y'                                      
              WHEN 10                                                           
              WHEN 23                                                           
                 DISPLAY 'REGN NOT FOUND IN K893: '                             
                          K115-REGN-NO                                          
              WHEN OTHER                                                        
                DISPLAY 'START ERROR BP13K893 '  BP13K893-STATUS                
                MOVE     BP13K893-STATUS TO RETURN-CODE                         
                PERFORM 9999-CLOSING        THRU 9999-EXIT                      
                                                                                
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       2200-READNEXT-BP13K893.                                                  
      *-------------------------------------------------------------*           
                                                                                
           READ BP13K893 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                         TO WS-K893-EOF                 
           END-READ.                                                            
                                                                                
           EVALUATE BP13K893-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                 IF K893-NUM-REGN-HIST = K115-REGN-NO                           
                    MOVE 'Y'                     TO WS-K893-FND                 
                    MOVE 'Y'                     TO WS-K893-EOF                 
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE 'Y'                        TO WS-K893-EOF                 
                 DISPLAY 'RECORD NOT FOUND IN K893' K115-REGN-NO                
              WHEN OTHER                                                        
                 DISPLAY 'START ERROR BP13K893 '  BP13K893-STATUS               
                 MOVE     BP13K893-STATUS TO RETURN-CODE                        
                 PERFORM 9999-CLOSING        THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       2500-READ-BB24.                                                          
      *-------------------------                                                
                                                                                
           MOVE 'N' TO WS-K510-EOF                                              
           MOVE SPACES TO WS-HDBREF.                                            
           PERFORM 2600-START-BB24K510  THRU 2600-EXIT.                         
           PERFORM 2700-READ-BB24K510   THRU 2700-EXIT                          
                   UNTIL WS-NRIC        NOT = K510-NUM-UIN                      
                      OR WS-K510-EOF = 'Y'.                                     
           CLOSE BB24K510.                                                      
           IF BB24K510-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BB24K510-STATUS ' BB24K510-STATUS           
              MOVE     BB24K510-STATUS TO RETURN-CODE                           
              PERFORM 9999-CLOSING        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE 'N' TO WS-K500-EOF                                              
           PERFORM 2800-START-BB24K500  THRU 2800-EXIT.                         
           PERFORM 2900-READ-BB24K500   THRU 2900-EXIT                          
                   UNTIL K500-NUM-HDB-REF NOT = WS-HDBREF                       
                      OR WS-K500-EOF = 'Y'.                                     
           CLOSE BB24K500.                                                      
           IF BB24K500-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BB24K500-STATUS ' BB24K500-STATUS           
              MOVE     BB24K500-STATUS TO RETURN-CODE                           
              PERFORM 9999-CLOSING        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
       2500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       2600-START-BB24K510.                                                     
      *-------------------------                                                
                                                                                
           OPEN INPUT   BB24K510.                                               
                                                                                
           IF BB24K510-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BB24K510-STATUS ' BB24K510-STATUS         
              MOVE     BB24K510-STATUS TO RETURN-CODE                           
              PERFORM 9999-CLOSING        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE LOW-VALUES               TO K510-KEY-FLD.                       
           MOVE WS-NRIC                  TO K510-NUM-UIN.                       
           START BB24K510 KEY NOT LESS THAN K510-KEY-FLD.                       
                                                                                
           IF BB24K510-STATUS = ZEROES                                          
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'START ERROR, BB24K510-STATUS ' BB24K510-STATUS           
              MOVE     BB24K510-STATUS TO RETURN-CODE                           
              PERFORM 9999-CLOSING        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
       2600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       2700-READ-BB24K510.                                                      
      *-------------------------                                                
                                                                                
           READ BB24K510 NEXT RECORD AT END                                     
                MOVE 'Y'   TO  WS-K510-EOF                                      
                GO TO 2700-EXIT.                                                
                                                                                
           IF BB24K510-STATUS = 00 OR 02                                        
              IF WS-NRIC        = K510-NUM-UIN                                  
                 MOVE LOW-VALUES        TO  K500-KEY-FLD                        
                 MOVE K510-NUM-HDB-REF  TO  K500-NUM-HDB-REF                    
                 MOVE K510-NUM-HDB-REF  TO  WS-HDBREF                           
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'READ ERROR, BB24K510-STATUS ' BB24K510-STATUS            
              MOVE     BB24K510-STATUS TO RETURN-CODE                           
              PERFORM 9999-CLOSING        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
       2700-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       2800-START-BB24K500.                                                     
      *-------------------------                                                
                                                                                
           OPEN INPUT   BB24K500.                                               
                                                                                
           IF BB24K500-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BB24K500-STATUS ' BB24K500-STATUS         
              MOVE     BB24K500-STATUS TO RETURN-CODE                           
              PERFORM 9999-CLOSING        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
           START BB24K500 KEY NOT LESS THAN K500-KEY-FLD.                       
                                                                                
           IF BB24K500-STATUS = ZEROES                                          
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'START ERROR, BB24K500-STATUS ' BB24K500-STATUS           
              MOVE     BB24K500-STATUS TO RETURN-CODE                           
              PERFORM 9999-CLOSING        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
       2800-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       2900-READ-BB24K500.                                                      
      *-------------------------                                                
                                                                                
           READ BB24K500 NEXT RECORD AT END                                     
                MOVE 'Y'   TO  WS-K500-EOF                                      
                GO TO 2900-EXIT.                                                
                                                                                
           IF BB24K500-STATUS = 00 OR 02                                        
              DISPLAY 'K500 HDB REF:' K500-NUM-HDB-REF                          
              DISPLAY 'K500 DTE APT:' K500-DTE-FIRST-APPTM                      
              DISPLAY 'K500 DTE APR:' K500-DTE-APPROVAL                         
              IF WS-HDBREF        = K500-NUM-HDB-REF                            
                 AND K500-DTE-FIRST-APPTM > 20101130                            
                 AND K500-DTE-APPROVAL    > 20101130                            
                     MOVE 'Y' TO WS-WRITE-FLAG                                  
                     MOVE 'Y'   TO  WS-K500-EOF                                 
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'READ ERROR, BB24K500-STATUS ' BB24K500-STATUS            
              MOVE     BB24K500-STATUS TO RETURN-CODE                           
              PERFORM 9999-CLOSING        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
       2900-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------                                                
       3000-WRITE-HEADER.                                                       
      *-------------------------                                                
                                                                                
            MOVE   WS-CNT-24A-PAGE  TO RA24-PAGE.                               
            WRITE  R24A-PRINT-REC  FROM RA24-PR-HDG-01 AFTER PAGE.              
            WRITE  R24A-PRINT-REC  FROM RA24-PR-HDG-02 AFTER 1.                 
            WRITE  R24A-PRINT-REC  FROM RA24-PR-HDG-03A AFTER 1.                
            WRITE  R24A-PRINT-REC  FROM RA24-PR-HDG-04  AFTER 2.                
            WRITE  R24A-PRINT-REC  FROM RA24-PR-HDG-DASH AFTER 1.               
            ADD 6  TO WS-CNT-24A-WRITE.                                         
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       3100-WRITE-DETAIL.                                                       
      *-------------------------                                                
                                                                                
            PERFORM 3150-INIT-DTL   THRU 3150-EXIT.                             
                                                                                
            MOVE K893-NUM-REGN                TO DTL-NUM-REGN.                  
                                                                                
            MOVE K115-SCHEME                  TO WS-FMT-NUM-SCH.                
            MOVE K115-ACC                     TO WS-FMT-NUM-ACC.                
            MOVE K115-CHK                     TO WS-FMT-NUM-ACC-CK.             
            MOVE K115-ACC-TYPE                TO WS-FMT-NUM-LESSEE.             
            MOVE WS-FMT-SCH-ACC               TO DTL-SCH-ACC.                   
                                                                                
            INITIALIZE WS-FMT-NUM-SCH                                           
                       WS-FMT-NUM-ACC                                           
                       WS-FMT-NUM-ACC-CK                                        
                       WS-FMT-NUM-LESSEE.                                       
            MOVE K500-NUM-SCH                 TO WS-FMT-NUM-SCH.                
            MOVE K500-NUM-ACCNT               TO WS-FMT-NUM-ACC.                
            MOVE K500-NUM-CDG                 TO WS-FMT-NUM-ACC-CK.             
            MOVE K500-CDE-LESSEE              TO WS-FMT-NUM-LESSEE.             
            MOVE WS-FMT-SCH-ACC               TO DTL-NPL-SCH-ACC.               
            STRING K500-DTE-FIRST-APPTM ( 7:2) '/'                              
                   K500-DTE-FIRST-APPTM (5:2)  '/'                              
                   K500-DTE-FIRST-APPTM (1:4)                                   
                   DELIMITED BY SIZE  INTO DTL-FIRST-APPT-DTE                   
            END-STRING                                                          
                                                                                
            IF K115-DTE-KEY-ISSUED = SPACES OR LOW-VALUES OR ZEROES             
               MOVE '00/00/0000'           TO DTL-ISSUE-DTE                     
            ELSE                                                                
               STRING K115-DTE-KEY-ISSUED(7:2) '/'                              
                      K115-DTE-KEY-ISSUED(5:2) '/'                              
                      K115-DTE-KEY-ISSUED(1:4)                                  
                      DELIMITED BY SIZE  INTO DTL-ISSUE-DTE                     
               END-STRING                                                       
            END-IF                                                              
                                                                                
            PERFORM 3200-GET-K115      THRU 3200-EXIT.                          
            PERFORM 3250-GET-ADDRESS   THRU 3250-EXIT.                          
            PERFORM 3300-READ-BP13K350 THRU 3300-EXIT.                          
            ADD 2 TO  WS-CNT-24A-WRITE                                          
            ADD 1 TO  WS-CNT-24A-DTL                                            
            MOVE WS-CNT-24A-DTL TO DTL-SER-NUM                                  
            WRITE  R24A-PRINT-REC  FROM RA24-PR-DTL-01 AFTER 1.                 
                                                                                
      *                                                                         
      *     IF WS-CNT-24A-WRITE >= 35                                           
      *       MOVE ZEROES TO WS-CNT-24A-WRITE                                   
      *       ADD 1                   TO WS-CNT-24A-PAGE                        
      *       MOVE   WS-CNT-24A-PAGE  TO RA24-PAGE                              
      *       WRITE  R24A-PRINT-REC  FROM RA24-PR-HDG-SPACE AFTER 1             
      *       WRITE  R24A-PRINT-REC  FROM RA24-PR-HDG-SPACE AFTER 1             
      *       WRITE  R24A-PRINT-REC  FROM RA24-PR-HDG-01 AFTER PAGE             
      *       WRITE  R24A-PRINT-REC  FROM RA24-PR-HDG-02 AFTER 1                
      *       WRITE  R24A-PRINT-REC  FROM RA24-PR-HDG-03A AFTER 1               
      *       WRITE  R24A-PRINT-REC  FROM RA24-PR-HDG-04  AFTER 2               
      *       WRITE  R24A-PRINT-REC  FROM RA24-PR-HDG-DASH AFTER 1              
      *       ADD 6       TO WS-CNT-24A-WRITE                                   
      *     END-IF.                                                             
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       3150-INIT-DTL.                                                           
      *-------------------------                                                
                                                                                
            INITIALIZE    DTL-SER-NUM                                           
                          DTL-NUM-REGN                                          
                          DTL-NPL-SCH-ACC                                       
                          DTL-SCH-ACC                                           
                          DTL-BLK                                               
                          DTL-STREET                                            
                          DTL-UNIT                                              
                          DTL-ACCEPT-DTE                                        
                          DTL-KEY-AVAIL-DTE                                     
                          DTL-ISSUE-DTE                                         
                          DTL-FIRST-APPT-DTE                                    
                          DTL-CASH-COLL                                         
                          DTL-CASH-USED                                         
                          DTL-CASH-REFUND.                                      
                                                                                
       3150-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       3200-GET-K115.                                                           
      *-------------------------                                                
                                                                                
            IF K115-DTE-KEY-AVAIL = SPACES OR LOW-VALUES OR ZEROES              
               MOVE '00/00/0000'           TO DTL-KEY-AVAIL-DTE                 
            ELSE                                                                
               STRING K115-DTE-KEY-AVAIL (7:2) '/'                              
                      K115-DTE-KEY-AVAIL (5:2) '/'                              
                      K115-DTE-KEY-AVAIL (1:4)                                  
                      DELIMITED BY SIZE  INTO DTL-KEY-AVAIL-DTE                 
               END-STRING                                                       
            END-IF.                                                             
                                                                                
            IF K115-DTE-ACCEPTANCE = SPACES OR LOW-VALUES OR ZEROES             
               MOVE '00/00/0000'           TO DTL-ACCEPT-DTE                    
            ELSE                                                                
               STRING K115-DTE-ACCEPTANCE(7:2) '/'                              
                      K115-DTE-ACCEPTANCE(5:2) '/'                              
                      K115-DTE-ACCEPTANCE(1:4)                                  
                      DELIMITED BY SIZE  INTO DTL-ACCEPT-DTE                    
               END-STRING                                                       
            END-IF.                                                             
                                                                                
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       3250-GET-ADDRESS.                                                        
      *-------------------------                                                
                                                                                
      *------------------------------------                                     
      *      CALL BP13C913 TO GET ADDRESS                                       
      *------------------------------------                                     
                                                                                
            MOVE  SPACES                  TO  BP13COMM8-REC.                    
            INITIALIZE                        BP13COMM8-REC.                    
                                                                                
            MOVE  K115-SCH-ACC-NO         TO  WS-LINK-REC.                      
                                                                                
            CALL 'BP13C913'     USING  WS-LINK-REC,                             
                                       BP13COMM8-REC.                           
                                                                                
            IF COMM8-CDE-SYSERR NOT = 0                                         
               MOVE COMM8-CDE-SYSERR TO SQL-CODE                                
               IF COMM8-CDE-SYSERR = 100                                        
                  DISPLAY 'SCHEME-ACCT-NO = ' WS-LINK-REC                       
                  DISPLAY 'SCH-ACC NOT FOUND INF PIDB TABLE   ' SQL-CODE        
               ELSE                                                             
                  DISPLAY 'ERROR IN READING PIDB TABLE   ' SQL-CODE.            
                                                                                
            IF COMM8-CDE-SYSERR = 0                                             
               MOVE COMM8-NUM-BLK       TO DTL-BLK                              
               MOVE COMM8-NME-STREET    TO DTL-STREET                           
               MOVE COMM8-NUM-UNIT-MAIN   TO  WS-F140-HSE-MAIN                  
               MOVE COMM8-NUM-UNIT-SUB    TO  WS-F140-HSE-SUB                   
               STRING COMM8-NUM-LEVEL   '-'                                     
                      WS-F140-HSEMAIN                                           
                      DELIMITED BY SIZE  INTO DTL-UNIT                          
            END-IF.                                                             
                                                                                
                                                                                
       3250-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       3300-READ-BP13K350.                                                      
      *-------------------------                                                
                                                                                
            DISPLAY 'K350 REGN:' K350-NUM-REGN                                  
            MOVE K893-NUM-REGN          TO K350-NUM-REGN.                       
            READ BP13K350.                                                      
                                                                                
            IF BP13K350-STATUS = '00'                                           
               MOVE K350-AMT-ADV-COLLECTION      TO DTL-CASH-COLL               
               MOVE K350-AMT-ADV-COLLECTION-USED TO DTL-CASH-USED               
               MOVE K350-AMT-REFUND-ADV-COL      TO DTL-CASH-REFUND             
               MOVE 'Y'                          TO WS-ADV-COLLECTED            
               ADD  1                            TO WS-CNT-WITHADV              
            ELSE                                                                
               PERFORM 3400-READ-BP13K352 THRU 3400-EXIT                        
            END-IF.                                                             
                                                                                
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       3400-READ-BP13K352.                                                      
      *-------------------------                                                
                                                                                
            MOVE K893-NUM-REGN          TO K352-NUM-REGN.                       
            READ BP13K352.                                                      
                                                                                
            IF BP13K352-STATUS = '00'                                           
               MOVE K352-AMT-ADV-COLLECTION      TO DTL-CASH-COLL               
               MOVE K352-AMT-ADV-COLLECTION-USED TO DTL-CASH-USED               
               MOVE K352-AMT-REFUND-ADV-COL      TO DTL-CASH-REFUND             
               MOVE 'Y'                          TO WS-ADV-COLLECTED            
               ADD  1                            TO WS-CNT-WITHADV              
            ELSE                                                                
               MOVE ZEROES                       TO DTL-CASH-COLL               
               MOVE ZEROES                       TO DTL-CASH-USED               
               MOVE ZEROES                       TO DTL-CASH-REFUND             
               ADD  1                            TO WS-CNT-WOADV                
            END-IF.                                                             
                                                                                
       3400-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------                                                
       9999-CLOSING.                                                            
      *-------------------------                                                
                                                                                
            DISPLAY '*********************'.                                    
            DISPLAY '*  BP13CAM2 TOTALS  *'.                                    
            DISPLAY '*********************'.                                    
            DISPLAY 'K115  READ:    ' WS-CNT-K115-RD.                           
            DISPLAY 'WITH ADV  :    ' WS-CNT-WITHADV.                           
            DISPLAY 'W/O  ADV  :    ' WS-CNT-WOADV.                             
                                                                                
            CLOSE BP13K893                                                      
                  BP13K352                                                      
                  BP13K350                                                      
                  BM06K115                                                      
                  BP13R24A.                                                     
                                                                                
            STOP RUN.                                                           
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
