       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C35d.                                                 
      *AUTHOR.        ALWYN BENNY.                                              
      *DATE-WRITTEN.  28/08/2012.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *  OBJECTIVE :                                               *            
      *                                                            *            
      *    TO SEND EMAIL TO CSM WHEN COURT ORDER WITH NO CPF REFUND*            
      *    IS COMPLETED SUB TRNSFER.                               *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE(YMD)  BY    DESCRIPTION                      *            
      * -------- ---------- ----- -------------------------------- *            
      * BP134561 2012-08-28 AB9   NEW PROGRAM                      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13F880 ASSIGN        TO BP13F880.                           
                                                                                
           SELECT BP13K800 ASSIGN        TO BP13K800                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K800-NUM-REGN                       
                           FILE STATUS   IS BP13K800-STATUS.                    
                                                                                
           SELECT BP13K022  ASSIGN       TO BP13K022                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K022-KEY-FLD                        
                            ALTERNATE RECORD KEY IS K022-NUM-REGN               
                            WITH DUPLICATES                                     
                            FILE STATUS  IS BP13K022-STATUS.                    
                                                                                
           SELECT P13F35D  ASSIGN        TO P13F35D.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
                                                                                
       FD   BP13F880                                                            
            RECORDING  MODE IS F                                                
            RECORD CONTAINS 2000 CHARACTERS.                                    
                                                                                
       COPY BP13F880.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD  P13F35D                                                              
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING  MODE IS F.                                                
       01  P13F35D-REC    PIC X(80).                                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
                                                                                
       01  WS-VARIABLES.                                                        
           05  CNT-F880-READ        PIC 9(05)  VALUE ZEROES.                    
           05  CNT-K800-READ        PIC 9(05)  VALUE ZEROES.                    
           05  CNT-K800-UPD         PIC 9(05)  VALUE ZEROES.                    
           05  CNT-K022-NOTFND      PIC 9(05)  VALUE ZEROES.                    
           05  WS-I                 PIC 9(02)  VALUE ZEROES.                    
           05  WS-CNT               PIC 9(02)  VALUE ZEROES.                    
           05  WS-F880-EOF          PIC X(01)  VALUE SPACES.                    
               88  F880-EOF                    VALUE 'Y'.                       
           05  WS-K022-FND          PIC X(01)  VALUE SPACES.                    
               88  K022-FND                    VALUE 'Y'.                       
           05  WS-K800-FND          PIC X(01)  VALUE SPACES.                    
               88  K800-FND                    VALUE 'Y'.                       
           05  BP13K022-STATUS      PIC 9(02)  VALUE ZEROES.                    
           05  BP13K800-STATUS      PIC 9(02)  VALUE ZEROES.                    
           05  WS-UPD-HDR-WRITE     PIC X(1)   VALUE SPACES.                    
                                                                                
       01  WS-PREB-CSM              PIC X(5)   VALUE SPACES.                    
       01  WS-POSB-CSM              PIC X(5)   VALUE SPACES.                    
       01  WS-CSM-NKNME             PIC X(5)   VALUE SPACES.                    
       01  WS-CSM-FOUND             PIC X(01)  VALUE SPACES.                    
                                                                                
       01  P13F35D-DETAIL.                                                      
           05 FILLER                   PIC X(01)  VALUE SPACES.                 
           05 F35D-REGN                PIC X(08)  VALUE SPACES.                 
           05 FILLER                   PIC X(04)  VALUE SPACES.                 
           05 F35D-SCH-ACC             PIC X(11)  VALUE SPACES.                 
           05 FILLER                   PIC X(04)  VALUE SPACES.                 
           05 F35D-LOAN-TAG            PIC X(01)  VALUE SPACES.                 
           05 FILLER                   PIC X(03)  VALUE SPACES.                 
           05 F35D-OIC-NCK-NAME        PIC X(05)  VALUE SPACES.                 
           05 FILLER                   PIC X(02)  VALUE SPACES.                 
           05 F35D-CORDER-STATUS       PIC X(10)  VALUE SPACES.                 
           05 FILLER                   PIC X(02)  VALUE SPACES.                 
           05 F35D-DTE-APPROVAL        PIC X(08)  VALUE SPACES.                 
           05 FILLER                   PIC X(01)  VALUE SPACES.                 
           05 F35D-NUM-COMPL-TAG       PIC X(01)  VALUE SPACES.                 
           05 FILLER                   PIC X(19)  VALUE SPACES.                 
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
            PERFORM 1000-OPEN-ROUTINE         THRU 1000-EXIT.                   
            PERFORM 2000-READ-BP13F880        THRU 2000-EXIT.                   
            PERFORM 3000-PROCESS-DETAIL       THRU 3000-EXIT                    
                              UNTIL F880-EOF.                                   
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F880                                                 
                       BP13K022                                                 
                I-O    BP13K800                                                 
                OUTPUT P13F35D.                                                 
                                                                                
           IF BP13K022-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K022 - OPEN ERROR (' BP13K022-STATUS ')'             
              MOVE BP13K022-STATUS  TO RETURN-CODE                              
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K800 - OPEN ERROR (' BP13K800-STATUS ')'             
              MOVE BP13K800-STATUS  TO RETURN-CODE                              
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F880.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F880                                                        
             AT END                                                             
                MOVE 'Y'   TO WS-F880-EOF                                       
                GO         TO 2000-EXIT                                         
           END-READ.                                                            
           ADD             1 TO     CNT-F880-READ.                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DETAIL.                                                     
      *-------------------------------------------------------------            
                                                                                
           PERFORM 7000-READ-BP13K800       THRU  7000-EXIT.                    
           IF K800-FND                                                          
              IF K800-NUM-COURTORDER-W-NOREFUND = 'Y' AND                       
                 K800-NUM-SUBTRF-COMPL-MAIL NOT = 'Y'                           
                 PERFORM 8000-READ-BP13K022       THRU  8000-EXIT               
                 PERFORM 3100-WRITE-P13F35D       THRU  3100-EXIT               
                 PERFORM 3200-UPDATE-BP13K800     THRU  3200-EXIT               
              END-IF                                                            
           END-IF.                                                              
           PERFORM 2000-READ-BP13F880        THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-WRITE-P13F35D.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE  SPACES                  TO  P13F35D-DETAIL.                    
           INITIALIZE                        P13F35D-DETAIL.                    
           MOVE K800-NUM-REGN            TO F35D-REGN.                          
           MOVE K800-NUM-SCH-ACC         TO F35D-SCH-ACC.                       
           MOVE K800-NUM-LOAN-TAG        TO F35D-LOAN-TAG.                      
           MOVE WS-CSM-NKNME             TO F35D-OIC-NCK-NAME.                  
           MOVE K800-DTE-CORDER-APPROVAL TO F35D-DTE-APPROVAL.                  
           MOVE 'Y'                      TO K800-NUM-SUBTRF-COMPL-MAIL.         
           MOVE K800-NUM-SUBTRF-COMPL-MAIL   TO F35D-NUM-COMPL-TAG.             
           MOVE 'COMPLETED'                  TO F35D-CORDER-STATUS.             
           WRITE P13F35D-REC FROM P13F35D-DETAIL.                               
           ADD 1                    TO WS-CNT.                                  
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3200-UPDATE-BP13K800.                                                    
      *-------------------------------------------------------------            
                                                                                
            REWRITE BP13K800-MASTER.                                            
            IF BP13K800-STATUS = 00                                             
              ADD   1                TO CNT-K800-UPD                            
            ELSE                                                                
                 DISPLAY 'BP13K800 - UPDATE RROR (' BP13K800-STATUS ')'         
                 MOVE BP13K800-STATUS TO RETURN-CODE                            
                 GO TO 9000-CLOSE-ROUTINE                                       
            END-IF.                                                             
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       7000-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE F880-NUM-REGN          to    K800-NUM-REGN.                     
           READ BP13K800.                                                       
                                                                                
           IF BP13K800-STATUS = 00 OR 02                                        
              MOVE 'Y'               TO WS-K800-FND                             
              ADD   1                TO CNT-K800-READ                           
           ELSE                                                                 
              IF BP13K800-STATUS = 23                                           
                 MOVE 'N'            TO WS-K800-FND                             
              ELSE                                                              
                 DISPLAY 'BP13K800 - READ ERROR (' BP13K800-STATUS ')'          
                 MOVE BP13K800-STATUS TO RETURN-CODE                            
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8000-READ-BP13K022.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE F880-NUM-REGN  TO K022-NUM-REGN.                                
           MOVE SPACES         TO WS-PREB-CSM WS-POSB-CSM                       
                                             WS-CSM-FOUND.                      
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           IF BP13K022-STATUS = 00                                              
              MOVE 'Y'                     TO   WS-CSM-FOUND                    
              PERFORM  8001-GET-CSM        THRU 8001-EXIT                       
           ELSE                                                                 
           IF BP13K022-STATUS = 23                                              
              DISPLAY '> CSM FOR ' K022-NUM-REGN ' NOT FOUND.'                  
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K022, STATUS ' BP13K022-STATUS         
              MOVE BP13K022-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                       
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       8001-GET-CSM.                                                            
      *-------------------------------------------------------------            
                                                                                
            MOVE SPACES                 TO   WS-CSM-NKNME.                      
            IF K022-NUM-OIC NOT = SPACES AND LOW-VALUES                         
               IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                      
                  MOVE K022-NUM-OIC        TO WS-PREB-CSM                       
               ELSE                                                             
                  MOVE K022-NUM-OIC-APPL   TO WS-PREB-CSM                       
               END-IF                                                           
            END-IF.                                                             
           IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                          
              MOVE SPACES                          TO WS-POSB-CSM               
           ELSE                                                                 
              MOVE K022-NUM-OIC                    TO WS-POSB-CSM               
           END-IF.                                                              
           IF K800-NUM-PROV-TAG = 'Y'                                           
              MOVE SPACES         TO    WS-POSB-CSM                             
              MOVE WS-PREB-CSM    TO    WS-CSM-NKNME                            
           ELSE                                                                 
              IF WS-POSB-CSM NOT = SPACES                                       
                 MOVE WS-POSB-CSM  TO   WS-CSM-NKNME                            
              ELSE                                                              
                 MOVE WS-PREB-CSM  TO   WS-CSM-NKNME                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8001-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' - BP13C35D CONTROL TOTALS -----'.                          
           DISPLAY ' '.                                                         
           DISPLAY ' NO. OF BP13F880 RECS READ   = ' CNT-F880-READ.             
           DISPLAY ' NO. OF BP13K800 RECS READ   = ' CNT-K800-READ.             
           DISPLAY ' NO. OF BP13K800 RECS UPD    = ' CNT-K800-UPD.              
           DISPLAY ' NO. OF RECORDS  WRITTEN     = ' WS-CNT.                    
           DISPLAY ' '.                                                         
                                                                                
            CLOSE BP13F880                                                      
                  BP13K022                                                      
                  BP13K800                                                      
                  P13F35D.                                                      
                                                                                
           IF BP13K022-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K022 - CLOSE ERROR (' BP13K022-STATUS ')'            
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K800 - CLOSE ERROR (' BP13K800-STATUS ')'            
           END-IF.                                                              
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
