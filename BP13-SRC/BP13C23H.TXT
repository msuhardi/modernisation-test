       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C23H.                                                 
       AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
       DATE-WRITTEN.  16/04/2008.                                               
      * ========================================================= *             
      *              SORT OUT F800 RECORDS FOR                    *             
      *            PRINTING OF NOTIFICATION LETTER                *             
      * ========================================================= *             
      *  PURPOSE OF THIS PROGRAM: SORT OUT F800 RECORDS FOR ONLINE*             
      *                           PRINTING OF NOTIFICATION LETTER *             
      *                                                           *             
      *  INPUT        :  BP13F205                                 *             
      *                  BP13F800                                 *             
      *  I-O          :  P13F800B                                 *             
                                                                  *             
      * ----------------------------------------------------------*             
      * CHG REF  DATE     BY   DESCRIPTION                        *             
      * -------- -------- --- ------------------------------------*             
      * BP133307 16/04/08 RB12 NEW PROGRAM                        *             
      * BP133342 12/05/08 LMS1 TO REMOVE CHECKING OF ZONE.        *             
      * ==========================================================*             
                                                                                
       ENVIRONMENT    DIVISION.                                                 
       CONFIGURATION  SECTION.                                                  
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13F205  ASSIGN        TO BP13F205.                          
                                                                                
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
                                                                                
           SELECT P13F800B  ASSIGN        TO P13F800B.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F205                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   P13F800B                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   F800-REC                      PIC X(2000).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-F800-READ-CNT               PIC 9(8) VALUE ZEROES.                
       01  WS-F800-KEY-FND                PIC 9(8) VALUE ZEROES.                
       01  WS-F800-WRITE-CNT              PIC 9(8) VALUE ZEROES.                
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-DTE-CCYY                PIC 9(4) VALUE ZEROES.                
           05  WS-DTE-MM                  PIC 9(2) VALUE ZEROES.                
           05  WS-DTE-DD                  PIC 9(2) VALUE ZEROES.                
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F800-EOF-FLAG           PIC X VALUE 'N'.                      
               88 WS-F800-EOF-YES               VALUE 'Y'.                      
               88 WS-F800-EOF-NO                VALUE 'N'.                      
           05  WS-F800-KEY-FOUND-FLAG     PIC X VALUE 'N'.                      
               88 WS-F800-KEY-FOUND             VALUE 'Y'.                      
               88 WS-F800-KEY-NOT-FOUND         VALUE 'N'.                      
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-PRE-PROCESS         THRU 1000-EXIT.                     
           PERFORM 1500-READ-BP13F800       THRU 1500-EXIT.                     
           PERFORM 2000-PROCESS-F800        THRU 2000-EXIT                      
             UNTIL WS-F800-EOF-YES.                                             
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-PRE-PROCESS.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F205                                                 
                       BP13F800                                                 
                OUTPUT P13F800B.                                                
                                                                                
           READ BP13F205.                                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       1500-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800 AT END                                                 
                MOVE 'Y' TO WS-F800-EOF-FLAG                                    
                GO TO 1500-EXIT.                                                
                                                                                
           ADD 1 TO WS-F800-READ-CNT.                                           
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-PROCESS-F800.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF F800-NUM-NEW-TOWN  = F205-NUM-NT-ZONE AND                         
              F800-DTE-BK-APPT  >= F205-DTE-ALLOCN AND                          
              F800-DTE-BALLOT    = F205-DTE-BALLOT                              
              WRITE F800-REC FROM BP13F800-MASTER                               
              ADD 1 TO WS-F800-WRITE-CNT                                        
           END-IF.                                                              
                                                                                
           PERFORM 1500-READ-BP13F800 THRU 1500-EXIT.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '********* BP13C23H ************'.                           
           DISPLAY '                               '.                           
           DISPLAY '***** SORT OUT BP13F800 *******'.                           
           DISPLAY 'RECS READ   FROM BP13F800   : ' WS-F800-READ-CNT.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN  TO BP13F800   : ' WS-F800-WRITE-CNT.          
           DISPLAY '                               '.                           
                                                                                
           CLOSE   BP13F205                                                     
                   BP13F800                                                     
                   P13F800B.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
