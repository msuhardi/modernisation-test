      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.   BP13C283.                                                  
      *AUTHOR.       SURESH.                                                    
      *DATE-WRITTEN. 25/10/2000.                                                
                                                                                
      *****************************************************************         
      * OBJECTIVE : TO CREATE DEBARMENT RECORD FOR RELOCATION         *         
      *             'APPLICATION FOR ENJOYING DISCOUNTED SELLING PRICE*         
      *                                                               *         
      *****************************************************************         
      *  INPUT FILE :- BP13F310 - TRANSACTION FILE                    *         
      *                BP13K820 - SOC OCCUPIER FILE                   *         
      *  OUTPUT FILE:- BP04K010 - DEBARMENT FILE                      *         
      *****************************************************************         
      * CHGE REQ OIC  DATE        REMARKS                             *         
      * -------- ---- ----------  ----------------------------------- *         
      * BP130938 SN   19/01/2001  NEW PROGRAM.                        *         
      * BP130938 SN   03/02/2001  ADD CLOSE FILE FOR BP13K830         *         
      * BP130938 SN   09/03/2001  ADD CCYY TO DEBAR-REF-NO            *         
      * BP132657 YCH2 15/02/2005  DELETE INTO FROM READ BP13K060      *         
      * BP135599 PCL4 11/12/2014  TO UPDATE K010-INTERFACE-SYS        *         
      *****************************************************************         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310 ASSIGN TO BP13F310.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE          IS RANDOM                                
                  ORGANIZATION         IS INDEXED                               
                  RECORD KEY           IS K800-NUM-REGN                         
                  FILE STATUS          IS WS-K800-STATUS.                       
                                                                                
           SELECT BP13K820  ASSIGN TO BP13K820                                  
                  ORGANIZATION         IS INDEXED                               
                  ACCESS MODE          IS DYNAMIC                               
                  RECORD KEY           IS K820-KEY-FLD                          
                  FILE STATUS          IS WS-K820-STATUS.                       
                                                                                
           SELECT BP13K830  ASSIGN TO BP13K830                                  
                  ORGANIZATION         IS INDEXED                               
                  ACCESS MODE          IS RANDOM                                
                  RECORD KEY           IS K830-KEY-FLD                          
                  ALTERNATE RECORD KEY IS K830-NUM-REGN                         
                                          WITH DUPLICATES                       
                  FILE STATUS          IS WS-K830-STATUS.                       
                                                                                
           SELECT BP13K060  ASSIGN TO BP13K060                                  
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K060-KEY-FLD                               
                  FILE STATUS     IS WS-K060-STATUS.                            
                                                                                
           SELECT BP04K010 ASSIGN TO BP04K010                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K010-PRIME-KEY                             
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
           SELECT BP13L283 ASSIGN TO BP13L283.                                  
                                                                                
                                                                                
      *---------------------*                                                   
       DATA DIVISION.                                                           
      *---------------------*                                                   
       FILE SECTION.                                                            
                                                                                
       FD   BP13F310                                                            
            RECORDING MODE  IS  F                                               
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 150 CHARACTERS                                      
            LABEL  RECORDS  ARE STANDARD.                                       
       COPY BP13F310.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25 CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD   BP04K010                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY P04K010.                                                            
                                                                                
       FD   BP13L283                                                            
            RECORDING MODE  IS  F                                               
            BLOCK CONTAINS  0   RECORDS                                         
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   BP13L283-REC                   PIC X(132).                          
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F310-EOF                PIC X    VALUE 'N'.                   
               88 F310-EOF                         VALUE 'Y'.                   
           05 WS-K060-EOF                 PIC X VALUE 'N'.                      
           05 WS-FLAG                     PIC X VALUE 'Y'.                      
           05  WS-FOUND                   PIC X    VALUE SPACES.                
           05  WS-DEBARMENT-TAG           PIC X    VALUE SPACES.                
           05  WS-DEBAR-TAG               PIC X    VALUE SPACES.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT-F310           PIC 9(6)    VALUE ZEROS.              
           05  WS-K820-READ               PIC 9(6)    VALUE ZEROS.              
           05  WS-K010-WRITE              PIC 9(6)    VALUE ZEROS.              
                                                                                
       01  WS-TEMPORARY-VARS.                                                   
           05  WS-DEBAR.                                                        
               10 WS-DEBREF-DIGIT         PIC 9(7) VALUE ZEROES.                
               10 WS-DEB-REFN  REDEFINES WS-DEBREF-DIGIT.                       
                  15 WS-DEB               PIC X(7).                             
           05  WS-PREV-REGN               PIC X(8) VALUE SPACES.                
                                                                                
       01  WS-REL                         PIC X(2).                             
       01  WS-REL1                        PIC X(2).                             
       01  WS-K060-REL                    PIC X(4).                             
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-F310-STATUS             PIC 99   VALUE ZEROS.                 
           05  WS-K820-STATUS             PIC 99   VALUE ZEROS.                 
           05  WS-K830-STATUS             PIC 99   VALUE ZEROS.                 
           05  WS-K800-STATUS             PIC 99   VALUE ZEROS.                 
           05  WS-K010-STATUS             PIC 99   VALUE ZEROS.                 
           05  WS-K060-STATUS             PIC X(2) VALUE SPACES.                
           05  WS-K800-MATCH              PIC X.                                
           05  WS-K830-MATCH              PIC X.                                
                                                                                
       01  WS-DATE                        PIC X(08) VALUE SPACES.               
       01  WS-K060-DESC                   PIC X(20).                            
                                                                                
      *-------------------*                                                     
       01  WS-LISTING-CONTROLS.                                                 
           05  WS-SEQ           PIC 9(5)        VALUE ZEROS.                    
           05  CNT-PAGE         PIC 9(5)        VALUE ZEROS.                    
           05  CNT-NUM-LINE     PIC 9(5)        VALUE ZEROS.                    
                                                                                
      ******************************************************************        
      *    BP13L283-REC                                                *        
      ******************************************************************        
                                                                                
       01  L280-PR-HEAD-01.                                                     
           05  FILLER              PIC X(8)      VALUE 'BP13L283'.              
           05  FILLER              PIC X(4)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'SYSTEM OF COMMITMENT'.                     
           05  FILLER              PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L280-DATE           PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L280-PAGENO         PIC ZZZZ9 .                                  
           05  FILLER              PIC X(6)      VALUE SPACES.                  
                                                                                
       01  L280-PR-HEAD-02.                                                     
           05  FILLER              PIC X(42)     VALUE SPACES.                  
           05  FILLER              PIC X(48)     VALUE                          
           'REPORT ON UPDATE OF DEBARMENT RECORD            '.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(27)     VALUE                          
           'USER : SALES UNIT, RHS, PPU'.                                       
                                                                                
       01  L280-PR-HEAD-03.                                                     
           05  FILLER              PIC X(42)     VALUE SPACES.                  
           05  FILLER              PIC X(49)     VALUE                          
           'APPLICATIONS UNDER RED SCHEME                   '.                  
                                                                                
       01  L280-PR-HEAD-04.                                                     
           05  FILLER              PIC X(14) VALUE ' S/NO  REGN NO'.            
           05  FILLER              PIC X(09) VALUE SPACES.                      
           05  FILLER              PIC X(04) VALUE 'NT  '.                      
           05  FILLER              PIC X(03) VALUE SPACES.                      
           05  FILLER              PIC X(04) VALUE 'FLAT'.                      
           05  FILLER              PIC X(03) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'NRIC     '.                 
           05  FILLER              PIC X(04) VALUE SPACES.                      
           05  FILLER              PIC X(04) VALUE 'NAME'.                      
           05  FILLER              PIC X(35) VALUE SPACES.                      
           05  FILLER              PIC X(07) VALUE 'STATUS '.                   
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(12) VALUE 'RELATIONSHIP'.              
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  FILLER              PIC X(14) VALUE 'DEBARMENT-CODE'.            
           05  FILLER              PIC X(02) VALUE SPACES.                      
                                                                                
       01  L280-PR-HEAD-05.                                                     
           05  FILLER              PIC X(07) VALUE SPACES.                      
           05  FILLER              PIC X(11) VALUE 'HDB-REF    '.               
           05  FILLER              PIC X(12) VALUE SPACES.                      
           05  FILLER              PIC X(04) VALUE 'TYPE'.                      
           05  FILLER              PIC X(03) VALUE SPACES.                      
           05  FILLER              PIC X(94) VALUE SPACES.                      
                                                                                
       01  L280-PR-HEAD-06.                                                     
           05  FILLER              PIC X(07) VALUE SPACES.                      
           05  FILLER              PIC X(12) VALUE 'CORR-ADDRESS'.              
                                                                                
       01  L280-PR-DETAIL.                                                      
           05  L280-NO             PIC ZZZZ9.                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  L280-CUR-REGN-NO    PIC X(8).                                    
           05  FILLER              PIC X(8)      VALUE SPACES.                  
           05  L280-NT             PIC X(2).                                    
           05  FILLER              PIC X(05)      VALUE SPACES.                 
           05  L280-FLAT           PIC X(2).                                    
           05  FILLER              PIC X(05)      VALUE SPACES.                 
           05  L280-NRIC           PIC X(9).                                    
           05  FILLER              PIC X(5) VALUE SPACES.                       
           05  L280-NAME           PIC X(40).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  L280-STATUS         PIC X(3).                                    
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  L280-RELATIONSHIP   PIC X(10).                                   
           05  FILLER              PIC X(8)      VALUE SPACES.                  
           05  L280-DEBAR-CODE     PIC X(2).                                    
                                                                                
       01  L280-PR-DETAIL-A.                                                    
           05  FILLER              PIC X(7) VALUE SPACES.                       
           05  L280-SCHEME         PIC 9(4).                                    
           05  L280-D1             PIC X.                                       
           05  L280-ACC            PIC 9(5).                                    
           05  L280-D2             PIC X.                                       
           05  L280-CHK            PIC 9(2).                                    
           05  FILLER              PIC X(3)      VALUE SPACES.                  
                                                                                
       01  L280-PR-DETAIL-B.                                                    
           05  FILLER              PIC X(7)      VALUE SPACES.                  
           05  L280-BLK            PIC X(04)     VALUE SPACES.                  
           05  L280-BLK-NO         PIC X(05).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  L280-FLAT-NO.                                                    
               10  L280-HEX        PIC X(02)     VALUE SPACES.                  
               10  L280-LEVEL-NO   PIC X(02).                                   
               10  L280-DASH       PIC X(01)     VALUE SPACES.                  
               10  L280-UNIT-NO    PIC X(04).                                   
               10  L280-SUB-UNIT   PIC X(03).                                   
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  L280-STREET         PIC X(20).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  L280-POSTAL1        PIC X(2)      VALUE SPACES.                  
           05  L280-POSTAL-CODE    PIC X(6).                                    
           05  L280-POSTAL2        PIC X(1)      VALUE SPACES.                  
                                                                                
                                                                                
       01  L280-PR-LINE.                                                        
           05  FILLER              PIC X         VALUE SPACES.                  
           05  FILLER              PIC X(131)    VALUE ALL '-'.                 
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
      *-------------------*                                                     
       000-MAIN-LOGIC.                                                          
      *-------------------*                                                     
            PERFORM 100-OPEN-FILES          THRU 100-EXIT.                      
            PERFORM 200-READ-BP13F310       THRU 200-EXIT.                      
            PERFORM 300-PROCESS             THRU 300-EXIT                       
                    UNTIL F310-EOF.                                             
            PERFORM 999-CLOSE-FILES         THRU 999-EXIT.                      
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------                                                        
       100-OPEN-FILES.                                                          
      *-----------------                                                        
           OPEN INPUT  BP13F310                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K830                                                 
                       BP13K060                                                 
                I-O    BP04K010                                                 
                OUTPUT BP13L283.                                                
                                                                                
           IF WS-K820-STATUS  =  00  OR  97                                     
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'OPENING ERROR, K820-STATUS ' WS-K820-STATUS              
              MOVE WS-K820-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS  =  00  OR  97                                     
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'OPENING ERROR, K830-STATUS ' WS-K830-STATUS              
              MOVE WS-K830-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS  =  00  OR  97                                     
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'OPENING ERROR, K010-STATUS ' WS-K010-STATUS              
              MOVE WS-K010-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS =  00  OR  97                                      
              CONTINUE                                                          
           ELSE                                                                 
                 DISPLAY 'ERROR IN OPENING-BP13K060'                            
                 DISPLAY 'WS-K060-STATUS IS: ' WS-K060-STATUS                   
                 MOVE WS-K060-STATUS       TO RETURN-CODE                       
                 PERFORM 999-CLOSE-FILES THRU 999-EXIT                          
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS TO RETURN-CODE                                
                 PERFORM 999-CLOSE-FILES THRU 999-EXIT                          
           END-IF.                                                              
                                                                                
            MOVE 60                         TO CNT-NUM-LINE.                    
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       200-READ-BP13F310.                                                       
      *-----------------*                                                       
           READ BP13F310 AT END                                                 
                MOVE 'Y'  TO WS-F310-EOF                                        
                GO TO 200-EXIT.                                                 
           ADD   1        TO WS-READ-CNT-F310.                                  
           MOVE SPACES    TO WS-FOUND.                                          
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------                                                        
       300-PROCESS.                                                             
      *-----------------                                                        
           MOVE F310-REGN-NO              TO K820-NUM-REGN K800-NUM-REGN        
           PERFORM  850-K800-READ         THRU 850-EXIT.                        
           PERFORM  900-K830-READ         THRU 900-EXIT.                        
           MOVE LOW-VALUES                TO K820-NUM-NRIC.                     
                                                                                
           START BP13K820 KEY IS > K820-KEY-FLD                                 
           IF  WS-K820-STATUS = ZEROS                                           
               PERFORM 400-READ-K820  THRU 400-EXIT                             
               PERFORM 350-ONE-PRINT THRU  350-EXIT                             
               PERFORM 450-PROCESS-K820  THRU 450-EXIT                          
                   UNTIL K820-NUM-REGN NOT = F310-REGN-NO                       
                   OR    K820-KEY-FLD      = HIGH-VALUES                        
                MOVE 'Y' TO WS-FLAG                                             
           ELSE                                                                 
           IF  WS-K820-STATUS = 23                                              
                 DISPLAY 'READING ERROR, K820-STATUS ' WS-K820-STATUS           
                         ' K820-KEY ' K820-KEY-FLD                              
                 MOVE WS-K820-STATUS       TO RETURN-CODE                       
                 PERFORM 999-CLOSE-FILES THRU 999-EXIT                          
           END-IF                                                               
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-BP13F310            THRU 200-EXIT.                  
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       350-ONE-PRINT.                                                           
             IF K820-NUM-REGN  = F310-REGN-NO                                   
               MOVE F310-REGN-NO         TO L280-CUR-REGN-NO                    
               MOVE '-'                  TO L280-D1 L280-D2                     
               MOVE F310-SCH             TO L280-SCHEME                         
               MOVE F310-ACC             TO L280-ACC                            
               MOVE F310-LESSEE-CODE     TO L280-CHK                            
               MOVE K800-NUM-NEW-TOWN    TO L280-NT                             
               MOVE F310-CDE-FLAT-TYPE   TO L280-FLAT                           
             END-IF.                                                            
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-READ-K820.                                                           
            READ BP13K820 NEXT                                                  
                 AT END MOVE HIGH-VALUES TO K820-KEY-FLD                        
                 GO TO 400-EXIT.                                                
            ADD 1 TO WS-K820-READ.                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------                                                     
       450-PROCESS-K820.                                                        
      *--------------------                                                     
                                                                                
            IF K820-NUM-REGN = F310-REGN-NO                                     
               IF K820-NUM-DEBAR-SHAP1 = 'Y' OR                                 
                  K820-NUM-DEBAR-SHAP2 = 'Y'                                    
                    PERFORM 500-PREPARE-DEBARMENT-REC THRU 500-EXIT             
                    PERFORM 800-PROCESS-LIST  THRU 800-EXIT                     
               END-IF                                                           
            END-IF.                                                             
            MOVE 'N'  TO WS-FLAG                                                
            PERFORM 400-READ-K820  THRU 400-EXIT.                               
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
      *--------------------------*                                              
       500-PREPARE-DEBARMENT-REC.                                               
      *--------------------------*                                              
            MOVE SPACES TO P04K010-REC.                                         
             IF K820-NUM-DEBAR-SHAP1 = 'Y'                                      
                MOVE 'T1'                   TO K010-REASON-CODE                 
             END-IF                                                             
             IF K820-NUM-DEBAR-SHAP2 = 'Y'                                      
                MOVE 'T2'                   TO K010-REASON-CODE                 
             END-IF                                                             
                                                                                
              MOVE 'BP13'                   TO K010-INTERFACE-SYS               
              MOVE K820-NUM-NRIC            TO K010-NUM-UINFIN                  
              MOVE K820-NME-OCCP            TO K010-NAME                        
              MOVE F310-SCH-ACC(1:9)   TO K010-EX-SCH-ACCTNO                    
              MOVE F310-SCH-ACC(10:2)  TO K010-LSE-TEN-CODE                     
              MOVE '1'                      TO K010-PROP-TYPE                   
              MOVE '999'                    TO K010-PERIOD-DEBAR                
              MOVE ZEROS                    TO K010-AMT-OWING                   
              MOVE FUNCTION CURRENT-DATE    TO K010-EFF-DTE                     
                                               K010-INCLUSION-DATE              
                                               K010-LUPDATE                     
              MOVE 'BP13'                   TO K010-INTERFACE-SYS               
              MOVE  F310-NUM-ACCEPT-INFO    TO K010-DEB-REF-NO                  
              MOVE  F310-DTE-TRANS(1:4)     TO K010-DEBREF-YR                   
              MOVE  F310-NUM-BLK            TO K010-BLOCK                       
              MOVE  F310-NME-STREET         TO K010-STREETNAME                  
              MOVE  F310-NUM-POSTAL         TO K010-POSTCODE                    
            STRING F310-NUM-LEVEL F310-NUM-MAIN                                 
                        DELIMITED BY SIZE INTO K010-HOUSE-NO                    
           WRITE P04K010-REC                                                    
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN 00                                                              
                ADD 1 TO WS-K010-WRITE                                          
           WHEN 22                                                              
           WHEN 02                                                              
                DISPLAY 'DUPLICATE RECORD BP04K010: ' K010-PRIME-KEY            
           WHEN OTHER                                                           
                MOVE WS-K010-STATUS       TO RETURN-CODE                        
                DISPLAY 'ERROR IN WRITING BP04K010: ' K010-PRIME-KEY            
                DISPLAY F310-REGN-NO                                            
                DISPLAY WS-K010-STATUS                                          
                PERFORM 999-CLOSE-FILES THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       550-PREP-LIST.                                                           
      *--------------*                                                          
            MOVE K820-NUM-NRIC        TO L280-NRIC.                             
            MOVE K820-NME-OCCP        TO L280-NAME.                             
            IF K820-NUM-DEBAR-SHAP1 = 'Y'                                       
            MOVE 'T1'                 TO L280-DEBAR-CODE                        
            END-IF.                                                             
            IF K820-NUM-DEBAR-SHAP2 = 'Y'                                       
            MOVE 'T2'                 TO L280-DEBAR-CODE                        
            END-IF.                                                             
                 IF K820-NUM-NRIC = K800-NUM-NRIC1                              
                    MOVE 'HA1' TO L280-STATUS                                   
                 ELSE                                                           
                  IF K820-NUM-NRIC = K800-NUM-NRIC2                             
                     MOVE 'HA2' TO L280-STATUS                                  
                  ELSE                                                          
                   IF K820-NUM-NRIC = K800-NUM-NRIC3                            
                      MOVE 'HA3' TO L280-STATUS                                 
                   ELSE                                                         
                    IF K820-NUM-NRIC = K800-NUM-NRIC4                           
                       MOVE 'HA4' TO L280-STATUS                                
                    ELSE                                                        
                       MOVE 'OC'  TO L280-STATUS                                
                    END-IF                                                      
                   END-IF                                                       
                  END-IF                                                        
                  END-IF                                                        
              MOVE  K820-NUM-RELATIONSHIP    TO  WS-REL                         
              MOVE  '35'                     TO  WS-REL1                        
                                                                                
           STRING  WS-REL1  WS-REL  DELIMITED BY SIZE INTO WS-K060-REL          
           MOVE WS-K060-REL             TO K060-KEY-FLD.                        
           PERFORM 700-K060-READ        THRU  700-EXIT.                         
            MOVE WS-K060-DESC           TO L280-RELATIONSHIP.                   
            WRITE BP13L283-REC         FROM L280-PR-DETAIL   AFTER 1.           
            WRITE BP13L283-REC         FROM L280-PR-DETAIL-A AFTER 1.           
            WRITE BP13L283-REC         FROM L280-PR-DETAIL-B AFTER 1.           
            MOVE SPACES              TO L280-PR-DETAIL L280-PR-DETAIL-A         
                                        L280-PR-DETAIL-B                        
            ADD 1                      TO CNT-NUM-LINE.                         
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       600-PRINT-LIST.                                                          
      *----------------------------------------------------------------*        
            PERFORM 650-PRINT-HEADER THRU 650-EXIT.                             
            MOVE SPACES           TO BP13L283-REC.                              
            ADD 1                 TO CNT-NUM-LINE.                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       650-PRINT-HEADER.                                                        
      *-------------------*                                                     
            ADD 1                 TO CNT-PAGE.                                  
            MOVE CNT-PAGE         TO L280-PAGENO.                               
            MOVE 8                TO CNT-NUM-LINE.                              
                                                                                
            MOVE FUNCTION CURRENT-DATE TO WS-DATE.                              
            STRING WS-DATE (7:2) '/'                                            
                   WS-DATE (5:2) '/'                                            
                   WS-DATE (1:4)                                                
                   DELIMITED BY SIZE                                            
              INTO L280-DATE.                                                   
                                                                                
            WRITE BP13L283-REC FROM L280-PR-HEAD-01 AFTER PAGE.                 
            WRITE BP13L283-REC FROM L280-PR-HEAD-02 AFTER 1.                    
            WRITE BP13L283-REC FROM L280-PR-HEAD-03 AFTER 1.                    
            WRITE BP13L283-REC FROM L280-PR-HEAD-04 AFTER 3.                    
            WRITE BP13L283-REC FROM L280-PR-HEAD-05 AFTER 1.                    
            WRITE BP13L283-REC FROM L280-PR-HEAD-06 AFTER 1.                    
            WRITE BP13L283-REC FROM L280-PR-LINE    AFTER 1.                    
                                                                                
            MOVE SPACES           TO BP13L283-REC.                              
            WRITE BP13L283-REC.                                                 
                                                                                
       650-EXIT.                                                                
            EXIT.                                                               
                                                                                
       700-K060-READ.                                                           
             READ BP13K060.                                                     
              IF WS-K060-STATUS = '00'                                          
                 MOVE K060-DESC TO WS-K060-DESC                                 
              ELSE                                                              
                IF WS-K060-STATUS = '23'                                        
                  MOVE SPACES TO WS-K060-DESC                                   
                ELSE                                                            
                  DISPLAY 'WS-K060-STATUS IS: ' WS-K060-STATUS                  
                  PERFORM 999-CLOSE-FILES THRU 999-EXIT                         
                END-IF                                                          
              END-IF.                                                           
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ************************************************************              
       800-PROCESS-LIST.                                                        
      ************************************************************              
           IF CNT-NUM-LINE > 53                                                 
               PERFORM 600-PRINT-LIST THRU 600-EXIT                             
           END-IF.                                                              
           IF WS-FLAG = 'N'                                                     
              CONTINUE                                                          
           ELSE                                                                 
             ADD   1                   TO WS-SEQ                                
             MOVE WS-SEQ               TO L280-NO                               
           END-IF.                                                              
             PERFORM 550-PREP-LIST  THRU 550-EXIT.                              
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
       850-K800-READ.                                                           
                 READ BP13K800.                                                 
                 IF WS-K800-STATUS = 0 OR 02                                    
                    MOVE 'Y' TO WS-K800-MATCH                                   
                 ELSE                                                           
                    IF WS-K800-STATUS = 23                                      
                       MOVE 'N' TO WS-K800-MATCH                                
                    ELSE                                                        
                     DISPLAY 'READ ERROR, K800-STATUS = ' WS-K800-STATUS        
                     DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                   
                     MOVE WS-K800-STATUS TO RETURN-CODE                         
                     GO TO 999-CLOSE-FILES                                      
                    END-IF                                                      
                 END-IF.                                                        
        850-EXIT.                                                               
            EXIT.                                                               
                                                                                
       900-K830-READ.                                                           
               IF WS-K800-MATCH = 'Y'                                           
                 INITIALIZE BP13K830-REC                                        
                 MOVE K800-NUM-REGN       TO K830-NUM-REGN                      
                 READ BP13K830 KEY IS K830-NUM-REGN                             
                 IF WS-K830-STATUS = 00 OR 02                                   
                   MOVE K830-NUM-BLK-HDR          TO L280-BLK                   
                   MOVE K830-NUM-MAIN-BLK         TO L280-BLK-NO                
                   MOVE K830-NUM-LVL-CHAR         TO L280-HEX                   
                   MOVE K830-NUM-LVL-NUM          TO L280-LEVEL-NO              
                   MOVE K830-NUM-LVL-DASH         TO L280-DASH                  
                   MOVE K830-NUM-HSEMAIN          TO L280-UNIT-NO               
                   MOVE K830-NUM-HSESUB           TO L280-SUB-UNIT              
                   MOVE K830-NME-STREET           TO L280-STREET                
                   MOVE 'S('                      TO L280-POSTAL1               
                   MOVE K830-NUM-POSTAL           TO L280-POSTAL-CODE           
                   MOVE ')'                       TO L280-POSTAL2               
                 ELSE                                                           
                    IF WS-K830-STATUS = 23                                      
                       MOVE 'N' TO WS-K830-MATCH                                
                    ELSE                                                        
                     DISPLAY 'READ ERROR, K830-STATUS = ' WS-K830-STATUS        
                     DISPLAY 'K830-NUM-REGN = ' K830-NUM-REGN                   
                     MOVE WS-K830-STATUS TO RETURN-CODE                         
                     GO TO 999-CLOSE-FILES                                      
                    END-IF                                                      
                 END-IF                                                         
               END-IF.                                                          
        900-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------*                                                
       999-CLOSE-FILES.                                                         
      *------------------------*                                                
           CLOSE BP13F310                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13K060                                                       
                 BP04K010                                                       
                 BP13L283.                                                      
                                                                                
           IF WS-F310-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13F310, STATUS ' WS-F310-STATUS          
              MOVE WS-F310-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K800, STATUS ' WS-K800-STATUS          
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K820, STATUS ' WS-K820-STATUS          
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K830, STATUS ' WS-K830-STATUS          
              MOVE WS-K830-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K060, STATUS ' WS-K060-STATUS          
              MOVE WS-K060-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING 3BP04K010, STATUS ' WS-K010-STATUS         
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           DISPLAY 'NO OF REC READ        BP13F310   = '                        
                    WS-READ-CNT-F310.                                           
           DISPLAY 'NO OF REC READ        BP13K820   = '                        
                    WS-K820-READ                                                
           DISPLAY 'NO OF REC WRITTEN     BP04K010   = '                        
                    WS-K010-WRITE                                               
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
