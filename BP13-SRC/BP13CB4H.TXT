       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB4H.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  25/04/2016.                                               
      * ========================================================== *            
      *             SYSTEM OF COMMITMENT  (BP13)                   *            
      * ========================================================== *            
      *                                                            *            
      *  DESCRIPTION :  TO MATCH AB02F930 AND AB02F935             *            
      *  INPUT FILE  :  AB02F935                                   *            
      *                 AB02F930                                   *            
      *  OUTPUT FILES:  P13F935A --- MATCH                         *            
      *              :  P13F935B --- NOT FOUND IN F930             *            
      *              :  P13F930A --- NOT FOUND IN F935             *            
      * ---------------------------------------------------------- *            
      * CHG-NO.  BY     ON          DESCRIPTION                    *            
      * -------- ---    ----------  -----------                    *            
      * XXXXXXXX ZDD1   25/04/2016  NEW PROGRAM                    *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AB02F935  ASSIGN       TO AB02F935.                           
           SELECT AB02F930  ASSIGN       TO AB02F930.                           
                                                                                
           SELECT P13F935A  ASSIGN       TO P13F935A.                           
           SELECT P13F935B  ASSIGN       TO P13F935B.                           
           SELECT P13F930A  ASSIGN       TO P13F930A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *---------------------------------------------------------------*         
       FD  AB02F935                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY AB02F935.                                                           
                                                                                
       FD  AB02F930                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY AB02F930.                                                           
                                                                                
       FD  P13F935A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F935A-REC     PIC X(200).                                         
                                                                                
       FD  P13F935B                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F935B-REC     PIC X(200).                                         
                                                                                
       FD  P13F930A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F930A-REC     PIC X(150).                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-VARIABLES.                                                        
           05  WS-F935-EOF         PIC X(01)  VALUE SPACES.                     
               88 F935-END-OF-FILE   VALUE 'Y'.                                 
           05  WS-F930-EOF         PIC X(01)  VALUE SPACES.                     
               88 F930-END-OF-FILE   VALUE 'Y'.                                 
           05  WS-F935-READ-CNT    PIC 9(08)  VALUE ZEROES.                     
           05  WS-F930-READ-CNT    PIC 9(08)  VALUE ZEROES.                     
           05  WS-WRT-MATCH        PIC 9(08)  VALUE ZEROES.                     
           05  WS-WRT-N-FND-F935   PIC 9(08)  VALUE ZEROES.                     
           05  WS-WRT-N-FND-F930   PIC 9(08)  VALUE ZEROES.                     
                                                                                
       01  WS-DATE-TIME            PIC X(15)  VALUE SPACES.                     
                                                                                
       01  WS-CURR-DATE            PIC X(10)  VALUE SPACES.                     
       01  WS-CURR-TIME            PIC X(08)  VALUE SPACES.                     
                                                                                
       01  WS-EDT-CNT              PIC Z(7)9.                                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-AB02F935     THRU 2000-EXIT.                       
           PERFORM 2100-READ-AB02F930     THRU 2100-EXIT.                       
           PERFORM 3000-PROCESS-F935      THRU 3000-EXIT                        
                                 UNTIL F935-END-OF-FILE OR                      
                                       F930-END-OF-FILE.                        
           PERFORM 4000-CHECK-EOF         THRU 4000-EXIT.                       
           PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  AB02F935                                                 
                       AB02F930                                                 
              OUTPUT   P13F935A                                                 
                       P13F935B                                                 
                       P13F930A.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-AB02F935.                                                      
      *---------------------------------------------------------------*         
           READ AB02F935 AT END                                                 
                          MOVE 'Y'  TO WS-F935-EOF                              
                         NOT AT END                                             
                          ADD  1    TO WS-F935-READ-CNT                         
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2100-READ-AB02F930.                                                      
      *---------------------------------------------------------------*         
           READ AB02F930 AT END                                                 
                          MOVE 'Y'  TO WS-F930-EOF                              
                         NOT AT END                                             
                          ADD  1    TO WS-F930-READ-CNT                         
           END-READ.                                                            
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-F935.                                                       
      *---------------------------------------------------------------*         
                                                                                
           IF AB02F930-DETAIL-REC(1:64) =                                       
              AB02F935-DETAIL-REC(1:64)                                         
              WRITE P13F935A-REC   FROM   AB02F935-DETAIL-REC                   
              ADD 1 TO WS-WRT-MATCH                                             
              PERFORM 2000-READ-AB02F935      THRU 2000-EXIT                    
              PERFORM 2100-READ-AB02F930      THRU 2100-EXIT                    
           ELSE                                                                 
               IF AB02F930-DETAIL-REC(1:64) >                                   
                  AB02F935-DETAIL-REC(1:64)                                     
                  WRITE P13F935B-REC   FROM   AB02F935-DETAIL-REC               
                  ADD 1 TO WS-WRT-N-FND-F930                                    
                  PERFORM 2000-READ-AB02F935      THRU 2000-EXIT                
               ELSE                                                             
                  WRITE P13F930A-REC   FROM   AB02F930-DETAIL-REC               
                  ADD 1 TO WS-WRT-N-FND-F935                                    
                  PERFORM 2100-READ-AB02F930      THRU 2100-EXIT                
               END-IF                                                           
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-CHECK-EOF.                                                          
      *---------------------------------------------------------------*         
           IF F935-END-OF-FILE AND  F930-END-OF-FILE                            
              CONTINUE                                                          
           ELSE                                                                 
             IF F935-END-OF-FILE                                                
                PERFORM UNTIL F930-END-OF-FILE                                  
                  WRITE P13F930A-REC   FROM   AB02F930-DETAIL-REC               
                  ADD 1 TO WS-WRT-N-FND-F935                                    
                  PERFORM 2100-READ-AB02F930  THRU 2100-EXIT                    
                END-PERFORM                                                     
             END-IF                                                             
             IF F930-END-OF-FILE                                                
                PERFORM UNTIL F935-END-OF-FILE                                  
                  WRITE P13F935B-REC   FROM   AB02F935-DETAIL-REC               
                  ADD 1 TO WS-WRT-N-FND-F930                                    
                  PERFORM 2000-READ-AB02F935  THRU 2000-EXIT                    
                END-PERFORM                                                     
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY ' '.                                                         
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
           DISPLAY '   PROGRAM : BP13CB4H ' .                                   
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
           DISPLAY ' '.                                                         
           DISPLAY ' REC READ FROM AB02F935 - '   WS-F935-READ-CNT.             
           DISPLAY ' REC READ FROM AB02F930 - '   WS-F930-READ-CNT.             
           DISPLAY ' MATCHED CASES             -' WS-WRT-MATCH.                 
           DISPLAY ' NOT FOUND IN F930         -' WS-WRT-N-FND-F930.            
           DISPLAY ' NOT FOUND IN F935         -' WS-WRT-N-FND-F935.            
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
                                                                                
           CLOSE AB02F935                                                       
                 AB02F930                                                       
                 P13F935A                                                       
                 P13F935B                                                       
                 P13F930A.                                                      
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
