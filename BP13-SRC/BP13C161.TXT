       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C161.                                                 
      *AUTHOR.        CKK7.                                                     
      *DATE-WRITTEN.  18/12/2001.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *   OBJECTIVE   :  PROGRAM TO COMPARE 2 K730 FILES           *            
      *                                                            *            
      *                                                            *            
      *   INPUT FILES :  1.BP13F500 - DATA ENTRY FILE FOR REQUEST  *            
      *                               FOR REGN                     *            
      *                  2.BP13K730 - DATA ENTRY FILE FOR REQUEST  *            
      *                               FOR REGN                     *            
      *                  2.BP13F730 - OUTPUT FILE                  *            
      *                                                            *            
      * CHG REF  DATE      OIC   REMARKS                           *            
      * -------- --------  ----  -----------------------------     *            
      * BP131064 20011218  CKK7  NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F500  ASSIGN       TO BP13F500.                           
                                                                                
           SELECT BP13F730  ASSIGN       TO BP13F730.                           
                                                                                
           SELECT BP13K730  ASSIGN       TO BP13K730                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K730-KEY-NEW                        
                            FILE STATUS  IS WS-K730-STATUS.                     
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F500           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               LABEL RECORD IS STANDARD                         
                               RECORDING MODE  IS  F.                           
       COPY BP13F500.                                                           
                                                                                
       FD   BP13F730           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               LABEL RECORD IS STANDARD                         
                               RECORDING MODE  IS  F.                           
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K730           RECORD CONTAINS 500 CHARACTERS.                  
       COPY BP13K730.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K730-STATUS           PIC 9(2)   VALUE 00.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F500-READ             PIC 9(4)   VALUE ZEROES.                
           05  WS-K730-READ             PIC 9(4)   VALUE ZEROES.                
           05  WS-K730-NOTFND           PIC 9(4)   VALUE ZEROES.                
           05  WS-F730-REC-WRT          PIC 9(4)   VALUE ZEROES.                
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  WS-F500-EOF              PIC X      VALUE 'N'.                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 1500-F500-READ    THRU 1500-EXIT.                            
           PERFORM 2000-MAIN-ROUTINE THRU 2000-EXIT                             
                       UNTIL WS-F500-EOF = 'Y'.                                 
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F500                                                 
                       BP13K730                                                 
                OUTPUT BP13F730.                                                
                                                                                
           IF WS-K730-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K730 ERROR ' WS-K730-STATUS                  
              MOVE     WS-K730-STATUS TO RETURN-CODE                            
              PERFORM  9000-CLOSE-FILES THRU 9000-EXIT.                         
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1500-F500-READ.                                                          
      *-------------------------------------------------------------            
           READ BP13F500 AT END MOVE 'Y'  TO WS-F500-EOF                        
                            GO TO 1500-EXIT.                                    
                                                                                
           ADD  1                         TO WS-F500-READ.                      
                                                                                
       1500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE F500-NUM-REGN              TO K730-NUM-REGN.                    
           READ BP13K730.                                                       
           EVALUATE WS-K730-STATUS                                              
           WHEN 00                                                              
                ADD 1                      TO WS-K730-READ                      
                MOVE SPACES                TO BP13F730-REC                      
                INITIALIZE                    BP13F730-REC                      
                MOVE BP13K730-REC          TO BP13F730-REC                      
                WRITE BP13F730-REC                                              
                ADD 1 TO WS-F730-REC-WRT                                        
           WHEN 23                                                              
                ADD 1                      TO WS-K730-NOTFND                    
                DISPLAY "K730 REC NOT FND, STATUS:" WS-K730-STATUS              
                DISPLAY "K730-KEY-FLD  = "  F500-NUM-REGN                       
           WHEN OTHER                                                           
                MOVE WS-K730-STATUS        TO RETURN-CODE                       
                DISPLAY "BP13K730 READ FILE ERROR:" WS-K730-STATUS              
                PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
           PERFORM 1500-F500-READ         THRU 1500-EXIT.                       
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13C161 CONTROL '.                                         
           DISPLAY '-----------------------------------'.                       
           DISPLAY 'TOTAL F500 REC READ      = ' WS-F500-READ.                  
           DISPLAY 'TOTAL K730 REC READ      = ' WS-K730-READ.                  
           DISPLAY 'TOTAL F730 REC WRITE     = ' WS-F730-REC-WRT.               
           DISPLAY 'TOTAL K730 REC RECNFND   = ' WS-K730-NOTFND.                
                                                                                
           CLOSE      BP13F500                                                  
                      BP13F730                                                  
                      BP13K730.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
