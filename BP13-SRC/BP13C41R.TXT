      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C41R.                                                 
       AUTHOR.        ZARITA DANE SARMIENTO.                                    
       DATE-WRITTEN.  APR  27,2021.                                             
      *===============================================================*         
      * OBJECTIVE  :  GENERATE A REPORT FOR FTP WITH K800-DTE-SO-APPT *         
      *               ONLY FOR THE PREVIOUS MONTH                     *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :  1. BP13F400                                     *         
      *               2. BM06K100                                     *         
      *               5. BP13K800                                     *         
      *               6. BP13K820                                     *         
      *               7. BP13K830                                     *         
      *               8. BP13K857                                     *         
      *               9. BP13K893                                     *         
      *              10. BP13K895                                     *         
      * OUTPUT FILE:  1. BP13REPO                                     *         
      *               2. BP13NOTP                                     *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP138693 27/04/2021 ZAR7 NEW PROGRAM.                         *         
      * BP138693 03/06/2021 ZAR7 REVISED AND CORRECT CONDITIONS       *         
      * BP138693 24/06/2021 ZAR7 CATER FOR FT 1-3 ONLY ADD KEY-ISSUE  *         
      * BP138693 25/06/2021 ZAR7 CATER FOR PCD >=202105 AND <=202112  *         
      * BP138693 28/06/2021 ZAR7 BYPASS '## REG NUM                   *         
      * BP138693 05/07/2021 ZAR7 CHECK FOR 3 FT AND DTE-ACCEPTANCE    *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F400  ASSIGN        TO BP13F400.                         
                                                                                
                                                                                
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
           SELECT  BP13K820  ASSIGN        TO BP13K820                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K820-KEY-FLD                      
                             FILE STATUS   IS WS-K820-STATUS.                   
                                                                                
           SELECT  BP13K830  ASSIGN        TO BP13K830                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K830-KEY-FLD                      
                             ALTERNATE KEY IS K830-NUM-REGN                     
                             FILE STATUS   IS WS-K830-STATUS.                   
                                                                                
           SELECT  BP13K857  ASSIGN        TO BP13K857                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K857-KEY-FLD                      
                             FILE STATUS   IS WS-K857-STATUS.                   
                                                                                
           SELECT  BM06K100  ASSIGN        TO BM06K100                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K100-KEY-FLD                      
                             FILE STATUS   IS WS-K100-STATUS.                   
                                                                                
           SELECT  BM06K110  ASSIGN        TO BM06K110                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K110-KEY-FLD                      
                             FILE STATUS   IS WS-K110-STATUS.                   
                                                                                
           SELECT  BP13K893  ASSIGN        TO BP13K893                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K893-KEY-FLD                      
                             FILE STATUS   IS WS-K893-STATUS.                   
                                                                                
           SELECT  BP13K895  ASSIGN        TO BP13K895                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K895-KEY-FLD                      
                             FILE STATUS   IS WS-K895-STATUS.                   
                                                                                
           SELECT  BP13REPO  ASSIGN        TO BP13REPO.                         
           SELECT  BP13REPA  ASSIGN        TO BP13REPA.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400  CHARACTERS.                                    
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250  CHARACTERS.                                    
       COPY BP13K830.                                                           
                                                                                
       FD   BP13K857                                                            
            RECORD CONTAINS 200  CHARACTERS.                                    
       COPY BP13K857.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BP13K895                                                            
            RECORD CONTAINS 450  CHARACTERS.                                    
       COPY BP13K895.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BM06K100.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
       FD   BP13F400                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F400.                                                           
                                                                                
       FD   BP13REPO                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 1000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BP13REPO-REC.                                                       
            05  RPT-SERIAL-NO              PIC ZZZZZZ9.                         
            05  FILLER-1                   PIC X(01).                           
            05  RPT-FLAT-BUYER-NME1        PIC X(66).                           
            05  FILLER-2                   PIC X(01).                           
            05  RPT-FLAT-BUYER-NME2        PIC X(66).                           
            05  FILLER-3                   PIC X(01).                           
            05  RPT-FLAT-BUYER-NME3        PIC X(66).                           
            05  FILLER-4                   PIC X(01).                           
            05  RPT-FLAT-BUYER-NME4        PIC X(66).                           
            05  FILLER-5                   PIC X(01).                           
            05  RPT-NRIC1                  PIC X(09).                           
            05  FILLER-6                   PIC X(01).                           
            05  RPT-NRIC2                  PIC X(09).                           
            05  FILLER-7                   PIC X(01).                           
            05  RPT-NRIC3                  PIC X(09).                           
            05  FILLER-8                   PIC X(01).                           
            05  RPT-NRIC4                  PIC X(09).                           
            05  FILLER-9                   PIC X(01).                           
            05  RPT-NUM-PROJ-NAME          PIC X(40).                           
            05  FILLER-9A                  PIC X(01).                           
            05  RPT-NUM-BLK                PIC X(05).                           
            05  FILLER-10                  PIC X(01).                           
            05  RPT-HOUSE-NO               PIC X(12).                           
            05  FILLER-11                  PIC X(01).                           
            05  RPT-STREET-NAME            PIC X(32).                           
            05  FILLER-12                  PIC X(01).                           
            05  RPT-POSTAL                 PIC X(08).                           
            05  FILLER-13                  PIC X(01).                           
            05  RPT-FLAT-TYPE              PIC X(16).                           
            05  FILLER-14                  PIC X(01).                           
            05  RPT-DTE-PCD-QTR            PIC X(08).                           
            05  FILLER-15                  PIC X(01).                           
            05  RPT-DTE-KEY-COLL           PIC X(08).                           
            05  FILLER-16                  PIC X(01).                           
            05  RPT-CORR-ADDRESS           PIC X(99).                           
            05  FILLER-17                  PIC X(01).                           
            05  RPT-CORR-EMAIL-ADD         PIC X(50).                           
            05  FILLER                     PIC X(397).                          
                                                                                
       FD   BP13REPA                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 1000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BP13REPA-REC                   PIC X(1000).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K820-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K830-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K857-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K893-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K895-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K100-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K110-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-EOF-F400             PIC X(01) VALUE 'N'.                     
           05  WS-EOF-K893             PIC X(01) VALUE 'N'.                     
           05  WS-EOF-K895             PIC X(01) VALUE 'N'.                     
           05  WS-FIRST-REC            PIC X(01) VALUE 'Y'.                     
           05  WS-DTE-ACCEPT           PIC X(08) VALUE SPACES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F400-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-REPO-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-REPA-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT                  PIC 9(2)  VALUE ZEROES.                  
           05  WS-BYPASS               PIC 9(7)  VALUE ZEROES.                  
           05  WS-BYPASS-FT            PIC 9(7)  VALUE ZEROES.                  
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
       01 WS-LAST-MONTH.                                                        
          05  WS-LAST-MM                   PIC 9(02).                           
                                                                                
       01 WS-HEADER.                                                            
            05  HDR-SERIAL-NO       PIC X(07) VALUE 'S.NO '.                    
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-FLAT-BUYER-NME1 PIC X(66) VALUE 'FLAT BUYER NAME1'.         
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-FLAT-BUYER-NME2 PIC X(66) VALUE 'FLAT BUYER NAME2'.         
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-FLAT-BUYER-NME3 PIC X(66) VALUE 'FLAT BUYER NAME3'.         
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-FLAT-BUYER-NME4 PIC X(66) VALUE 'FLAT BUYER NAME4'.         
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-NRIC1           PIC X(09) VALUE 'NRIC1   '.                 
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-NRIC2           PIC X(09) VALUE 'NRIC2   '.                 
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-NRIC3           PIC X(09) VALUE 'NRIC3   '.                 
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-NRIC4           PIC X(09) VALUE 'NRIC4   '.                 
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-NUM-PROJ-NAME   PIC X(40) VALUE 'PROJ NAME'.                
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-NUM-BLK         PIC X(05) VALUE 'BLOCK'.                    
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-HOUSE-NO        PIC X(12) VALUE 'UNT-LEVEL '.               
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-STREET-NAME     PIC X(32) VALUE 'STREET NAME'.              
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HQR-POSTAL          PIC X(06) VALUE 'POSTAL'.                   
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-FLAT-TYPE       PIC X(16) VALUE 'FLAT TYPE'.                
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-PCD-QTR         PIC X(11) VALUE 'DTE PCD QTR'.              
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-DTE-KEY-COLL    PIC X(14) VALUE 'DTE KEY-ISSUED'.           
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-CORR-ADDRESS    PIC X(99) VALUE 'CORR ADDRESS'.             
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-EMAIL-ADD       PIC X(13) VALUE 'EMAIL ADDRESS'.            
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  FILLER              PIC X(426) VALUE SPACES.                    
                                                                                
       01 WS-BP13C913              PIC X(08)  VALUE 'BP13C913'.                 
       01 WS-BP13C916              PIC X(08)  VALUE 'BP13C916'.                 
                                                                                
       01 WS-NUM-HSEMAIN.                                                       
          05  WS-HASH                      PIC X(01) VALUE '#'.                 
          05  WS-NUM-LEVEL                 PIC X(02) VALUE SPACES.              
          05  WS-HIPHEN                    PIC X(01) VALUE '-'.                 
          05  WS-UNIT-MAIN                 PIC X(04) VALUE SPACES.              
          05  WS-UNIT-SUB                  PIC X(03) VALUE SPACES.              
                                                                                
       01  WS-K820-DTLS.                                                        
           05  WS-FIELD  OCCURS 4 TIMES.                                        
               10 WS-NRIC-K820          PIC X(9)     VALUE SPACES.              
               10 WS-NAME-K820          PIC X(66)    VALUE SPACES.              
                                                                                
       01   WS-DATA-ELEMENTS.                                                   
            05  WS-SERIAL-NUM              PIC 9(07) VALUE ZEROES.              
            05  WS-FLAT-BUYER-NME1         PIC X(66) VALUE SPACES.              
            05  WS-FLAT-BUYER-NME2         PIC X(66) VALUE SPACES.              
            05  WS-FLAT-BUYER-NME3         PIC X(66) VALUE SPACES.              
            05  WS-FLAT-BUYER-NME4         PIC X(66) VALUE SPACES.              
            05  WS-NRIC1                   PIC X(09) VALUE SPACES.              
            05  WS-NRIC2                   PIC X(09) VALUE SPACES.              
            05  WS-NRIC3                   PIC X(09) VALUE SPACES.              
            05  WS-NRIC4                   PIC X(09) VALUE SPACES.              
            05  WS-FLAT-TYPE               PIC X(02) VALUE ZEROES.              
            05  WS-DTE-PCD-QTR             PIC X(06) VALUE SPACES.              
            05  WS-DTE-KEY-COLL            PIC X(08) VALUE SPACES.              
            05  WS-CORR-EMAIL-ADD          PIC X(50) VALUE SPACES.              
            05  WS-CORR-ADDRESS            PIC X(99) VALUE SPACES.              
            05  WS-NUM-BLK                 PIC X(05) VALUE SPACES.              
            05  WS-HOUSE-NO                PIC X(11) VALUE SPACES.              
            05  WS-STREET-NAME             PIC X(32) VALUE SPACES.              
            05  WS-POSTAL                  PIC X(06) VALUE SPACES.              
            05  WS-NME-LOCAL-PRCNCT        PIC X(40) VALUE SPACES.              
                                                                                
       01  SQL-CODE                        PIC 9(4).                            
                                                                                
      *---------------------------------------------------------------*         
      *        LINKAGE FOR BP13C916                                   *         
      *---------------------------------------------------------------*         
       01  WS-LINK-REC-C916.                                                    
           05  WS-LINK-BLDNG-GL             PIC X(5).                           
                                                                                
       01  WS-BP13C916-REC.                                                     
           05  C916-NME-LOCAL-PRCNCT        PIC X(40).                          
           05  C916-CDE-SYSERR              PIC S9(3).                          
           05  C916-CDE-ERROR               PIC X(5).                           
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13C913                        *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH         PIC X(4).                            
               10  WS-LINK-NUM-ACC         PIC X(5).                            
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F400      THRU 2000-EXIT                       
             UNTIL WS-EOF-F400 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F400                                                 
                       BM06K100                                                 
                       BM06K110                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K830                                                 
                       BP13K857                                                 
                       BP13K893                                                 
                       BP13K895                                                 
                OUTPUT BP13REPO                                                 
                       BP13REPA.                                                
                                                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR OPENING : ' WS-K820-STATUS              
              MOVE WS-K820-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K830 - ERROR OPENING : ' WS-K830-STATUS              
              MOVE WS-K830-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K857 - ERROR OPENING : ' WS-K857-STATUS              
              MOVE WS-K857-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K893 - ERROR OPENING : ' WS-K893-STATUS              
              MOVE WS-K893-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K895-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K895 - ERROR OPENING : ' WS-K895-STATUS              
              MOVE WS-K895-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K100 - ERROR OPENING : ' WS-K100-STATUS              
              MOVE WS-K100-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR OPENING : ' WS-K110-STATUS              
              MOVE WS-K110-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-FIRST-REC = 'Y'                                                
              WRITE BP13REPO-REC FROM  WS-HEADER                                
              WRITE BP13REPA-REC FROM  WS-HEADER                                
              MOVE 'N' TO WS-FIRST-REC                                          
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
           IF WS-CURR-MM = 1                                                    
              MOVE 12 TO WS-LAST-MM                                             
           ELSE                                                                 
              COMPUTE WS-LAST-MM = WS-CURR-MM - 1                               
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F400.                                                      
      ******************************************************************        
                                                                                
           READ BP13F400           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F400                             
                GO                 TO   2000-EXIT.                              
           ADD  1                  TO WS-F400-READ.                             
                                                                                
           IF F400-NUM-REGN (1:1) NOT  = '#'                                    
             PERFORM 3000-READ-INPUT-FILES      THRU 3000-EXIT                  
           ELSE                                                                 
             ADD 1                       TO WS-BYPASS                           
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3000-READ-INPUT-FILES.                                                   
      *************************************************************             
                                                                                
           PERFORM 3100-READ-BP13K800  THRU 3100-EXIT.                          
                                                                                
           PERFORM 3400-READ-BM06K110  THRU 3400-EXIT                           
                                                                                
           PERFORM 3500-READ-BP13K830  THRU 3500-EXIT                           
           PERFORM 3600-READ-BP13K857  THRU 3600-EXIT                           
                                                                                
           IF F400-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                      
               MOVE  SPACES                  TO  BP13COMM8-REC                  
               INITIALIZE                        BP13COMM8-REC                  
               MOVE F400-NUM-SCH-ACC (1:9)    TO  WS-LINK-REC                   
                                                                                
               PERFORM 3800-CALL-BP13C913  THRU 3800-EXIT                       
               MOVE ZEROES   TO COMM8-CDE-SYSERR                                
               IF COMM8-CDE-SYSERR = 0                                          
                  PERFORM 3850-MOVE-PIDB-DETLS THRU 3850-EXIT                   
               ELSE                                                             
                  CONTINUE                                                      
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 4100-MOVE-FIELDS    THRU 4100-EXIT.                          
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3100-READ-BP13K800.                                                      
      *************************************************************             
           MOVE SPACES                   TO BP13K800-MASTER                     
                                            WS-DTE-ACCEPT.                      
           INITIALIZE                       BP13K800-MASTER.                    
                                                                                
           MOVE F400-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    MOVE K800-NUM-NRIC1        TO WS-NRIC-K820(1)               
                    MOVE K800-NUM-NRIC2        TO WS-NRIC-K820(2)               
                    MOVE K800-NUM-NRIC3        TO WS-NRIC-K820(3)               
                    MOVE K800-NUM-NRIC4        TO WS-NRIC-K820(4)               
                    MOVE K800-NUM-FLAT-TYPE    TO WS-FLAT-TYPE                  
                    MOVE K800-NUM-NRIC1        TO WS-NRIC1                      
                    MOVE K800-NUM-NRIC2        TO WS-NRIC2                      
                    MOVE K800-NUM-NRIC3        TO WS-NRIC3                      
                    MOVE K800-NUM-NRIC4        TO WS-NRIC4                      
                    MOVE K800-DTE-ACCEPTANCE   TO WS-DTE-ACCEPT                 
                    PERFORM 3200-READ-BP13K820  THRU 3200-EXIT                  
                          VARYING WS-CNT FROM 1 BY 1 UNTIL WS-CNT > 4           
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO BP13K800-MASTER                     
                    INITIALIZE              BP13K800-MASTER                     
                    PERFORM 3150-READ-BP13K893 THRU 3150-EXIT                   
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    MOVE WS-K800-STATUS          TO RETURN-CODE                 
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3150-READ-BP13K893.                                                      
      *************************************************************             
           MOVE SPACES                   TO BP13K893-MASTER.                    
           INITIALIZE                       BP13K893-MASTER.                    
                                                                                
           MOVE F400-NUM-REGN            TO K893-KEY-FLD.                       
                                                                                
           START  BP13K893 KEY  >= K893-KEY-FLD                                 
                                                                                
           EVALUATE WS-K893-STATUS                                              
               WHEN 00                                                          
                 READ BP13K893 NEXT RECORD                                      
                  AT END MOVE 'Y'    TO WS-EOF-K893                             
                  NOT AT END                                                    
                                                                                
                  IF WS-K893-STATUS NOT = '00'                                  
                     MOVE WS-K893-STATUS       TO RETURN-CODE                   
                     DISPLAY 'READ ERROR IN K893 ' F400-NUM-REGN                
                     PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                  
                  END-IF                                                        
                                                                                
                  IF K893-NUM-REGN-HIST = F400-NUM-REGN                         
                     MOVE K893-NUM-NRIC1        TO WS-NRIC-K820(1)              
                     MOVE K893-NUM-NRIC2        TO WS-NRIC-K820(2)              
                     MOVE K893-NUM-NRIC3        TO WS-NRIC-K820(3)              
                     MOVE K893-NUM-NRIC4        TO WS-NRIC-K820(4)              
                     MOVE K893-NUM-FLAT-TYPE    TO WS-FLAT-TYPE                 
                     MOVE K893-NUM-NRIC1        TO WS-NRIC1                     
                     MOVE K893-NUM-NRIC2        TO WS-NRIC2                     
                     MOVE K893-NUM-NRIC3        TO WS-NRIC3                     
                     MOVE K893-NUM-NRIC4        TO WS-NRIC4                     
                     MOVE K893-DTE-ACCEPTANCE   TO WS-DTE-ACCEPT                
                     PERFORM 3200-READ-BP13K820  THRU 3200-EXIT                 
                          VARYING WS-CNT FROM 1 BY 1 UNTIL WS-CNT > 4           
                     MOVE 'Y' TO WS-EOF-K893                                    
                  ELSE                                                          
                     CONTINUE                                                   
                  END-IF                                                        
                 END-READ                                                       
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO BP13K893-MASTER                     
                    INITIALIZE              BP13K893-MASTER                     
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K893 READ ERROR ' WS-K893-STATUS               
                    MOVE WS-K893-STATUS          TO RETURN-CODE                 
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3150-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3200-READ-BP13K820.                                                      
      *************************************************************             
           IF  WS-NRIC-K820(WS-CNT) NOT = SPACES AND LOW-VALUES                 
               MOVE SPACES                   TO BP13K820-REC                    
               INITIALIZE                       BP13K820-REC                    
               MOVE F400-NUM-REGN            TO K820-NUM-REGN                   
               MOVE WS-NRIC-K820(WS-CNT)     TO K820-NUM-NRIC                   
            ELSE                                                                
               MOVE  SPACES                  TO WS-NAME-K820(WS-CNT)            
               GO TO 3200-EXIT                                                  
           END-IF.                                                              
                                                                                
           READ BP13K820 KEY IS K820-KEY-FLD.                                   
           EVALUATE WS-K820-STATUS                                              
             WHEN '00'                                                          
               IF K820-NME-OCCP   NOT = SPACES AND LOW-VALUES                   
                  MOVE  K820-NME-OCCP   TO WS-NAME-K820(WS-CNT)                 
               ELSE                                                             
                  MOVE  SPACES          TO WS-NAME-K820(WS-CNT)                 
               END-IF                                                           
                                                                                
             WHEN 10                                                            
             WHEN 23                                                            
                  MOVE SPACES           TO BP13K820-REC                         
                  INITIALIZE               BP13K820-REC                         
                  PERFORM 3250-READ-BP13K895 THRU 3250-EXIT                     
                                                                                
             WHEN OTHER                                                         
                  DISPLAY 'BP13K820 READ ERROR ' WS-K820-STATUS                 
                  MOVE WS-K820-STATUS          TO RETURN-CODE                   
                  PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3250-READ-BP13K895.                                                      
      *************************************************************             
                                                                                
           IF  WS-NRIC-K820(WS-CNT) NOT = SPACES AND LOW-VALUES                 
               MOVE SPACES                   TO BP13K895-REC                    
               INITIALIZE                       BP13K895-REC                    
               MOVE F400-NUM-REGN            TO K895-NUM-REGN-HIST              
               MOVE WS-NRIC-K820(WS-CNT)     TO K895-NUM-NRIC-HIST              
            ELSE                                                                
               MOVE  SPACES                  TO WS-NAME-K820(WS-CNT)            
               GO TO 3250-EXIT                                                  
           END-IF.                                                              
                                                                                
           START  BP13K895 KEY  >= K895-KEY-FLD                                 
                                                                                
           EVALUATE WS-K895-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                 READ BP13K895 NEXT RECORD                                      
                  AT END MOVE 'Y'    TO WS-EOF-K895                             
                  NOT AT END                                                    
                                                                                
                  IF WS-K895-STATUS NOT = '00'                                  
                     MOVE SPACES               TO WS-NAME-K820(WS-CNT)          
                     MOVE WS-K895-STATUS       TO RETURN-CODE                   
                     DISPLAY 'READ ERROR IN K895' F400-NUM-REGN                 
                     PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                  
                  END-IF                                                        
                                                                                
                  IF K895-NUM-REGN-HIST = F400-NUM-REGN AND                     
                     K895-NUM-NRIC-HIST = WS-NRIC-K820(WS-CNT)                  
                      MOVE  K895-NME-OCCP   TO WS-NAME-K820(WS-CNT)             
                      MOVE 'Y' TO WS-EOF-K895                                   
                  ELSE                                                          
                      MOVE  SPACES          TO WS-NAME-K820(WS-CNT)             
                      MOVE 'Y' TO WS-EOF-K895                                   
                  END-IF                                                        
                 END-READ                                                       
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO BP13K895-REC                        
                    INITIALIZE              BP13K895-REC                        
                    MOVE SPACES          TO WS-NAME-K820(WS-CNT)                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K895 READ ERROR ' WS-K895-STATUS               
                    MOVE WS-K895-STATUS          TO RETURN-CODE                 
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3250-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3400-READ-BM06K110.                                                      
      *************************************************************             
           MOVE SPACES                   TO K110-REC.                           
           INITIALIZE                       K110-REC.                           
                                                                                
           MOVE F400-NUM-SCH-ACC         TO K110-KEY-FLD.                       
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3450-READ-BM06K100  THRU 3450-EXIT                  
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO K100-REC                            
                    INITIALIZE              K100-REC                            
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BM06K110 READ ERROR ' WS-K110-STATUS               
                    MOVE WS-K110-STATUS          TO RETURN-CODE                 
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3450-READ-BM06K100.                                                      
      *************************************************************             
           MOVE SPACES                   TO K100-REC.                           
           INITIALIZE                       K100-REC.                           
                                                                                
           MOVE K110-AIX1                TO K100-KEY-FLD.                       
                                                                                
           READ BM06K100.                                                       
                                                                                
           EVALUATE WS-K100-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    MOVE K100-DTE-PACD-QTR     TO WS-DTE-PCD-QTR                
                    MOVE K100-CDE-BLK TO WS-LINK-BLDNG-GL                       
      *             DISPLAY 'BLDNG : ' WS-LINK-BLDNG-GL                         
                                                                                
                    CALL WS-BP13C916  USING WS-LINK-REC-C916                    
                                            WS-BP13C916-REC                     
                    MOVE C916-NME-LOCAL-PRCNCT TO                               
                                            WS-NME-LOCAL-PRCNCT                 
      *             MOVE 'TEST PROJ'           TO                               
      *                                     WS-NME-LOCAL-PRCNCT                 
      *             DISPLAY 'PRCNCT: ' C916-NME-LOCAL-PRCNCT                    
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO K100-REC                            
                    INITIALIZE              K100-REC                            
                    MOVE SPACES          TO WS-DTE-PCD-QTR                      
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BM06K100 READ ERROR ' WS-K100-STATUS               
                    MOVE WS-K100-STATUS          TO RETURN-CODE                 
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3450-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3500-READ-BP13K830.                                                      
      *************************************************************             
           MOVE SPACES                   TO BP13K830-REC.                       
           INITIALIZE                       BP13K830-REC.                       
                                                                                
           MOVE F400-NUM-REGN            TO K830-NUM-REGN.                      
                                                                                
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K830-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                                                                                
                IF K830-NUM-MAIL-ADDR NOT = SPACES AND LOW-VALUES               
                   MOVE K830-NUM-MAIL-ADDR(2:98) TO WS-CORR-ADDRESS             
                ELSE                                                            
                 IF K830-NUM-RESIDENT-ADDR NOT = SPACES AND LOW-VALUES          
                   MOVE K830-NUM-RESIDENT-ADDR(2:98) TO WS-CORR-ADDRESS         
                   ELSE                                                         
                      MOVE SPACES                 TO WS-CORR-ADDRESS            
                  END-IF                                                        
                 END-IF                                                         
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO BP13K830-REC                        
                    INITIALIZE              BP13K830-REC                        
                    MOVE SPACES          TO WS-CORR-ADDRESS                     
               WHEN OTHER                                                       
                    DISPLAY 'BP13K830 READ ERROR ' WS-K830-STATUS               
                    MOVE WS-K830-STATUS          TO RETURN-CODE                 
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3600-READ-BP13K857.                                                      
      *************************************************************             
           MOVE SPACES                   TO BP13K857-REC.                       
           INITIALIZE                       BP13K857-REC.                       
                                                                                
           MOVE F400-NUM-REGN            TO K857-KEY-FLD.                       
                                                                                
           READ BP13K857.                                                       
                                                                                
           EVALUATE WS-K857-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                                                                                
                 IF K857-NUM-EMAIL NOT = SPACES AND LOW-VALUES                  
                    MOVE K857-NUM-EMAIL      TO WS-CORR-EMAIL-ADD               
                 ELSE                                                           
                    MOVE SPACES              TO WS-CORR-EMAIL-ADD               
                 END-IF                                                         
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO BP13K857-REC                        
                    INITIALIZE              BP13K857-REC                        
                    MOVE SPACES          TO WS-CORR-EMAIL-ADD                   
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K857 READ ERROR ' WS-K857-STATUS               
                    MOVE WS-K857-STATUS          TO RETURN-CODE                 
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3800-CALL-BP13C913.                                                      
      ******************************************************************        
      *    CONTINUE.                                                            
            CALL WS-BP13C913 USING WS-LINK-REC,                                 
                                  BP13COMM8-REC.                                
       3800-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3850-MOVE-PIDB-DETLS.                                                    
      ******************************************************************        
      *    CONTINUE.                                                            
           MOVE COMM8-NUM-LEVEL       TO  WS-NUM-LEVEL.                         
           MOVE COMM8-NUM-UNIT-MAIN   TO  WS-UNIT-MAIN.                         
           MOVE COMM8-NUM-UNIT-SUB    TO  WS-UNIT-SUB.                          
           MOVE COMM8-NUM-BLK         TO  WS-NUM-BLK.                           
           MOVE COMM8-NME-STREET      TO  WS-STREET-NAME.                       
           MOVE COMM8-NUM-POSTAL-CODE TO  WS-POSTAL.                            
           MOVE WS-NUM-HSEMAIN        TO  WS-HOUSE-NO.                          
                                                                                
       3850-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       4100-MOVE-FIELDS.                                                        
      ******************************************************************        
           MOVE SPACES                  TO BP13REPO-REC.                        
           INITIALIZE                      BP13REPO-REC.                        
                                                                                
           MOVE SPACES                  TO BP13REPA-REC.                        
           INITIALIZE                      BP13REPA-REC.                        
                                                                                
           MOVE  ';' TO FILLER-1  FILLER-2  FILLER-3 FILLER-4  FILLER-5         
                        FILLER-6  FILLER-7  FILLER-8 FILLER-9  FILLER-10        
                        FILLER-9A                                               
                        FILLER-11 FILLER-12 FILLER-13 FILLER-14                 
                        FILLER-15 FILLER-16 FILLER-17.                          
                                                                                
           MOVE  WS-NAME-K820(1)       TO RPT-FLAT-BUYER-NME1                   
           MOVE  WS-NAME-K820(2)       TO RPT-FLAT-BUYER-NME2                   
           MOVE  WS-NAME-K820(3)       TO RPT-FLAT-BUYER-NME3                   
           MOVE  WS-NAME-K820(4)       TO RPT-FLAT-BUYER-NME4                   
           MOVE  WS-NRIC1              TO RPT-NRIC1                             
           MOVE  WS-NRIC2              TO RPT-NRIC2                             
           MOVE  WS-NRIC3              TO RPT-NRIC3                             
           MOVE  WS-NRIC4              TO RPT-NRIC4                             
           MOVE  SPACES                TO RPT-FLAT-TYPE                         
           IF WS-FLAT-TYPE(1:1) = '1'                                           
              MOVE 'STUDIO APARTMENT'  TO RPT-FLAT-TYPE                         
           END-IF.                                                              
           IF WS-FLAT-TYPE(1:1) = '2'                                           
              MOVE '2-ROOM          '  TO RPT-FLAT-TYPE                         
           END-IF.                                                              
           IF WS-FLAT-TYPE(1:1) = '3'                                           
              MOVE '3-ROOM          '  TO RPT-FLAT-TYPE                         
           END-IF.                                                              
           MOVE  WS-DTE-PCD-QTR        TO RPT-DTE-PCD-QTR                       
           MOVE  WS-CORR-EMAIL-ADD     TO RPT-CORR-EMAIL-ADD                    
           MOVE  WS-CORR-ADDRESS       TO RPT-CORR-ADDRESS                      
           MOVE  WS-NUM-BLK            TO RPT-NUM-BLK                           
           MOVE  WS-HOUSE-NO           TO RPT-HOUSE-NO                          
           MOVE  WS-STREET-NAME        TO RPT-STREET-NAME                       
           MOVE  WS-POSTAL             TO RPT-POSTAL                            
           MOVE  F400-DTE-KEY-ISSUE    TO RPT-DTE-KEY-COLL                      
           MOVE  WS-NME-LOCAL-PRCNCT   TO RPT-NUM-PROJ-NAME                     
                                                                                
           IF WS-FLAT-TYPE(1:1) = SPACES OR                                     
              WS-FLAT-TYPE(1:1) = 'E' OR                                        
              WS-FLAT-TYPE(1:1) >= 4                                            
             ADD 1                       TO WS-BYPASS-FT                        
             INITIALIZE                        WS-DATA-ELEMENTS                 
             INITIALIZE                        WS-K820-DTLS                     
             GO TO 4100-EXIT                                                    
           END-IF.                                                              
                                                                                
           IF WS-DTE-PCD-QTR   >= '202105' AND                                  
              WS-DTE-PCD-QTR   <= '202112'                                      
              IF WS-DTE-ACCEPT <= '20210620'                                    
                ADD 1                       TO WS-CNT-REPA-WRITE                
                MOVE WS-CNT-REPA-WRITE      TO RPT-SERIAL-NO                    
                WRITE BP13REPA-REC   FROM BP13REPO-REC                          
              ELSE                                                              
                DISPLAY 'REG / DTE-ACCEPT - ' WS-DTE-ACCEPT '/'                 
                                              F400-NUM-REGN                     
                ADD 1                       TO WS-BYPASS                        
              END-IF                                                            
           ELSE                                                                 
              IF F400-DTE-KEY-ISSUE >= 20201128 AND                             
                F400-DTE-KEY-ISSUE <= 20210630                                  
                ADD 1                       TO WS-CNT-REPO-WRITE                
                MOVE WS-CNT-REPO-WRITE      TO RPT-SERIAL-NO                    
                WRITE BP13REPO-REC                                              
              ELSE                                                              
                DISPLAY 'REG / DTE-KEY-ISS- ' F400-DTE-KEY-ISSUE '/'            
                                              F400-NUM-REGN                     
                ADD 1                       TO WS-BYPASS                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           INITIALIZE                        WS-DATA-ELEMENTS.                  
           INITIALIZE                        WS-K820-DTLS.                      
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C41R'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F400 RECORDS READ............. : '            
                    WS-F400-READ.                                               
           DISPLAY '(2) NO OF BP13REPO(WITH TP APPT DATE) WRITTEN: '            
                    WS-CNT-REPO-WRITE.                                          
           DISPLAY '(3) NO OF BP13NOTP(NO TP APPT DATE) WRITTEN  : '            
                    WS-CNT-REPA-WRITE.                                          
           DISPLAY '(4) NO OF BYPASS CASES                       : '            
                    WS-BYPASS.                                                  
           DISPLAY '(4) NO OF BYPASS CASES(FLAT TYPE 4 ABOVE)    : '            
                    WS-BYPASS-FT.                                               
                                                                                
           CLOSE    BP13F400                                                    
                    BM06K110                                                    
                    BM06K100                                                    
                    BP13K800                                                    
                    BP13K820                                                    
                    BP13K830                                                    
                    BP13K857                                                    
                    BP13K893                                                    
                    BP13K895                                                    
                    BP13REPO                                                    
                    BP13REPA.                                                   
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K100 - ERROR CLOSING : ' WS-K100-STATUS              
              MOVE WS-K100-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR CLOSING : ' WS-K110-STATUS              
              MOVE WS-K110-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR CLOSING : ' WS-K820-STATUS              
              MOVE WS-K820-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K830 - ERROR CLOSING : ' WS-K830-STATUS              
              MOVE WS-K830-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K857 - ERROR CLOSING : ' WS-K857-STATUS              
              MOVE WS-K857-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K893 - ERROR CLOSING : ' WS-K893-STATUS              
              MOVE WS-K893-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K895-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K895 - ERROR CLOSING : ' WS-K895-STATUS              
              MOVE WS-K895-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
