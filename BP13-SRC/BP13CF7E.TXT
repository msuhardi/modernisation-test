       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF7E.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  03 DECEMBER 2015.                                         
      * ==========================================================*             
      *                                                           *             
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                     *             
      *    SYSTEM ID   : BP13                                     *             
      *    OBJECTIVE   : TO CALCULATE THE TOTAL NUMBER OF         *             
      *                  UNITS FOR THE DIFFERENT ALLOC SCH.       *             
      *                                                           *             
      * ==========================================================*             
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                     *             
      * --------  ----  --------  --------------------------------*             
      * BP136096  ESA1  01122015  NEW PROGRAM(MODEL AFTER BP13CF78*             
      * BP138924  SMP3  11102021  REMOVE HARDCODING ON ELDER-SPS  *             
      *                           AND ELDER-NONSPS                *             
      * ==========================================================*             
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F010 ASSIGN TO BP13F010.                                  
                                                                                
           SELECT BP13F713 ASSIGN TO BP13F713.                                  
                                                                                
           SELECT BP13K010 ASSIGN TO BP13K010                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K010-KEY-FLD                               
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F010                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F010.                                                           
                                                                                
       FD  BP13K010                                                             
           RECORD CONTAINS 600 CHARACTERS.                                      
       COPY BP13K010.                                                           
                                                                                
       FD  BP13F713                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 20 CHARACTERS                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F713.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       COPY PRIOSCH.                                                            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 WS-K010-STATUS              PIC 99    VALUE ZEROS.                
                                                                                
       01  WS-COUNTER.                                                          
           05 WS-F010-READ                PIC 9(5)  VALUE ZEROS.                
           05 WS-K010-REWRITE             PIC 9(5)  VALUE ZEROS.                
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-TOT-ELDER-SPS            PIC 9(5)  VALUE ZEROS.                
           05 WS-TOT-ELDER-NONSPS         PIC 9(5)  VALUE ZEROS.                
           05 WS-MAX-PA1                  PIC 9(5)  VALUE ZEROS.                
           05 WS-MAX-PA2                  PIC 9(5)  VALUE ZEROS.                
           05 WS-MAX-PA3                  PIC 9(5)  VALUE ZEROS.                
           05 WS-DIFF-SPS                 PIC 9(5)  VALUE ZEROS.                
           05 WS-DIFF-NSPS                PIC 9(5)  VALUE ZEROS.                
                                                                                
       01 WS-K010-FND                     PIC X(1)  VALUE 'N'.                  
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-CC                       PIC 99.                               
           05 WS-YY                       PIC 99.                               
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
                                                                                
       01  WS-SYS-TIME                    PIC 9(8).                             
                                                                                
       01  WS-EOF-F010                    PIC 9     VALUE 0.                    
           88 EOF-F010                    VALUE 1.                              
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 250-READ-F713      THRU 250-EXIT.                            
           PERFORM 200-READ-F010      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-F010   THRU 300-EXIT                             
             UNTIL EOF-F010.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       100-INITIALIZATION.                                                      
      ******************************************************************        
           OPEN INPUT  BP13F713                                                 
                       BP13F010                                                 
                I-O    BP13K010.                                                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              MOVE WS-K010-STATUS            TO RETURN-CODE                     
              DISPLAY 'ERROR OPENING BP13K010'                                  
              GO TO 999-CLOSE-FILES                                             
           END-IF.                                                              
                                                                                
           ACCEPT WS-SYS-DATE              FROM DATE YYYYMMDD.                  
           ACCEPT WS-SYS-TIME              FROM TIME.                           
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       200-READ-F010.                                                           
      ******************************************************************        
           READ BP13F010 AT END                                                 
                MOVE 1 TO WS-EOF-F010                                           
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1  TO WS-F010-READ.                                              
                                                                                
           IF F010-NUM-TOT-NON-ELDER NOT NUMERIC                                
              MOVE ZEROES        TO F010-NUM-TOT-NON-ELDER                      
           END-IF.                                                              
           IF F010-NUM-TOT-ELDER-SPS NOT NUMERIC                                
              MOVE ZEROES        TO F010-NUM-TOT-ELDER-SPS                      
           END-IF.                                                              
           IF F010-NUM-TOT-ELDER-NONSPS NOT NUMERIC                             
              MOVE ZEROES        TO F010-NUM-TOT-ELDER-NONSPS                   
           END-IF.                                                              
                                                                                
           IF F010-NUM-REG-ELDER     NOT NUMERIC                                
              MOVE ZEROES        TO F010-NUM-REG-ELDER                          
           END-IF.                                                              
           IF F010-NUM-REG-NON-ELDER NOT NUMERIC                                
              MOVE ZEROES        TO F010-NUM-REG-NON-ELDER                      
           END-IF.                                                              
           IF F010-NUM-REG-ELDER-SPS NOT NUMERIC                                
              MOVE ZEROES        TO F010-NUM-REG-ELDER-SPS                      
           END-IF.                                                              
           IF F010-NUM-REG-ELDER-NONSPS NOT NUMERIC                             
              MOVE ZEROES        TO F010-NUM-REG-ELDER-NONSPS                   
           END-IF.                                                              
                                                                                
           IF F010-NUM-APP-ELDER     NOT NUMERIC                                
              MOVE ZEROES        TO F010-NUM-APP-ELDER                          
           END-IF.                                                              
           IF F010-NUM-APP-NON-ELDER NOT NUMERIC                                
              MOVE ZEROES        TO F010-NUM-APP-NON-ELDER                      
           END-IF.                                                              
           IF F010-NUM-APP-ELDER-SPS NOT NUMERIC                                
              MOVE ZEROES        TO F010-NUM-APP-ELDER-SPS                      
           END-IF.                                                              
           IF F010-NUM-APP-ELDER-NONSPS NOT NUMERIC                             
              MOVE ZEROES        TO F010-NUM-APP-ELDER-NONSPS                   
           END-IF.                                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       250-READ-F713.                                                           
      ******************************************************************        
           READ BP13F713 AT END                                                 
                DISPLAY 'BP13F713 EMPTY FILE '                                  
                GO TO 999-CLOSE-FILES.                                          
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-F010.                                                        
      ******************************************************************        
                                                                                
           IF F010-KEY-FLD = SPACES OR LOW-VALUES                               
              PERFORM 200-READ-F010           THRU 200-EXIT                     
              GO TO 300-EXIT                                                    
           END-IF.                                                              
                                                                                
           PERFORM 400-READ-BP13K010 THRU 400-EXIT.                             
                                                                                
           INITIALIZE WS-VARIABLES.                                             
                                                                                
           IF F713-CDE-COHORT = 'E '                                            
              IF WS-K010-FND = 'Y'                                              
                 MOVE F010-NUM-REG-ELDER  TO K010-NUM-TOT-ELDER                 
                 COMPUTE K010-NUM-TOT-NON-ELDER = ((F010-NUM-TOT-PA -           
                        (K010-REG-PA1 + K010-REG-PA2 + K010-REG-PA3)) -         
                         K010-NUM-TOT-ELDER)                                    
                                                                                
                 MOVE F010-NUM-REG-ELDER-SPS                                    
                                          TO K010-NUM-TOT-ELDER-SPS             
                 MOVE F010-NUM-REG-ELDER-NONSPS                                 
                                          TO K010-NUM-TOT-ELDER-NONSPS          
                 MOVE F010-REG-ELDER1     TO K010-NUM-MAX-ELDER1                
                 MOVE F010-REG-ELDER2     TO K010-NUM-MAX-ELDER2                
                 MOVE F010-REG-ELDER3     TO K010-NUM-MAX-ELDER3                
                                                                                
                 COMPUTE WS-TOT-ELDER-SPS    = (K010-NUM-TOT-ELDER *            
                                                K010-NUM-ELDER-SPS) /           
                                                100                             
                 COMPUTE WS-TOT-ELDER-NONSPS = (K010-NUM-TOT-ELDER *            
                                                K010-NUM-ELDER-NONSPS) /        
                                                100                             
                 COMPUTE WS-DIFF-SPS  = F010-NUM-APP-ELDER-SPS -                
                                        F010-NUM-REG-ELDER-SPS                  
                 COMPUTE WS-DIFF-NSPS = F010-NUM-APP-ELDER-NONSPS -             
                                        F010-NUM-REG-ELDER-NONSPS               
                                                                                
                 IF F010-NUM-APP-ELDER-SPS < WS-TOT-ELDER-SPS AND               
                    F010-NUM-APP-ELDER-SPS > F010-NUM-REG-ELDER-SPS AND         
                    K010-NUM-TOT-NON-ELDER > WS-DIFF-SPS                        
                    ADD WS-DIFF-SPS       TO K010-NUM-TOT-ELDER                 
                    SUBTRACT WS-DIFF-SPS  FROM K010-NUM-TOT-NON-ELDER           
                    MOVE F010-NUM-APP-ELDER-SPS                                 
                                          TO K010-NUM-TOT-ELDER-SPS             
                    COMPUTE K010-NUM-TOT-ELDER-NONSPS =                         
                            K010-NUM-TOT-ELDER -                                
                            K010-NUM-TOT-ELDER-SPS                              
                    COMPUTE WS-MAX-PA1 = K010-NUM-MAX-PA1 -                     
                                         K010-REG-PA1                           
                    COMPUTE WS-MAX-PA2 = K010-NUM-MAX-PA2 -                     
                                         K010-REG-PA2                           
                    COMPUTE WS-MAX-PA3 = K010-NUM-MAX-PA3 -                     
                                         K010-REG-PA3                           
                    MOVE WS-MAX-PA1       TO K010-NUM-MAX-ELDER1                
                    MOVE WS-MAX-PA2       TO K010-NUM-MAX-ELDER2                
                    MOVE WS-MAX-PA3       TO K010-NUM-MAX-ELDER3                
                 END-IF                                                         
                                                                                
                 IF F010-NUM-APP-ELDER-NONSPS < WS-TOT-ELDER-NONSPS AND         
                    F010-NUM-APP-ELDER-NONSPS >                                 
                    F010-NUM-REG-ELDER-NONSPS AND                               
                    K010-NUM-TOT-NON-ELDER > WS-DIFF-NSPS                       
                    ADD WS-DIFF-NSPS      TO K010-NUM-TOT-ELDER                 
                    SUBTRACT WS-DIFF-NSPS FROM K010-NUM-TOT-NON-ELDER           
                    MOVE F010-NUM-APP-ELDER-NONSPS                              
                                          TO K010-NUM-TOT-ELDER-NONSPS          
                    COMPUTE K010-NUM-TOT-ELDER-SPS =                            
                            K010-NUM-TOT-ELDER -                                
                            K010-NUM-TOT-ELDER-NONSPS                           
                    COMPUTE WS-MAX-PA1 = K010-NUM-MAX-PA1 -                     
                                         K010-REG-PA1                           
                    COMPUTE WS-MAX-PA2 = K010-NUM-MAX-PA2 -                     
                                         K010-REG-PA2                           
                    COMPUTE WS-MAX-PA3 = K010-NUM-MAX-PA3 -                     
                                         K010-REG-PA3                           
                    MOVE WS-MAX-PA1       TO K010-NUM-MAX-ELDER1                
                    MOVE WS-MAX-PA2       TO K010-NUM-MAX-ELDER2                
                    MOVE WS-MAX-PA3       TO K010-NUM-MAX-ELDER3                
                 END-IF                                                         
                                                                                
                 MOVE F010-NUM-APP-ELDER        TO K010-NUM-APP-ELDER           
                 MOVE F010-NUM-APP-NON-ELDER    TO                              
                      K010-NUM-APP-NON-ELDER                                    
                 MOVE F010-NUM-APP-ELDER-SPS    TO                              
                      K010-NUM-APP-ELDER-SPS                                    
                 MOVE F010-NUM-APP-ELDER-NONSPS TO                              
                      K010-NUM-APP-ELDER-NONSPS                                 
                                                                                
                 MOVE ZEROES              TO K010-REG-ELDER1                    
                                             K010-REG-ELDER2                    
                                             K010-REG-ELDER3                    
                 PERFORM 500-UPDATE-BP13K010 THRU 500-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F713-CDE-COHORT = 'PA'                                            
              IF WS-K010-FND = 'Y'                                              
                 ADD  K010-REG-ELDER1     TO K010-REG-PA1                       
                 ADD  K010-REG-ELDER2     TO K010-REG-PA2                       
                 ADD  K010-REG-ELDER3     TO K010-REG-PA3                       
                 COMPUTE K010-NUM-TOT-NON-ELDER = (K010-NUM-TOT-PA -            
                        (K010-REG-PA1 + K010-REG-PA2 + K010-REG-PA3))           
                 PERFORM 500-UPDATE-BP13K010 THRU 500-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F010           THRU 200-EXIT.                       
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-READ-BP13K010.                                                       
      ******************************************************************        
           MOVE 'N'           TO WS-K010-FND.                                   
           MOVE F010-KEY-FLD  TO K010-KEY-FLD.                                  
           READ BP13K010.                                                       
                                                                                
           EVALUATE WS-K010-STATUS                                              
              WHEN 00                                                           
                   MOVE 'Y'       TO WS-K010-FND                                
              WHEN 23                                                           
                   DISPLAY 'ERROR READING BP13K010, STATUS '                    
                   WS-K010-STATUS ' KEY ' K010-KEY-FLD                          
                   INITIALIZE BP13K010-TENDER                                   
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13K010, STATUS '                    
                   WS-K010-STATUS ' KEY ' K010-KEY-FLD                          
                   INITIALIZE BP13K010-TENDER                                   
           END-EVALUATE.                                                        
                                                                                
           IF K010-NUM-TOT-PA NOT NUMERIC                                       
              MOVE ZEROES        TO K010-NUM-TOT-PA                             
           END-IF.                                                              
           IF K010-REG-PA1 NOT NUMERIC                                          
              MOVE ZEROES        TO K010-REG-PA1                                
           END-IF.                                                              
           IF K010-REG-PA2 NOT NUMERIC                                          
              MOVE ZEROES        TO K010-REG-PA2                                
           END-IF.                                                              
           IF K010-REG-PA3 NOT NUMERIC                                          
              MOVE ZEROES        TO K010-REG-PA3                                
           END-IF.                                                              
                                                                                
           IF K010-NUM-TOT-ELDER-MIN NOT NUMERIC                                
              MOVE ZEROES        TO K010-NUM-TOT-ELDER-MIN                      
           END-IF.                                                              
           IF K010-NUM-TOT-NON-ELDER NOT NUMERIC                                
              MOVE ZEROES        TO K010-NUM-TOT-NON-ELDER                      
           END-IF.                                                              
           IF K010-NUM-TOT-ELDER NOT NUMERIC                                    
              MOVE ZEROES        TO K010-NUM-TOT-ELDER                          
           END-IF.                                                              
           IF K010-NUM-TOT-ELDER-SPS NOT NUMERIC                                
              MOVE ZEROES        TO K010-NUM-TOT-ELDER-SPS                      
           END-IF.                                                              
           IF K010-NUM-TOT-ELDER-NONSPS NOT NUMERIC                             
              MOVE ZEROES        TO K010-NUM-TOT-ELDER-NONSPS                   
           END-IF.                                                              
                                                                                
           IF K010-NUM-REG-ELDER        NOT NUMERIC                             
              MOVE ZEROES        TO K010-NUM-REG-ELDER                          
           END-IF.                                                              
           IF K010-NUM-REG-NON-ELDER    NOT NUMERIC                             
              MOVE ZEROES        TO K010-NUM-REG-NON-ELDER                      
           END-IF.                                                              
           IF K010-NUM-REG-ELDER-SPS    NOT NUMERIC                             
              MOVE ZEROES        TO K010-NUM-REG-ELDER-SPS                      
           END-IF.                                                              
           IF K010-NUM-REG-ELDER-NONSPS NOT NUMERIC                             
              MOVE ZEROES        TO K010-NUM-REG-ELDER-NONSPS                   
           END-IF.                                                              
                                                                                
           IF K010-NUM-APP-ELDER        NOT NUMERIC                             
              MOVE ZEROES        TO K010-NUM-APP-ELDER                          
           END-IF.                                                              
           IF K010-NUM-APP-NON-ELDER    NOT NUMERIC                             
              MOVE ZEROES        TO K010-NUM-APP-NON-ELDER                      
           END-IF.                                                              
           IF K010-NUM-APP-ELDER-SPS    NOT NUMERIC                             
              MOVE ZEROES        TO K010-NUM-APP-ELDER-SPS                      
           END-IF.                                                              
           IF K010-NUM-APP-ELDER-NONSPS NOT NUMERIC                             
              MOVE ZEROES        TO K010-NUM-APP-ELDER-NONSPS                   
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       500-UPDATE-BP13K010.                                                     
      ******************************************************************        
                                                                                
           REWRITE BP13K010-TENDER.                                             
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN 00                                                              
              ADD   1            TO   WS-K010-REWRITE                           
           WHEN OTHER                                                           
              DISPLAY 'ERROR REWRITING BP13K010, STATUS ' WS-K010-STATUS        
                                                  ' KEY ' K010-KEY-FLD          
              MOVE    WS-K010-STATUS TO RETURN-CODE                             
              GO TO   999-CLOSE-FILES                                           
           END-EVALUATE.                                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-FILES.                                                         
      ******************************************************************        
           CLOSE BP13K010                                                       
                 BP13F010                                                       
                 BP13F713.                                                      
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              MOVE WS-K010-STATUS TO RETURN-CODE                                
              DISPLAY 'ERROR CLOSING BP13K010, ' WS-K010-STATUS                 
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CF7E ================*'.              
           DISPLAY '  NO. OF F010 RECORDS READ      = ' WS-F010-READ.           
           DISPLAY '  NO. OF K010 RECORDS REWRITTEN = ' WS-K010-REWRITE.        
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
