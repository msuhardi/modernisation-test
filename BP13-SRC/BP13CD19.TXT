       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD19.                                                 
      *AUTHOR.        EDEN ROSE DINGAL                                          
      *DATE-WRITTEN.  31/03/08.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  UPDATE BP13KB20 HH & 5YR-OCCUPN WHEN        *            
      *                HH = SPACES                                 *            
      *                                                            *            
      *  INPUT FILE :  1. BP13F500                                 *            
      *  I-O FILE   :  1. BP13KB20                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP133244 ED1 20080331 NEW PROGRAM                          *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
                                                                                
           SELECT BP13KB20 ASSIGN TO BP13KB20                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE IS RANDOM                                           
                RECORD KEY IS KB20-KEY-FLD                                      
                FILE STATUS IS KB20-STATUS.                                     
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13KB20                                                             
           RECORD CONTAINS 3300 CHARACTERS.                                     
       COPY BP13KB20.                                                           
                                                                                
       FD  BP13F500                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F500.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  NO-F500-READ        PIC 9(5)    VALUE ZEROS.                     
           05  NO-KB20-REWRITE     PIC 9(5)    VALUE ZEROS.                     
           05  WS-BYPASS-CTR       PIC 9(5)    VALUE ZEROS.                     
                                                                                
       01  WS-CUR-DTE               PIC X(8)  VALUE SPACES.                     
       01  KB20-STATUS              PIC 99    VALUE 00.                         
       01  WS-EOF                   PIC X       VALUE 'N'.                      
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPENING-ROUTINE  THRU 1000-EXIT.                        
           PERFORM 2000-READ-F500        THRU 2000-EXIT.                        
           PERFORM 4000-PROCESS-FILES    THRU 4000-EXIT                         
                   UNTIL WS-EOF = 'Y'.                                          
           PERFORM 9000-CLOSING-ROUTINE  THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT BP13F500                                                  
                I-O   BP13KB20.                                                 
                                                                                
           IF KB20-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13KB20 - OPEN ERROR, STATUS: ' KB20-STATUS             
              MOVE KB20-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                    
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DTE.                            
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2000-READ-F500.                                                          
      *-------------------------------------------------------------            
            READ BP13F500 AT END                                                
                 MOVE 'Y' TO WS-EOF                                             
                 GO TO 2000-EXIT.                                               
            ADD 1 TO NO-F500-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES               TO KB20-KEY-FLD.                           
           MOVE F500-NUM-REGN        TO KB20-KEY-FLD.                           
                                                                                
           PERFORM 4100-READ-KB20  THRU 4100-EXIT.                              
                                                                                
           IF KB20-STATUS = 00                                                  
              IF KB20-NUM-HH = SPACES OR LOW-VALUES                             
                 MOVE F500-CDE-HOUSEHOLD     TO  KB20-NUM-HH                    
                 MOVE F500-NUM-5YR-OCCUPN    TO  KB20-NUM-5YR                   
                 PERFORM 4200-REWRITE-KB20 THRU  4200-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F500  THRU 2000-EXIT.                              
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4100-READ-KB20.                                                          
      *-------------------------------------------------------------            
            READ BP13KB20.                                                      
            EVALUATE KB20-STATUS                                                
            WHEN 00                                                             
                 CONTINUE                                                       
            WHEN 23                                                             
                 DISPLAY 'BP13KB20 - REC NOT FND, KEY: '                        
                          KB20-KEY-FLD                                          
            WHEN OTHER                                                          
                 DISPLAY 'BP13KB20 - ERROR READ, STATUS: '                      
                          KB20-STATUS                                           
            END-EVALUATE.                                                       
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
       4200-REWRITE-KB20.                                                       
      *-------------------------------------------------------------            
            REWRITE BP13KB20-REC.                                               
            EVALUATE KB20-STATUS                                                
            WHEN 00                                                             
              ADD 1 TO NO-KB20-REWRITE                                          
                                                                                
            WHEN OTHER                                                          
                 DISPLAY 'BP13KB20 - REWRITE ERROR, STATUS: '                   
                          KB20-STATUS                                           
                          ' KEY IS ' KB20-KEY-FLD                               
            END-EVALUATE.                                                       
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           CLOSE BP13KB20                                                       
                 BP13F500.                                                      
                                                                                
           IF KB20-STATUS NOT = '00'                                            
             DISPLAY 'CLOSING ERROR KB20, STATUS ' KB20-STATUS                  
             MOVE KB20-STATUS TO RETURN-CODE.                                   
                                                                                
                                                                                
           DISPLAY '-----------------------------------------'.                 
           DISPLAY '          PROGRAM BP13CD19'.                                
           DISPLAY '        RUN DATE : ' WS-CUR-DTE.                            
           DISPLAY 'NO. OF F500 REC READ    > ' NO-F500-READ.                   
           DISPLAY 'NO. OF KB20 REC UPDATED > ' NO-KB20-REWRITE.                
           DISPLAY '-----------------------------------------'.                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
