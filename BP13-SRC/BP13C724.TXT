       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C724.                                                 
      *AUTHOR.        CKK7.                                                     
      *DATE-WRITTEN.  16/03/2003.                                               
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *--------------------------------------------------------*                
      * >  THIS PROGRAM IS USED TO MATCH THE SOC MASTER AND    *                
      *    EC MASTER WITH THE BE APPLICATION AND BTO           *                
      * >  THE PROGRAM IS RUN UNDER BP13A699.                  *                
      * >  ONLY REPORTS WILL BE PRINTED FOR USER TO FOLLOW UP  *                
      *========================================================*                
      * CHG-NO   BY   ON     DESCRIPTION                       *                
      * --------------------------------                       *                
      * BP132290 CKK7 130303 NEW PROGRAM TO MATCH FILES        *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F500   ASSIGN       TO BP13F500.                          
                                                                                
           SELECT BP13F410   ASSIGN       TO BP13F410.                          
                                                                                
           SELECT P13C724A   ASSIGN       TO P13C724A.                          
                                                                                
           SELECT P13C724B   ASSIGN       TO P13C724B.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F500                                                            
            RECORD CONTAINS 88 CHARACTERS                                       
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   BP13F500-REC.                                                       
            05  F500-NUM-REF              PIC X(06).                            
            05  F500-NUM-NRIC             PIC X(09).                            
            05  F500-CDE-ZONE             PIC X(02).                            
            05  F500-CDE-HOUSEHOLD        PIC X(01).                            
            05  F500-NUM-MCPS             PIC X(01).                            
            05  F500-CDE-SERVE            PIC X(01).                            
            05  F500-NUM-REGN             PIC X(08).                            
            05  F500-CDE-NT               PIC X(03).                            
            05  F500-CDE-FT               PIC X(02).                            
            05  F500-CDE-ALLOC-SCH        PIC X(03).                            
            05  F500-CDE-CAT              PIC X(01).                            
            05  F500-CDE-SUCC-NT-ZONE     PIC X(03).                            
            05  F500-CDE-UNSUCC           PIC X(01).                            
            05  F500-DTE-BALLOT           PIC X(06).                            
            05  F500-NUM-APPLN-TYPE       PIC X(01).                            
            05  F500-NUM-WARD             PIC X(03).                            
            05  F500-NUM-ALLO-CAT         PIC X(03).                            
            05  F500-NUM-RANDOM           PIC X(28).                            
            05  F500-NUM-SEQ-REF          PIC X(06).                            
                                                                                
       FD   BP13F410                                                            
            RECORD CONTAINS 63 CHARACTERS                                       
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   BP13F410-REC.                                                       
            05  F410-NUM-REGN             PIC X(08).                            
            05  F410-NUM-NRIC             PIC X(09).                            
            05  F410-CDE-NT               PIC X(03).                            
            05  F410-DTE-BALLOT           PIC X(06).                            
            05  F410-SCH-ACC              PIC X(11).                            
            05  F410-DTE-SA               PIC X(08).                            
            05  F410-NUM-ZONE             PIC X(02).                            
            05  F410-NUM-FT               PIC X(02).                            
            05  F410-CDE-HH               PIC X(01).                            
            05  F410-CDE-STATUS           PIC X(01).                            
            05  F410-DTE-SO               PIC X(08).                            
            05  F410-CDE-TAG              PIC X(01).                            
            05  F410-ALLOC-CAT            PIC X(03).                            
                                                                                
       FD   P13C724A                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   WS-P13C724A-REC               PIC X(132).                           
                                                                                
       FD   P13C724B                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   WS-P13C724B-REC               PIC X(132).                           
                                                                                
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-MATCH             PIC 9(08)    VALUE ZEROES.                   
           05 WS-NOT-MATCH         PIC 9(08)    VALUE ZEROES.                   
           05 WS-F500-READ         PIC 9(08)    VALUE ZEROES.                   
           05 WS-F410-READ         PIC 9(08)    VALUE ZEROES.                   
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-LINE-CTR1         PIC 9(08)    VALUE 57.                       
           05 WS-PAGE-NO1          PIC 9(08)    VALUE ZEROES.                   
           05 WS-NUM-REGN1         PIC 9(08)    VALUE ZEROES.                   
           05 WS-SNO1              PIC 9(04)    VALUE ZEROES.                   
           05 WS-LINE-CTR2         PIC 9(08)    VALUE 57.                       
           05 WS-PAGE-NO2          PIC 9(08)    VALUE ZEROES.                   
           05 WS-NUM-REGN2         PIC 9(08)    VALUE ZEROES.                   
           05 WS-SNO2              PIC 9(04)    VALUE ZEROES.                   
                                                                                
           05 WS-DATE              PIC X(08)    VALUE SPACES.                   
           05 WS-SYS-DATE          PIC X(10)    VALUE SPACES.                   
                                                                                
       01  WS-F500-NRIC            PIC X(09)    VALUE SPACES.                   
       01  WS-F410-NRIC            PIC X(09)    VALUE SPACES.                   
       01  WS-SCH-ACC              PIC X(09)    VALUE SPACES.                   
                                                                                
       01  LCHK1-PR-HEAD-01.                                                    
           05  FILLER                  PIC X(8)  VALUE 'P13L724A'.              
           05  FILLER                  PIC X(5)  VALUE SPACES .                 
           05  FILLER                  PIC X(8)  VALUE 'HDBCAT 3'.              
           05  FILLER                  PIC X(24) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
                                       'S Y S T E M   O F   '.                  
           05  FILLER                  PIC X(19) VALUE                          
                                       'C O M M I T M E N T'.                   
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'DATE :'.                
           05  LCHK1-DATE-RUN          PIC X(10).                               
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'PAGE : '.               
           05  LCHK1-PAGENO            PIC ZZZZ9 .                              
                                                                                
       01  LCHK1-PR-HEAD-02.                                                    
           05  FILLER                  PIC X(40)  VALUE SPACES .                
           05  FILLER                  PIC X(100) VALUE                         
               'REPORT FOR BE/BTO VS BOOKED FLAT IN SOC MASTER'.                
                                                                                
       01  LCHK1-PR-HEAD-03.                                                    
           05  FILLER                  PIC X(04) VALUE 'S/NO'.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE 'BE REGNO'.              
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE 'BE REFNO'.              
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE 'BE DTEBAL'.             
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(12) VALUE 'BE ZN/NT/FT'.           
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE 'NRIC'.                  
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE 'STA'.                   
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'REGNO'.                 
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE 'DTEBAL'.                
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE 'MODE'.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE 'ZN/NT/FT'.              
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE 'DATE-SA'.               
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE 'SCH-ACC'.               
                                                                                
       01  LCHK1-PR-HEAD-04.                                                    
           05  FILLER                  PIC X(26) VALUE SPACES.                  
           05  FILLER                  PIC X(27) VALUE 'ADDRESS'.               
                                                                                
       01  LCHK1-PR-DTL-01.                                                     
           05  LCHK1-SNO               PIC Z(4)  VALUE ZEROES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LCHK1-NUM-REGN          PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LCHK1-NUM-REF           PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  LCHK1-DTE-BALLOT-BE     PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  LCHK1-ZN-NT-FT-BE       PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  LCHK1-NUM-NRIC          PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LCHK1-OTHER-STATUS      PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  LCHK1-OTHER-NUM-REGN    PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LCHK1-DTE-BALLOT        PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LCHK1-MODE-ALLOC        PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  LCHK1-ZN-NT-FT          PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LCHK1-DTE-SA            PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LCHK1-SCH-ACC           PIC X(11) VALUE SPACES.                  
                                                                                
       01  LCHK1-PR-LINE               PIC X(132) VALUE ALL '-'.                
       01  LCHK1-PR-SPACE              PIC X(132) VALUE SPACES.                 
                                                                                
       01  LCHK1-PR-DTL-02.                                                     
           05  FILLER                  PIC X(26) VALUE SPACES.                  
           05  LCHK1-NUM-ADDRESS       PIC X(58) VALUE SPACES.                  
           05  FILLER                  PIC X(13) VALUE ' SINGAPORE - '.         
           05  LCHK1-NUM-POSTAL        PIC X(06) VALUE SPACES.                  
                                                                                
       01  LCHK2-PR-HEAD-01.                                                    
           05  FILLER                  PIC X(8)  VALUE 'P13L724B'.              
           05  FILLER                  PIC X(5)  VALUE SPACES .                 
           05  FILLER                  PIC X(8)  VALUE 'HDBCAT 3'.              
           05  FILLER                  PIC X(24) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
                                       'S Y S T E M   O F   '.                  
           05  FILLER                  PIC X(19) VALUE                          
                                       'C O M M I T M E N T'.                   
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'DATE :'.                
           05  LCHK2-DATE-RUN          PIC X(10).                               
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'PAGE : '.               
           05  LCHK2-PAGENO            PIC ZZZZ9 .                              
                                                                                
       01  LCHK2-PR-HEAD-02.                                                    
           05  FILLER                  PIC X(40)  VALUE SPACES .                
           05  FILLER                  PIC X(100) VALUE                         
               'REPORT FOR BE/BTO VS ALREADY TAKEN POSSESSION CASES'.           
                                                                                
       01  LCHK2-PR-HEAD-2A.                                                    
           05  FILLER                  PIC X(50)  VALUE SPACES .                
           05  FILLER                  PIC X(90) VALUE                          
               'REPORT FOR BE/BTO VS EC CASES'.                                 
                                                                                
       01  LCHK2-PR-HEAD-03.                                                    
           05  FILLER                  PIC X(04) VALUE 'S/NO'.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE 'BE REGNO'.              
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE 'BE REFNO'.              
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE 'BE DTEBAL'.             
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(12) VALUE 'BE ZN/NT/FT'.           
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE 'NRIC'.                  
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE 'STA'.                   
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'REGNO'.                 
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE 'DTEBAL'.                
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE 'MODE'.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE 'ZN/NT/FT'.              
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE 'DATE-TP'.               
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE 'SCH-ACC'.               
                                                                                
       01  LCHK2-PR-HEAD-04.                                                    
           05  FILLER                  PIC X(26) VALUE SPACES.                  
           05  FILLER                  PIC X(27) VALUE 'ADDRESS'.               
                                                                                
       01  LCHK2-PR-DTL-01.                                                     
           05  LCHK2-SNO               PIC Z(4)  VALUE ZEROES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LCHK2-NUM-REGN          PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LCHK2-NUM-REF           PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  LCHK2-DTE-BALLOT-BE     PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  LCHK2-ZN-NT-FT-BE       PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  LCHK2-NUM-NRIC          PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LCHK2-OTHER-STATUS      PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  LCHK2-OTHER-NUM-REGN    PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LCHK2-DTE-BALLOT        PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LCHK2-MODE-ALLOC        PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  LCHK2-ZN-NT-FT          PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LCHK2-DTE-TP            PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LCHK2-SCH-ACC           PIC X(11) VALUE SPACES.                  
                                                                                
       01  LCHK2-PR-LINE               PIC X(132) VALUE ALL '-'.                
       01  LCHK2-PR-SPACE              PIC X(132) VALUE SPACES.                 
                                                                                
       01  LCHK2-PR-DTL-02.                                                     
           05  FILLER                  PIC X(26) VALUE SPACES.                  
           05  LCHK2-NUM-ADDRESS       PIC X(58) VALUE SPACES.                  
           05  FILLER                  PIC X(13) VALUE ' SINGAPORE - '.         
           05  LCHK2-NUM-POSTAL        PIC X(06) VALUE SPACES.                  
                                                                                
      *================================================================*        
       PROCEDURE DIVISION.                                                      
      *================================================================*        
                                                                                
      *-----------*                                                             
       000-CONTROL.                                                             
      *-----------*                                                             
                                                                                
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
           PERFORM 200-READ-F500     THRU 200-EXIT.                             
           PERFORM 300-READ-F410     THRU 300-EXIT.                             
           PERFORM 400-PROCESS-REC   THRU 400-EXIT                              
                   UNTIL WS-F500-NRIC = HIGH-VALUES                             
                     AND WS-F410-NRIC = HIGH-VALUES.                            
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       100-OPEN-FILES.                                                          
      *--------------*                                                          
                                                                                
           OPEN INPUT  BP13F500                                                 
                       BP13F410                                                 
               OUTPUT  P13C724A                                                 
                       P13C724B.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       200-READ-F500.                                                           
      *-----------------*                                                       
                                                                                
           READ BP13F500 AT END                                                 
                MOVE HIGH-VALUES TO WS-F500-NRIC                                
                GO TO 200-EXIT.                                                 
                                                                                
           MOVE F500-NUM-NRIC           TO WS-F500-NRIC.                        
           ADD 1 TO WS-F500-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       300-READ-F410.                                                           
      *-----------------*                                                       
                                                                                
           READ BP13F410 AT END                                                 
                MOVE HIGH-VALUES TO WS-F410-NRIC                                
                GO TO 300-EXIT.                                                 
                                                                                
           MOVE F410-NUM-NRIC           TO WS-F410-NRIC.                        
           ADD 1 TO WS-F410-READ.                                               
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       400-PROCESS-REC.                                                         
      *-----------------*                                                       
           IF WS-F500-NRIC   =   WS-F410-NRIC AND                               
              F500-NUM-REGN NOT = F410-NUM-REGN                                 
              ADD        1                  TO WS-MATCH                         
              PERFORM 500-WRITE-REPORT      THRU 500-EXIT                       
              PERFORM 300-READ-F410         THRU 300-EXIT                       
              PERFORM 200-READ-F500         THRU 200-EXIT                       
           ELSE                                                                 
              IF WS-F410-NRIC < WS-F500-NRIC                                    
                 PERFORM 300-READ-F410       THRU 300-EXIT                      
                 ADD        1                TO WS-NOT-MATCH                    
              ELSE                                                              
                 PERFORM 200-READ-F500       THRU 200-EXIT                      
                 ADD        1                TO WS-NOT-MATCH                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       500-WRITE-REPORT.                                                        
      *----------------*                                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO    WS-DATE                          
           STRING WS-DATE(7:2) '-' WS-DATE(5:2) '-' WS-DATE(1:4)                
                  DELIMITED  BY SIZE INTO WS-SYS-DATE.                          
                                                                                
           IF F410-CDE-TAG = 'Y'                                                
              PERFORM 600-RPT-BE-VS-OTHER-BOOKED  THRU  600-EXIT                
           ELSE                                                                 
              IF F410-CDE-TAG = 'N'                                             
                 PERFORM 700-RPT-BE-VS-OTHER-PST     THRU  700-EXIT             
              ELSE                                                              
                 MOVE LCHK2-PR-HEAD-2A  TO LCHK2-PR-HEAD-02                     
                 PERFORM 700-RPT-BE-VS-OTHER-PST     THRU  700-EXIT             
              END-IF                                                            
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------*                                              
       600-RPT-BE-VS-OTHER-BOOKED.                                              
      *--------------------------*                                              
                                                                                
           MOVE   SPACES           TO   LCHK1-PR-DTL-01                         
           INITIALIZE LCHK1-PR-DTL-01.                                          
                                                                                
           IF WS-LINE-CTR1 > 55                                                 
              ADD   1              TO   WS-PAGE-NO1                             
              MOVE  WS-PAGE-NO1    TO   LCHK1-PAGENO                            
              MOVE  WS-SYS-DATE    TO   LCHK1-DATE-RUN                          
                                                                                
              WRITE WS-P13C724A-REC FROM LCHK1-PR-HEAD-01 AFTER PAGE            
              WRITE WS-P13C724A-REC FROM LCHK1-PR-HEAD-02                       
              WRITE WS-P13C724A-REC FROM LCHK1-PR-LINE    AFTER 2               
              WRITE WS-P13C724A-REC FROM LCHK1-PR-HEAD-03                       
              WRITE WS-P13C724A-REC FROM LCHK1-PR-HEAD-04                       
              WRITE WS-P13C724A-REC FROM LCHK1-PR-LINE                          
                                                                                
              MOVE  7              TO   WS-LINE-CTR1                            
           END-IF                                                               
                                                                                
           IF F500-NUM-REGN NOT = WS-NUM-REGN1                                  
              MOVE F500-NUM-REGN   TO   LCHK1-NUM-REGN                          
              ADD  1               TO   WS-SNO1                                 
              MOVE WS-SNO1         TO   LCHK1-SNO                               
           ELSE                                                                 
              MOVE SPACES          TO   LCHK1-NUM-REGN                          
              MOVE ZEROES          TO   LCHK1-SNO                               
           END-IF                                                               
                                                                                
           MOVE F500-NUM-REF       TO   LCHK1-NUM-REF.                          
                                                                                
           STRING F500-CDE-ZONE '/' F500-CDE-NT '/' F500-CDE-FT                 
                  DELIMITED BY SIZE INTO LCHK1-ZN-NT-FT-BE.                     
                                                                                
           MOVE F500-DTE-BALLOT    TO   LCHK1-DTE-BALLOT-BE.                    
           MOVE F500-NUM-NRIC      TO   LCHK1-NUM-NRIC.                         
                                                                                
           MOVE F410-CDE-STATUS    TO   LCHK1-OTHER-STATUS.                     
           MOVE F410-NUM-REGN      TO   LCHK1-OTHER-NUM-REGN.                   
           MOVE F410-DTE-BALLOT    TO   LCHK1-DTE-BALLOT.                       
           MOVE F410-ALLOC-CAT     TO   LCHK1-MODE-ALLOC.                       
                                                                                
           STRING F410-NUM-ZONE '/' F410-CDE-NT '/' F410-NUM-FT                 
                  DELIMITED BY SIZE INTO LCHK1-ZN-NT-FT.                        
                                                                                
           STRING F410-DTE-SA(7:2) '-' F410-DTE-SA(5:2) '-'                     
                  F410-DTE-SA(1:4) DELIMITED BY SIZE                            
                  INTO LCHK1-DTE-SA.                                            
           MOVE F410-SCH-ACC       TO   LCHK1-SCH-ACC.                          
                                                                                
           WRITE WS-P13C724A-REC   FROM LCHK1-PR-DTL-01 AFTER 1.                
           WRITE WS-P13C724A-REC   FROM LCHK1-PR-DTL-02 AFTER 1.                
           WRITE WS-P13C724A-REC   FROM LCHK1-PR-SPACE  AFTER 1.                
           ADD    3                TO   WS-LINE-CTR1.                           
           MOVE F500-NUM-REGN      TO   WS-NUM-REGN1.                           
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       700-RPT-BE-VS-OTHER-PST.                                                 
      *-----------------------*                                                 
                                                                                
           MOVE   SPACES           TO   LCHK2-PR-DTL-01                         
           INITIALIZE LCHK2-PR-DTL-01.                                          
                                                                                
           IF WS-LINE-CTR2 > 55                                                 
              ADD   1              TO   WS-PAGE-NO2                             
              MOVE  WS-PAGE-NO2    TO   LCHK2-PAGENO                            
              MOVE  WS-SYS-DATE    TO   LCHK2-DATE-RUN                          
                                                                                
              WRITE WS-P13C724B-REC FROM LCHK2-PR-HEAD-01 AFTER PAGE            
              WRITE WS-P13C724B-REC FROM LCHK2-PR-HEAD-02                       
              WRITE WS-P13C724B-REC FROM LCHK2-PR-LINE    AFTER 2               
              WRITE WS-P13C724B-REC FROM LCHK2-PR-HEAD-03                       
              WRITE WS-P13C724B-REC FROM LCHK2-PR-HEAD-04                       
              WRITE WS-P13C724B-REC FROM LCHK2-PR-LINE                          
                                                                                
              MOVE  7              TO   WS-LINE-CTR2                            
           END-IF                                                               
                                                                                
           IF F500-NUM-REGN NOT = WS-NUM-REGN2                                  
              MOVE F500-NUM-REGN   TO   LCHK2-NUM-REGN                          
              ADD  1               TO   WS-SNO2                                 
              MOVE WS-SNO2         TO   LCHK2-SNO                               
           ELSE                                                                 
              MOVE SPACES          TO   LCHK2-NUM-REGN                          
              MOVE ZEROES          TO   LCHK2-SNO                               
           END-IF                                                               
                                                                                
           MOVE F500-NUM-REF       TO   LCHK2-NUM-REF.                          
                                                                                
           STRING F500-CDE-ZONE '/' F500-CDE-NT '/' F500-CDE-FT                 
                  DELIMITED BY SIZE INTO LCHK2-ZN-NT-FT-BE.                     
                                                                                
           MOVE F500-DTE-BALLOT    TO   LCHK2-DTE-BALLOT-BE.                    
           MOVE F500-NUM-NRIC      TO   LCHK2-NUM-NRIC.                         
                                                                                
           MOVE F410-CDE-STATUS    TO   LCHK2-OTHER-STATUS.                     
           MOVE F410-NUM-REGN      TO   LCHK2-OTHER-NUM-REGN.                   
           MOVE F410-DTE-BALLOT    TO   LCHK2-DTE-BALLOT.                       
           MOVE F410-ALLOC-CAT     TO   LCHK2-MODE-ALLOC.                       
                                                                                
           STRING F410-NUM-ZONE '/' F410-CDE-NT '/' F410-NUM-FT                 
                  DELIMITED BY SIZE INTO LCHK2-ZN-NT-FT.                        
                                                                                
           STRING F410-DTE-SO(7:2) '-' F410-DTE-SO(5:2) '-'                     
                  F410-DTE-SO(1:4) DELIMITED BY SIZE                            
                  INTO LCHK2-DTE-TP.                                            
                                                                                
           MOVE F410-SCH-ACC       TO   LCHK2-SCH-ACC.                          
                                                                                
           WRITE WS-P13C724B-REC   FROM LCHK2-PR-DTL-01 AFTER 1.                
           WRITE WS-P13C724B-REC   FROM LCHK2-PR-DTL-02 AFTER 1.                
           WRITE WS-P13C724B-REC   FROM LCHK2-PR-SPACE  AFTER 1.                
           ADD    3                TO   WS-LINE-CTR2.                           
           MOVE F500-NUM-REGN      TO   WS-NUM-REGN2.                           
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       900-CLOSE-ROUTINE.                                                       
      *-----------------*                                                       
                                                                                
           DISPLAY '              PROGRAM-ID :  BP13C724 '.                     
           DISPLAY ' '.                                                         
           DISPLAY '1. NO OF RECS READ FROM F500      : ' WS-F500-READ.         
           DISPLAY '2. NO OF RECS READ FROM F410      : ' WS-F410-READ.         
           DISPLAY '3. NO OF RECS MATCHED             : ' WS-MATCH.             
           DISPLAY '4. NO OF RECS NOT MATCHED         : ' WS-NOT-MATCH.         
           DISPLAY '  '                                                         
                                                                                
           CLOSE BP13F500                                                       
                 BP13F410                                                       
                 P13C724A                                                       
                 P13C724B.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------- END OF PROGRAM  ---------------------*              
                                                                                
                                                                                
