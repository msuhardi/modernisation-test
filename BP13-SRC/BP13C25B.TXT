      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C25B.                                                 
      *AUTHOR.        NINO ANGELO GARILLO.                                      
      *DATE-WRITTEN.  OCT2004.                                                  
      *****************************************************************         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *****************************************************************         
      *  OBJECTIVE   :  PRINT APPOINTMENT DETAILS FOR WIS             *         
      *---------------------------------------------------------------*         
      *  INPUT FILES : 1.  BP13F205 - CONTROL FILE                    *         
      *                2.  BP13K225 - QUEUE FILE                      *         
      *                                                               *         
      *  LETTER      : 1.  BP13L25B - APPOINTMENT DETAILS             *         
      *---------------------------------------------------------------*         
      * CHG REF# BY    DATE      DESCRIPTION                          *         
      * -------- ---- --------   -------------------------------------*         
      * BP132763 GAN1 14/10/2005 PRINT APPMT DETAILS FOR WIS          *         
      * BP132770 LMS1 14/11/2005 TO ADD ETHNIC                        *         
      *****************************************************************         
      *---------------------------------------------------------------*         
       ENVIRONMENT DIVISION.                                                    
      *---------------------------------------------------------------*         
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN         TO BP13F205.                          
                                                                                
           SELECT BP13K225  ASSIGN        TO BP13K225                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K225-KEY-FLD                       
                            ALTERNATE KEY IS K225-ALT-KEY                       
                            FILE STATUS   IS WS-K225-STATUS.                    
                                                                                
           SELECT BP13L25B  ASSIGN        TO BP13L25B.                          
                                                                                
      *---------------------------------------------------------------*         
       DATA DIVISION.                                                           
      *---------------------------------------------------------------*         
       FILE SECTION.                                                            
                                                                                
       FD   BP13F205                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 80 CHARACTERS.                                      
       COPY BP13F205.                                                           
                                                                                
       FD   BP13K225                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K225.                                                           
                                                                                
       FD  BP13L25B                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 80 CHARACTERS.                                       
       01  PRINT-REC                    PIC X(80).                              
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01  WS-WORK-VARIABLES.                                                   
           05 WS-SYSTEM-DATE.                                                   
              10 WS-SYS-DATE               PIC X(08) VALUE SPACES.              
              10 FILLER                    PIC X(13) VALUE SPACES.              
           05 WS-TIME-HH                   PIC X(02) VALUE SPACES.              
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 WS-K225-STATUS               PIC 9(02) VALUE ZEROS.               
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F205-READ-CTR             PIC 9(03) VALUE ZEROES.              
           05 WS-K225-READ-CNT             PIC 9(04) VALUE ZEROES.              
           05 WS-LINE-CNT                  PIC 99    VALUE ZEROES.              
           05 WS-PAGE-CNT                  PIC 9999  VALUE ZEROES.              
                                                                                
       01  WS-SWITCHES.                                                         
           05 WS-BP13F205-EOF-SW           PIC X(01) VALUE SPACES.              
              88 C-BP13F205-EOF            VALUE 'Y'.                           
           05 WS-BP13K225-EOF-SW           PIC X(01) VALUE SPACES.              
              88 C-BP13K225-EOF            VALUE 'Y'.                           
           05 WS-K225-REC-FND-SW           PIC X(01) VALUE SPACES.              
              88 C-K225-REC-FND            VALUE 'Y'.                           
           05 WS-K225-FIRST-READ-SW        PIC X(01) VALUE SPACES.              
              88 C-K225-FIRST-READ         VALUE 'Y'.                           
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR APPOINTMENT DETAILS                     *            
      *------------------------------------------------------------*            
       01  L25B-PRINT-HEADS.                                                    
           05 L25B-HEAD-01.                                                     
              10 FILLER                    PIC X(05) VALUE 'PAGE '.             
              10 L25B-PAGE                 PIC ZZZ9.                            
              10 FILLER                    PIC X(54) VALUE SPACES.              
              10 FILLER                    PIC X(07) VALUE 'DATE : '.           
              10 L25B-RUN-DATE.                                                 
                 15 L25B-RUN-DD            PIC X(2).                            
                 15 FILLER                 PIC X(1)  VALUE '/'.                 
                 15 L25B-RUN-MM            PIC X(2).                            
                 15 FILLER                 PIC X(1)  VALUE '/'.                 
                 15 L25B-RUN-CY            PIC X(4).                            
                                                                                
           05 L25B-HEAD-02.                                                     
              10 FILLER                    PIC X(26) VALUE SPACES.              
              10 FILLER                    PIC X(27) VALUE                      
                 'APPOINTMENT DETAILS FOR WIS'.                                 
              10 FILLER                    PIC X(10) VALUE SPACES.              
              10 FILLER                    PIC X(04) VALUE 'CAT3'.              
                                                                                
           05 L25B-HEAD-03.                                                     
              10 FILLER                    PIC X(26) VALUE SPACES.              
              10 FILLER                    PIC X(27) VALUE ALL '-'.             
                                                                                
           05 L25B-HEAD-04.                                                     
              10 FILLER                    PIC X(24) VALUE SPACES.              
              10 FILLER                    PIC X(07) VALUE 'FROM : '.           
              10 L25B-STA-DATE.                                                 
                 15 L25B-STA-DD            PIC X(02).                           
                 15 FILLER                 PIC X(01) VALUE '/'.                 
                 15 L25B-STA-MM            PIC X(02).                           
                 15 FILLER                 PIC X(01) VALUE '/'.                 
                 15 L25B-STA-CY            PIC X(04).                           
              10 FILLER                    PIC X(04) VALUE ' TO '.              
              10 L25B-END-DATE.                                                 
                 15 L25B-END-DD            PIC X(02).                           
                 15 FILLER                 PIC X(01) VALUE '/'.                 
                 15 L25B-END-MM            PIC X(02).                           
                 15 FILLER                 PIC X(01) VALUE '/'.                 
                 15 L25B-END-CY            PIC X(04).                           
                                                                                
           05 L25B-HEAD-05.                                                     
              10 FILLER                    PIC X(68) VALUE                      
                 ' QUEUE APPMT      APPMT PRT NRIC      NAME'.                  
              10 FILLER                    PIC X(12) VALUE                      
                 ' TEL      E'.                                                 
                                                                                
           05 L25B-HEAD-06.                                                     
              10 FILLER                    PIC X(68) VALUE                      
                 '       DATE       TIME  LTR'.                                 
                                                                                
           05 L25B-HEAD-07.                                                     
              10 FILLER                    PIC X(80) VALUE ALL '-'.             
                                                                                
       01  L25B-PRINT-DETAILS.                                                  
           05 L25B-DETAILS.                                                     
              10 FILLER                    PIC X(01) VALUE SPACES.              
              10 L25B-NUM-QUEUE            PIC X(05) VALUE SPACES.              
              10 FILLER                    PIC X(01) VALUE SPACES.              
              10 L25B-DTE-APPMT            PIC X(10) VALUE SPACES.              
              10 FILLER                    PIC X(01) VALUE SPACES.              
              10 L25B-TME-APPMT            PIC X(05) VALUE SPACES.              
              10 FILLER                    PIC X(02) VALUE SPACES.              
              10 L25B-NUM-PRT-LTR          PIC X(01) VALUE SPACES.              
              10 FILLER                    PIC X(02) VALUE SPACES.              
              10 L25B-NUM-NRIC             PIC X(09) VALUE SPACES.              
              10 FILLER                    PIC X(01) VALUE SPACES.              
              10 L25B-NME-NAME             PIC X(30) VALUE SPACES.              
              10 FILLER                    PIC X(01) VALUE SPACES.              
              10 L25B-NUM-TEL              PIC X(08) VALUE SPACES.              
              10 FILLER                    PIC X(01) VALUE SPACES.              
              10 L25B-NUM-CAT              PIC X(01) VALUE SPACES.              
              10 FILLER                    PIC X(01) VALUE SPACES.              
                                                                                
      *---------------------------------------------------------------*         
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
      *-------------------*                                                     
       0000-MAIN-ROUTINE.                                                       
      *-------------------*                                                     
           PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                  
           PERFORM 2000-PROCESS-RECORDS        THRU 2000-EXIT                   
             UNTIL C-BP13F205-EOF.                                              
           PERFORM 9000-CLOSE-FILES            THRU 9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       1000-OPEN-FILES.                                                         
      *-----------------*                                                       
                                                                                
           OPEN INPUT BP13F205                                                  
                      BP13K225                                                  
               OUTPUT BP13L25B.                                                 
                                                                                
           IF WS-K225-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR: BP13K225 STATUS ' WS-K225-STATUS             
              MOVE WS-K225-STATUS               TO RETURN-CODE                  
              PERFORM 9000-CLOSE-FILES          THRU 9000-EXIT                  
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO L25B-RUN-CY.                          
           MOVE WS-SYS-DATE(5:2)       TO L25B-RUN-MM.                          
           MOVE WS-SYS-DATE(7:2)       TO L25B-RUN-DD.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       2000-PROCESS-RECORDS.                                                    
      *----------------------*                                                  
                                                                                
           READ BP13F205   AT END                                               
                MOVE 'Y'   TO WS-BP13F205-EOF-SW                                
                GO TO 2000-EXIT.                                                
                                                                                
           ADD  1  TO  WS-F205-READ-CTR.                                        
                                                                                
           MOVE SPACES             TO WS-BP13K225-EOF-SW                        
           MOVE 'Y'                TO WS-K225-REC-FND-SW                        
                                      WS-K225-FIRST-READ-SW.                    
                                                                                
           MOVE SPACES             TO K225-ALT-KEY.                             
           MOVE F205-DTE-START     TO K225-DTE-APPOINTMENT.                     
           PERFORM 2100-START-BP13K225            THRU 2100-EXIT.               
           PERFORM 2200-READNXT-BP13K225          THRU 2200-EXIT                
             UNTIL C-BP13K225-EOF                                               
                OR NOT C-K225-REC-FND.                                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       2100-START-BP13K225.                                                     
      *---------------------*                                                   
                                                                                
           START BP13K225 KEY NOT LESS THAN K225-ALT-KEY.                       
           EVALUATE WS-K225-STATUS                                              
              WHEN 00                                                           
                 MOVE 'Y'        TO WS-K225-REC-FND-SW                          
                                                                                
              WHEN 23                                                           
                 MOVE 'N'        TO WS-K225-REC-FND-SW                          
                 DISPLAY 'APPMT DATE NOT FOUND IN BP13K225: '                   
                          F205-DTE-START '-' F205-DTE-END                       
                                                                                
              WHEN OTHER                                                        
                 DISPLAY 'START READ ERROR BP13K225,STATUS '                    
                          WS-K225-STATUS                                        
                 MOVE WS-K225-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       2200-READNXT-BP13K225.                                                   
      *-----------------------*                                                 
                                                                                
           READ BP13K225 NEXT RECORD AT END                                     
                MOVE 'Y'             TO WS-BP13K225-EOF-SW                      
                GO TO 2200-EXIT.                                                
                                                                                
           EVALUATE WS-K225-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 PERFORM 2210-CHECK-APPMT-DATE THRU 2210-EXIT                   
                                                                                
              WHEN 10                                                           
                 MOVE 'Y'            TO WS-BP13K225-EOF-SW                      
                                                                                
              WHEN OTHER                                                        
                 DISPLAY 'READNEXT ERROR:BP13K225 STAT ' WS-K225-STATUS         
                 MOVE WS-K225-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------*                                               
       2210-CHECK-APPMT-DATE.                                                   
      *-------------------------*                                               
                                                                                
           IF (K225-DTE-APPOINTMENT >= F205-DTE-START) AND                      
              (K225-DTE-APPOINTMENT <= F205-DTE-END)                            
              MOVE 'N'        TO WS-K225-FIRST-READ-SW                          
              ADD  1          TO WS-K225-READ-CNT                               
              PERFORM 3000-PRINT-APPMT-LETTER     THRU 3000-EXIT                
           ELSE                                                                 
              IF K225-DTE-APPOINTMENT > F205-DTE-END                            
                 IF C-K225-FIRST-READ                                           
                    DISPLAY 'APPMT DATE NOT FOUND IN BP13K225: '                
                             F205-DTE-START '-' F205-DTE-END                    
                 END-IF                                                         
                 MOVE 'Y'     TO WS-BP13K225-EOF-SW                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------*                                               
       3000-PRINT-APPMT-LETTER.                                                 
      *-------------------------*                                               
                                                                                
           IF (WS-LINE-CNT = 0)                                                 
              PERFORM 4000-PRINT-HEADING  THRU 4000-EXIT                        
              MOVE 9                        TO WS-LINE-CNT.                     
                                                                                
           PERFORM 5000-PRINT-DETAIL      THRU 5000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------*                                               
       4000-PRINT-HEADING.                                                      
      *-------------------------*                                               
                                                                                
           ADD  1                      TO WS-PAGE-CNT.                          
           MOVE WS-PAGE-CNT            TO L25B-PAGE.                            
           WRITE PRINT-REC           FROM L25B-HEAD-01 AFTER PAGE.              
           WRITE PRINT-REC           FROM L25B-HEAD-02 AFTER 1.                 
           WRITE PRINT-REC           FROM L25B-HEAD-03 AFTER 1.                 
                                                                                
           MOVE SPACES                 TO PRINT-REC.                            
           WRITE PRINT-REC                             AFTER 1.                 
                                                                                
           MOVE F205-DTE-START (7:2)   TO L25B-STA-DD.                          
           MOVE F205-DTE-START (5:2)   TO L25B-STA-MM.                          
           MOVE F205-DTE-START (1:4)   TO L25B-STA-CY.                          
           MOVE F205-DTE-END   (7:2)   TO L25B-END-DD.                          
           MOVE F205-DTE-END   (5:2)   TO L25B-END-MM.                          
           MOVE F205-DTE-END   (1:4)   TO L25B-END-CY.                          
           WRITE PRINT-REC           FROM L25B-HEAD-04 AFTER 1.                 
           WRITE PRINT-REC           FROM L25B-HEAD-05 AFTER 2.                 
           WRITE PRINT-REC           FROM L25B-HEAD-06 AFTER 1.                 
           WRITE PRINT-REC           FROM L25B-HEAD-07 AFTER 1.                 
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       5000-PRINT-DETAIL.                                                       
      *--------------------*                                                    
                                                                                
           MOVE SPACES                 TO L25B-PRINT-DETAILS.                   
                                                                                
           MOVE K225-NUM-QUEUE         TO L25B-NUM-QUEUE.                       
           STRING K225-DTE-APPOINTMENT (7:2) '/'                                
                  K225-DTE-APPOINTMENT (5:2) '/'                                
                  K225-DTE-APPOINTMENT (1:4)                                    
                  DELIMITED BY SIZE  INTO L25B-DTE-APPMT                        
           END-STRING.                                                          
           STRING K225-TME-APPOINTMENT (1:2) ':'                                
                  K225-TME-APPOINTMENT (3:2)                                    
                  DELIMITED BY SIZE  INTO L25B-TME-APPMT                        
           END-STRING.                                                          
           MOVE K225-NUM-PRINT-LETTER  TO L25B-NUM-PRT-LTR.                     
           IF K225-NUM-NRIC(1:1) = '#'                                          
              MOVE SPACES              TO L25B-NUM-NRIC                         
           ELSE                                                                 
              MOVE K225-NUM-NRIC       TO L25B-NUM-NRIC                         
           END-IF.                                                              
           MOVE K225-NME-HA1           TO L25B-NME-NAME.                        
           MOVE K225-NUM-TEL           TO L25B-NUM-TEL.                         
           MOVE K225-NUM-CAT           TO L25B-NUM-CAT.                         
                                                                                
           WRITE PRINT-REC           FROM L25B-DETAILS AFTER 1.                 
                                                                                
           ADD 1                       TO WS-LINE-CNT.                          
           IF WS-LINE-CNT > 63                                                  
              MOVE 0                   TO WS-LINE-CNT                           
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       9000-CLOSE-FILES.                                                        
      *--------------------*                                                    
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C25B'.                                   
           DISPLAY 'CURRENT DATE : ' L25B-RUN-DATE.                             
           DISPLAY SPACES.                                                      
           DISPLAY '*--------------- COUNTERS ---------------*'.                
           DISPLAY 'NUMBER OF BP13F205 RECORDS  : ' WS-F205-READ-CTR.           
           DISPLAY 'NUMBER OF BP13K225 RECORDS  : ' WS-K225-READ-CNT.           
                                                                                
            CLOSE  BP13F205                                                     
                   BP13K225                                                     
                   BP13L25B.                                                    
                                                                                
            IF WS-K225-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR:BP13K225 STATUS ' WS-K225-STATUS            
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
