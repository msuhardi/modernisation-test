       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB9L.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  26 JUL 2016.                                              
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  MATCH BP13F730 (8AM) WITH BP13F55Z(11AM),     *                
      *          OUTPUT CASES NOT FOUND IN 8AM FILE            *                
      *          OUTPUT CASES FOUND IN 8AM FILE WITH CHANGES   *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP136363 FNP1 260716  NEW PROGRAM                      *                
      * BP136363 FNP1 090916  USE F730 8AM FILE INSTEAD OF F55Z*                
      *========================================================*                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT P13F55ZA ASSIGN TO P13F55ZA.                                  
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
           SELECT P13F55ZB ASSIGN TO P13F55ZB.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       COPY BP13F730.                                                           
                                                                                
       FD   P13F55ZA                                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       COPY BP13F55Z.                                                           
                                                                                
       FD  P13F55ZB                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 400 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORD IS STANDARD.                                            
       01  P13F55ZB-REC               PIC X(400).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F730-READ          PIC 9(7)    VALUE ZEROES.                   
           05 WS-F55Z-READ          PIC 9(7)    VALUE ZEROES.                   
           05 WS-F55ZB-WRITE        PIC 9(7)    VALUE ZEROES.                   
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF          PIC X       VALUE 'N'.                      
           05  WS-F55Z-EOF          PIC X       VALUE 'N'.                      
           05  WS-DIFF              PIC X       VALUE 'N'.                      
                                                                                
       01  WS-WORKING-FIELDS.                                                   
           05  WS-F730-MATCHED      PIC 9(8)    VALUE ZEROES.                   
           05  WS-F730-NOMATCH      PIC 9(8)    VALUE ZEROES.                   
           05  WS-F55Z-NOMATCH      PIC 9(8)    VALUE ZEROES.                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *------------------------------------------------------------             
       0000-MAIN.                                                               
      *------------------------------------------------------------             
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 1100-READ-BP13F730   THRU 1100-EXIT.                         
           PERFORM 1200-READ-BP13F55Z   THRU 1200-EXIT.                         
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL F730-NUM-REGN = HIGH-VALUES AND                              
                   F55Z-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1000-OPEN-ROUTINE.                                                       
      *------------------------------------------------------------             
                                                                                
           OPEN INPUT  P13F55ZA                                                 
                       BP13F730                                                 
                OUTPUT P13F55ZB.                                                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1100-READ-BP13F730.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13F730 AT END                                                 
              MOVE HIGH-VALUES TO F730-NUM-REGN                                 
                GO TO 1100-EXIT.                                                
                                                                                
           ADD 1 TO WS-F730-READ.                                               
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       1200-READ-BP13F55Z.                                                      
      *------------------------------------------------------------             
                                                                                
           READ P13F55ZA AT END                                                 
              MOVE HIGH-VALUES TO F55Z-NUM-REGN                                 
                GO TO 1200-EXIT.                                                
                                                                                
           ADD 1 TO WS-F55Z-READ.                                               
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       2000-PROCESS-RECORDS.                                                    
      *------------------------------------------------------------             
                                                                                
           IF F730-NUM-REGN = F55Z-NUM-REGN                                     
              ADD 1 TO WS-F730-MATCHED                                          
              WRITE P13F55ZB-REC FROM BP13F55Z-REC                              
              ADD 1 TO WS-F55ZB-WRITE                                           
              PERFORM 1100-READ-BP13F730    THRU 1100-EXIT                      
              PERFORM 1200-READ-BP13F55Z    THRU 1200-EXIT                      
           ELSE                                                                 
              IF F730-NUM-REGN < F55Z-NUM-REGN                                  
                 ADD 1 TO WS-F730-NOMATCH                                       
                 PERFORM 1100-READ-BP13F730   THRU 1100-EXIT                    
              ELSE                                                              
                 ADD 1 TO WS-F55Z-NOMATCH                                       
                 PERFORM 1200-READ-BP13F55Z    THRU 1200-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '***********BP13CB9L END OF JOB*******************'.         
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'NO OF RECORDS READ FROM BP13F730   ' WS-F730-READ.          
           DISPLAY 'NO OF RECORDS READ FROM BP13F55Z   ' WS-F55Z-READ.          
           DISPLAY 'NO OF RECORDS MATCHED W/ F55Z   ' WS-F730-MATCHED.          
           DISPLAY 'FOUND F55ZA   NOT MTCH W/ F55Z  ' WS-F730-NOMATCH.          
           DISPLAY 'FOUND F55Z    NOT MTCH W/ F55Z  ' WS-F55Z-NOMATCH.          
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF OUPUT                     ' WS-F55ZB-WRITE.           
                                                                                
                                                                                
           CLOSE P13F55ZA                                                       
                 BP13F730                                                       
                 P13F55ZB.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
