      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.    BP13C14E.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  19/06/23.                                                 
      *DATE-COMPILED.                                                           
      *REMARKS.                                                                 
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME  :    SYSTEM OF COMMITMENT   (SOC)               *        
      *                                                                *        
      *   SYSTEM ID    :    BP13                                       *        
      *                                                                *        
      *   OBJECTIVE    :    TO TRANSFER THE AMT REFUNDED BY EXTERNAL   *        
      *                     SOLICITOR                                  *        
      *                                                                *        
      *   INPUT FILES  :    BP13F310                                   *        
      *                :    BP13F885                                   *        
      *   I-O   FILES  :    BP13K800                                   *        
      *   OUTPUT FILES :    BP13F130                                   *        
      *                                                                *        
      *   REPORT       :    1. BP13L14E                                *        
      *                                                                *        
      *   MODIFICATION HISTORY:                                        *        
      *   =====================                                        *        
      *   CHGE-NO  BY    DATE   DESCRIPTION                            *        
      *   -------  ---  ------  -------------------------------------  *        
      *   BP139632 ZAR7 19/06/23 NEW PROGRAM                           *        
      *   BP139632 ZAR7 11/07/23 TO READ BP13K885 FOR UPDATE           *        
      *   BP139652 KAR5 16/08/23 TO CATER FOR PARTIAL REFUND OF CPF    *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT    DIVISION.                                                 
      ******************************************************************        
      *================================================================*        
       CONFIGURATION  SECTION.                                                  
      *================================================================*        
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
      *================================================================*        
       INPUT-OUTPUT SECTION.                                                    
      *================================================================*        
      *----------------------------------------------------------------*        
       FILE-CONTROL.                                                            
      *----------------------------------------------------------------*        
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K800-NUM-REGN                                   
                  ACCESS MODE IS RANDOM                                         
                  FILE STATUS IS WS-K800-FILE-STATUS.                           
                                                                                
           SELECT BP13K885 ASSIGN TO BP13K885                                   
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K885-KEY-FLD                                    
                  ACCESS MODE IS DYNAMIC                                        
                  FILE STATUS IS WS-K885-FILE-STATUS.                           
                                                                                
           SELECT BP13F310  ASSIGN       TO BP13F310.                           
           SELECT BP13F130  ASSIGN       TO BP13F130.                           
           SELECT BP13L14E  ASSIGN       TO BP13L14E.                           
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *================================================================*        
       FILE SECTION.                                                            
      *================================================================*        
                                                                                
       FD  BP13K800                                                             
                                   BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 2000 CHARACTERS              
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K885                                                             
                                   BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 400 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13K885.                                                           
                                                                                
       FD  BP13F130                                                             
                                   BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 150 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F130.                                                           
                                                                                
       FD  BP13F310                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 150  CHARACTERS.                                     
                                                                                
       01  BP13F310-TRANS-REC.                                                  
           05  F310-KEY-FLD.                                                    
               10  F310-REGN-NO                   PIC X(8).                     
               10  F310-CDE-TRANS                 PIC X(2).                     
           05  FILLER                             PIC X(6).                     
           05  F310-SCH-ACC.                                                    
               10  F310-SCH                       PIC X(4).                     
               10  F310-ACC                       PIC X(5).                     
               10  F310-LESSEE-CODE               PIC X(2).                     
           05  F310-CDE-PREMIUM                   PIC X.                        
           05  F310-CDE-ELIG-TAG                  PIC X.                        
           05  F310-DTE-TRANS.                                                  
               10  F310-DTE-CC                    PIC X(2).                     
               10  F310-DTE-YY                    PIC X(2).                     
               10  F310-DTE-MM                    PIC X(2).                     
               10  F310-DTE-DD                    PIC X(2).                     
           05  F310-NUM-PREMIUM-RATE              PIC 9(2).                     
           05  F310-CDE-SALES-TYPE                PIC X(2).                     
           05  F310-CDE-BO                        PIC X(2).                     
           05  F310-CDE-TC                        PIC X(2).                     
           05  F310-CDE-CC                        PIC X(2).                     
           05  F310-NUM-SCHEDULE                  PIC X(10).                    
           05  F310-NUM-ACCEPT-INFO REDEFINES F310-NUM-SCHEDULE.                
               10  F310-NUM-NRIC                  PIC X(9).                     
               10  FILLER                         PIC X(1).                     
           05  F310-CDE-LESSEE                    PIC X(2).                     
           05  F310-CDE-ECD                       PIC X.                        
           05  F310-CDE-NT                        PIC X(3).                     
           05  F310-CDE-FLAT-TYPE                 PIC X(2).                     
           05  F310-NUM-BLK                       PIC X(05).                    
           05  F310-NME-STREET                    PIC X(20).                    
           05  F310-NUM-HSE.                                                    
               10  F310-NUM-LEVEL-R.                                            
                   15  F310-NUM-LEVEL             PIC X(02).                    
                   15  FILLER                     PIC X(01).                    
               10  F310-NUM-MAIN                  PIC X(05).                    
           05  F310-NUM-POSTAL                    PIC X(06).                    
           05  F310-CDE-BUC.                                                    
               10  F310-CDE-CLS-DESG              PIC X.                        
               10  F310-CDE-TYP-DESG              PIC X(3).                     
               10  F310-CDE-MODL-DESG             PIC X(2).                     
               10  F310-CDE-CONFIG-DESG           PIC X.                        
               10  F310-CDE-CLS-ACTUSE            PIC X.                        
               10  F310-CDE-TYP-ACTUSE            PIC X(2).                     
           05  F310-CDE-SA-TP-TAG                 PIC X.                        
           05  F310-CDE-RELEASE-PAYMENT           PIC X.                        
           05  F310-AMT-STAMP-DUTIES              PIC 9(5)V99.                  
           05  F310-NUM-ZONE                      PIC X(2).                     
           05  F310-CDE-OCS-TAG                   PIC X(1).                     
           05  FILLER                             PIC X(8).                     
           05  F310-DTE-UPDATE                    PIC X(8).                     
           05  F310-USERID                        PIC X(8).                     
                                                                                
       FD   BP13L14E                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   L14E-REC            PIC X(132).                                     
                                                                                
      *================================================================*        
       WORKING-STORAGE SECTION.                                                 
      *================================================================*        
      *----------------------------------------------------------------*        
      *     ERROR VARIABLES                                            *        
      *----------------------------------------------------------------*        
       01  WS-ERR-MSG                     PIC X(80).                            
                                                                                
      *----------------------------------------------------------------*        
      *     FLAG VARIABLES                                             *        
      *----------------------------------------------------------------*        
       01  WS-FLAGS.                                                            
           05  WS-K800-FILE-STATUS        PIC 9(2).                             
           05  WS-K885-FILE-STATUS        PIC 9(2).                             
           05  WS-BP13F310-EOF            PIC X(1)   VALUE 'N'.                 
           05  WS-EOF-K885                PIC X(1)   VALUE 'N'.                 
           05  WS-K885-FND                PIC X(1)   VALUE 'N'.                 
       01  WS-CD                          PIC S9(7)V99   VALUE ZEROES.          
       01  WS-NO-F130-WR                  PIC 9(5)  VALUE ZEROES.               
       01  WS-K885-AMT-REF-EX-SOL         PIC 9(7)V99 VALUE ZEROES.             
       01  WS-K885-AMT-REFUND             PIC 9(7)V99 VALUE ZEROES.             
                                                                                
      *----------------------------------------------------------------*        
      *    COUNTER VARIABLES                                           *        
      *----------------------------------------------------------------*        
       01  WS-COUNTERS.                                                         
           05  WS-CNT-BP13F310-READ       PIC 9(8)   VALUE ZEROES.              
           05  WS-CNT-K800-WRITE          PIC 9(8)   VALUE ZEROES.              
           05  WS-CNT-K885-WRITE          PIC 9(8)   VALUE ZEROES.              
           05  WS-CNT-F130-WRITE          PIC 9(8)   VALUE ZEROES.              
           05  WS-CNT-L14E-WRITE          PIC 9(8)   VALUE ZEROES.              
                                                                                
      *----------------------------------------------------------------*        
      *    PRINTING VARIABLES                                          *        
      *----------------------------------------------------------------*        
       01  WS-PRINT-VARIABLES.                                                  
           05  WS-SEQ-CNT                 PIC 9(5)   VALUE ZEROES.              
           05  WS-PAGE-CNT                PIC 9(5)   VALUE 0.                   
           05  WS-LINE-CNT                PIC 9(2)   VALUE 0.                   
                                                                                
      *----------------------------------------------------------------*        
      *  MISCELLEANEOUS VARIABLES                                      *        
      *----------------------------------------------------------------*        
       01  WS-K885-KEY-FLD                PIC X(17).                            
       01  WS-STRING-POINTER              PIC 9(2).                             
       01  WS-CURRENT-DATE.                                                     
               10  WS-CURR-YY             PIC X(4).                             
               10  WS-CURR-MM             PIC X(2).                             
               10  WS-CURR-DD             PIC X(2).                             
                                                                                
       01 IDX                             PIC 9(1).                             
       01 WS-FILLER.                                                            
          05 WS-NUM-NRIC-VLTRY  OCCURS 4 TIMES.                                 
             10 WS-K885-NUM-NRIC          PIC X(09).                            
             10 WS-K885-AMT-VOLUNTARY     PIC S9(7)V99 VALUE ZEROES.            
                                                                                
      *----------------------------------------------------------------*        
      *    REPORT LAYOUT OF SUMMARY AGEING REPORT.                     *        
      *----------------------------------------------------------------*        
       01  L14E-PRT-HDR-1.                                                      
           05  FILLER                     PIC X      VALUE SPACE.               
           05  FILLER                     PIC X(8)   VALUE 'BP13L14E'.          
           05  FILLER                     PIC X(13)  VALUE SPACES.              
           05  FILLER                     PIC X(4)   VALUE 'HDB3'.              
           05  FILLER                     PIC X(14)  VALUE SPACES.              
           05  FILLER                     PIC X(39)                             
               VALUE 'S Y S T E M   O F   C O M M I T M E N T'.                 
           05  FILLER                     PIC X(10)  VALUE SPACES.              
           05  FILLER                     PIC X(7)   VALUE 'DATE : '.           
           05  L14E-PRT-DATE              PIC X(10)  VALUE SPACES.              
           05  FILLER                     PIC X(10)  VALUE SPACES.              
           05  FILLER                     PIC X(9)   VALUE 'PAGE   : '.         
           05  L14E-PRT-PAGE              PIC ZZ9.                              
                                                                                
       01  L14E-PRT-HDR-2.                                                      
           05  FILLER                     PIC X(25)  VALUE SPACES.              
           05  FILLER                     PIC X(50)                             
           VALUE 'ERROR LIST OF CASES THAT TRANSFER FROM CPF '.                 
           05  L14E-RUN-DATE              PIC X(10).                            
                                                                                
       01  L14E-PRT-HDR-3.                                                      
           05  FILLER                     PIC X(43)  VALUE SPACES.              
           05  FILLER                     PIC X(31)                             
           VALUE ' TO CASH                       '.                             
                                                                                
       01  L14E-PRT-HDR-4.                                                      
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE 'S/N'.               
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(08)  VALUE 'REGN-NO '.          
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(17)                             
               VALUE 'NRIC             '.                                       
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(70)                             
               VALUE 'EX-SOL REFUND    AMT-REFUND'.                             
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(19)                             
               VALUE '                   '.                                     
                                                                                
       01  L14E-PRT-HDR-5.                                                      
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(08)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(17)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(70)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(19)  VALUE SPACES.              
                                                                                
       01  L14E-PRT-HDR-6.                                                      
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(04)  VALUE '===='.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(08)  VALUE '========'.          
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(17)                             
               VALUE '================='.                                       
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(70)  VALUE ALL '='.             
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(19)                             
               VALUE '==================='.                                     
                                                                                
       01  L14E-PRT-DTL-1.                                                      
           05  FILLER                     PIC X(01)  VALUE SPACES.              
           05  L14E-SERIAL-NO             PIC ZZZZ9.                            
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  L14E-REGN-NO               PIC X(08).                            
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  L14E-NRIC                  PIC X(09).                            
           05  FILLER                     PIC X(06)  VALUE SPACES.              
           05  L14E-AMT-EX-SOL            PIC ZZZZZZZZ9.99.                     
           05  FILLER                     PIC X(08)  VALUE SPACES.              
           05  L14E-AMT-REFUND            PIC ZZZZZZZZ9.99.                     
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(04)  VALUE SPACES.              
           05  FILLER                     PIC X(10).                            
           05  FILLER                     PIC X(03)  VALUE SPACE.               
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *================================================================*        
       0000-MAIN-ROUTINE.                                                       
      *================================================================*        
                                                                                
           PERFORM 1000-INITIALIZATION      THRU 1000-EXIT.                     
           PERFORM 2000-PROCESS-ROUTINE     THRU 2000-EXIT                      
                   UNTIL WS-BP13F310-EOF = 'Y'.                                 
           PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       1000-INITIALIZATION.                                                     
      *================================================================*        
           OPEN INPUT  BP13F310                                                 
                I-O    BP13K800                                                 
                       BP13K885                                                 
                OUTPUT BP13F130                                                 
                       BP13L14E.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO    WS-CURRENT-DATE.              
           STRING WS-CURR-DD '/' WS-CURR-MM '/'   WS-CURR-YY                    
                  DELIMITED BY SIZE INTO L14E-PRT-DATE.                         
                                                                                
           READ  BP13F310 AT END                                                
                 MOVE 'Y'          TO  WS-BP13F310-EOF.                         
                                                                                
           IF WS-BP13F310-EOF = 'Y'                                             
              STRING 'NO PRINT FILE RECORDS FOUND'                              
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
           ELSE                                                                 
              ADD 1                TO WS-CNT-BP13F310-READ                      
           END-IF.                                                              
           MOVE 60                 TO WS-LINE-CNT.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       2000-PROCESS-ROUTINE.                                                    
      *================================================================*        
                                                                                
           IF   F310-CDE-TRANS  = 'ES'                                          
             MOVE F310-REGN-NO  TO K800-NUM-REGN                                
           ELSE                                                                 
           MOVE SPACES TO K800-NUM-REGN.                                        
                                                                                
           READ  BP13K800.                                                      
                                                                                
           IF WS-K800-FILE-STATUS = '00'                                        
              PERFORM 3600-START-K885  THRU 3600-EXIT                           
              IF WS-K885-FND = 'Y'                                              
                PERFORM 3100-READ-K885   THRU 3100-EXIT                         
                PERFORM 3000-UPDATE-K800 THRU 3000-EXIT                         
                PERFORM 3200-UPDATE-K885 THRU 3200-EXIT                         
              ELSE                                                              
                PERFORM 4000-FORMAT-L14E THRU 4000-EXIT                         
                PERFORM 6000-PRINT-L14E  THRU 6000-EXIT                         
              END-IF                                                            
           ELSE                                                                 
              PERFORM 4000-FORMAT-L14E THRU 4000-EXIT                           
              PERFORM 6000-PRINT-L14E  THRU 6000-EXIT                           
           END-IF.                                                              
                                                                                
           PERFORM VARYING IDX FROM 1 BY 1                                      
              UNTIL IDX > 4                                                     
              MOVE SPACES       TO WS-K885-NUM-NRIC(IDX)                        
              MOVE ZEROES       TO WS-K885-AMT-VOLUNTARY(IDX)                   
           END-PERFORM.                                                         
                                                                                
           READ  BP13F310 AT END                                                
                 MOVE 'Y'        TO WS-BP13F310-EOF                             
                 GO  TO 2000-EXIT                                               
                 END-READ.                                                      
                                                                                
           ADD   1                   TO WS-CNT-BP13F310-READ.                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3000-UPDATE-K800.                                                        
      *================================================================*        
           IF K885-AMT-REF-EX-SOL NOT NUMERIC                                   
              MOVE ZEROES  TO K885-AMT-REF-EX-SOL                               
           END-IF.                                                              
                                                                                
           IF K885-AMT-VOLUNTARY  NOT NUMERIC                                   
              MOVE ZEROES  TO K885-AMT-VOLUNTARY                                
           END-IF.                                                              
                                                                                
           ADD K885-AMT-REF-EX-SOL    TO K800-AMT-SOC-CASH                      
           ADD K885-AMT-VOLUNTARY     TO K800-AMT-SOC-CASH                      
                                                                                
           IF WS-K885-NUM-NRIC(1) NOT = SPACES AND LOW-VALUES                   
              COMPUTE K800-AMT-CPF1-PAID = K800-AMT-CPF1-PAID -                 
                                           WS-K885-AMT-VOLUNTARY(1)             
           END-IF.                                                              
           IF WS-K885-NUM-NRIC(2) NOT = SPACES AND LOW-VALUES                   
              COMPUTE K800-AMT-CPF2-PAID = K800-AMT-CPF2-PAID -                 
                                           WS-K885-AMT-VOLUNTARY(2)             
           END-IF.                                                              
           IF WS-K885-NUM-NRIC(3) NOT = SPACES AND LOW-VALUES                   
              COMPUTE K800-AMT-CPF3-PAID = K800-AMT-CPF3-PAID -                 
                                           WS-K885-AMT-VOLUNTARY(3)             
           END-IF.                                                              
           IF WS-K885-NUM-NRIC(4) NOT = SPACES AND LOW-VALUES                   
              COMPUTE K800-AMT-CPF4-PAID = K800-AMT-CPF4-PAID -                 
                                           WS-K885-AMT-VOLUNTARY(4)             
           END-IF.                                                              
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF WS-K800-FILE-STATUS = '00'                                        
              ADD 1 TO  WS-CNT-K800-WRITE                                       
              PERFORM 3500-UPDATE-F130 THRU 3500-EXIT                           
           ELSE                                                                 
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3100-READ-K885.                                                          
      *================================================================*        
                                                                                
           MOVE SPACES          TO BP13K885-REC.                                
           MOVE WS-K885-KEY-FLD TO K885-KEY-FLD                                 
                                                                                
           READ  BP13K885.                                                      
                                                                                
           IF WS-K885-FILE-STATUS = '00'                                        
             IF K885-AMT-REF-EX-SOL = K885-AMT-REFUND                           
               MOVE 'Y'  TO WS-K885-FND                                         
             ELSE                                                               
               DISPLAY 'REFUND NOT = EX-SOL-REF ' F310-REGN-NO                  
             END-IF                                                             
           ELSE                                                                 
             IF WS-K885-FILE-STATUS = '23'                                      
                MOVE 'N'  TO WS-K885-FND                                        
             ELSE                                                               
               DISPLAY 'BP13K885 - FILE ERROR ' WS-K885-FILE-STATUS             
               MOVE WS-K885-FILE-STATUS TO RETURN-CODE                          
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3200-UPDATE-K885.                                                        
      *================================================================*        
           PERFORM 3100-READ-K885  THRU 3100-EXIT.                              
           IF WS-K885-FND NOT = 'Y'                                             
              GO TO 3200-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE ZEROES   TO    K885-AMT-REFUND.                                 
                                                                                
           REWRITE BP13K885-REC.                                                
                                                                                
           IF WS-K885-FILE-STATUS = '00'                                        
              ADD 1 TO  WS-CNT-K885-WRITE                                       
           ELSE                                                                 
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3500-UPDATE-F130.                                                        
      *================================================================*        
                                                                                
              MOVE  SPACES               TO BP13F130-REC                        
              MOVE  '27'                 TO F130-TRANS-TYPE                     
              MOVE  'TR'                 TO F130-CDE-PAYMENT-TYPE               
              MOVE  F310-DTE-TRANS       TO F130-DTE-POST                       
              MOVE  K885-AMT-REF-EX-SOL  TO F130-AMT-RECEIPT                    
              ADD   1                    TO WS-NO-F130-WR                       
              MOVE F310-REGN-NO        TO F130-NUM-REGN                         
              MOVE  F310-DTE-UPDATE    TO F130-DTE-TRANS                        
              MOVE ZEROS                 TO F130-COUNT-HISTORY                  
              WRITE BP13F130-REC.                                               
              ADD 1 TO  WS-CNT-F130-WRITE.                                      
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3600-START-K885.                                                         
      *================================================================*        
                                                                                
           MOVE SPACES          TO BP13K885-REC                                 
                                   WS-EOF-K885                                  
                                   WS-K885-FND.                                 
                                                                                
           MOVE ZEROES          TO K885-AMT-REF-EX-SOL.                         
           MOVE F310-REGN-NO    TO K885-NUM-REGN                                
                                                                                
           START  BP13K885 KEY  >= K885-KEY-FLD                                 
                                                                                
           EVALUATE WS-K885-FILE-STATUS                                         
           WHEN 00                                                              
           WHEN 02                                                              
              PERFORM 3700-NEXT-K885 THRU 3700-EXIT                             
                 UNTIL WS-EOF-K885 = 'Y' OR                                     
                       F310-REGN-NO NOT = K885-NUM-REGN                         
           WHEN 23                                                              
                MOVE ZEROES  TO K885-AMT-REF-EX-SOL                             
                                K885-AMT-REFUND                                 
           WHEN OTHER                                                           
               DISPLAY 'BP13K885 - FILE ERROR ' WS-K885-FILE-STATUS             
               MOVE WS-K885-FILE-STATUS TO RETURN-CODE                          
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3700-NEXT-K885.                                                          
      *================================================================*        
                                                                                
           READ BP13K885 NEXT RECORD                                            
                                                                                
           EVALUATE WS-K885-FILE-STATUS                                         
           WHEN 00                                                              
           WHEN 02                                                              
                 IF F310-REGN-NO = K885-NUM-REGN                                
                    DISPLAY 'READNEXT - '                                       
                         'F310 - ' F310-REGN-NO                                 
                         ' K885 - ' K885-NUM-REGN                               
                         ' SOL  - ' K885-AMT-REF-EX-SOL                         
                         ' REF  - ' K885-AMT-REFUND                             
                   IF K885-AMT-REF-EX-SOL NOT NUMERIC                           
                      MOVE ZEROES  TO K885-AMT-REF-EX-SOL                       
                   END-IF                                                       
                   IF K885-AMT-REFUND     NOT NUMERIC                           
                      MOVE ZEROES  TO K885-AMT-REFUND                           
                   END-IF                                                       
                   IF K885-AMT-REF-EX-SOL = K885-AMT-REFUND                     
                     MOVE 'Y'  TO WS-K885-FND                                   
                     MOVE K885-KEY-FLD   TO WS-K885-KEY-FLD                     
                   ELSE                                                         
                     MOVE K885-AMT-REF-EX-SOL  TO WS-K885-AMT-REF-EX-SOL        
                     MOVE K885-AMT-REFUND      TO WS-K885-AMT-REFUND            
                     DISPLAY 'REFUND NOT = EX-SOL-REF ' F310-REGN-NO            
                   END-IF                                                       
                                                                                
                   IF K885-PARTIAL-REFUND-TAG = 'Y'                             
                      IF K885-NUM-NRIC = K800-NUM-NRIC1                         
                         MOVE K885-NUM-NRIC   TO WS-K885-NUM-NRIC(1)            
                         MOVE K885-AMT-VOLUNTARY                                
                                      TO WS-K885-AMT-VOLUNTARY(1)               
                      END-IF                                                    
                      IF K885-NUM-NRIC = K800-NUM-NRIC2                         
                         MOVE K885-NUM-NRIC   TO WS-K885-NUM-NRIC(2)            
                         MOVE K885-AMT-VOLUNTARY                                
                                      TO WS-K885-AMT-VOLUNTARY(2)               
                      END-IF                                                    
                      IF K885-NUM-NRIC = K800-NUM-NRIC3                         
                         MOVE K885-NUM-NRIC   TO WS-K885-NUM-NRIC(3)            
                         MOVE K885-AMT-VOLUNTARY                                
                                      TO WS-K885-AMT-VOLUNTARY(3)               
                      END-IF                                                    
                      IF K885-NUM-NRIC = K800-NUM-NRIC4                         
                         MOVE K885-NUM-NRIC   TO WS-K885-NUM-NRIC(4)            
                         MOVE K885-AMT-VOLUNTARY                                
                                      TO WS-K885-AMT-VOLUNTARY(4)               
                      END-IF                                                    
                   END-IF                                                       
                 ELSE                                                           
                  MOVE 'Y'     TO WS-EOF-K885                                   
                 END-IF                                                         
           WHEN 10                                                              
                MOVE 'Y'     TO WS-EOF-K885                                     
           WHEN OTHER                                                           
               DISPLAY 'BP13K885 - FILE ERROR ' WS-K885-FILE-STATUS             
               MOVE WS-K885-FILE-STATUS TO RETURN-CODE                          
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3700-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4000-FORMAT-L14E.                                                        
      *================================================================*        
                                                                                
           ADD 1                       TO WS-SEQ-CNT.                           
           MOVE WS-SEQ-CNT             TO L14E-SERIAL-NO.                       
           MOVE F310-REGN-NO           TO L14E-REGN-NO.                         
                                                                                
                                                                                
           MOVE K885-NUM-NRIC           TO L14E-NRIC.                           
           MOVE WS-K885-AMT-REF-EX-SOL  TO L14E-AMT-EX-SOL.                     
           MOVE WS-K885-AMT-REFUND      TO L14E-AMT-REFUND.                     
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       6000-PRINT-L14E.                                                         
      *================================================================*        
           IF WS-LINE-CNT > 45                                                  
              PERFORM 6100-PRINT-L14E-HDG THRU 6100-EXIT                        
           END-IF.                                                              
                                                                                
           WRITE L14E-REC    FROM L14E-PRT-DTL-1   AFTER 2.                     
           ADD 3                    TO WS-LINE-CNT.                             
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       6100-PRINT-L14E-HDG.                                                     
      *================================================================*        
           ADD   1                   TO WS-PAGE-CNT.                            
           MOVE  WS-PAGE-CNT         TO L14E-PRT-PAGE.                          
           MOVE  L14E-PRT-DATE       TO L14E-RUN-DATE.                          
           MOVE  8                   TO WS-LINE-CNT.                            
                                                                                
           WRITE L14E-REC    FROM L14E-PRT-HDR-1   AFTER PAGE.                  
           WRITE L14E-REC    FROM L14E-PRT-HDR-2   AFTER 2.                     
           WRITE L14E-REC    FROM L14E-PRT-HDR-3   AFTER 1.                     
           WRITE L14E-REC    FROM L14E-PRT-HDR-4   AFTER 2.                     
           WRITE L14E-REC    FROM L14E-PRT-HDR-5   AFTER 1.                     
           WRITE L14E-REC    FROM L14E-PRT-HDR-6   AFTER 1.                     
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9900-HANDLE-ABEND.                                                       
      *================================================================*        
            CLOSE   BP13F310                                                    
                    BP13F130                                                    
                    BP13K800                                                    
                    BP13K885                                                    
                    BP13L14E.                                                   
                                                                                
           IF WS-K800-FILE-STATUS NOT = ZEROES                                  
              DISPLAY 'BP13K800 - CLOSE ERROR ' WS-K800-FILE-STATUS             
              MOVE WS-K800-FILE-STATUS TO RETURN-CODE.                          
                                                                                
           IF WS-K885-FILE-STATUS NOT = ZEROES                                  
              DISPLAY 'BP13K885 - CLOSE ERROR ' WS-K885-FILE-STATUS             
              MOVE WS-K885-FILE-STATUS TO RETURN-CODE.                          
                                                                                
            DISPLAY '***************************************'                   
            DISPLAY '*    BP13C14E  CONTROL TOTAL          *'                   
            DISPLAY '***************************************'                   
            DISPLAY '* NO. OF INPUT READ : ' WS-CNT-BP13F310-READ.              
            DISPLAY '***************************************'                   
            DISPLAY '* ABEND-DESC: ' WS-ERR-MSG.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9900-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9999-CLOSE-FILES.                                                        
      *================================================================*        
            CLOSE   BP13F310                                                    
                    BP13K800                                                    
                    BP13L14E.                                                   
                                                                                
           IF WS-K800-FILE-STATUS NOT = ZEROES                                  
              DISPLAY 'BP13K800 - CLOSE ERROR ' WS-K800-FILE-STATUS             
              MOVE WS-K800-FILE-STATUS TO RETURN-CODE.                          
                                                                                
            DISPLAY '***************************************'                   
            DISPLAY '*    BP13C14E  CONTROL TOTAL          *'                   
            DISPLAY '***************************************'                   
            DISPLAY '* NO. OF INPUT READ : ' WS-CNT-BP13F310-READ.              
            DISPLAY '***************************************'                   
            DISPLAY '* REMARKS: ' WS-ERR-MSG.                                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
