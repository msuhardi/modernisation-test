      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C950.                                                 
       AUTHOR.        ABH.                                                      
      *DATE-WRITTEN.  26/06/06.                                                 
                                                                                
      * ============================================================ *          
      *                SYSTEM OF COMMITMENT  (BP13)                  *          
      * ============================================================ *          
      *    OBJECTIVE                                                 *          
      *      1.  THIS SUBROUTINE GETS THE INTEREST RATE FROM         *          
      *          AB03K050.                                           *          
      *                                                              *          
      * ============================================================ *          
      * CHG-NO   BY   DATE    DESCRIPTION                            *          
      * -------- ---  ------  -----------                            *          
      * BP132772 BA3  260606  NEW PROGRAM                            *          
      * BP133173 BA3  221007  REMOVED HARCODE VALUE OF               *          
      *                       K050-CDE-LOAN-SCH                      *          
      * ============================================================ *          
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AB03K050  ASSIGN        TO AB03K050                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K050-KEY                           
                            FILE STATUS   IS AB03K050-STATUS.                   
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
       FILE SECTION.                                                            
                                                                                
       FD   AB03K050                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY AB03K050.                                                           
                                                                                
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-C908-PARAMETERS.                                                  
           05  WS-INPUT-PARAMETERS.                                             
               10  WS-C908-TXT-IN-SIZE       PIC 9(2).                          
               10  WS-C908-TXT-INPUT         PIC X(30).                         
           05  WS-OUTPUT-PARAMETERS.                                            
               10  WS-C908-TXT-OUT-SIZE      PIC 9(2).                          
               10  WS-C908-TXT-OUTPUT        PIC X(40).                         
           05  WS-C908-RETCODE               PIC S9(04)  COMP.                  
                                                                                
       01  WS-INDICATORS.                                                       
           05  AB03K050-STATUS          PIC 99     VALUE ZEROS.                 
                                                                                
           05  WS-K050-EOF              PIC X(1)   VALUE 'N'.                   
           05  WS-AB03K050-REC          PIC X(200) VALUE SPACES.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F310-CNT              PIC 9(2)   VALUE ZEROS.                 
                                                                                
       01  WS-COMP-VARIABLES.                                                   
           05  WS-COMPUTE-DATE.                                                 
               10  WS-COMPUTE-CC        PIC 99.                                 
               10  WS-COMPUTE-YY        PIC 99     VALUE ZEROS.                 
               10  WS-COMPUTE-MM        PIC 99     VALUE ZEROS.                 
               10  WS-COMPUTE-DD        PIC 99     VALUE ZEROS.                 
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
       01  C950-FIELD.                                                          
           05  C950-PROCESS-DATE       PIC X(08).                               
           05  C950-PROCESS-DATE-NUM REDEFINES C950-PROCESS-DATE.               
               10  C950-PROCESS-CC     PIC 99.                                  
               10  C950-PROCESS-YY     PIC 99.                                  
               10  C950-PROCESS-MM     PIC 99.                                  
               10  C950-PROCESS-DD     PIC 99.                                  
           05  C950-INTREST-RATE       PIC 9(02)V9(4).                          
           05  C950-FACT               PIC 9(02).                               
           05  C950-REPYMT-PERIOD      PIC 9(02).                               
           05  C950-CDE-LOAN-SCH       PIC X(01).                               
                                                                                
                                                                                
      *-------------------------------------*                                   
       PROCEDURE DIVISION USING C950-FIELD.                                     
      *-------------------------------------*                                   
                                                                                
      ******************************************************************        
       1000-MAIN.                                                               
      ******************************************************************        
           OPEN INPUT AB03K050.                                                 
                                                                                
           IF AB03K050-STATUS NOT = 0 AND 97                                    
              DISPLAY 'OPENING ERROR, AB03K050-STATUS ' AB03K050-STATUS         
              MOVE     AB03K050-STATUS TO RETURN-CODE                           
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           PERFORM  2000-MOVE-DATE        THRU 2000-EXIT.                       
           PERFORM  3000-PROCESS-AB03K050 THRU 3000-EXIT.                       
           PERFORM  4000-MOVE-RATE        THRU 4000-EXIT.                       
           PERFORM  9000-CLOSE-ROUTINE    THRU 9000-EXIT.                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-MOVE-DATE.                                                          
      ******************************************************************        
                                                                                
            MOVE C950-PROCESS-DATE-NUM       TO  WS-COMPUTE-DATE.               
            MOVE 01                          TO  WS-COMPUTE-DD.                 
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-AB03K050.                                                   
      ******************************************************************        
            MOVE LOW-VALUES            TO K050-DTE-INT-EFF.                     
            MOVE C950-CDE-LOAN-SCH     TO K050-CDE-LOAN-SCH.                    
                                                                                
            START  AB03K050   KEY  > K050-KEY.                                  
                                                                                
            IF  AB03K050-STATUS NOT = 00                                        
                DISPLAY 'ERROR READING AB03K050, STATUS '                       
                                       AB03K050-STATUS                          
                MOVE AB03K050-STATUS TO RETURN-CODE                             
                GO TO 9000-CLOSE-ROUTINE                                        
            END-IF.                                                             
                                                                                
            MOVE 'N' TO WS-K050-EOF.                                            
            PERFORM  UNTIL  K050-DTE-INT-EFF >= WS-COMPUTE-DATE                 
                                        OR                                      
                            WS-K050-EOF = 'Y'                                   
              MOVE  AB03K050-REC    TO  WS-AB03K050-REC                         
              READ  AB03K050  NEXT  AT  END  MOVE 'Y'  TO  WS-K050-EOF          
                                             GO TO  3000-EXIT                   
              END-READ                                                          
              IF  AB03K050-STATUS  NOT = ZERO                                   
                  DISPLAY 'ERROR READING AB03K050, STATUS '                     
                                         AB03K050-STATUS                        
                  MOVE AB03K050-STATUS TO RETURN-CODE                           
                  GO TO 9000-CLOSE-ROUTINE                                      
              END-IF                                                            
              IF  K050-CDE-LOAN-SCH NOT = C950-CDE-LOAN-SCH                     
                  MOVE  'Y'       TO     WS-K050-EOF                            
              END-IF                                                            
            END-PERFORM.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4000-MOVE-RATE.                                                          
      ******************************************************************        
                                                                                
            IF K050-DTE-INT-EFF > WS-COMPUTE-DATE  OR                           
               K050-CDE-LOAN-SCH NOT = C950-CDE-LOAN-SCH                        
               MOVE  WS-AB03K050-REC     TO   AB03K050-REC                      
            END-IF.                                                             
                                                                                
            IF K050-DTE-INT-EFF IS NOT NUMERIC                                  
               DISPLAY 'NO MATCH IN   AB03K050         '                        
               MOVE '99'            TO RETURN-CODE                              
               GO TO 9000-CLOSE-ROUTINE                                         
            END-IF.                                                             
                                                                                
      *     MOVE K050-PCT-PA-INT        TO C950-INTREST-RATE.                   
      *     MOVE K050-CDE-PA-FACTOR     TO C950-FACT.                           
                                                                                
            IF  K050-CDE-LOAN-SCH = C950-CDE-LOAN-SCH                           
                MOVE K050-CDE-PA-FACTOR  TO C950-FACT                           
            ELSE                                                                
                MOVE K050-CDE-MKT-FACTOR TO C950-FACT                           
            END-IF.                                                             
                                                                                
            IF K050-CDE-LOAN-SCH = C950-CDE-LOAN-SCH                            
               COMPUTE C950-INTREST-RATE = K050-PCT-PA-INT * 0.01               
               MOVE    K050-CDE-PA-FACTOR  TO  C950-FACT                        
            ELSE                                                                
               MOVE    K050-CDE-MKT-FACTOR  TO  C950-FACT                       
               IF C950-REPYMT-PERIOD = 1 AND                                    
                  C950-REPYMT-PERIOD <= 5                                       
                  COMPUTE C950-INTREST-RATE = K050-PCT-MKT-INT(1)               
                                            * 0.01                              
               ELSE                                                             
                 IF C950-REPYMT-PERIOD >= 6 AND                                 
                    C950-REPYMT-PERIOD <= 10                                    
                    COMPUTE C950-INTREST-RATE = K050-PCT-MKT-INT(2)             
                                              * 0.01                            
                 ELSE                                                           
                    COMPUTE C950-INTREST-RATE = K050-PCT-MKT-INT(3)             
                                           * 0.01                               
                 END-IF                                                         
               END-IF                                                           
            END-IF.                                                             
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       9000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
           CLOSE AB03K050.                                                      
                                                                                
           IF AB03K050-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR, AB03K050-STATUS ' AB03K050-STATUS.        
                                                                                
           GOBACK.                                                              
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
