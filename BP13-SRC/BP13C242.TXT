      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
                                                                                
       PROGRAM-ID.    BP13C242.                                                 
       AUTHOR.        J MOHAMED SHAMSUDEEN.                                     
      *DATE-WRITTEN.  08/12/98.                                                 
      * ============================================================== *        
      *                  SYSTEM OF COMMITMENT  (BP13)                  *        
      * ============================================================== *        
      *                                                                *        
      *   OBJECTIVE   :  1.  THIS IS A PROGRAM TO PRINT A REPORT ON    *        
      *                      ACCEPTANCES OF FLATS WITH FLOOR AREA =< 90*        
      *                                                                *        
      *   INPUT FILES :  1.  BP13F310  -  TRANSACTION FILE             *        
      *                  2.  BP13K800  -  SOC APPLICATION MASTER FILE  *        
      *                  3.  BM06K110  -  BLOCK FILE                   *        
      *                                                                *        
      *   LISTINGS    :  1.  REPORT ON FLATS =< 90 SQM FLOOR AREA      *        
      *                      (BP13L242)                                *        
      *                                                                *        
      *  CHGE-NO   BY    DATE   DESCRIPTION                            *        
      *  -------   ---  ------  -----------                            *        
      *  BP130539  JMS  081298   NEW PGM.                              *        
      *  BP130778  GBR  15/12/99 TO REMOVE CALLING BP13C904.           *        
      *  BP130768  JMS  20/12/99 TO PRINT ONLY WHEN THE CASE IS VALID. *        
      *  BP131018  MBS  18/05/01 ADD 'RED' IN THE CONDITION,           *        
      *                 IF K800-NUM-ALLO-SCHEME = 'REN'                *        
      *  BP135533  ESA1 131114   TO CATER FOR ALLOC SCHEMES            *        
      *                          'REG' & 'RNG'                         *        
      ******************************************************************        
                                                                                
      ***********************                                                   
       ENVIRONMENT DIVISION.                                                    
      ***********************                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310  ASSIGN       TO BP13F310.                           
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS BP13K800-STATUS.                    
                                                                                
                                                                                
           SELECT BM06K110  ASSIGN       TO BM06K110                            
                            ACCESS  MODE IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD   KEY IS K110-KEY-FLD                        
                            FILE STATUS  IS BM06K110-STATUS.                    
                                                                                
           SELECT BP13L242  ASSIGN       TO BP13L242.                           
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F310                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13F310.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13L242                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   BP13L242-REC         PIC X(132).                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
                                                                                
       01  WS-INDICATORS.                                                       
           05  BP13K800-STATUS              PIC 99    VALUE ZEROS.              
           05  BM06K110-STATUS              PIC 99    VALUE ZEROS.              
           05  WS-F310-EOF                  PIC X     VALUE 'N'.                
           05  K110-NOT-FOUND               PIC X     VALUE 'N'.                
           05  K800-NOT-FOUND               PIC X     VALUE 'N'.                
           05  WS-PRINT-TAG                 PIC X     VALUE 'N'.                
           05  WS-FLOOR                     PIC ZZZZZZZ.ZZ.                     
           05  WS-PRE-TYPE                  PIC X(02) VALUE SPACES.             
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F310                  PIC 9(8)  VALUE ZEROS.              
           05  WS-CNT-K110                  PIC 9(8)  VALUE ZEROS.              
           05  WS-CNT-K800                  PIC 9(8)  VALUE ZEROS.              
           05  WS-PAGE-NO                   PIC 9(4)  VALUE ZEROS.              
           05  WS-CNT                       PIC 9(2)  VALUE 99.                 
           05  WS-SEQ-NO                    PIC 9(4)  VALUE ZEROS.              
           05  WS-CNT-PROC                  PIC 9(8)  VALUE ZEROS.              
           05  WS-CNT-LETTERS               PIC 9(8)  VALUE ZEROS.              
           05  WS-CNT-SKIPPED               PIC 9(8)  VALUE ZEROS.              
           05  WS-CNT-ACCEPT                PIC 9(8)  VALUE ZEROS.              
                                                                                
       01  WS-COUNTERS-DISPLAY.                                                 
           05  WS-EDIT-CNT-F310             PIC ZZ,ZZZ,ZZ9.                     
           05  WS-EDIT-CNT-K110             PIC ZZ,ZZZ,ZZ9.                     
           05  WS-EDIT-CNT-K800             PIC ZZ,ZZZ,ZZ9.                     
           05  WS-EDIT-CNT-PROC             PIC ZZ,ZZZ,ZZ9.                     
           05  WS-EDIT-CNT-LETTERS          PIC ZZ,ZZZ,ZZ9.                     
           05  WS-EDIT-CNT-SKIPPED          PIC ZZ,ZZZ,ZZ9.                     
                                                                                
       01  WS-ADDRESS1.                                                         
           05  FILLER                       PIC X(05) VALUE 'BLOCK'.            
           05  WS-BLK-NO                    PIC X(04) VALUE SPACES.             
           05  FILLER                       PIC X(03) VALUE ' , '.              
           05  WS-ST-NAME                   PIC X(20) VALUE SPACES.             
                                                                                
       01  WS-ADDRESS2.                                                         
           05  FILLER                       PIC X(02) VALUE '# '.               
           05  WS-LEVEL-NO                  PIC X(02) VALUE SPACES.             
           05  FILLER                       PIC X(03) VALUE ' - '.              
           05  WS-MAIN-UNIT                 PIC X(04) VALUE SPACES.             
           05  FILLER                       PIC X(03) VALUE ' , '.              
           05  FILLER                       PIC X(09) VALUE 'SINGAPORE'.        
           05  FILLER                       PIC X(02) VALUE ' ('.               
           05  WS-COUNTRY-POSTCDE           PIC X(06) VALUE SPACES.             
           05  FILLER                       PIC X(01) VALUE ')'.                
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DTE.                                                          
               10  WS-DTE-YYYY              PIC X(4)  VALUE SPACES.             
               10  WS-DTE-MM                PIC X(2)  VALUE SPACES.             
               10  WS-DTE-DD                PIC X(2)  VALUE SPACES.             
                                                                                
           05  WS-EDITED-DATE.                                                  
               10  WS-EDIT-DD               PIC XX    VALUE SPACES.             
               10  FILLER                   PIC X     VALUE '/'.                
               10  WS-EDIT-MM               PIC XX    VALUE SPACES.             
               10  FILLER                   PIC X     VALUE '/'.                
               10  WS-EDIT-CY               PIC X(4)  VALUE SPACES.             
                                                                                
           05  WS-CURR-DATE.                                                    
                   15  WS-CY                PIC 9(4)  VALUE 00.                 
                   15  WS-MM                PIC 9(2)  VALUE 00.                 
                   15  WS-DD                PIC 9(2)  VALUE 00.                 
                                                                                
       01  WS-VALID-RM-TYPE                 PIC X(02).                          
           88  WS-ROOM-TYPE                 VALUE ' 4' '04' '4 '.               
                                                                                
      *-----------------------------------------------------------*             
      *    WS PRINT LAY-OUT FOR LISTING                                         
      *-----------------------------------------------------------*             
       01  L242-PR-HEAD01.                                                      
           05  FILLER                  PIC X(08) VALUE 'BP13L242'.              
           05  FILLER                  PIC X(35) VALUE SPACES.                  
           05  FILLER                  PIC X(29) VALUE                          
               '  SYSTEM  OF  COMMITMENT '.                                     
           05  FILLER                  PIC X(32) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE 'PAGE : '.               
           05  L242-PAGE               PIC ZZZZ9.                               
           05  FILLER                  PIC X(16) VALUE SPACES.                  
                                                                                
       01  L242-PR-HEAD02.                                                      
           05  FILLER                  PIC X(39) VALUE SPACES.                  
           05  FILLER                  PIC X(65) VALUE                          
               'CONTROL LIST FOR ACCEPTANCE OF FLATS'.                          
           05  FILLER                  PIC X(07) VALUE 'DATE :'.                
           05  L242-DATE               PIC X(10) VALUE SPACES.                  
                                                                                
       01  L242-PR-HEAD03.                                                      
           05  FILLER                  PIC X(12) VALUE 'ROOM TYPE : '.          
           05  L242-RM-TYPE            PIC X(02) VALUE SPACES.                  
                                                                                
       01  L242-PR-HEAD-1.                                                      
           05  FILLER                  PIC X(04)  VALUE 'S/NO'.                 
           05  FILLER                  PIC X(02)  VALUE SPACES.                 
           05  FILLER                  PIC X(08)  VALUE 'REGN  NO'.             
           05  FILLER                  PIC X(02)  VALUE SPACES.                 
           05  FILLER                  PIC X(11)  VALUE 'SCH ACCT NO'.          
           05  FILLER                  PIC X(02)  VALUE SPACES.                 
           05  FILLER                  PIC X(07)  VALUE 'IDP SCH'.              
           05  FILLER                  PIC X(02)  VALUE SPACES.                 
           05  FILLER                  PIC X(04)  VALUE ' NT '.                 
           05  FILLER                  PIC X(02)  VALUE SPACES.                 
           05  FILLER                  PIC X(03)  VALUE 'CAT'.                  
           05  FILLER                  PIC X(02)  VALUE SPACES.                 
           05  FILLER                  PIC X(13)  VALUE 'ACCEPTED DATE'.        
           05  FILLER                  PIC X(02)  VALUE SPACES.                 
           05  FILLER                  PIC X(06)  VALUE ' FLAT '.               
           05  FILLER                  PIC X(02)  VALUE SPACES.                 
           05  FILLER                  PIC X(11)  VALUE 'FLOOR  AREA'.          
           05  FILLER                  PIC X(02)  VALUE SPACES.                 
           05  FILLER                  PIC X(08)  VALUE 'BOOK FEE'.             
           05  FILLER                  PIC X(02)  VALUE SPACES.                 
           05  FILLER                  PIC X(11)  VALUE 'ADDRESS'.              
                                                                                
       01  L242-PR-HEAD-2.                                                      
           05  FILLER                  PIC X(39)  VALUE SPACES.                 
           05  FILLER                  PIC X(04)  VALUE 'CODE'.                 
           05  FILLER                  PIC X(22)  VALUE SPACES.                 
           05  FILLER                  PIC X(06)  VALUE 'STATUS'.               
           05  FILLER                  PIC X(02)  VALUE SPACES.                 
           05  FILLER                  PIC X(11)  VALUE '(IN SQ MTS)'.          
                                                                                
       01  L242-PR-DETL-1.                                                      
           05  L242-SEQ-NO                PIC ZZZ9.                             
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  L242-REGN-NO               PIC X(08) VALUE SPACES.               
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  L242-SCH-ACC               PIC X(11) VALUE SPACES.               
           05  FILLER                     PIC X(04) VALUE SPACES.               
           05  L242-IDP-SCH               PIC X(03) VALUE SPACES.               
           05  FILLER                     PIC X(05) VALUE SPACES.               
           05  L242-NT-CODE               PIC X(03) VALUE SPACES.               
           05  FILLER                     PIC X(03) VALUE SPACES.               
           05  L242-CAT                   PIC X(01) VALUE SPACES.               
           05  FILLER                     PIC X(03) VALUE SPACES.               
           05  L242-ACCEPTED-DTE          PIC X(10) VALUE SPACES.               
           05  FILLER                     PIC X(07) VALUE SPACES.               
           05  L242-FLAT-STAT             PIC X(02) VALUE SPACES.               
           05  FILLER                     PIC X(04) VALUE SPACES.               
           05  L242-FLOOR-AREA            PIC X(10) VALUE SPACES.               
           05  FILLER                     PIC X(03) VALUE SPACES.               
           05  L242-BOOK-FEE              PIC Z,ZZ9.99 VALUE ZERO.              
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  L242-ADDRESS1              PIC X(32) VALUE SPACES.               
                                                                                
       01  L242-PR-DETL-2.                                                      
           05  FILLER                     PIC X(98) VALUE SPACES.               
           05  L242-ADDRESS2              PIC X(32) VALUE SPACES.               
                                                                                
       01  L242-NIL-ACCEPT-CASES.                                               
           05  FILLER                     PIC X(10) VALUE SPACES.               
           05  FILLER                     PIC X(53) VALUE                       
               'THERE WERE NO ACCEPTED CASES FOR FLOOR AREA <= 90 SQM'.         
           05  FILLER                     PIC X(50) VALUE                       
               '  FOR RELLOCATION SCHEME (REN).'.                               
                                                                                
       01  L242-NIL-RECORDS.                                                    
           05  FILLER                     PIC X(10) VALUE SPACES.               
           05  FILLER                     PIC X(53) VALUE                       
               'THERE WERE NO RECORDS SORTED TO PRINT.'.                        
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       100-MAIN-ROUTINE.                                                        
      ******************************************************************        
                                                                                
           PERFORM 200-OPEN-FILES              THRU    200-EXIT.                
           PERFORM 300-READ-F310               THRU    300-EXIT                 
                       UNTIL WS-F310-EOF = 'Y'.                                 
           PERFORM 1000-CLOSE-ROUTINE          THRU    1000-EXIT.               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-OPEN-FILES.                                                          
      ******************************************************************        
           OPEN INPUT BP13K800                                                  
                      BM06K110                                                  
                      BP13F310                                                  
               OUTPUT BP13L242.                                                 
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR-BP13K800-STATUS ' BP13K800-STATUS          
              MOVE     BP13K800-STATUS TO RETURN-CODE                           
              PERFORM  1000-CLOSE-ROUTINE.                                      
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR-BM06K110-STATUS ' BM06K110-STATUS          
              MOVE     BM06K110-STATUS TO RETURN-CODE                           
              PERFORM  1000-CLOSE-ROUTINE.                                      
                                                                                
           MOVE 00 TO WS-PAGE-NO.                                               
           MOVE 00 TO WS-SEQ-NO.                                                
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-READ-F310.                                                           
      ******************************************************************        
      *--------------------------------------------------------*                
      *    1.   READ FILE BP13F310.                            *                
      *    2.   CHECK F310-ALLOC-STATUS.                       *                
      *--------------------------------------------------------*                
                                                                                
           READ BP13F310                                                        
                AT END MOVE 'Y' TO WS-F310-EOF                                  
                       GO TO 300-EXIT.                                          
                                                                                
           ADD  1  TO  WS-CNT-F310.                                             
           MOVE F310-CDE-FLAT-TYPE TO WS-VALID-RM-TYPE.                         
           IF NOT WS-ROOM-TYPE                                                  
              DISPLAY " ROOM TYPE IS NOT 04 FOR " F310-REGN-NO                  
              GO TO 300-EXIT                                                    
           END-IF.                                                              
           PERFORM 400-PROCESS-LISTING         THRU    400-EXIT.                
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       400-PROCESS-LISTING.                                                     
      ******************************************************************        
                                                                                
           MOVE      'N'                   TO      K800-NOT-FOUND               
                                                   K110-NOT-FOUND               
                                                   WS-PRINT-TAG.                
           PERFORM    500-READ-K800        THRU    500-EXIT.                    
           IF K800-NOT-FOUND = 'Y'                                              
              ADD   1  TO  WS-CNT-SKIPPED                                       
              GO TO    400-EXIT                                                 
           END-IF.                                                              
                                                                                
           PERFORM    600-READ-K110        THRU    600-EXIT.                    
           IF K110-NOT-FOUND = 'Y'                                              
              ADD   1  TO  WS-CNT-SKIPPED                                       
              GO TO  400-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM    700-MOVE-PIDB-INFO   THRU    700-EXIT.                    
           IF WS-PRINT-TAG = 'N'                                                
              ADD   1  TO  WS-CNT-SKIPPED                                       
              GO TO  400-EXIT                                                   
           END-IF.                                                              
                                                                                
           ADD   1    TO  WS-CNT-PROC WS-CNT-ACCEPT.                            
           PERFORM    800-PRINT-LIST       THRU    800-EXIT.                    
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       500-READ-K800.                                                           
      ******************************************************************        
                                                                                
           MOVE  SPACES         TO      BP13K800-MASTER.                        
           MOVE  F310-REGN-NO   TO      K800-NUM-REGN L242-REGN-NO.             
                                                                                
           READ  BP13K800.                                                      
           ADD   1       TO   WS-CNT-K800.                                      
                                                                                
           IF BP13K800-STATUS = 00                                              
              MOVE    'N'  TO   K800-NOT-FOUND                                  
           ELSE                                                                 
           IF BP13K800-STATUS = 23                                              
              MOVE    'Y'  TO   K800-NOT-FOUND                                  
              DISPLAY 'REC NOT FOUND IN BP13K800 ' K800-NUM-REGN                
              GO TO    500-EXIT                                                 
           ELSE                                                                 
              DISPLAY 'READ ERROR, BP13K800-STATUS ' BP13K800-STATUS            
              MOVE     BP13K800-STATUS TO RETURN-CODE                           
              PERFORM  1000-CLOSE-ROUTINE                                       
           END-IF.                                                              
                                                                                
           IF K800-NUM-SCH-ACC = SPACES OR LOW-VALUES                           
              DISPLAY 'NO FLAT ACCEPTED BY THE CASE' K800-NUM-REGN              
              MOVE    'Y'  TO    K800-NOT-FOUND                                 
              GO TO    500-EXIT                                                 
           END-IF.                                                              
                                                                                
           MOVE   K800-NUM-SCH-ACC      TO     L242-SCH-ACC.                    
           MOVE   K800-NUM-IDP-SCHEME   TO     L242-IDP-SCH.                    
           MOVE   K800-NUM-NEW-TOWN     TO     L242-NT-CODE.                    
           MOVE   K800-AMT-BK-REQ       TO     L242-BOOK-FEE.                   
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       600-READ-K110.                                                           
      ******************************************************************        
      *--------------------------------------------------------*                
      *    USE F310-SCH-ACCT-NO TO ACCESS BM06K110 FOR NEW     *                
      *        FLAT INFORMATION.                               *                
      *--------------------------------------------------------*                
           MOVE SPACES       TO K110-REC.                                       
           MOVE F310-SCH-ACC TO K110-KEY-FLD.                                   
                                                                                
           READ BM06K110.                                                       
           ADD  1  TO  WS-CNT-K110.                                             
                                                                                
           IF BM06K110-STATUS = 00 OR 02                                        
              CONTINUE                                                          
           ELSE                                                                 
           IF BM06K110-STATUS = 23                                              
              MOVE    'Y'  TO K110-NOT-FOUND                                    
              DISPLAY 'REC NOT FND, K110-KEY-FLD   ' K110-KEY-FLD               
              GO TO  600-EXIT                                                   
           ELSE                                                                 
              DISPLAY 'READ ERROR, K110-STATUS  = ' BM06K110-STATUS             
              DISPLAY '            K110-KEY-FLD = ' K110-KEY-FLD                
              PERFORM  1000-CLOSE-ROUTINE                                       
           END-IF.                                                              
                                                                                
           MOVE    K110-FLAT-STATUS         TO      L242-FLAT-STAT.             
           MOVE    K110-CATEGORY            TO      L242-CAT.                   
           MOVE    K110-DTE-ACCEPTANCE(7:2) TO      WS-EDIT-DD.                 
           MOVE    K110-DTE-ACCEPTANCE(5:2) TO      WS-EDIT-MM.                 
           MOVE    K110-DTE-ACCEPTANCE(1:4) TO      WS-EDIT-CY.                 
           MOVE    WS-EDITED-DATE           TO      L242-ACCEPTED-DTE.          
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       700-MOVE-PIDB-INFO.                                                      
      ******************************************************************        
                                                                                
           IF F310-CDE-BO NOT           = SPACES                                
              IF K800-NUM-IDP-SCHEME    = 'NRT'   AND                           
                 K800-NUM-ALLO-SCHEME   = 'REN' OR 'RED' OR                     
                                          'REG' OR 'RNG'                        
                 IF F310-QTY-AREA-STRATA <= 90                                  
                    MOVE F310-NUM-BLK    TO WS-BLK-NO                           
                    MOVE F310-NME-STREET TO WS-ST-NAME                          
                    MOVE F310-NUM-MAIN   TO WS-MAIN-UNIT                        
                    MOVE F310-NUM-LEVEL  TO WS-LEVEL-NO                         
                    MOVE F310-NUM-POSTAL TO WS-COUNTRY-POSTCDE                  
                    MOVE F310-QTY-AREA-STRATA TO WS-FLOOR                       
                                                                                
                    MOVE WS-ADDRESS1   TO L242-ADDRESS1                         
                    MOVE WS-ADDRESS2   TO L242-ADDRESS2                         
                    MOVE WS-FLOOR      TO L242-FLOOR-AREA                       
                    MOVE 'Y'           TO WS-PRINT-TAG                          
                 ELSE                                                           
                    DISPLAY 'FLOOR AREA > 90  FOR ' K800-NUM-REGN               
                    MOVE SPACES        TO L242-ADDRESS1                         
                    MOVE SPACES        TO L242-ADDRESS2                         
                    MOVE SPACES        TO L242-FLOOR-AREA                       
                    MOVE 'N'           TO WS-PRINT-TAG                          
                 END-IF                                                         
              ELSE                                                              
                 DISPLAY 'NOT A RELLOCATION SCHEME CASE ' K800-NUM-REGN         
                 MOVE SPACES           TO L242-ADDRESS1                         
                 MOVE SPACES           TO L242-ADDRESS2                         
                 MOVE SPACES           TO L242-FLOOR-AREA                       
                 MOVE 'N'              TO WS-PRINT-TAG                          
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'NO BO ASSOCIATED WITH THE CASE' K800-NUM-REGN            
              MOVE SPACES              TO L242-ADDRESS1                         
              MOVE SPACES              TO L242-ADDRESS2                         
              MOVE SPACES              TO L242-FLOOR-AREA                       
              MOVE 'N'                 TO WS-PRINT-TAG                          
           END-IF.                                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       800-PRINT-LIST.                                                          
      ******************************************************************        
                                                                                
           IF WS-CNT > 50  OR  F310-CDE-FLAT-TYPE NOT = WS-PRE-TYPE             
              PERFORM   850-PRINT-HEADING   THRU    850-EXIT                    
           END-IF.                                                              
                                                                                
           ADD   3            TO  WS-CNT.                                       
           ADD   1            TO  WS-SEQ-NO.                                    
           MOVE  WS-SEQ-NO    TO  L242-SEQ-NO.                                  
           MOVE  SPACES       TO  BP13L242-REC.                                 
           WRITE BP13L242-REC.                                                  
           WRITE BP13L242-REC FROM L242-PR-DETL-1.                              
           WRITE BP13L242-REC FROM L242-PR-DETL-2.                              
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       850-PRINT-HEADING.                                                       
      ******************************************************************        
                                                                                
           MOVE   FUNCTION CURRENT-DATE TO   WS-CURR-DATE.                      
           MOVE   WS-CY                 TO   WS-EDIT-CY.                        
           MOVE   WS-MM                 TO   WS-EDIT-MM.                        
           MOVE   WS-DD                 TO   WS-EDIT-DD.                        
           MOVE   WS-EDITED-DATE        TO   L242-DATE.                         
                                                                                
           MOVE   F310-CDE-FLAT-TYPE    TO   WS-PRE-TYPE L242-RM-TYPE.          
           MOVE   8                     TO   WS-CNT.                            
           MOVE   SPACES                TO   BP13L242-REC.                      
           WRITE  BP13L242-REC.                                                 
           ADD    1                     TO   WS-PAGE-NO.                        
           MOVE   WS-PAGE-NO            TO   L242-PAGE.                         
           WRITE  BP13L242-REC          FROM L242-PR-HEAD01 AFTER PAGE.         
           WRITE  BP13L242-REC          FROM L242-PR-HEAD02.                    
           MOVE   SPACES                TO   BP13L242-REC.                      
           WRITE  BP13L242-REC.                                                 
           WRITE  BP13L242-REC          FROM L242-PR-HEAD03.                    
           MOVE   ALL '-'               TO   BP13L242-REC.                      
           WRITE  BP13L242-REC.                                                 
           WRITE  BP13L242-REC          FROM L242-PR-HEAD-1.                    
           WRITE  BP13L242-REC          FROM L242-PR-HEAD-2.                    
           MOVE   ALL '-'               TO   BP13L242-REC.                      
           WRITE  BP13L242-REC.                                                 
                                                                                
       850-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
            IF WS-CNT-F310      = ZEROES                                        
               PERFORM 850-PRINT-HEADING         THRU 850-EXIT                  
               WRITE BP13L242-REC FROM L242-NIL-RECORDS      AFTER 5            
            ELSE                                                                
               IF WS-CNT-ACCEPT = ZEROES                                        
                  PERFORM 850-PRINT-HEADING      THRU 850-EXIT                  
                  WRITE BP13L242-REC FROM L242-NIL-ACCEPT-CASES AFTER 5         
               END-IF                                                           
            END-IF.                                                             
                                                                                
            MOVE    WS-CNT-F310    TO    WS-EDIT-CNT-F310.                      
            MOVE    WS-CNT-K800    TO    WS-EDIT-CNT-K800.                      
            MOVE    WS-CNT-K110    TO    WS-EDIT-CNT-K110.                      
            MOVE    WS-CNT-PROC    TO    WS-EDIT-CNT-PROC.                      
            MOVE    WS-SEQ-NO      TO    WS-EDIT-CNT-LETTERS.                   
            MOVE    WS-CNT-SKIPPED TO    WS-EDIT-CNT-SKIPPED.                   
                                                                                
            DISPLAY ' '.                                                        
            DISPLAY ' ***  BP13C242 CONTROL TOTALS  ***'.                       
            DISPLAY ' '.                                                        
            DISPLAY ' NO READ FROM F310 : ' WS-EDIT-CNT-F310.                   
            DISPLAY ' NO READ FROM K800 : ' WS-EDIT-CNT-K800.                   
            DISPLAY ' NO READ FROM K110 : ' WS-EDIT-CNT-K110.                   
            DISPLAY ' NO PROCESSED      : ' WS-EDIT-CNT-PROC.                   
            DISPLAY ' NO PRINTED        : ' WS-EDIT-CNT-LETTERS                 
            DISPLAY ' NO SKIPPED        : ' WS-EDIT-CNT-SKIPPED                 
            CLOSE BP13F310                                                      
                  BP13K800                                                      
                  BM06K110                                                      
                  BP13L242.                                                     
                                                                                
            IF BP13K800-STATUS NOT = 00                                         
               DISPLAY 'CLOSING ERROR-BP13K800-STAT ' BP13K800-STATUS.          
            IF BM06K110-STATUS NOT = 00                                         
               DISPLAY 'CLOSING ERROR-BM06K110-STAT ' BM06K110-STATUS.          
                                                                                
            STOP RUN.                                                           
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
