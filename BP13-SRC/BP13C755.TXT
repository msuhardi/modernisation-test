       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C755.                                                 
       AUTHOR.        RAYATHATHA ASHOK.                                         
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    PURPOSE :                                           *                
      *    EXTRACT EFFECTIVE DATE OF SALE FROM SMS             *                
      *    (WHEN THE MOVE IN DATE FROM RDB = SPACES)           *                
      *                                                        *                
      *    EXTRACT DATE OF LETTING ORDER FROM CIS              *                
      *    (WHEN THE MOVE IN DATE FROM RDB = SPACES)           *                
      *                                                        *                
      * REQUEST OIC  DATE    DESCRIPTION                       *                
      * ======= ===  ====    ===========                       *                
      * C930317 LMS 930424 - TO CHANGE B01K003 TO AB03K030     *                
      *                      DUE TO SMS PHASE 3B.              *                
      * I920718 LMS 930424 - TO CONVERT TO COBOL II.           *                
      *BP130418 LSB 980625 - AC17 Y2000 CHANGES                *                
      *BP130630 NOR 990114 - CHANGE F715 TO F716 & EXPAND 350  *                
      *BP136450 SMR 160926 - INCLUDE FD STATEMENT IN AC17K010  *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716   ASSIGN TO BP13F716.                                
           SELECT BP13OUT    ASSIGN TO BP13OUT.                                 
                                                                                
           SELECT AB03K030   ASSIGN TO AB03K030                                 
                       ORGANIZATION IS INDEXED                                  
                       ACCESS  MODE IS DYNAMIC                                  
                       RECORD  KEY  IS K030-NUM-HDB-REF                         
                       FILE STATUS  IS AB03K030-STATUS.                         
                                                                                
           SELECT AC17K010   ASSIGN TO AC17K010                                 
                       ORGANIZATION IS INDEXED                                  
                       ACCESS  MODE IS DYNAMIC                                  
                       RECORD  KEY  IS K010-NUM-SCHM-ACCNT                      
                       FILE STATUS  IS AC17K010-STATUS.                         
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13OUT                                                              
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 350 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  BP13OUT-REC             PIC X(350).                                  
                                                                                
       FD  BP13F716                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 350 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F716.                                                           
                                                                                
       FD  AB03K030                                                             
           RECORD CONTAINS 800 CHARACTERS.                                      
       COPY AB03K030.                                                           
                                                                                
       FD  AC17K010                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY AC17K010.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-VARIABLES.                                                        
           05  AB03K030-STATUS         PIC 9(2)      VALUE 0.                   
           05  AC17K010-STATUS         PIC 9(2)      VALUE 0.                   
           05  WS-F716-EOF             PIC X         VALUE 'N'.                 
               88 F716-EOF                           VALUE 'Y'.                 
                                                                                
           05  WS-COUNTERS             COMP-3.                                  
               10  WS-F716-CNT-READ    PIC 9(9)      VALUE 0.                   
               10  WS-K010-CNT-READ    PIC 9(9)      VALUE 0.                   
               10  WS-K030-CNT-READ    PIC 9(9)      VALUE 0.                   
               10  WS-K010-CNT-UPD     PIC 9(9)      VALUE 0.                   
               10  WS-WRITE            PIC 9(9)      VALUE 0.                   
                                                                                
       01  WS-PREV-SCHACC.                                                      
           05  WS-PREV-SCH             PIC X(09).                               
           05  WS-PREV-LSE             PIC X(02).                               
                                                                                
       01  WS-DTE-MOVING-IN.                                                    
           05  WS-F716-CC              PIC X(2).                                
           05  WS-DTE-F716-MOVING-IN.                                           
               10  WS-F716-YY          PIC X(2).                                
               10  WS-F716-MM          PIC X(2).                                
               10  WS-F716-DD          PIC X(2).                                
                                                                                
       01  WS-DTE.                                                              
           05  WS-DD                   PIC X(2).                                
           05  WS-MM                   PIC X(2).                                
           05  WS-YY                   PIC X(4).                                
                                                                                
       01  WS-SALE-EFF-DATE            PIC X(8).                                
                                                                                
       01  WS-EDIT-DATE.                                                        
           05  WS-EDIT-YY              PIC X(4).                                
           05  WS-EDIT-MM              PIC X(2).                                
           05  WS-EDIT-DD              PIC X(2).                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-PROGRAM.                                                       
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-BP13F716 THRU 2000-EXIT.                           
           PERFORM 3000-MAIN-ROUTINE  THRU 3000-EXIT UNTIL F716-EOF.            
           PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT.                           
                                                                                
       1000-OPEN-FILES.                                                         
           OPEN INPUT  AB03K030                                                 
                       AC17K010                                                 
                       BP13F716                                                 
                OUTPUT BP13OUT.                                                 
                                                                                
           IF AB03K030-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING AB03K030, STATUS - '                       
                                     AB03K030-STATUS                            
              MOVE AB03K030-STATUS       TO RETURN-CODE                         
              PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT.                        
                                                                                
           IF AC17K010-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING AC17K010, STATUS - '                       
                                     AC17K010-STATUS                            
              MOVE AC17K010-STATUS       TO RETURN-CODE                         
              PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F716.                                                      
           READ BP13F716  AT END                                                
                MOVE 'Y' TO WS-F716-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
              ADD 1 TO WS-F716-CNT-READ.                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-MAIN-ROUTINE.                                                       
                                                                                
           IF F716-DTE-MOVING-IN = SPACES OR LOW-VALUE                          
              IF F716-NUM-SCH-ACCNT > '700000000' OR                            
                 F716-NUM-SCH-ACCNT = '700000000'                               
                 PERFORM 4000-READ-AB03K030 THRU 4000-EXIT                      
              ELSE                                                              
                 PERFORM 5000-START-AC17K010 THRU 5000-EXIT.                    
                                                                                
            WRITE BP13OUT-REC FROM BP13F716-REC.                                
            ADD 1 TO WS-WRITE.                                                  
                                                                                
           PERFORM 2000-READ-BP13F716 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-READ-AB03K030.                                                      
                                                                                
           MOVE SPACE TO WS-DTE-MOVING-IN.                                      
           MOVE SPACES             TO K030-NUM-HDB-REF.                         
           MOVE F716-NUM-SCH-ACCNT TO K030-NUM-SCH-ACCNT.                       
           START AB03K030 KEY > K030-NUM-HDB-REF.                               
           IF AB03K030-STATUS = 00                                              
              PERFORM 4200-READ-AB03K030 THRU 4200-EXIT                         
              PERFORM 4500-PROCESS-AB03K030 THRU 4500-EXIT                      
                UNTIL K030-NUM-SCH-ACCNT NOT = F716-NUM-SCH-ACCNT               
              MOVE WS-DTE-MOVING-IN      TO F716-DTE-MOVING-IN                  
           ELSE                                                                 
           IF AB03K030-STATUS = 23                                              
              DISPLAY 'SCH-ACC NOT MATCH IN SMS ' F716-NUM-SCH-ACCNT            
           ELSE                                                                 
              DISPLAY 'ERROR READING AB03K030, STATUS - '                       
                                     AB03K030-STATUS.                           
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4200-READ-AB03K030.                                                      
            READ AB03K030 NEXT AT END                                           
                 MOVE HIGH-VALUE TO K030-NUM-HDB-REF.                           
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4500-PROCESS-AB03K030.                                                   
            IF F716-NUM-SCH-ACCNT = K030-NUM-SCH-ACCNT                          
               IF F716-NUM-NRIC = K030-NUM-UIN1 OR                              
                  F716-NUM-NRIC = K030-NUM-UIN2 OR                              
                  F716-NUM-NRIC = K030-NUM-UIN3 OR                              
                  F716-NUM-NRIC = K030-NUM-UIN4                                 
                  MOVE K030-DTE-SALE-EFF TO WS-DTE-MOVING-IN                    
               ELSE                                                             
                  NEXT SENTENCE                                                 
            ELSE                                                                
               NEXT SENTENCE.                                                   
                                                                                
            PERFORM 4200-READ-AB03K030 THRU 4200-EXIT.                          
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-START-AC17K010.                                                     
                                                                                
           MOVE SPACES             TO K010-NUM-SCHM-ACCNT                       
                                      WS-PREV-SCHACC.                           
           MOVE F716-NUM-SCH-ACCNT TO K010-NUM-SCHM-ACCNT                       
                                      WS-PREV-SCHACC.                           
           START AC17K010 KEY NOT < K010-NUM-SCHM-ACCNT.                        
                                                                                
           IF AC17K010-STATUS = 0                                               
              PERFORM 5500-READ-AC17K010 THRU 5500-EXIT                         
                UNTIL WS-PREV-SCHACC NOT = K010-NUM-SCHM-ACCNT                  
           ELSE                                                                 
              IF AC17K010-STATUS = 23                                           
                 DISPLAY 'SCH-ACC NOT MATCH IN RM  '                            
                                      F716-NUM-SCH-ACCNT                        
              ELSE                                                              
                 DISPLAY 'ERROR STARTING AC17K010, STATUS - '                   
                                        AC17K010-STATUS                         
                 MOVE AC17K010-STATUS        TO RETURN-CODE.                    
                                                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5500-READ-AC17K010.                                                      
                                                                                
           READ AC17K010 NEXT.                                                  
                                                                                
           IF  F716-NUM-SCH-ACCNT = WS-PREV-SCH                                 
              ADD 1                  TO WS-K010-CNT-READ                        
              MOVE K010-NUM-SCHM-ACCNT TO WS-PREV-SCHACC                        
              PERFORM 6000-CHECK     THRU 6000-EXIT                             
           ELSE                                                                 
              NEXT SENTENCE.                                                    
                                                                                
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       6000-CHECK.                                                              
           IF F716-DTE-MOVING-IN = SPACES OR LOW-VALUE                          
              ADD 1                  TO WS-K010-CNT-UPD                         
              MOVE K010-DTE-TENC     TO WS-DTE                                  
              MOVE WS-DD             TO WS-EDIT-DD                              
              MOVE WS-MM             TO WS-EDIT-MM                              
              MOVE WS-YY             TO WS-EDIT-YY                              
              MOVE WS-EDIT-DATE      TO F716-DTE-MOVING-IN.                     
                                                                                
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9999-CLOSE-FILES.                                                        
           DISPLAY '******>>>>>>>>>>> BP13C755 <<<<<<<<<<<******'.              
           DISPLAY ' '.                                                         
           DISPLAY '****** MATCH SMS TO GET EFF-SALE DATE ******'.              
           DISPLAY '****** MATCH CIS TO GET TEN-COMM DATE ******'.              
           DISPLAY ' '.                                                         
           DISPLAY 'NO. OF RECORDS READ       = ' WS-F716-CNT-READ.             
           DISPLAY 'NO. OF RECORDS WRITE      = ' WS-WRITE.                     
           DISPLAY 'NO. OF READ FROM AC17K010 = '  WS-K010-CNT-READ.            
           DISPLAY 'MOVE-IN-DTE UPD FR RENTAL = '  WS-K010-CNT-UPD.             
           DISPLAY 'MOVE-IN-DTE UPD FR SMS    = '  WS-K030-CNT-READ.            
                                                                                
           CLOSE  AB03K030 AC17K010 BP13F716 BP13OUT.                           
                                                                                
           IF AB03K030-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING AB03K030, STATUS - '                       
                                     AB03K030-STATUS                            
              MOVE AB03K030-STATUS       TO RETURN-CODE.                        
                                                                                
           IF AC17K010-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING AC17K010, STATUS - '                       
                                     AC17K010-STATUS                            
              MOVE AC17K010-STATUS       TO RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
