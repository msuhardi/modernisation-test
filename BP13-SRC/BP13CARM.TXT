      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13CARM.                                                 
      *AUTHOR.        CS94317.                                                  
      *DATE-WRITTEN.  14/09/2023.                                               
                                                                                
      ***************************************************************           
      *            SYSTEM OF COMMITMENT (BP13)                      *           
      *=============================================================*           
      *  OBJECTIVE  : THE ROUTINE CATERS TO ADDITION/SUBTRACTION OF *           
      *               YEARS/MONTHS/DAYS TO A GIVEN DATE AND ARRIVE  *           
      *               AT A NEW DATE                                 *           
      *  INPUT      : DATE FORMAT IN THE FORMAT CCYYMMDD            *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       DATA DIVISION.                                                           
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-STORE1                    PIC 9(6).                            
           05 WS-STORE2                    PIC 9(6).                            
           05 WS-STORE3                    PIC 9(6).                            
           05 WS-YEAR-DAY                  PIC 9(4).                            
           05 WS-LEAP-YR-TYP               PIC X.                               
           05 WS-LEAP-CHK-DAYS             PIC 9(3).                            
           05 WS-TEMP-DATE.                                                     
              10 WS-TEMP-CCYY              PIC 9(4).                            
              10 WS-TEMP-MM                PIC 9(2).                            
              10 WS-TEMP-DD                PIC 9(2).                            
           05 WS-TEMP1-CCYY                PIC 9(4).                            
           05 WS-TEMP1-MM                  PIC 9(2).                            
           05 WS-DAY.                                                           
              10 WS-DAYS                   PIC 9(2) OCCURS 12.                  
                                                                                
      *-----------------------*                                                 
       LINKAGE SECTION.                                                         
      *-----------------------*                                                 
       COPY P13CARM.                                                            
                                                                                
       PROCEDURE DIVISION USING P13CARMWR.                                      
                                                                                
      *****************                                                         
       0000-MAIN-PROC.                                                          
      *****************                                                         
      *    MOVE P13CARM-INP              TO WS-P13CARM-INP                      
           PERFORM 1000-VALIDATE-INPUT   THRU 1000-EXIT                         
           GOBACK.                                                              
           .                                                                    
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       1000-VALIDATE-INPUT.                                                     
      **********************                                                    
                                                                                
           IF CARM-DATE-IN-DD = 0                                               
              MOVE 0                TO CARM-DATE-IN-DD                          
           END-IF                                                               
                                                                                
           IF CARM-DATE-IN-CCYY < 1800                                          
              MOVE '01'                 TO CARM-RETURN-CODE                     
              MOVE 'INVALID INPUT DATE' TO CARM-MESSAGE                         
              MOVE 0                    TO CARM-DATE-OUT                        
              GOBACK                                                            
           END-IF                                                               
                                                                                
           IF CARM-DATE-IN-MM < 1 OR > 12                                       
              MOVE '01'                 TO CARM-RETURN-CODE                     
              MOVE 'INVALID INPUT DATE' TO CARM-MESSAGE                         
              MOVE 0                    TO CARM-DATE-OUT                        
              GOBACK                                                            
           END-IF                                                               
                                                                                
           IF CARM-NO-OF-YEAR-N NOT NUMERIC                                     
              MOVE '02'                     TO CARM-RETURN-CODE                 
              MOVE 'YEAR/MM/DD NOT NUMERIC' TO CARM-MESSAGE                     
              MOVE 0                        TO CARM-DATE-OUT                    
              GOBACK                                                            
           END-IF                                                               
                                                                                
           IF CARM-NO-OF-MONTH-N NOT NUMERIC                                    
              MOVE '02'                     TO CARM-RETURN-CODE                 
              MOVE 'YEAR/MM/DD NOT NUMERIC' TO CARM-MESSAGE                     
              MOVE 0                        TO CARM-DATE-OUT                    
              GOBACK                                                            
           END-IF                                                               
                                                                                
           IF CARM-NO-OF-MONTH-N NOT = 0                                        
              IF CARM-DATE-IN-MM < 1 OR > 12                                    
                 MOVE '01'                  TO CARM-RETURN-CODE                 
                 MOVE 'INVALID INPUT DATE'  TO CARM-MESSAGE                     
                 MOVE 0                     TO CARM-DATE-OUT                    
                 GOBACK                                                         
              END-IF                                                            
           END-IF                                                               
                                                                                
           IF CARM-NO-OF-DAY-N NOT NUMERIC                                      
              MOVE '02'                     TO CARM-RETURN-CODE                 
              MOVE 'YEAR/MM/DD NOT NUMERIC' TO CARM-MESSAGE                     
              MOVE 0                        TO CARM-DATE-OUT                    
              GOBACK                                                            
           END-IF                                                               
                                                                                
      *** VALIDATION OF OPERATION CODE                                          
                                                                                
           IF CARM-OPR-CODE NOT = 'A' AND                                       
              CARM-OPR-CODE NOT = 'S'                                           
              MOVE '03'                     TO CARM-RETURN-CODE                 
              MOVE 'INVALID OPERATION CODE' TO CARM-MESSAGE                     
              MOVE 0                        TO CARM-DATE-OUT                    
              GOBACK                                                            
           END-IF                                                               
                                                                                
           IF CARM-NO-OF-YEAR = '00' AND                                        
              CARM-NO-OF-MONTH = '00' AND                                       
              CARM-NO-OF-DAY   = '000'                                          
              MOVE '00'                     TO CARM-RETURN-CODE                 
              MOVE ' '                      TO CARM-MESSAGE                     
              MOVE CARM-DATE-IN             TO CARM-DATE-OUT                    
              GOBACK                                                            
           END-IF                                                               
                                                                                
           PERFORM 2000-COMPUTE-DATE        THRU 2000-EXIT                      
           .                                                                    
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       2000-COMPUTE-DATE.                                                       
      ********************                                                      
           COMPUTE WS-STORE1 = CARM-DATE-IN-CCYY * 12                           
           COMPUTE WS-STORE1 = WS-STORE1 + CARM-DATE-IN-MM                      
           COMPUTE WS-STORE2 = CARM-NO-OF-YEAR-N * 12                           
           COMPUTE WS-STORE2 = WS-STORE2 + CARM-NO-OF-MONTH-N                   
                                                                                
           IF CARM-OPR-CODE = 'A'                                               
              COMPUTE WS-STORE3 = WS-STORE1 + WS-STORE2                         
           ELSE                                                                 
              COMPUTE WS-STORE3 = WS-STORE1 - WS-STORE2                         
           END-IF                                                               
                                                                                
           IF WS-STORE3 > 120000                                                
              MOVE '05'                     TO CARM-RETURN-CODE                 
              MOVE 'OUTPUT DATE IS BEYOND YEAR 9999'                            
                                            TO CARM-MESSAGE                     
              MOVE 0                        TO CARM-DATE-OUT                    
              GOBACK                                                            
           END-IF                                                               
                                                                                
           DIVIDE WS-STORE3 BY 12 GIVING WS-TEMP-CCYY                           
                                  REMAINDER WS-TEMP-MM                          
           IF WS-TEMP-MM = 0                                                    
              MOVE 12                       TO WS-TEMP-MM                       
              COMPUTE WS-TEMP-CCYY = WS-TEMP-CCYY - 1                           
           END-IF                                                               
                                                                                
           MOVE 31                  TO WS-DAYS(01) WS-DAYS(03)                  
                                       WS-DAYS(05) WS-DAYS(07)                  
                                       WS-DAYS(08) WS-DAYS(10)                  
                                       WS-DAYS(12)                              
           MOVE 30                  TO WS-DAYS(04) WS-DAYS(06)                  
                                       WS-DAYS(09) WS-DAYS(11)                  
                                                                                
           PERFORM 3000-LEAP-YEAR-CHECK     THRU 3000-EXIT                      
                                                                                
           IF CARM-DATE-IN-DD > WS-DAYS(WS-TEMP-MM)                             
              MOVE WS-DAYS(WS-TEMP-MM)      TO WS-TEMP-DD                       
           ELSE                                                                 
              MOVE CARM-DATE-IN-DD         TO WS-TEMP-DD                        
           END-IF                                                               
                                                                                
           MOVE 0                           TO WS-YEAR-DAY                      
           COMPUTE WS-YEAR-DAY = WS-YEAR-DAY + WS-TEMP-DD                       
           COMPUTE WS-TEMP-MM  = WS-TEMP-MM - 1                                 
                                                                                
           PERFORM UNTIL WS-TEMP-MM = 0                                         
              COMPUTE WS-YEAR-DAY = WS-YEAR-DAY + WS-DAYS(WS-TEMP-MM)           
              COMPUTE WS-TEMP-MM  = WS-TEMP-MM - 1                              
           END-PERFORM                                                          
                                                                                
           IF CARM-OPR-CODE = 'A'                                               
              COMPUTE WS-YEAR-DAY = WS-YEAR-DAY + CARM-NO-OF-DAY-N              
           END-IF                                                               
                                                                                
           IF CARM-OPR-CODE = 'S'                                               
              IF WS-YEAR-DAY < CARM-NO-OF-DAY-N                                 
                 PERFORM UNTIL WS-YEAR-DAY >= CARM-NO-OF-DAY-N                  
                    COMPUTE WS-TEMP-CCYY = WS-TEMP-CCYY - 1                     
                    PERFORM 3000-LEAP-YEAR-CHECK     THRU 3000-EXIT             
                    IF WS-LEAP-YR-TYP = 'Y'                                     
                       COMPUTE WS-YEAR-DAY = WS-YEAR-DAY + 366                  
                    ELSE                                                        
                       COMPUTE WS-YEAR-DAY = WS-YEAR-DAY + 365                  
                    END-IF                                                      
                 END-PERFORM                                                    
              END-IF                                                            
              COMPUTE WS-YEAR-DAY = WS-YEAR-DAY - CARM-NO-OF-DAY-N              
           END-IF                                                               
                                                                                
           IF WS-YEAR-DAY = 0                                                   
              COMPUTE CARM-DATE-OUT-CCYY = WS-TEMP-CCYY - 1                     
              MOVE 12                    TO CARM-DATE-OUT-MM                    
              MOVE 31                    TO CARM-DATE-OUT-DD                    
              MOVE '00'                  TO CARM-RETURN-CODE                    
              MOVE '  '                  TO CARM-MESSAGE                        
              GOBACK                                                            
           END-IF                                                               
                                                                                
           PERFORM 3000-LEAP-YEAR-CHECK  THRU 3000-EXIT                         
                                                                                
           PERFORM UNTIL WS-YEAR-DAY NOT > WS-LEAP-CHK-DAYS                     
              COMPUTE WS-YEAR-DAY  = WS-YEAR-DAY - WS-LEAP-CHK-DAYS             
              COMPUTE WS-TEMP-CCYY = WS-TEMP-CCYY + 1                           
              PERFORM 3000-LEAP-YEAR-CHECK  THRU 3000-EXIT                      
           END-PERFORM                                                          
                                                                                
           IF WS-TEMP-CCYY > 9999                                               
              MOVE '05'                     TO CARM-RETURN-CODE                 
              MOVE 'OUTPUT DATE IS BEYOND YEAR 9999'                            
                                            TO CARM-MESSAGE                     
              MOVE 0                        TO CARM-DATE-OUT                    
              GOBACK                                                            
           END-IF                                                               
                                                                                
           IF WS-YEAR-DAY = WS-LEAP-CHK-DAYS                                    
              MOVE WS-TEMP-CCYY             TO CARM-DATE-OUT-CCYY               
              MOVE 12                       TO CARM-DATE-OUT-MM                 
              MOVE 31                       TO CARM-DATE-OUT-DD                 
              MOVE '00'                     TO CARM-RETURN-CODE                 
              MOVE '  '                     TO CARM-MESSAGE                     
              GOBACK                                                            
           END-IF                                                               
                                                                                
           MOVE 01                          TO WS-TEMP-MM                       
           PERFORM UNTIL WS-YEAR-DAY NOT > WS-DAYS(WS-TEMP-MM)                  
              COMPUTE WS-YEAR-DAY = WS-YEAR-DAY - WS-DAYS(WS-TEMP-MM)           
              COMPUTE WS-TEMP-MM  = WS-TEMP-MM + 1                              
           END-PERFORM                                                          
                                                                                
           MOVE WS-TEMP-CCYY                TO CARM-DATE-OUT-CCYY               
           MOVE WS-TEMP-MM                  TO CARM-DATE-OUT-MM                 
           MOVE WS-YEAR-DAY                 TO CARM-DATE-OUT-DD                 
           MOVE '00'                        TO CARM-RETURN-CODE                 
           MOVE '  '                        TO CARM-MESSAGE                     
           GOBACK                                                               
           .                                                                    
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       3000-LEAP-YEAR-CHECK.                                                    
      ***********************                                                   
           DIVIDE WS-TEMP-CCYY BY 400 GIVING WS-TEMP1-CCYY                      
                                      REMAINDER WS-TEMP1-MM                     
           IF WS-TEMP1-MM = 0                                                   
              MOVE 'Y'                   TO WS-LEAP-YR-TYP                      
           ELSE                                                                 
              DIVIDE WS-TEMP-CCYY BY 100 GIVING WS-TEMP1-CCYY                   
                                         REMAINDER WS-TEMP1-MM                  
              IF WS-TEMP1-MM = 0                                                
                 MOVE 'N'                TO WS-LEAP-YR-TYP                      
              ELSE                                                              
                 DIVIDE WS-TEMP-CCYY BY 4 GIVING WS-TEMP1-CCYY                  
                                         REMAINDER WS-TEMP1-MM                  
                 IF WS-TEMP1-MM = 0                                             
                    MOVE 'Y'             TO WS-LEAP-YR-TYP                      
                 ELSE                                                           
                    MOVE 'N'             TO WS-LEAP-YR-TYP                      
                 END-IF                                                         
              END-IF                                                            
           END-IF                                                               
                                                                                
           IF WS-LEAP-YR-TYP = 'Y'                                              
              MOVE 29                    TO WS-DAYS(02)                         
              MOVE 366                   TO WS-LEAP-CHK-DAYS                    
           ELSE                                                                 
              MOVE 28                    TO WS-DAYS(02)                         
              MOVE 365                   TO WS-LEAP-CHK-DAYS                    
           END-IF                                                               
           .                                                                    
       3000-EXIT.                                                               
           EXIT.                                                                
