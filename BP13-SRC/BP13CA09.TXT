      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13CA09.                                                 
      *AUTHOR.        NINO ANGELO GARILLO.                                      
      *DATE-WRITTEN.  08/09/2006.                                               
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      * OBJECTIVE :                                                   *         
      *  1. PRINT REPORT ON FLATLIST FOR GRO BY PRJCT/BLOCK/RANDOM.   *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * CHG REF  BY    DATE    DESCRIPTION                            *         
      * -------- ---  -------- -----------                            *         
      * BP132789 GAN1 08/09/06 NEW PROGRAM.                           *         
      * BP133932 PCL3 07/09/10 TO CATER FOR HANDLING FLAT TYPE OF EC  *         
      *                        AND DBSS CASES.                        *         
      *===============================================================*         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F100 ASSIGN TO BP13F100.                                  
                                                                                
           SELECT BP13LA09 ASSIGN TO BP13LA09.                                  
                                                                                
      *---------------*                                                         
       DATA DIVISION.                                                           
      *---------------*                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F100                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 800 CHARACTERS.                                      
       01  BP13F100-REC.                                                        
           05  F100-NUM-PRJCT          PIC X(03).                               
           05  F100-NUM-BLOCK          PIC X(05).                               
           05  F100-NUM-LEVEL          PIC X(02).                               
           05  F100-NUM-UNIT           PIC X(07).                               
           05  FILLER                  PIC X(01).                               
           05  F100-NUM-RANDOM         PIC X(28).                               
           05  F100-NUM-PRIO           PIC X(01).                               
           05  F100-NUM-FT-BALLOT      PIC X(02).                               
           05  FILLER                  PIC X(22).                               
           05  F100-NUM-TYP-ACTUSE     PIC X(02).                               
           05  FILLER                  PIC X(727).                              
                                                                                
       FD  BP13LA09                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01 BP13LA09-REC            PIC X(132).                                   
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
      *------------------------------*                                          
      *   WORKING STORAGE VARIABLES  *                                          
      *------------------------------*                                          
       01 WS-F100-EOF             PIC X(01) VALUE 'N'.                          
          88 F100-END-OF-FILE               VALUE 'Y'.                          
       01 WS-FIRST-SW             PIC X(01) VALUE 'N'.                          
          88 WS-FIRST                       VALUE 'Y'.                          
       01 WS-F100-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-LA09-WRITE-CNT       PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-EDT-DATE             PIC X(10) VALUE SPACES.                       
       01 WS-CUR-DATE             PIC X(08) VALUE SPACES.                       
                                                                                
       01 WS-EDIT1                PIC Z(07)9.                                   
                                                                                
       01 WS-LINE-CNT             PIC 9(02) VALUE ZEROES.                       
       01 WS-PAGE-CNT             PIC 9(06) VALUE ZEROES.                       
       01 WS-MAX-LINES            PIC 9(02) VALUE ZEROES.                       
       01 WS-SLNO                 PIC 9(05) VALUE ZEROES.                       
       01 WS-PREV-F100-KEY-FLD    PIC X(10) VALUE SPACES.                       
                                                                                
       01 WS-F100-KEY-FLD.                                                      
          05 WS-F100-PRJCT        PIC X(03).                                    
          05 WS-F100-BLOCK        PIC X(05).                                    
          05 WS-F100-FT           PIC X(02).                                    
                                                                                
       01 WS-REPORT-VARS.                                                       
          05 LA09-HEADER1.                                                      
             10 FILLER            PIC X(08) VALUE 'BP13LA09'.                   
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(08) VALUE 'HDBCAT 3'.                   
             10 FILLER            PIC X(29) VALUE SPACES.                       
             10 FILLER            PIC X(20) VALUE                               
                'SYSTEM OF COMMITMENT'.                                         
             10 FILLER            PIC X(23) VALUE SPACES.                       
             10 FILLER            PIC X(07) VALUE 'DATE : '.                    
             10 LA09-DATE         PIC X(12) VALUE SPACES.                       
             10 FILLER            PIC X(04) VALUE SPACES.                       
             10 FILLER            PIC X(07) VALUE 'PAGE : '.                    
             10 LA09-PAGE         PIC Z(05)9.                                   
          05 LA09-HEADER2.                                                      
             10 FILLER            PIC X(39) VALUE SPACES.                       
             10 FILLER            PIC X(43) VALUE                               
                'UNIT SET ASIDE FOR PRIORITY (FINAL)'.                          
             10 FILLER            PIC X(41) VALUE SPACES.                       
          05 LA09-HEADER3.                                                      
             10 FILLER            PIC X(12) VALUE 'PRJCT     : '.               
             10 LA09-PROJECT      PIC X(03) VALUE SPACES.                       
          05 LA09-HEADER4.                                                      
             10 FILLER            PIC X(12) VALUE 'BLOCK     : '.               
             10 LA09-BLOCK        PIC X(05) VALUE SPACES.                       
          05 LA09-HEADER5.                                                      
             10 LA09-FT-HDR5      PIC X(12) VALUE 'FLAT TYPE : '.               
             10 LA09-FT           PIC X(05) VALUE SPACES.                       
          05 LA09-HEADER6.                                                      
             10 LA09-FT-HDR6      PIC X(41) VALUE                               
                ' S/NO    PRJCT  BLOCK   UNIT       FT'.                        
             10 FILLER            PIC X(07) VALUE                               
                ' RANDOM'.                                                      
             10 FILLER            PIC X(25) VALUE SPACES.                       
          05 LA09-HEADER7.                                                      
             10 FILLER            PIC X(132) VALUE ALL '-'.                     
          05 LA09-DETAILS.                                                      
             10 LA09-SLNO         PIC Z(4)9.                                    
             10 FILLER            PIC X(04) VALUE SPACES.                       
             10 LA09-PRJCT        PIC X(03) VALUE SPACES.                       
             10 FILLER            PIC X(04) VALUE SPACES.                       
             10 LA09-BLK          PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LA09-UNIT         PIC X(10) VALUE SPACES.                       
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LA09-FLAT-TYPE    PIC X(06) VALUE SPACES.                       
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LA09-RANDOM       PIC X(28) VALUE SPACES.                       
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 3000-READ-BP13F100      THRU 3000-EXIT.                      
           PERFORM 4000-PROCESS-DATA       THRU 4000-EXIT                       
             UNTIL F100-END-OF-FILE.                                            
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN  INPUT  BP13F100                                                
                 OUTPUT BP13LA09.                                               
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4)  DELIMITED BY SIZE                           
                  INTO WS-EDT-DATE.                                             
                                                                                
           MOVE  'Y'          TO WS-FIRST-SW.                                   
           MOVE  '55'         TO WS-MAX-LINES.                                  
           MOVE  '60'         TO WS-LINE-CNT.                                   
           MOVE WS-EDT-DATE   TO LA09-DATE.                                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-READ-BP13F100.                                                      
      *---------------------------------------------------------------*         
           READ BP13F100                                                        
                AT END MOVE 'Y' TO WS-F100-EOF                                  
                       GO TO 3000-EXIT.                                         
                                                                                
           MOVE F100-NUM-PRJCT          TO WS-F100-PRJCT.                       
           MOVE F100-NUM-BLOCK          TO WS-F100-BLOCK.                       
           MOVE F100-NUM-TYP-ACTUSE     TO WS-F100-FT.                          
                                                                                
           IF WS-FIRST                                                          
              MOVE 'N'                  TO WS-FIRST-SW                          
              MOVE WS-F100-KEY-FLD      TO WS-PREV-F100-KEY-FLD                 
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-F100-READ-CNT.                                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-PROCESS-DATA.                                                       
      *---------------------------------------------------------------*         
           DISPLAY 'F100 : ' WS-F100-KEY-FLD                                    
                                                                                
           PERFORM 5000-PRINT-REPORT   THRU 5000-EXIT.                          
                                                                                
           PERFORM 3000-READ-BP13F100  THRU 3000-EXIT.                          
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5000-PRINT-REPORT.                                                       
      *---------------------------------------------------------------*         
           IF (WS-LINE-CNT > WS-MAX-LINES)                                      
           OR (WS-F100-KEY-FLD NOT = WS-PREV-F100-KEY-FLD)                      
              MOVE  SPACES              TO BP13LA09-REC                         
              IF (WS-F100-KEY-FLD NOT = WS-PREV-F100-KEY-FLD)                   
                 MOVE WS-F100-KEY-FLD   TO WS-PREV-F100-KEY-FLD                 
                 MOVE  ZEROES           TO WS-SLNO WS-PAGE-CNT                  
              END-IF                                                            
              ADD      1                TO WS-PAGE-CNT                          
              MOVE     0                TO WS-LINE-CNT                          
              MOVE WS-PAGE-CNT          TO LA09-PAGE                            
              MOVE F100-NUM-PRJCT       TO LA09-PROJECT                         
              MOVE F100-NUM-BLOCK       TO LA09-BLOCK                           
              MOVE F100-NUM-TYP-ACTUSE  TO LA09-FT                              
                                                                                
              IF F100-NUM-PRJCT(1:1) = 'E'                                      
                 MOVE SPACES                           TO LA09-FT-HDR5          
                                                          LA09-FT               
                 MOVE ' S/NO    PRJCT  BLOCK   UNIT'   TO LA09-FT-HDR6          
              ELSE                                                              
                 MOVE 'FLAT TYPE : '                   TO LA09-FT-HDR5          
                 MOVE ' S/NO    PRJCT  BLOCK   UNIT       FT'                   
                                                       TO LA09-FT-HDR6          
              END-IF                                                            
                                                                                
              WRITE BP13LA09-REC      FROM LA09-HEADER1 AFTER PAGE              
              WRITE BP13LA09-REC      FROM LA09-HEADER2                         
              WRITE BP13LA09-REC      FROM LA09-HEADER3                         
              WRITE BP13LA09-REC      FROM LA09-HEADER4                         
              WRITE BP13LA09-REC      FROM LA09-HEADER5                         
              WRITE BP13LA09-REC      FROM LA09-HEADER6 AFTER 2                 
              WRITE BP13LA09-REC      FROM LA09-HEADER7                         
              ADD     11                TO WS-LINE-CNT                          
           END-IF.                                                              
                                                                                
           MOVE   SPACES                TO LA09-DETAILS.                        
           INITIALIZE                      LA09-DETAILS.                        
           ADD    1                     TO WS-SLNO.                             
           MOVE  WS-SLNO                TO LA09-SLNO.                           
           MOVE F100-NUM-PRJCT          TO LA09-PRJCT.                          
           MOVE F100-NUM-BLOCK          TO LA09-BLK.                            
           MOVE F100-NUM-TYP-ACTUSE     TO LA09-FLAT-TYPE.                      
           MOVE F100-NUM-RANDOM         TO LA09-RANDOM.                         
           STRING F100-NUM-LEVEL '-' F100-NUM-UNIT                              
           DELIMITED BY SIZE          INTO LA09-UNIT.                           
                                                                                
           MOVE     SPACES              TO BP13LA09-REC.                        
           WRITE BP13LA09-REC         FROM LA09-DETAILS.                        
           ADD    1                     TO WS-LINE-CNT.                         
           ADD    1                     TO WS-LA09-WRITE-CNT.                   
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13CA09       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F100-READ-CNT    TO WS-EDIT1.                                
           DISPLAY 'NO OF RECS READ FROM BP13F100  : ' WS-EDIT1.                
           MOVE WS-LA09-WRITE-CNT  TO WS-EDIT1.                                 
           DISPLAY 'NO OF RECS WRITTEN TO BP13LA09 : ' WS-EDIT1.                
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           CLOSE   BP13F100                                                     
                   BP13LA09.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
