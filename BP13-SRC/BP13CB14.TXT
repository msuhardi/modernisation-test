       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB14.                                                 
       AUTHOR.        EDEN ROSE DINGAL.                                         
       DATE-WRITTEN.  02/07/2007.                                               
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : THIS PRGM CREATES A BP13F740 FILE     *                
      *                  USING THE SMALLEST RANDOM KEY PER REGN*                
      *                                                        *                
      *    INPUT  FILE  : BP13F740  - ALL CASES                *                
      *    OUTPUT FILE  : P13F740B  - ALL CASES WITH COUNT     *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                         *                
      * BP133142 ED1 020707    NEW PROGRAM                     *                
      * BP133316 ED1 100408    REMOVE DUP WS-F740-READ COUNT   *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
           SELECT P13F740B ASSIGN TO P13F740B.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F740.                                                           
                                                                                
       FD   P13F740B                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       01   P13F740B-REC PIC X(500).                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F740-READ                PIC 9(7)    VALUE ZEROS.              
           05 WS-F740B-WRITE              PIC 9(7)    VALUE ZEROS.              
           05 WS-OTHERS                   PIC 9(7)    VALUE ZEROS.              
           05 WS-NMR-F740                 PIC 9(7)    VALUE ZEROS.              
                                                                                
       01  WS-SUCC-FD                     PIC X       VALUE 'N'.                
       01  WS-EOF-F740                    PIC X       VALUE 'N'.                
       01  WS-PREV-REGN                   PIC X(8)    VALUE SPACES.             
       01  WS-PREV-F740-REC               PIC X(500)  VALUE SPACES.             
       01  WS-CNT                         PIC 9(2)    VALUE ZEROS.              
       01  WS-SUCC-CNT                    PIC 9(2)    VALUE ZEROS.              
       01  WS-UNSUCC-CNT                  PIC 9(2)    VALUE ZEROS.              
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************                                                        
       000-MAIN-ROUTINE.                                                        
      ******************                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F740      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL WS-EOF-F740 = 'Y'.                                           
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ********************                                                      
       100-INITIALIZATION.                                                      
      ********************                                                      
           OPEN INPUT  BP13F740                                                 
                OUTPUT P13F740B.                                                
                                                                                
           MOVE ZEROS                   TO WS-COUNTERS.                         
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************                                                         
       200-READ-F740.                                                           
      *****************                                                         
           READ BP13F740 AT END                                                 
                MOVE 'Y'             TO WS-EOF-F740                             
                MOVE HIGH-VALUES     TO F740-NUM-REGN                           
                GO TO 200-EXIT.                                                 
                                                                                
           ADD  1                    TO WS-F740-READ.                           
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************                                                         
       300-PROCESS-RTN.                                                         
      *****************                                                         
           MOVE F740-NUM-REGN      TO WS-PREV-REGN.                             
                                                                                
           PERFORM 400-MATCH-REC  THRU 400-EXIT UNTIL                           
              F740-NUM-REGN NOT = WS-PREV-REGN                                  
                 OR WS-EOF-F740 = 'Y'.                                          
                                                                                
           PERFORM 500-WRITE-RECORD    THRU 500-EXIT.                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***************                                                           
       400-MATCH-REC.                                                           
      ***************                                                           
            IF F740-NUM-REGN = WS-PREV-REGN                                     
               MOVE BP13F740-REC TO WS-PREV-F740-REC                            
            END-IF.                                                             
                                                                                
            PERFORM 200-READ-F740  THRU 200-EXIT.                               
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************                                                    
       500-WRITE-RECORD.                                                        
      **********************                                                    
            MOVE WS-PREV-F740-REC    TO P13F740B-REC.                           
            WRITE                       P13F740B-REC.                           
            ADD  1                   TO WS-F740B-WRITE.                         
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       999-CLOSE-FILES.                                                         
           CLOSE BP13F740                                                       
                 P13F740B.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CB14 ================*'.              
           DISPLAY '  NO. OF F740 RECORDS READ     = ' WS-F740-READ.            
           DISPLAY '  NO. OF F740B RECORDS WRITTEN = ' WS-F740B-WRITE.          
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
                                                                                
