      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CD52.                                                 
       AUTHOR.        JIANG BO.                                                 
       DATE-WRITTEN.  05/09/08.                                                 
      *****************************************************************         
      * OBJECTIVE - UPON DBSS FLAT HANDOVER TO HDB, SOU WILL UPDATE             
      *             HANDOVER DATE VIA ONLINE PANEL TO BP13KD15.                 
      *             BATCH JOB WILL RUN TO MOD FILE TO AB02F120, SMS             
      *             WILL PASS FILE TO TC SALES SYSTEM.                          
      *                                                                         
      *     INPUT FILES :                                                       
      *                  1. BP13FA20                                            
      *                  2. BP13KA10                                            
      *                  3. BM06K100                                            
      *     OUTPUT FILES :                                                      
      *                  1. SY21F120                                            
      *----------------------------------------------------------------         
      * REF NO    DD/MM/YY  BY   AMENDMENTS/ENHANCEMENTS                        
      * --------  --------  ---  ------------------------                       
      * BP133472  15/09/08  JB8  NEW PROGRAM.                                   
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FA20     ASSIGN        TO BP13FA20.                       
           SELECT BM06F120     ASSIGN        TO BM06F120.                       
           SELECT SY21F120     ASSIGN        TO SY21F120.                       
                                                                                
           SELECT BP13KA10  ASSIGN        TO BP13KA10                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KA10-KEY-FLD                       
                            FILE STATUS   IS WS-KA10-STATUS.                    
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13FA20                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FA20.                                                           
                                                                                
       FD   BM06F120                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 700 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BM06F120.                                                           
                                                                                
       FD   BP13KA10                                                            
            RECORD CONTAINS 800 CHARACTERS.                                     
       COPY BP13KA10.                                                           
                                                                                
       FD   SY21F120                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY SY21F120.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  WORK-FILE-VARIABLE.                                                  
           05  WS-KA10-STATUS          PIC 9(2)   VALUE ZEROES.                 
           05  WS-EOF-FA20             PIC X      VALUE 'N'.                    
           05  WS-EOF-F120             PIC X      VALUE 'N'.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FA20-READ            PIC 9(7)   VALUE ZEROES.                 
           05  WS-F120-READ            PIC 9(7)   VALUE ZEROES.                 
           05  WS-MATCH                PIC 9(7)   VALUE ZEROES.                 
           05  WS-F120-WRITE           PIC 9(7)   VALUE ZEROES.                 
                                                                                
       01  WS-CURRENT-DATE             PIC 9(08)  VALUE ZEROES.                 
       01  WS-CURRENT-TIME             PIC 9(08)  VALUE ZEROES.                 
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13FA20      THRU 2000-EXIT.                      
           PERFORM 2100-READ-BM06F120      THRU 2100-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-FA20 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BM06F120                                                 
                       BP13FA20                                                 
                       BP13KA10                                                 
                OUTPUT SY21F120.                                                
                                                                                
           IF WS-KA10-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13KA10 OPEN ERROR ' WS-KA10-STATUS                     
              MOVE WS-KA10-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13FA20.                                                      
      ******************************************************************        
                                                                                
           READ BP13FA20           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-FA20                             
                GO                 TO   2000-EXIT.                              
                                                                                
           DISPLAY '-FA20: ' FA20-NUM-POSTAL                                    
                                                                                
           ADD  1                  TO   WS-FA20-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2100-READ-BM06F120.                                                      
      ******************************************************************        
                                                                                
           READ BM06F120           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F120                             
                MOVE HIGH-VALUES   TO   F120-POSTAL-CODE                        
                GO                 TO   2100-EXIT.                              
                                                                                
           DISPLAY ' F120: ' F120-POSTAL-CODE                                   
                                                                                
           ADD  1                  TO   WS-F120-READ.                           
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF F120-POSTAL-CODE = FA20-NUM-POSTAL                                
              ADD 1                            TO WS-MATCH                      
              PERFORM 4100-GET-NME-LESSEE    THRU 4100-EXIT                     
              PERFORM 3500-OUTPUT-RECORDS    THRU 3500-EXIT                     
                UNTIL F120-POSTAL-CODE NOT = FA20-NUM-POSTAL                    
                   OR WS-EOF-F120 = 'Y'                                         
           ELSE                                                                 
              IF F120-POSTAL-CODE > FA20-NUM-POSTAL                             
                 PERFORM 2000-READ-BP13FA20  THRU 2000-EXIT                     
              ELSE                                                              
                 PERFORM 2100-READ-BM06F120  THRU 2100-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3500-OUTPUT-RECORDS.                                                     
      ******************************************************************        
                                                                                
           PERFORM 4000-MOVE-VALUES          THRU 4000-EXIT.                    
           PERFORM 2100-READ-BM06F120        THRU 2100-EXIT.                    
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-MOVE-VALUES.                                                        
      ******************************************************************        
                                                                                
           ACCEPT WS-CURRENT-TIME      FROM TIME.                               
           ACCEPT WS-CURRENT-DATE      FROM DATE YYYYMMDD.                      
           MOVE SPACES                   TO F120-RECORD.                        
           INITIALIZE                       F120-RECORD.                        
           MOVE 'BP13'                   TO F120-NUM-SYS.                       
           MOVE 'DB'                     TO F120-NUM-TX.                        
           MOVE SPACES                   TO F120-NUM-OFFICE.                    
           MOVE SPACES                   TO F120-NUM-TNCY-EX                    
           MOVE 'N'                      TO F120-NUM-SC-ELIG                    
           STRING F120-SCHEME F120-ACC F120-CHK '00'                            
              DELIMITED BY SIZE INTO F120-HDB-REF.                              
           MOVE FA20-DTE-HANDOVER        TO F120-DTE-SALE-EFF                   
                                            F120-DTE-SC-ELIG.                   
                                                                                
           MOVE KA10-NME-DVLPR-PARENT    TO F120-NME-LESSEE-1ST.                
           MOVE 'X77777777'              TO F120-NUM-UIN-FIN-LESSEE-1ST.        
           MOVE WS-CURRENT-DATE          TO F120-DTE-TX                         
           STRING WS-CURRENT-TIME(1:2) ':'                                      
                  WS-CURRENT-TIME(3:2) ':'                                      
                  WS-CURRENT-TIME(5:2)                                          
              DELIMITED BY SIZE INTO F120-TME-TX                                
                                                                                
           WRITE F120-RECORD.                                                   
           ADD 1                         TO WS-F120-WRITE.                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4100-GET-NME-LESSEE.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                      TO BP13KA10-REC.                    
           INITIALIZE                          BP13KA10-REC.                    
           MOVE FA20-NUM-PRJCT              TO KA10-NUM-PRJCT.                  
                                                                                
           READ BP13KA10.                                                       
                                                                                
           IF WS-KA10-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-KA10-STATUS = 23 OR 10                                      
                 MOVE SPACES                TO KA10-NME-PRJCT                   
              ELSE                                                              
                 DISPLAY 'BP13KA10 - ERROR READING : ' WS-KA10-STATUS           
                 MOVE WS-KA10-STATUS        TO RETURN-CODE                      
                 GO TO 9999-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CD52'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13FA20 RECORDS READ............. : '                
                    WS-FA20-READ.                                               
           DISPLAY 'NO OF BM06F120 RECORDS READ............. : '                
                    WS-F120-READ.                                               
           DISPLAY 'NO OF MATCH RECORDS .................... : '                
                    WS-MATCH.                                                   
           DISPLAY 'NO OF SY21F120 RECORDS WRITTEN.......... : '                
                    WS-F120-WRITE.                                              
                                                                                
           CLOSE BM06F120                                                       
                 BP13FA20                                                       
                 BP13KA10                                                       
                 SY21F120.                                                      
                                                                                
           IF WS-KA10-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13KA10 CLOSE ERROR ' WS-KA10-STATUS                    
              MOVE WS-KA10-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
