       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CFA3.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  21/06/17.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  TO CHECK FOR CASES HAS HAND PHONE NUMBER    *            
      *                                                                         
      *  INPUT FILE :  1. BP13F740                                 *            
      *                2. BP13K820                                 *            
      *                3. BP13K830                                 *            
      *  OUTPUT FILE:  1. P13F740A                                 *            
      *                2. P13F740B                                 *            
      *                3. P13LCFA3                                 *            
      * ---------------------------------------------------------- *            
      * BP136992 ZDD 20170814   NEW PROGRAM                        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS WS-K820-STATUS.                            
                                                                                
           SELECT BP13K830 ASSIGN TO BP13K830                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K830-KEY-FLD                               
                  ALTERNATE KEY   IS K830-NUM-REGN                              
                  FILE STATUS     IS WS-K830-STATUS.                            
                                                                                
           SELECT P13F740A ASSIGN TO P13F740A.                                  
           SELECT P13F740B ASSIGN TO P13F740B.                                  
           SELECT P13LCFA3 ASSIGN TO P13LCFA3.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F740                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F740.                                                           
                                                                                
       FD  P13F740A                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  P13F740A-REC  PIC X(500).                                            
                                                                                
       FD  P13F740B                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  P13F740B-REC  PIC X(500).                                            
                                                                                
       FD  P13LCFA3                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  LCFA3-PRTREC                     PIC X(132).                         
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  NO-F740-READ        PIC 9(5)    VALUE ZEROS.                     
           05  NO-F740-W-HP        PIC 9(5)    VALUE ZEROS.                     
           05  NO-F740-WO-HP       PIC 9(5)    VALUE ZEROS.                     
           05  WS-EOF              PIC X(1)    VALUE 'N'.                       
           05  WS-K857-STATUS      PIC 9(2)    VALUE ZEROS.                     
           05  WS-K830-STATUS      PIC 9(2)    VALUE ZEROS.                     
           05  WS-K820-STATUS      PIC 9(2)    VALUE ZEROS.                     
           05  WS-FOUND            PIC X(1)    VALUE 'N'.                       
           05  WS-CNT              PIC 9(06)   VALUE ZEROS.                     
           05  WS-CNT-PAGE         PIC 9(04)   VALUE ZEROS.                     
           05  WS-CNT-LINE         PIC 9(06)   VALUE 60.                        
           05  WS2-MAX-LINES-LCFA3 PIC 9(2)    VALUE 37.                        
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR CONTROL LISTING                         *            
      *------------------------------------------------------------*            
       01  LCFA3-HDG1.                                                          
           05  LCFA3-LISTING            PIC X(14) VALUE 'P13LCFA3'.             
           05  FILLER                   PIC X(8)  VALUE 'HDB3'.                 
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  LCFA3-DATE               PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(8)  VALUE ' PAGE: '.              
           05  LCFA3-PAGE               PIC ZZZZ9.                              
                                                                                
       01  LCFA3-HDG2.                                                          
           05  FILLER                   PIC X(45) VALUE SPACES.                 
           05  LCFA3-LISTING-TYPE       PIC X(87) VALUE                         
               'APPLICANTS WITH HAND PHONE NUMBER'.                             
                                                                                
       01  LCFA3-HDG4.                                                          
           05  FILLER                   PIC X(7)  VALUE ' S/N   '.              
           05  FILLER                   PIC X(9)  VALUE 'REF-NO   '.            
           05  FILLER                   PIC X(15) VALUE                         
               'APPLICANT NAMES'.                                               
           05  FILLER                   PIC X(8)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE '  UIN  '.              
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  FILLER                   PIC X(5)  VALUE 'CAT  '.                
           05  FILLER                   PIC X(5)  VALUE 'ALLOC'.                
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  FILLER                   PIC X(4)  VALUE 'FLAT'.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'HHTY   '.              
           05  FILLER                   PIC X(4)  VALUE 'MCPS'.                 
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'RES-HA1'.              
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE 'SUCC'.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE 'NT1   '.               
           05  FILLER                   PIC X(6)  VALUE 'UNSUCC'.               
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE 'BALLOT'.               
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE 'ZE'.                   
                                                                                
       01  LCFA3-HDG5.                                                          
           05  FILLER                   PIC X(7)  VALUE SPACES.                 
           05  FILLER                   PIC X(8)  VALUE 'REGN-NO'.              
           05  FILLER                   PIC X(40) VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE 'SCH'.                  
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  FILLER                   PIC X(4)  VALUE 'TYPE'.                 
           05  FILLER                   PIC X(5)  VALUE SPACES.                 
           05  FILLER                   PIC X(4)  VALUE '    '.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  FILLER                   PIC X(8)  VALUE '       '.              
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(5)  VALUE '     '.                
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'ZONE   '.              
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE '   '.                  
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(4)  VALUE 'ZONE'.                 
           05  FILLER                   PIC X(5)  VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE 'POS'.                  
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE 'Q-POS'.                
                                                                                
       01  LCFA3-HDG6.                                                          
           05  FILLER                   PIC X(7)  VALUE '       '.              
           05  FILLER                   PIC X(9)  VALUE 'LOAN-TAG '.            
           05  FILLER                   PIC X(13) VALUE                         
               'REQ-STATUS   '.                                                 
           05  FILLER                   PIC X(15) VALUE                         
               'HP NUMBER      '.                                               
                                                                                
       01  LCFA3-DTL-1.                                                         
           05  LCFA3-SERIAL-NO          PIC ZZZ9.                               
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  LCFA3-REF-NO             PIC X(8)  VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  LCFA3-NAME-1             PIC X(22) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  LCFA3-NRIC-1             PIC X(9)  VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  LCFA3-CAT                PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  LCFA3-ALLOC              PIC X(3)  VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE SPACES.                 
           05  LCFA3-FLAT               PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  LCFA3-HHTY               PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE SPACES.                 
           05  LCFA3-MCPS               PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(9)  VALUE SPACES.                 
           05  LCFA3-RES-HA1            PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE SPACES.                 
           05  LCFA3-SUCC               PIC X(3)  VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE SPACES.                 
           05  LCFA3-NT1                PIC X(3)  VALUE SPACES.                 
           05  FILLER                   PIC X(5)  VALUE SPACES.                 
           05  LCFA3-UNSUCC             PIC X(3)  VALUE SPACES.                 
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  LCFA3-BALLOT-POS         PIC X(5)  VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  LCFA3-ZONE               PIC X(2)  VALUE SPACES.                 
                                                                                
       01  LCFA3-DTL-2.                                                         
           05  FILLER                   PIC X(7)  VALUE SPACES.                 
           05  LCFA3-REGN-NO            PIC X(8)  VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  LCFA3-NAME-2             PIC X(22) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  LCFA3-NRIC-2             PIC X(9)  VALUE SPACES.                 
           05  FILLER                   PIC X(79) VALUE SPACES.                 
           05  LCFA3-QUEUE              PIC X(5)  VALUE SPACES.                 
                                                                                
       01  LCFA3-DTL-3.                                                         
           05  FILLER                   PIC X(16) VALUE SPACES.                 
           05  LCFA3-BLK                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  LCFA3-UNIT               PIC X(11) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  LCFA3-ADDR2              PIC X(65) VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  LCFA3-ADDR3              PIC X(20) VALUE SPACES.                 
                                                                                
       01  LCFA3-DTL-4.                                                         
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  LCFA3-LOAN-TAG           PIC X(6)  VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  LCFA3-REQ-STATUS         PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  LCFA3-HP                 PIC X(50) VALUE SPACES.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPENING-ROUTINE  THRU 1000-EXIT.                        
           PERFORM 2000-READ-F740        THRU 2000-EXIT.                        
           PERFORM 4000-PROCESS-FILES    THRU 4000-EXIT                         
                   UNTIL WS-EOF = 'Y'.                                          
           PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F740                                                 
                       BP13K830                                                 
                       BP13K820                                                 
                OUTPUT P13F740A                                                 
                       P13F740B                                                 
                       P13LCFA3.                                                
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 OPEN ERROR, STATUS IS ' WS-K820-STATUS          
              MOVE WS-K820-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K830 OPEN ERROR, STATUS IS ' WS-K830-STATUS          
              MOVE WS-K830-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-F740.                                                          
      *-------------------------------------------------------------            
            READ BP13F740 AT END                                                
                 MOVE 'Y' TO WS-EOF                                             
                 GO TO 2000-EXIT.                                               
            ADD 1 TO NO-F740-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           PERFORM 4200-PROCESS-RECORD  THRU 4200-EXIT                          
           PERFORM 2000-READ-F740 THRU 2000-EXIT.                               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4200-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
                                                                                
            MOVE SPACES       TO P13F740A-REC                                   
                                                                                
            MOVE 'N'                      TO   WS-FOUND.                        
                                                                                
           MOVE F740-NUM-REGN  TO K820-NUM-REGN                                 
           MOVE F740-NUM-NRIC1 TO K820-NUM-NRIC                                 
           PERFORM 4500-READ-BP13K820  THRU 4500-EXIT                           
                                                                                
                                                                                
            IF WS-FOUND = 'N'                                                   
               WRITE P13F740A-REC FROM BP13F740-REC                             
               ADD 1 TO NO-F740-WO-HP                                           
            ELSE                                                                
               WRITE P13F740B-REC FROM BP13F740-REC                             
               ADD 1 TO NO-F740-W-HP                                            
               PERFORM 4600-MOVE-DETAILS   THRU 4600-EXIT                       
               PERFORM 4400-WRITE-LISTING  THRU 4400-EXIT                       
            END-IF.                                                             
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4400-WRITE-LISTING.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE F740-NUM-REF              TO    LCFA3-REF-NO.                   
           MOVE F740-NUM-REGN             TO    LCFA3-REGN-NO.                  
           MOVE F740-NUM-NRIC1            TO    LCFA3-NRIC-1.                   
           MOVE F740-CDE-NT1              TO    LCFA3-NT1.                      
           MOVE F740-CDE-HOUSEHOLD        TO    LCFA3-HHTY.                     
           MOVE F740-NUM-MCPS             TO    LCFA3-MCPS.                     
           MOVE F740-CDE-RESIDENT-HA1     TO    LCFA3-RES-HA1.                  
           MOVE F740-CDE-FLAT-TYPE        TO    LCFA3-FLAT.                     
           MOVE F740-CDE-ALLOC-SCH        TO    LCFA3-ALLOC.                    
           MOVE F740-CDE-UNSUCC           TO    LCFA3-UNSUCC.                   
           MOVE F740-CDE-CATEGORY         TO    LCFA3-CAT.                      
           MOVE F740-CDE-SUCC-NT-ZONE     TO    LCFA3-SUCC.                     
           MOVE F740-CDE-ZONE             TO    LCFA3-ZONE.                     
           IF F740-NUM-NRIC2 (2:1) NOT = '#'                                    
              MOVE F740-NUM-NRIC2         TO    LCFA3-NRIC-2                    
           ELSE                                                                 
              MOVE SPACES                 TO    LCFA3-NRIC-2.                   
           MOVE F740-NUM-NT-FT-QUEUE      TO    LCFA3-QUEUE.                    
           MOVE F740-NUM-BALLOT-POSITION  TO    LCFA3-BALLOT-POS.               
           MOVE F740-NUM-LOAN-TAG         TO    LCFA3-LOAN-TAG.                 
           MOVE F740-CDE-REQUEST-STATUS   TO    LCFA3-REQ-STATUS.               
                                                                                
           ADD    1                    TO    WS-CNT.                            
           MOVE   NO-F740-W-HP         TO    LCFA3-SERIAL-NO.                   
                                                                                
           IF WS-CNT-LINE > WS2-MAX-LINES-LCFA3                                 
              ADD   1                  TO   WS-CNT-PAGE                         
              MOVE  1                  TO   WS-CNT-LINE                         
              MOVE  WS-CNT-PAGE        TO   LCFA3-PAGE                          
              WRITE LCFA3-PRTREC       FROM LCFA3-HDG1 AFTER PAGE               
                                                                                
              WRITE LCFA3-PRTREC       FROM LCFA3-HDG2 AFTER 1                  
              WRITE LCFA3-PRTREC       FROM LCFA3-HDG4 AFTER 2                  
              WRITE LCFA3-PRTREC       FROM LCFA3-HDG5 AFTER 1                  
              WRITE LCFA3-PRTREC       FROM LCFA3-HDG6 AFTER 1                  
                                                                                
              MOVE  SPACES             TO   LCFA3-PRTREC                        
              WRITE LCFA3-PRTREC                                                
           END-IF.                                                              
                                                                                
           WRITE   LCFA3-PRTREC         FROM  LCFA3-DTL-1 AFTER 1.              
           WRITE   LCFA3-PRTREC         FROM  LCFA3-DTL-2 AFTER 1.              
           WRITE   LCFA3-PRTREC         FROM  LCFA3-DTL-3 AFTER 1.              
           WRITE   LCFA3-PRTREC         FROM  LCFA3-DTL-4 AFTER 1.              
           MOVE    SPACES               TO    LCFA3-PRTREC.                     
           WRITE   LCFA3-PRTREC.                                                
           ADD     4                    TO    WS-CNT-LINE.                      
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4500-READ-BP13K820.                                                      
      *-------------------------------------------------------------            
           MOVE 'N' TO WS-FOUND                                                 
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
            WHEN 00                                                             
                 IF K820-NUM-HP-PGR NOT = SPACES AND LOW-VALUES                 
                  MOVE 'Y' TO WS-FOUND                                          
                 END-IF                                                         
            WHEN 23                                                             
                  CONTINUE                                                      
            WHEN OTHER                                                          
              DISPLAY 'ERROR READING OF BP13K820. STATUS IS '                   
                       WS-K820-STATUS                                           
              MOVE WS-K820-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4600-MOVE-DETAILS.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES TO K820-KEY-FLD K830-KEY-FLD                             
                          LCFA3-NAME-1 LCFA3-NAME-2.                            
                                                                                
           MOVE K820-NME-OCCP TO LCFA3-NAME-1                                   
           MOVE K820-NUM-HP-PGR  TO LCFA3-HP                                    
                                                                                
           IF F740-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
               IF F740-NUM-NRIC2(1:2) NOT = '##'                                
                  MOVE SPACES TO K820-KEY-FLD                                   
                  MOVE F740-NUM-REGN  TO K820-NUM-REGN                          
                  MOVE F740-NUM-NRIC2 TO K820-NUM-NRIC                          
                  PERFORM 4500-READ-BP13K820  THRU 4500-EXIT                    
                  MOVE K820-NME-OCCP   TO LCFA3-NAME-2                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE F740-NUM-REGN  TO K830-NUM-REGN                                 
           PERFORM 4700-READ-BP13K830  THRU 4700-EXIT                           
              IF (K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR =  '#') AND                           
                 (K830-NUM-MAIL-LVL-NUM =  SPACES OR LOW-VALUES))               
                 MOVE K830-NUM-BLK         TO LCFA3-BLK                         
                 IF K830-NUM-LVL-NUM = SPACES AND                               
                    K830-NUM-HSEMAIN = SPACES                                   
                    MOVE SPACES            TO  LCFA3-UNIT                       
                 ELSE                                                           
                    MOVE K830-NUM-FLAT     TO  LCFA3-UNIT                       
                 END-IF                                                         
                 MOVE K830-NME-STREET-BLDG(1:47) TO LCFA3-ADDR2                 
                 IF K830-NUM-POSTAL  = SPACES OR LOW-VALUES OR ZEROES           
                    MOVE SPACES            TO LCFA3-ADDR3                       
                    DISPLAY ' RES POSTAL CDE IS ' K830-NUM-POSTAL               
                            ' FOR REC KEY-FLD ' K830-KEY-FLD                    
                 ELSE                                                           
                    STRING 'SINGAPORE '  K830-NUM-POSTAL                        
                       DELIMITED BY SIZE INTO LCFA3-ADDR3                       
                 END-IF                                                         
              ELSE                                                              
                 MOVE K830-NUM-MAIL-BLK        TO LCFA3-BLK                     
                 IF K830-NUM-MAIL-LVL-NUM = SPACES AND                          
                    K830-NUM-MAIL-HSEMAIN = SPACES                              
                    MOVE SPACES                TO LCFA3-UNIT                    
                 ELSE                                                           
                    MOVE K830-NUM-MAIL-FLAT    TO LCFA3-UNIT                    
                 END-IF                                                         
                 MOVE K830-NME-MAIL-STR-BLDG(1:47) TO LCFA3-ADDR2               
                 IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES OR ZEROS        
                    MOVE SPACES                TO LCFA3-ADDR3                   
                    DISPLAY ' MAIL POSTAL CDE IS ' K830-NUM-POSTAL              
                            ' FOR REC KEY-FLD ' K830-KEY-FLD                    
                 ELSE                                                           
                    STRING 'SINGAPORE '      K830-NUM-MAIL-POSTAL               
                           DELIMITED BY SIZE INTO LCFA3-ADDR3                   
                 END-IF                                                         
              END-IF.                                                           
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4700-READ-BP13K830.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K830  KEY IS  K830-NUM-REGN                                 
                                                                                
           EVALUATE WS-K830-STATUS                                              
            WHEN 00                                                             
            WHEN 02                                                             
            WHEN 23                                                             
               CONTINUE                                                         
            WHEN OTHER                                                          
              DISPLAY 'ERROR READING OF BP13K830. STATUS IS '                   
                       WS-K830-STATUS                                           
              MOVE WS-K830-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4700-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           CLOSE P13F740A                                                       
                 P13F740B                                                       
                 P13LCFA3                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13F740.                                                      
                                                                                
           DISPLAY 'NO. OF F740 REC READ     > ' NO-F740-READ.                  
           DISPLAY 'NO. OF F740 REC W/O HP   > ' NO-F740-WO-HP.                 
           DISPLAY 'NO. OF F740 REC W/  HP   > ' NO-F740-W-HP.                  
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 CLOSE ERROR, STATUS IS ' WS-K820-STATUS         
              MOVE WS-K820-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
