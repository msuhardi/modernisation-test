       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C42H.                                                 
      *AUTHOR.        KSJ3                                                      
      *DATE-WRITTEN.  17/12/2015.                                               
      * ========================================================== *            
      *                SYSTEM OF COMMITMENT (BP13)                 *            
      * ========================================================== *            
      *  OBJECTIVE :                                               *            
      *    1. CREATE RESALE LEVY VOUCHERS FOR SO RAISED FOR THE    *            
      *       DAY.                                                 *            
      *                                                            *            
      *  INPUT   :  BP13F410 - SOC DAILY SALES ORDER FILE          *            
      *             BP13K800 - SOC APPLICATION MASTER FILE         *            
      *             BP13K162 - SOC LEVY TAG FILE                   *            
      *             BP13K470 - SOC RESALE PREMIUM LEVY FILE        *            
      *             BP13K022 - SOC OIC FILE                        *            
      *                                                            *            
      *  OUTPUT  :  BP13KC10 - EJV AND REGN LISTING                *            
      *                                                            *            
      *  LISTING :  P13L42HB - EMAIL REPORT                        *            
      *             P13L42HC - ERROR LISTING                       *            
      *                                                            *            
      * ========================================================== *            
      * CHG-NO    BY    DATE    DESCRIPTION                        *            
      * BP135600  KSJ3  171215  NEW PROGRAM                        *            
      * BP135600  KSJ3  120216  -UPDATE EMAIL DETAILS              *            
      *                         -CHECK BP13K470 FOR OLD FLAT DTLS. *            
      *                          IF NO OLD FLAT TYPE, GET FROM     *            
      *                          BP13K162. IF NFND, PRINT TO RPT.  *            
      * BP135861  KSJ3  240216  FIX INDEX ON OLD FLAT TYPE         *            
      * BP136173  KSJ3  110416  SKIP RECS WHERE K470 LEVY AMT = 0  *            
      *                         AND GET LEVY AMOUNT FROM K470      *            
      * BP136344  KSJ3  140716  Remove Ivy and Serena from email   *            
      *                         recp and add Seng Teck Lim         *            
      * BP136477  RJB1  181016  TO REPLACE KIM HUAT TO YEE MIEN    *            
      * BP137140  PCL4  191217  TO REDEFINE AG07 FILE ERROR CODE   *            
      * BP137782  KV5   160419  REMOVE SENG TECK LIM FROM EMAIL    *            
      *                         RECP AND ADD FATIMAH AND PRISCILLA *            
      * BP138339  KR13  290620  RMVE L42HA REPORT - IOMS CHANGES.  *            
      * BP138757  KV8   150621  TO REMOVE LEE ADDED BERNICE        *            
      * BP139030  ZAR7  070122  REPLACE CHUA YEE MIEN TO LIM KOK CHUN           
      * BP139030  ZAR7  110122  REPLACE Bernice JH GAN with Jialing HE          
      * BP139451  KV8   270223  ADDED TO HARANMRIT                              
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN        TO BP13F410.                           
                                                                                
           SELECT BP13K800 ASSIGN        TO BP13K800                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K800-NUM-REGN                       
                           FILE STATUS   IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K162 ASSIGN        TO BP13K162                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K162-KEY-FLD                        
                           FILE STATUS   IS WS-K162-STATUS.                     
                                                                                
           SELECT BP13K470 ASSIGN        TO BP13K470                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K470-KEY-FLD                        
                           FILE STATUS   IS WS-K470-STATUS.                     
                                                                                
           SELECT BP13K022 ASSIGN        TO BP13K022                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K022-KEY-FLD                        
                           ALTERNATE KEY IS K022-NUM-REGN                       
                           FILE STATUS   IS WS-K022-STATUS.                     
                                                                                
           SELECT BP13KC10 ASSIGN        TO BP13KC10                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS KC10-KEY-FLD                        
                           FILE STATUS   IS WS-KC10-STATUS.                     
                                                                                
           SELECT P13L42HB ASSIGN        TO P13L42HB.                           
           SELECT P13L42HC ASSIGN        TO P13L42HC.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F410                                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
           COPY BP13F410.                                                       
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13K800.                                                       
                                                                                
       FD  BP13K162                                                             
           RECORD CONTAINS 600 CHARACTERS.                                      
           COPY BP13K162.                                                       
                                                                                
       FD  BP13K470                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
           COPY BP13K470.                                                       
                                                                                
       FD  BP13K022                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
           COPY BP13K022.                                                       
                                                                                
       FD  BP13KC10                                                             
           RECORD CONTAINS 800 CHARACTERS.                                      
           COPY BP13KC10.                                                       
                                                                                
       FD  P13L42HB                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  P13L42HB-REC                  PIC X(132).                            
                                                                                
       FD  P13L42HC                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  P13L42HC-REC                  PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K162-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K470-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K022-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-KC10-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-AG07-STATUS            PIC 9(2)   VALUE ZEROS.                
           05  WS-AG07-STAT-X REDEFINES WS-AG07-STATUS PIC X(2).                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F410-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F410-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K800-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K162-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K470-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K022-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KC10-WRT           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-L42HB-WRT          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-L42HC-WRT          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-PAGE-A             PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-PAGE-B             PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-PAGE-C             PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-LINE-A             PIC 9(2)   VALUE 61.                   
           05  WS-CNT-LINE-B             PIC 9(2)   VALUE 61.                   
           05  WS-CNT-LINE-C             PIC 9(2)   VALUE 61.                   
           05  WS-CNT-SNO-A              PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-SNO-B              PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-SNO-C              PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-NRIC               PIC 9(1)   VALUE 00.                   
           05  WS-CNT-NRIC-RSL           PIC 9(1)   VALUE 00.                   
           05  WS-CNT-TMP                PIC 9(2)   VALUE 00.                   
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F410-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-KC10-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K162-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K470-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K022-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-SKIP-REC               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-FMT8-CCYY      PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-FMT8-MM        PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-FMT8-DD        PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-FMT10-DD       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-MM       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-CCYY.                                           
                   15  WS-DTE-FMT10-CC   PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-FMT10-YY   PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-TMP                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-INT                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-FMT-HDB-REF.                                                  
               10  WS-NUM-SCH            PIC X(4)   VALUE SPACES.               
               10  FILLER                PIC X(1)   VALUE '-'.                  
               10  WS-NUM-ACC            PIC X(4)   VALUE SPACES.               
               10  FILLER                PIC X(1)   VALUE '-'.                  
               10  WS-NUM-ACC-CK         PIC X(1)   VALUE SPACES.               
               10  FILLER                PIC X(1)   VALUE '-'.                  
               10  WS-NUM-LESSEE-CODE    PIC X(2)   VALUE SPACES.               
           05  WS-NUM-LEVY-TYPE          PIC X(1)   VALUE SPACES.               
           05  WS-NME-FLAT-TYPE          PIC X(10)  VALUE SPACES.               
           05  WS-NUM-FLAT-TYPE          PIC X(2)   VALUE SPACES.               
           05  WS-NUM-DR-GL-ACCT         PIC X(13)  VALUE SPACES.               
           05  WS-NUM-CR-GL-ACCT         PIC X(13)  VALUE SPACES.               
           05  WS-NUM-VR.                                                       
               10  WS-NUM-DDD            PIC X(3).                              
               10  WS-NUM-FIN-YEAR       PIC X(2).                              
               10  WS-NUM-VR-SERIAL      PIC X(6).                              
                                                                                
       01  WS-LOOP-VARS.                                                        
           05  WS-IDX                    PIC 9(1)   VALUE ZEROES.               
           05  WS-IDX-MAX                PIC 9(1)   VALUE 8.                    
           05  WS-IDX-NRIC               PIC 9(1)   VALUE ZEROES.               
           05  WS-IDX-NRIC-MAX           PIC 9(1)   VALUE 4.                    
           05  WS-TABLE OCCURS 4 TIMES.                                         
               10  WS-NUM-NRIC           PIC X(9)   VALUE SPACES.               
               10  WS-OLD-HDB-REF.                                              
                   15  WS-OLD-SCH        PIC X(4)   VALUE SPACES.               
                   15  WS-OLD-ACC        PIC X(4)   VALUE SPACES.               
                   15  WS-OLD-ACC-CK     PIC X(1)   VALUE SPACES.               
                   15  WS-OLD-LSE-CD     PIC X(2)   VALUE SPACES.               
               10  WS-OLD-FLAT-TYPE      PIC X(2)   VALUE SPACES.               
               10  WS-AMT-LEVY           PIC S9(6)V99.                          
                                                                                
      *-------------------------------------------------------------            
      *    EMAIL REPORT COMMANDS                                                
      *-------------------------------------------------------------            
       01  MAIL-COMMANDS.                                                       
           05  WS-MAIL-HELO              PIC X(132) VALUE                       
               'HELO SGPHDB1'.                                                  
           05  WS-MAIL-MAIL-FROM         PIC X(132) VALUE                       
               'MAIL FROM:<OPCP@SGPHDB1>'.                                      
           05  WS-MAIL-RCPT-TO-3         PIC X(132) VALUE                       
               'RCPT TO:<LIM_Kok_Chun@hdb.gov.sg>'.                             
           05  WS-MAIL-RCPT-TO-4         PIC X(132) VALUE                       
               'RCPT TO:<HE_Jialing@hdb.gov.sg>'.                               
           05  WS-MAIL-RCPT-TO-5         PIC X(132) VALUE                       
               'RCPT TO:<FATIMAH_MN_ABDUL_RAZAK@HDB.GOV.SG>'.                   
           05  WS-MAIL-RCPT-TO-6         PIC X(132) VALUE                       
               'RCPT TO:<PRISCILLA_YY_TAN@HDB.GOV.SG>'.                         
           05  WS-MAIL-DATA              PIC X(132) VALUE 'DATA'.               
           05  WS-MAIL-FROM              PIC X(132) VALUE                       
               'FROM:Soc System - Email Alert'.                                 
           05  WS-MAIL-TO-3              PIC X(132) VALUE                       
               'TO:<LIM_Kok_Chun@hdb.gov.sg>'.                                  
           05  WS-MAIL-TO-4              PIC X(132) VALUE                       
               'TO:<HE_Jialing@hdb.gov.sg>'.                                    
           05  WS-MAIL-TO-5              PIC X(132) VALUE                       
               'TO:<FATIMAH_MN_ABDUL_RAZAK@HDB.GOV.SG>'.                        
           05  WS-MAIL-TO-6              PIC X(132) VALUE                       
               'TO:<PRISCILLA_YY_TAN@HDB.GOV.SG>'.                              
           05  WS-MAIL-TO-6A             PIC X(132) VALUE                       
               'TO:<haranmrit_kaur_tara_singh@hdb.gov.sg>'.                     
           05  WS-MAIL-SUBJECT.                                                 
               10  FILLER                PIC X(132) VALUE                       
                   'SUBJECT : Payment Voucher on Resale Levy'.                  
           05  WS-MAIL-DATE.                                                    
               10  FILLER                PIC X(6)   VALUE                       
                   'DATE: '.                                                    
               10  WS-MAIL-DTE-CURRENT   PIC X(10)  VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      *    P13L42HA REPORT                                                      
      *-------------------------------------------------------------            
       01  L42HA-HEADER1.                                                       
           05  FILLER                    PIC X(8)   VALUE 'P13L42HA'.           
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE 'HDBCAT 3'.           
           05  FILLER                    PIC X(28)  VALUE SPACES.               
           05  FILLER                    PIC X(39)  VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(17)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE 'DATE: '.             
           05  L42HA-DTE-CURRENT         PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE '  PAGE: '.           
           05  L42HA-CNT-PAGE            PIC ZZZ9.                              
                                                                                
       01  L42HA-HEADER2.                                                       
           05  FILLER                    PIC X(36)  VALUE SPACES.               
           05  FILLER                    PIC X(59)  VALUE                       
               'P A Y M E N T   V O U C H E R   O N   R E S A L E   L E         
      -        'V Y'.                                                           
                                                                                
      *-------------------------------------------------------------            
      *    P13L42HB REPORT                                                      
      *-------------------------------------------------------------            
       01  L42HB-HEADER1.                                                       
           05  FILLER                    PIC X(8)   VALUE 'P13L42HB'.           
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE 'HDBCAT 3'.           
           05  FILLER                    PIC X(28)  VALUE SPACES.               
           05  FILLER                    PIC X(39)  VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(17)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE 'DATE: '.             
           05  L42HB-DTE-CURRENT         PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE '  PAGE: '.           
           05  L42HB-CNT-PAGE            PIC ZZZ9.                              
                                                                                
       01  L42HB-HEADER2.                                                       
           05  FILLER                    PIC X(36)  VALUE SPACES.               
           05  FILLER                    PIC X(59)  VALUE                       
               'P A Y M E N T   V O U C H E R   O N   R E S A L E   L E         
      -        'V Y'.                                                           
                                                                                
       01  L42HB-HEADER3.                                                       
           05  FILLER                    PIC X(132) VALUE                       
               'S/No   Voucher No      Regn No    NRIC        ----------        
      -        '-- New Flat Details ------------   ----- Old Flat Detail        
      -        's ----   OIC'.                                                  
                                                                                
       01  L42HB-HEADER4.                                                       
           05  FILLER                    PIC X(132) VALUE                       
               '                                              Scheme Acc        
      -        't      Flat Type    Levy Amount    Scheme Acct      Flat        
      -        ' Type'.                                                         
                                                                                
       01  L42HB-HEADER5.                                                       
           05  FILLER                    PIC X(132) VALUE                       
               '----   -------------   --------   ---------   ----------        
      -        '----   ----------   ------------   --------------   ----        
      -        '------   --------'.                                             
                                                                                
       01  L42HB-NIL.                                                           
           05  FILLER                    PIC X(50)  VALUE SPACES.               
           05  FILLER                    PIC X(32)  VALUE                       
               '***** NO RECORDS PROCESSED *****'.                              
                                                                                
       01  L42HB-DETAIL.                                                        
           05  L42HB-SNO                 PIC ZZZ9.                              
           05  L42HB-SNO-X REDEFINES L42HB-SNO.                                 
               10  FILLER                PIC X(4).                              
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  L42HB-NUM-VR              PIC X(13)  VALUE SPACES.               
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  L42HB-NUM-REGN            PIC X(8)   VALUE SPACES.               
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  L42HB-NUM-NRIC            PIC X(9)   VALUE SPACES.               
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  L42HB-NEW-HDB-REF         PIC X(14)  VALUE SPACES.               
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  L42HB-NEW-FLAT-TYPE       PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  L42HB-AMT-LEVY            PIC Z,ZZZ,ZZ9.99.                      
           05  L42HB-AMT-LEVY-X REDEFINES L42HB-AMT-LEVY.                       
               10  FILLER                PIC X(12).                             
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  L42HB-OLD-HDB-REF         PIC X(14)  VALUE SPACES.               
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  L42HB-OLD-FLAT-TYPE       PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  L42HB-NUM-OIC             PIC X(8)   VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      *    P13L42HC REPORT                                                      
      *-------------------------------------------------------------            
       01  L42HC-HEADER1.                                                       
           05  FILLER                    PIC X(8)   VALUE 'P13L42HC'.           
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE 'HDBCAT 3'.           
           05  FILLER                    PIC X(28)  VALUE SPACES.               
           05  FILLER                    PIC X(39)  VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(17)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE 'DATE: '.             
           05  L42HC-DTE-CURRENT         PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE '  PAGE: '.           
           05  L42HC-CNT-PAGE            PIC ZZZ9.                              
                                                                                
       01  L42HC-HEADER2.                                                       
           05  FILLER                    PIC X(44)  VALUE SPACES.               
           05  FILLER                    PIC X(44)  VALUE                       
               'EXCEPTION REPORT ON RESALE LEVY FOR DAILY SO'.                  
                                                                                
       01  L42HC-HEADER3.                                                       
           05  FILLER                    PIC X(132) VALUE                       
               '     S/NO     REGN NO      NRIC          ERROR DESCRIPTI        
      -        'ON'.                                                            
                                                                                
       01  L42HC-HEADER4.                                                       
           05  FILLER                    PIC X(132) VALUE                       
               '     ----     --------     ---------     ---------------        
      -        '-----------------------------------'.                           
                                                                                
       01  L42HC-DETAIL.                                                        
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L42HC-SNO                 PIC ZZZ9.                              
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L42HC-NUM-REGN            PIC X(8)   VALUE SPACES.               
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L42HC-NUM-NRIC            PIC X(9)   VALUE SPACES.               
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L42HC-NUM-DESC            PIC X(50)  VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      *    WS VARIABLES PASSED FROM SUB-PROGRAM "AG07C907"                      
      *-------------------------------------------------------------            
       COPY AG7C907C.                                                           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-BP13F410-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F410-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BP13K800                                                 
                       BP13K162                                                 
                       BP13K470                                                 
                       BP13K022                                                 
                I-O    BP13KC10                                                 
                OUTPUT P13L42HB                                                 
                       P13L42HC.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K162-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K162 ERROR ' WS-K162-STATUS                  
              MOVE     WS-K162-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K470-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K470 ERROR ' WS-K470-STATUS                  
              MOVE     WS-K470-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KC10-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC10 ERROR ' WS-KC10-STATUS                  
              MOVE     WS-KC10-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    OPEN VSAM FOR VOUCHER NUMBER GENERATION                              
      *-------------------------------------------------------------            
           MOVE 'AG07K960'                 TO   C907-NUM-FILENME.               
           CALL 'K960OPEN' USING AG07C907-AREA.                                 
                                                                                
           IF C907-NUM-FILE-STATUS NOT = '00' AND '97'                          
              DISPLAY 'OPENING AG07K960 ERROR ' C907-NUM-FILE-STATUS            
              MOVE     C907-NUM-FILE-STATUS     TO   WS-AG07-STAT-X             
              MOVE     WS-AG07-STATUS           TO   RETURN-CODE                
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           PERFORM 8100-CONV-DATE-TO-10    THRU 8100-EXIT.                      
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       1210-GEN-VR-NO.                                                          
      *=============================================================            
                                                                                
           MOVE 'SS '                      TO   C907-NUM-DDD.                   
           MOVE 'AG07K960'                 TO   C907-NUM-FILENME.               
                                                                                
           CALL 'AG07C907' USING AG07C907-AREA.                                 
                                                                                
           IF C907-NUM-FILE-STATUS =  '00'                                      
              STRING C907-NUM-DDD                                               
                     C907-NUM-FIN-YR                                            
                     C907-NUM-VR-SERIAL                                         
                     DELIMITED BY SIZE INTO WS-NUM-VR                           
              END-STRING                                                        
           ELSE                                                                 
              MOVE C907-NUM-FILE-STATUS    TO   WS-AG07-STAT-X                  
              MOVE WS-AG07-STATUS          TO   RETURN-CODE                     
              DISPLAY 'ERROR CALLING AG07C907, STATUS = '                       
                       C907-NUM-FILE-STATUS                                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1210-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1300-PRINT-RPTB-HDR.                                                     
      *=============================================================            
                                                                                
           MOVE WS-DTE-CURR-10             TO   WS-MAIL-DTE-CURRENT.            
           WRITE P13L42HB-REC              FROM WS-MAIL-HELO.                   
           WRITE P13L42HB-REC              FROM WS-MAIL-MAIL-FROM.              
           WRITE P13L42HB-REC              FROM WS-MAIL-RCPT-TO-4.              
           WRITE P13L42HB-REC              FROM WS-MAIL-RCPT-TO-5.              
           WRITE P13L42HB-REC              FROM WS-MAIL-RCPT-TO-6.              
           WRITE P13L42HB-REC              FROM WS-MAIL-RCPT-TO-3.              
           WRITE P13L42HB-REC              FROM WS-MAIL-DATA.                   
           WRITE P13L42HB-REC              FROM WS-MAIL-FROM.                   
           WRITE P13L42HB-REC              FROM WS-MAIL-TO-4.                   
           WRITE P13L42HB-REC              FROM WS-MAIL-TO-5.                   
           WRITE P13L42HB-REC              FROM WS-MAIL-TO-6.                   
           WRITE P13L42HB-REC              FROM WS-MAIL-TO-6A.                  
           WRITE P13L42HB-REC              FROM WS-MAIL-TO-3.                   
           WRITE P13L42HB-REC              FROM WS-MAIL-SUBJECT.                
           WRITE P13L42HB-REC              FROM WS-MAIL-DATE.                   
                                                                                
           WRITE P13L42HB-REC              FROM L42HA-HEADER1.                  
           WRITE P13L42HB-REC              FROM L42HA-HEADER2.                  
                                                                                
           MOVE SPACES                     TO   P13L42HB-REC.                   
           WRITE P13L42HB-REC.                                                  
                                                                                
           WRITE P13L42HB-REC              FROM L42HB-HEADER3.                  
           WRITE P13L42HB-REC              FROM L42HB-HEADER4.                  
           WRITE P13L42HB-REC              FROM L42HB-HEADER5.                  
                                                                                
           MOVE SPACES                     TO   P13L42HB-REC.                   
           WRITE P13L42HB-REC.                                                  
                                                                                
           MOVE 1                          TO   WS-CNT-LINE-B.                  
                                                                                
       1300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       1500-PRINT-RPTB-NIL.                                                     
      *=============================================================            
                                                                                
           PERFORM 1300-PRINT-RPTB-HDR     THRU 1300-EXIT.                      
                                                                                
           MOVE SPACES                     TO   P13L42HB-REC.                   
           WRITE P13L42HB-REC.                                                  
                                                                                
           WRITE P13L42HB-REC FROM L42HB-NIL.                                   
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F410-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F410 AT END                                                 
                MOVE 'Y'                   TO   WS-F410-EOF                     
                GO                         TO   2000-EXIT.                      
                                                                                
           ADD 1                           TO   WS-CNT-F410-READ.               
                                                                                
           PERFORM VARYING WS-IDX-NRIC FROM 1 BY 1                              
                     UNTIL WS-IDX-NRIC > WS-IDX-NRIC-MAX                        
              MOVE SPACES TO WS-NUM-NRIC(WS-IDX-NRIC)                           
                             WS-OLD-HDB-REF(WS-IDX-NRIC)                        
                             WS-OLD-FLAT-TYPE(WS-IDX-NRIC)                      
              MOVE ZEROES TO WS-AMT-LEVY(WS-IDX-NRIC)                           
           END-PERFORM.                                                         
                                                                                
           MOVE 'N'                        TO   WS-SKIP-REC.                    
           MOVE SPACES                     TO   L42HB-NUM-NRIC                  
                                                L42HC-NUM-DESC.                 
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13K800-MASTER.                
           MOVE F410-NUM-REGN              TO   K800-NUM-REGN.                  
           PERFORM 3100-READ-BP13K800      THRU 3100-EXIT.                      
           PERFORM 3110-GET-APPLN-NRIC     THRU 3110-EXIT.                      
                                                                                
           IF K800-AMT-PREMIUM      > 0 AND                                     
              K800-NUM-PREMIUM-RATE = 0                                         
                                                                                
      *-------------------------------------------------------------            
      *    CHECK LEVY VOUCHER FILE FOR EXISTING VOUCHER                         
      *-------------------------------------------------------------            
              MOVE F410-NUM-REGN           TO   KC10-NUM-REGN                   
              PERFORM 3200-READ-BP13KC10   THRU 3200-EXIT                       
                                                                                
              IF WS-KC10-FND = 'Y'                                              
                 MOVE SPACES                    TO   L42HC-NUM-NRIC             
                 STRING 'VOUCHER ALREADY ASSIGNED: '                            
                        KC10-NUM-DDD      '-'                                   
                        KC10-NUM-FIN-YEAR '-'                                   
                        KC10-NUM-VR-SERIAL                                      
                        DELIMITED BY SIZE       INTO L42HC-NUM-DESC             
                 PERFORM 8900-PRINT-ERROR       THRU 8900-EXIT                  
              END-IF                                                            
                                                                                
      *-------------------------------------------------------------            
      *    CHECK PREMIUM FILE AND OLD FLAT DETAILS                              
      *-------------------------------------------------------------            
              IF WS-SKIP-REC NOT = 'Y'                                          
                 PERFORM VARYING WS-IDX-NRIC FROM 1 BY 1                        
                         UNTIL WS-IDX-NRIC > WS-CNT-NRIC                        
                    PERFORM 3300-CHECK-PREMIUM  THRU 3300-EXIT                  
                                                                                
                    IF (WS-K470-FND = 'Y') AND                                  
                       (WS-OLD-FLAT-TYPE(WS-IDX-NRIC) = SPACES OR               
                                                   LOW-VALUES OR ZEROES)        
                       PERFORM 3400-GET-OLD-FLAT-DTLS                           
                                                THRU 3400-EXIT                  
                    END-IF                                                      
                 END-PERFORM                                                    
                                                                                
                 IF WS-CNT-NRIC-RSL = 0                                         
                    MOVE SPACES                 TO   L42HC-NUM-NRIC             
                    MOVE 'REGN NOT FOUND IN PREMIUM'                            
                                                TO   L42HC-NUM-DESC             
                    PERFORM 8900-PRINT-ERROR    THRU 8900-EXIT                  
                 END-IF                                                         
              END-IF                                                            
                                                                                
      *-------------------------------------------------------------            
      *    IF SKIP REC, READ NEXT RECORD AND DO NOT PRINT VOUCHER               
      *-------------------------------------------------------------            
              IF WS-SKIP-REC = 'Y'                                              
                 PERFORM 2000-BP13F410-READ     THRU 2000-EXIT                  
                 GO TO 3000-EXIT                                                
              END-IF                                                            
                                                                                
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GENERATE NEW VOUCHER NO FOR EVERY REGN                               
      *-------------------------------------------------------------            
              PERFORM 1210-GEN-VR-NO       THRU 1210-EXIT                       
              MOVE 0                       TO   WS-CNT-SNO-A                    
                                                                                
      *-------------------------------------------------------------            
      *    WRITE TO VOUCHER REPORT AND LEVY VOUCHER FILE IF NRIC                
      *    HAS EXISTING FLAT                                                    
      *-------------------------------------------------------------            
              PERFORM 3610-HEADER-BP13KC10      THRU 3610-EXIT                  
              MOVE 0                            TO   WS-IDX                     
              PERFORM VARYING WS-IDX-NRIC FROM 1 BY 1                           
                      UNTIL WS-NUM-NRIC(WS-IDX-NRIC) = ZEROES OR SPACES         
                                                       OR LOW-VALUES            
                         OR WS-IDX-NRIC > WS-IDX-NRIC-MAX                       
      *               UNTIL WS-IDX-NRIC > WS-CNT-NRIC-RSL                       
                 IF WS-OLD-HDB-REF(WS-IDX-NRIC) NOT = SPACES AND                
                                                      LOW-VALUES                
                    MOVE K800-NUM-FLAT-TYPE     TO   WS-NUM-FLAT-TYPE           
                    PERFORM 8300-GET-DR-GL      THRU 8300-EXIT                  
                                                                                
                    MOVE WS-OLD-FLAT-TYPE(WS-IDX-NRIC)                          
                                                TO   WS-NUM-FLAT-TYPE           
                    PERFORM 8400-GET-CR-GL      THRU 8400-EXIT                  
                                                                                
                    PERFORM 3620-POPULATE-BP13KC10                              
                                                THRU 3620-EXIT                  
                 END-IF                                                         
              END-PERFORM                                                       
              PERFORM 3630-WRITE-BP13KC10       THRU 3630-EXIT                  
                                                                                
      *-------------------------------------------------------------            
      *    GET REGN OIC                                                         
      *-------------------------------------------------------------            
              MOVE SPACES                  TO   BP13K022-REC                    
              MOVE F410-NUM-REGN           TO   K022-NUM-REGN                   
              PERFORM 3700-READ-BP13K022   THRU 3700-EXIT                       
                                                                                
      *-------------------------------------------------------------            
      *    WRITE TO DAILY EMAIL REPORT                                          
      *-------------------------------------------------------------            
              PERFORM 3800-WRITE-P13L42HB  THRU 3800-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13F410-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-READ-BP13K800.                                                      
      *=============================================================            
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              ADD 1                        TO   WS-CNT-K800-READ                
                                                                                
           WHEN 23                                                              
              DISPLAY 'REGN NOT FOUND IN BP13K800 - ' F410-NUM-REGN             
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
                                                                                
           WHEN OTHER                                                           
              MOVE WS-K800-STATUS          TO   RETURN-CODE                     
              DISPLAY 'ERROR READING BP13K800, STATUS = '                       
                       WS-K800-STATUS                                           
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3110-GET-APPLN-NRIC.                                                     
      *=============================================================            
                                                                                
           MOVE 0                          TO   WS-CNT-NRIC                     
                                                WS-CNT-NRIC-RSL.                
                                                                                
           IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE K800-NUM-NRIC1          TO   WS-NUM-NRIC(1)                  
              ADD  1                       TO   WS-CNT-NRIC                     
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE K800-NUM-NRIC2          TO   WS-NUM-NRIC(2)                  
              ADD  1                       TO   WS-CNT-NRIC                     
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE K800-NUM-NRIC3          TO   WS-NUM-NRIC(3)                  
              ADD  1                       TO   WS-CNT-NRIC                     
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE K800-NUM-NRIC4          TO   WS-NUM-NRIC(4)                  
              ADD  1                       TO   WS-CNT-NRIC                     
           END-IF.                                                              
                                                                                
           IF K800-AMT-PREMIUM NOT NUMERIC                                      
              MOVE ZEROES                  TO   K800-AMT-PREMIUM                
           END-IF.                                                              
                                                                                
           IF K800-NUM-PREMIUM-RATE NOT NUMERIC                                 
              MOVE ZEROES                  TO   K800-NUM-PREMIUM-RATE           
           END-IF.                                                              
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3200-READ-BP13KC10.                                                      
      *=============================================================            
                                                                                
           READ BP13KC10.                                                       
                                                                                
           EVALUATE WS-KC10-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-KC10-FND                     
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-KC10-FND                     
                                                                                
           WHEN OTHER                                                           
              MOVE WS-KC10-STATUS          TO   RETURN-CODE                     
              DISPLAY 'ERROR READING BP13KC10, STATUS = '                       
                       WS-KC10-STATUS                                           
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3300-CHECK-PREMIUM.                                                      
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13K162-REC.                   
           MOVE F410-NUM-REGN              TO   K470-NUM-REGN.                  
           MOVE WS-NUM-NRIC(WS-IDX-NRIC)   TO   K470-NUM-NRIC.                  
                                                                                
           PERFORM 3310-READ-BP13K470      THRU 3310-EXIT.                      
                                                                                
           IF WS-K470-FND = 'Y'                                                 
              IF K470-AMT-RESALE-LEVY NOT NUMERIC                               
                 MOVE ZEROES          TO   K470-AMT-RESALE-LEVY                 
              END-IF                                                            
                                                                                
              IF K470-AMT-RESALE-LEVY = ZEROES                                  
                 MOVE 'N'             TO   WS-K470-FND                          
              ELSE                                                              
                 MOVE K470-NUM-HDB-REF                                          
                                      TO   WS-OLD-HDB-REF(WS-IDX-NRIC)          
                 MOVE K470-AMT-RESALE-LEVY                                      
                                      TO   WS-AMT-LEVY(WS-IDX-NRIC)             
                                                                                
                 IF K470-OLD-FLAT-TYPE(1:1) = '0'                               
                    MOVE SPACES       TO   WS-OLD-FLAT-TYPE(WS-IDX-NRIC)        
                    MOVE K470-OLD-FLAT-TYPE(2:1)                                
                                      TO   WS-OLD-FLAT-TYPE(WS-IDX-NRIC)        
                 ELSE                                                           
                    MOVE K470-OLD-FLAT-TYPE                                     
                                      TO   WS-OLD-FLAT-TYPE(WS-IDX-NRIC)        
                 END-IF                                                         
                                                                                
                 ADD 1                TO   WS-CNT-NRIC-RSL                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3310-READ-BP13K470.                                                      
      *=============================================================            
                                                                                
           READ BP13K470.                                                       
                                                                                
           EVALUATE WS-K470-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE 'Y'                     TO   WS-K470-FND                     
              ADD   1                      TO   WS-CNT-K470-READ                
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K470-FND                     
                                                                                
           WHEN OTHER                                                           
              MOVE WS-K470-STATUS          TO   RETURN-CODE                     
              DISPLAY 'ERROR READING BP13K470, STATUS = '                       
                       WS-K470-STATUS                                           
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3310-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3400-GET-OLD-FLAT-DTLS.                                                  
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13K162-REC.                   
           MOVE F410-NUM-REGN              TO   K162-NUM-REGNO.                 
           MOVE WS-NUM-NRIC(WS-IDX-NRIC)   TO   K162-NUM-NRIC.                  
                                                                                
           PERFORM 3410-READ-BP13K162      THRU 3410-EXIT.                      
                                                                                
           IF WS-K162-FND = 'Y'                                                 
              MOVE K162-NUM-FT        TO   WS-OLD-FLAT-TYPE(WS-IDX-NRIC)        
           ELSE                                                                 
              MOVE WS-NUM-NRIC(WS-IDX-NRIC)     TO   L42HC-NUM-NRIC             
              MOVE 'OLD FLAT TYPE MISSING'      TO   L42HC-NUM-DESC             
              PERFORM 8900-PRINT-ERROR          THRU 8900-EXIT                  
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3410-READ-BP13K162.                                                      
      *=============================================================            
                                                                                
           READ BP13K162.                                                       
                                                                                
           EVALUATE WS-K162-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE 'Y'                     TO   WS-K162-FND                     
              ADD   1                      TO   WS-CNT-K162-READ                
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K162-FND                     
                                                                                
           WHEN OTHER                                                           
              MOVE WS-K162-STATUS          TO   RETURN-CODE                     
              DISPLAY 'ERROR READING BP13K162, STATUS = '                       
                       WS-K162-STATUS                                           
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3410-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3610-HEADER-BP13KC10.                                                    
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13KC10-REC.                   
                                                                                
           PERFORM VARYING WS-IDX FROM 1 BY 1 UNTIL WS-IDX > WS-IDX-MAX         
              MOVE 0                       TO   KC10-AMT-TX(WS-IDX)             
                                                KC10-AMT-TX-GST(WS-IDX)         
           END-PERFORM.                                                         
                                                                                
           MOVE F410-NUM-REGN              TO   KC10-NUM-REGN.                  
           MOVE WS-NUM-VR                  TO   KC10-NUM-VR.                    
           MOVE WS-DTE-CURR-8              TO   KC10-DTE-VR-PREPARE.            
           MOVE 'L'                        TO   KC10-NUM-LEVY-TYPE.             
           MOVE 'BP13C42H'                 TO   KC10-NUM-USERID.                
           MOVE WS-DTE-CURR-8              TO   KC10-DTE-UPDATE.                
                                                                                
       3610-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3620-POPULATE-BP13KC10.                                                  
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    NEW FLAT VOUCHER ENTRY                                               
      *-------------------------------------------------------------            
           ADD  1                          TO WS-IDX.                           
           MOVE WS-NUM-NRIC(WS-IDX-NRIC)   TO KC10-NUM-NRIC(WS-IDX).            
           MOVE K800-NUM-FLAT-TYPE         TO KC10-NUM-FLAT-TYPE(WS-IDX)        
           MOVE 'B7'                       TO KC10-NUM-VR-SYS(WS-IDX).          
           MOVE K800-NUM-SCH-ACC           TO KC10-NUM-HDB-REF(WS-IDX).         
           MOVE WS-NUM-DR-GL-ACCT          TO KC10-NUM-GL-ACCT(WS-IDX).         
           MOVE 'D'                        TO KC10-NUM-CR-DR(WS-IDX).           
           MOVE WS-AMT-LEVY(WS-IDX-NRIC)   TO KC10-AMT-TX(WS-IDX).              
           MOVE ZEROES                     TO KC10-AMT-TX-GST(WS-IDX).          
           MOVE 'N'                        TO KC10-NUM-GST-TYP(WS-IDX).         
                                                                                
      *-------------------------------------------------------------            
      *    RESALE FLAT VOUCHER ENTRY                                            
      *-------------------------------------------------------------            
           ADD  1                          TO WS-IDX.                           
           MOVE WS-NUM-NRIC(WS-IDX-NRIC)   TO KC10-NUM-NRIC(WS-IDX).            
           MOVE WS-OLD-FLAT-TYPE(WS-IDX-NRIC)                                   
                                           TO KC10-NUM-FLAT-TYPE(WS-IDX)        
           MOVE 'B8'                       TO KC10-NUM-VR-SYS(WS-IDX).          
           MOVE WS-OLD-HDB-REF(WS-IDX-NRIC)                                     
                                           TO KC10-NUM-HDB-REF(WS-IDX).         
           MOVE WS-NUM-CR-GL-ACCT          TO KC10-NUM-GL-ACCT(WS-IDX).         
           MOVE 'C'                        TO KC10-NUM-CR-DR(WS-IDX).           
           MOVE WS-AMT-LEVY(WS-IDX-NRIC)   TO KC10-AMT-TX(WS-IDX).              
           MOVE ZEROES                     TO KC10-AMT-TX-GST(WS-IDX).          
           MOVE 'N'                        TO KC10-NUM-GST-TYP(WS-IDX).         
                                                                                
       3620-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3630-WRITE-BP13KC10.                                                     
      *=============================================================            
                                                                                
           WRITE BP13KC10-REC.                                                  
                                                                                
           EVALUATE WS-KC10-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-KC10-WRT                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'REWRITE BP13KC10 ERROR ' WS-KC10-STATUS                  
              MOVE     WS-KC10-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3630-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3700-READ-BP13K022.                                                      
      *=============================================================            
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE 'Y'                     TO   WS-K022-FND                     
              ADD   1                      TO   WS-CNT-K022-READ                
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K022-FND                     
                                                                                
           WHEN OTHER                                                           
              MOVE WS-K022-STATUS          TO   RETURN-CODE                     
              DISPLAY 'ERROR READING BP13K022, STATUS = '                       
                       WS-K022-STATUS                                           
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3800-WRITE-P13L42HB.                                                     
      *=============================================================            
                                                                                
           IF WS-CNT-LINE-B >= 60                                               
              PERFORM 1300-PRINT-RPTB-HDR  THRU 1300-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-L42HB-WRT.               
           ADD  1                          TO   WS-CNT-SNO-B.                   
           MOVE WS-CNT-SNO-B               TO   L42HB-SNO-X.                    
           STRING WS-NUM-DDD '-' WS-NUM-FIN-YEAR '-' WS-NUM-VR-SERIAL           
                  DELIMITED BY SIZE        INTO L42HB-NUM-VR.                   
           MOVE F410-NUM-REGN              TO   L42HB-NUM-REGN.                 
                                                                                
           IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES AND ZEROES           
              MOVE K800-NUM-SCH            TO   WS-NUM-SCH                      
              MOVE K800-NUM-ACC            TO   WS-NUM-ACC                      
              MOVE K800-NUM-ACC-CK         TO   WS-NUM-ACC-CK                   
              MOVE K800-NUM-LESSEE-CODE    TO   WS-NUM-LESSEE-CODE              
              MOVE WS-FMT-HDB-REF          TO   L42HB-NEW-HDB-REF               
           ELSE                                                                 
              MOVE SPACES                  TO   L42HB-NEW-HDB-REF               
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-FLAT-TYPE         TO   WS-NUM-FLAT-TYPE.               
           PERFORM 8200-EXP-FLAT-TYPE      THRU 8200-EXIT.                      
           MOVE WS-NME-FLAT-TYPE           TO   L42HB-NEW-FLAT-TYPE.            
                                                                                
           IF WS-K022-FND = 'Y'                                                 
              MOVE K022-NUM-OIC            TO   L42HB-NUM-OIC                   
           ELSE                                                                 
              MOVE SPACES                  TO   L42HB-NUM-OIC                   
           END-IF.                                                              
                                                                                
           PERFORM VARYING WS-IDX FROM 1 BY 1                                   
                     UNTIL WS-IDX > WS-CNT-NRIC                                 
              MOVE WS-NUM-NRIC(WS-IDX)     TO   L42HB-NUM-NRIC                  
                                                                                
              IF WS-OLD-HDB-REF(WS-IDX) NOT = SPACES AND LOW-VALUES             
                                              AND ZEROES                        
                 MOVE WS-OLD-SCH(WS-IDX)   TO   WS-NUM-SCH                      
                 MOVE WS-OLD-ACC(WS-IDX)   TO   WS-NUM-ACC                      
                 MOVE WS-OLD-ACC-CK(WS-IDX)     TO   WS-NUM-ACC-CK              
                 MOVE WS-OLD-LSE-CD(WS-IDX)     TO   WS-NUM-LESSEE-CODE         
                 MOVE WS-FMT-HDB-REF       TO   L42HB-OLD-HDB-REF               
              ELSE                                                              
                 MOVE SPACES               TO   L42HB-OLD-HDB-REF               
              END-IF                                                            
                                                                                
              MOVE WS-OLD-FLAT-TYPE(WS-IDX)                                     
                                           TO   WS-NUM-FLAT-TYPE                
              PERFORM 8200-EXP-FLAT-TYPE   THRU 8200-EXIT                       
              MOVE WS-NME-FLAT-TYPE        TO   L42HB-OLD-FLAT-TYPE             
              MOVE WS-AMT-LEVY(WS-IDX)     TO   L42HB-AMT-LEVY                  
                                                                                
              WRITE P13L42HB-REC           FROM L42HB-DETAIL                    
                                                                                
              IF WS-IDX = 1                                                     
                 MOVE SPACES               TO   L42HB-SNO-X                     
                                                L42HB-NUM-REGN                  
                                                L42HB-NEW-HDB-REF               
                                                L42HB-NEW-FLAT-TYPE             
                                                L42HB-NUM-OIC                   
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           MOVE SPACES                     TO   P13L42HB-REC.                   
           WRITE P13L42HB-REC.                                                  
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8000-CONV-DATE-TO-8.                                                     
      *=============================================================            
      *    INPUT  - WS-DTE-FMT10                                                
      *    OUTPUT - WS-DTE-FMT8                                                 
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT10-CCYY  TO  WS-DTE-FMT8-CCYY.                        
           MOVE WS-DTE-FMT10-MM    TO  WS-DTE-FMT8-MM.                          
           MOVE WS-DTE-FMT10-DD    TO  WS-DTE-FMT8-DD.                          
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8100-CONV-DATE-TO-10.                                                    
      *=============================================================            
      *    INPUT  - WS-DTE-FMT8                                                 
      *    OUTPUT - WS-DTE-FMT10                                                
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT8-CCYY   TO  WS-DTE-FMT10-CCYY.                       
           MOVE WS-DTE-FMT8-MM     TO  WS-DTE-FMT10-MM.                         
           MOVE WS-DTE-FMT8-DD     TO  WS-DTE-FMT10-DD.                         
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8200-EXP-FLAT-TYPE.                                                      
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   WS-NME-FLAT-TYPE.               
                                                                                
           EVALUATE WS-NUM-FLAT-TYPE(1:1)                                       
           WHEN '1'                                                             
              MOVE '1-ROOM'                TO   WS-NME-FLAT-TYPE                
           WHEN '2'                                                             
              MOVE '2-ROOM'                TO   WS-NME-FLAT-TYPE                
           WHEN '3'                                                             
              MOVE '3-ROOM'                TO   WS-NME-FLAT-TYPE                
           WHEN '4'                                                             
              MOVE '4-ROOM'                TO   WS-NME-FLAT-TYPE                
           WHEN '5'                                                             
              MOVE '5-ROOM'                TO   WS-NME-FLAT-TYPE                
           WHEN '6'                                                             
           WHEN 'E'                                                             
              MOVE 'EXEC APT'              TO   WS-NME-FLAT-TYPE                
           END-EVALUATE.                                                        
                                                                                
       8200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8300-GET-DR-GL.                                                          
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   WS-NUM-DR-GL-ACCT.              
                                                                                
           IF WS-NUM-FLAT-TYPE(1:1) = '0'                                       
              MOVE WS-NUM-FLAT-TYPE(2:1)   TO   WS-NUM-FLAT-TYPE(1:1)           
           END-IF.                                                              
                                                                                
           EVALUATE WS-NUM-FLAT-TYPE(1:1)                                       
           WHEN '1'                                                             
              MOVE '9000000406981'         TO   WS-NUM-DR-GL-ACCT               
           WHEN '2'                                                             
              MOVE '9000000406982'         TO   WS-NUM-DR-GL-ACCT               
           WHEN '3'                                                             
              MOVE '9000000406983'         TO   WS-NUM-DR-GL-ACCT               
           WHEN '4'                                                             
              MOVE '9000000406984'         TO   WS-NUM-DR-GL-ACCT               
           WHEN '5'                                                             
              MOVE '9000000406985'         TO   WS-NUM-DR-GL-ACCT               
           WHEN OTHER                                                           
              MOVE '9000000406986'         TO   WS-NUM-DR-GL-ACCT               
           END-EVALUATE.                                                        
                                                                                
       8300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8400-GET-CR-GL.                                                          
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   WS-NUM-CR-GL-ACCT.              
                                                                                
           IF WS-NUM-FLAT-TYPE(1:1) = '0'                                       
              MOVE WS-NUM-FLAT-TYPE(2:1)   TO   WS-NUM-FLAT-TYPE(1:1)           
           END-IF.                                                              
                                                                                
           EVALUATE WS-NUM-FLAT-TYPE(1:1)                                       
           WHEN '1'                                                             
              MOVE '9000000406894'         TO   WS-NUM-CR-GL-ACCT               
           WHEN '2'                                                             
              MOVE '9000000406896'         TO   WS-NUM-CR-GL-ACCT               
           WHEN '3'                                                             
              MOVE '9000000406898'         TO   WS-NUM-CR-GL-ACCT               
           WHEN '4'                                                             
              MOVE '9000000406900'         TO   WS-NUM-CR-GL-ACCT               
           WHEN '5'                                                             
              MOVE '9000000406902'         TO   WS-NUM-CR-GL-ACCT               
           WHEN OTHER                                                           
              MOVE '9000000406904'         TO   WS-NUM-CR-GL-ACCT               
           END-EVALUATE.                                                        
                                                                                
       8400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8900-PRINT-ERROR.                                                        
      *=============================================================            
                                                                                
           IF WS-CNT-LINE-C > 50                                                
              PERFORM 8910-PRINT-ERROR-HDR THRU 8910-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-SNO-C                    
                                                WS-CNT-LINE-C.                  
           MOVE WS-CNT-SNO-C               TO   L42HC-SNO.                      
           MOVE F410-NUM-REGN              TO   L42HC-NUM-REGN.                 
                                                                                
           WRITE P13L42HC-REC              FROM L42HC-DETAIL.                   
           ADD  1                          TO   WS-CNT-L42HC-WRT.               
                                                                                
           MOVE 'Y'                        TO   WS-SKIP-REC.                    
                                                                                
       8900-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8910-PRINT-ERROR-HDR.                                                    
      *=============================================================            
                                                                                
           ADD 1                           TO   WS-CNT-PAGE-C.                  
           MOVE WS-CNT-PAGE-C              TO   L42HC-CNT-PAGE.                 
           MOVE WS-DTE-CURR-10             TO   L42HC-DTE-CURRENT.              
                                                                                
           WRITE P13L42HC-REC FROM L42HC-HEADER1 AFTER PAGE.                    
           WRITE P13L42HC-REC FROM L42HC-HEADER2.                               
           WRITE P13L42HC-REC FROM L42HC-HEADER3 AFTER 2.                       
           WRITE P13L42HC-REC FROM L42HC-HEADER4.                               
                                                                                
           MOVE SPACES                     TO   P13L42HC-REC.                   
           WRITE P13L42HC-REC.                                                  
                                                                                
           MOVE 6                          TO   WS-CNT-LINE-C.                  
                                                                                
       8910-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           IF WS-CNT-L42HB-WRT = 0                                              
              PERFORM 1500-PRINT-RPTB-NIL  THRU 1500-EXIT                       
           END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*------- BP13C42H CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F410 READ      : ' WS-CNT-F410-READ.          
           DISPLAY ' 2. NO OF BP13F410 SKIPPED   : ' WS-CNT-F410-SKIP.          
           DISPLAY ' 3. NO OF BP13K800 READ      : ' WS-CNT-K800-READ.          
           DISPLAY ' 4. NO OF BP13K162 READ      : ' WS-CNT-K162-READ.          
           DISPLAY ' 5. NO OF BP13K470 READ      : ' WS-CNT-K470-READ.          
           DISPLAY ' 6. NO OF BP13K022 READ      : ' WS-CNT-K022-READ.          
           DISPLAY ' 7. NO OF BP13KC10 WRITTEN   : ' WS-CNT-KC10-WRT.           
           DISPLAY ' 9. NO OF P13L42HB WRITTEN   : ' WS-CNT-L42HB-WRT.          
           DISPLAY ' 10.NO OF P13L42HC WRITTEN   : ' WS-CNT-L42HC-WRT.          
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F410                                                       
                 BP13K800                                                       
                 BP13K162                                                       
                 BP13K470                                                       
                 BP13K022                                                       
                 BP13KC10                                                       
                 P13L42HB                                                       
                 P13L42HC.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K162-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K162 ERROR ' WS-K162-STATUS                  
              MOVE     WS-K162-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K470-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K470 ERROR ' WS-K470-STATUS                  
              MOVE     WS-K470-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-KC10-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC10 ERROR ' WS-KC10-STATUS                  
              MOVE     WS-KC10-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    CLOSE VSAM FOR VOUCHER NUMBER GENERATION                             
      *-------------------------------------------------------------            
           MOVE 'AG07K960'                 TO   C907-NUM-FILENME.               
           CALL 'K960CLSE' USING AG07C907-AREA.                                 
                                                                                
           IF C907-NUM-FILE-STATUS NOT = '00' AND '97'                          
              DISPLAY 'CLOSING AG07K960 ERROR ' C907-NUM-FILE-STATUS            
              MOVE     C907-NUM-FILE-STATUS     TO   WS-AG07-STAT-X             
              MOVE     WS-AG07-STATUS           TO   RETURN-CODE                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
