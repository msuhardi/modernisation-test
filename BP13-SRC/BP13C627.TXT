       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C627.                                                    
      *AUTHOR. LSB.                                                             
      *DATE-WRITTEN : 31.08.94                                                  
      *----------------------------------------------------------------*        
      *  SYSTEM OF COMMITMENT (BP13)                                   *        
      *----------------------------------------------------------------*        
      *                                                                *        
      *  OBJECTIVE   : PROGRAM BASED ON BP13F500 SEQUENCE FILE WHICH   *        
      *                ALREADY TAGGED AS UPG OR UP3 UPGRADER TO MATCH  *        
      *                SOC OCCUPIER. IF MATCH OUTPUT OCCUPIER REC FOR  *        
      *                DETERMINE 10YRS OCCUPATION PERIOD.              *        
      *                                                                *        
      *                IF NOT MATCHED, NHH ALSO OUTPUT TO P13F558A.    *        
      *                                                                *        
      * NOTE : SIMILAR PROGRAM IS BP13C809 WITH ONLY 1 OUTPUT P13F558O *        
      *----------------------------------------------------------------*        
      *  CHGE   OIC   DATE   DESCRIPTION                               *        
      *  ----   ---   ----   -----------                               *        
      * N940014 YCH   111094 CHANGE OF KEY FIELD                       *        
      * C950230 LSB   110295 DO NOT UPDATE DTE-REGN, ALREADY UPD IN    *        
      *                      BP13C577 (BP13A537/BP13A500 JCL)          *        
      *BP130323 LSB   220897 COPY FROM BP13C809, ADD ONE OUTPUT FILE   *        
      *BP130377 LSB   180997 READ F558 UNRIL F558-REGN NOT = F500-REGN *        
      *BP130445 SSS   050298 EXPAND F558 RECL FROM 150 TO 400          *        
      *BP130652 NOR   190299 EXPAND F500 RECL FROM 300 TO 500          *        
      *BP130897 AKT   030800 EXPAND F558 RECL FROM 400 TO 600          *        
      *----------------------------------------------------------------*        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT P13F558O ASSIGN TO P13F558O.                                  
           SELECT P13F558A ASSIGN TO P13F558A.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F558                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 600 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F558.                                                           
                                                                                
       FD  BP13F500                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 500 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F500.                                                           
      *                                                                         
       FD  P13F558O                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 600 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
                                                                                
       01  P13F558O-REC            PIC X(600).                                  
      *                                                                         
       FD  P13F558A                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 600 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
                                                                                
       01  P13F558A-REC            PIC X(600).                                  
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F558-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F500-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-MATCH                PIC 9(8)    VALUE ZERO.                      
       01  WS-F500-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-F558-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-F558O-WRITE          PIC 9(8)    VALUE ZEROS.                     
       01  WS-F558A-WRITE          PIC 9(8)    VALUE ZEROS.                     
       01  WS-F558-REGN            PIC X(8)    VALUE SPACES.                    
                                                                                
       77  WS-EOF-F500             PIC X(3).                                    
           88  EOF-OF-F500                     VALUE 'YES'.                     
       77  WS-EOF-F558             PIC X(3).                                    
           88  EOF-OF-F558                     VALUE 'YES'.                     
                                                                                
       COPY WSF558.                                                             
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       000-MAIN-LOGIC.                                                          
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F500 THRU 200-EXIT.                                 
           PERFORM 300-READ-F558 THRU 300-EXIT.                                 
           PERFORM 400-PROCESS   THRU 400-EXIT                                  
                   UNTIL F500-NUM-REGN = HIGH-VALUE                             
                    AND  F558-NUM-REGN = HIGH-VALUE.                            
           PERFORM 900-FINALIZATION THRU 900-EXIT.                              
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
           OPEN INPUT  BP13F500                                                 
                       BP13F558                                                 
                OUTPUT P13F558O                                                 
                       P13F558A.                                                
           MOVE 'NO' TO WS-EOF-F500.                                            
           MOVE 'NO' TO WS-EOF-F558.                                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F500.                                                           
           READ BP13F500 AT END MOVE 'YES' TO WS-EOF-F500                       
                                MOVE HIGH-VALUE TO F500-NUM-REGN                
               GO TO 200-EXIT.                                                  
            ADD 1 TO WS-F500-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-F558.                                                           
           READ BP13F558 AT END MOVE 'YES' TO WS-EOF-F558                       
                                MOVE HIGH-VALUE TO F558-NUM-REGN                
               GO TO 300-EXIT.                                                  
            ADD 1 TO WS-F558-READ.                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *  MATCH  BY REGN                                               *         
      *****************************************************************         
       400-PROCESS.                                                             
                                                                                
           IF F500-NUM-REGN    = F558-NUM-REGN                                  
                ADD 1 TO WS-MATCH                                               
                PERFORM 450-OUTPUT-MATCH-REC THRU 450-EXIT                      
                      UNTIL F558-NUM-REGN NOT = F500-NUM-REGN                   
                      OR F558-NUM-REGN = HIGH-VALUE                             
                PERFORM 200-READ-F500 THRU 200-EXIT                             
           ELSE                                                                 
           IF F500-NUM-REGN   < F558-NUM-REGN                                   
                ADD 1 TO WS-F500-NOTMATCH                                       
                PERFORM 200-READ-F500 THRU 200-EXIT                             
           ELSE                                                                 
                ADD 1 TO WS-F558-NOTMATCH                                       
                MOVE F558-NUM-REGN TO WS-F558-REGN                              
                PERFORM 600-OUTPUT-UNMATCH-NHH THRU 600-EXIT                    
                      UNTIL F558-NUM-REGN = HIGH-VALUE                          
                      OR F558-NUM-REGN NOT = WS-F558-REGN.                      
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       450-OUTPUT-MATCH-REC.                                                    
                                                                                
           WRITE P13F558O-REC FROM BP13F558-REC.                                
           ADD 1 TO WS-F558O-WRITE.                                             
                                                                                
           PERFORM 300-READ-F558 THRU 300-EXIT.                                 
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-OUTPUT-UNMATCH-NHH.                                                  
           ADD 1 TO WS-F558A-WRITE.                                             
           WRITE P13F558A-REC FROM BP13F558-REC.                                
           PERFORM 300-READ-F558 THRU 300-EXIT.                                 
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-FINALIZATION.                                                        
             DISPLAY '*--------BP13C627  -----------*'.                         
             DISPLAY '* UPG-REQUEST MATCH OCCUP REC FOR '.                      
             DISPLAY '* FOR FIVE YEARS OCCUPATION PERIOD'.                      
             DISPLAY '*                                 '.                      
             DISPLAY '1. BP13F500-READ      ' WS-F500-READ.                     
             DISPLAY '2. BP13F558-READ      ' WS-F558-READ.                     
             DISPLAY '3. MATCH              ' WS-MATCH.                         
             DISPLAY '4. F500-NOT-MATCH     ' WS-F500-NOTMATCH.                 
             DISPLAY '5. F558-NOT-MATCH     ' WS-F558-NOTMATCH.                 
             DISPLAY '6. F558O-WRITE -UPG   ' WS-F558O-WRITE.                   
             DISPLAY '7. F558A-WRITE -NHH   ' WS-F558A-WRITE.                   
             DISPLAY '8. OR CURRENT SMS UPG'.                                   
             DISPLAY ' NOTE : 2) = 7) + 8)'.                                    
                                                                                
           CLOSE      BP13F500                                                  
                      BP13F558                                                  
                      P13F558O                                                  
                      P13F558A.                                                 
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
