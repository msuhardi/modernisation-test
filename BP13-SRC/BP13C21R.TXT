      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C21R.                                                 
      *AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
      *DATE-WRITTEN.  10 JUNE 2009.                                             
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO ADD RECORDS TO BP13KH30                  *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13F240 -- BLOCK HEADER FLAT FILE                    *           
      *   2.  BP13K222 -- SUMMARY ALLOCATION FILE                   *           
      *                                                             *           
      *   I-O FILES :                                               *           
      *   1.  BP13KH30 -- SUPPLY FILE                               *           
      *   2.  BP13KH40 -- SUPPLY FILE                               *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP133627 11/06/2009 RB12 NEW PROGRAM                        *           
      * BP134006 26/08/2010 ESA1 TO ADD WRITING IN BP13KH40         *           
      * BP134006 12/10/2010 ESA1 EXTEND BP13KH40 TO 300%            *           
      * BP134125 20/05/2011 ESA1 TO CATER FOR NEW BP13KH40 FIELDS   *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F240 ASSIGN TO BP13F240.                                  
                                                                                
           SELECT BP13K222 ASSIGN TO BP13K222                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K222-KEY-FLD                               
                  FILE STATUS     IS BP13K222-STATUS.                           
                                                                                
           SELECT BP13KH30 ASSIGN TO BP13KH30                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KH30-KEY-FLD                               
                  FILE STATUS     IS BP13KH30-STATUS.                           
                                                                                
           SELECT BP13KH40 ASSIGN TO BP13KH40                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KH40-KEY-FLD                               
                  FILE STATUS     IS BP13KH40-STATUS.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F240                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13K240.                                                           
                                                                                
       FD   BP13KH30                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13KH30.                                                           
                                                                                
       FD   BP13KH40                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13KH40.                                                           
                                                                                
       FD   BP13K222                                                            
            RECORD CONTAINS  100 CHARACTERS.                                    
       COPY BP13K222.                                                           
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-CURR-DATE           PIC X(08) VALUE ZEROES.                       
       01  WS-KH30-ERR-CNT        PIC 9(08) VALUE ZEROES.                       
       01  WS-KH30-FND-SW         PIC X     VALUE SPACES.                       
       01  WS-KH30-WRITE-CNT      PIC 9(08) VALUE ZEROES.                       
       01  WS-KH30-DUPL-REC       PIC 9(08) VALUE ZEROES.                       
       01  WS-KH30-WRITE-ERR      PIC 9(08) VALUE ZEROES.                       
       01  WS-KH40-ERR-CNT        PIC 9(08) VALUE ZEROES.                       
       01  WS-KH40-FND-SW         PIC X     VALUE SPACES.                       
       01  WS-KH40-WRITE-CNT      PIC 9(08) VALUE ZEROES.                       
       01  WS-KH40-DUPL-REC       PIC 9(08) VALUE ZEROES.                       
       01  WS-KH40-WRITE-ERR      PIC 9(08) VALUE ZEROES.                       
       01  WS-K222-READ-ERR       PIC 9(08) VALUE ZEROES.                       
       01  WS-K222-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-F240-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-F240-EOF            PIC X     VALUE SPACES.                       
       01  WS-FLAG                PIC X     VALUE SPACES.                       
       01  BP13KH30-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KH40-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13K222-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  WS-PREV-KEY            PIC X(38) VALUE SPACES.                       
       01  WS-START-QUEUE         PIC X(05) VALUE ZEROES.                       
       01  WS-START-QUEUE-NO REDEFINES WS-START-QUEUE.                          
           05  WS-START-QUEUE-NUM PIC 9(05).                                    
       01  WS-END-QUEUE-NO        PIC 9(05)V99 VALUE ZEROES.                    
       01  WS-END-QUEUE-NUM REDEFINES WS-END-QUEUE-NO.                          
           05  WS-END-QUEUE       PIC 9(05).                                    
           05  WS-END-QUEUE-DEC   PIC 9(02).                                    
       01  WS-RATE                PIC 9(02)V99 VALUE ZEROES.                    
       01  WS-CTR                 PIC 9(02) VALUE ZEROES.                       
       01  WS-CTR2                PIC 9(02) VALUE ZEROES.                       
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F240    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F240-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F240                                                 
                       BP13K222                                                 
                I-O    BP13KH30                                                 
                       BP13KH40.                                                
                                                                                
           IF BP13K222-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K222 : ' BP13K222-STATUS             
              MOVE BP13K222-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KH30-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KH30 : ' BP13KH30-STATUS             
              MOVE BP13KH30-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KH40-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KH40 : ' BP13KH40-STATUS             
              MOVE BP13KH40-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           ACCEPT WS-CURR-DATE FROM DATE YYYYMMDD.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F240.                                                          
      *---------------*                                                         
                                                                                
           READ BP13F240 AT END                                                 
              MOVE 'Y' TO WS-F240-EOF                                           
              GO TO 2000-EXIT.                                                  
                                                                                
           ADD 1 TO WS-F240-READ-CNT.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           IF (WS-PREV-KEY NOT = K240-ALT-KEY5) AND                             
              (K240-ALT-KEY5 NOT = SPACES AND LOW-VALUES)                       
              MOVE K240-ALT-KEY5 TO WS-PREV-KEY                                 
              PERFORM 3200-CREATE-BP13KH30 THRU 3200-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F240          THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3200-CREATE-BP13KH30.                                                    
      *--------------------*                                                    
                                                                                
           MOVE SPACES                   TO   BP13KH30-REC                      
                                              BP13KH40-REC.                     
           INITIALIZE                         BP13KH30-REC                      
                                              BP13KH40-REC.                     
                                                                                
           MOVE K240-NUM-SELECTION5      TO   KH30-NUM-SELECTION-TYPE           
                                              KH40-NUM-SELECTION-TYPE.          
           MOVE K240-DTE-BALLOT5         TO   KH30-DTE-BALLOT                   
                                              KH40-DTE-BALLOT.                  
           MOVE K240-NUM-NEW-TOWN5       TO   KH30-NUM-NEW-TOWN                 
                                              KH40-NUM-NEW-TOWN.                
           MOVE K240-NUM-FLAT-TYPE5      TO   KH30-NUM-FLAT-TYPE                
                                              KH40-NUM-FLAT-TYPE.               
           MOVE 'P13C21R'                TO   KH30-NUM-USERID                   
                                              KH40-NUM-USERID.                  
           ACCEPT KH30-DTE-UPDATE        FROM DATE YYYYMMDD.                    
           ACCEPT KH40-DTE-UPDATE        FROM DATE YYYYMMDD.                    
           MOVE ZEROES                   TO KH40-NUM-TOTAL-INVITED              
                                            KH40-NUM-TOT-INV-FT-FD              
                                            KH40-NUM-TOT-INV-FT                 
                                            KH40-NUM-TOT-ACCEPT-FT-FD           
                                            KH40-NUM-TOT-ACCEPT-FT.             
                                                                                
           PERFORM 3300-READ-BP13K222    THRU 3300-EXIT.                        
                                                                                
           IF K222-NUM-FLAT-OFFER <= ZEROES                                     
              GO TO 3200-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 3400-GET-QUEUE-RANGE  THRU 3400-EXIT.                        
           PERFORM 3500-WRITE-BP13KH30   THRU 3500-EXIT.                        
           PERFORM 3600-WRITE-BP13KH40   THRU 3600-EXIT.                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3300-READ-BP13K222.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                     TO BP13K222-REC.                     
           INITIALIZE                         BP13K222-REC.                     
           MOVE K240-NUM-BTO-ZONE          TO K222-NUM-NT-ZONE.                 
           MOVE K240-DTE-ALLOC             TO K222-DTE-ALLOC.                   
           MOVE '00'                       TO K222-NUM-FLAT-TYPE.               
                                                                                
           READ BP13K222 KEY IS K222-KEY-FLD.                                   
                                                                                
           IF BP13K222-STATUS = 0                                               
              DISPLAY 'RECORD FOUND KEY IS ' K222-KEY-FLD                       
              ADD   1  TO WS-K222-READ-CNT                                      
              MOVE K222-NUM-FLAT-OFFER    TO KH30-NUM-FLAT-SUPPLY               
                                             KH40-NUM-FLAT-SUPPLY               
              MOVE K222-DTE-START-BKAPPMT TO KH30-DTE-START-BKAPPMT             
                                             KH40-DTE-START-BKAPPMT             
              MOVE K222-DTE-END-BKAPPMT   TO KH30-DTE-END-BKAPPMT               
                                             KH40-DTE-END-BKAPPMT               
           ELSE                                                                 
           IF BP13K222-STATUS = 23                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K222:' K222-KEY-FLD              
              ADD   1     TO WS-K222-READ-ERR                                   
              GO          TO 3200-EXIT                                          
           ELSE                                                                 
           IF BP13K222-STATUS NOT = 0                                           
              MOVE '4'  TO RETURN-CODE                                          
              DISPLAY  'READ ERROR IN BP13K222 ' K222-KEY-FLD                   
              ADD   1   TO WS-K222-READ-ERR                                     
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3400-GET-QUEUE-RANGE.                                                    
      *--------------------*                                                    
                                                                                
           MOVE 1 TO WS-CTR.                                                    
                                                                                
           PERFORM 3450-ASSIGN-QUEUE-NOS THRU 3450-EXIT                         
              UNTIL WS-CTR > 20.                                                
                                                                                
           MOVE 1 TO WS-CTR2.                                                   
                                                                                
           PERFORM 3460-GET-PRECENT-KH40 THRU 3460-EXIT                         
              UNTIL WS-CTR2 > 30.                                               
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3450-ASSIGN-QUEUE-NOS.                                                   
      *----------------------*                                                  
                                                                                
           IF WS-CTR = 1                                                        
              MOVE '00001' TO WS-START-QUEUE                                    
           ELSE                                                                 
              MOVE KH30-NUM-END-QUEUE(WS-CTR - 1) TO WS-START-QUEUE             
              ADD 1 TO WS-START-QUEUE-NUM                                       
           END-IF.                                                              
                                                                                
           MOVE WS-START-QUEUE   TO KH30-NUM-START-QUEUE(WS-CTR).               
                                                                                
           COMPUTE KH30-NUM-SUPPLY-PCT(WS-CTR) = WS-CTR * 10.                   
                                                                                
           COMPUTE WS-RATE = KH30-NUM-SUPPLY-PCT(WS-CTR) / 100.                 
                                                                                
           COMPUTE WS-END-QUEUE-NO = WS-RATE * KH30-NUM-FLAT-SUPPLY.            
                                                                                
           IF WS-END-QUEUE-DEC >= 50                                            
              ADD 1 TO WS-END-QUEUE                                             
           END-IF.                                                              
                                                                                
           MOVE WS-END-QUEUE     TO KH30-NUM-END-QUEUE(WS-CTR).                 
                                                                                
           ADD 1 TO WS-CTR.                                                     
                                                                                
       3450-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3460-GET-PRECENT-KH40.                                                   
      *----------------------*                                                  
                                                                                
           COMPUTE KH40-NUM-SUPPLY-PCT(WS-CTR2) = WS-CTR2 * 10.                 
           ADD     1       TO  WS-CTR2.                                         
                                                                                
       3460-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       3500-WRITE-BP13KH30.                                                     
      *--------------------*                                                    
                                                                                
           WRITE BP13KH30-REC.                                                  
                                                                                
           IF BP13KH30-STATUS = 0                                               
              ADD   1  TO WS-KH30-WRITE-CNT                                     
           ELSE                                                                 
           IF BP13KH30-STATUS = 2 OR 22                                         
              ADD   1  TO WS-KH30-DUPL-REC                                      
              DISPLAY 'DUPLICATE RECORD IN BP13KH30:'                           
              DISPLAY ' KEY     : ' KH30-KEY-FLD                                
           ELSE                                                                 
           IF BP13KH30-STATUS NOT = 0                                           
              MOVE '4'  TO RETURN-CODE                                          
              DISPLAY 'WRITE ERROR, STATUS : ' BP13KH30-STATUS                  
              DISPLAY ' KEY     : ' KH30-KEY-FLD                                
              ADD   1   TO WS-KH30-WRITE-ERR                                    
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3600-WRITE-BP13KH40.                                                     
      *--------------------*                                                    
                                                                                
           WRITE BP13KH40-REC.                                                  
                                                                                
           IF BP13KH40-STATUS = 0                                               
              ADD   1  TO WS-KH40-WRITE-CNT                                     
           ELSE                                                                 
           IF BP13KH40-STATUS = 2 OR 22                                         
              ADD   1  TO WS-KH40-DUPL-REC                                      
              DISPLAY 'DUPLICATE RECORD IN BP13KH40:'                           
              DISPLAY ' KEY     : ' KH30-KEY-FLD                                
           ELSE                                                                 
           IF BP13KH40-STATUS NOT = 0                                           
              MOVE '4'  TO RETURN-CODE                                          
              DISPLAY 'WRITE ERROR, STATUS : ' BP13KH40-STATUS                  
              DISPLAY ' KEY     : ' KH40-KEY-FLD                                
              ADD   1   TO WS-KH40-WRITE-ERR                                    
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C21R  *************'.                           
           DISPLAY 'RECS READ FROM  BP13F240    : ' WS-F240-READ-CNT.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ   FROM BP13K222   : ' WS-K222-READ-CNT.           
           DISPLAY 'RECS NOTFND   IN BP13K222   : ' WS-K222-READ-ERR.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN  IN BP13KH30   : ' WS-KH30-WRITE-CNT.          
           DISPLAY 'RECS ERROR WRITE BP13KH30   : ' WS-KH30-WRITE-ERR.          
           DISPLAY 'RECS FOUND    IN BP13KH30   : ' WS-KH30-DUPL-REC.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN  IN BP13KH40   : ' WS-KH40-WRITE-CNT.          
           DISPLAY 'RECS ERROR WRITE BP13KH40   : ' WS-KH40-WRITE-ERR.          
           DISPLAY 'RECS FOUND    IN BP13KH40   : ' WS-KH40-DUPL-REC.           
                                                                                
           CLOSE BP13F240                                                       
                 BP13K222                                                       
                 BP13KH30                                                       
                 BP13KH40.                                                      
                                                                                
           IF BP13K222-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K222-STATUS ' BP13K222-STATUS          
            MOVE BP13K222-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KH30-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KH30-STATUS ' BP13KH30-STATUS          
            MOVE BP13KH30-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KH40-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KH40-STATUS ' BP13KH40-STATUS          
            MOVE BP13KH40-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
