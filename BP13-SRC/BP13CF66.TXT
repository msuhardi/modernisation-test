       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF66.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  28/09/2012.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  PROCESS BP13F20Z TO PRODUCE BP13K740 AND    *            
      *                BP13K825 RECORDS FOR REINSTATEMENT          *            
      *                                                            *            
      *  INPUT      :  BP13F20Z  CASES TO BE REINSTATED            *            
      *                BP13K590  BP13.K590.CANOCCP                 *            
      *  I-O        :  BP13K20Z  BP13.K20Z.REINSTAT                *            
      *                BP13K595  BP13.K595.BALLOT.LOG              *            
      *  OUTPUT     :  BP13K740  BP13.K740.ADHOC                   *            
      *                BP13K825  BP13.K825.ADHOC                   *            
      *                BP13LF66  GENERATED REPORT                  *            
      *                                                            *            
      * ========================================================== *            
      * CHG-NO   DATE     BY   DESCRIPTION                         *            
      * -------- -------- ---- ----------------------------------- *            
      * BP135592 28092012 SMR2 NEW PROGRAM                         *            
      * BP135592 08122014 SMR2 USE CANCELLED CASES WITH RANDOM NO  *            
      *                        ADD NEW REPORT - BP13LF66           *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F20Z  ASSIGN       TO BP13F20Z.                           
                                                                                
           SELECT BP13K595  ASSIGN       TO BP13K595                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K595-KEY-FLD                        
                            FILE STATUS  IS WS-K595-STATUS.                     
                                                                                
           SELECT BP13K590  ASSIGN       TO BP13K590                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K590-KEY-FLD                        
                            FILE STATUS  IS WS-K590-STATUS.                     
                                                                                
           SELECT BP13K20Z  ASSIGN       TO BP13K20Z                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K20Z-KEY-FLD                        
                            FILE STATUS  IS WS-K20Z-STATUS.                     
                                                                                
           SELECT BP13K740  ASSIGN       TO BP13K740                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K740-KEY-FLD                        
                            FILE STATUS  IS WS-K740-STATUS.                     
                                                                                
           SELECT BP13K825  ASSIGN       TO BP13K825                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K825-KEY-FLD                        
                            FILE STATUS  IS WS-K825-STATUS.                     
                                                                                
           SELECT BP13LF66  ASSIGN       TO BP13LF66.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F20Z                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 100 CHARACTERS.                                     
       01   BP13F20Z-REC.                                                       
            05  F20Z-KEY-FLD               PIC X(17).                           
            05  FILLER                     PIC X(83).                           
                                                                                
       FD   BP13K595                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K595.                                                           
                                                                                
       FD   BP13K590                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K590.                                                           
                                                                                
       FD   BP13K20Z                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K20Z.                                                           
                                                                                
       FD   BP13K740                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K740.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K825.                                                           
                                                                                
       FD   BP13LF66                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORDS   ARE OMITTED                                         
            RECORDING MODE   IS F.                                              
       01   BP13LF66-REC                   PIC X(132).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K595-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K590-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K20Z-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K740-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K825-STATUS             PIC 9(02) VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F20Z-READ               PIC 9(06) VALUE ZEROES.               
           05  WS-K20Z-UPD                PIC 9(06) VALUE ZEROES.               
           05  WS-K740-DUPL               PIC 9(06) VALUE ZEROES.               
           05  WS-K740-WRITE              PIC 9(06) VALUE ZEROES.               
           05  WS-K595-WRITE              PIC 9(06) VALUE ZEROES.               
           05  WS-LF66-WRITE              PIC 9(06) VALUE ZEROES.               
           05  WS-K825-DUPL               PIC 9(06) VALUE ZEROES.               
           05  WS-K825-RC44               PIC 9(06) VALUE ZEROES.               
           05  WS-K825-WRITE              PIC 9(06) VALUE ZEROES.               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F20Z-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-K595-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-K590-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-K20Z-FND                PIC X(01) VALUE 'N'.                  
           05  WS-LF66-PRINTED            PIC X(01) VALUE 'N'.                  
                                                                                
       01  WS-DATE-TIME-VARS.                                                   
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY            PIC 9(04).                            
               10  WS-SYS-MM              PIC 9(02).                            
               10  WS-SYS-DD              PIC 9(02).                            
           05  WS-EDITED-DATE             PIC X(10).                            
           05  WS-SYSTEM-TIME             PIC 9(08).                            
                                                                                
       01  WS-WORKING-VARS.                                                     
           05  WS-BP13K595-REC            PIC X(500).                           
                                                                                
      **************************************************************            
      *       BP13LF66 -  R E P O R T   L A Y O U T                *            
      **************************************************************            
       01  LF66-HEADING-01.                                                     
           05  FILLER              PIC X(08)     VALUE 'BP13LF66'.              
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDB3'.                  
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(13)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  LF66-RUN-DATE       PIC X(10).                                   
                                                                                
       01  LF66-HEADING-02.                                                     
           05  FILLER              PIC X(28)     VALUE SPACES.                  
           05  FILLER              PIC X(51)     VALUE                          
               'REPORT ON CANCELLED REGISTRATION NUMBER CREATED IN '.           
           05  FILLER              PIC X(10)     VALUE                          
               'SOC MASTER'.                                                    
                                                                                
       01  LF66-HEADING-03.                                                     
           05  FILLER              PIC X(51)     VALUE                          
               '   BALLOT QUARTER   ALLOCATION MODE   SALES REGN NO'.           
           05  FILLER              PIC X(50)     VALUE                          
               '   RESULT TAG   COHORT   UPDATED BY   UPDATED DATE'.            
                                                                                
       01  LF66-DETAIL.                                                         
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LF66-BALLOT-QTR     PIC X(06).                                   
           05  FILLER              PIC X(11)     VALUE SPACES.                  
           05  LF66-ALLOC-MODE     PIC X(03).                                   
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  LF66-REG-NO         PIC X(08).                                   
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  LF66-RESULT-TAG     PIC X(01).                                   
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  LF66-COHORT         PIC X(02).                                   
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  LF66-UPD-BY         PIC X(08).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LF66-UPD-DATE       PIC X(08).                                   
                                                                                
       01  LF66-TRAILER-01.                                                     
           05  FILLER              PIC X(49)     VALUE                          
               '   REVIEWED BY : ________________________________'.             
           05  FILLER              PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(53)     VALUE                          
               'DATE AND SIGNATURE : ________________________________'.         
                                                                                
       01  LF66-TRAILER-02.                                                     
           05  FILLER              PIC X(42)     VALUE                          
               '                    (NAME AND DESIGNATION)'.                    
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F20Z       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECORDS     THRU 3000-EXIT                      
             UNTIL WS-F20Z-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F20Z                                                 
                       BP13K590                                                 
                  I-O  BP13K20Z                                                 
                       BP13K595                                                 
               OUTPUT  BP13K740                                                 
                       BP13K825                                                 
                       BP13LF66.                                                
                                                                                
           IF WS-K595-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K595 OPEN ERROR ' WS-K595-STATUS                
              MOVE WS-K595-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K590-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K590 OPEN ERROR ' WS-K590-STATUS                
              MOVE WS-K590-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K20Z-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K20Z OPEN ERROR ' WS-K20Z-STATUS                
              MOVE WS-K20Z-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K740-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K740 OPEN ERROR ' WS-K740-STATUS                
              MOVE WS-K740-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K825 OPEN ERROR ' WS-K825-STATUS                
              MOVE WS-K825-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-SYSTEM-DATE.                  
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
             INTO WS-EDITED-DATE.                                               
                                                                                
           MOVE WS-EDITED-DATE              TO LF66-RUN-DATE.                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F20Z.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F20Z                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F20Z-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F20Z-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORDS.                                                    
      *-------------------------------------------------------------            
                                                                                
           PERFORM 3100-READ-BP13K20Z       THRU 3100-EXIT.                     
           IF WS-K20Z-FND = 'Y'                                                 
              PERFORM 4000-STARTBR-BP13K595 THRU 4000-EXIT                      
              PERFORM 5000-STARTBR-BP13K590 THRU 5000-EXIT                      
              PERFORM 6000-UPDATE-BP13K20Z  THRU 6000-EXIT                      
           END-IF.                                                              
           PERFORM 2000-READ-BP13F20Z       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-READ-BP13K20Z.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                      TO BP13K20Z-REC.                    
           INITIALIZE                          BP13K20Z-REC.                    
                                                                                
           MOVE 'N'                         TO WS-K20Z-FND.                     
                                                                                
           MOVE F20Z-KEY-FLD                TO K20Z-KEY-FLD.                    
                                                                                
           READ BP13K20Z.                                                       
                                                                                
           EVALUATE WS-K20Z-STATUS                                              
           WHEN ZEROES                                                          
              MOVE 'Y'                      TO WS-K20Z-FND                      
                                                                                
           WHEN 23                                                              
              DISPLAY 'BP13K20Z - RECORD NOT FOUND, KEY : '                     
                      K20Z-KEY-FLD                                              
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K20Z READ ERROR: ' WS-K20Z-STATUS                    
              MOVE WS-K20Z-STATUS            TO RETURN-CODE                     
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-STARTBR-BP13K595.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                       TO BP13K595-REC.                   
           INITIALIZE                           BP13K595-REC.                   
                                                                                
           MOVE 'N'                          TO WS-K595-EOF.                    
                                                                                
           MOVE K20Z-NUM-REGN                TO K595-NUM-REGN-REF.              
                                                                                
           START BP13K595 KEY IS >= K595-KEY-FLD.                               
                                                                                
           EVALUATE WS-K595-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              PERFORM 4100-READNEXT-BP13K595 THRU 4100-EXIT                     
                UNTIL K595-NUM-REGN-REF NOT = K20Z-NUM-REGN                     
                   OR WS-K595-EOF = 'Y'                                         
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              DISPLAY 'BP13K595 - RECORD NOT FOUND, KEY : '                     
                      K595-NUM-REGN-REF                                         
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K595 STARTBR ERROR: ' WS-K595-STATUS                 
              MOVE WS-K595-STATUS            TO RETURN-CODE                     
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-READNEXT-BP13K595.                                                  
      *-------------------------------------------------------------            
                                                                                
           READ BP13K595 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                     TO WS-K595-EOF.                    
                                                                                
           EVALUATE WS-K595-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              IF K595-NUM-REGN-REF = K20Z-NUM-REGN AND                          
                (K595-NUM-RANDOM NOT = SPACES AND LOW-VALUES) AND               
                 K595-CDE-REQUEST-STATUS = 'C'                                  
                 MOVE BP13K595-REC           TO WS-BP13K595-REC                 
                 PERFORM 4200-WRITE-BP13K740 THRU 4200-EXIT                     
                 PERFORM 4300-WRITE-BP13K595 THRU 4300-EXIT                     
                 PERFORM 4400-WRITE-BP13LF66 THRU 4400-EXIT                     
                 MOVE 'Y'                    TO WS-K595-EOF                     
              END-IF                                                            
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                       TO WS-K595-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K595 READNEXT ERROR: ' WS-K595-STATUS                
              MOVE WS-K595-STATUS            TO RETURN-CODE                     
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4200-WRITE-BP13K740.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                      TO BP13K740-REC.                    
           INITIALIZE                          BP13K740-REC.                    
                                                                                
           MOVE BP13K595-REC                TO BP13K740-REC.                    
                                                                                
           IF K20Z-NUM-RESULT-STATUS = 'U'                                      
              MOVE 'NS'                     TO K740-NUM-BOOK-STATUS             
           ELSE                                                                 
              MOVE K595-CDE-NT1             TO K740-CDE-SUCC-NT-ZONE            
           END-IF.                                                              
                                                                                
           MOVE SPACES                      TO K740-CDE-ELIG-TAG.               
           MOVE K20Z-NUM-RESULT-STATUS      TO K740-CDE-REQUEST-STATUS.         
           MOVE K20Z-NUM-COHORT             TO K740-CDE-COHORT.                 
      *    MOVE K20Z-NUM-QUEUE              TO K740-NUM-NT-FT-QUEUE.            
                                                                                
           WRITE BP13K740-REC.                                                  
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-K740-WRITE                    
                                                                                
           WHEN 22                                                              
              ADD 1                         TO WS-K740-DUPL                     
              DISPLAY 'DUPLICATE RECORD FOR BP13K740. REGN IS - '               
                      K740-NUM-REGN                                             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K740 WRITE ERROR: ' WS-K740-STATUS                   
              MOVE WS-K740-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4300-WRITE-BP13K595.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-BP13K595-REC             TO BP13K595-REC.                    
                                                                                
           IF K20Z-NUM-RESULT-STATUS = 'U'                                      
              MOVE 'NS'                     TO K595-NUM-BOOK-STATUS             
           ELSE                                                                 
              MOVE K595-CDE-NT1             TO K595-CDE-SUCC-NT-ZONE            
           END-IF.                                                              
                                                                                
           MOVE SPACES                      TO K595-CDE-ELIG-TAG.               
           MOVE K20Z-NUM-RESULT-STATUS      TO K595-CDE-REQUEST-STATUS.         
           MOVE K20Z-NUM-COHORT             TO K595-CDE-COHORT.                 
           MOVE 'RU'                        TO K595-NUM-REQUEST-REASON.         
           MOVE WS-SYSTEM-DATE              TO K595-DTE-UPDATE.                 
           MOVE FUNCTION CURRENT-DATE(9:8)  TO WS-SYSTEM-TIME.                  
           MOVE WS-SYSTEM-TIME              TO K595-TME-SYSTEM.                 
                                                                                
           WRITE BP13K595-REC.                                                  
                                                                                
           EVALUATE WS-K595-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              ADD 1                         TO WS-K595-WRITE                    
                                                                                
           WHEN 22                                                              
              DISPLAY 'DUPLICATE KEY IN BP13K595. REGN IS - '                   
                      K595-NUM-REGN-REF                                         
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K595 WRITE ERROR: ' WS-K595-STATUS                   
              MOVE WS-K595-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4400-WRITE-BP13LF66.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF WS-LF66-PRINTED = 'N'                                             
              MOVE 'Y'                      TO WS-LF66-PRINTED                  
              WRITE BP13LF66-REC            FROM LF66-HEADING-01                
              WRITE BP13LF66-REC            FROM LF66-HEADING-02                
              WRITE BP13LF66-REC            FROM LF66-HEADING-03                
                                            AFTER ADVANCING 3 LINES             
           END-IF.                                                              
                                                                                
           MOVE K20Z-DTE-BALLOT             TO LF66-BALLOT-QTR.                 
           MOVE K20Z-NUM-SALES-MODE         TO LF66-ALLOC-MODE.                 
           MOVE K20Z-NUM-REGN               TO LF66-REG-NO.                     
           MOVE K20Z-NUM-RESULT-STATUS      TO LF66-RESULT-TAG.                 
           MOVE K20Z-NUM-COHORT             TO LF66-COHORT.                     
           MOVE K20Z-NUM-USERID             TO LF66-UPD-BY.                     
           MOVE K20Z-DTE-UPDATE             TO LF66-UPD-DATE.                   
                                                                                
           WRITE BP13LF66-REC               FROM LF66-DETAIL.                   
           ADD 1                            TO WS-LF66-WRITE.                   
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-STARTBR-BP13K590.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                       TO BP13K590-REC.                   
           INITIALIZE                           BP13K590-REC.                   
                                                                                
           MOVE 'N'                          TO WS-K590-EOF.                    
                                                                                
           MOVE K20Z-NUM-REGN                TO K590-NUM-REGN.                  
                                                                                
           START BP13K590 KEY IS >= K590-KEY-FLD.                               
                                                                                
           EVALUATE WS-K590-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              PERFORM 5100-READNEXT-BP13K590 THRU 5100-EXIT                     
                UNTIL K590-NUM-REGN NOT = K20Z-NUM-REGN                         
                   OR WS-K590-EOF = 'Y'                                         
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              DISPLAY 'BP13K590 - RECORD NOT FOUND, KEY : '                     
                      K590-NUM-REGN                                             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K590 STARTBR ERROR: ' WS-K590-STATUS                 
              MOVE WS-K590-STATUS            TO RETURN-CODE                     
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5100-READNEXT-BP13K590.                                                  
      *-------------------------------------------------------------            
                                                                                
           READ BP13K590 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                     TO WS-K590-EOF.                    
                                                                                
           EVALUATE WS-K590-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              IF K20Z-NUM-REGN = K590-NUM-REGN                                  
                 PERFORM 5200-WRITE-BP13K825 THRU 5200-EXIT                     
              END-IF                                                            
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                       TO WS-K590-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K590 READNEXT ERROR: ' WS-K590-STATUS                
              MOVE WS-K590-STATUS            TO RETURN-CODE                     
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5200-WRITE-BP13K825.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                      TO BP13K825-REC.                    
           INITIALIZE                          BP13K825-REC.                    
                                                                                
           MOVE BP13K590-REC                TO BP13K825-REC.                    
                                                                                
           WRITE BP13K825-REC.                                                  
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              ADD 1                         TO WS-K825-WRITE                    
                                                                                
           WHEN 22                                                              
              ADD 1                         TO WS-K825-DUPL                     
              DISPLAY 'DUPLICATE RECORD FOR BP13K825. KEY IS - '                
                      K825-KEY-FLD                                              
                                                                                
           WHEN 44                                                              
              ADD 1                         TO WS-K825-RC44                     
              DISPLAY 'BOUNDARY VIOLATION ON BP13K825. KEY IS - '               
                      K825-KEY-FLD                                              
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K825 WRITE ERROR: ' WS-K825-STATUS                   
              MOVE WS-K825-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-UPDATE-BP13K20Z.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-SYSTEM-DATE              TO K20Z-DTE-CREATE-SOC.             
           MOVE 'BP13CF66'                  TO K20Z-NUM-CREATE-SOC.             
                                                                                
           REWRITE BP13K20Z-REC.                                                
                                                                                
           EVALUATE WS-K20Z-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-K20Z-UPD                      
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K20Z REWRITE ERROR: ' WS-K20Z-STATUS                 
              MOVE WS-K20Z-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
                                                                                
           IF WS-LF66-PRINTED = 'Y'                                             
              WRITE BP13LF66-REC            FROM LF66-TRAILER-01                
                                            AFTER ADVANCING 3 LINES             
              WRITE BP13LF66-REC            FROM LF66-TRAILER-02                
           END-IF.                                                              
                                                                                
           DISPLAY '==============================================='.           
           DISPLAY '      PROGRAM : BP13CF66       ' WS-EDITED-DATE.            
           DISPLAY '==============================================='.           
           DISPLAY ' '.                                                         
           DISPLAY 'BP13F20Z RECORDS READ       ' WS-F20Z-READ.                 
           DISPLAY 'BP13K20Z RECORDS UPDATED    ' WS-K20Z-UPD.                  
           DISPLAY 'DUPLICATE BP13K740 RECORDS  ' WS-K740-DUPL.                 
           DISPLAY 'BP13K740 RECORDS WRITTEN    ' WS-K740-WRITE.                
           DISPLAY 'BP13K595 RECORDS WRITTEN    ' WS-K595-WRITE.                
           DISPLAY 'BP13LF66 RECORDS WRITTEN    ' WS-LF66-WRITE.                
           DISPLAY 'DUPLICATE BP13K825 RECORDS  ' WS-K825-DUPL.                 
           DISPLAY 'BOUNDARY VIOLATION BP13K825 ' WS-K825-RC44.                 
           DISPLAY 'BP13K825 RECORDS WRITTEN    ' WS-K825-WRITE.                
           DISPLAY ' '.                                                         
           DISPLAY '==============================================='.           
                                                                                
           CLOSE BP13F20Z                                                       
                 BP13K595                                                       
                 BP13K590                                                       
                 BP13K20Z                                                       
                 BP13K740                                                       
                 BP13K825                                                       
                 BP13LF66.                                                      
                                                                                
           IF WS-K595-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K595 CLOSE ERROR ' WS-K595-STATUS               
              MOVE WS-K595-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K590-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K590 CLOSE ERROR ' WS-K590-STATUS               
              MOVE WS-K590-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K20Z-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K20Z CLOSE ERROR ' WS-K20Z-STATUS               
              MOVE WS-K20Z-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K740-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K740 CLOSE ERROR ' WS-K740-STATUS               
              MOVE WS-K740-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K825 CLOSE ERROR ' WS-K825-STATUS               
              MOVE WS-K825-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
