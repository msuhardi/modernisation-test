       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C39K.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  08/08/2016.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *    OBJECTIVE  :                                            *            
      *        1.  THIS PROGRAMME WILL CREATE THE GL DETAIL        *            
      *            TRANSACTION FILE BP13F180 FOR PREMIUMS          *            
      *            FOR LBS AND 2-ROOM FLEXI CASES (ONE TIME)       *            
      *     INPUT FILES:  1. BP13F120                              *            
      *                   2. BP13K690 - FOR PREMIUM                *            
      *     OUTPUT FILES: 1. BP13F180                              *            
      *     LISTING:      1. BP13L39K (CONTROL LIST)               *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG-NO   BY   DATE     DESCRIPTION                         *            
      * -------  ---  ------   -----------                         *            
      * BP136398 FNP1 15082016  NEW PGM (SIMILAR TO BP13C39C)      *            
      * BP137219 FNP1 19022018  CATER FOR FSH GRANT                *            
      * BP137181 FNP1 29062018  CHANGE 'FS' TO 'SE' FOR F180-JR-SC *            
      * BP138663 FP6  29062021  CATER FOR AMORTISATION OF CCA FLATS*            
      * BP138944 FP6  14102021  EXPAND K690 FROM 600 TO 800        *            
      * BP139942 ZAR7 21032024  REPLACE 410101 WITH 41CZ01         *            
      * ********************************************************** *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F120 ASSIGN TO  BP13F120.                                 
                                                                                
           SELECT SY02F001 ASSIGN TO  SY02F001.                                 
                                                                                
           SELECT BP13F180 ASSIGN TO BP13F180.                                  
                                                                                
           SELECT BP13K690 ASSIGN       TO BP13K690                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K690-KEY-FLD                         
                           FILE STATUS  IS K690-STATUS.                         
                                                                                
           SELECT BP13L39K ASSIGN TO BP13L39K.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F120                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           RECORDING MODE F                                                     
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F120.                                                           
                                                                                
       FD  BP13F180                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 120 CHARACTERS                                       
           RECORDING MODE F                                                     
           LABEL RECORDS ARE OMITTED.                                           
       COPY BP13F180.                                                           
                                                                                
       FD  BP13K690                                                             
           RECORD CONTAINS 800 CHARACTERS.                                      
       COPY BP13K690.                                                           
                                                                                
       FD BP13L39K                                                              
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE F                                                     
           DATA RECORD IS BP13L39K-REPORT.                                      
       01  BP13L39K-REPORT               PIC X(132).                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
       01  WS-F120-EOF-FLAG               PIC X          VALUE 'N'.             
       01  WS-F180-EOF-FLAG               PIC X          VALUE 'N'.             
       01  K690-STATUS                    PIC 99         VALUE 0.               
       01  WS-K690-FOUND                  PIC X          VALUE 'N'.             
                                                                                
       01  WS-AMORT-INCOME-VARS.                                                
          05  WS-DR-AMORT-AMT-DEFER       PIC S9(08)V99   VALUE ZEROS.          
          05  WS-CR-AMORT-AMT-DEFER       PIC S9(08)V99   VALUE ZEROS.          
          05  WS-DR-AMORT-AMT-DEFER-CCA   PIC S9(08)V99   VALUE ZEROS.          
          05  WS-CR-AMORT-AMT-DEFER-CCA   PIC S9(08)V99   VALUE ZEROS.          
          05  WS-DR-AMORT-AMT-DEFER-FSH   PIC S9(08)V99   VALUE ZEROS.          
          05  WS-CR-AMORT-AMT-DEFER-FSH   PIC S9(08)V99   VALUE ZEROS.          
          05  WS-CR-AMORT-AMT-SGP         PIC S9(08)V99   VALUE ZEROS.          
          05  WS-CR-AMORT-AMT-SPR         PIC S9(08)V99   VALUE ZEROS.          
          05  WS-CR-AMORT-AMT-ELD         PIC S9(08)V99   VALUE ZEROS.          
          05  WS-CR-AMORT-AMT-SGP-CCA     PIC S9(08)V99   VALUE ZEROS.          
          05  WS-CR-AMORT-AMT-SPR-CCA     PIC S9(08)V99   VALUE ZEROS.          
          05  WS-CR-AMORT-AMT-ELD-CCA     PIC S9(08)V99   VALUE ZEROS.          
          05  WS-DR-AMORT-DEFER           PIC 9(3)        VALUE ZEROS.          
          05  WS-CR-AMORT-DEFER           PIC 9(3)        VALUE ZEROS.          
          05  WS-DR-AMORT-DEFER-CCA       PIC 9(3)        VALUE ZEROS.          
          05  WS-CR-AMORT-DEFER-CCA       PIC 9(3)        VALUE ZEROS.          
          05  WS-DR-AMORT-DEFER-FSH       PIC 9(3)        VALUE ZEROS.          
          05  WS-CR-AMORT-DEFER-FSH       PIC 9(3)        VALUE ZEROS.          
          05  WS-CR-AMORT-SGP             PIC 9(3)        VALUE ZEROS.          
          05  WS-CR-AMORT-SPR             PIC 9(3)        VALUE ZEROS.          
          05  WS-CR-AMORT-ELD             PIC 9(3)        VALUE ZEROS.          
          05  WS-CR-AMORT-SGP-CCA         PIC 9(3)        VALUE ZEROS.          
          05  WS-CR-AMORT-SPR-CCA         PIC 9(3)        VALUE ZEROS.          
          05  WS-CR-AMORT-ELD-CCA         PIC 9(3)        VALUE ZEROS.          
          05  WS-CR-GL-DEFER              PIC X(13)       VALUE SPACES.         
          05  WS-DR-GL-DEFER              PIC X(13)       VALUE SPACES.         
          05  WS-CR-GL-DEFER-CCA          PIC X(13)       VALUE SPACES.         
          05  WS-DR-GL-DEFER-CCA          PIC X(13)       VALUE SPACES.         
          05  WS-CR-GL-DEFER-FSH          PIC X(13)       VALUE SPACES.         
          05  WS-DR-GL-DEFER-FSH          PIC X(13)       VALUE SPACES.         
          05  WS-GL-DEFER-2R              PIC X(13)       VALUE SPACES.         
          05  WS-CR-GL-SGP                PIC X(13)       VALUE SPACES.         
          05  WS-CR-GL-SPR                PIC X(13)       VALUE SPACES.         
          05  WS-CR-GL-ELD                PIC X(13)       VALUE SPACES.         
          05  WS-CR-GL-SGP-CCA            PIC X(13)       VALUE SPACES.         
          05  WS-CR-GL-SPR-CCA            PIC X(13)       VALUE SPACES.         
          05  WS-CR-GL-ELD-CCA            PIC X(13)       VALUE SPACES.         
                                                                                
       01  WS-CR-TOT                      PIC S9(11)V99   VALUE ZEROS.          
       01  WS-DR-TOT                      PIC S9(11)V99   VALUE ZEROS.          
       01  WS-CR-CASES                    PIC 9(5)       VALUE ZEROS.           
       01  WS-DR-CASES                    PIC 9(5)       VALUE ZEROS.           
       01  WS-READ-F120-COUNT             PIC 9(5)       VALUE ZEROS.           
       01  WS-WRITE-F180-COUNT            PIC 9(5)       VALUE ZEROS.           
       01  WS-WITH-GRANT                  PIC 9(5)       VALUE ZEROS.           
       01  K690-FND-CNT                   PIC 9(5)       VALUE ZEROS.           
       01  K690-NFND-CNT                  PIC 9(5)       VALUE ZEROS.           
       01  WS-F180-PERIOD                 PIC 9(2)       VALUE ZEROS.           
       01  WS-TOT-F180-CREDIT             PIC 9(5)       VALUE ZEROS.           
       01  WS-TOT-F180-DEBIT              PIC 9(5)       VALUE ZEROS.           
       01  WS-PAGE-COUNT                  PIC 99         VALUE ZEROS.           
       01  WS-YEAR                        PIC 999.                              
       01  WS-LEAP-YEAR                   PIC 99.                               
       01  WS-ODD-MONTH                   PIC 9.                                
       01  WS-MONTH                       PIC X(2).                             
           88  CHECK-MM         VALUE '04' '06' '09' '11'.                      
           88  CHECK-FEB        VALUE '02'.                                     
       01  SY02F001-EOF                   PIC X(01)    VALUE 'N'.               
                                                                                
       01  WS-GL-ACCT-VARS.                                                     
           05 WS-ACC-NO1.                                                       
              10 WS-ACC-LEAD              PIC X(01).                            
              10 WS-ACC-INTERNAL          PIC X(06).                            
              10 WS-ACC-FILLER            PIC X(02).                            
              10 WS-ACC-GL                PIC X(04).                            
                                                                                
       01  WS-AMORT-VARS.                                                       
           05 WS-AMT-BAL-DEF              PIC 9(6)V99 VALUE ZEROES.             
           05 WS-AMT-BAL-NEW              PIC 9(6)V99 VALUE ZEROES.             
           05 WS-AMT-BAL-SGP              PIC 9(6)V99 VALUE ZEROES.             
           05 WS-AMT-BAL-SPR              PIC 9(6)V99 VALUE ZEROES.             
           05 WS-AMT-BAL-ELD              PIC 9(6)V99 VALUE ZEROES.             
           05 WS-AMT-BAL-ORIG-X           PIC X(8)    VALUE SPACES.             
           05 WS-AMT-BAL-NUM  REDEFINES WS-AMT-BAL-ORIG-X.                      
              10 WS-AMT-BAL-ORIG          PIC 9(6)V99.                          
           05 WS-AMT-DIFF                 PIC 9(6)V99 VALUE ZEROES.             
                                                                                
      ****************************************************************          
      *    WORKING STORAGE FOR SYSTEM DATE FOR EDITING INTO REPORT   *          
      *    DATE.                                                     *          
      ****************************************************************          
       01  WS-CURRENT-DATE.                                                     
           05 CURRENT-CC                  PIC 99.                               
           05 CURRENT-YY                  PIC 99.                               
           05 CURRENT-MM                  PIC 99.                               
           05 CURRENT-DD                  PIC 99.                               
      ****************************************************************          
      *    GET K160-DTE-OPEN-BF AS GL TRANS DATE FOR MONTH           *          
      ****************************************************************          
       01  WS-F180-TRANS-DATE.                                                  
           05 WS-F180-TRANS-CCYY          PIC 9(4).                             
           05 WS-F180-TRANS-CCYY-N REDEFINES WS-F180-TRANS-CCYY.                
              10 WS-F180-TRANS-CC         PIC 99.                               
              10 WS-F180-TRANS-YY         PIC 99.                               
           05 WS-F180-TRANS-MM            PIC 99.                               
           05 WS-F180-TRANS-DD            PIC 99.                               
                                                                                
      ****************************************************************          
      *    PAGE TITLE FOR REPORT BP13L39K.                           *          
      ****************************************************************          
       01  WS-TITLE-1.                                                          
           05  FILLER               PIC X(8)     VALUE 'BP13L39K'.              
           05  FILLER               PIC X(6)     VALUE SPACES.                  
           05  FILLER               PIC X(4)     VALUE 'HDB3'.                  
           05  FILLER               PIC X(28)    VALUE SPACES.                  
           05  FILLER               PIC X(39)    VALUE                          
           'S Y S T E M   O F   C O M M I T M E N T'.                           
           05  FILLER               PIC X(13)    VALUE SPACES.                  
           05  FILLER               PIC X(7)     VALUE 'DATE : '.               
           05  L39K-EDITED-DATE.                                                
               10  CURRENT-DD       PIC 99.                                     
               10  FILLER           PIC X        VALUE '/'.                     
               10  CURRENT-MM       PIC 99.                                     
               10  FILLER           PIC X        VALUE '/'.                     
               10  CURRENT-CC       PIC 99.                                     
               10  CURRENT-YY       PIC 99.                                     
           05  FILLER               PIC X(3)     VALUE SPACES.                  
           05  FILLER               PIC X(7)     VALUE 'PAGE : '.               
           05  L39K-PAGE            PIC ZZZZ9.                                  
                                                                                
       01  WS-TITLE-2.                                                          
           05  FILLER                     PIC X(37)      VALUE SPACES.          
           05  FILLER                     PIC X(57)      VALUE                  
            'REPORT ON DEBITS AND CREDITS TO AND FROM CLEARING ACCOUNT'.        
           05  FILLER                     PIC X(38)      VALUE SPACES.          
                                                                                
       01  WS-TITLE-3.                                                          
           05  FILLER                     PIC X(48)      VALUE SPACES.          
           05  FILLER                     PIC X(36)      VALUE                  
            'BP13F120 AND GENERAL LEDGER BP13F180'.                             
           05  FILLER                     PIC X(48)      VALUE SPACES.          
                                                                                
      ******************************************************************        
      *    HEADERS FOR TOTAL NUMBER OF RECORDS READ/WRITTEN FROM/TO    *        
      *    FILES BP13F120 AND BP13F180.                                *        
      ******************************************************************        
       01  WS-HEADER-LINE-1.                                                    
           05  FILLER                     PIC X(30)      VALUE SPACES.          
           05  L39K-READ-F120-CNT         PIC ZZZZ9.                            
           05  FILLER                     PIC X(44)      VALUE                  
               ' RECORDS READ FROM CLEARING ACCOUNT BP13F120'.                  
           05  FILLER                     PIC X(53)      VALUE SPACES.          
                                                                                
       01  WS-HEADER-LINE-2.                                                    
           05  FILLER                     PIC X(30)      VALUE SPACES.          
           05  L39K-WRITE-F180-CNT        PIC ZZZZ9.                            
           05  FILLER                     PIC X(44)      VALUE                  
               ' RECORDS WRITTEN TO GENERAL LEDGER  BP13F180'.                  
           05  FILLER                     PIC X(53)      VALUE SPACES.          
      ******************************************************************        
      *    SUMMARY OF NUMBER OF CREDITS AND DEBITS RECORDS TRANSFERED  *        
      *    ACCORDING TO PAYMENT TYPES.                                 *        
      ******************************************************************        
       01  WS-SUMMARY-HEADER-1.                                                 
           05  FILLER                     PIC X(30)      VALUE SPACES.          
           05  FILLER                     PIC X(44)      VALUE                  
               'SUMMARY OF RECORDS WRITTEN TO GENERAL LEDGER'.                  
           05  FILLER                     PIC X(58)      VALUE SPACES.          
                                                                                
       01  WS-SUMMARY-HEADER-2.                                                 
           05  FILLER                     PIC X(30)      VALUE SPACES.          
           05  FILLER                     PIC X(20)      VALUE                  
               'PAYMENT TYPE        '.                                          
           05  FILLER                     PIC X(4)       VALUE SPACES.          
           05  FILLER                     PIC X(6)       VALUE                  
               'DEBITS'.                                                        
           05  FILLER                     PIC X(3)       VALUE SPACES.          
           05  FILLER                     PIC X(7)       VALUE                  
               'CREDITS'.                                                       
           05  FILLER                     PIC X(3)       VALUE SPACES.          
           05  FILLER                     PIC X(15)      VALUE                  
               'AMT DEBITED ($)'.                                               
           05  FILLER                     PIC X(3)       VALUE SPACES.          
           05  FILLER                     PIC X(15)      VALUE                  
               'AMT CREDITED($)'.                                               
           05  FILLER                     PIC X(26)      VALUE SPACES.          
                                                                                
       01  WS-SUMMARY-DETAIL-1.                                                 
           05  FILLER                     PIC X(20)      VALUE SPACES.          
           05  L39K-AMORT-DESC1           PIC X(16)      VALUE SPACES.          
           05  FILLER                     PIC X          VALUE '('.             
           05  L39K-DEFER-GL              PIC X(13)      VALUE SPACES.          
           05  FILLER                     PIC X(1)       VALUE ')'.             
           05  FILLER                     PIC X(5)       VALUE SPACES.          
           05  L39K-DR-CASES              PIC ZZZZ9.                            
           05  FILLER                     PIC X(2)       VALUE SPACES.          
           05  L39K-CR-CASES              PIC ZZZZ9.                            
           05  FILLER                     PIC X(2)       VALUE SPACES.          
           05  L39K-DR-DEFER-DEBIT        PIC ZZZZZZZZZZ9.99.                   
           05  FILLER                     PIC X(02)      VALUE SPACES.          
           05  L39K-CR-DEFER-CREDIT       PIC ZZZZZZZZZZ9.99.                   
                                                                                
       01  WS-SUMMARY-DETAIL-2.                                                 
           05  FILLER                     PIC X(20)      VALUE SPACES.          
           05  L39K-AMORT-DESC            PIC X(16)      VALUE SPACES.          
           05  FILLER                     PIC X          VALUE '('.             
           05  L39K-AMORT-GL              PIC X(13)      VALUE SPACES.          
           05  FILLER                     PIC X(1)       VALUE ')'.             
           05  FILLER                     PIC X(5)       VALUE SPACES.          
           05  L39K-DR-AMORT-CASES        PIC ZZZZ9.                            
           05  FILLER                     PIC X(2)       VALUE SPACES.          
           05  L39K-CR-AMORT-CASES        PIC ZZZZ9.                            
           05  FILLER                     PIC X(2)       VALUE SPACES.          
           05  L39K-DR-AMORT-DEBIT        PIC ZZZZZZZZZZ9.99.                   
           05  FILLER                     PIC X(02)      VALUE SPACES.          
           05  L39K-CR-AMORT-CREDIT       PIC ZZZZZZZZZZ9.99.                   
                                                                                
       01  WS-FOOTER-LINE-1.                                                    
           05  FILLER                     PIC X(30)      VALUE SPACES.          
           05  FILLER                     PIC X(20)      VALUE                  
               'TOTAL               '.                                          
           05  FILLER                     PIC X(5)       VALUE SPACES.          
           05  L39K-DR-CASES-TOT          PIC ZZZZ9.                            
           05  FILLER                     PIC X(2)       VALUE SPACES.          
           05  L39K-CR-CASES-TOT          PIC ZZZZ9.                            
           05  FILLER                     PIC X(3)       VALUE SPACES.          
           05  L39K-DR-TOT                PIC ZZZZZZZZZ9.99.                    
           05  FILLER                     PIC X(3)       VALUE SPACES.          
           05  L39K-CR-TOT                PIC ZZZZZZZZZ9.99.                    
           05  FILLER                     PIC X(26)      VALUE SPACES.          
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       000-MAINT-ROUTINE.                                                       
                                                                                
           PERFORM 100-OPEN-FILE             THRU 100-EXIT.                     
           PERFORM 200-READ-F120             THRU 200-EXIT.                     
           PERFORM 300-PROCESS-GLTRAN-DETAIL THRU 300-EXIT                      
                   UNTIL WS-F120-EOF-FLAG = 'Y'.                                
           PERFORM 700-PRINT-REPORT          THRU 700-EXIT.                     
           PERFORM 999-CLOSE-FILES           THRU 999-EXIT.                     
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***************************************************************           
      *    OPENING FILES SECTION                                    *           
      ***************************************************************           
       100-OPEN-FILE.                                                           
           OPEN INPUT BP13F120                                                  
                      BP13K690                                                  
                      SY02F001                                                  
               OUTPUT BP13F180                                                  
                      BP13L39K.                                                 
                                                                                
           IF K690-STATUS NOT = 00 AND 97                                       
              MOVE K690-STATUS          TO RETURN-CODE                          
              DISPLAY 'ERROR OPENING BM06K690 FILE ' K690-STATUS                
              GO TO 999-CLOSE-FILES.                                            
                                                                                
           READ SY02F001 AT END MOVE 'Y' TO SY02F001-EOF.                       
           IF SY02F001-EOF = 'Y'                                                
              DISPLAY 'RECORD NOT FOUND IN SY02F001.'                           
              DISPLAY 'PROCESSING TERMINATED.'                                  
              PERFORM 999-CLOSE-FILES                                           
           ELSE                                                                 
              MOVE F001-DTE-CURRENT TO WS-F180-TRANS-DATE                       
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    READ FILE BP13F120 FROM THE BEGINING                       *         
      *****************************************************************         
       200-READ-F120.                                                           
                                                                                
           READ BP13F120  AT END                                                
                MOVE 'Y' TO WS-F120-EOF-FLAG                                    
                GO TO 200-EXIT.                                                 
           PERFORM 600-CHK-PERIOD    THRU 600-EXIT.                             
           PERFORM 610-CHK-TRAN-DTE  THRU 610-EXIT.                             
           ADD 1 TO WS-READ-F120-COUNT.                                         
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    CHECKS FOR THE PAYMENT TYPE AND TRANSFER CONTROL TO CHECK   *        
      *    FOR TRANSACTION TYPE.                                       *        
      ******************************************************************        
       300-PROCESS-GLTRAN-DETAIL.                                               
                                                                                
           IF F120-CDE-PAYMENT-TYPE = 'VRL' OR 'SA'                             
              PERFORM 330-READ-BP13K690     THRU 330-EXIT                       
              IF WS-K690-FOUND = 'Y'                                            
                 PERFORM 360-COMPARE-BAL    THRU 360-EXIT                       
                 PERFORM 410-DR-DEFER       THRU 410-EXIT                       
                 PERFORM 510-CR-DEFER-OTH   THRU 510-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F120          THRU 200-EXIT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
      ******************************************************************        
      *    READ BP13K690 TO GET THE AMORT AMOUNT FOR GRANT AND FOR     *        
      *    PREMIUM                                                     *        
      ******************************************************************        
       330-READ-BP13K690.                                                       
                                                                                
           MOVE 'N'               TO WS-K690-FOUND.                             
           MOVE SPACES            TO K690-KEY-FLD.                              
           MOVE F120-NUM-SCH-ACCT TO K690-KEY-FLD.                              
                                                                                
           READ BP13K690.                                                       
                                                                                
           EVALUATE K690-STATUS                                                 
             WHEN 00                                                            
                MOVE 'Y' TO WS-K690-FOUND                                       
                ADD 1 TO K690-FND-CNT                                           
                PERFORM 340-INITIALIZE-FIELDS    THRU 340-EXIT                  
             WHEN 23                                                            
                MOVE 'N' TO WS-K690-FOUND                                       
                ADD 1 TO K690-NFND-CNT                                          
             WHEN OTHER                                                         
                DISPLAY 'BP13K690 READ ERROR, STATUS IS '                       
                       K690-STATUS ' KEY IS ' K690-KEY-FLD                      
                       MOVE K690-STATUS TO RETURN-CODE                          
                PERFORM 999-CLOSE-FILES  THRU 999-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       330-EXIT.                                                                
           EXIT.                                                                
                                                                                
       340-INITIALIZE-FIELDS.                                                   
                                                                                
                                                                                
                                                                                
           IF K690-AMT-SINGLES-PREMIUM NOT NUMERIC                              
              MOVE ZEROES TO K690-AMT-SINGLES-PREMIUM                           
           END-IF.                                                              
                                                                                
           IF K690-AMT-AMORT-SGP-MTH NOT NUMERIC                                
              MOVE ZEROES TO K690-AMT-AMORT-SGP-MTH                             
           END-IF.                                                              
                                                                                
           IF K690-AMT-AMORT-SGP-ACCUM NOT NUMERIC                              
              MOVE ZEROES TO K690-AMT-AMORT-SGP-ACCUM                           
           END-IF.                                                              
                                                                                
           IF K690-AMT-SC-SPR-PREMIUM  NOT NUMERIC                              
              MOVE ZEROES TO K690-AMT-SC-SPR-PREMIUM                            
           END-IF.                                                              
                                                                                
           IF K690-AMT-AMORT-SPR-MTH   NOT NUMERIC                              
              MOVE ZEROES TO K690-AMT-AMORT-SPR-MTH                             
           END-IF.                                                              
                                                                                
           IF K690-AMT-AMORT-SPR-ACCUM NOT NUMERIC                              
              MOVE ZEROES TO K690-AMT-AMORT-SPR-ACCUM                           
           END-IF.                                                              
                                                                                
           IF K690-AMT-ELDERLY-PREMIUM NOT NUMERIC                              
              MOVE ZEROES TO K690-AMT-ELDERLY-PREMIUM                           
           END-IF.                                                              
                                                                                
           IF K690-AMT-AMORT-ELD-MTH   NOT NUMERIC                              
              MOVE ZEROES TO K690-AMT-AMORT-ELD-MTH                             
           END-IF.                                                              
                                                                                
           IF K690-AMT-AMORT-ELD-ACCUM NOT NUMERIC                              
              MOVE ZEROES TO K690-AMT-AMORT-ELD-ACCUM                           
           END-IF.                                                              
                                                                                
       340-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    COMPARE ORIGINAL DEFER INCOME BAL WITH NEW COMPUTED SPLIT            
      *****************************************************************         
       360-COMPARE-BAL.                                                         
           MOVE ZEROES            TO WS-AMT-BAL-DEF                             
                                     WS-AMT-BAL-NEW                             
                                     WS-AMT-BAL-SGP                             
                                     WS-AMT-BAL-SPR                             
                                     WS-AMT-BAL-ELD.                            
                                                                                
           COMPUTE WS-AMT-BAL-DEF = K690-AMT-DEFER-INCOME.                      
                                                                                
                                                                                
           IF K690-AMT-SINGLES-PREMIUM NOT = ZEROES                             
              COMPUTE WS-AMT-BAL-SGP = K690-AMT-SINGLES-PREMIUM                 
                                                                                
           END-IF.                                                              
                                                                                
           IF K690-AMT-SC-SPR-PREMIUM NOT = ZEROES                              
              COMPUTE WS-AMT-BAL-SPR = K690-AMT-SC-SPR-PREMIUM                  
           END-IF.                                                              
                                                                                
           IF K690-AMT-ELDERLY-PREMIUM NOT = ZEROES                             
              COMPUTE WS-AMT-BAL-ELD = K690-AMT-ELDERLY-PREMIUM                 
           END-IF.                                                              
                                                                                
           COMPUTE WS-AMT-BAL-NEW = WS-AMT-BAL-DEF +                            
                                    WS-AMT-BAL-SGP +                            
                                    WS-AMT-BAL-SPR +                            
                                    WS-AMT-BAL-ELD.                             
                                                                                
       360-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    DEBIT ORIGINAL DEFER INCOME BALANCE                                  
      *****************************************************************         
       410-DR-DEFER.                                                            
                                                                                
           MOVE SPACES                 TO   F180-GL-REC.                        
           MOVE ZEROES                 TO   F180-AMT-GST-RATE.                  
           MOVE WS-F180-TRANS-YY       TO   F180-JR-YY F180-TRANS-YY.           
           MOVE WS-F180-TRANS-MM       TO   F180-JR-MM F180-TRANS-MM.           
           MOVE WS-F180-TRANS-DD       TO   F180-TRANS-DD.                      
           MOVE WS-F180-PERIOD         TO   F180-PERIOD.                        
           MOVE F120-NUM-SCH-ACCT      TO   F180-NUM-SCH-ACC.                   
           IF F120-CDE-PAYMENT-TYPE = 'VRL'                                     
              IF F120-CDE-VALID = 'H'                                           
                 MOVE 'SE'             TO F180-JR-SC                            
              ELSE                                                              
                 MOVE 'SF'                TO   F180-JR-SC                       
              END-IF                                                            
           ELSE                                                                 
              MOVE 'SA'                   TO   F180-JR-SC                       
           END-IF.                                                              
           COMPUTE F180-CR-AMT = K690-AMT-SELLING-PRICE.                        
                                                                                
           MOVE SPACES                 TO WS-ACC-NO1.                           
           MOVE '0'                    TO WS-ACC-LEAD.                          
           MOVE '57'                   TO WS-ACC-FILLER.                        
           IF F120-CDE-VALID = 'H'                                              
              MOVE '419200'            TO WS-ACC-INTERNAL                       
              MOVE '4045'              TO WS-ACC-GL                             
              MOVE 'SOC-AMORT OF FSH'  TO F180-TRANS-DESC                       
              ADD   1                     TO WS-DR-AMORT-DEFER-FSH              
              ADD F180-CR-AMT             TO WS-DR-AMORT-AMT-DEFER-FSH          
              MOVE WS-ACC-NO1             TO WS-DR-GL-DEFER-FSH                 
                                             F180-ACC-NO1                       
           ELSE                                                                 
              IF K690-CDE-TYP-ACTUSE = '01' AND                                 
                 K690-CDE-MODL-DESG  = '74'                                     
                 MOVE '407600'            TO WS-ACC-INTERNAL                    
                 MOVE '4046'              TO WS-ACC-GL                          
                 MOVE 'SOC-AMORT OF BAL DEFER REQ SURR CCA'                     
                                          TO F180-TRANS-DESC                    
                 ADD   1                  TO WS-DR-AMORT-DEFER-CCA              
                 ADD F180-CR-AMT          TO WS-DR-AMORT-AMT-DEFER-CCA          
                 MOVE WS-ACC-NO1          TO WS-DR-GL-DEFER-CCA                 
                                             F180-ACC-NO1                       
              ELSE                                                              
                 MOVE '407600'            TO WS-ACC-INTERNAL                    
                 MOVE '4041'              TO WS-ACC-GL                          
                 MOVE 'SOC-AMORT OF BAL DEFER REQ SURR'                         
                                          TO F180-TRANS-DESC                    
                 ADD   1                  TO WS-DR-AMORT-DEFER                  
                 ADD F180-CR-AMT          TO WS-DR-AMORT-AMT-DEFER              
                 MOVE WS-ACC-NO1          TO WS-DR-GL-DEFER                     
                                             F180-ACC-NO1                       
              END-IF                                                            
           END-IF.                                                              
           WRITE F180-GL-REC.                                                   
           ADD 1                       TO   WS-WRITE-F180-COUNT.                
                                                                                
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    PREMIUMS                                                             
      *****************************************************************         
       510-CR-DEFER-OTH.                                                        
                                                                                
           MOVE SPACES              TO   F180-GL-REC                            
           MOVE ZEROES              TO   F180-AMT-GST-RATE                      
           IF F120-CDE-PAYMENT-TYPE = 'VRL'                                     
              IF F120-CDE-VALID = 'H'                                           
                 MOVE 'SE'             TO   F180-JR-SC                          
              ELSE                                                              
                 MOVE 'SF'             TO   F180-JR-SC                          
              END-IF                                                            
           ELSE                                                                 
              MOVE 'SA'                TO   F180-JR-SC                          
           END-IF                                                               
           MOVE WS-F180-TRANS-YY    TO   F180-JR-YY F180-TRANS-YY               
           MOVE WS-F180-TRANS-MM    TO   F180-JR-MM F180-TRANS-MM               
           MOVE WS-F180-TRANS-DD    TO   F180-TRANS-DD                          
           MOVE WS-F180-PERIOD      TO   F180-PERIOD                            
           MOVE F120-NUM-SCH-ACCT   TO   F180-NUM-SCH-ACC                       
           COMPUTE F180-CR-AMT = 0 - WS-AMT-BAL-DEF                             
                                                                                
           MOVE SPACES              TO   WS-ACC-NO1                             
           MOVE '0'                 TO   WS-ACC-LEAD                            
           MOVE '57'                TO   WS-ACC-FILLER                          
           IF F120-CDE-VALID = 'H'                                              
             MOVE '419200'            TO   WS-ACC-INTERNAL                      
             MOVE '4045'              TO   WS-ACC-GL                            
             MOVE 'SOC-AMORT OF FSH'                                            
                                      TO   F180-TRANS-DESC                      
             ADD   1                  TO   WS-CR-AMORT-DEFER-FSH                
             ADD F180-CR-AMT          TO   WS-CR-AMORT-AMT-DEFER-FSH            
             MOVE WS-ACC-NO1          TO   WS-CR-GL-DEFER-FSH                   
                                           F180-ACC-NO1                         
           ELSE                                                                 
             IF K690-CDE-TYP-ACTUSE = '01' AND                                  
                K690-CDE-MODL-DESG  = '74'                                      
                MOVE '407600'         TO   WS-ACC-INTERNAL                      
                MOVE '4046'           TO   WS-ACC-GL                            
                MOVE 'SOC-AMORT OF BAL DEFER REQ SURR CCA'                      
                                      TO   F180-TRANS-DESC                      
                ADD   1               TO   WS-CR-AMORT-DEFER-CCA                
                ADD F180-CR-AMT       TO   WS-CR-AMORT-AMT-DEFER-CCA            
                MOVE WS-ACC-NO1       TO   WS-CR-GL-DEFER-CCA                   
                                           F180-ACC-NO1                         
              ELSE                                                              
                MOVE '407600'         TO   WS-ACC-INTERNAL                      
                MOVE '4041'           TO   WS-ACC-GL                            
                MOVE 'SOC-AMORT OF BAL DEFER REQ SURR'                          
                                      TO   F180-TRANS-DESC                      
                ADD   1               TO   WS-CR-AMORT-DEFER                    
                ADD F180-CR-AMT       TO   WS-CR-AMORT-AMT-DEFER                
                MOVE WS-ACC-NO1       TO   WS-CR-GL-DEFER                       
                                           F180-ACC-NO1                         
              END-IF                                                            
           END-IF                                                               
                                                                                
           WRITE F180-GL-REC                                                    
           ADD 1                    TO   WS-WRITE-F180-COUNT.                   
                                                                                
           IF K690-AMT-SINGLES-PREMIUM NOT = ZEROES                             
              MOVE SPACES              TO   F180-GL-REC                         
              MOVE ZEROES              TO   F180-AMT-GST-RATE                   
              IF F120-CDE-PAYMENT-TYPE = 'VRL'                                  
                 IF F120-CDE-VALID = 'H'                                        
                    MOVE 'SE'             TO   F180-JR-SC                       
                 ELSE                                                           
                    MOVE 'SF'             TO   F180-JR-SC                       
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'SA'                TO   F180-JR-SC                       
              END-IF                                                            
              MOVE WS-F180-TRANS-YY    TO   F180-JR-YY F180-TRANS-YY            
              MOVE WS-F180-TRANS-MM    TO   F180-JR-MM F180-TRANS-MM            
              MOVE WS-F180-TRANS-DD    TO   F180-TRANS-DD                       
              MOVE WS-F180-PERIOD      TO   F180-PERIOD                         
              MOVE F120-NUM-SCH-ACCT   TO   F180-NUM-SCH-ACC                    
              COMPUTE F180-CR-AMT = 0 - WS-AMT-BAL-SGP                          
                                                                                
              MOVE SPACES              TO   WS-ACC-NO1                          
              MOVE '0'                 TO   WS-ACC-LEAD                         
              MOVE '57'                TO   WS-ACC-FILLER                       
              MOVE '41CZ01'            TO   WS-ACC-INTERNAL                     
              IF K690-CDE-TYP-ACTUSE = '01' AND                                 
                 K690-CDE-MODL-DESG  = '74'                                     
                 MOVE '4046'           TO   WS-ACC-GL                           
                 MOVE 'SOC-AMORT OF BAL SINGLES PREMIUM CCA'                    
                                       TO   F180-TRANS-DESC                     
                 ADD   1               TO   WS-CR-AMORT-SGP-CCA                 
                 ADD F180-CR-AMT       TO   WS-CR-AMORT-AMT-SGP-CCA             
                 MOVE WS-ACC-NO1       TO   WS-CR-GL-SGP-CCA                    
                                               F180-ACC-NO1                     
              ELSE                                                              
                 MOVE '4041'           TO   WS-ACC-GL                           
                 MOVE 'SOC-AMORT OF BAL SINGLES PREMIUM'                        
                                       TO   F180-TRANS-DESC                     
                 ADD   1               TO   WS-CR-AMORT-SGP                     
                 ADD F180-CR-AMT       TO   WS-CR-AMORT-AMT-SGP                 
                 MOVE WS-ACC-NO1       TO   WS-CR-GL-SGP                        
                                            F180-ACC-NO1                        
              END-IF                                                            
                                                                                
              WRITE F180-GL-REC                                                 
              ADD 1                    TO   WS-WRITE-F180-COUNT                 
           END-IF.                                                              
                                                                                
           IF K690-AMT-SC-SPR-PREMIUM  NOT = ZEROES                             
              MOVE SPACES              TO   F180-GL-REC                         
              MOVE ZEROES              TO   F180-AMT-GST-RATE                   
              IF F120-CDE-PAYMENT-TYPE = 'VRL'                                  
                 IF F120-CDE-VALID = 'H'                                        
                    MOVE 'SE'                TO   F180-JR-SC                    
                 ELSE                                                           
                    MOVE 'SF'                TO   F180-JR-SC                    
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'SA'                TO   F180-JR-SC                       
              END-IF                                                            
              MOVE WS-F180-TRANS-YY    TO   F180-JR-YY F180-TRANS-YY            
              MOVE WS-F180-TRANS-MM    TO   F180-JR-MM F180-TRANS-MM            
              MOVE WS-F180-TRANS-DD    TO   F180-TRANS-DD                       
              MOVE WS-F180-PERIOD      TO   F180-PERIOD                         
              MOVE F120-NUM-SCH-ACCT   TO   F180-NUM-SCH-ACC                    
              COMPUTE F180-CR-AMT = 0 - WS-AMT-BAL-SPR                          
                                                                                
              MOVE SPACES              TO   WS-ACC-NO1                          
              MOVE '0'                 TO   WS-ACC-LEAD                         
              MOVE '57'                TO   WS-ACC-FILLER                       
              MOVE '41CZ01'            TO   WS-ACC-INTERNAL                     
              IF K690-CDE-TYP-ACTUSE = '01' AND                                 
                 K690-CDE-MODL-DESG  = '74'                                     
                 MOVE '4046'           TO   WS-ACC-GL                           
                 MOVE 'SOC-AMORT OF BAL SC-SPR PREMIUM CCA'                     
                                       TO   F180-TRANS-DESC                     
                 ADD   1               TO   WS-CR-AMORT-SPR-CCA                 
                 ADD F180-CR-AMT       TO   WS-CR-AMORT-AMT-SPR-CCA             
                 MOVE WS-ACC-NO1       TO   WS-CR-GL-SPR-CCA                    
                                               F180-ACC-NO1                     
              ELSE                                                              
                 MOVE '4041'           TO   WS-ACC-GL                           
                 MOVE 'SOC-AMORT OF BAL SC-SPR  PREMIUM'                        
                                       TO   F180-TRANS-DESC                     
                 ADD   1               TO   WS-CR-AMORT-SPR                     
                 ADD F180-CR-AMT       TO   WS-CR-AMORT-AMT-SPR                 
                 MOVE WS-ACC-NO1       TO   WS-CR-GL-SPR                        
                                            F180-ACC-NO1                        
              END-IF                                                            
                                                                                
              WRITE F180-GL-REC                                                 
              ADD 1                    TO   WS-WRITE-F180-COUNT                 
           END-IF.                                                              
                                                                                
           IF K690-AMT-ELDERLY-PREMIUM NOT = ZEROES                             
              MOVE SPACES              TO   F180-GL-REC                         
              MOVE ZEROES              TO   F180-AMT-GST-RATE                   
              IF F120-CDE-PAYMENT-TYPE = 'VRL'                                  
                 IF F120-CDE-VALID = 'H'                                        
                    MOVE 'SE'                TO   F180-JR-SC                    
                 ELSE                                                           
                    MOVE 'SF'                TO   F180-JR-SC                    
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'SA'                TO   F180-JR-SC                       
              END-IF                                                            
              MOVE WS-F180-TRANS-YY    TO   F180-JR-YY F180-TRANS-YY            
              MOVE WS-F180-TRANS-MM    TO   F180-JR-MM F180-TRANS-MM            
              MOVE WS-F180-TRANS-DD    TO   F180-TRANS-DD                       
              MOVE WS-F180-PERIOD      TO   F180-PERIOD                         
              MOVE F120-NUM-SCH-ACCT   TO   F180-NUM-SCH-ACC                    
              COMPUTE F180-CR-AMT = 0 - WS-AMT-BAL-ELD                          
                                                                                
              MOVE SPACES              TO   WS-ACC-NO1                          
              MOVE '0'                 TO   WS-ACC-LEAD                         
              MOVE '57'                TO   WS-ACC-FILLER                       
              MOVE '407400'            TO   WS-ACC-INTERNAL                     
              IF K690-CDE-TYP-ACTUSE = '01' AND                                 
                 K690-CDE-MODL-DESG  = '74'                                     
                 MOVE '4046'           TO   WS-ACC-GL                           
                 MOVE 'SOC-AMORT OF BAL ELDERLY PREMIUM CCA'                    
                                       TO   F180-TRANS-DESC                     
                 ADD   1               TO   WS-CR-AMORT-ELD-CCA                 
                 ADD F180-CR-AMT       TO   WS-CR-AMORT-AMT-ELD-CCA             
                 MOVE WS-ACC-NO1       TO   WS-CR-GL-ELD-CCA                    
                                            F180-ACC-NO1                        
              ELSE                                                              
                 MOVE '4041'           TO   WS-ACC-GL                           
                 MOVE 'SOC-AMORT OF BAL ELDERLY PREMIUM'                        
                                       TO   F180-TRANS-DESC                     
                 ADD   1               TO   WS-CR-AMORT-ELD                     
                 ADD F180-CR-AMT       TO   WS-CR-AMORT-AMT-ELD                 
                 MOVE WS-ACC-NO1       TO   WS-CR-GL-ELD                        
                                            F180-ACC-NO1                        
              END-IF                                                            
                                                                                
              WRITE F180-GL-REC                                                 
              ADD 1                    TO   WS-WRITE-F180-COUNT                 
           END-IF.                                                              
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    CORRECTING CALENDAR DATE TO FINANCIAL YEAR PERIOD          *         
      *****************************************************************         
       600-CHK-PERIOD.                                                          
                                                                                
                                                                                
           IF WS-F180-TRANS-MM   < 4                                            
              ADD 9  WS-F180-TRANS-MM  GIVING WS-F180-PERIOD.                   
                                                                                
           IF WS-F180-TRANS-MM   > 3                                            
              SUBTRACT 3 FROM WS-F180-TRANS-MM  GIVING WS-F180-PERIOD.          
                                                                                
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ****************************************************************          
      *    MOVING THE APPROPRIATE MONTH END DATE ACCORDING TO        *          
      *    CALENDAR MONTH AND YEAR                                   *          
      ****************************************************************          
       610-CHK-TRAN-DTE.                                                        
           DIVIDE WS-F180-TRANS-CCYY  BY 4 GIVING WS-YEAR REMAINDER             
                                            WS-LEAP-YEAR.                       
                                                                                
           MOVE WS-F180-TRANS-MM TO WS-MONTH.                                   
           IF WS-F180-TRANS-MM = 2                                              
              IF WS-LEAP-YEAR = 0                                               
                 MOVE 29 TO WS-F180-TRANS-DD                                    
              ELSE                                                              
                 MOVE 28 TO WS-F180-TRANS-DD                                    
           ELSE                                                                 
               IF CHECK-MM                                                      
                  MOVE 30 TO WS-F180-TRANS-DD                                   
               ELSE                                                             
                  MOVE 31 TO WS-F180-TRANS-DD.                                  
                                                                                
                                                                                
       610-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***************************************************************           
      *     PRINT REPORT OF RECORDS READ AND WRITTEN TO CLEARING    *           
      *     FILE BP13F120 AND GENERAL LEDGER BP13F180 RESPECTIVELY. *           
      ***************************************************************           
       700-PRINT-REPORT.                                                        
           ADD 1 TO WS-PAGE-COUNT.                                              
                                                                                
           MOVE WS-READ-F120-COUNT    TO L39K-READ-F120-CNT.                    
           MOVE WS-WRITE-F180-COUNT   TO L39K-WRITE-F180-CNT.                   
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
           MOVE CORRESPONDING WS-CURRENT-DATE TO L39K-EDITED-DATE.              
           MOVE WS-PAGE-COUNT                 TO L39K-PAGE.                     
                                                                                
           WRITE BP13L39K-REPORT FROM WS-TITLE-1 AFTER PAGE.                    
           WRITE BP13L39K-REPORT FROM WS-TITLE-2 AFTER 2.                       
           WRITE BP13L39K-REPORT FROM WS-TITLE-3 AFTER 2.                       
           WRITE BP13L39K-REPORT FROM WS-HEADER-LINE-1 AFTER 4.                 
           WRITE BP13L39K-REPORT FROM WS-HEADER-LINE-2 AFTER 2.                 
           WRITE BP13L39K-REPORT FROM WS-SUMMARY-HEADER-1 AFTER 4.              
           WRITE BP13L39K-REPORT FROM WS-SUMMARY-HEADER-2 AFTER 2.              
                                                                                
      **PRINT TOTAL FOR DEFER INCOME ORIGINAL                                   
           INITIALIZE WS-SUMMARY-DETAIL-1.                                      
           MOVE 'SELLING PRICE'      TO  L39K-AMORT-DESC1.                      
           MOVE WS-DR-GL-DEFER       TO  L39K-DEFER-GL.                         
           MOVE WS-DR-AMORT-DEFER    TO  L39K-DR-CASES.                         
           MOVE WS-DR-AMORT-AMT-DEFER TO  L39K-DR-DEFER-DEBIT.                  
           IF L39K-DEFER-GL NOT = SPACES                                        
              WRITE BP13L39K-REPORT FROM WS-SUMMARY-DETAIL-1 AFTER 1            
           END-IF.                                                              
                                                                                
      **PRINT TOTAL FOR DEFER INCOME ORIGINAL - CCA                             
           INITIALIZE WS-SUMMARY-DETAIL-1.                                      
           MOVE 'CCA-SELL PRICE '         TO  L39K-AMORT-DESC1.                 
           MOVE WS-DR-GL-DEFER-CCA        TO  L39K-DEFER-GL.                    
           MOVE WS-DR-AMORT-DEFER-CCA     TO  L39K-DR-CASES.                    
           MOVE WS-DR-AMORT-AMT-DEFER-CCA TO  L39K-DR-DEFER-DEBIT.              
           IF L39K-DEFER-GL NOT = SPACES                                        
              WRITE BP13L39K-REPORT FROM WS-SUMMARY-DETAIL-1 AFTER 1            
           END-IF.                                                              
                                                                                
                                                                                
      **PRINT TOTAL FOR FSH                                                     
           INITIALIZE WS-SUMMARY-DETAIL-1.                                      
           MOVE 'FSH SP'                  TO  L39K-AMORT-DESC1.                 
           MOVE WS-DR-GL-DEFER-FSH        TO  L39K-DEFER-GL.                    
           MOVE WS-DR-AMORT-DEFER-FSH     TO  L39K-DR-CASES.                    
           MOVE WS-DR-AMORT-AMT-DEFER-FSH TO  L39K-DR-DEFER-DEBIT.              
           IF L39K-DEFER-GL NOT = SPACES                                        
              WRITE BP13L39K-REPORT FROM WS-SUMMARY-DETAIL-1 AFTER 1            
           END-IF.                                                              
                                                                                
                                                                                
      **PRINT BAL DEFER FOR THE SPLIT AMOUNT                                    
           INITIALIZE WS-SUMMARY-DETAIL-2.                                      
           MOVE 'FLX            '    TO  L39K-AMORT-DESC.                       
           MOVE WS-CR-GL-DEFER       TO  L39K-AMORT-GL.                         
           MOVE WS-CR-AMORT-DEFER    TO  L39K-CR-AMORT-CASES.                   
           MOVE WS-CR-AMORT-AMT-DEFER    TO L39K-CR-AMORT-CREDIT.               
           IF L39K-AMORT-GL NOT = SPACES                                        
              WRITE BP13L39K-REPORT FROM WS-SUMMARY-DETAIL-2 AFTER 1            
           END-IF.                                                              
                                                                                
      **PRINT BAL DEFER FOR THE SPLIT AMOUNT - CCA                              
           INITIALIZE WS-SUMMARY-DETAIL-2.                                      
           MOVE 'CCA            '         TO  L39K-AMORT-DESC.                  
           MOVE WS-CR-GL-DEFER-CCA        TO  L39K-AMORT-GL.                    
           MOVE WS-CR-AMORT-DEFER-CCA     TO  L39K-CR-AMORT-CASES.              
           MOVE WS-CR-AMORT-AMT-DEFER-CCA TO L39K-CR-AMORT-CREDIT.              
           IF L39K-AMORT-GL NOT = SPACES                                        
              WRITE BP13L39K-REPORT FROM WS-SUMMARY-DETAIL-2 AFTER 1            
           END-IF.                                                              
                                                                                
      **PRINT BAL DEFER FOR THE FSH SPLIT AMOUNT                                
           INITIALIZE WS-SUMMARY-DETAIL-2.                                      
           MOVE 'FSH SP         '     TO  L39K-AMORT-DESC.                      
           MOVE WS-CR-GL-DEFER-FSH    TO  L39K-AMORT-GL.                        
           MOVE WS-CR-AMORT-DEFER-FSH TO  L39K-CR-AMORT-CASES.                  
           MOVE WS-CR-AMORT-AMT-DEFER-FSH    TO L39K-CR-AMORT-CREDIT.           
           IF L39K-AMORT-GL NOT = SPACES                                        
              WRITE BP13L39K-REPORT FROM WS-SUMMARY-DETAIL-2 AFTER 1            
           END-IF.                                                              
                                                                                
      **PRINT BAL FOR THE SINGLES PREM                                          
           INITIALIZE WS-SUMMARY-DETAIL-2.                                      
           MOVE 'SINGLE PREMIUM  '   TO  L39K-AMORT-DESC.                       
           MOVE WS-CR-GL-SGP         TO  L39K-AMORT-GL.                         
           MOVE WS-CR-AMORT-SGP      TO  L39K-CR-AMORT-CASES.                   
           MOVE WS-CR-AMORT-AMT-SGP      TO L39K-CR-AMORT-CREDIT.               
           IF L39K-AMORT-GL NOT = SPACES                                        
              WRITE BP13L39K-REPORT FROM WS-SUMMARY-DETAIL-2 AFTER 1            
           END-IF.                                                              
                                                                                
      **PRINT BAL FOR THE SINGLES PREM - CCA                                    
           INITIALIZE WS-SUMMARY-DETAIL-2.                                      
           MOVE 'CCA-SINGLE PREM '   TO  L39K-AMORT-DESC.                       
           MOVE WS-CR-GL-SGP-CCA     TO  L39K-AMORT-GL.                         
           MOVE WS-CR-AMORT-SGP-CCA  TO  L39K-CR-AMORT-CASES.                   
           MOVE WS-CR-AMORT-AMT-SGP-CCA  TO L39K-CR-AMORT-CREDIT.               
           IF L39K-AMORT-GL NOT = SPACES                                        
              WRITE BP13L39K-REPORT FROM WS-SUMMARY-DETAIL-2 AFTER 1            
           END-IF.                                                              
                                                                                
      **PRINT BAL FOR THE SC/SPR  PREM                                          
           INITIALIZE WS-SUMMARY-DETAIL-2.                                      
           MOVE 'SC/SPR PREMIUM '    TO  L39K-AMORT-DESC.                       
           MOVE WS-CR-GL-SPR         TO  L39K-AMORT-GL.                         
           MOVE WS-CR-AMORT-SPR      TO  L39K-CR-AMORT-CASES.                   
           MOVE WS-CR-AMORT-AMT-SPR      TO L39K-CR-AMORT-CREDIT.               
           IF L39K-AMORT-GL NOT = SPACES                                        
              WRITE BP13L39K-REPORT FROM WS-SUMMARY-DETAIL-2 AFTER 1            
           END-IF.                                                              
                                                                                
      **PRINT BAL FOR THE SC/SPR  PREM - CCA                                    
           INITIALIZE WS-SUMMARY-DETAIL-2.                                      
           MOVE 'CCA-SC/SPR PREM'    TO  L39K-AMORT-DESC.                       
           MOVE WS-CR-GL-SPR-CCA     TO  L39K-AMORT-GL.                         
           MOVE WS-CR-AMORT-SPR-CCA  TO  L39K-CR-AMORT-CASES.                   
           MOVE WS-CR-AMORT-AMT-SPR-CCA  TO L39K-CR-AMORT-CREDIT.               
           IF L39K-AMORT-GL NOT = SPACES                                        
              WRITE BP13L39K-REPORT FROM WS-SUMMARY-DETAIL-2 AFTER 1            
           END-IF.                                                              
                                                                                
      **PRINT BAL FOR THE ELDERLY PREM                                          
           INITIALIZE WS-SUMMARY-DETAIL-2.                                      
           MOVE 'ELDERLY PREMIUM '   TO  L39K-AMORT-DESC.                       
           MOVE WS-CR-GL-ELD         TO  L39K-AMORT-GL.                         
           MOVE WS-CR-AMORT-ELD      TO  L39K-CR-AMORT-CASES.                   
           MOVE WS-CR-AMORT-AMT-ELD      TO L39K-CR-AMORT-CREDIT.               
           IF L39K-AMORT-GL NOT = SPACES                                        
              WRITE BP13L39K-REPORT FROM WS-SUMMARY-DETAIL-2 AFTER 1            
           END-IF.                                                              
                                                                                
      **PRINT BAL FOR THE ELDERLY PREM - CCA                                    
           INITIALIZE WS-SUMMARY-DETAIL-2.                                      
           MOVE 'CCA-ELD PREM    '       TO  L39K-AMORT-DESC.                   
           MOVE WS-CR-GL-ELD-CCA         TO  L39K-AMORT-GL.                     
           MOVE WS-CR-AMORT-ELD-CCA      TO  L39K-CR-AMORT-CASES.               
           MOVE WS-CR-AMORT-AMT-ELD-CCA  TO L39K-CR-AMORT-CREDIT.               
           IF L39K-AMORT-GL NOT = SPACES                                        
              WRITE BP13L39K-REPORT FROM WS-SUMMARY-DETAIL-2 AFTER 1            
           END-IF.                                                              
                                                                                
                                                                                
      *COMPUTE TOTAL DEBIT AND CREDIT AMOUNTS                                   
                                                                                
           COMPUTE WS-DR-TOT =  WS-DR-AMORT-AMT-DEFER +                         
                                WS-DR-AMORT-AMT-DEFER-CCA +                     
                                WS-DR-AMORT-AMT-DEFER-FSH.                      
                                                                                
           COMPUTE WS-CR-TOT =  WS-CR-AMORT-AMT-DEFER +                         
                                WS-CR-AMORT-AMT-DEFER-CCA +                     
                                WS-CR-AMORT-AMT-DEFER-FSH +                     
                                WS-CR-AMORT-AMT-SGP +                           
                                WS-CR-AMORT-AMT-SGP-CCA +                       
                                WS-CR-AMORT-AMT-SPR +                           
                                WS-CR-AMORT-AMT-SPR-CCA +                       
                                WS-CR-AMORT-AMT-ELD +                           
                                WS-CR-AMORT-AMT-ELD-CCA.                        
                                                                                
           MOVE WS-CR-TOT         TO L39K-CR-TOT.                               
           MOVE WS-DR-TOT         TO L39K-DR-TOT.                               
                                                                                
      *COMPUTE TOTAL CASES OF DEBIT AND CREDIT                                  
                                                                                
           COMPUTE WS-CR-CASES = WS-CR-AMORT-DEFER +                            
                                 WS-CR-AMORT-DEFER-CCA +                        
                                 WS-CR-AMORT-DEFER-FSH +                        
                                 WS-CR-AMORT-SGP   +                            
                                 WS-CR-AMORT-SGP-CCA +                          
                                 WS-CR-AMORT-SPR +                              
                                 WS-CR-AMORT-SPR-CCA +                          
                                 WS-CR-AMORT-ELD +                              
                                 WS-CR-AMORT-ELD-CCA.                           
                                                                                
           COMPUTE WS-DR-CASES = WS-DR-AMORT-DEFER +                            
                                 WS-DR-AMORT-DEFER-CCA +                        
                                 WS-DR-AMORT-DEFER-FSH.                         
                                                                                
           MOVE WS-CR-CASES       TO L39K-CR-CASES-TOT.                         
           MOVE WS-DR-CASES       TO L39K-DR-CASES-TOT.                         
                                                                                
           WRITE BP13L39K-REPORT FROM WS-FOOTER-LINE-1 AFTER 4.                 
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
      ***************************************************************           
      *     CLOSE ALL FILES AND STOP RUN.                           *           
      ***************************************************************           
       999-CLOSE-FILES.                                                         
           DISPLAY '  PROGRAM : BP13C39K      : '.                              
           DISPLAY 'RECORDS READ FROM BP13F120: ' WS-READ-F120-COUNT.           
           DISPLAY 'RECORDS WRITTEN IN BP13F180: ' WS-WRITE-F180-COUNT          
           DISPLAY 'RECORDS FOUND IN BP13K690: ' K690-FND-CNT.                  
           DISPLAY 'RECORDS FOUND WITH GRANTS: ' WS-WITH-GRANT.                 
                                                                                
           CLOSE BP13F120                                                       
                 BP13F180                                                       
                 BP13K690                                                       
                 SY02F001                                                       
                 BP13L39K.                                                      
                                                                                
                                                                                
           IF K690-STATUS NOT = 00 AND 97                                       
              MOVE K690-STATUS          TO RETURN-CODE                          
              DISPLAY 'ERROR CLOSING BM06K690 FILE ' K690-STATUS                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
