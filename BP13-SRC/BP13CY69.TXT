       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY69.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *                                                                         
      ************************************************************              
      *                   SOC SYSTEM (SOC)                       *              
      *==========================================================*              
      * DATE WRITTEN      : 02 MAY 2008                          *              
      *                                                          *              
      * 1. OBJECTIVE      : BTO APPLICATION BY NRIC MATCH WITH   *              
      *                   : SMS, DTE-SALES > DTE-REQUEST, COUNT  *              
      *                   : AS BOUGHT A FLAT.                    *              
      *                   : IF NOT FOUND, COUNT AS NEW REQUEST.  *              
      *                                                          *              
      * 2. INPUT          : BP13F595                             *              
      *                   : BP13K813                             *              
      *                   : AB03K030 - AB03.K030.SALEMAST        *              
      *                   : AB03K030 - AB03.K030.SALEMAST        *              
      *                                                          *              
      * 3. OUTPUT         : BP13F557                             *              
      *                   : P13F595N                             *              
      *==========================================================*              
      *  MODIFICATIONS:                                          *              
      *                                                          *              
      *  CHGE-NO  DATE       OIC  DESCRIPTION                    *              
      *  -------- ---------- ---- ------------------------------ *              
      *  BP133340 02/05/2008 JB8  NEW PROGRAM.(BP13C993)         *              
      *  BP133434 21/11/2008 JB8  RECTIFY NOTFND OUTPUT FILE     *              
      *  BP135340 16/05/2014 IMC1 REPLACE BP13K816 WITH BP13K813 *              
      *  BP135615 23/12/2014 IMC1 EXPAND F557 TO X(400)          *              
      ************************************************************              
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F595     ASSIGN        TO BP13F595.                       
                                                                                
           SELECT AB03K080     ASSIGN        TO AB03K080                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K080-KEY                        
                               FILE STATUS   IS WS-K080-STATUS.                 
                                                                                
           SELECT AB03K030     ASSIGN        TO AB03K030                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K030-NUM-HDB-REF                
                               FILE STATUS   IS WS-K030-STATUS.                 
                                                                                
           SELECT BP13K813     ASSIGN        TO BP13K813                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K813-KEY-FLD                    
                               FILE STATUS   IS WS-K813-STATUS.                 
                                                                                
           SELECT BP13F557     ASSIGN        TO BP13F557.                       
           SELECT P13F595F     ASSIGN        TO P13F595F.                       
           SELECT P13F595N     ASSIGN        TO P13F595N.                       
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F595                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       COPY BP13F595.                                                           
                                                                                
       FD  AB03K080                                                             
                               RECORD CONTAINS 50 CHARACTERS.                   
       COPY AB03K080.                                                           
                                                                                
       FD  AB03K030                                                             
                               RECORD CONTAINS 800 CHARACTERS.                  
       COPY AB03K030.                                                           
                                                                                
       FD  BP13K813                                                             
                               RECORD CONTAINS 1000 CHARACTERS.                 
       COPY BP13K813.                                                           
                                                                                
       FD  BP13F557                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 400 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       COPY BP13F557.                                                           
                                                                                
       FD  P13F595F                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595F-REC.                                                        
           05  FILLER                    PIC X(500).                            
                                                                                
       FD  P13F595N                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595N-REC.                                                        
           05  FILLER                    PIC X(500).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05  WS-K080-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K030-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K813-STATUS            PIC 9(2)  VALUE ZEROES.                
                                                                                
       01  END-OF-FILE-FLAGS.                                                   
           05  WS-F595-EOF               PIC X     VALUE 'N'.                   
           05  WS-K080-EOF               PIC X     VALUE 'N'.                   
                                                                                
       01  COUNTERS.                                                            
           05  WS-F595-READ-CNT          PIC 9(6)  VALUE ZEROES.                
           05  WS-F557-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-595F-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-595N-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-NRIC-LIST-CNT          PIC 9(1)  VALUE ZEROES.                
           05  WS-CNT                    PIC 9(1)  VALUE ZERO.                  
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURRENT-DATE1          PIC X(8) VALUE SPACES.                 
           05  WS-CURRENT-TIME           PIC X(9) VALUE SPACES.                 
               05  WS-TIME REDEFINES WS-CURRENT-TIME.                           
                   10  WS-HHMMSSMS       PIC X(8).                              
                   10  WS-GM             PIC X(1).                              
           05  FILLER                    PIC X(4) VALUE SPACES.                 
                                                                                
       01  TEMPORARY-VARIABLES.                                                 
           05  WS-NRIC OCCURS 2 TIMES.                                          
               10 WS-NRIC-LIST           PIC X(9)  VALUE SPACES.                
           05  WS-RUN-DATE               PIC X(10) VALUE SPACES.                
           05  WS-NRIC-FOUND             PIC X(1)  VALUE 'N'.                   
           05  WS-K080-REC-PROCESS       PIC X(1)  VALUE 'N'.                   
           05  WS-K080-NRIC-PREV         PIC X(9)  VALUE SPACES.                
           05  WS-FOUND                  PIC X(1)  VALUE 'N'.                   
           05  WS-BUC-FOUND              PIC X(1)  VALUE 'N'.                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
           PERFORM 1000-OPEN-ROUTINE THRU 1000-EXIT.                            
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-F595-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       1000-OPEN-ROUTINE.                                                       
      *************************************************************             
           OPEN INPUT  BP13F595                                                 
                       BP13K813                                                 
                       AB03K080                                                 
                       AB03K030                                                 
                OUTPUT BP13F557                                                 
                       P13F595F                                                 
                       P13F595N.                                                
                                                                                
           IF WS-K080-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K080 OPEN ERROR ' WS-K080-STATUS                     
              MOVE WS-K080-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K030-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K030 OPEN ERROR ' WS-K030-STATUS                     
              MOVE WS-K030-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K813 OPEN ERROR ' WS-K813-STATUS                     
              MOVE WS-K813-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CURRENT-DATE.                    
                                                                                
           STRING WS-CURRENT-DATE1(7:2) '/'                                     
                  WS-CURRENT-DATE1(5:2) '/'                                     
                  WS-CURRENT-DATE1(1:4)                                         
                  DELIMITED BY SIZE                                             
             INTO WS-RUN-DATE                                                   
                                                                                
           PERFORM 1100-READ-BP13F595 THRU 1100-EXIT.                           
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       1100-READ-BP13F595.                                                      
      *************************************************************             
           READ BP13F595                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F595-EOF                         
            NOT AT END                                                          
                ADD 1                    TO WS-F595-READ-CNT                    
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       2000-PROCESS-RECORDS.                                                    
      *************************************************************             
           MOVE SPACES                   TO WS-NRIC-LIST(1)                     
                                            WS-NRIC-LIST(2).                    
                                                                                
           MOVE F595-NUM-NRIC1           TO WS-NRIC-LIST(1).                    
           MOVE 1                        TO WS-NRIC-LIST-CNT.                   
                                                                                
           IF F595-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND                    
              F595-NUM-NRIC2(1:1) NOT = '#'                                     
              MOVE F595-NUM-NRIC2        TO WS-NRIC-LIST(2)                     
              ADD 1                      TO WS-NRIC-LIST-CNT                    
           END-IF.                                                              
                                                                                
           MOVE 'N'                      TO WS-NRIC-FOUND.                      
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1                                   
             UNTIL WS-CNT > WS-NRIC-LIST-CNT                                    
                OR WS-NRIC-FOUND = 'Y'                                          
                   MOVE SPACES           TO BP13F557-REC                        
                                                                                
                   PERFORM 3000-START-AB03K080 THRU 3000-EXIT                   
           END-PERFORM.                                                         
                                                                                
           IF WS-NRIC-FOUND = 'N'                                               
              MOVE SPACES                 TO P13F595N-REC                       
              MOVE BP13F595-REC           TO P13F595N-REC                       
              PERFORM 7000-DECODE-NT    THRU 7000-EXIT                          
              WRITE P13F595N-REC                                                
              ADD 1                       TO WS-595N-WRITE-CNT                  
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-BP13F595 THRU 1100-EXIT.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3000-START-AB03K080.                                                     
      *************************************************************             
           MOVE SPACES                   TO AB03K080-REC.                       
           INITIALIZE                       AB03K080-REC.                       
                                                                                
           MOVE WS-NRIC-LIST(WS-CNT)     TO K080-NUM-UIN                        
                                            WS-K080-NRIC-PREV.                  
           MOVE 'N'                      TO WS-K080-EOF                         
                                            WS-K080-REC-PROCESS.                
                                                                                
           START AB03K080 KEY >= K080-KEY.                                      
                                                                                
           EVALUATE WS-K080-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3100-READNEXT-AB03K080 THRU 3100-EXIT               
                      UNTIL K080-NUM-UIN NOT = WS-K080-NRIC-PREV                
                         OR WS-K080-EOF = 'Y'                                   
                         OR WS-NRIC-FOUND = 'Y'                                 
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
      *             DISPLAY 'NRIC NOT FOUND IN K080: ' K080-NUM-UIN             
               WHEN OTHER                                                       
                    DISPLAY 'AB03K080 START ERROR ' WS-K080-STATUS              
                    DISPLAY 'K080 KEY: ' K080-KEY                               
                    MOVE WS-K080-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3100-READNEXT-AB03K080.                                                  
      *************************************************************             
           READ AB03K080 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                 TO WS-K080-EOF                         
           END-READ.                                                            
                                                                                
           EVALUATE WS-K080-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K080-NUM-UIN = WS-K080-NRIC-PREV                         
                       PERFORM 3500-READ-AB03K030 THRU 3500-EXIT                
                                                                                
                       MOVE 'Y'          TO WS-K080-REC-PROCESS                 
                    ELSE                                                        
                       IF WS-K080-REC-PROCESS NOT = 'Y'                         
                          CONTINUE                                              
      *                   DISPLAY 'NRIC NOT FOUND IN K080: '                    
      *                           WS-K080-NRIC-PREV                             
                       END-IF                                                   
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
      *             DISPLAY 'NRIC NOT FOUND IN K080: ' K080-NUM-UIN             
               WHEN OTHER                                                       
                    DISPLAY 'AB03K080 READ NEXT ERROR ' WS-K080-STATUS          
                    DISPLAY 'K080 KEY: ' K080-KEY                               
                    MOVE WS-K080-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3500-READ-AB03K030.                                                      
      *************************************************************             
           MOVE SPACES                   TO AB03K030-REC.                       
           INITIALIZE                       AB03K030-REC.                       
                                                                                
           MOVE K080-NUM-HDB-REF         TO K030-NUM-HDB-REF.                   
           MOVE 'N'                      TO WS-FOUND.                           
                                                                                
           READ AB03K030.                                                       
                                                                                
           EVALUATE WS-K030-STATUS                                              
              WHEN 00                                                           
                 IF F595-CDE-HOUSEHOLD = 'H' OR 'T'                             
                    IF K030-CDE-SALES-TYP = '6 ' OR '6J' OR '8C' OR '8D'        
                       OR '8F' OR '9B' OR '15' OR '8Q'                          
                       IF K030-CDE-ACCT-STAT = 'C'    AND                       
                         K030-DTE-EXE-SALE-AGRMT(1:6) >= F595-DTE-BALLOT        
                            MOVE 'Y'  TO WS-FOUND                               
                       ELSE                                                     
                         MOVE 'N' TO WS-FOUND                                   
                       END-IF                                                   
                    END-IF                                                      
                 ELSE                                                           
                    IF K030-DTE-SALE-EFF > K030-DTE-APPLN                       
                       IF K030-CDE-ACCT-STAT = 'C'    AND                       
                         K030-DTE-EXE-SALE-AGRMT(1:6) >= F595-DTE-BALLOT        
                         MOVE 'Y' TO WS-FOUND                                   
                       ELSE                                                     
                         MOVE 'N'  TO WS-FOUND                                  
                       END-IF                                                   
                    ELSE                                                        
                       MOVE 'N'  TO WS-FOUND                                    
                    END-IF                                                      
                 END-IF                                                         
                                                                                
                 IF WS-FOUND = 'Y'                                              
                    MOVE SPACES                 TO BP13F557-REC                 
                    MOVE F595-NUM-REGN          TO F557-NUM-REGN                
                    MOVE F595-CDE-HOUSEHOLD     TO F557-NUM-HH                  
                    MOVE F595-CDE-FLAT-TYPE     TO F557-NUM-FLAT-TYPE           
                    MOVE F595-NUM-NRIC1         TO F557-NUM-NRIC1               
                    MOVE K030-NUM-HDB-REF TO                                    
                                        F557-NUM-HDBREF-SMS-HA1                 
                    MOVE K030-CDE-SALES-TYP TO                                  
                                   F557-NUM-SALE-TYPE-SMS-HA1                   
                    MOVE K030-AMT-SELL-PRICE TO F557-AMT-SELLING-PRICE          
                    MOVE K030-DTE-EXE-SALE-AGRMT TO                             
                                   F557-DTE-SALE-EFF-SMS-HA1                    
                                                                                
                    WRITE BP13F557-REC                                          
                    ADD 1                       TO WS-F557-WRITE-CNT            
                    MOVE SPACES                 TO P13F595F-REC                 
                    MOVE BP13F595-REC           TO P13F595F-REC                 
                    WRITE P13F595F-REC                                          
                    ADD 1                       TO WS-595F-WRITE-CNT            
                 ELSE                                                           
                    MOVE SPACES                 TO P13F595N-REC                 
                    MOVE BP13F595-REC           TO P13F595N-REC                 
                                                                                
                    PERFORM 7000-DECODE-NT    THRU 7000-EXIT                    
                                                                                
                    WRITE P13F595N-REC                                          
                    ADD 1                       TO WS-595N-WRITE-CNT            
                 END-IF                                                         
                                                                                
                 MOVE 'Y'                       TO WS-NRIC-FOUND                
              WHEN 10                                                           
              WHEN 23                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'AB03K030 READ ERROR ' WS-K030-STATUS                  
                 DISPLAY 'K030 KEY: ' K030-NUM-HDB-REF                          
                 MOVE WS-K030-STATUS  TO RETURN-CODE                            
                                                                                
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ************************************************************              
       7000-DECODE-NT.                                                          
      ************************************************************              
                                                                                
           MOVE SPACES                          TO BP13K813-REC.                
           INITIALIZE                              BP13K813-REC.                
                                                                                
           MOVE F595-CDE-NT1                    TO K813-NUM-ZONE.               
                                                                                
           START BP13K813 KEY >= K813-KEY-FLD                                   
                                                                                
           IF WS-K813-STATUS = 00                                               
              READ BP13K813 NEXT RECORD                                         
              IF K813-NUM-ZONE = F595-CDE-NT1                                   
                 IF K813-NUM-FLAT-TYPE(1:1) = '3' OR '4' OR '5'                 
                    MOVE SPACE           TO K813-NUM-FLAT-TYPE(2:1)             
                 END-IF                                                         
                 STRING K813-CDE-NT K813-NUM-FLAT-TYPE                          
                    DELIMITED BY SIZE INTO F595-CDE-NT1                         
              ELSE                                                              
                 DISPLAY 'BP13K813 REC NOT FND : ' WS-K813-STATUS               
                         ' NT=' F595-CDE-NT1                                    
              END-IF                                                            
           ELSE                                                                 
                 DISPLAY 'ERROR READING - BP13K813 : ' WS-K813-STATUS           
                         ' NT=' K813-NUM-ZONE                                   
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       9000-CLOSE-ROUTINE.                                                      
      *************************************************************             
           DISPLAY '   '.                                                       
           DISPLAY 'RUN DATE : ' WS-RUN-DATE.                                   
           DISPLAY '*--------- BP13CY69 CONTROL TOTAL ---------*'.              
           DISPLAY '   '.                                                       
           DISPLAY 'NO. OF READS FROM F595   : ' WS-F595-READ-CNT.              
           DISPLAY 'RECS WRITE BP13F557      : ' WS-F557-WRITE-CNT.             
           DISPLAY 'RECS WRITE P13F595F      : ' WS-595F-WRITE-CNT.             
           DISPLAY 'RECS WRITE P13F595N      : ' WS-595N-WRITE-CNT.             
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F595                                                       
                 AB03K080                                                       
                 AB03K030                                                       
                 BP13K813                                                       
                 BP13F557                                                       
                 P13F595F                                                       
                 P13F595N.                                                      
                                                                                
           IF WS-K080-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K080 CLOSE ERROR ' WS-K080-STATUS                    
              MOVE WS-K080-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K030-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K030 CLOSE ERROR ' WS-K030-STATUS                    
              MOVE WS-K030-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K813 CLOSE ERROR ' WS-K813-STATUS                    
              MOVE WS-K813-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
