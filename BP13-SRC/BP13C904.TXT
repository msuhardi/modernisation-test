       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C904.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  23 AUG 2017.                                              
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  REPORT ON DEATH-CASES AND RENUNCIATION OF     *                
      *          PR CASES (EXCLUDE CANCELLED AND W/ KEY ISSD)  *                
      *      2.  OUTPUT 2 FILES, SERS AND NON-SERS             *                
      *          P13L904A - NON-SERS CASES                     *                
      *          P13L904B - SERS CASES                         *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP136843 FNP1 230817  NEW PROGRAM                      *                
      *========================================================*                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F820 ASSIGN TO BP13F820.                                  
                                                                                
           SELECT BM06K110     ASSIGN        TO BM06K110                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K110-KEY-FLD                    
                               FILE STATUS   IS WS-K110-STATUS.                 
                                                                                
           SELECT BP13K800     ASSIGN        TO BP13K800                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K800-NUM-REGN                   
                               FILE STATUS   IS WS-K800-STATUS.                 
                                                                                
           SELECT BP13K022     ASSIGN        TO BP13K022                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K022-KEY-FLD                    
                               ALTERNATE KEY IS K022-NUM-REGN                   
                               FILE STATUS   IS WS-K022-STATUS.                 
                                                                                
           SELECT BP13K024     ASSIGN        TO BP13K024                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K024-KEY-FLD                    
                               FILE STATUS   IS WS-K024-STATUS.                 
                                                                                
           SELECT P13L904A    ASSIGN        TO P13L904A.                        
           SELECT P13L904B    ASSIGN        TO P13L904B.                        
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F820                                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       COPY BP13F820.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K024.                                                           
                                                                                
       FD  P13L904A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORD IS STANDARD.                                            
       01  P13L904A-REC        PIC X(300).                                      
                                                                                
       FD  P13L904B                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORD IS STANDARD.                                            
       01  P13L904B-REC        PIC X(300).                                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS       PIC 9(2)    VALUE ZEROES.                   
           05  WS-K110-STATUS       PIC 9(2)    VALUE ZEROES.                   
           05  WS-K022-STATUS       PIC 9(2)    VALUE ZEROES.                   
           05  WS-K024-STATUS       PIC 9(02)   VALUE ZEROES.                   
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F820-EOF          PIC X       VALUE 'N'.                      
           05  WS-K800-FND          PIC X       VALUE 'N'.                      
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F820-READ          PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K110-FND          PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K800-FND          PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K820-READ         PIC 9(7)    VALUE ZEROES.                   
           05 WS-L904A-WRITE        PIC 9(7)    VALUE ZEROES.                   
           05 WS-L904B-WRITE        PIC 9(7)    VALUE ZEROES.                   
           05 WS-REC-SNA            PIC 9(7)    VALUE ZEROES.                   
           05 WS-REC-SNB            PIC 9(7)    VALUE ZEROES.                   
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-DTE-RENUN         PIC X(8)    VALUE SPACES.                   
           05  WS-DTE-DEATH         PIC X(8)    VALUE SPACES.                   
                                                                                
       01  WS-L904-REC.                                                         
            05  L904-NUM-SN              PIC ZZZZZZ9.                           
            05  FILLER1                  PIC X(01)  VALUE SPACE.                
            05  L904-NUM-REGN            PIC X(08)  VALUE SPACES.               
            05  FILLER2                  PIC X(01)  VALUE SPACE.                
            05  L904-HDB-REF             PIC X(11)  VALUE SPACES.               
            05  FILLER18                 PIC X(01)  VALUE SPACE.                
            05  L904-NUM-ZONE            PIC X(04)  VALUE SPACES.               
            05  FILLER3                  PIC X(01)  VALUE SPACE.                
            05  L904-NUM-NT              PIC X(03)  VALUE SPACES.               
            05  FILLER4                  PIC X(01)  VALUE SPACE.                
            05  L904-NUM-FT              PIC X(02)  VALUE SPACES.               
            05  FILLER5                  PIC X(01)  VALUE SPACE.                
            05  L904-NRIC                PIC X(09)  VALUE SPACES.               
            05  FILLER6                  PIC X(01)  VALUE SPACE.                
            05  L904-NAME                PIC X(66)  VALUE SPACES.               
            05  FILLER7                  PIC X(01)  VALUE SPACE.                
            05  L904-NUM-STAT            PIC X(09)  VALUE SPACES.               
            05  FILLER8                  PIC X(01)  VALUE SPACE.                
            05  L904-DTE-DEATH           PIC X(10)  VALUE SPACES.               
            05  FILLER9                  PIC X(01)  VALUE SPACE.                
            05  L904-DTE-RENUN           PIC X(10)  VALUE SPACES.               
            05  FILLER10                 PIC X(01)  VALUE SPACE.                
            05  L904-DTE-SALE-AGMT       PIC X(10)  VALUE SPACES.               
            05  FILLER11                 PIC X(01)  VALUE SPACE.                
            05  L904-DTE-FILE-RCVD       PIC X(10)  VALUE SPACES.               
            05  FILLER12                 PIC X(01)  VALUE SPACE.                
            05  L904-DTE-BOOK            PIC X(10)  VALUE SPACES.               
            05  FILLER12A                PIC X(01)  VALUE SPACE.                
            05  L904-DTE-BALLOT          PIC X(08)  VALUE SPACES.               
            05  FILLER13                 PIC X(01)  VALUE SPACE.                
            05  L904-PRE-NUM-OIC         PIC X(07)  VALUE SPACES.               
            05  FILLER14                 PIC X(01)  VALUE SPACE.                
            05  L904-PRE-NME-OIC         PIC X(30)  VALUE SPACES.               
            05  FILLER15                 PIC X(01)  VALUE SPACE.                
            05  L904-POS-NUM-OIC         PIC X(08)  VALUE SPACES.               
            05  FILLER16                 PIC X(01)  VALUE SPACE.                
            05  L904-POS-NME-OIC         PIC X(30)  VALUE SPACES.               
            05  FILLER17                 PIC X(01)  VALUE SPACE.                
            05  L904-STATUS              PIC X(30)  VALUE SPACES.               
            05  FILLER19                 PIC X(01)  VALUE SPACE.                
                                                                                
       01  WS-HEADER.                                                           
            05  FILLER                   PIC X(07)  VALUE 'S/NO.'.              
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(08)  VALUE 'REGN'.               
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(11)  VALUE 'HDB REF'.            
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(04)  VALUE 'ZONE'.               
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(03)  VALUE 'NT'.                 
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(02)  VALUE 'FT'.                 
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(09)  VALUE 'NRIC'.               
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(66)  VALUE 'NAME'.               
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(09)  VALUE 'HA/OCC   '.          
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(10)  VALUE 'DTE DEATH'.          
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(10)  VALUE 'D-RENOUNCE'.         
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(10)  VALUE 'DTE SA'.             
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(10)  VALUE 'FILE RCVD '.         
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(10)  VALUE 'DTE BOOK  '.         
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(08)  VALUE 'DTE BAL'.            
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(07)  VALUE 'PRE OIC'.            
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(30)  VALUE                       
                                                  'NAME PRE OIC'.               
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(08)  VALUE 'POST OIC'.           
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(30)  VALUE                       
                                                  'NAME POST OIC'.              
            05  FILLER                   PIC X(01)  VALUE ';'.                  
            05  FILLER                   PIC X(10)  VALUE 'STATUS  '.           
            05  FILLER                   PIC X(01)  VALUE ';'.                  
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *------------------------------------------------------------             
       0000-MAIN.                                                               
      *------------------------------------------------------------             
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 1100-READ-BP13F820   THRU 1100-EXIT.                         
           PERFORM 1200-WRITE-HEADER    THRU 1200-EXIT.                         
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-F820-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1000-OPEN-ROUTINE.                                                       
      *------------------------------------------------------------             
                                                                                
           OPEN INPUT  BP13F820                                                 
                       BP13K800                                                 
                       BP13K022                                                 
                       BP13K024                                                 
                       BM06K110                                                 
                OUTPUT P13L904A                                                 
                       P13L904B.                                                
                                                                                
           IF WS-K022-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K022 OPEN ERROR ' WS-K022-STATUS                     
              MOVE WS-K022-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K024 OPEN ERROR ' WS-K024-STATUS                     
              MOVE WS-K024-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K800 OPEN ERROR ' WS-K800-STATUS                     
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BM06K110 OPEN ERROR ' WS-K110-STATUS                     
              MOVE WS-K110-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1100-READ-BP13F820.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13F820 AT END MOVE 'Y' TO WS-F820-EOF                         
                GO TO 1100-EXIT.                                                
                                                                                
           ADD 1 TO WS-F820-READ.                                               
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       1200-WRITE-HEADER.                                                       
      *------------------------------------------------------------             
                                                                                
           WRITE P13L904A-REC            FROM WS-HEADER.                        
           WRITE P13L904B-REC            FROM WS-HEADER.                        
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       2000-PROCESS-RECORDS.                                                    
      *------------------------------------------------------------             
                                                                                
           MOVE 'N'                     TO WS-K800-FND.                         
           MOVE SPACES                  TO WS-DTE-RENUN                         
                                           WS-DTE-DEATH.                        
           MOVE BP13F820-REC(235:8)     TO WS-DTE-RENUN.                        
           MOVE BP13F820-REC(243:8)     TO WS-DTE-DEATH.                        
                                                                                
           PERFORM 2100-READ-BP13K800   THRU 2100-EXIT.                         
                                                                                
           IF WS-K800-FND = 'Y'                                                 
              IF K800-NUM-STATUS NOT = 'C'                                      
                 IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                
                    PERFORM 2200-K110-READ THRU 2200-EXIT                       
                 END-IF                                                         
                 IF K110-DTE-KEY-ISSUED = SPACES OR LOW-VALUES OR               
                                          '00000000'                            
                    PERFORM 5000-WRITE-OUTPUT   THRU 5000-EXIT                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-BP13F820   THRU  1100-EXIT.                        
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       2100-READ-BP13K800.                                                      
      *------------------------------------------------------------             
                                                                                
           MOVE SPACES                   TO K800-NUM-REGN.                      
           MOVE F820-NUM-REGN            TO K800-NUM-REGN                       
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    ADD 1 TO CNT-K800-FND                                       
                    MOVE 'Y'             TO WS-K800-FND                         
               WHEN 10                                                          
               WHEN 23                                                          
                    DISPLAY 'RECORD NOT FOUND IN BP13K800, KEY IS: '            
                                  K800-NUM-REGN                                 
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       2200-K110-READ.                                                          
      *------------------------------------------------------------             
                                                                                
           MOVE SPACES                   TO K110-KEY-FLD.                       
           MOVE K800-NUM-SCH-ACC         TO K110-KEY-FLD.                       
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
                    ADD 1 TO CNT-K110-FND                                       
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES       TO K110-DTE-KEY-ISSUED                    
                                         K110-DTE-ACCEPTANCE                    
               WHEN OTHER                                                       
                    DISPLAY 'BM06K110 READ ERROR ' WS-K110-STATUS               
                    DISPLAY 'K110 KEY: ' K110-REGN-NO                           
                    MOVE WS-K110-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------------------------------------------------*         
       4000-GET-OIC.                                                            
      *---------------------------------------------------------------*         
           MOVE F820-NUM-REGN      TO K022-NUM-REGN.                            
                                                                                
           READ BP13K022  KEY IS K022-NUM-REGN.                                 
                                                                                
           EVALUATE WS-K022-STATUS                                              
           WHEN 00                                                              
              IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                       
                 IF K022-NUM-OIC = SPACES OR LOW-VALUES                         
                    MOVE SPACES             TO L904-PRE-NUM-OIC                 
                 ELSE                                                           
                    MOVE K022-NUM-OIC       TO L904-PRE-NUM-OIC                 
                                               K024-KEY-FLD                     
                 END-IF                                                         
              ELSE                                                              
                 MOVE K022-NUM-OIC-APPL  TO L904-PRE-NUM-OIC                    
                                            K024-KEY-FLD                        
              END-IF                                                            
           WHEN 23                                                              
                MOVE SPACES     TO BP13K022-REC                                 
                INITIALIZE         BP13K022-REC                                 
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K022, STATUS IS '                    
                         WS-K022-STATUS                                         
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4100-GET-OIC-NAME-OIC2.                                                  
      *---------------------------------------------------------------*         
                                                                                
           PERFORM 4200-READ-BP13K024 THRU 4200-EXIT.                           
           MOVE K024-NME-OIC       TO L904-PRE-NME-OIC.                         
                                                                                
           IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                          
              MOVE SPACES         TO L904-POS-NUM-OIC                           
                                     L904-POS-NME-OIC                           
           ELSE                                                                 
              MOVE SPACES         TO BP13K024-REC                               
              INITIALIZE             BP13K024-REC                               
              MOVE K022-NUM-OIC   TO K024-NUM-OIC                               
                                                                                
              PERFORM 4200-READ-BP13K024 THRU 4200-EXIT                         
              MOVE K024-NME-OIC   TO L904-POS-NME-OIC                           
              MOVE K022-NUM-OIC   TO L904-POS-NUM-OIC                           
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4200-READ-BP13K024.                                                      
      *---------------------------------------------------------------*         
                                                                                
           READ BP13K024.                                                       
                                                                                
           EVALUATE WS-K024-STATUS                                              
           WHEN 00                                                              
              CONTINUE                                                          
           WHEN 23                                                              
                MOVE SPACES     TO BP13K024-REC                                 
                INITIALIZE         BP13K024-REC                                 
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K024, STATUS IS '                    
                         WS-K024-STATUS                                         
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       5000-WRITE-OUTPUT.                                                       
      *------------------------------------------------------------             
           MOVE SPACES                     TO WS-L904-REC.                      
           MOVE ';'                     TO FILLER1  FILLER2  FILLER3            
                                           FILLER4  FILLER5  FILLER6            
                                           FILLER7  FILLER8  FILLER9            
                                           FILLER10 FILLER11 FILLER12           
                                           FILLER12A                            
                                           FILLER13 FILLER14 FILLER15           
                                           FILLER16 FILLER17 FILLER18           
                                           FILLER19.                            
                                                                                
           MOVE F820-NME-OCCP              TO L904-NAME.                        
           MOVE F820-NUM-REGN              TO L904-NUM-REGN.                    
           MOVE F820-NUM-NRIC              TO L904-NRIC.                        
                                                                                
           IF WS-DTE-RENUN NOT = SPACES AND LOW-VALUES                          
              STRING WS-DTE-RENUN(7:2) '/'                                      
                     WS-DTE-RENUN(5:2) '/'                                      
                     WS-DTE-RENUN(1:4)                                          
                  DELIMITED BY SIZE INTO L904-DTE-RENUN                         
           ELSE                                                                 
              MOVE SPACES             TO L904-DTE-RENUN                         
           END-IF.                                                              
                                                                                
           IF WS-DTE-DEATH NOT = SPACES AND LOW-VALUES                          
              STRING WS-DTE-DEATH(7:2) '/'                                      
                     WS-DTE-DEATH(5:2) '/'                                      
                     WS-DTE-DEATH(1:4)                                          
                  DELIMITED BY SIZE INTO L904-DTE-DEATH                         
           ELSE                                                                 
              MOVE SPACES             TO L904-DTE-DEATH                         
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-SCH-ACC      TO L904-HDB-REF.                          
           MOVE K800-NUM-ZONE         TO L904-NUM-ZONE.                         
           MOVE K800-NUM-NEW-TOWN     TO L904-NUM-NT.                           
           MOVE K800-NUM-FLAT-TYPE    TO L904-NUM-FT.                           
                                                                                
           IF  F820-NUM-NRIC = K800-NUM-NRIC1  OR                               
                               K800-NUM-NRIC2  OR                               
                               K800-NUM-NRIC3  OR                               
                               K800-NUM-NRIC4                                   
               MOVE 'APPLICANT'       TO L904-NUM-STAT                          
           ELSE                                                                 
               MOVE 'OCCUPIER'        TO L904-NUM-STAT                          
           END-IF.                                                              
                                                                                
           IF K800-DTE-SALE-AGMT NOT = SPACES AND LOW-VALUES                    
              STRING K800-DTE-SALE-AGMT(7:2) '/'                                
                     K800-DTE-SALE-AGMT(5:2) '/'                                
                     K800-DTE-SALE-AGMT(1:4)                                    
                   DELIMITED BY SIZE INTO L904-DTE-SALE-AGMT                    
           ELSE                                                                 
              MOVE SPACES              TO L904-DTE-SALE-AGMT                    
           END-IF.                                                              
                                                                                
           IF K800-DTE-FILE-RECEIVED NOT = SPACES AND LOW-VALUES                
              STRING K800-DTE-FILE-RECEIVED(7:2) '/'                            
                     K800-DTE-FILE-RECEIVED(5:2) '/'                            
                     K800-DTE-FILE-RECEIVED(1:4)                                
                   DELIMITED BY SIZE INTO L904-DTE-FILE-RCVD                    
           ELSE                                                                 
              MOVE SPACES              TO L904-DTE-FILE-RCVD                    
           END-IF.                                                              
                                                                                
           MOVE SPACES           TO L904-DTE-BOOK.                              
           IF K110-DTE-ACCEPTANCE NOT = SPACES AND LOW-VALUES AND               
                                        ZEROES                                  
              STRING K110-DTE-ACCEPTANCE(7:2) '/'                               
                     K110-DTE-ACCEPTANCE(5:2) '/'                               
                     K110-DTE-ACCEPTANCE(1:4)                                   
                   DELIMITED BY SIZE INTO L904-DTE-BOOK                         
           ELSE                                                                 
              MOVE SPACES              TO L904-DTE-BOOK                         
           END-IF.                                                              
                                                                                
           IF K800-DTE-BALLOT NOT = SPACES AND LOW-VALUES                       
              STRING K800-DTE-BALLOT(5:2) '/'                                   
                     K800-DTE-BALLOT(1:4)                                       
                   DELIMITED BY SIZE INTO L904-DTE-BALLOT                       
           ELSE                                                                 
              MOVE SPACES              TO L904-DTE-BALLOT                       
           END-IF.                                                              
                                                                                
           MOVE SPACES          TO L904-STATUS                                  
           IF K800-DTE-BALLOT = SPACES OR LOW-VALUES                            
              MOVE 'BEF BALLOT' TO L904-STATUS                                  
           ELSE                                                                 
              IF K800-NUM-SCH-ACC = SPACES OR LOW-VALUES                        
                MOVE 'BEF BOOK  ' TO L904-STATUS                                
              ELSE                                                              
                IF K800-DTE-SALE-AGMT = SPACES OR LOW-VALUES                    
                  MOVE 'BEF SA    ' TO L904-STATUS                              
                ELSE                                                            
                  MOVE 'BEF TP    ' TO L904-STATUS                              
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K800-NUM-ALLO-CAT NOT = 'SRS'                                     
              PERFORM 4000-GET-OIC           THRU 4000-EXIT                     
              PERFORM 4100-GET-OIC-NAME-OIC2 THRU 4100-EXIT                     
              ADD  1                         TO WS-REC-SNA                      
              MOVE WS-REC-SNA                TO L904-NUM-SN                     
              WRITE P13L904A-REC             FROM WS-L904-REC                   
              ADD  1                         TO WS-L904A-WRITE                  
           ELSE                                                                 
              MOVE SPACES                    TO L904-PRE-NUM-OIC                
                                                L904-PRE-NME-OIC                
                                                L904-POS-NUM-OIC                
                                                L904-POS-NME-OIC                
              ADD  1                         TO WS-REC-SNB                      
              MOVE WS-REC-SNB                TO L904-NUM-SN                     
              WRITE P13L904B-REC             FROM WS-L904-REC                   
              ADD  1                         TO WS-L904B-WRITE                  
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------------------------------------------------             
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '***********BP13C904 END OF JOB*******************'.         
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'NO OF RECORDS READ FROM F820    ' WS-F820-READ.             
           DISPLAY 'NO OF RECORDS FOUND IN K800     ' CNT-K800-FND.             
           DISPLAY 'NO OF RECORDS FOUND IN K110     ' CNT-K110-FND.             
           DISPLAY 'NO OF RECORDS WRITTEN TO L904A  ' WS-REC-SNA.               
           DISPLAY 'NO OF RECORDS WRITTEN TO L904B  ' WS-REC-SNB.               
                                                                                
           CLOSE BP13F820                                                       
                 BP13K800                                                       
                 BP13K022                                                       
                 BP13K024                                                       
                 BM06K110                                                       
                 P13L904A                                                       
                 P13L904B.                                                      
                                                                                
           IF WS-K022-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K022 CLOSE ERROR ' WS-K022-STATUS                    
              MOVE WS-K022-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K800 CLOSE ERROR ' WS-K800-STATUS                    
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K024 CLOSE ERROR ' WS-K024-STATUS                    
              MOVE WS-K024-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BM06K110 CLOSE ERROR ' WS-K110-STATUS                    
              MOVE WS-K110-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
