      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.   BP13C345.                                                  
      *AUTHOR.       MCC.                                                       
      *DTE-WRITTEN.  09DEC2002.                                                 
                                                                                
      * ======================================================== *              
      * SYSTEM OF COMMITMENT  (BP13)                             *              
      * ======================================================== *              
      *    OBJECTIVE  :                                          *              
      *            TO MONITOR CHANGES ON S&P INFO                *              
      *            SENT BY HDB TO CPF THROUGH IDNET.             *              
      *                                                          *              
      *     INPUT FILES:                                         *              
      *         1. BP13F445 -                                    *              
      *         2. BP13K440 -                                    *              
      *         3. BP13K800 - SOC MASTER                         *              
      *         4. BP13K670 - BO INFO                            *              
      *         5. BP13K820 - OCCUP FILE                         *              
      *         6. BM06K110 - FIS FILE                           *              
      *         7. BB14K580 - UNTLEASE FILE                      *              
      *         8. BB14K500 - BLKLEASE FILE                      *              
      *                                                          *              
      *     OUTPUT FILES:                                        *              
      *         1. P13F445A - AMENDMENT CASES                    *              
      *                                                          *              
      * -------------------------------------------------------- *              
      * CHG NO   DATE     BY   DESCRIPTION                       *              
      * -------  -------- ---  -----------                       *              
      * BP132249 20021205 MCC  NEW-PROGRAM                       *              
      * BP132441 20040106 CLT  MOVE BUYER LAWYER INFO            *              
      * BP132660 20050131 JF4  TO GET PREMIUM FROM BP13K800      *              
      * BP132672 20050307 JF4  ADD NUM-TOWN                      *              
      * BP132680 20050418 GAN1 REMOVE K440-DTE-APPL-RECEIVED DATA*              
      * BP132680 20050421 GAN1 PUT IN K440-DTE-APPL-RECEIVED DATA*              
      * BP132680 20050428 JF4  TAG AS '2' FOR CASES WHERE        *              
      *                        LOAN-TAG CHGED FROM 'B' TO OTHERS *              
      * BP132698 20050518 JF4  HANDLE 'B' TO 'N' CASES           *              
      * BP132814 20050204 LSB  ADD DBSS                          *              
      * BP132891 20060825 SD10 TO SKIP RECORD IF NOT FOUND IN    *              
      *                        BP13K800                          *              
      * BP133137 20080313 ED1  TO CHECK FOR LEAP YEARS           *              
      * BP133729 20091013 PCL3 TO BYPASS NPL CASES               *              
      * BP134608 20120817 AB9  CATER FOR RESALE LEVY             *              
      * BP134872 20130411 ESD1 TO ADD LEASE HOMMENCEMENT DATE,   *              
      *                        TENURE PERIOD AND STUDIO APT TAG. *              
      * BP134833 20130725 ESD1 TO MOVE 99 ON TENURE PERIOD FOR   *              
      *                        NORMAL FLAT AND 30 FOR STUDIO.    *              
      * BP136088 15012016 KSJ3 TO CATER FOR 2-ROOM FLEXI CASES   *              
      * BP136161 10032016 KSJ3 TO TAG SA/SLF FOR SERS LBS CASES  *              
      * BP136161 22032016 KSJ3 TO INCL BP17K000 TO GET SCH ACCT  *              
      * BP136161 18042016 KSJ3 TO REMOVE SERS ANNOUNCEMENT DATE  *              
      *                        CHECKING                          *              
      * BP136161 23052016 KSJ3 TO REMOVE 30 YEARS LEASE TENURE   *              
      *                        LIMIT FOR LBS STUDIO TAGGING      *              
      * BP136921 04082017 KSJ3 REMOVE CONDITION TO ADD PREM AMT  *              
      * BP137349 26072018 PCL4 TO CATER FOR FRESH START HOUSING  *              
      * BP137772 08042019 DN8  TO CATER FOR COMMENCEMENT DATE FROM              
      *                        BB14K580.                         *              
      * BP137974 13122019 KTM3 TO CATER FOR CENTS FOR WS-SELL    *              
      * BP138321 02122020 KTM3 TO CATER FOR FSH                  *              
      * BP138702 06042021 ZDD3 TO CATER FOR CCA 1F               *              
      ************************************************************              
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F445 ASSIGN TO BP13F445.                                  
                                                                                
           SELECT BP13K440 ASSIGN TO BP13K440                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K440-NUM-REGN                              
                  FILE STATUS     IS WS-K440-STATUS.                            
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS WS-K820-STATUS.                            
                                                                                
           SELECT BP13K670 ASSIGN TO BP13K670                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K670-KEY-FLD                               
                  FILE STATUS     IS WS-K670-STATUS.                            
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K110-KEY-FLD OF K110-REC                   
                  FILE STATUS     IS WS-K110-STATUS.                            
                                                                                
           SELECT BM06K100 ASSIGN TO BM06K100                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K100-KEY-FLD OF K100-REC                   
                  FILE STATUS     IS WS-K100-STATUS.                            
                                                                                
           SELECT BB14K500 ASSIGN TO BB14K500                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K500-KEY-FLD                               
                  FILE STATUS     IS WS-K500-STATUS.                            
                                                                                
           SELECT BB14K580 ASSIGN TO BB14K580                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K580-KEY-FLD                               
                  FILE STATUS     IS WS-K580-STATUS.                            
                                                                                
           SELECT P13F445A ASSIGN TO P13F445A.                                  
                                                                                
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       FILE SECTION.                                                            
                                                                                
       FD   BP13F445                                                            
            RECORDING MODE IS F.                                                
       COPY BP13F445.                                                           
                                                                                
       FD   BP13K440.                                                           
       COPY BP13K440.                                                           
                                                                                
       FD   BP13K800.                                                           
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820.                                                           
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K670.                                                           
       COPY BP13K670.                                                           
                                                                                
       FD   BM06K100.                                                           
       COPY BM06K100.                                                           
                                                                                
       FD   BM06K110.                                                           
       COPY BM06K110.                                                           
                                                                                
       FD   BB14K500.                                                           
       COPY BB14K500.                                                           
                                                                                
       FD   BB14K580.                                                           
       COPY BB14K580.                                                           
                                                                                
       FD   P13F445A                                                            
            RECORDING MODE IS F.                                                
       01   P13F445A-REC            PIC X(1000).                                
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
       01  WS-VARIABLES.                                                        
           05 WS-CURRENT-DATE        PIC X(8)    VALUE SPACES.                  
           05 WS-SQL-CODE            PIC 9(4)    VALUE ZEROS.                   
           05 WS-NUM-NRIC            PIC X(9)    VALUE SPACES.                  
           05 WS-SELL-PRICE          PIC 9(8)V99 VALUE ZEROES.                  
           05 WS-AMT-PREM            PIC S9(7)V99 VALUE ZEROES.                 
           05 WS-NUM-HDB-REF         PIC X(11)   VALUE SPACES.                  
           05 WS-K440-REC.                                                      
              10 FILLER              PIC X(149)  VALUE SPACES.                  
              10 WS-ACTION-TAG       PIC X(1)    VALUE SPACES.                  
              10 FILLER              PIC X(850)  VALUE SPACES.                  
           05 WS-K110-DTE-ACCEPTANCE.                                           
              10 WS-K110-DTE-CCYY    PIC 9(4) VALUE ZEROES.                     
              10 WS-K110-DTE-MM      PIC 9(2) VALUE ZEROES.                     
              10 WS-K110-DTE-DD      PIC 9(2) VALUE ZEROES.                     
                                                                                
       01  WS-FLAGS-AND-FILE-STATUS.                                            
           05 WS-F445-EOF            PIC X(1) VALUE 'N'.                        
           05 WS-K440-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K800-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K820-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K670-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K110-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K100-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K500-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K580-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K440-NOTFND-FLAG    PIC X(1) VALUE 'N'.                        
              88 WS-K440-NOTFOUND                VALUE 'Y'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F445-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-K440-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-K440-NOTFD          PIC 9(5) VALUE ZEROES.                     
           05 WS-K800-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-K800-NOTFD          PIC 9(5) VALUE ZEROES.                     
           05 WS-K800-NPL            PIC 9(5) VALUE ZEROES.                     
           05 WS-K820-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-K820-NOTFD          PIC 9(5) VALUE ZEROES.                     
           05 WS-K670-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-K670-NOTFD          PIC 9(5) VALUE ZEROES.                     
           05 WS-K100-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-K100-NOTFD          PIC 9(5) VALUE ZEROES.                     
           05 WS-K110-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-K110-NOTFD          PIC 9(5) VALUE ZEROES.                     
           05 WS-K500-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-K500-NOTFD          PIC 9(5) VALUE ZEROES.                     
           05 WS-K580-READ           PIC 9(5) VALUE ZEROES.                     
           05 WS-K580-NOTFD          PIC 9(5) VALUE ZEROES.                     
           05 WS-F445A-WRITE         PIC 9(5) VALUE ZEROES.                     
           05 WS-K440-WRITE          PIC 9(5) VALUE ZEROES.                     
           05 WS-K440-REWRITE        PIC 9(5) VALUE ZEROES.                     
           05 WS-K440-DELETE         PIC 9(5) VALUE ZEROES.                     
           05 WS-K440-CANCEL-DELETE  PIC 9(5) VALUE ZEROES.                     
           05 WS-K440-KISSUE-DELETE  PIC 9(5) VALUE ZEROES.                     
                                                                                
       01  WS-BP13C913                   PIC X(08) VALUE 'BP13C913'.    01190014
       01  WS-REMAINDER                  PIC 9     VALUE ZERO.          01190014
       01  WS-QUOTIENT                   PIC 99    VALUE ZEROS.         01200014
       01  WS-DATE.                                                     3029    
           10  WS-CC1                    PIC 99    VALUE ZEROS.         0028    
           10  WS-YY1                    PIC 99    VALUE ZEROS.         0028    
           10  WS-MM1                    PIC 99    VALUE ZEROS.         0028    
           10  WS-DD1                    PIC 99    VALUE ZEROS.         0028    
                                                                                
      *----------------------------------------------------------------*        
      *    LINKAGE VARIABLES USED BY THE CALLED SUB-PROGRAM            *        
      *            (BP13C913)  TO RETRIEVE ADDRESS                     *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH      PIC X(4).                               
               10  WS-LINK-NUM-ACC      PIC X(5).                               
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *----------------------------------------------------------------*        
      *    SERS ALLOC SCHEME CHECKING VARIABLES                        *        
      *----------------------------------------------------------------*        
       COPY SERSCH.                                                             
       01  WS-SERS-VARIABLES.                                                   
           05  WS-ALLOC-SCH             PIC X(03) VALUE SPACES.                 
           05  WS-SCH-FND               PIC X(01) VALUE 'N'.                    
           05  WS-DTE-SERS-ANNOUNCE     PIC X(08) VALUE SPACES.                 
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *---------------*                                                         
       000-MAIN-LOGIC.                                                          
      *---------------*                                                         
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F445    THRU 200-EXIT.                             
            PERFORM 300-PROCESS      THRU 300-EXIT                              
                    UNTIL WS-F445-EOF = 'Y'.                                    
            PERFORM 999-CLOSE-FILES  THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------*                                                         
       100-OPEN-FILES.                                                          
      *---------------*                                                         
           OPEN INPUT  BP13F445                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K670                                                 
                       BM06K110                                                 
                       BM06K100                                                 
                       BB14K500                                                 
                       BB14K580                                                 
                I-O    BP13K440                                                 
                OUTPUT P13F445A.                                                
                                                                                
           IF WS-K440-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING   BP13K440, STATUS ' WS-K440-STATUS        
              MOVE WS-K440-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING   BP13K800, STATUS ' WS-K800-STATUS        
              MOVE WS-K800-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K820-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING   BP13K820, STATUS ' WS-K820-STATUS        
              MOVE WS-K820-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K670-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING   BP13K670, STATUS ' WS-K670-STATUS        
              MOVE WS-K670-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K100-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING   BM06K100, STATUS ' WS-K100-STATUS        
              MOVE WS-K100-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K110-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING   BM06K110, STATUS ' WS-K110-STATUS        
              MOVE WS-K110-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K580-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING   BB14K580, STATUS ' WS-K580-STATUS        
              MOVE WS-K580-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K500-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING   BB14K500, STATUS ' WS-K500-STATUS        
              MOVE WS-K500-STATUS       TO RETURN-CODE.                         
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURRENT-DATE.                  
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------*                                                          
       200-READ-F445.                                                           
      *--------------*                                                          
           READ BP13F445 AT END                                                 
           MOVE 'Y'                      TO WS-F445-EOF                         
           GO TO 200-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-F445-READ.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------*                                                            
       300-PROCESS.                                                             
      *------------*                                                            
           IF F445-NUM-REGN = SPACES OR LOW-VALUES                              
              PERFORM 200-READ-F445    THRU 200-EXIT                            
              GO TO 300-EXIT.                                                   
                                                                                
           MOVE SPACES           TO BP13K440-REC                                
                                    WS-K440-REC                                 
                                    WS-K440-NOTFND-FLAG.                        
           INITIALIZE               WS-K440-REC.                                
           MOVE F445-NUM-REGN    TO K440-NUM-REGN.                              
                                                                                
           READ BP13K440.                                                       
           IF WS-K440-STATUS = 00                                               
              ADD 1               TO WS-K440-READ                               
              MOVE K440-NUM-HDB-REF TO WS-NUM-HDB-REF                           
              IF K440-NUM-LEASE-TENURE IS NOT NUMERIC                           
                 MOVE ZEROES TO K440-NUM-LEASE-TENURE                           
              END-IF                                                            
              MOVE BP13K440-REC   TO WS-K440-REC                                
              PERFORM 400-CHECK-CHANGES THRU 400-EXIT                           
           ELSE                                                                 
              IF WS-K440-STATUS = 23                                            
                 ADD 1            TO WS-K440-NOTFD                              
              ELSE                                                              
                 DISPLAY 'ERROR READING   BP13K440, STATUS '                    
                                                    WS-K440-STATUS              
                 MOVE WS-K440-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
           PERFORM 200-READ-F445    THRU 200-EXIT.                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       400-CHECK-CHANGES.                                                       
      *------------------*                                                      
           IF F445-NUM-LEASE-TENURE IS NOT NUMERIC                              
              MOVE ZEROES TO F445-NUM-LEASE-TENURE                              
           END-IF.                                                              
           MOVE SPACES TO BP13K440-REC.                                         
           INITIALIZE     BP13K440-REC.                                         
                                                                                
           MOVE F445-NUM-REGN              TO K440-NUM-REGN.                    
           MOVE F445-CDE-ACTION-TAG        TO K440-CDE-ACTION-TAG.              
           MOVE F445-NUM-ADJ-FLAT          TO K440-NUM-ADJ-FLAT.                
           MOVE F445-NUM-COMB-FLAT         TO K440-NUM-COMB-FLAT.               
           MOVE F445-NUM-BUYER-RET-HDB-REF TO K440-NUM-BUYER-RET-HDB-REF        
           MOVE F445-AMT-VALUATION         TO K440-AMT-VALUATION.               
           MOVE F445-AMT-CASH-DOWNPAYMENT  TO K440-AMT-CASH-DOWNPAYMENT.        
           MOVE F445-DTE-APPL-RECEIVED     TO K440-DTE-APPL-RECEIVED.           
                                                                                
           MOVE F445-NUM-STATUS-COMPLETION                                      
                                          TO K440-NUM-STATUS-COMPLETION.        
           MOVE F445-DTE-EXPECTED-TOP     TO K440-DTE-EXPECTED-TOP.             
                                                                                
           IF F445-AMT-CPF-GRANT-BUYER1 IS NUMERIC                              
              MOVE F445-AMT-CPF-GRANT-BUYER1                                    
                                           TO K440-AMT-CPF-GRANT-BUYER1         
           ELSE                                                                 
              MOVE ZEROES                  TO K440-AMT-CPF-GRANT-BUYER1         
           END-IF.                                                              
                                                                                
           IF F445-AMT-CPF-GRANT-BUYER2 IS NUMERIC                              
              MOVE F445-AMT-CPF-GRANT-BUYER2                                    
                                           TO K440-AMT-CPF-GRANT-BUYER2         
           ELSE                                                                 
              MOVE ZEROES                  TO K440-AMT-CPF-GRANT-BUYER2         
           END-IF.                                                              
                                                                                
           IF F445-AMT-CPF-GRANT-BUYER3 IS NUMERIC                              
              MOVE F445-AMT-CPF-GRANT-BUYER3                                    
                                           TO K440-AMT-CPF-GRANT-BUYER3         
           ELSE                                                                 
              MOVE ZEROES                  TO K440-AMT-CPF-GRANT-BUYER3         
           END-IF.                                                              
                                                                                
           IF F445-AMT-CPF-GRANT-BUYER4 IS NUMERIC                              
              MOVE F445-AMT-CPF-GRANT-BUYER4                                    
                                           TO K440-AMT-CPF-GRANT-BUYER4         
           ELSE                                                                 
              MOVE ZEROES                  TO K440-AMT-CPF-GRANT-BUYER4         
           END-IF.                                                              
                                                                                
           IF F445-NUM-LEASE-TENURE IS NUMERIC                                  
              MOVE F445-NUM-LEASE-TENURE TO K440-NUM-LEASE-TENURE               
           ELSE                                                                 
              MOVE ZEROES                TO K440-NUM-LEASE-TENURE               
           END-IF.                                                              
                                                                                
           MOVE F445-DTE-LEASE-COMMENCEMENT TO                                  
                K440-DTE-LEASE-COMMENCEMENT                                     
           MOVE F445-NUM-STUDIO        TO  K440-NUM-STUDIO                      
           MOVE F445-RESEND-TAG        TO  K440-RESEND-TAG                      
                                                                                
      *----------------------------------*                                      
      * EXTRACT LATEST INFO FROM BP13K800                                       
      *----------------------------------*                                      
           PERFORM 410-READ-BP13K800       THRU 410-EXIT.                       
           IF (WS-K800-STATUS     = 23)                                         
              GO TO 400-EXIT.                                                   
                                                                                
           IF (K800-NUM-ALLO-CAT    = 'NPL') AND                                
              ((K800-NUM-FLAT-TYPE  = '1A' OR '2A') OR                          
               (K800-NUM-IDP-SCHEME = 'SA '))                                   
              CONTINUE                                                          
           ELSE                                                                 
              IF K800-NUM-ALLO-CAT    = 'NPL'                                   
                 GO TO 400-EXIT                                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K800-NUM-LEASE-TENURE NOT NUMERIC                                 
              MOVE ZEROES                  TO   K800-NUM-LEASE-TENURE           
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-STATUS            TO   K440-NUM-STATUS.                
           MOVE K800-NUM-SCH-ACC           TO   K440-NUM-HDB-REF.               
           MOVE K800-NUM-HOLDING-TYPE      TO   K440-NUM-HOLDING-TYPE.          
           MOVE K800-NUM-LOAN-TAG          TO   K440-CDE-BUYER-LOAN-TAG.        
           MOVE K800-NUM-NEW-TOWN          TO   K440-NUM-TOWN                   
                                                                                
           IF K800-NUM-ALLO-CAT = 'DBS'                                         
              MOVE 'Y'                     TO   K440-NUM-DBSS                   
              MOVE K800-NUM-PAYMENT-SCHEME TO   K440-NUM-PAYMENT-SCHEME         
           END-IF.                                                              
                                                                                
           IF K800-DTE-SO-APPT = SPACES OR LOW-VALUES                           
              MOVE ZEROES                  TO   K440-DTE-SO-APPT                
           ELSE                                                                 
              MOVE K800-DTE-SO-APPT        TO   K440-DTE-SO-APPT                
           END-IF.                                                              
                                                                                
           EVALUATE K800-NUM-ROOM                                               
           WHEN '1'                                                             
              MOVE '01'                    TO   K440-NUM-FLAT-TYPE              
           WHEN '2'                                                             
              MOVE '02'                    TO   K440-NUM-FLAT-TYPE              
           WHEN '3'                                                             
              MOVE '03'                    TO   K440-NUM-FLAT-TYPE              
           WHEN '4'                                                             
              MOVE '04'                    TO   K440-NUM-FLAT-TYPE              
           WHEN '5'                                                             
              MOVE '05'                    TO   K440-NUM-FLAT-TYPE              
           WHEN '6'                                                             
              MOVE '06'                    TO   K440-NUM-FLAT-TYPE              
           WHEN '7'                                                             
              MOVE '07'                    TO   K440-NUM-FLAT-TYPE              
           WHEN '8'                                                             
              MOVE '08'                    TO   K440-NUM-FLAT-TYPE              
           WHEN 'E'                                                             
              MOVE '06'                    TO   K440-NUM-FLAT-TYPE              
           WHEN OTHER                                                           
              MOVE SPACES                  TO   K440-NUM-FLAT-TYPE              
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------*                                      
      * EXTRACT LATEST INFO FROM PIDB                                           
      *----------------------------------*                                      
           PERFORM 500-CALL-BP13C913       THRU 500-EXIT.                       
           MOVE COMM8-NUM-BLK              TO   K440-NUM-BLOCK.                 
           MOVE COMM8-NME-STREET           TO   K440-NME-STREET.                
           MOVE COMM8-NUM-LEVEL            TO   K440-NUM-HSELEVEL.              
           STRING COMM8-NUM-UNIT-MAIN    COMM8-NUM-UNIT-SUB                     
                  DELIMITED BY SIZE      INTO   K440-NUM-MAIN.                  
           MOVE COMM8-NUM-POSTAL-CODE      TO   K440-NUM-POSTAL.                
           MOVE COMM8-CDE-ST               TO   K440-CDE-STREET.                
           MOVE COMM8-NUM-MGMT             TO   K440-NUM-BO.                    
                                                                                
      *----------------------------------*                                      
      * EXTRACT LATEST INFO FROM FIS                                            
      *----------------------------------*                                      
           PERFORM 600-READ-BM06K110       THRU 600-EXIT.                       
           IF K110-DTE-ACCEPTANCE = SPACES OR LOW-VALUES                        
              MOVE ZEROES                  TO   K440-DTE-BOOK                   
           ELSE                                                                 
              MOVE K110-DTE-ACCEPTANCE     TO   K440-DTE-BOOK                   
           END-IF.                                                              
                                                                                
           IF K110-DTE-KEY-ISSUED = SPACES OR LOW-VALUES                        
              MOVE ZEROES                  TO   K440-DTE-KEY-ISSUE              
           ELSE                                                                 
              MOVE K110-DTE-KEY-ISSUED     TO   K440-DTE-KEY-ISSUE              
           END-IF.                                                              
                                                                                
           IF K110-CURR-SELLING-PRICE NOT  NUMERIC                              
              MOVE ZEROES             TO   K110-CURR-SELLING-PRICE.             
                                                                                
           IF K800-NUM-PREMIUM-RATE   NOT  NUMERIC                              
              MOVE ZEROES             TO   K800-NUM-PREMIUM-RATE.               
                                                                                
           IF K800-AMT-PREMIUM        NOT  NUMERIC                              
              MOVE ZEROES             TO   K800-AMT-PREMIUM.                    
                                                                                
           MOVE K110-CURR-SELLING-PRICE TO WS-SELL-PRICE.                       
                                                                                
      *    IF K800-NUM-PREMIUM-RATE > ZEROES                                    
              MOVE K800-AMT-PREMIUM   TO   WS-AMT-PREM                          
              ADD WS-AMT-PREM         TO   WS-SELL-PRICE                        
      *    ELSE                                                                 
      *       IF K800-NUM-RESALE-LEVY-TAG = 'Y'     AND                         
      *          K800-NUM-PREMIUM-RATE    = ZEROES  AND                         
      *          K800-AMT-PREMIUM         > ZEROS                               
      *            MOVE K800-AMT-PREMIUM   TO   WS-AMT-PREM                     
      *            ADD WS-AMT-PREM         TO   WS-SELL-PRICE                   
      *       END-IF                                                            
      *    END-IF.                                                              
                                                                                
           MOVE WS-SELL-PRICE         TO   K440-AMT-PURCHASE-PRICE              
                                           K440-AMT-VALUATION.                  
                                                                                
      *-------------------------------------*                                   
      * EXTRACT LATEST INFO FROM OCCUP FILE                                     
      *-------------------------------------*                                   
           PERFORM 700-GET-APPL-DETAILS    THRU 700-EXIT.                       
                                                                                
      *---------------------------*                                             
      * EXTRACT LATEST BO MASTER                                                
      *---------------------------*                                             
           IF K800-NUM-LOAN-TAG = 'B'                                           
              PERFORM 800-READ-BP13K670       THRU 800-EXIT                     
              MOVE K670-NME-BUYER-LAWYER TO                                     
                   K440-NME-BUYER-SOLICITOR                                     
              MOVE K670-NME-BUYER-LAWYER-ADDR TO                                
                K440-NUM-BUYER-SOLICITOR-ADDR                                   
           END-IF.                                                              
                                                                                
           MOVE F445-DTE-HDB-SEND          TO   K440-DTE-HDB-SEND.              
           MOVE F445-DTE-UPDATE            TO   K440-DTE-UPDATE.                
           MOVE F445-NUM-USERID            TO   K440-NUM-USERID.                
           IF WS-K440-REC NOT = BP13K440-REC  OR                                
              F445-RESEND-TAG = 'Y'                                             
              MOVE WS-CURRENT-DATE         TO   K440-DTE-HDB-SEND               
                                                K440-DTE-UPDATE                 
              MOVE 'BP13C345'              TO   K440-NUM-USERID                 
                                                                                
              MOVE K110-AIX1  TO   K100-KEY-FLD OF K100-REC                     
              PERFORM 601-READ-BM06K100       THRU 601-EXIT                     
              MOVE K100-CDE-BLK         TO K500-KEY-FLD                         
              MOVE K110-SCH-ACC-NO(1:9)  TO K580-KEY-FLD                        
              PERFORM 650-READ-BB14K580  THRU 650-EXIT                          
              PERFORM 660-READ-BB14K500  THRU 660-EXIT                          
              IF (K580-DTE-LEASE-COMM = SPACES OR LOW-VALUES) OR                
                 (K580-DTE-LEASE-COMM IS NOT NUMERIC)                           
                  MOVE '00000000'  TO K580-DTE-LEASE-COMM                       
              END-IF                                                            
              IF (K500-DTE-LEASE-COMM-FLAT = SPACES OR LOW-VALUES) OR           
                 (K500-DTE-LEASE-COMM-FLAT IS NOT NUMERIC)                      
                  MOVE '00000000'  TO K500-DTE-LEASE-COMM-FLAT                  
              END-IF                                                            
              IF (K580-NUM-LEASE-PERIOD NOT NUMERIC) OR                         
                 (K580-NUM-LEASE-PERIOD = 0)                                    
                 MOVE 30         TO K580-NUM-LEASE-PERIOD                       
              END-IF                                                            
              IF (K500-NUM-LEASE-PERIOD-FLAT NOT NUMERIC) OR                    
                 (K500-NUM-LEASE-PERIOD-FLAT = 0)                               
                 MOVE 99         TO K500-NUM-LEASE-PERIOD-FLAT                  
              END-IF                                                            
              PERFORM 440-COMP-SA-SLF-TAG THRU 440-EXIT                         
                                                                                
              IF (K800-NUM-STATUS = 'C') OR                                     
                 (K800-NUM-SCH-ACC = SPACES OR LOW-VALUES) OR                   
                 (K800-NUM-SCH-ACC NOT = WS-NUM-HDB-REF)                        
                 IF F445-RESEND-TAG = 'Y'                                       
                    MOVE F445-CDE-ACTION-TAG TO K440-CDE-ACTION-TAG             
                                                WS-ACTION-TAG                   
                 ELSE                                                           
                    MOVE '3'              TO   K440-CDE-ACTION-TAG              
                                               WS-ACTION-TAG                    
                 END-IF                                                         
                 WRITE P13F445A-REC    FROM WS-K440-REC                         
                 ADD 01                TO   WS-F445A-WRITE                      
                 PERFORM 920-DELETE-BP13K440 THRU 920-EXIT                      
              ELSE                                                              
                 IF F445-RESEND-TAG = 'Y'                                       
                    MOVE F445-CDE-ACTION-TAG TO K440-CDE-ACTION-TAG             
                 ELSE                                                           
                    MOVE '2'              TO   K440-CDE-ACTION-TAG              
                 END-IF                                                         
                 ADD 01                TO   WS-F445A-WRITE                      
                 IF (K110-DTE-KEY-ISSUED NOT = SPACES AND LOW-VALUES            
                                                      AND ZEROES)               
                    MOVE 'C'            TO K440-NUM-STATUS-COMPLETION           
                    MOVE ZEROS          TO K440-DTE-EXPECTED-TOP                
                    WRITE P13F445A-REC    FROM BP13K440-REC                     
      *             PERFORM 920-DELETE-BP13K440  THRU 920-EXIT                  
                 ELSE                                                           
                    PERFORM 420-UPDATE-STATUS    THRU 420-EXIT                  
                    PERFORM 900-REWRITE-BP13K440 THRU 900-EXIT                  
                    WRITE P13F445A-REC    FROM BP13K440-REC                     
                 END-IF                                                         
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       410-READ-BP13K800.                                                       
      *------------------*                                                      
           MOVE SPACES           TO BP13K800-MASTER.                            
           MOVE F445-NUM-REGN    TO K800-NUM-REGN.                              
                                                                                
           READ BP13K800.                                                       
           IF WS-K800-STATUS = 00                                               
              ADD 1              TO WS-K800-READ                                
                                                                                
              IF (K800-NUM-ALLO-CAT    = 'NPL') AND                             
                 ((K800-NUM-FLAT-TYPE  = '1A' OR '2A') OR                       
                  (K800-NUM-IDP-SCHEME = 'SA '))                                
                  CONTINUE                                                      
              ELSE                                                              
                 IF K800-NUM-ALLO-CAT    = 'NPL'                                
                    ADD 1           TO WS-K800-NPL                              
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES        TO BP13K800-MASTER                             
              IF WS-K800-STATUS = 23                                            
                 ADD 1           TO WS-K800-NOTFD                               
                 DISPLAY 'REC NOTFD BP13K800 ' WS-K800-STATUS ' '               
                                   F445-NUM-REGN                                
              ELSE                                                              
                 DISPLAY 'ERROR READING   BP13K800, STATUS '                    
                                                    WS-K800-STATUS              
                 MOVE WS-K800-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       420-UPDATE-STATUS.                                                       
      *------------------*                                                      
                                                                                
           IF (K110-FLAT-STATUS = 'NW') AND                                     
              (K110-DTE-HANDOVER = ZEROES OR SPACES) AND                        
              (K110-DTE-KEY-AVAIL = ZEROES OR SPACES)                           
               MOVE 'U'               TO K440-NUM-STATUS-COMPLETION             
               IF K580-DTE-UNIT-DELIVERY NOT = ZEROES AND SPACES                
                  MOVE K580-DTE-UNIT-DELIVERY                                   
                                      TO K440-DTE-EXPECTED-TOP                  
               ELSE                                                             
                  MOVE K110-DTE-ACCEPTANCE  TO WS-K110-DTE-ACCEPTANCE           
                  COMPUTE WS-K110-DTE-CCYY = WS-K110-DTE-CCYY + 2               
                  IF WS-K110-DTE-MM  = 02 AND WS-K110-DTE-DD = 29               
                     PERFORM 430-CHECK-LEAP-YR THRU 430-EXIT                    
                  END-IF                                                        
                  MOVE WS-K110-DTE-ACCEPTANCE                                   
                                         TO K440-DTE-EXPECTED-TOP               
               END-IF                                                           
           ELSE                                                                 
               MOVE 'C'               TO K440-NUM-STATUS-COMPLETION             
               MOVE ZEROS             TO K440-DTE-EXPECTED-TOP                  
           END-IF.                                                              
                                                                                
           IF F445-RESEND-TAG = 'Y'                                             
              MOVE SPACES TO K440-RESEND-TAG                                    
           END-IF.                                                              
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       430-CHECK-LEAP-YR.                                                       
      *------------------*                                                      
           MOVE WS-K110-DTE-ACCEPTANCE     TO WS-DATE.                  03390017
                                                                        03390017
           DIVIDE WS-YY1 BY 4 GIVING    WS-QUOTIENT                     03390017
                              REMAINDER WS-REMAINDER.                   03400017
           IF WS-REMAINDER = 0                                          03410017
              MOVE 29  TO WS-K110-DTE-DD                                03420017
           ELSE                                                         03410017
              MOVE 28  TO WS-K110-DTE-DD                                03420017
           END-IF.                                                      03430017
                                                                        03430017
                                                                                
       430-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       440-COMP-SA-SLF-TAG.                                                     
      *--------------------*                                                    
           MOVE SPACES                   TO K440-NUM-STUDIO.                    
                                                                                
      *---------------------------------------------------------------          
      *    TAG FOR FSH CASES                                                    
      *---------------------------------------------------------------          
           IF K800-NUM-ALLO-SCHEME = 'FSH'                                      
              MOVE SPACES                TO K440-NUM-STUDIO                     
             IF K580-DTE-LEASE-COMM NOT = LOW-VALUES AND SPACES AND             
                                           ZEROES                               
              MOVE K580-DTE-LEASE-COMM   TO K440-DTE-LEASE-COMMENCEMENT         
             ELSE                                                               
              MOVE ZEROES                TO K440-DTE-LEASE-COMMENCEMENT         
             END-IF                                                             
              MOVE K800-NUM-FLAT-TYPE    TO K440-NUM-FLAT-TYPE                  
              MOVE K800-NUM-LEASE-TENURE TO K440-NUM-LEASE-TENURE               
              GO TO 440-EXIT                                                    
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------          
      *    TAG FOR 2 ROOM FLEXI (2F)  AND CCA 1F                                
      *---------------------------------------------------------------          
           IF (K800-NUM-FLAT-TYPE    = '2F' OR '1F')  AND                       
              (K800-NUM-LEASE-TENURE > ZEROES AND                               
               K800-NUM-LEASE-TENURE <= 45)                                     
              MOVE K800-NUM-FLAT-TYPE    TO K440-NUM-FLAT-TYPE                  
              MOVE K800-NUM-LEASE-TENURE TO K440-NUM-LEASE-TENURE               
             IF K580-DTE-LEASE-COMM NOT = LOW-VALUES AND SPACES AND             
                                           ZEROES                               
              MOVE K580-DTE-LEASE-COMM   TO K440-DTE-LEASE-COMMENCEMENT         
             ELSE                                                               
              MOVE ZEROES                TO K440-DTE-LEASE-COMMENCEMENT         
             END-IF                                                             
              MOVE 'Y'                   TO K440-NUM-STUDIO                     
              GO TO 440-EXIT                                                    
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------          
      *    TAG FOR STUDIO APT (1A AND 2A)                                       
      *---------------------------------------------------------------          
           IF (K800-NUM-FLAT-TYPE = '1A' OR '2A') OR                            
              (K800-NUM-IDP-SCHEME = 'SA ')                                     
              MOVE 'Y'                   TO K440-NUM-STUDIO                     
              MOVE K580-DTE-LEASE-COMM   TO K440-DTE-LEASE-COMMENCEMENT         
              MOVE K580-NUM-LEASE-PERIOD TO K440-NUM-LEASE-TENURE               
              GO TO 440-EXIT                                                    
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------          
      *    TAG FOR SERS LEASE BUY BACK SCHEME                                   
      *---------------------------------------------------------------          
           IF (K800-NUM-LEASE-BUY-BACK = 'Y')   AND                             
              (K800-NUM-LEASE-TENURE   > ZEROES)                                
              MOVE K800-NUM-ALLO-SCHEME  TO WS-ALLOC-SCH                        
              PERFORM 441-CHECK-SERS     THRU 441-EXIT                          
                                                                                
              IF WS-SCH-FND             = 'Y' OR                                
                 K800-NUM-PORT-ELIG-TAG = 'Y'                                   
                 MOVE 'Y'                TO K440-NUM-STUDIO                     
                 MOVE K800-NUM-LEASE-TENURE                                     
                                         TO K440-NUM-LEASE-TENURE               
                 IF F445-DTE-KEY-ISSUE = SPACES OR LOW-VALUES OR                
                                         ZEROES                                 
                    MOVE ZEROES          TO K440-DTE-LEASE-COMMENCEMENT         
                 ELSE                                                           
                    MOVE F445-DTE-KEY-ISSUE                                     
                                         TO K440-DTE-LEASE-COMMENCEMENT         
                 END-IF                                                         
                 GO TO 440-EXIT                                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------          
      *    FOR ALL OTHER CASES                                                  
      *---------------------------------------------------------------          
           IF K440-NUM-STUDIO = SPACES                                          
              MOVE K500-DTE-LEASE-COMM-FLAT                                     
                                         TO K440-DTE-LEASE-COMMENCEMENT         
              MOVE K500-NUM-LEASE-PERIOD-FLAT                                   
                                         TO K440-NUM-LEASE-TENURE               
           END-IF.                                                              
                                                                                
       440-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------*                                                         
       441-CHECK-SERS.                                                          
      *---------------*                                                         
                                                                                
           IF WS-ALLOC-SCH  NOT =  LOW-VALUES AND SPACES                        
              SET WS-SERS-PTR  TO 1                                             
              SEARCH WS-SERS-SCH                                                
                 AT END                                                         
                    MOVE 'N'               TO   WS-SCH-FND                      
                 WHEN WS-SERS-SCHEME(WS-SERS-PTR) = WS-ALLOC-SCH                
                    MOVE 'Y'               TO   WS-SCH-FND                      
              END-SEARCH                                                        
           ELSE                                                                 
              MOVE 'N'                     TO   WS-SCH-FND                      
           END-IF.                                                              
                                                                                
       441-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       500-CALL-BP13C913.                                                       
      *------------------*                                                      
           MOVE SPACES            TO BP13COMM8-REC.                             
           INITIALIZE                BP13COMM8-REC.                             
                                                                                
           MOVE K800-NUM-SCH-ACC  TO WS-LINK-REC.                               
                                                                                
      **** REMOVE AFTER TESTING ****                                            
      *    MOVE '020400372'       TO WS-LINK-REC.                               
      **** REMOVE AFTER TESTING ****                                            
                                                                                
           CALL WS-BP13C913  USING WS-LINK-REC,                                 
                                  BP13COMM8-REC.                                
                                                                                
           IF COMM8-CDE-SYSERR NOT = ZEROES                                     
              MOVE COMM8-CDE-SYSERR TO WS-SQL-CODE                              
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE   ' WS-SQL-CODE        
              ELSE                                                              
                 DISPLAY 'ERROR READING  PBF TABLE, STATUS '                    
                                                    WS-SQL-CODE.                
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       600-READ-BM06K110.                                                       
      *------------------*                                                      
           MOVE K800-NUM-SCH-ACC TO K110-SCH-ACC-NO.                            
                                                                                
           READ BM06K110.                                                       
           IF WS-K110-STATUS = 00                                               
              ADD 1              TO WS-K110-READ                                
           ELSE                                                                 
              MOVE SPACES TO K110-REC                                           
              IF WS-K110-STATUS = 23                                            
                 ADD 1           TO WS-K110-NOTFD                               
                 DISPLAY 'REC NOTFD BM06K110 ' WS-K110-STATUS ' '               
                                               F445-NUM-HDB-REF                 
              ELSE                                                              
                 DISPLAY 'ERROR READING   BM06K110, STATUS '                    
                                                    WS-K110-STATUS              
                 MOVE WS-K110-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       601-READ-BM06K100.                                                       
      *------------------*                                                      
                                                                                
           READ BM06K100.                                                       
           IF WS-K100-STATUS = 00                                               
              ADD 1              TO WS-K100-READ                                
           ELSE                                                                 
              MOVE SPACES TO K100-REC                                           
              IF WS-K100-STATUS = 23                                            
                 ADD 1           TO WS-K100-NOTFD                               
                 DISPLAY 'REC NOTFD BM06K100 ' WS-K100-STATUS ' '               
                                               K100-KEY-FLD OF K100-REC         
              ELSE                                                              
                 DISPLAY 'ERROR READING   BM06K100, STATUS '                    
                                                    WS-K100-STATUS              
                 MOVE WS-K100-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       601-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       650-READ-BB14K580.                                                       
      *------------------*                                                      
                                                                                
           READ BB14K580.                                                       
           IF WS-K580-STATUS = 00                                               
              ADD 1              TO WS-K580-READ                                
           ELSE                                                                 
              MOVE SPACES TO BB14K580-REC                                       
              IF WS-K580-STATUS = 23                                            
                 ADD 1           TO WS-K580-NOTFD                               
                 DISPLAY 'REC NOTFD BB14K580 ' WS-K580-STATUS ' '               
                                               K580-NUM-SCH-ACCNT               
              ELSE                                                              
                 DISPLAY 'ERROR READ BB14K580,STATUS ' WS-K580-STATUS           
                 MOVE WS-K580-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       660-READ-BB14K500.                                                       
      *------------------*                                                      
                                                                                
           READ BB14K500.                                                       
           IF WS-K500-STATUS = 00                                               
              ADD 1              TO WS-K500-READ                                
           ELSE                                                                 
              MOVE SPACES TO BB14K500-REC                                       
              IF WS-K500-STATUS = 23                                            
                 ADD 1           TO WS-K500-NOTFD                               
                 DISPLAY 'REC NOTFD BB14K500 ' WS-K500-STATUS ' '               
                                               K500-CDE-BLK                     
              ELSE                                                              
                 DISPLAY 'ERROR READ BB14K500,STATUS ' WS-K500-STATUS           
                 MOVE WS-K500-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       660-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       700-GET-APPL-DETAILS.                                                    
      *---------------------*                                                   
           MOVE SPACES                  TO   BP13K820-REC.                      
           MOVE F445-NUM-REGN           TO   K820-NUM-REGN.                     
                                                                                
           MOVE K800-NUM-NRIC1          TO   K820-NUM-NRIC                      
                                             WS-NUM-NRIC                        
                                             K440-NUM-BUYER1-NRIC.              
                                                                                
           PERFORM 710-READ-BP13K820    THRU 710-EXIT.                          
           MOVE K820-NME-OCCP           TO   K440-NME-BUYER1.                   
                                                                                
           IF K820-NME-OCCP = 'JTC' OR 'ADM'                                    
              MOVE SPACES               TO   K440-NUM-BUYER1-CPF                
           ELSE                                                                 
              MOVE K820-NUM-CPF         TO   K440-NUM-BUYER1-CPF                
           END-IF.                                                              
                                                                                
           STRING K800-NUM-SHR1 '/' K800-NUM-SHR-BASE                           
                  DELIMITED BY SIZE   INTO  K440-NUM-BUYER1-PCT-HOLDING.        
                                                                                
           IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-NRIC2       TO   K820-NUM-NRIC WS-NUM-NRIC          
              PERFORM 710-READ-BP13K820 THRU 710-EXIT                           
              IF WS-K820-STATUS = 00                                            
                 MOVE K800-NUM-NRIC2    TO   K440-NUM-BUYER2-NRIC               
                 MOVE K820-NME-OCCP     TO   K440-NME-BUYER2                    
                 MOVE K820-NUM-CPF      TO   K440-NUM-BUYER2-CPF                
                 STRING K800-NUM-SHR2 '/' K800-NUM-SHR-BASE                     
                    DELIMITED BY SIZE INTO  K440-NUM-BUYER2-PCT-HOLDING         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-NRIC3       TO   K820-NUM-NRIC WS-NUM-NRIC          
              PERFORM 710-READ-BP13K820 THRU 710-EXIT                           
              IF WS-K820-STATUS = 00                                            
                 MOVE K800-NUM-NRIC3    TO   K440-NUM-BUYER3-NRIC               
                 MOVE K820-NME-OCCP     TO   K440-NME-BUYER3                    
                 MOVE K820-NUM-CPF      TO   K440-NUM-BUYER3-CPF                
                 STRING K800-NUM-SHR3 '/' K800-NUM-SHR-BASE                     
                    DELIMITED BY SIZE INTO  K440-NUM-BUYER3-PCT-HOLDING         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-NRIC4       TO   K820-NUM-NRIC WS-NUM-NRIC          
              PERFORM 710-READ-BP13K820 THRU 710-EXIT                           
              IF WS-K820-STATUS = 00                                            
                 MOVE K800-NUM-NRIC4    TO   K440-NUM-BUYER4-NRIC               
                 MOVE K820-NME-OCCP     TO   K440-NME-BUYER4                    
                 MOVE K820-NUM-CPF      TO   K440-NUM-BUYER4-CPF                
                 STRING K800-NUM-SHR4 '/' K800-NUM-SHR-BASE                     
                    DELIMITED BY SIZE INTO  K440-NUM-BUYER4-PCT-HOLDING         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       710-READ-BP13K820.                                                       
      *------------------*                                                      
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              ADD 1               TO WS-K820-READ                               
           ELSE                                                                 
              MOVE SPACES TO BP13K820-REC                                       
              IF WS-K820-STATUS = 23                                            
                 ADD 1               TO WS-K820-NOTFD                           
                 DISPLAY 'REC NOTFD BP13K820 ' WS-K820-STATUS ' '               
                               F445-NUM-REGN WS-NUM-NRIC                        
              ELSE                                                              
                 DISPLAY 'ERROR READING   BP13K820, STATUS '                    
                                                    WS-K820-STATUS              
                 MOVE WS-K820-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       710-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       800-READ-BP13K670.                                                       
      *------------------*                                                      
           MOVE SPACES           TO BP13K670-REC.                               
           MOVE F445-NUM-REGN    TO K670-NUM-REGN.                              
                                                                                
           READ BP13K670.                                                       
           IF WS-K670-STATUS = 00                                               
              ADD 1              TO WS-K670-READ                                
           ELSE                                                                 
              MOVE SPACES        TO BP13K670-REC                                
              IF WS-K670-STATUS = 23                                            
                 ADD 1           TO WS-K670-NOTFD                               
                 DISPLAY 'REC NOTFD BP13K670 ' WS-K670-STATUS ' '               
                                               F445-NUM-REGN                    
              ELSE                                                              
                 DISPLAY 'ERROR READING   BP13K670, STATUS '                    
                                                    WS-K670-STATUS              
                 MOVE WS-K670-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------*                                                   
       900-REWRITE-BP13K440.                                                    
      *---------------------*                                                   
           REWRITE BP13K440-REC.                                                
                                                                                
           EVALUATE WS-K440-STATUS                                              
           WHEN 0                                                               
              MOVE  SPACES       TO   P13F445A-REC                              
              ADD   1            TO   WS-K440-REWRITE                           
           WHEN OTHER                                                           
              DISPLAY 'ERROR REWRITING BP13K440, STATUS ' WS-K440-STATUS        
              MOVE    WS-K440-STATUS TO RETURN-CODE                             
              GO TO   999-CLOSE-FILES                                           
           END-EVALUATE.                                                        
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------*                                                   
       920-DELETE-BP13K440.                                                     
      *---------------------*                                                   
           IF K440-CDE-ACTION-TAG = '3'                                         
              ADD 1              TO   WS-K440-CANCEL-DELETE                     
           ELSE                                                                 
              ADD 1              TO   WS-K440-KISSUE-DELETE                     
           END-IF.                                                              
                                                                                
           DELETE BP13K440.                                                     
                                                                                
           EVALUATE WS-K440-STATUS                                              
           WHEN 0                                                               
              ADD 1              TO   WS-K440-DELETE                            
           WHEN OTHER                                                           
              DISPLAY 'ERROR DELETING  BP13K440,STATUS ' WS-K440-STATUS         
              MOVE    WS-K440-STATUS TO RETURN-CODE                             
              GO TO   999-CLOSE-FILES                                           
           END-EVALUATE.                                                        
                                                                                
       920-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *----------------*                                                        
       999-CLOSE-FILES.                                                         
      *----------------*                                                        
           DISPLAY ' '.                                                         
           DISPLAY '*----------------- BP13C345 -------------------'.           
                                                                                
           DISPLAY 'NO OF REC READ      BP13F445   = ' WS-F445-READ.            
           DISPLAY 'NO OF REC READ      BP13K440   = ' WS-K440-READ.            
           DISPLAY 'NO OF REC NOTFD     BP13K440   = ' WS-K440-NOTFD.           
           DISPLAY 'NO OF REC WRITTEN   BP13K440   = ' WS-K440-WRITE.           
           DISPLAY 'NO OF REC REWRITTEN BP13K440   = ' WS-K440-REWRITE.         
           DISPLAY 'NO OF REC DELETED   BP13K440   = ' WS-K440-DELETE.          
           DISPLAY '         (CANCELLED CASES)     = '                          
                                             WS-K440-CANCEL-DELETE.             
           DISPLAY '         (KEY ISSUED CASES)    = '                          
                                             WS-K440-KISSUE-DELETE.             
           DISPLAY '                                 '.                         
           DISPLAY 'NO OF REC READ      BP13K800   = ' WS-K800-READ.            
           DISPLAY 'NO OF REC NOTFD     BP13K800   = ' WS-K800-NOTFD.           
           DISPLAY 'NO OF NPL CASES     BP13K800   = ' WS-K800-NPL.             
           DISPLAY '                                 '.                         
           DISPLAY 'NO OF REC READ      BP13K820   = ' WS-K820-READ.            
           DISPLAY 'NO OF REC NOTFD     BP13K820   = ' WS-K820-NOTFD.           
           DISPLAY '                                 '.                         
           DISPLAY 'NO OF REC READ      BP13K670   = ' WS-K670-READ.            
           DISPLAY 'NO OF REC NOTFD     BP13K670   = ' WS-K670-NOTFD.           
           DISPLAY '                                 '.                         
           DISPLAY 'NO OF REC READ      BM06K110   = ' WS-K110-READ.            
           DISPLAY 'NO OF REC NOTFD     BM06K110   = ' WS-K110-NOTFD.           
           DISPLAY '                                 '.                         
           DISPLAY '                                 '.                         
           DISPLAY 'NO OF REC READ      BM06K100   = ' WS-K100-READ.            
           DISPLAY 'NO OF REC NOTFD     BM06K100   = ' WS-K100-NOTFD.           
           DISPLAY '                                 '.                         
           DISPLAY '                                 '.                         
           DISPLAY 'NO OF REC READ      BB14K500   = ' WS-K500-READ.            
           DISPLAY 'NO OF REC NOTFD     BB14K500   = ' WS-K500-NOTFD.           
           DISPLAY 'NO OF REC READ      BB14K580   = ' WS-K580-READ.            
           DISPLAY 'NO OF REC NOTFD     BB14K580   = ' WS-K580-NOTFD.           
           DISPLAY '                                 '.                         
           DISPLAY 'NO OF REC WRITTEN   P13F445A   = ' WS-F445A-WRITE.          
                                                                                
           CLOSE BP13F445                                                       
                 BP13K440                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K670                                                       
                 BM06K110                                                       
                 BM06K100                                                       
                 BB14K500                                                       
                 BB14K580                                                       
                 P13F445A.                                                      
                                                                                
           IF WS-K440-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR CLOSING   BP13K440, STATUS ' WS-K440-STATUS        
              MOVE WS-K440-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR CLOSING   BP13K800, STATUS ' WS-K800-STATUS        
              MOVE WS-K800-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K820-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR CLOSING   BP13K820, STATUS ' WS-K820-STATUS        
              MOVE WS-K820-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K670-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR CLOSING   BP13K670, STATUS ' WS-K670-STATUS        
              MOVE WS-K670-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K110-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR CLOSING   BM06K110, STATUS ' WS-K110-STATUS        
              MOVE WS-K110-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K100-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR CLOSING   BM06K100, STATUS ' WS-K100-STATUS        
              MOVE WS-K100-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K500-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR CLOSING   BB14K500, STATUS ' WS-K500-STATUS        
              MOVE WS-K500-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K580-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR CLOSING   BB14K580, STATUS ' WS-K580-STATUS        
              MOVE WS-K580-STATUS       TO RETURN-CODE.                         
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
