      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C30A.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  AUG 03, 2018.                                             
      *===============================================================*         
      *   OBJECTIVE:  PROGRAM TO DELETE BP13K40C FOR INCOMPLETE       *         
      *               RAISING OF SALES ORDER                          *         
      *===============================================================*         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F40C                                  *         
      *                  2. BP13K400                                  *         
      *                  3. BP13K40C                                  *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP137182 03/08/2018 PCL4 NEW PROGRAM.                         *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F40C  ASSIGN       TO BP13F40C.                          
                                                                                
                                                                                
           SELECT  BP13K400  ASSIGN       TO BP13K400                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K400-KEY-FLD                       
                             FILE STATUS  IS WS-K400-STATUS.                    
                                                                                
           SELECT  BP13K40C  ASSIGN       TO BP13K40C                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K40C-KEY-FLD                       
                             FILE STATUS  IS WS-K40C-STATUS.                    
                                                                                
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F40C                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F40C.                                                           
                                                                                
                                                                                
       FD   BP13K400                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K400.                                                           
                                                                                
       FD   BP13K40C                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K40C.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-F40C-EOF             PIC X(1)  VALUE 'N'.                     
           05  WS-K400-FND             PIC X(1)  VALUE 'N'.                     
           05  WS-K400-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K40C-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F40C-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-DELETE           PIC 9(08) VALUE ZEROES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE            THRU 1000-EXIT.                 
           PERFORM 2000-READ-BP13F40C           THRU 2000-EXIT.                 
                                                                                
           PERFORM 3000-PROCESS-RECORDS         THRU 3000-EXIT                  
             UNTIL WS-F40C-EOF = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE           THRU 9999-EXIT.                 
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F40C                                                 
                       BP13K400                                                 
                  I-O  BP13K40C.                                                
                                                                                
                                                                                
           IF WS-K400-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K400 - ERROR OPENING : ' WS-K400-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K40C-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K40C - ERROR OPENING : ' WS-K40C-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F40C.                                                      
      ******************************************************************        
                                                                                
           READ BP13F40C           AT END                                       
                MOVE 'Y'           TO WS-F40C-EOF                               
                GO                 TO 2000-EXIT.                                
                                                                                
           ADD  1                  TO WS-F40C-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 3100-READ-BP13K400               THRU 3100-EXIT.             
                                                                                
           IF WS-K400-FND NOT = 'Y'                                             
              PERFORM 3200-READ-BP13K40C            THRU 3200-EXIT              
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F40C               THRU 2000-EXIT.             
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-READ-BP13K400.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                            TO BP13K400-REC.              
           INITIALIZE                                BP13K400-REC.              
                                                                                
           MOVE SPACES                            TO WS-K400-FND.               
           MOVE F40C-KEY-FLD                      TO K400-KEY-FLD.              
                                                                                
           READ BP13K400.                                                       
                                                                                
           EVALUATE WS-K400-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                      TO WS-K400-FND                
                                                                                
               WHEN 23                                                          
                    CONTINUE                                                    
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K400 : ' WS-K400-STATUS          
                            ' REGN = ' K400-KEY-FLD                             
                    PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-READ-BP13K40C.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                            TO BP13K40C-REC.              
           INITIALIZE                                BP13K40C-REC.              
                                                                                
           MOVE F40C-KEY-FLD                      TO K40C-KEY-FLD.              
                                                                                
           READ BP13K40C.                                                       
                                                                                
           EVALUATE WS-K40C-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3300-DELETE-BP13K40C  THRU 3300-EXIT                
                                                                                
               WHEN 23                                                          
                    CONTINUE                                                    
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K40C : ' WS-K40C-STATUS          
                            ' REGN = ' K40C-KEY-FLD                             
                    PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3300-DELETE-BP13K40C.                                                    
      ******************************************************************        
                                                                                
           DELETE BP13K40C.                                                     
                                                                                
           EVALUATE WS-K40C-STATUS                                              
               WHEN 00                                                          
                    ADD 1                   TO WS-CNT-DELETE                    
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR DELETING BP13K40C :' WS-K40C-STATUS          
                            ' REGN = ' K40C-KEY-FLD                             
                    PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C30A'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1)  NO OF BP13F40C RECORDS READ............. : '           
                    WS-F40C-READ.                                               
           DISPLAY '(2)  NO OF BP13K40C RECORDS DELETED.......... : '           
                    WS-CNT-DELETE.                                              
                                                                                
           CLOSE    BP13F40C                                                    
                    BP13K400                                                    
                    BP13K40C.                                                   
                                                                                
           IF WS-K400-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K400 - ERROR CLOSING : ' WS-K400-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K40C-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K40C - ERROR CLOSING : ' WS-K40C-STATUS              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
