       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C31Y.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  28/10/22.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  UPDATE BP13K885 NUM-CITIZENSHIP             *            
      *                                                            *            
      *  INPUT FILE :  1. BP13F890                                 *            
      *  I-O FILE   :  1. BP13K885                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP138916 ZAR7 20221028 NEW PROGRAM                         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F890 ASSIGN TO BP13F890.                                  
                                                                                
           SELECT BP13K885 ASSIGN TO BP13K885                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE IS RANDOM                                           
                RECORD KEY IS K885-KEY-FLD                                      
                FILE STATUS IS K885-STATUS.                                     
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13K885                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K885.                                                           
                                                                                
       FD  BP13F890                                                             
           RECORD CONTAINS 400 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F890.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  NO-F890-READ        PIC 9(5)    VALUE ZEROS.                     
           05  NO-K885-REWRITE     PIC 9(5)    VALUE ZEROS.                     
           05  WS-BYPASS-CTR       PIC 9(5)    VALUE ZEROS.                     
           05  WS-LINE-CTR         PIC 9(2)    VALUE 99.                        
           05  WS-PAGE-CTR         PIC 9(3)    VALUE ZEROES.                    
                                                                                
       01  WS-DATE                  PIC X(8)  VALUE SPACES.                     
       01  WS-EOF                   PIC X     VALUE 'N'.                        
       01  K885-STATUS              PIC 99    VALUE 0.                          
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPENING-ROUTINE  THRU 1000-EXIT.                        
           PERFORM 2000-READ-F890        THRU 2000-EXIT.                        
           PERFORM 4000-PROCESS-FILES    THRU 4000-EXIT                         
                   UNTIL WS-EOF = 'Y'.                                          
           PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT BP13F890                                                  
                I-O   BP13K885.                                                 
                                                                                
           IF K885-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K885 - OPEN ERROR, STATUS: ' K885-STATUS             
              MOVE K885-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                    
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-F890.                                                          
      *-------------------------------------------------------------            
            READ BP13F890 AT END                                                
                 MOVE 'Y' TO WS-EOF                                             
                 GO TO 2000-EXIT.                                               
            ADD 1 TO NO-F890-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           PERFORM 4200-UPDATE-K885-REC THRU 4200-EXIT                          
           PERFORM 2000-READ-F890 THRU 2000-EXIT.                               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4200-UPDATE-K885-REC.                                                    
      *-------------------------------------------------------------            
            MOVE F890-NUM-REGN TO K885-NUM-REGN.                                
            MOVE F890-NUM-NRIC TO K885-NUM-NRIC.                                
            READ BP13K885                                                       
            EVALUATE K885-STATUS                                                
            WHEN 00                                                             
                 PERFORM 4300-UPDATE-K885-REC THRU 4300-EXIT                    
                                                                                
            WHEN 23                                                             
                 DISPLAY 'BP13K885 - RECORD NOT FOUND, KEY: '                   
                          F890-NUM-REGN '/' F890-NUM-NRIC                       
            WHEN OTHER                                                          
                 DISPLAY 'BP13K885 - ERROR READ, STATUS: '                      
                          K885-STATUS                                           
                 PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT                  
            END-EVALUATE.                                                       
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4300-UPDATE-K885-REC.                                                    
      *-------------------------------------------------------------            
              DISPLAY ' R:' K885-KEY-FLD                                        
                      ' OLD CTZNSHIP ' K885-NUM-CITIZENSHIP                     
                                                                                
              MOVE F890-NUM-CITIZENSHIP TO K885-NUM-CITIZENSHIP                 
                                                                                
              DISPLAY ' NEW CTZNSHIP ' K885-NUM-CITIZENSHIP                     
                                                                                
           REWRITE BP13K885-REC.                                                
           EVALUATE K885-STATUS                                                 
           WHEN 00                                                              
                ADD 1 TO NO-K885-REWRITE                                        
           WHEN OTHER                                                           
                DISPLAY 'BP13K885 - REWRITE ERROR, STATUS: '                    
                         K885-STATUS                                            
                PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           CLOSE BP13K885                                                       
                 BP13F890.                                                      
                                                                                
           IF K885-STATUS NOT = '00'                                            
             DISPLAY 'CLOSING ERROR K885, STATUS ' K885-STATUS                  
             MOVE K885-STATUS TO RETURN-CODE.                                   
                                                                                
           DISPLAY 'NO. OF F890 REC READ   > ' NO-F890-READ.                    
           DISPLAY 'NO. OF F890 REC UPDATE > ' NO-K885-REWRITE.                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
