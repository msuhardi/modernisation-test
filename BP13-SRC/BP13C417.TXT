      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13C417.                                                 
      *AUTHOR.        JIJO FRANCIS.                                             
      *DATE-WRITTEN.  21 JUN 2005.                                              
                                                                                
      *===============================================================*         
      *                  SYSTEM OF COMMITMENT (BP13)                  *         
      *===============================================================*         
      *  OBJECTIVES : TO SEND EMAIL ALERT TO CSM FOR CASES WITH       *         
      *               LUMPSUM PAID BY CPFB DON'T TALLY WITH THE       *         
      *               LUMPSUM REQUIRED                                *         
      *                                                               *         
      *  INPUT FILES  - BP13F354                                      *         
      *                 BP13K022                                      *         
      *                 BP13K022                                      *         
      *  OUTPUT FILES - BP13MAIL                                      *         
      *---------------------------------------------------------------*         
      * CHG REF   BY    DATE        DESCRIPTION                       *         
      *---------  ----  ----------  -----------                       *         
      * BP132707  JF4   21/06/2005  NEW PROGRAM                       *         
      * BP135537  RJB1  23/02/2015  CHANGE THE EMAIL RECIPIENT FROM   *         
      *                             NICKNAME INTO COMPLETE NAME       *         
      *===============================================================*         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F354  ASSIGN        TO BP13F354.                          
                                                                                
           SELECT BP13MAIL  ASSIGN        TO BP13MAIL.                          
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            FILE STATUS   IS BP13K022-STATUS.                   
                                                                                
           SELECT BP13K024  ASSIGN        TO BP13K024                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K024-KEY-FLD                       
                            FILE STATUS   IS BP13K024-STATUS.                   
                                                                                
                                                                                
      *---------------*                                                         
       DATA DIVISION.                                                           
      *---------------*                                                         
       FILE SECTION.                                                            
      *---------------*                                                         
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS  100 CHARACTERS.                                    
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS  100 CHARACTERS.                                    
       COPY BP13K024.                                                           
                                                                                
       FD   BP13F354                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS   80 CHARACTERS.                                    
       01   BP13F354-REC.                                                       
            10  F354-NUM-REGN           PIC  X(08).                             
            10  F354-NUM-SCH-ACC        PIC  X(11).                             
            10  F354-NUM-CPF            PIC  X(09).                             
            10  F354-DTE-VALUE          PIC  X(08).                             
            10  F354-AMT-LUMPSUM-REQD   PIC S9(07)V99.                          
            10  F354-AMT-LUMPSUM-PAID   PIC S9(07)V99.                          
            10  FILLER                  PIC  X(26).                             
                                                                                
       FD   BP13MAIL                                                            
            RECORDING  MODE  IS F                                               
            RECORD CONTAINS   80 CHARACTERS.                                    
       01  MAIL-PRTREC                  PIC X(80).                              
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  BP13F354-EOF-TAG         PIC X(01).                              
               88  BP13F354-EOF         VALUE 'Y'.                              
           05  BP13K022-STATUS          PIC 9(02) VALUE ZEROES.                 
           05  BP13K024-STATUS          PIC 9(02) VALUE ZEROES.                 
                                                                                
       01  WS-OIC-TAG                   PIC X(01) VALUE SPACES.                 
           88  OIC-FOUND                VALUE 'Y'.                              
           88  OIC-NOT-FOUND            VALUE 'N'.                              
                                                                                
       01  WS-EMAIL-TAG                 PIC X(01) VALUE SPACES.                 
           88  SEND-EMAIL               VALUE 'Y'.                              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F354-READ-CNT         PIC 9(06) VALUE ZEROES.                 
           05  WS-MAIL-SEND-CNT         PIC 9(06) VALUE ZEROES.                 
           05  WS-K022-NOTFND-CNT       PIC 9(02) VALUE ZEROES.                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-CURR-DATE             PIC X(08) VALUE SPACES.                 
           05  WS-EDIT-DATE             PIC X(10) VALUE SPACES.                 
           05  WS-NUM-REGN              PIC X(08) VALUE SPACES.                 
           05  WS-EDIT-CNT              PIC Z(05)9.                             
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *                 O U T P U T   L A Y O U T                      *        
      *----------------------------------------------------------------*        
       01  MAIL-HEADER-1.                                                       
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
      **   05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB3'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-2.                                                       
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  SEND-ID         PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
      **   05  SEND-ID         PIC X(07)  VALUE 'CS26858'.                      
      **   05  FILLER          PIC X(09)  VALUE '@SGPHDB3>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-3.                                                       
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  TO-MAIL-ID-A    PIC X(31)  VALUE SPACES.                         
           05  FILLER          PIC X(40)  VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-4.                                                       
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  RCPT-ID         PIC X(12)  VALUE 'WEE_LEE_YEAN'.                 
      **   05  RCPT-ID         PIC X(04)  VALUE 'CLT1'.                         
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-HEADER-5.                                                       
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-6.                                                       
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  FROM-MAIL-ID    PIC X(10)  VALUE                                 
               'SOC System'.                                                    
           05  FILLER          PIC X(65)  VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-7.                                                       
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  TO-MAIL-ID      PIC X(31)  VALUE SPACES.                         
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-8.                                                       
           05  FILLER          PIC X(04)  VALUE 'CC:<'.                         
           05  CC-MAIL-ID      PIC X(12)  VALUE 'WEE_LEE_YEAN'.                 
      **   05  CC-MAIL-ID      PIC X(04)  VALUE 'CLT1'.                         
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
           05  FILLER          PIC X(60)  VALUE SPACES.                         
                                                                                
       01  MAIL-BODY-TXT-1.                                                     
           05  FILLER          PIC X(10)  VALUE 'Subject : '.                   
           05  SUBJECT-1       PIC X(41)  VALUE                                 
               'HBL2 CPF Payment on Bank Refinancing Case'.                     
           05  FILLER          PIC X(29)  VALUE SPACES.                         
                                                                                
       01  MAIL-BODY-TXT-1-ULINE.                                               
           05  FILLER          PIC X(10)  VALUE '----------'.                   
           05  SUBJECT-1       PIC X(41)  VALUE                                 
               '-----------------------------------------'.                     
           05  FILLER          PIC X(29)  VALUE SPACES.                         
                                                                                
       01  MAIL-BODY-TXT-2.                                                     
           05  FILLER          PIC X(03)  VALUE 'To '.                          
           05  CSM-NAME        PIC X(30)  VALUE SPACES.                         
           05  FILLER          PIC X(47)  VALUE SPACES.                         
                                                                                
       01  MAIL-BODY-TXT-3.                                                     
           05  FILLER          PIC X(45)  VALUE                                 
               'CPFB has remitted the following lump sum for '.                 
           05  FILLER          PIC X(20)  VALUE                                 
               'Registration number '.                                          
           05  REGN-NO         PIC X(08)  VALUE SPACES.                         
           05  FILLER          PIC X(01)  VALUE '.'.                            
           05  FILLER          PIC X(06)  VALUE SPACES.                         
                                                                                
       01  MAIL-BODY-TXT-4.                                                     
           05  FILLER          PIC X(48)  VALUE                                 
               'For your follow-up. Please inform voucher team t'.              
           05  FILLER          PIC X(23)  VALUE                                 
               'o raise Journal Voucher'.                                       
           05  FILLER          PIC X(09)  VALUE SPACES.                         
                                                                                
       01  MAIL-BODY-TXT-5.                                                     
           05  FILLER          PIC X(10)  VALUE 'after TP.'.                    
           05  FILLER          PIC X(70)  VALUE SPACES.                         
                                                                                
       01  MAIL-BODY-TXT-6.                                                     
           05  FILLER          PIC X(10)  VALUE 'Details : '.                   
           05  FILLER          PIC X(70)  VALUE SPACES.                         
                                                                                
       01  MAIL-BODY-TXT-6-ULINE.                                               
           05  FILLER          PIC X(10)  VALUE '----------'.                   
           05  FILLER          PIC X(70)  VALUE SPACES.                         
                                                                                
       01  MAIL-BODY-TXT-7.                                                     
           05  FILLER          PIC X(45)  VALUE                                 
               'Sch-Acc No.     CPF No.       Value Date     '.                 
           05  FILLER          PIC X(30)  VALUE                                 
               'Lumpsum Reqd     Lumpsum Paid'.                                 
           05  FILLER          PIC X(05)  VALUE SPACES.                         
                                                                                
       01  MAIL-BODY-TXT-7-ULINE.                                               
           05  FILLER          PIC X(45)  VALUE                                 
               '-----------     -------       ----------     '.                 
           05  FILLER          PIC X(30)  VALUE                                 
               '------------     ------------'.                                 
           05  FILLER          PIC X(05)  VALUE SPACES.                         
                                                                                
       01  MAIL-DETAIL-LINE.                                                    
           05  SCH-ACC         PIC X(11)  VALUE SPACES.                         
           05  FILLER          PIC X(05)  VALUE SPACES.                         
           05  CPF-ACC         PIC X(09)  VALUE SPACES.                         
           05  FILLER          PIC X(05)  VALUE SPACES.                         
           05  VALUE-DATE      PIC X(10)  VALUE SPACES.                         
           05  FILLER          PIC X(05)  VALUE SPACES.                         
           05  LUMPSUM-REQD    PIC $,$$$,$$9.99.                                
           05  FILLER          PIC X(05)  VALUE SPACES.                         
           05  LUMPSUM-PAID    PIC $,$$$,$$9.99.                                
           05  FILLER          PIC X(06)  VALUE SPACES.                         
                                                                                
       01  MAIL-BODY-BLANK-LINE.                                                
           05  FILLER          PIC X(80)  VALUE SPACES.                         
                                                                                
       01  MAIL-FOOTER-1.                                                       
           05  FILLER          PIC X(08)  VALUE '(Signed)'.                     
           05  FILLER          PIC X(72)  VALUE SPACES.                         
                                                                                
       01  MAIL-FOOTER-2.                                                       
           05  FILLER          PIC X(12)  VALUE 'Wee Lee Yean'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-FOOTER-3.                                                       
           05  FILLER          PIC X(14)  VALUE 'Agreement Team'.               
           05  FILLER          PIC X(66)  VALUE SPACES.                         
                                                                                
       01  MAIL-FOOTER-END.                                                     
           05  FILLER          PIC X      VALUE '.'.                            
                                                                                
                                                                                
      *----------------------*                                                  
       PROCEDURE DIVISION.                                                      
      *----------------------*                                                  
                                                                                
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13F354     THRU 2000-EXIT.                       
           PERFORM 3000-START-PROCESS     THRU 3000-EXIT                        
                                          UNTIL BP13F354-EOF.                   
           PERFORM 6000-END-PROCESS       THRU 6000-EXIT.                       
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
           OPEN INPUT  BP13K022                                                 
                       BP13K024                                                 
                       BP13F354                                                 
                OUTPUT BP13MAIL.                                                
                                                                                
           IF BP13K022-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K022 OPEN ERROR, STATUS ' BP13K022-STATUS             
             MOVE  BP13K022-STATUS         TO RETURN-CODE                       
             PERFORM 9000-CLOSE-FILES                                           
           END-IF.                                                              
                                                                                
           IF BP13K024-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K024 OPEN ERROR, STATUS ' BP13K024-STATUS             
             MOVE  BP13K024-STATUS         TO RETURN-CODE                       
             PERFORM 9000-CLOSE-FILES                                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-CURR-DATE.                   
           STRING WS-CURR-DATE(7:2), '/',                                       
                  WS-CURR-DATE(5:2), '/',                                       
                  WS-CURR-DATE(1:4)                                             
                  DELIMITED BY SIZE        INTO WS-EDIT-DATE                    
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-BP13F354.                                                      
      *----------------------------------------------------------------*        
           READ BP13F354 AT END                                                 
                          MOVE  'Y'    TO   BP13F354-EOF-TAG                    
                         NOT AT END                                             
                          ADD    1     TO   WS-F354-READ-CNT                    
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-START-PROCESS.                                                      
      *----------------------------------------------------------------*        
           IF F354-NUM-REGN NOT = WS-NUM-REGN                                   
              IF WS-F354-READ-CNT NOT = 1                                       
                 PERFORM 5400-SETUP-FOOTER   THRU 5400-EXIT                     
              END-IF                                                            
                                                                                
              MOVE F354-NUM-REGN             TO   K022-NUM-REGN                 
              PERFORM 4000-READ-BP13K022     THRU 4000-EXIT                     
              IF OIC-FOUND                                                      
                 MOVE K022-NUM-OIC           TO   K024-NUM-OIC                  
                 PERFORM 4200-READ-BP13K024  THRU 4200-EXIT                     
                 MOVE 'Y'                    TO   WS-EMAIL-TAG                  
              ELSE                                                              
                 MOVE SPACE                  TO   WS-EMAIL-TAG                  
              END-IF                                                            
                                                                                
              IF SEND-EMAIL                                                     
                 PERFORM 5000-SETUP-HEADER   THRU 5000-EXIT                     
                 PERFORM 5200-SETUP-DETAIL   THRU 5200-EXIT                     
                 ADD  1                      TO   WS-MAIL-SEND-CNT              
              END-IF                                                            
              MOVE F354-NUM-REGN             TO   WS-NUM-REGN                   
           ELSE                                                                 
              IF SEND-EMAIL                                                     
                 PERFORM 5200-SETUP-DETAIL   THRU 5200-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F354        THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-READ-BP13K022.                                                      
      *---------------------------------------------------------------*         
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
           EVALUATE BP13K022-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
                MOVE  'Y'              TO WS-OIC-TAG                            
             WHEN 23                                                            
                MOVE  'N'              TO WS-OIC-TAG                            
                ADD    1               TO WS-K022-NOTFND-CNT                    
                DISPLAY 'BP13K022 REC NOT FOUND, KEY : ' F354-NUM-REGN          
             WHEN OTHER                                                         
                DISPLAY ' BP13K022 READ ERROR, STATUS ' BP13K022-STATUS         
                        '  KEY : ' F354-NUM-REGN                                
                MOVE BP13K022-STATUS   TO RETURN-CODE                           
                PERFORM 9000-CLOSE-FILES                                        
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       4200-READ-BP13K024.                                                      
      *---------------------------------------------------------------*         
           READ BP13K024.                                                       
           EVALUATE BP13K024-STATUS                                             
             WHEN 00                                                            
                CONTINUE                                                        
             WHEN 23                                                            
                MOVE SPACES            TO K024-NME-OIC                          
                DISPLAY 'BP13K024 REC NOT FOUND, KEY : ' K022-NUM-OIC           
             WHEN OTHER                                                         
                DISPLAY ' BP13K024 READ ERROR, STATUS ' BP13K024-STATUS         
                        '  KEY : ' K022-NUM-OIC                                 
                MOVE BP13K024-STATUS   TO RETURN-CODE                           
                PERFORM 9000-CLOSE-FILES                                        
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       5000-SETUP-HEADER.                                                       
      *---------------------------------------------------------------*         
           STRING K022-NUM-OIC   DELIMITED BY SPACES                            
                  '@HDB.GOV.SG>' DELIMITED BY SIZE                              
                                   INTO TO-MAIL-ID                              
           END-STRING.                                                          
           MOVE TO-MAIL-ID         TO   TO-MAIL-ID-A.                           
                                                                                
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-1.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-2.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-3.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-4.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-5.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-6.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-7.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-8.                          
                                                                                
           MOVE K024-NME-OIC       TO   CSM-NAME.                               
           MOVE F354-NUM-REGN      TO   REGN-NO.                                
                                                                                
           WRITE MAIL-PRTREC       FROM MAIL-BODY-TXT-1.                        
           WRITE MAIL-PRTREC       FROM MAIL-BODY-TXT-1-ULINE.                  
           WRITE MAIL-PRTREC       FROM MAIL-BODY-BLANK-LINE.                   
           WRITE MAIL-PRTREC       FROM MAIL-BODY-TXT-2.                        
           WRITE MAIL-PRTREC       FROM MAIL-BODY-BLANK-LINE.                   
           WRITE MAIL-PRTREC       FROM MAIL-BODY-TXT-3.                        
           WRITE MAIL-PRTREC       FROM MAIL-BODY-TXT-4.                        
           WRITE MAIL-PRTREC       FROM MAIL-BODY-TXT-5.                        
           WRITE MAIL-PRTREC       FROM MAIL-BODY-BLANK-LINE.                   
           WRITE MAIL-PRTREC       FROM MAIL-BODY-TXT-6.                        
           WRITE MAIL-PRTREC       FROM MAIL-BODY-TXT-6-ULINE.                  
           WRITE MAIL-PRTREC       FROM MAIL-BODY-BLANK-LINE.                   
           WRITE MAIL-PRTREC       FROM MAIL-BODY-TXT-7.                        
           WRITE MAIL-PRTREC       FROM MAIL-BODY-TXT-7-ULINE.                  
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       5200-SETUP-DETAIL.                                                       
      *---------------------------------------------------------------*         
           MOVE SPACES     TO MAIL-DETAIL-LINE.                                 
           INITIALIZE         MAIL-DETAIL-LINE.                                 
                                                                                
           IF F354-NUM-REGN NOT = WS-NUM-REGN                                   
              MOVE F354-NUM-SCH-ACC    TO   SCH-ACC                             
           ELSE                                                                 
              MOVE SPACES              TO   SCH-ACC                             
           END-IF.                                                              
           MOVE F354-NUM-CPF           TO   CPF-ACC.                            
           MOVE F354-AMT-LUMPSUM-REQD  TO   LUMPSUM-REQD.                       
           MOVE F354-AMT-LUMPSUM-PAID  TO   LUMPSUM-PAID.                       
                                                                                
           STRING F354-DTE-VALUE (7:2) '/'                                      
                  F354-DTE-VALUE (5:2) '/'                                      
                  F354-DTE-VALUE (1:4)                                          
                  DELIMITED BY SIZE    INTO VALUE-DATE                          
           END-STRING.                                                          
                                                                                
           WRITE MAIL-PRTREC           FROM MAIL-DETAIL-LINE.                   
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       5400-SETUP-FOOTER.                                                       
      *---------------------------------------------------------------*         
           WRITE MAIL-PRTREC           FROM MAIL-BODY-BLANK-LINE.               
           WRITE MAIL-PRTREC           FROM MAIL-BODY-BLANK-LINE.               
           WRITE MAIL-PRTREC           FROM MAIL-BODY-BLANK-LINE.               
                                                                                
           WRITE MAIL-PRTREC           FROM MAIL-FOOTER-1.                      
           WRITE MAIL-PRTREC           FROM MAIL-FOOTER-2.                      
           WRITE MAIL-PRTREC           FROM MAIL-FOOTER-3.                      
                                                                                
           WRITE MAIL-PRTREC           FROM MAIL-FOOTER-END.                    
           WRITE MAIL-PRTREC           FROM MAIL-BODY-BLANK-LINE.               
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       6000-END-PROCESS.                                                        
      *----------------------------------------------------------------*        
           IF WS-MAIL-SEND-CNT > 0                                              
              PERFORM 5400-SETUP-FOOTER    THRU 5400-EXIT                       
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-FILES.                                                        
      *----------------------------------------------------------------*        
           DISPLAY ' '.                                                         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' BP13C417 - CONTROL LIST    DATE : ' WS-EDIT-DATE.          
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F354-READ-CNT     TO WS-EDIT-CNT.                            
           DISPLAY 'CASES READ FROM BP13F354    : ' WS-EDIT-CNT.                
           DISPLAY ' '.                                                         
           MOVE WS-K022-NOTFND-CNT   TO WS-EDIT-CNT.                            
           DISPLAY 'CASES NOT FND IN OIC FILE   : ' WS-EDIT-CNT.                
           DISPLAY ' '.                                                         
           MOVE WS-MAIL-SEND-CNT     TO WS-EDIT-CNT.                            
           DISPLAY 'CASES WRITTEN TO EMAIL FILE : ' WS-EDIT-CNT.                
           DISPLAY ' '.                                                         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
                                                                                
           CLOSE  BP13K022                                                      
                  BP13K024                                                      
                  BP13F354                                                      
                  BP13MAIL.                                                     
                                                                                
           IF BP13K022-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K022 CLOSE ERROR, STATUS ' BP13K022-STATUS            
             MOVE  BP13K022-STATUS         TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF BP13K024-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K024 CLOSE ERROR, STATUS ' BP13K024-STATUS            
             MOVE  BP13K024-STATUS         TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
      *                 END OF BP13C417                              *          
      *--------------------------------------------------------------*          
