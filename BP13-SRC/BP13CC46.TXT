       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC46.                                                 
      *AUTHOR.        ED1.                                                      
      *DATE-WRITTEN.  23/01/2007.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO MATCH FD30 WITH F205 TO UPDATE BP13KD30         *        
      *             WITH EQUAL DUE DATE                                *        
      *                                                                *        
      * INPUT  FILES : BP13FD30                                        *        
      *                BP13F205                                        *        
      * I-O    FILE  : BP13KD30                                        *        
      * OUTPUT FILE  : BP13LC46                                        *        
      *                                                                *        
      * CHG-NO    BY    ON    - DESCRIPTION                            *        
      * ---------------------------------                              *        
      * BP132936 ED1 01/02/07   NEW PROGRAM                            *        
      * BP133095 BA3 27/08/07   REMOVE PRINTING OF BLANK PAGE          *        
      * BP133209 ED1 30/01/08   CATER FOR SWAPPING OF PP-SCHED IN KD30 *        
      *                         CHK WS-IDX = KD30 PP SCHED BEFORE UPD  *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT   BP13FD30 ASSIGN TO BP13FD30.                                
           SELECT   BP13F205 ASSIGN TO BP13F205.                                
           SELECT   BP13LC46 ASSIGN TO BP13LC46.                                
                                                                                
           SELECT BP13KD30  ASSIGN       TO BP13KD30                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KD30-KEY-FLD                        
                            FILE STATUS  IS WS-KD30-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F205    BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 80 CHARACTERS                           
                        LABEL RECORDS ARE STANDARD                              
                        RECORDING MODE IS F.                                    
       COPY BP13F205.                                                           
                                                                                
       FD   BP13FD30    BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 2000 CHARACTERS                         
                        LABEL RECORDS ARE STANDARD                              
                        RECORDING MODE IS F.                                    
       COPY BP13FD30.                                                           
                                                                                
       FD   BP13KD30    RECORD CONTAINS 2000 CHARACTERS.                        
       COPY BP13KD30.                                                           
                                                                                
       FD   BP13LC46    BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 132 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01  LC46-PRTREC  PIC X(132).                                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  WS-KD30-UPD                  PIC 9(07) VALUE ZEROS.              
           05  WS-SNO                       PIC 9(5) VALUE ZEROES.              
           05  WS-PAGE-CTR                  PIC 9(4) VALUE ZEROES.              
           05  WS-LINE-CTR                  PIC 9(2) VALUE ZEROES.              
                                                                                
       01  WS-FD30-EOF                      PIC X     VALUE 'N'.                
       01  WS-F205-EOF                      PIC X     VALUE 'N'.                
       01  WS-UPD-REC                       PIC X     VALUE 'N'.                
       01  WS-KD30-FND                      PIC X     VALUE 'N'.                
       01  WS-KD30-STATUS                   PIC 99    VALUE ZEROS.              
       01  WS-KD30-IDX                      PIC 99    VALUE ZEROS.              
       01  WS-CNT                           PIC 99    VALUE ZEROS.              
       01  WS-CTR                           PIC 99    VALUE ZEROS.              
       01  WS-IDX                           PIC 99    VALUE ZEROS.              
       01  WS-AMT-PP-REQ                    PIC S9(6)V99 VALUE ZEROES.          
                                                                                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE                  PIC X(08) VALUE SPACES.             
           05  WS-SYS-TIME                  PIC X(08) VALUE SPACES.             
           05  FILLER                       PIC X(05) VALUE SPACES.             
                                                                                
       01  WS-DUE-DATE.                                                         
           05  WS-YR                       PIC 9(04) VALUE ZEROES.              
           05  WS-MTH                      PIC 9(02) VALUE ZEROES.              
           05  WS-DD                       PIC 9(02) VALUE ZEROES.              
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CUR-DD                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-MM                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-CCYY                 PIC 9(04) VALUE ZEROES.              
                                                                                
       01  WS-MAX-LINE               PIC 99    VALUE 56.                        
                                                                                
       01 WS-MONTH-NAME.                                                        
          05 WS-STRING PIC X(36) VALUE                                          
                'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                         
          05 WS-MONTH  REDEFINES WS-STRING OCCURS 12 TIMES PIC XXX.             
                                                                        01010000
       01  WS-BLANK-LINE  PIC X(132)             VALUE SPACES.                  
                                                                                
       01  WS-DASH-LINE   PIC X(132)             VALUE ALL '-'.                 
                                                                                
      *-- CONTROL REPORT BP13LC46 --------------------------------*             
                                                                                
       01  LC46-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13LC46'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LC46-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LC46-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  LC46-HEADER02.                                                       
           05  FILLER              PIC X(28)     VALUE SPACES.                  
           05  FILLER              PIC X(52)     VALUE                          
               'REPORT ON ACCOUNTS WITH UPDATED CHEQUE DELIVERY DATE'.          
           05  FILLER              PIC X(14)     VALUE                          
               ' FOR DUE DATE '.                                                
           05  LC46-DD-HDR         PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X         VALUE SPACE.                   
           05  LC46-MMM-HDR        PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X         VALUE SPACE.                   
           05  LC46-YR-HDR         PIC X(04)     VALUE SPACES.                  
                                                                                
       01  LC46-HEADER03.                                                       
           05  FILLER              PIC X(03)     VALUE 'S/N'.                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REGN NO'.               
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'PP REQ'.                
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PP LOAN'.               
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'LS-REQ'.                
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'LS-PAID'.               
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE 'DATE PP DUE'.           
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'FIN-DATE'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(16)     VALUE                          
                                                'DATE CHEQUE SENT'.             
       01 LC46-DETAILS.                                                         
          05 LC46-DTL-SERIAL      PIC ZZZZ9      VALUE ZEROES.                  
          05 FILLER               PIC X(01)      VALUE SPACES.                  
          05 LC46-REGNO           PIC X(08)      VALUE SPACES.                  
          05 FILLER               PIC X(02)      VALUE SPACES.                  
          05 LC46-PP-REQ          PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
          05 FILLER               PIC X(03)      VALUE SPACES.                  
          05 LC46-PP-LOAN         PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
          05 FILLER               PIC X(04)      VALUE SPACES.                  
          05 LC46-LS-REQ          PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
          05 FILLER               PIC X(04)      VALUE SPACES.                  
          05 LC46-LS-PAID         PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
          05 FILLER               PIC X(04)      VALUE SPACES.                  
          05 LC46-PP-DUE          PIC X(10)      VALUE SPACES.                  
          05 FILLER               PIC X(04)      VALUE SPACES.                  
          05 LC46-FIN-DATE        PIC X(10)      VALUE SPACES.                  
          05 FILLER               PIC X(04)      VALUE SPACES.                  
          05 LC46-CHQ-SENT        PIC X(10)      VALUE SPACES.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F205       THRU 2000-EXIT.                     
           PERFORM 2100-READ-BP13FD30       THRU 2100-EXIT.                     
           PERFORM 3000-PROCESS-RECORD   THRU 3000-EXIT                         
                            UNTIL WS-FD30-EOF = 'Y'.                            
                                                                                
           PERFORM 9000-CLOSING-ROUTINE     THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT BP13FD30                                                  
                      BP13F205                                                  
                I-O   BP13KD30                                                  
               OUTPUT BP13LC46.                                                 
                                                                                
           IF WS-KD30-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-KD30-STATUS ' WS-KD30-STATUS           
              MOVE     WS-KD30-STATUS       TO   RETURN-CODE                    
              PERFORM  9000-CLOSING-ROUTINE THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
           MOVE WS-SYS-DATE(1:4)      TO WS-CUR-CCYY.                           
           MOVE WS-SYS-DATE(5:2)      TO WS-CUR-MM.                             
           MOVE WS-SYS-DATE(7:2)      TO WS-CUR-DD.                             
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F205.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F205                                                        
             AT END MOVE 'Y'         TO WS-F205-EOF                             
             GO TO 2000-EXIT.                                                   
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BP13FD30.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FD30                                                        
             AT END MOVE 'Y'         TO  WS-FD30-EOF                            
             GO TO 2100-EXIT.                                                   
                                                                                
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
      *--> ONLY 1 REC IN BP13F205 IS COMPARED WITH BP13FD30 RECS                
                                                                                
           MOVE 'N'                          TO WS-UPD-REC.                     
           MOVE 'N'                          TO WS-KD30-FND.                    
           MOVE  ZEROES                      TO WS-IDX.                         
           MOVE  F205-NUM-SELECTION-NEW(1:2) TO WS-IDX.                         
                                                                                
           IF WS-IDX = SPACES OR LOW-VALUES OR ZEROES                           
              MOVE 'Y' TO WS-FD30-EOF                                           
              GO TO   3000-EXIT                                                 
           END-IF.                                                              
                                                                                
           PERFORM 4000-READ-BP13KD30     THRU 4000-EXIT.                       
                                                                                
           IF WS-UPD-REC = 'Y' AND WS-KD30-FND = 'Y'                            
              PERFORM 4150-MOVE-DATA         THRU 4150-EXIT                     
              PERFORM 4200-REWRITE-BP13KD30  THRU 4200-EXIT                     
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-BP13FD30  THRU 2100-EXIT.                          
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-READ-BP13KD30.                                                      
      *-------------------------------------------------------------            
                                                                                
           INITIALIZE               BP13KD30-REC.                               
           MOVE FD30-NUM-REGN    TO KD30-NUM-REGN.                              
                                                                                
           READ BP13KD30.                                                       
                                                                                
           EVALUATE WS-KD30-STATUS                                              
           WHEN 00                                                              
                MOVE 'Y'  TO  WS-KD30-FND                                       
                PERFORM 4100-VALIDATE-REC  THRU 4100-EXIT                       
           WHEN 23                                                              
                DISPLAY 'BP13KD30 REC NOTFND, KEY ' FD30-NUM-REGN               
           WHEN OTHER                                                           
                DISPLAY 'BP13KD30 READ ERR, STATUS ' WS-KD30-STATUS             
                        ', KEY ' FD30-NUM-REGN                                  
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-VALIDATE-REC.                                                       
      *-------------------------------------------------------------            
           MOVE 0 TO WS-KD30-IDX.                                               
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1                                   
                     UNTIL WS-CNT > 15                                          
             IF KD30-NUM-PP-SCHDL(WS-CNT) = WS-IDX                              
                MOVE WS-CNT TO WS-KD30-IDX                                      
             END-IF                                                             
           END-PERFORM.                                                         
                                                                                
           IF FD30-AMT-PP-REQ(WS-KD30-IDX) IS NOT NUMERIC                       
              MOVE ZEROES              TO FD30-AMT-PP-REQ(WS-KD30-IDX)          
           END-IF.                                                              
           IF FD30-AMT-PP-LOAN(WS-KD30-IDX) IS NOT NUMERIC                      
              MOVE ZEROES              TO FD30-AMT-PP-LOAN(WS-KD30-IDX)         
           END-IF.                                                              
           IF FD30-AMT-PP-PAID(WS-KD30-IDX) IS NOT NUMERIC                      
              MOVE ZEROES              TO FD30-AMT-PP-PAID(WS-KD30-IDX)         
           END-IF.                                                              
                                                                                
           MOVE ZEROES TO          WS-AMT-PP-REQ.                               
                                                                                
           COMPUTE WS-AMT-PP-REQ = FD30-AMT-PP-REQ(WS-KD30-IDX) -               
                                   FD30-AMT-PP-LOAN(WS-KD30-IDX).               
                                                                                
           IF ((FD30-DTE-FIN-PROCESS(WS-KD30-IDX) NOT =                         
                        SPACES AND LOW-VALUES AND ZEROES)          AND          
               (FD30-AMT-PP-PAID(WS-KD30-IDX)    = WS-AMT-PP-REQ)  AND          
               (FD30-DTE-PP-DUE(WS-KD30-IDX)     = F205-DTE-ALLOCN))            
                                                                                
               MOVE 'Y'  TO WS-UPD-REC                                          
                                                                                
           END-IF.                                                              
                                                                                
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4150-MOVE-DATA.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE F205-DTE-ALLOCN-NEW   TO                                        
                                      KD30-DTE-CHEQUE-SENT(WS-KD30-IDX).        
           MOVE WS-SYS-DATE           TO KD30-DTE-UPDATE.                       
           MOVE WS-SYS-TIME           TO KD30-TME-UPDATE.                       
           MOVE 'BP13CC46'            TO KD30-NUM-USERID.                       
                                                                                
                                                                                
       4150-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4200-REWRITE-BP13KD30.                                                   
      *-------------------------------------------------------------            
                                                                                
           REWRITE BP13KD30-REC.                                                
                                                                                
           EVALUATE WS-KD30-STATUS                                              
           WHEN 00                                                              
                ADD 1 TO WS-KD30-UPD                                            
                PERFORM 5000-MOVE-REPORT-DTLS THRU 5000-EXIT                    
           WHEN OTHER                                                           
                DISPLAY 'BP13KD30 REWRITE ERR, STATUS ' WS-KD30-STATUS          
                        ', KEY ' FD30-NUM-REGN                                  
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-MOVE-REPORT-DTLS.                                                   
      *-------------------------------------------------------------            
                                                                                
           INITIALIZE  LC46-DETAILS.                                            
                                                                                
           MOVE SPACES                     TO  LC46-DD-HDR.                     
           MOVE SPACES                     TO  LC46-MMM-HDR.                    
           MOVE SPACES                     TO  LC46-YR-HDR.                     
           MOVE SPACES                     TO  WS-DUE-DATE.                     
           MOVE KD30-DTE-PP-DUE(WS-KD30-IDX) TO  WS-DUE-DATE                    
                                               LC46-PP-DUE.                     
           MOVE WS-DD                      TO  LC46-DD-HDR.                     
           MOVE WS-MONTH(WS-MTH)           TO  LC46-MMM-HDR.                    
           MOVE WS-YR                      TO  LC46-YR-HDR.                     
           MOVE KD30-NUM-REGN              TO  LC46-REGNO.                      
           MOVE KD30-AMT-PP-REQ(WS-KD30-IDX)  TO  LC46-PP-REQ.                  
           MOVE KD30-AMT-PP-LOAN(WS-KD30-IDX) TO  LC46-PP-LOAN.                 
           MOVE WS-AMT-PP-REQ                 TO  LC46-LS-REQ.                  
           MOVE KD30-AMT-PP-PAID(WS-KD30-IDX) TO  LC46-LS-PAID.                 
           MOVE KD30-DTE-FIN-PROCESS(WS-KD30-IDX)                               
                                           TO  LC46-FIN-DATE.                   
           MOVE KD30-DTE-CHEQUE-SENT(WS-KD30-IDX)                               
                                           TO  LC46-CHQ-SENT.                   
                                                                                
           PERFORM 6000-PRINT-REPORT       THRU 6000-EXIT.                      
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       6000-PRINT-REPORT.                                                       
      *--------------------------------------------------------------           
                                                                                
           ADD   1                      TO  WS-SNO.                             
           MOVE  WS-SNO                 TO  LC46-DTL-SERIAL.                    
           MOVE  WS-CURRENT-DATE        TO  LC46-DATE-HDR.                      
                                                                                
           IF WS-LINE-CTR > WS-MAX-LINE OR                                      
              WS-SNO       = 1                                                  
              ADD            1   TO WS-PAGE-CTR                                 
              MOVE ZEROES        TO LC46-PAGE-HDR                               
              MOVE WS-PAGE-CTR   TO LC46-PAGE-HDR                               
              PERFORM 7000-PRINT-HEADING THRU 7000-EXIT                         
           END-IF.                                                              
                                                                                
           WRITE LC46-PRTREC FROM LC46-DETAILS.                                 
           WRITE LC46-PRTREC FROM WS-BLANK-LINE.                                
                                                                                
           ADD 2 TO WS-LINE-CTR.                                                
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       7000-PRINT-HEADING.                                                      
      *--------------------------------------------------------------           
                                                                                
            WRITE LC46-PRTREC FROM WS-BLANK-LINE AFTER PAGE.                    
            WRITE LC46-PRTREC FROM WS-BLANK-LINE AFTER 3.                       
            WRITE LC46-PRTREC FROM LC46-HEADER01.                               
            WRITE LC46-PRTREC FROM LC46-HEADER02.                               
            WRITE LC46-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC46-PRTREC FROM LC46-HEADER03.                               
            WRITE LC46-PRTREC FROM WS-DASH-LINE.                                
                                                                                
            INITIALIZE WS-LINE-CTR.                                             
            MOVE  9 TO WS-LINE-CTR.                                             
                                                                                
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CC46'.                                
           DISPLAY 'RUN DATE       : ' WS-CURRENT-DATE.                         
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BP13KD30 REWRITTEN: ' WS-KD30-UPD.                   
                                                                                
           CLOSE BP13FD30                                                       
                 BP13F205                                                       
                 BP13KD30                                                       
                 BP13LC46.                                                      
                                                                                
           IF WS-KD30-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING ERROR, WS-KD30-STATUS ' WS-KD30-STATUS           
              MOVE     WS-KD30-STATUS       TO   RETURN-CODE                    
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
