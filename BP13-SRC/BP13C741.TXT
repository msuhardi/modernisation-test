       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C741.                                                  
      *AUTHOR.       RAYATHATHA ASHOK.                                          
      *DATE-WRITTEN. 03.06.92.                                                  
      *REMARKS.                                                                 
      ************************************************************              
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                      *              
      *                                                          *              
      *  SYSTEM ID   : BP13                                      *              
      *                                                          *              
      *  OBJECTIVE   : TO OUTPUT PRIORITY CASES FOR PIGEONHOLING.*              
      *                1) MTF, SFF, RH, REN                      *              
      *                2) CCC (GRO)                              *              
      *                                                          *              
      *            FOR NT WITH LAND                              *              
      *                AFTER PRIORITY PIGEONHOLING, THE UNSUCC   *              
      *                CASES CAN BE PIGEONHOLE UNDER PUBLIC IN   *              
      *                TRANSITIONAL, OR NHH OR COHORT ACCORDINGLY*              
      *                                                          *              
      *            FOR MATURE NT                                 *              
      *                AFTER PRIORITY PIGEONHOLING, THE UNSUCC   *              
      *                CASES CAN BE PIGEONHOLE UNDER PUBLIC.     *              
      *                                                          *              
      *  INPUT FILE  : BP13F520                                  *              
      *                                                          *              
      *  OUTPUT FILES: BP13PR                                    *              
      *              : BP13GRO                                   *              
      *                                                          *              
      * CHG-NO  BY   ON        DESCRIPTION                       *              
      * ----------------------------------                       *              
      * I920718 LJL  3/4/93 - TO CONVERT TO COBOLII.             *              
      * C950543 LSB 22/5/95 - DO NOT OUTPUT JB TO PRIORITY FILE  *              
      *BP130652 NOR 20/2/99 - EXPAND F520 RECL FROM 300 TO 500   *              
      *BP134483 ZDD 05/4/12 - ADD MGP AND PORT-TAG               *              
      *BP134718 SMR 031212  - CHECK CONDITION FOR GRO BEFORE MGP *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
           SELECT BP13PR   ASSIGN TO BP13PR.                                    
           SELECT BP13GRO  ASSIGN TO BP13GRO.                                   
           SELECT BP13PUB  ASSIGN TO BP13PUB.                                   
           SELECT BP13MGP  ASSIGN TO BP13MGP.                                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F520                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       COPY BP13F520.                                                           
                                                                                
       FD  BP13PR                                                               
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  BP13PR-REC.                                                          
           05 BP13PR-DETAIL              PIC X(500).                            
                                                                                
       FD  BP13GRO                                                              
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  BP13GRO-REC.                                                         
           05 BP13GRO-DETAIL             PIC X(500).                            
                                                                                
       FD  BP13PUB                                                              
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  BP13PUB-REC.                                                         
           05 BP13PUB-DETAIL              PIC X(500).                           
                                                                                
       FD  BP13MGP                                                              
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  BP13MGP-REC.                                                         
           05 BP13MGP-DETAIL              PIC X(500).                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
                                                                                
       01  WS-ALLOCSCH-TYPE              PIC X(3)  VALUE SPACES.                
       COPY PRIOSCH.                                                            
                                                                                
       01  WS-F520-CNT                   PIC 9(6)  VALUE ZERO.                  
       01  WS-PR-CNT                     PIC 9(6)  VALUE ZERO.                  
       01  WS-PUB-CNT                    PIC 9(6)  VALUE ZERO.                  
       01  WS-GRO-CNT                    PIC 9(6)  VALUE ZERO.                  
       01  WS-MGP-CNT                    PIC 9(6)  VALUE ZERO.                  
       01  WS-EOF-F520                   PIC X     VALUE 'N'.                   
           88  EOF-OF-F520                         VALUE 'Y'.                   
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-LOGIC.                                                         
                                                                                
           PERFORM 1000-INIT-ROUTINE     THRU 1000-EXIT.                        
           PERFORM 2000-READ-F520        THRU 2000-EXIT.                        
           PERFORM 3000-MAIN-ROUTINE     THRU 3000-EXIT                         
                   UNTIL EOF-OF-F520.                                           
           PERFORM 4000-CLOSE-ROUTINE    THRU 4000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-INIT-ROUTINE.                                                       
                                                                                
           OPEN INPUT  BP13F520                                                 
               OUTPUT  BP13PR                                                   
                       BP13GRO                                                  
                       BP13PUB                                                  
                       BP13MGP.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-F520.                                                          
                                                                                
           READ BP13F520                 AT END                                 
           MOVE 'Y'                      TO WS-EOF-F520                         
           GO TO 2000-EXIT.                                                     
                                                                                
           ADD 1 TO WS-F520-CNT.                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-MAIN-ROUTINE.                                                       
                                                                                
           PERFORM 3100-OUTPUT-PRIORITY THRU 3100-EXIT.                         
           IF WS-ALLOCSCH-TYPE = 'CCC'                                          
               MOVE BP13F520-REC TO BP13GRO-DETAIL                              
               WRITE BP13GRO-REC                                                
               ADD  1                     TO WS-GRO-CNT                         
           ELSE                                                                 
           IF F520-NUM-MGPS = 'P' OR 'C'                                        
               MOVE BP13F520-REC TO BP13MGP-DETAIL                              
               WRITE BP13MGP-REC                                                
               ADD  1                     TO WS-MGP-CNT                         
           ELSE                                                                 
           IF F520-NUM-PORT-ELIG-TAG = 'P'                                      
               MOVE BP13F520-REC TO BP13PR-DETAIL                               
               WRITE BP13PR-REC                                                 
               ADD  1                     TO WS-PR-CNT                          
           ELSE                                                                 
           IF WS-ALLOCSCH-TYPE = 'PA ' OR 'JB'                                  
               MOVE BP13F520-REC TO BP13PUB-DETAIL                              
               WRITE BP13PUB-REC                                                
               ADD  1                     TO WS-PUB-CNT                         
           ELSE                                                                 
               MOVE BP13F520-REC TO BP13PR-DETAIL                               
               WRITE BP13PR-REC                                                 
               ADD  1                     TO WS-PR-CNT.                         
                                                                                
           PERFORM 2000-READ-F520        THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3100-OUTPUT-PRIORITY.                                                    
           SET WS-ALLOC-PTR TO 1.                                               
                                                                                
           SEARCH WS-ALLOC-SCH                                                  
              AT END MOVE 'XXX' TO WS-ALLOCSCH-TYPE                             
              WHEN F520-CDE-ALLOC-SCH =                                         
                                WS-ALLOC-SCH-DESP(WS-ALLOC-PTR)                 
              MOVE WS-MAIN-SCH-CD(WS-ALLOC-PTR) TO                              
                                      WS-ALLOCSCH-TYPE.                         
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4000-CLOSE-ROUTINE.                                                      
                                                                                
           DISPLAY '  '.                                                        
           DISPLAY '***** SUMMARY REPORT FOR BP13C741 *****'.                   
           DISPLAY 'EXTRACT PRIORITY REQUEST FROM BALLOT FILE'.                 
           DISPLAY '  '.                                                        
           DISPLAY 'TOTAL NO OF REC READ FROM BP13F520     = '                  
                   WS-F520-CNT.                                                 
           DISPLAY 'TOTAL MTF/SFF/RH/REN/RDC WRITTEN IN BP13PR = '              
                   WS-PR-CNT.                                                   
           DISPLAY 'TOTAL GRO      REC  WRITTEN IN BP13GRO = '                  
                   WS-GRO-CNT.                                                  
           DISPLAY 'TOTAL PUBLIC   REC  WRITTEN IN BP13PUB = '                  
                   WS-PUB-CNT.                                                  
           DISPLAY 'TOTAL MGP      REC  WRITTEN IN BP13MGP = '                  
                   WS-MGP-CNT.                                                  
                                                                                
           CLOSE  BP13F520                                                      
                  BP13PR                                                        
                  BP13GRO                                                       
                  BP13PUB                                                       
                  BP13MGP.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
