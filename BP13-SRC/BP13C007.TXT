      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C007.                                                 
      *AUTHOR.        MTDULLONA.                                                
      *DATE-WRITTEN.  APRIL2004.                                                
      *****************************************************************         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *****************************************************************         
      *  OBJECTIVE   :  PRINT APPOINTMENT LETTER FOR WIS              *         
      *---------------------------------------------------------------*         
      *  INPUT FILES : 1.  BP13F205 - CONTROL FILE                    *         
      *                2.  BP13K225 - QUEUE FILE                      *         
      *                                                               *         
      *  LETTER      : 1.  BP13L007 - APPOINTMENT LETTER (BP13F0)     *         
      *---------------------------------------------------------------*         
      * CHG REF# BY    DATE      DESCRIPTION                          *         
      * -------- ---- --------   -------------------------------------*         
      * BP132501 MTD  04/02/2004 PRINT APPMT LETTER FOR WIS           *         
      * BP132621 MTD  19/20/2004 ADD DAY OF THE WEEK                  *         
      * BP132682 JF4  06/04/2005 PRINT APPMT LETTER FOR WIS (SITE)    *         
      *****************************************************************         
      *---------------------------------------------------------------*         
       ENVIRONMENT DIVISION.                                                    
      *---------------------------------------------------------------*         
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN         TO BP13F205.                          
                                                                                
           SELECT BP13K225  ASSIGN        TO BP13K225                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K225-KEY-FLD                       
                            ALTERNATE KEY IS K225-ALT-KEY                       
                            FILE STATUS   IS WS-K225-STATUS.                    
                                                                                
           SELECT BP13L007  ASSIGN        TO BP13L007.                          
                                                                                
      *---------------------------------------------------------------*         
       DATA DIVISION.                                                           
      *---------------------------------------------------------------*         
       FILE SECTION.                                                            
                                                                                
       FD   BP13F205                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 80 CHARACTERS.                                      
       COPY BP13F205.                                                           
                                                                                
       FD   BP13K225                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K225.                                                           
                                                                                
       FD  BP13L007                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13L007-REC                    PIC X(132).                          
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01  WS-WORK-VARIABLES.                                                   
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-DATE             PIC X(08) VALUE SPACES.              
               10  FILLER                  PIC X(13) VALUE SPACES.              
           05  WS-DISP-DATE.                                                    
               10  WS-DISP-DD              PIC X(02) VALUE SPACES.              
               10  FILLER                  PIC X(01) VALUE '/'.                 
               10  WS-DISP-MM              PIC X(02) VALUE SPACES.              
               10  FILLER                  PIC X(01) VALUE '/'.                 
               10  WS-DISP-CCYY            PIC X(04) VALUE SPACES.              
           05  WS-NME-LAUNCH-MO            PIC X(09) VALUE SPACES.              
           05  WS-NUM-QUEUE                PIC X(08) VALUE SPACES.              
           05  WS-TIME-HH                  PIC X(02) VALUE SPACES.              
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K225-STATUS              PIC 9(02) VALUE ZEROS.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F205-READ-CTR            PIC 9(03) VALUE ZEROES.              
           05  WS-PRT-CTR                  PIC 9(06) VALUE ZEROES.              
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-BP13F205-EOF-SW          PIC X(01) VALUE SPACES.              
               88  C-BP13F205-EOF          VALUE 'Y'.                           
           05  WS-BP13K225-EOF-SW          PIC X(01) VALUE SPACES.              
               88  C-BP13K225-EOF          VALUE 'Y'.                           
           05  WS-K225-REC-FND-SW          PIC X(01) VALUE SPACES.              
               88  C-K225-REC-FND          VALUE 'Y'.                           
           05  WS-K225-FIRST-READ-SW       PIC X(01) VALUE SPACES.              
               88  C-K225-FIRST-READ       VALUE 'Y'.                           
                                                                                
      *---------------------------------------------------------*               
      *    MONTH TABLE                                          *               
      *---------------------------------------------------------*               
       01  WS-MONTH-TABLE.                                                      
           05  FILLER             PIC X(11) VALUE '01JANUARY  '.                
           05  FILLER             PIC X(11) VALUE '02FEBRUARY '.                
           05  FILLER             PIC X(11) VALUE '03MARCH    '.                
           05  FILLER             PIC X(11) VALUE '04APRIL    '.                
           05  FILLER             PIC X(11) VALUE '05MAY      '.                
           05  FILLER             PIC X(11) VALUE '06JUNE     '.                
           05  FILLER             PIC X(11) VALUE '07JULY     '.                
           05  FILLER             PIC X(11) VALUE '08AUGUST   '.                
           05  FILLER             PIC X(11) VALUE '09SEPTEMBER'.                
           05  FILLER             PIC X(11) VALUE '10OCTOBER  '.                
           05  FILLER             PIC X(11) VALUE '11NOVEMBER '.                
           05  FILLER             PIC X(11) VALUE '12DECEMBER '.                
       01  FILLER REDEFINES       WS-MONTH-TABLE.                               
           05  WS-MONTH-TBL       OCCURS 12 INDEXED BY WS-MTH-IDX.              
               10  WS-NUM-MONTH   PIC X(02).                                    
               10  WS-NME-MONTH   PIC X(09).                                    
                                                                                
      *---------------------------------------------------------*               
      *    TIME TABLE                                           *               
      *---------------------------------------------------------*               
       01  WS-TIME-TABLE.                                                       
           05  FILLER             PIC X(04) VALUE '0707'.                       
           05  FILLER             PIC X(04) VALUE '0808'.                       
           05  FILLER             PIC X(04) VALUE '0909'.                       
           05  FILLER             PIC X(04) VALUE '1010'.                       
           05  FILLER             PIC X(04) VALUE '1111'.                       
           05  FILLER             PIC X(04) VALUE '1212'.                       
           05  FILLER             PIC X(04) VALUE '1301'.                       
           05  FILLER             PIC X(04) VALUE '1402'.                       
           05  FILLER             PIC X(04) VALUE '1503'.                       
           05  FILLER             PIC X(04) VALUE '1604'.                       
           05  FILLER             PIC X(04) VALUE '1705'.                       
           05  FILLER             PIC X(04) VALUE '1806'.                       
           05  FILLER             PIC X(04) VALUE '1907'.                       
           05  FILLER             PIC X(04) VALUE '2008'.                       
           05  FILLER             PIC X(04) VALUE '2109'.                       
           05  FILLER             PIC X(04) VALUE '2210'.                       
       01  FILLER REDEFINES       WS-TIME-TABLE.                                
           05  WS-TIME-TBL        OCCURS 16 INDEXED BY WS-TIME-IDX.             
               10  WS-TIME-ORG    PIC X(02).                                    
               10  WS-TIME-EQV    PIC X(02).                                    
                                                                                
      *------------------------------------------------------------*            
      *  BP13C908 PARAMETERS                                       *            
      *------------------------------------------------------------*            
       01  WS-C908-PARAMETERS.                                                  
           05  WS-INPUT-PARAMETERS.                                             
               10  WS-C908-TXT-IN-SIZE       PIC 9(2).                          
               10  WS-C908-TXT-INPUT         PIC X(30).                         
           05  WS-OUTPUT-PARAMETERS.                                            
               10  WS-C908-TXT-OUT-SIZE      PIC 9(2).                          
               10  WS-C908-TXT-OUTPUT        PIC X(40).                         
           05  WS-C908-RETCODE               PIC S9(04)  COMP.                  
                                                                                
      *------------------------------------------------------------*            
      *  AV02C001 PARAMETERS                                       *            
      *------------------------------------------------------------*            
       COPY AV02COMM.                                                           
                                                                                
      *------------------------------------------------------------*            
      *  DJDE LINE FOR WIS APPMT LETTER                            *            
      *------------------------------------------------------------*            
       01  DJDE-LINE-APPMT-H              PIC X(80)  VALUE                      
           '$DJDE$ FORMS=BP13F0,END;'.                                          
       01  DJDE-LINE-APPMT-S              PIC X(80)  VALUE                      
           '$DJDE$ FORMS=BP13AG,END;'.                                          
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR LETTER TO APPLICANT                     *            
      *------------------------------------------------------------*            
       01  L007-PRINT-LINES.                                                    
           05  L007-LINE-01.                                                    
               10  FILLER                   PIC X(72) VALUE SPACES.             
               10  L007-NUM-QUEUE-01        PIC X(05) VALUE SPACES.             
                                                                                
           05  L007-LINE-02.                                                    
               10  FILLER                   PIC X(72) VALUE SPACES.             
               10  L007-CUR-DATE            PIC X(10) VALUE SPACES.             
                                                                                
           05  L007-LINE-03.                                                    
               10  FILLER                   PIC X(31) VALUE SPACES.             
               10  L007-WIS-TOWN            PIC X(40) VALUE SPACES.             
                                                                                
           05  L007-LINE-04.                                                    
               10  FILLER                   PIC X(20) VALUE SPACES.             
               10  L007-LAUNCH-DATE         PIC X(14) VALUE SPACES.             
                                                                                
           05  L007-LINE-05.                                                    
               10  FILLER                   PIC X(40) VALUE SPACES.             
               10  L007-NUM-QUEUE-05        PIC X(05) VALUE SPACES.             
                                                                                
           05  L007-LINE-06.                                                    
               10  FILLER                   PIC X(40) VALUE SPACES.             
               10  L007-DTE-APPMT           PIC X(10) VALUE SPACES.             
               10  FILLER                   PIC X(01) VALUE SPACES.             
               10  L007-DAY-APPMT           PIC X(09) VALUE SPACES.             
                                                                                
           05  L007-LINE-07.                                                    
               10  FILLER                   PIC X(40) VALUE SPACES.             
               10  L007-TME-APPMT.                                              
                   15  L007-TME-APPMT-HHMM  PIC X(05) VALUE SPACES.             
                   15  FILLER               PIC X(01) VALUE SPACES.             
                   15  L007-TME-APPMT-AMPM  PIC X(02) VALUE SPACES.             
                                                                                
           05  L007-LINE-BARCODE.                                               
               10  FILLER                   PIC X(66) VALUE SPACES.             
               10  L007-BARCODE-QUEUE       PIC X(13) VALUE SPACES.             
                                                                                
           05  L007-LINE-BARCODE1.                                              
               10  FILLER                   PIC X(75) VALUE SPACES.             
               10  L007-BARCODE1-PREFIX     PIC X(02) VALUE SPACES.             
               10  L007-BARCODE1-QUEUE      PIC X(08) VALUE SPACES.             
                                                                                
      *---------------------------------------------------------------*         
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
      *-------------------*                                                     
       0000-MAIN-ROUTINE.                                                       
      *-------------------*                                                     
           PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                  
           PERFORM 2000-PROCESS-RECORDS        THRU 2000-EXIT                   
             UNTIL C-BP13F205-EOF.                                              
           PERFORM 9000-CLOSE-FILES            THRU 9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       1000-OPEN-FILES.                                                         
      *-----------------*                                                       
                                                                                
           OPEN INPUT BP13F205                                                  
                      BP13K225                                                  
               OUTPUT BP13L007.                                                 
                                                                                
           IF WS-K225-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR: BP13K225 STATUS ' WS-K225-STATUS             
              MOVE WS-K225-STATUS               TO RETURN-CODE                  
              PERFORM 9000-CLOSE-FILES          THRU 9000-EXIT                  
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       2000-PROCESS-RECORDS.                                                    
      *----------------------*                                                  
                                                                                
           READ BP13F205   AT END                                               
                MOVE 'Y'   TO WS-BP13F205-EOF-SW                                
                GO TO 2000-EXIT.                                                
                                                                                
           ADD  1  TO  WS-F205-READ-CTR.                                        
                                                                                
           IF WS-F205-READ-CTR = 1                                              
              PERFORM 2010-PRT-BLANK-PAGE         THRU 2010-EXIT                
           END-IF.                                                              
                                                                                
           MOVE SPACES             TO WS-BP13K225-EOF-SW                        
           MOVE 'Y'                TO WS-K225-REC-FND-SW                        
                                      WS-K225-FIRST-READ-SW.                    
                                                                                
           MOVE SPACES             TO K225-ALT-KEY.                             
           MOVE F205-DTE-START     TO K225-DTE-APPOINTMENT.                     
           PERFORM 2100-START-BP13K225            THRU 2100-EXIT.               
           PERFORM 2200-READNXT-BP13K225          THRU 2200-EXIT                
             UNTIL C-BP13K225-EOF OR                                            
                   NOT C-K225-REC-FND.                                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       2010-PRT-BLANK-PAGE.                                                     
      *---------------------*                                                   
                                                                                
           MOVE  SPACES       TO   BP13L007-REC.                                
           EVALUATE F205-CDE-LOCATION                                           
            WHEN 'H'                                                            
              WRITE BP13L007-REC      FROM DJDE-LINE-APPMT-H AFTER PAGE         
            WHEN 'S'                                                            
              WRITE BP13L007-REC      FROM DJDE-LINE-APPMT-S AFTER PAGE         
           END-EVALUATE.                                                        
           MOVE  SPACES       TO   BP13L007-REC.                                
           WRITE BP13L007-REC      AFTER 1.                                     
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       2100-START-BP13K225.                                                     
      *---------------------*                                                   
                                                                                
           START BP13K225 KEY NOT LESS THAN K225-ALT-KEY.                       
           EVALUATE WS-K225-STATUS                                              
              WHEN 00                                                           
                 MOVE 'Y'        TO WS-K225-REC-FND-SW                          
                                                                                
              WHEN 23                                                           
                 MOVE 'N'        TO WS-K225-REC-FND-SW                          
                 DISPLAY 'APPMT DATE NOT FOUND IN BP13K225: '                   
                          F205-DTE-START '-' F205-DTE-END                       
                                                                                
              WHEN OTHER                                                        
                 DISPLAY 'START READ ERROR BP13K225,STATUS '                    
                          WS-K225-STATUS                                        
                 MOVE WS-K225-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       2200-READNXT-BP13K225.                                                   
      *-----------------------*                                                 
                                                                                
           READ BP13K225 NEXT RECORD AT END                                     
                MOVE 'Y'            TO WS-BP13K225-EOF-SW                       
                GO TO 2200-EXIT.                                                
                                                                                
           EVALUATE WS-K225-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 PERFORM 2210-CHECK-APPMT-DATE        THRU 2210-EXIT            
                                                                                
              WHEN 10                                                           
                 MOVE 'Y'        TO WS-BP13K225-EOF-SW                          
                                                                                
              WHEN OTHER                                                        
                 DISPLAY 'READNEXT ERROR:BP13K225 STAT ' WS-K225-STATUS         
                 MOVE WS-K225-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-FILES             THRU 9000-EXIT            
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------*                                               
       2210-CHECK-APPMT-DATE.                                                   
      *-------------------------*                                               
                                                                                
           IF (K225-DTE-APPOINTMENT >= F205-DTE-START) AND                      
              (K225-DTE-APPOINTMENT <= F205-DTE-END)                            
              MOVE 'N'        TO WS-K225-FIRST-READ-SW                          
              PERFORM 3000-PRINT-APPMT-LETTER     THRU 3000-EXIT                
           ELSE                                                                 
              IF K225-DTE-APPOINTMENT > F205-DTE-END                            
                 IF C-K225-FIRST-READ                                           
                    DISPLAY 'APPMT DATE NOT FOUND IN BP13K225: '                
                             F205-DTE-START '-' F205-DTE-END                    
                 END-IF                                                         
                 MOVE 'Y'     TO WS-BP13K225-EOF-SW                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------*                                               
       3000-PRINT-APPMT-LETTER.                                                 
      *-------------------------*                                               
                                                                                
           PERFORM 3100-GET-DATA                 THRU 3100-EXIT.                
                                                                                
           MOVE  SPACES       TO   BP13L007-REC.                                
           EVALUATE F205-CDE-LOCATION                                           
            WHEN 'H'                                                            
              WRITE BP13L007-REC      FROM DJDE-LINE-APPMT-H AFTER PAGE         
            WHEN 'S'                                                            
              WRITE BP13L007-REC      FROM DJDE-LINE-APPMT-S AFTER PAGE         
           END-EVALUATE.                                                        
                                                                                
           MOVE  SPACES       TO   BP13L007-REC.                                
           WRITE BP13L007-REC  FROM L007-LINE-01 AFTER 6.                       
      ***  WRITE BP13L007-REC  FROM L007-LINE-02 AFTER 1.                       
           WRITE BP13L007-REC  FROM L007-LINE-03 AFTER 11.                      
           WRITE BP13L007-REC  FROM L007-LINE-04 AFTER 2.                       
           WRITE BP13L007-REC  FROM L007-LINE-05 AFTER 11.                      
           WRITE BP13L007-REC  FROM L007-LINE-06 AFTER 2.                       
           WRITE BP13L007-REC  FROM L007-LINE-07 AFTER 2.                       
                                                                                
           WRITE BP13L007-REC  FROM L007-LINE-BARCODE  AFTER 25.                
           WRITE BP13L007-REC  FROM L007-LINE-BARCODE1 AFTER 1.                 
                                                                                
           ADD  1 TO WS-PRT-CTR.                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       3100-GET-DATA.                                                           
      *----------------*                                                        
                                                                                
           MOVE SPACES                 TO L007-PRINT-LINES.                     
                                                                                
           MOVE K225-NUM-QUEUE         TO L007-NUM-QUEUE-01                     
                                          L007-NUM-QUEUE-05.                    
           MOVE WS-DISP-DATE           TO L007-CUR-DATE.                        
           MOVE K225-NME-TOWN          TO L007-WIS-TOWN.                        
                                                                                
           PERFORM 3110-GET-LAUNCH-DATE            THRU 3110-EXIT.              
                                                                                
           STRING K225-DTE-APPOINTMENT (7:2) '/'                                
                  K225-DTE-APPOINTMENT (5:2) '/'                                
                  K225-DTE-APPOINTMENT (1:4)                                    
                  DELIMITED BY SIZE  INTO L007-DTE-APPMT                        
           END-STRING.                                                          
                                                                                
           PERFORM 3140-GET-DAY-OF-WEEK            THRU 3140-EXIT.              
           PERFORM 3120-GET-APPMT-TIME             THRU 3120-EXIT.              
           PERFORM 3130-GET-QUEUE-BARCODE          THRU 3130-EXIT.              
                                                                                
           MOVE WS-NUM-QUEUE         TO L007-BARCODE1-QUEUE.                    
           MOVE 'SL'                 TO L007-BARCODE1-PREFIX.                   
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3110-GET-LAUNCH-DATE.                                                    
      *---------------------*                                                   
                                                                                
           MOVE SPACES          TO WS-NME-LAUNCH-MO.                            
                                                                                
           SET WS-MTH-IDX  TO 1.                                                
           SEARCH WS-MONTH-TBL                                                  
              AT END                                                            
                 MOVE SPACES                     TO WS-NME-LAUNCH-MO            
              WHEN K225-DTE-MONTH-LAUNCH = WS-NUM-MONTH (WS-MTH-IDX)            
                 MOVE WS-NME-MONTH (WS-MTH-IDX)  TO WS-NME-LAUNCH-MO            
           END-SEARCH.                                                          
                                                                                
           STRING WS-NME-LAUNCH-MO ' '                                          
                  K225-DTE-YEAR-LAUNCH                                          
                  DELIMITED BY '  '   INTO L007-LAUNCH-DATE                     
           END-STRING.                                                          
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3120-GET-APPMT-TIME.                                                     
      *--------------------*                                                    
                                                                                
           SET WS-TIME-IDX   TO 1.                                              
           SEARCH WS-TIME-TBL                                                   
              AT END                                                            
                 MOVE SPACES                   TO WS-TIME-HH                    
              WHEN K225-TME-APPOINTMENT(1:2) = WS-TIME-ORG(WS-TIME-IDX)         
                 MOVE WS-TIME-EQV(WS-TIME-IDX) TO WS-TIME-HH                    
           END-SEARCH.                                                          
                                                                                
           STRING WS-TIME-HH ':'                                                
                  K225-TME-APPOINTMENT (3:2)                                    
                  DELIMITED BY SIZE    INTO L007-TME-APPMT-HHMM                 
           END-STRING.                                                          
                                                                                
           IF K225-TME-APPOINTMENT(1:2) >= 12                                   
              MOVE 'PM'        TO L007-TME-APPMT-AMPM                           
           ELSE                                                                 
              MOVE 'AM'        TO L007-TME-APPMT-AMPM                           
           END-IF.                                                              
                                                                                
       3120-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       3130-GET-QUEUE-BARCODE.                                                  
      *-----------------------*                                                 
                                                                                
           STRING K225-NUM-QUEUE '000' DELIMITED BY SIZE                        
             INTO WS-NUM-QUEUE.                                                 
                                                                                
           STRING 'SL' WS-NUM-QUEUE DELIMITED BY SIZE                           
             INTO  WS-C908-TXT-INPUT.                                           
                                                                                
           MOVE SPACES               TO WS-C908-TXT-OUTPUT.                     
           MOVE 10                   TO WS-C908-TXT-IN-SIZE.                    
           MOVE ZEROS                TO WS-C908-RETCODE                         
                                        WS-C908-TXT-OUT-SIZE.                   
                                                                                
           CALL 'BP13C908'    USING WS-C908-PARAMETERS.                         
                                                                                
           IF WS-C908-RETCODE = ZEROS                                           
              MOVE WS-C908-TXT-OUTPUT(1:WS-C908-TXT-OUT-SIZE) TO                
                   L007-BARCODE-QUEUE                                           
           ELSE                                                                 
              DISPLAY 'ERROR IN BP13C908-QUEUE: ' WS-C908-TXT-INPUT             
              MOVE SPACES                 TO  L007-BARCODE-QUEUE                
           END-IF.                                                              
                                                                                
       3130-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       3140-GET-DAY-OF-WEEK.                                                    
      *-----------------------*                                                 
                                                                                
           MOVE SPACES                TO LINK-REC.                              
           MOVE K225-DTE-APPOINTMENT  TO WS-IN-DATE.                            
           MOVE 'DW'                  TO WS-IN-OPTION.                          
                                                                                
           PERFORM 3142-CALL-AV02C001       THRU 3142-EXIT.                     
                                                                                
           EVALUATE WS-RET-DAY                                                  
              WHEN 'MON'                                                        
                 MOVE 'MONDAY   '    TO L007-DAY-APPMT                          
              WHEN 'TUE'                                                        
                 MOVE 'TUESDAY  '    TO L007-DAY-APPMT                          
              WHEN 'WED'                                                        
                 MOVE 'WEDNESDAY'    TO L007-DAY-APPMT                          
              WHEN 'THU'                                                        
                 MOVE 'THURSDAY '    TO L007-DAY-APPMT                          
              WHEN 'FRI'                                                        
                 MOVE 'FRIDAY   '    TO L007-DAY-APPMT                          
              WHEN 'SAT'                                                        
                 MOVE 'SATURDAY '    TO L007-DAY-APPMT                          
              WHEN 'SUN'                                                        
                 MOVE 'SUNDAY   '    TO L007-DAY-APPMT                          
              WHEN OTHER                                                        
                 MOVE SPACES         TO L007-DAY-APPMT                          
           END-EVALUATE.                                                        
                                                                                
       3140-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3142-CALL-AV02C001.                                                      
      *--------------------*                                                    
                                                                                
           CALL 'AV02C001'      USING LINK-REC.                                 
                                                                                
           IF WS-RETURN = 'EID' OR 'EOP'                                        
              DISPLAY 'ERROR IN CALLING AV02C001-' WS-RETURN                    
                      ' ' WS-IN-DATE                                            
           END-IF.                                                              
                                                                                
       3142-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       9000-CLOSE-FILES.                                                        
      *--------------------*                                                    
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C007'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*--------------- COUNTERS ---------------*'.                
           DISPLAY 'NUMBER OF BP13F205 RECORDS  : ' WS-F205-READ-CTR.           
           DISPLAY 'NUMBER OF LETTERS PRINTED   : ' WS-PRT-CTR.                 
                                                                                
            CLOSE  BP13F205                                                     
                   BP13K225                                                     
                   BP13L007.                                                    
                                                                                
            IF WS-K225-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR:BP13K225 STATUS ' WS-K225-STATUS            
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
