      *-----------------------------------------------------------*             
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C3A1.                                                 
       AUTHOR.        ELGER DE GUZMAN.                                          
      *-----------------------------------------------------------*             
                                                                                
      *===========================================================*             
      *          BP13 - LOAN ELIGIBILITY & ASSESSMENT SYSTEM      *             
      *===========================================================*             
      *                                                           *             
      * OBJECTIVES :  THIS SUBROUTINE WILL UPDATE SOC AFTER       *             
      *               HLE HAS BEEN PROCESSED. APPROVE CASES ONLY  *             
      *                                                           *             
      *               FILES TO BE UPDATED :                       *             
      *               - BP13K800                                  *             
      *               - BP13K820                                  *             
      *               - BP13K640                                  *             
      *                                                           *             
      *===========================================================*             
      *  CHG-REQ   BY    DATE        DESCRIPTION                  *             
      *  --------  ----  ----------  ---------------------------- *             
      *  BP135420  ESD1  26/09/14    NEW PROGRAM                  *             
      *  BP135817  RJB1  26/06/15    BYPASS UPD IF SCHACC IS SPACES             
      *  BP135709  RJB1  27/02/17    BYPASS CANCELLED FLAT CASES  *             
      *                              WHEN LEAS RETURN 2ND CA CASE *             
      *                              TO UPDATE SOC                *             
      *  BP138047 KAC1   25/11/19 RECOMPILE FOR BP27 COPYBOOK UPDATE            
      *  BP139362 KAC1   11/10/22 CATER FOR REVISED FLD NME IN BP27K800         
      *                K800-NUM-INTEREST->K800-NUM-APPLICABLE1-INT-RATE         
      *                                                           *             
      *************************************************************             
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
                                                                                
           SELECT BP27K800  ASSIGN        TO BP27K800                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K800-NUM-HLA                    
                                                OF BP27K800-MASTER              
                            FILE STATUS      IS BP27-K800-STAT.                 
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K800-NUM-REGN                   
                            FILE STATUS      IS BP13-K800-STAT.                 
                                                                                
           SELECT BP13K640  ASSIGN        TO BP13K640                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K640-KEY-FLD                    
                            FILE STATUS      IS BP13-K640-STAT.                 
                                                                                
           SELECT BP13KC00  ASSIGN        TO BP13KC00                           
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS KC00-KEY-FLD                    
                            FILE STATUS      IS BP13-KC00-STAT.                 
                                                                                
           SELECT BP13K848  ASSIGN        TO BP13K848                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K848-KEY-FLD                    
                            FILE STATUS      IS BP13-K848-STAT.                 
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K110-KEY-FLD                    
                            FILE STATUS      IS BM06-K110-STAT.                 
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K820-KEY-FLD                    
                            FILE STATUS      IS BP13-K820-STAT.                 
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F800                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13F800.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K640                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K640.                                                           
                                                                                
       FD  BP13KC00                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13KC00.                                                           
                                                                                
       FD  BP13K848                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K848.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP27K800                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
       COPY BP27K800.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-RESP                   PIC 9(04)    VALUE ZEROES.             
           05  WS-RESP2                  PIC 9(04)    VALUE ZEROES.             
           05  WS-NRIC                   PIC X(09)    VALUE SPACES.             
           05  WS-CURR-SELLING-PRICE     PIC S9(7)V99 VALUE ZEROES.             
           05  WS-TEMP-AMT               PIC 9(07)    VALUE ZEROES.             
           05  WS-TEMP-AMT2              PIC 9(07)    VALUE ZEROES.             
           05  WS-FLAT-OK                PIC X(01)    VALUE 'N'.                
           05  WS-KC00-FND               PIC X(01)    VALUE 'N'.                
           05  WS-K640-FND               PIC X(01)    VALUE 'N'.                
           05  WS-EOF-BP13KC00              PIC X      VALUE 'N'.               
                                                                                
           05  WS-COMM-INT-RATE               PIC 9(02)V9(02).                  
           05  WS-COMM-REPAY-PERIOD           PIC 9(02).                        
           05  WS-COMM-MAX-LOAN               PIC 9(06).                        
           05  WS-COMM-MTH-INSTALL            PIC 9(05).                        
           05  WS-INSTAL                      PIC S9(5)V9(8) VALUE ZERO.        
           05  WS-INSTAL-DOL                  PIC S9(07) VALUE ZEROES.          
           05  WS-FAC                         PIC 9(02) VALUE ZEROES.           
           05  WS-INT                         PIC 99V9(8) VALUE ZEROES.         
           05  WS-DIV                         PIC 99V9(8) VALUE ZEROES.         
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY                  PIC 9(04).                          
           05  WS-CUR-MM                    PIC 9(02).                          
           05  WS-CUR-DD                    PIC 9(02).                          
                                                                                
       01  WS-SYSTEM-TIME.                                                      
           05 WS-HH                       PIC 9(2).                             
           05 WS-MM                       PIC 9(2).                             
           05 WS-SS                       PIC 9(2).                             
           05 WS-MI                       PIC 9(2).                             
                                                                                
       01  WS-F800-EOF                    PIC X   VALUE SPACE.                  
       01  WS-FILE-STATUS.                                                      
           05 BP13-K800-STAT              PIC 99    VALUE ZEROS.                
           05 BP27-K800-STAT              PIC 99    VALUE ZEROS.                
           05 BP13-K640-STAT              PIC 99    VALUE ZEROS.                
           05 BP13-KC00-STAT              PIC 99    VALUE ZEROS.                
           05 BP13-K848-STAT              PIC 99    VALUE ZEROS.                
           05 BM06-K110-STAT              PIC 99    VALUE ZEROS.                
           05 BP13-K820-STAT              PIC 99    VALUE ZEROS.                
                                                                                
       01  C3A1-COMMAREA.                                                       
           05  C3A1-NUM-REGN             PIC X(08)    VALUE SPACES.             
           05  C3A1-NUM-HLA              PIC X(09)    VALUE SPACES.             
           05  C3A1-NUM-ACTION           PIC X(01)    VALUE SPACES.             
           05  C3A1-NUM-USERID           PIC X(07)    VALUE SPACES.             
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *------------------------*                                                
       0000-MAIN-LOGIC.                                                         
      *------------------------*                                                
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-DATA    THRU 3000-EXIT                          
           UNTIL WS-F800-EOF = 'Y'.                                             
                                                                                
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP27K800                                                 
                       BP13KC00                                                 
                       BM06K110                                                 
                I-O    BP13K800                                                 
                       BP13K640                                                 
                       BP13K820                                                 
                       BP13K848                                                 
           INITIALIZE  BP13K800-MASTER BP13F800-MASTER BP13K848-MASTER          
                       BP27K800-MASTER BP13K820-REC K110-REC                    
                       BP13K640-TRANS-REC BP13KC00-REC                          
                                                                                
           IF BP13-K800-STAT NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' BP13-K800-STAT                  
              MOVE     BP13-K800-STAT      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF BP13-K640-STAT NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K640 ERROR ' BP13-K640-STAT                  
              MOVE     BP13-K640-STAT      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF BP13-KC00-STAT NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC00 ERROR ' BP13-KC00-STAT                  
              MOVE     BP13-KC00-STAT      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF BP13-K848-STAT NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K848 ERROR ' BP13-K848-STAT                  
              MOVE     BP13-K848-STAT      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF BP13-K820-STAT NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 ERROR ' BP13-K820-STAT                  
              MOVE     BP13-K820-STAT      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF BP27-K800-STAT NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP27K800 ERROR ' BP27-K800-STAT                  
              MOVE     BP27-K800-STAT      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF BM06-K110-STAT NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' BM06-K110-STAT                  
              MOVE     BM06-K110-STAT      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-READ-INPUT.                                                         
      *=============================================================*           
                                                                                
            READ BP13F800                                                       
                 AT END MOVE 'Y'  TO WS-F800-EOF                                
                                                                                
            END-READ.                                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-PROCESS-DATA.                                                       
      *=============================================================*           
                                                                                
           MOVE  F800-NUM-REGN       TO  C3A1-NUM-REGN                          
           MOVE  F800-NUM-HLA        TO  C3A1-NUM-HLA                           
           MOVE  'BP13C3A1'          TO  C3A1-NUM-USERID                        
                                                                                
           PERFORM 4000-READ-BP27K800        THRU 4000-EXIT.                    
           PERFORM 4100-READ-BP13K800        THRU 4100-EXIT.                    
           PERFORM 4200-READ-BP13K640        THRU 4200-EXIT.                    
           PERFORM 4400-READ-BP13KC00        THRU 4400-EXIT.                    
           IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                      
              PERFORM 4500-READ-BM06K110        THRU 4500-EXIT                  
              PERFORM 5000-UPDATE-FILES         THRU 5000-EXIT                  
           ELSE                                                                 
              DISPLAY 'SCH-ACC IS SPACES: ' K800-NUM-REGN                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-INPUT           THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-READ-BP27K800.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES                   TO BP27K800-MASTER.                    
           INITIALIZE                       BP27K800-MASTER.                    
                                                                                
           MOVE C3A1-NUM-HLA             TO K800-NUM-HLA OF                     
                                            BP27K800-MASTER.                    
           READ BP27K800                                                        
                                                                                
           EVALUATE BP27-K800-STAT                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 23                                                          
                    MOVE  BP27-K800-STAT TO RETURN-CODE                         
                    DISPLAY 'RECORD NOT FOUND BP27K800 : ' C3A1-NUM-HLA         
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
               WHEN OTHER                                                       
                    MOVE  BP27-K800-STAT TO RETURN-CODE                         
                    DISPLAY 'FILE ERROR  BP27K800 : ' C3A1-NUM-HLA              
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
            END-EVALUATE.                                                       
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4100-READ-BP13K800.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES                   TO BP13K800-MASTER.                    
           INITIALIZE                       BP13K800-MASTER.                    
                                                                                
           MOVE C3A1-NUM-REGN            TO K800-NUM-REGN OF                    
                                            BP13K800-MASTER.                    
           READ BP13K800                                                        
                                                                                
           EVALUATE BP13-K800-STAT                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 23                                                          
                    MOVE  BP13-K800-STAT TO RETURN-CODE                         
                    DISPLAY 'RECORD NOT FOUND BP13K800 : ' C3A1-NUM-REGN        
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
               WHEN OTHER                                                       
                    MOVE  BP13-K800-STAT TO RETURN-CODE                         
                    DISPLAY 'FILE ERROR  BP13K800 : ' C3A1-NUM-REGN             
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
            END-EVALUATE.                                                       
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4200-READ-BP13K640.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES                   TO BP13K640-TRANS-REC                  
           INITIALIZE                       BP13K640-TRANS-REC.                 
                                                                                
           MOVE C3A1-NUM-REGN            TO K640-REGN-NO                        
                                                                                
           READ BP13K640                                                        
                                                                                
           EVALUATE BP13-K640-STAT                                              
               WHEN 00                                                          
                    MOVE  'Y'   TO  WS-K640-FND                                 
               WHEN 23                                                          
                    MOVE  'N'   TO  WS-K640-FND                                 
               WHEN OTHER                                                       
                    MOVE  BP13-K640-STAT TO RETURN-CODE                         
                    DISPLAY 'FILE ERROR  BP13K640 : ' C3A1-NUM-REGN             
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4300-READ-BP13K820.                                                      
      *---------------------------------------------------------------*         
                                                                                
           INITIALIZE                             BP13K820-REC.                 
           MOVE SPACES                      TO    BP13K820-REC.                 
                                                                                
           MOVE K800-NUM-REGN               TO    K820-NUM-REGN.                
           MOVE WS-NRIC                     TO    K820-NUM-NRIC.                
                                                                                
           READ BP13K820                                                        
                                                                                
           EVALUATE BP13-K820-STAT                                              
               WHEN 00                                                          
                  MOVE 'CA'                TO   K820-NUM-CA-TAG                 
                  MOVE ZEROES              TO   K820-AMT-INCOME-CA              
                  MOVE FUNCTION CURRENT-DATE(1:8) TO K820-DTE-UPDATE OF         
                                                BP13K820-REC                    
                  MOVE C3A1-NUM-USERID  TO K820-NUM-USERID                      
                                             OF BP13K820-REC                    
               WHEN 23                                                          
                    MOVE  BP13-K820-STAT TO RETURN-CODE                         
                    DISPLAY 'RECORD NOT FND BP13K820 : ' C3A1-NUM-REGN          
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
               WHEN OTHER                                                       
                    MOVE  BP13-K820-STAT TO RETURN-CODE                         
                    DISPLAY 'FILE ERROR  BP13K820 : ' C3A1-NUM-REGN             
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
            END-EVALUATE.                                                       
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------*                                           
       4400-READ-BP13KC00.                                                      
      *-----------------------------*                                           
           MOVE SPACES                      TO   BP13KC00-REC.                  
           INITIALIZE                            BP13KC00-REC.                  
                                                                                
           MOVE C3A1-NUM-REGN               TO KC00-NUM-REGN                    
           MOVE 'N'                         TO WS-EOF-BP13KC00                  
                                               WS-KC00-FND                      
                                                                                
           START BP13KC00 KEY >= KC00-KEY-FLD                                   
                                                                                
           EVALUATE BP13-KC00-STAT                                              
           WHEN 00                                                              
              PERFORM 4410-READNXT-BP13KC00 THRU 4410-EXIT                      
                 UNTIL WS-EOF-BP13KC00 = 'Y'                                    
           WHEN 23                                                              
              MOVE    'N'    TO   WS-KC00-FND                                   
           WHEN OTHER                                                           
                 MOVE  BP13-KC00-STAT TO RETURN-CODE                            
                 DISPLAY 'FILE ERROR  BP13KC00 : ' KC00-KEY-FLD                 
                 PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------*                                           
       4410-READNXT-BP13KC00.                                                   
      *-----------------------------*                                           
           READ BP13KC00 NEXT                                                   
              AT END MOVE 'Y' TO WS-EOF-BP13KC00                                
                 GO TO 4410-EXIT.                                               
                                                                                
           EVALUATE BP13-KC00-STAT                                              
             WHEN 00                                                            
                IF KC00-NUM-REGN = K800-NUM-REGN                                
                   MOVE   'Y'    TO     WS-KC00-FND  WS-EOF-BP13KC00            
                ELSE                                                            
                   MOVE   'N'    TO     WS-KC00-FND                             
                   MOVE   'Y'    TO     WS-EOF-BP13KC00                         
                END-IF                                                          
             WHEN 46                                                            
             WHEN 10                                                            
             WHEN 23                                                            
                MOVE    'Y'    TO   WS-EOF-BP13KC00                             
                MOVE    'N'    TO   WS-KC00-FND                                 
             WHEN OTHER                                                         
              MOVE  BP13-KC00-STAT TO RETURN-CODE                               
              DISPLAY 'ERROR READNXT BP13KC00 FILE : ' BP13-KC00-STAT           
              DISPLAY 'KC00-NUM-REGN = ' KC00-NUM-REGN                          
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4410-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4500-READ-BM06K110.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES                   TO K110-REC                            
           INITIALIZE                       K110-REC.                           
                                                                                
           MOVE K800-NUM-SCH-ACC         TO K110-SCH-ACC-NO                     
                                                                                
           READ BM06K110                                                        
                                                                                
           EVALUATE BM06-K110-STAT                                              
               WHEN 00                                                          
                    IF K800-AMT-PREMIUM IS NOT NUMERIC                          
                       MOVE ZEROES  TO K800-AMT-PREMIUM                         
                    END-IF                                                      
                    IF K110-CURR-SELLING-PRICE IS NOT NUMERIC                   
                       MOVE ZEROES  TO K110-CURR-SELLING-PRICE                  
                    END-IF                                                      
                    COMPUTE WS-CURR-SELLING-PRICE = K800-AMT-PREMIUM +          
                                            K110-CURR-SELLING-PRICE             
               WHEN 23                                                          
                    MOVE  BM06-K110-STAT TO RETURN-CODE                         
                    DISPLAY 'RECORD NOT FND BM06K110: ' K800-NUM-SCH-ACC        
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
               WHEN OTHER                                                       
                    MOVE  BM06-K110-STAT TO RETURN-CODE                         
                    DISPLAY 'FILE ERROR  BM06K110 : ' K800-NUM-SCH-ACC          
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
            END-EVALUATE.                                                       
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       5000-UPDATE-FILES.                                                       
      *----------------------*                                                  
                                                                                
           PERFORM 7000-WRITE-K800-IMAGE  THRU 7000-EXIT                        
                                                                                
           IF WS-KC00-FND  =  'Y'                                               
              PERFORM 5100-UPDATE-K800      THRU  5100-EXIT                     
              PERFORM 5200-UPDATE-K820      THRU  5200-EXIT                     
              PERFORM 5300-UPDATE-BP13K640  THRU 5300-EXIT                      
           ELSE                                                                 
              PERFORM 5300-UPDATE-BP13K640  THRU 5300-EXIT                      
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       5100-UPDATE-K800.                                                        
      *----------------------*                                                  
                                                                                
           IF K800-NUM-SPR-TAG-GRANT NOT = SPACES AND LOW-VALUES                
              MOVE K800-NUM-SPR-TAG-GRANT                      TO               
                   K800-NUM-SPR-TAG         OF BP13K800-MASTER                  
           ELSE                                                                 
              MOVE K800-NUM-SPR-TAG         OF BP27K800-MASTER TO               
                   K800-NUM-SPR-TAG         OF BP13K800-MASTER                  
           END-IF.                                                              
                                                                                
           IF K800-NUM-SUBSIDISED-LOAN OF BP27K800-MASTER NOT NUMERIC           
              MOVE ZEROES TO K800-NUM-SUBSIDISED-LOAN OF BP27K800-MASTER        
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-SUBSIDISED-LOAN OF BP27K800-MASTER TO                  
                K800-NUM-SUBSIDISED-LOAN OF BP13K800-MASTER.                    
                                                                                
           IF K800-NUM-LOAN-POLICY-GRANT NOT = SPACES AND LOW-VALUES            
              MOVE K800-NUM-LOAN-POLICY-GRANT TO                                
                   K800-NUM-MORT-LOAN-POLICY                                    
           ELSE                                                                 
              IF K800-NUM-LOAN-TAG OF BP27K800-MASTER = 'U'                     
                 MOVE 'PB' TO K800-NUM-MORT-LOAN-POLICY                         
              ELSE                                                              
                 MOVE K800-NUM-LOAN-POLICY       TO                             
                      K800-NUM-MORT-LOAN-POLICY                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K800-AMT-CA-INCOME OF BP27K800-MASTER NOT NUMERIC                 
              MOVE ZEROES TO K800-AMT-CA-INCOME OF BP27K800-MASTER              
           END-IF.                                                              
           MOVE K800-AMT-CA-INCOME OF BP27K800-MASTER TO                        
                K800-AMT-CA-INCOME OF BP13K800-MASTER.                          
                                                                                
      *-----------------------------------------*                               
      * LEAS'S NUM-PPO IS SAME AS SOC'S NUM-CAP                                 
      * SOC'S K800-NUM-PPO IS NOT UPDATED AS IT                                 
      * HAS NO INFLUENCE OVER LOAN                                              
      *-----------------------------------------*                               
           IF K800-NUM-PPO-GRANT         NOT = SPACES AND LOW-VALUES            
              MOVE K800-NUM-PPO-GRANT                    TO                     
                   K800-NUM-CAP                                                 
           ELSE                                                                 
              MOVE K800-NUM-PPO       OF BP27K800-MASTER TO                     
                   K800-NUM-CAP                                                 
           END-IF.                                                              
                                                                                
           IF K800-NUM-LOAN-TAG-GRANT NOT = SPACES AND LOW-VALUES               
              IF K800-NUM-LOAN-TAG-GRANT = 'S'                                  
                 MOVE 'H' TO K800-NUM-LOAN-TAG OF BP13K800-MASTER               
                 MOVE ' ' TO K800-NUM-NON-CON  OF BP13K800-MASTER               
                 MOVE K800-NUM-PROPERTY-TAG TO K800-NUM-BUY-SELL-TAG            
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG-GRANT = 'U'                                  
                 MOVE 'H' TO K800-NUM-LOAN-TAG OF BP13K800-MASTER               
                 MOVE 'Y' TO K800-NUM-NON-CON  OF BP13K800-MASTER               
                 MOVE K800-NUM-PROPERTY-TAG TO K800-NUM-BUY-SELL-TAG            
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG-GRANT = 'A'                                  
                 MOVE 'H' TO K800-NUM-LOAN-TAG OF BP13K800-MASTER               
                 MOVE 'Y' TO K800-NUM-NON-CON  OF BP13K800-MASTER               
                 MOVE K800-NUM-PROPERTY-TAG TO K800-NUM-BUY-SELL-TAG            
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG-GRANT = 'H' OR 'E'                           
                 MOVE 'H' TO K800-NUM-LOAN-TAG OF BP13K800-MASTER               
                 MOVE ' ' TO K800-NUM-NON-CON  OF BP13K800-MASTER               
                 MOVE K800-NUM-PROPERTY-TAG TO K800-NUM-BUY-SELL-TAG            
              END-IF                                                            
           ELSE                                                                 
              IF K800-NUM-LOAN-TAG OF BP27K800-MASTER = 'S'                     
                 MOVE 'H'  TO  K800-NUM-LOAN-TAG OF BP13K800-MASTER             
                 MOVE ' '  TO  K800-NUM-NON-CON  OF BP13K800-MASTER             
                 MOVE K800-NUM-PROPERTY-TAG TO K800-NUM-BUY-SELL-TAG            
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG OF BP27K800-MASTER = 'U'                     
                 MOVE 'H'  TO  K800-NUM-LOAN-TAG OF BP13K800-MASTER             
                 MOVE 'Y'  TO  K800-NUM-NON-CON  OF BP13K800-MASTER             
                 MOVE K800-NUM-PROPERTY-TAG TO K800-NUM-BUY-SELL-TAG            
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG OF BP27K800-MASTER = 'A'                     
                 MOVE 'H' TO K800-NUM-LOAN-TAG OF BP13K800-MASTER               
                 MOVE 'Y' TO K800-NUM-NON-CON  OF BP13K800-MASTER               
                 MOVE K800-NUM-PROPERTY-TAG TO K800-NUM-BUY-SELL-TAG            
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG OF BP27K800-MASTER = 'H' OR 'E'              
                 MOVE 'H' TO K800-NUM-LOAN-TAG OF BP13K800-MASTER               
                 MOVE ' ' TO K800-NUM-NON-CON  OF BP13K800-MASTER               
                 MOVE K800-NUM-PROPERTY-TAG TO K800-NUM-BUY-SELL-TAG            
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE 'RSV'                      TO K800-NUM-HLA-STATUS OF            
                                              BP13K800-MASTER.                  
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO K800-DTE-UPDATE OF                
                                              BP13K800-MASTER.                  
                                                                                
           IF K800-NUM-ROOM = '6'                                               
              MOVE 'E' TO  K800-NUM-ROOM                                        
           END-IF                                                               
                                                                                
           PERFORM 6000-REWRITE-BP13K800   THRU 6000-EXIT.                      
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       5200-UPDATE-K820.                                                        
      *-----------------------*                                                 
           IF K800-NUM-NRIC1 OF BP13K800-MASTER NOT = SPACES AND                
                                                      LOW-VALUES                
              MOVE K800-NUM-NRIC1 OF BP13K800-MASTER  TO   WS-NRIC              
              PERFORM 4300-READ-BP13K820        THRU 4300-EXIT                  
              PERFORM 6200-REWRITE-BP13K820     THRU 6200-EXIT                  
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC2 OF BP13K800-MASTER NOT = SPACES AND                
                                                      LOW-VALUES                
              MOVE SPACES                             TO   WS-NRIC              
              MOVE K800-NUM-NRIC2 OF BP13K800-MASTER  TO   WS-NRIC              
              PERFORM 4300-READ-BP13K820        THRU 4300-EXIT                  
              PERFORM 6200-REWRITE-BP13K820     THRU 6200-EXIT                  
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 OF BP13K800-MASTER NOT = SPACES AND                
                                                      LOW-VALUES                
              MOVE SPACES                             TO   WS-NRIC              
              MOVE K800-NUM-NRIC3 OF BP13K800-MASTER  TO   WS-NRIC              
              PERFORM 4300-READ-BP13K820        THRU 4300-EXIT                  
              PERFORM 6200-REWRITE-BP13K820     THRU 6200-EXIT                  
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 OF BP13K800-MASTER NOT = SPACES AND                
                                                      LOW-VALUES                
              MOVE SPACES                             TO   WS-NRIC              
              MOVE K800-NUM-NRIC4 OF BP13K800-MASTER  TO   WS-NRIC              
              PERFORM 4300-READ-BP13K820        THRU 4300-EXIT                  
              PERFORM 6200-REWRITE-BP13K820     THRU 6200-EXIT                  
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       5300-UPDATE-BP13K640.                                                    
      *--------------------*                                                    
           MOVE SPACES                  TO   BP13K640-TRANS-REC.                
           INITIALIZE                        BP13K640-TRANS-REC.                
                                                                                
           MOVE C3A1-NUM-REGN           TO   K640-REGN-NO.                      
           PERFORM 5310-FMT-BP13K640    THRU 5310-EXIT.                         
                                                                                
           IF WS-K640-FND = 'Y'                                                 
              PERFORM 6100-REWRITE-BP13K640 THRU 6100-EXIT                      
           ELSE                                                                 
              PERFORM 7100-WRITE-K640 THRU 7100-EXIT                            
           END-IF.                                                              
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       5310-FMT-BP13K640.                                                       
      *-----------------*                                                       
           IF K800-NUM-SUBSIDISED-LOAN OF BP27K800-MASTER NOT NUMERIC           
              MOVE ZEROES TO K800-NUM-SUBSIDISED-LOAN OF BP27K800-MASTER        
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-SUBSIDISED-LOAN OF BP27K800-MASTER TO                  
                K640-NUM-SUBSIDISED-LOAN.                                       
                                                                                
           IF K800-NUM-LOAN-TAG-GRANT NOT = SPACES AND LOW-VALUES               
              IF K800-NUM-LOAN-TAG-GRANT = 'S'                                  
                 MOVE 'H'  TO  K800-NUM-LOAN-TAG-GRANT                          
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG-GRANT = 'U'                                  
                 MOVE 'A'  TO  K800-NUM-LOAN-TAG-GRANT                          
              END-IF                                                            
                                                                                
              MOVE K800-NUM-LOAN-TAG-GRANT TO K640-CDE-LOAN-OPTION              
           ELSE                                                                 
              IF K800-NUM-LOAN-TAG OF BP27K800-MASTER = 'S'                     
                 MOVE 'H'  TO  K800-NUM-LOAN-TAG OF BP27K800-MASTER             
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG OF BP27K800-MASTER = 'U'                     
                 MOVE 'A'  TO  K800-NUM-LOAN-TAG OF BP27K800-MASTER             
              END-IF                                                            
                                                                                
              MOVE K800-NUM-LOAN-TAG OF BP27K800-MASTER TO                      
                   K640-CDE-LOAN-OPTION                                         
           END-IF.                                                              
                                                                                
           IF K640-CDE-LOAN-OPTION = 'H'                                        
              MOVE 'P' TO K640-CDE-LOAN-OPTION                                  
           END-IF.                                                              
      ** TEST                                                                   
           IF K800-AMT-MAX-LOAN  OF BP27K800-MASTER = ZEROES OR                 
                                                      LOW-VALUES                
              DISPLAY K800-NUM-HLA OF BP27K800-MASTER ' ' K800-NUM-REGN         
           END-IF                                                               
                                                                                
      ** TEST                                                                   
                                                                                
                                                                                
           MOVE K800-AMT-MAX-LOAN  OF BP27K800-MASTER   TO                      
                K640-AMT-CA-LOAN.                                               
                                                                                
      * COMPUTE MAX LOAN                                                        
           IF K800-NUM-HLA-HDBREF-SALE(1:8) NOT = SPACES AND LOW-VALUES         
              IF K800-NUM-HLA-HDBREF-SALE(9:3)  = SPACES OR  LOW-VALUES         
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE  ZEROES TO WS-CURR-SELLING-PRICE                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           INITIALIZE                  WS-TEMP-AMT.                             
                                                                                
           COMPUTE WS-TEMP-AMT = 0.9 * WS-CURR-SELLING-PRICE.                   
                                                                                
           IF K800-AMT-MAX-LOAN <= WS-TEMP-AMT                                  
              MOVE K800-AMT-MAX-LOAN OF BP27K800-MASTER                         
                TO K640-AMT-MAX-LOAN                                            
           ELSE                                                                 
              INITIALIZE             WS-TEMP-AMT2                               
              COMPUTE WS-TEMP-AMT2 = WS-TEMP-AMT / 100                          
              INITIALIZE             WS-TEMP-AMT                                
              COMPUTE WS-TEMP-AMT  = WS-TEMP-AMT2 * 100                         
              MOVE WS-TEMP-AMT    TO K640-AMT-MAX-LOAN                          
           END-IF.                                                              
                                                                                
      *****IF K800-NUM-INTEREST    OF BP27K800-MASTER NOT NUMERIC               
      *       MOVE ZEROES TO K800-NUM-INTEREST OF BP27K800-MASTER               
      *    END-IF.                                                              
      *    MOVE K800-NUM-INTEREST  OF BP27K800-MASTER TO                        
      **********K640-AMT-INTEREST-RATE.                                         
           IF K800-NUM-APPLICABLE1-INT-RATE NOT NUMERIC                         
              MOVE ZEROES          TO K800-NUM-APPLICABLE1-INT-RATE             
           END-IF.                                                              
           MOVE K800-NUM-APPLICABLE1-INT-RATE TO                                
                K640-AMT-INTEREST-RATE.                                         
                                                                                
           IF K800-AMT-INSTAL      OF BP27K800-MASTER NOT NUMERIC               
              MOVE ZEROES TO K800-AMT-INSTAL OF BP27K800-MASTER                 
           END-IF.                                                              
           MOVE K800-AMT-INSTAL    OF BP27K800-MASTER   TO                      
                K640-AMT-ELIG-INSTAL.                                           
                                                                                
           IF K800-NUM-REPAY       OF BP27K800-MASTER NOT NUMERIC               
              MOVE ZEROES TO K800-NUM-REPAY OF BP27K800-MASTER                  
           END-IF.                                                              
           MOVE K800-NUM-REPAY     OF BP27K800-MASTER TO                        
                K640-AMT-REPAY-PERIOD.                                          
                                                                                
           MOVE K800-NUM-HOUSEHOLD OF BP13K800-MASTER TO                        
                K640-NUM-HOUSEHOLD.                                             
                                                                                
           MOVE K800-NUM-FLAT-UPGRADE  OF BP27K800-MASTER      TO               
                K640-NUM-FLAT-UPGRADE.                                          
                                                                                
           IF K800-NUM-LOAN-TAG-GRANT NOT = SPACES AND LOW-VALUES               
              IF K800-NUM-LOAN-TAG-GRANT = 'S'                                  
                 MOVE 'H'  TO  K800-NUM-LOAN-TAG-GRANT                          
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG-GRANT = 'U'                                  
                 MOVE 'A'  TO  K800-NUM-LOAN-TAG-GRANT                          
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG-GRANT = 'A'                                  
                 MOVE 'H' TO K640-NUM-LOAN-TAG                                  
              ELSE                                                              
                 MOVE K800-NUM-LOAN-TAG-GRANT TO                                
                      K640-NUM-LOAN-TAG                                         
              END-IF                                                            
           ELSE                                                                 
              IF K800-NUM-LOAN-TAG OF BP27K800-MASTER = 'S'                     
                 MOVE 'H'  TO  K800-NUM-LOAN-TAG OF BP27K800-MASTER             
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG OF BP27K800-MASTER = 'U'                     
                 MOVE 'A'  TO  K800-NUM-LOAN-TAG OF BP27K800-MASTER             
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG OF BP27K800-MASTER = 'A'                     
                 MOVE 'H' TO K640-NUM-LOAN-TAG                                  
              ELSE                                                              
                 MOVE K800-NUM-LOAN-TAG OF BP27K800-MASTER TO                   
                      K640-NUM-LOAN-TAG                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO K640-DTE-UPDATE.                  
           MOVE C3A1-NUM-USERID TO K640-USERID                                  
                                                                                
      * GET MONTHLY INSTALLMENT                                                 
           PERFORM 5320-COMPUTE-MTH-INSTAL THRU 5320-EXIT.                      
                                                                                
      * FORMAT OTHER DATA                                                       
           MOVE K800-NUM-SCH-ACC           TO K640-SCH-ACC.                     
           MOVE FUNCTION CURRENT-DATE(1:8) TO K640-DTE-LOAN-APPROVAL            
                                              K640-DTE-LOAN-APPLN-RETURN        
                                              K640-DTE-LETTER-PRINTED           
                                              K640-DTE-MLA-SENT.                
           MOVE '00000000'                 TO K640-DTE-REMINDER-SENT.           
           MOVE '01'                       TO K640-NUM-LETTER-PRINTED.          
           MOVE 'N'                        TO K640-NUM-OUTRGHT-PURC.            
                                                                                
       5310-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       5320-COMPUTE-MTH-INSTAL.                                                 
      *------------------------*                                                
                                                                                
      *****MOVE K800-NUM-INTEREST OF BP27K800-MASTER                            
      *****                                   TO WS-COMM-INT-RATE.              
           MOVE K800-NUM-APPLICABLE1-INT-RATE TO WS-COMM-INT-RATE.              
                                                                                
           MOVE K800-NUM-REPAY OF BP27K800-MASTER                               
                                              TO WS-COMM-REPAY-PERIOD.          
           MOVE K640-AMT-MAX-LOAN             TO WS-COMM-MAX-LOAN.              
                                                                                
      ** TEST                                                                   
           IF WS-COMM-MAX-LOAN  = ZEROES OR LOW-VALUES                          
              DISPLAY K800-NUM-HLA OF BP27K800-MASTER ' ' K800-NUM-REGN         
                      ':WS-COMM-MAX-LOAN:'                                      
              MOVE ZEROES TO K640-AMT-ELIG-INSTAL                               
              GO TO 5320-EXIT                                                   
           END-IF                                                               
      ** TEST                                                                   
                                                                                
                                                                                
           MOVE 12 TO WS-FAC.                                                   
                                                                                
           COMPUTE WS-INT = WS-COMM-INT-RATE / 100.                             
           COMPUTE WS-DIV = WS-INT / WS-FAC.                                    
                                                                                
                                                                                
           COMPUTE WS-INSTAL = (WS-COMM-MAX-LOAN * WS-DIV)/                     
                               (1 - (1 / (1 + WS-DIV) **                        
                               (WS-FAC * WS-COMM-REPAY-PERIOD))).               
                                                                                
           COMPUTE WS-INSTAL-DOL = WS-INSTAL + 0.99.                            
           MOVE WS-INSTAL-DOL         TO WS-COMM-MTH-INSTALL.                   
                                                                                
           MOVE WS-COMM-MTH-INSTALL   TO K640-AMT-ELIG-INSTAL.                  
                                                                                
       5320-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       6000-REWRITE-BP13K800.                                                   
      *================================================================*        
                                                                                
           REWRITE BP13K800-MASTER                                              
                                                                                
           IF BP13-K800-STAT = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              MOVE  BP13-K800-STAT TO RETURN-CODE                               
              DISPLAY 'ERROR WRITING BP13K800 FILE : ' BP13-K800-STAT           
              DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                          
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       6100-REWRITE-BP13K640.                                                   
      *================================================================*        
                                                                                
           REWRITE BP13K640-TRANS-REC                                           
                                                                                
           IF BP13-K640-STAT = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              MOVE  BP13-K640-STAT TO RETURN-CODE                               
              DISPLAY 'ERROR WRITING BP13K640 FILE : ' BP13-K640-STAT           
              DISPLAY 'K640-REGN-NO = ' K640-REGN-NO                            
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       6200-REWRITE-BP13K820.                                                   
      *================================================================*        
                                                                                
           REWRITE BP13K820-REC                                                 
                                                                                
           IF BP13-K820-STAT = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              MOVE  BP13-K820-STAT TO RETURN-CODE                               
              DISPLAY 'ERROR WRITING BP13K820 FILE : ' BP13-K820-STAT           
              DISPLAY 'K820-NUM-REGN = ' K820-NUM-REGN                          
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       6200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       7000-WRITE-K800-IMAGE.                                                   
      *--------------------------------------------------------------*          
                                                                                
           MOVE BP13K800-MASTER TO BP13K848-MASTER.                             
           MOVE 'C3A1'          TO K848-NUM-TRANS-BKIMAGE.                      
                                                                                
           MOVE C3A1-NUM-USERID TO K848-NUM-USERID.                             
           MOVE WS-CUR-DATE     TO K848-DTE-BKIMAGE.                            
                                                                                
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-SYSTEM-TIME.                   
                                                                                
           MOVE WS-SYSTEM-TIME  TO K848-TME-BKIMAGE.                            
           MOVE K848-NUM-REGN   TO K848-NUM-REGN-BKIMAGE.                       
                                                                                
           WRITE BP13K848-MASTER.                                               
                                                                                
           EVALUATE BP13-K848-STAT                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                 MOVE  BP13-K848-STAT TO RETURN-CODE                            
                 DISPLAY 'ERR WRITING BP13K848 FILE : ' BP13-K848-STAT          
                 DISPLAY 'K848-NUM-REGN = ' K848-NUM-REGN                       
                 PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       7100-WRITE-K640.                                                         
      *--------------------------------------------------------------*          
                                                                                
           WRITE BP13K640-TRANS-REC.                                            
                                                                                
           EVALUATE BP13-K640-STAT                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                 MOVE  BP13-K640-STAT TO RETURN-CODE                            
                 DISPLAY 'ERR WRITING BP13K640 FILE : ' BP13-K640-STAT          
                 DISPLAY 'K640-REGN-NO  = ' K640-REGN-NO                        
                 PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
                                                                                
           CLOSE BP13K800                                                       
                 BP13F800                                                       
                 BP13K820                                                       
                 BP13KC00                                                       
                 BP13K848                                                       
                 BP27K800                                                       
                 BM06K110                                                       
                 BP13K640.                                                      
                                                                                
           IF BP13-K800-STAT NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' BP13-K800-STAT                  
              MOVE     BP13-K800-STAT      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF BP13-K820-STAT NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K820 ERROR ' BP13-K820-STAT                  
              MOVE     BP13-K820-STAT      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF BP13-K640-STAT NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K640 ERROR ' BP13-K640-STAT                  
              MOVE     BP13-K640-STAT      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF BP13-KC00-STAT NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC00 ERROR ' BP13-KC00-STAT                  
              MOVE     BP13-KC00-STAT      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF BP13-K848-STAT NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K848 ERROR ' BP13-K848-STAT                  
              MOVE     BP13-K848-STAT      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF BM06-K110-STAT NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K110 ERROR ' BM06-K110-STAT                  
              MOVE     BM06-K110-STAT      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF BP27-K800-STAT NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP27K800 ERROR ' BP27-K800-STAT                  
              MOVE     BP27-K800-STAT      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
