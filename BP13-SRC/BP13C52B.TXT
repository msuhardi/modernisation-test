       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C52B.                                                 
      *AUTHOR.        SHEILA MAE PATIAG.                                        
      *DATE-WRITTEN.  16/04/2012.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  TO PREPARE FLAT AND ETHINIC QUOTA FOR       *            
      *                SHORTLISTING 2ND HUNDRED PERCENT            *            
      *                                                            *            
      *  INPUT      :  BP13F010  BP13.F030.BALFLAT.CC1             *            
      *  I-O        :  BP13K010  BP13.K010.TENDER                  *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY  DDMMCCYY DESCRIPTION                            *         
      * -------- ---- -------- -------------------------------------- *         
      * BP134483 SMR2 16042012 NEW PROGRAM                            *         
      * BP134536 SMR2 13062012 INCLUDED K010-STATUS = 23 (NOTFND)     *         
      * BP134861 EAA1 22042013 EXPAND BP13F010 AND BP13K010 TO 600    *         
      * BP134914 SMR2 17062013 ADD SAP QUOTA                          *         
      * BP136096 SMR2 10122015 CATER FOR ELDERLY / REMOVE SAP         *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F010 ASSIGN TO BP13F010.                                  
           SELECT BP13K010 ASSIGN TO BP13K010                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K010-KEY-FLD                               
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F010                                                            
            RECORD CONTAINS 600  CHARACTERS                                     
            BLOCK CONTAINS    0  RECORDS                                        
            RECORDING MODE   IS  F                                              
            LABEL RECORDS        STANDARD.                                      
       COPY BP13F010.                                                           
                                                                                
       FD   BP13K010                                                            
            RECORD CONTAINS 600  CHARACTERS.                                    
       COPY BP13K010.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-INDICATORS.                                                  
           05  WS-F010-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-F010-READ               PIC 9(06) VALUE ZEROES.               
           05  WS-F010-NTFND              PIC 9(06) VALUE ZEROES.               
           05  WS-K010-FOUND              PIC X(01) VALUE 'N'.                  
           05  WS-K010-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K010-DUPLICATE          PIC 9(06) VALUE ZEROES.               
           05  WS-K010-REWRITE            PIC 9(06) VALUE ZEROES.               
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY                PIC 9(04).                            
           05  WS-SYS-MM                  PIC 9(02).                            
           05  WS-SYS-DD                  PIC 9(02).                            
                                                                                
       01  WS-EDITED-DATE                 PIC X(10).                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-F010          THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-F010       THRU 3000-EXIT                       
             UNTIL WS-F010-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F010                                                 
                  I-O  BP13K010.                                                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K010 OPEN ERROR ' WS-K010-STATUS                
              MOVE WS-K010-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-SYSTEM-DATE.                   
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-F010.                                                          
      *-------------------------------------------------------------            
                                                                                
           READ BP13F010 AT END                                                 
                MOVE 'Y'                   TO WS-F010-EOF                       
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1                           TO WS-F010-READ.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-F010.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 4000-READ-K010          THRU 4000-EXIT.                      
                                                                                
           IF WS-K010-FOUND = 'Y'                                               
              PERFORM 5000-REWRITE-K010    THRU 5000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F010          THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-READ-K010.                                                          
      *--------------------------------------------------------*                
                                                                                
           MOVE LOW-VALUES                 TO K010-KEY-FLD.                     
           MOVE F010-KEY-FLD               TO K010-KEY-FLD.                     
                                                                                
           READ BP13K010.                                                       
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN ZEROES                                                          
              MOVE 'Y'                     TO WS-K010-FOUND                     
           WHEN 23                                                              
              ADD 1                        TO WS-F010-NTFND                     
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN READ OF BP13K010. STATUS IS '                   
                      WS-K010-STATUS                                            
              MOVE WS-K010-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-REWRITE-K010.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE ZEROES                     TO K010-NUM-COUNT                    
                                              K010-NUM-RDC                      
                                              K010-NUM-RH                       
                                              K010-NUM-REN                      
                                              K010-NUM-MT                       
                                              K010-NUM-JB                       
                                              K010-NUM-CC                       
                                              K010-NUM-TCP                      
                                              K010-NUM-COUNT2                   
                                              K010-NUM-TOT-NON-ELDER            
                                              K010-NUM-TOT-ELDER-SPS            
                                              K010-NUM-TOT-ELDER-NONSPS         
                                              K010-NUM-REG-ELDER                
                                              K010-NUM-REG-NON-ELDER            
                                              K010-NUM-REG-ELDER-SPS            
                                              K010-NUM-REG-ELDER-NONSPS         
                                              K010-NUM-APP-ELDER                
                                              K010-NUM-APP-NON-ELDER            
                                              K010-NUM-APP-ELDER-SPS            
                                              K010-NUM-APP-ELDER-NONSPS.        
           MOVE 100                        TO K010-NUM-PA.                      
           MOVE F010-NUM-TOT-UNIT          TO K010-NUM-TOT-UNIT.                
           MOVE F010-NUM-TOT-PA            TO K010-NUM-TOT-PA.                  
           MOVE F010-NUM-MAX-PA1           TO K010-NUM-MAX-PA1.                 
           MOVE F010-NUM-MAX-PA2           TO K010-NUM-MAX-PA2.                 
           MOVE F010-NUM-MAX-PA3           TO K010-NUM-MAX-PA3.                 
           MOVE F010-NUM-TOT-ELDER-MIN     TO K010-NUM-TOT-ELDER-MIN.           
                                                                                
           REWRITE BP13K010-TENDER.                                             
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO WS-K010-REWRITE                   
           WHEN 23                                                              
              CONTINUE                                                          
           WHEN 22                                                              
              ADD 1                        TO WS-K010-DUPLICATE                 
              DISPLAY 'DUPLICATE RECORD FOR BP13K010. KEY IS - '                
                      K010-KEY-FLD                                              
           WHEN OTHER                                                           
              DISPLAY 'ERROR REWRITING TO BP13K010. STATUS IS - '               
                      WS-K010-STATUS                                            
              MOVE WS-K010-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
                                                                                
           CLOSE BP13F010                                                       
                 BP13K010.                                                      
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K010 CLOSE ERROR ' WS-K010-STATUS               
              MOVE WS-K010-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           DISPLAY '==============================================='.           
           DISPLAY '      PROGRAM : BP13C52B       ' WS-EDITED-DATE.            
           DISPLAY '==============================================='.           
           DISPLAY ' '.                                                         
           DISPLAY 'BP13F010 RECORDS READ       ' WS-F010-READ.                 
           DISPLAY 'DUPLICATE BP13K010 RECORDS  ' WS-K010-DUPLICATE.            
           DISPLAY 'BP13K010 RECORDS REWRITTEN  ' WS-K010-REWRITE.              
           DISPLAY 'BP13F010 RECORDS NOT FOUND  ' WS-F010-NTFND.                
           DISPLAY ' '.                                                         
           DISPLAY '==============================================='.           
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
