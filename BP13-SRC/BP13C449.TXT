      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.   BP13C449.                                                  
      *AUTHOR.       SK.                                                        
      *DATE-WRITTEN. 12/03/2004.                                                
      *----------------------------------------------------------------*        
      *                                                                *        
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *    SYSTEM ID   : BP13                                          *        
      *                                                                *        
      *    OBJECTIVES  : MATCHING BANK ORIGINATION FILE & BANK BALANCE *        
      *                  FILE FOR PRINT THE REPORT ON CHANGE OF        *        
      *                  HBL TYPE & UPDATE                             *        
      *                                                                *        
      *    INPUT FILES : BP13F671                                      *        
      *                  BP13F673                                      *        
      *                                                                *        
      *    OUTPUT      : P13F671A                                      *        
      *----------------------------------------------------------------*        
      * CHG-NO  BY   DATE      DETAILS                                 *        
      *----------------------------------------------------------------*        
      *BP132400 SK   12032004  NEW PGM                                 *        
      *BP132526 SK   18062004  INCREASE BP13F671 TO 3000               *        
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *----------------------------------------------------------------*        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F671   ASSIGN TO BP13F671.                                
           SELECT BP13F673   ASSIGN TO BP13F673.                                
                                                                                
           SELECT P13F671A   ASSIGN TO P13F671A.                                
           SELECT P13L449A   ASSIGN TO P13L449A.                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F671                                                            
            RECORD CONTAINS 3000 CHARACTERS                                     
            BLOCK CONTAINS     0 RECORDS                                        
            RECORDING MODE    IS F                                              
            LABEL RECORDS    ARE STANDARD.                                      
                                                                                
       COPY BP13F671.                                                           
                                                                                
       FD   BP13F673                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
                                                                                
       COPY BP13F673.                                                           
                                                                                
       FD   P13F671A                                                            
            RECORD CONTAINS 3000 CHARACTERS                                     
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
                                                                                
       01   P13F671A-REC    PIC X(3000).                                        
                                                                                
       FD   P13L449A                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
                                                                                
       01   P13L449A-REC    PIC X(132).                                         
                                                                                
      *----------------------------------------------------------------*        
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
                                                                                
       01  WORK-AREAS.                                                          
           05 WS-MATCH                  PIC 9(08)    VALUE 0.                   
           05 WS-NOT-MATCH              PIC 9(08)    VALUE 0.                   
                                                                                
           05 WS-WRITE-F671             PIC 9(08)    VALUE 0.                   
           05 WS-READ-F671              PIC 9(08)    VALUE 0.                   
           05 WS-READ-F673              PIC 9(08)    VALUE 0.                   
           05 WS-HBL-TYPE               PIC X(01)    VALUE SPACES.              
           05 WS-FLAG                   PIC X(01)    VALUE SPACES.              
                                                                                
           05  WS-LINE-CNT              PIC 9(02)    VALUE 66.                  
           05  WS-PAGE-CNT              PIC 9(08)    VALUE 0.                   
           05  WS-CNT-PRINT-L449        PIC 9(08)    VALUE 1.                   
           05  WS-CNT-WRITE-L449        PIC 9(08)    VALUE 0.                   
                                                                                
       01  WS-F673-REC.                                                         
           05 WS-F673-REGN              PIC X(08)    VALUE SPACES.              
           05 WS-F673-CDE-TYPE          PIC X(03)    VALUE SPACES.              
           05 WS-F673-AMT-OPEN          PIC S9(7)V99 VALUE 0.                   
           05 FILLER                    PIC X(36)    VALUE SPACES.              
           05 WS-F673-AMT-CLOSE         PIC S9(7)V99 VALUE 0.                   
           05 FILLER                    PIC X(35)    VALUE SPACES.              
                                                                                
       01  WS-DATE                      PIC X(8)     VALUE SPACES.              
                                                                                
      *----------------------------------------------------------------*        
      *    P13L449A - LAYOUT                                           *        
      *----------------------------------------------------------------*        
                                                                                
       01  L449-PR-HEAD-01.                                                     
           05  FILLER                   PIC X(14) VALUE 'P13L449A'.             
           05  FILLER                   PIC X(8)  VALUE 'HDBCAT 3'.             
           05  FILLER                   PIC X(25) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(13) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  L449-DATE                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE: '.               
           05  L449-PAGE                PIC ZZZ9.                               
                                                                                
       01  L449-PR-HEAD-02.                                                     
           05  FILLER                   PIC X(45) VALUE SPACES.                 
           05  FILLER                   PIC X(75) VALUE                         
                'CHANGE OF HBL TYPE IN BANK ORIGINATION FILE'.                  
                                                                                
       01  L449-PR-HEAD-03.                                                     
           05  FILLER           PIC X(05)  VALUE ' S-NO'.                       
           05  FILLER           PIC X(01)  VALUE SPACES.                        
           05  FILLER           PIC X(10)  VALUE 'REGN NO'.                     
           05  FILLER           PIC X(10)  VALUE 'HBL TYPE'.                    
           05  FILLER           PIC X(10)  VALUE 'PAY TYPE'.                    
           05  FILLER           PIC X(04)  VALUE SPACES.                        
           05  FILLER           PIC X(12)  VALUE 'OPENING'.                     
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  FILLER           PIC X(12)  VALUE 'CLOSING'.                     
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  FILLER           PIC X(21)  VALUE 'HBL TYPE - MEMO FILE'.        
                                                                                
       01  L449-PR-HEAD-04.                                                     
           05  FILLER                PIC X(132) VALUE ALL '-'.                  
                                                                                
       01  L449-PR-DETAILS.                                                     
           05  L449-NUMBER           PIC ZZZZ9.                                 
           05  FILLER                PIC X(01)  VALUE SPACES.                   
           05  L449-REGN-NO          PIC X(10)  VALUE SPACES.                   
           05  FILLER                PIC X(03)  VALUE SPACES.                   
           05  L449-HBL-TYPE         PIC X(01)  VALUE SPACES.                   
           05  FILLER                PIC X(09)  VALUE SPACES.                   
           05  L449-PAY-TYPE         PIC X(03)  VALUE SPACES.                   
           05  FILLER                PIC X(05)  VALUE SPACES.                   
           05  L449-OPENING          PIC $$$$,$$9.99-.                          
           05  FILLER                PIC X(02)  VALUE SPACES.                   
           05  L449-CLOSING          PIC $$$$,$$9.99-.                          
           05  FILLER                PIC X(11)  VALUE SPACES.                   
           05  L449-HBL-TYPE1        PIC X(01)  VALUE SPACES.                   
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN.                                                               
      *----------------------------------------------------------------*        
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 1100-READ-F671     THRU 1100-EXIT.                           
           PERFORM 1200-READ-F673     THRU 1200-EXIT.                           
                                                                                
           PERFORM 2000-PROCESS       THRU 2000-EXIT                            
                   UNTIL F671-NUM-REGN = HIGH-VALUES                            
                     AND F673-NUM-REGN = HIGH-VALUES.                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  BP13F671                                                 
                       BP13F673                                                 
                OUTPUT P13F671A                                                 
                       P13L449A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-DATE.                             
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
              DELIMITED BY SIZE INTO L449-DATE.                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1100-READ-F671.                                                          
      *----------------------------------------------------------------*        
                                                                                
           READ BP13F671                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F671-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F671                                        
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1200-READ-F673.                                                          
      *----------------------------------------------------------------*        
                                                                                
           READ BP13F673                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F673-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F673                                        
           END-READ.                                                            
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
           MOVE 'N'  TO WS-FLAG.                                                
           MOVE  SPACES  TO WS-F673-REC.                                        
           INITIALIZE       WS-F673-REC.                                        
                                                                                
           IF F671-NUM-REGN = F673-NUM-REGN                                     
              ADD 1     TO WS-MATCH                                             
              PERFORM 2100-CHECK-F673 THRU 2100-EXIT                            
                  UNTIL F673-NUM-REGN = HIGH-VALUES OR                          
                        F671-NUM-REGN NOT = F673-NUM-REGN                       
              PERFORM 3000-WRITE-F671 THRU 3000-EXIT                            
              IF WS-FLAG = 'Y'                                                  
                 PERFORM 4000-PRINT-REPORT THRU 4000-EXIT                       
              END-IF                                                            
                                                                                
              PERFORM 1100-READ-F671  THRU 1100-EXIT                            
           ELSE                                                                 
              IF F671-NUM-REGN > F673-NUM-REGN                                  
                 PERFORM 1200-READ-F673  THRU 1200-EXIT                         
              ELSE                                                              
                 PERFORM 3000-WRITE-F671 THRU 3000-EXIT                         
                 PERFORM 1100-READ-F671  THRU 1100-EXIT                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2100-CHECK-F673.                                                         
      *----------------------------------------------------------------*        
                                                                                
           IF F671-NUM-HBL-TYP = '1'                                            
              IF F673-CDE-PAYMENT-TYPE = 'SMS'                                  
                 MOVE BP13F673-BAL  TO WS-F673-REC                              
                 MOVE 'Y'           TO WS-FLAG                                  
                 MOVE '2'           TO WS-HBL-TYPE                              
              END-IF                                                            
           ELSE                                                                 
           IF F671-NUM-HBL-TYP = '2'                                            
              IF F673-CDE-PAYMENT-TYPE = 'RSL'                                  
                 MOVE BP13F673-BAL  TO WS-F673-REC                              
                 MOVE 'Y'           TO WS-FLAG                                  
                 MOVE '1'           TO WS-HBL-TYPE                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 1200-READ-F673  THRU 1200-EXIT.                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-WRITE-F671.                                                         
      *----------------------------------------------------------------*        
                                                                                
           MOVE  SPACES  TO P13F671A-REC.                                       
           INITIALIZE       P13F671A-REC.                                       
                                                                                
           IF WS-FLAG = 'Y'                                                     
              MOVE F671-NUM-HBL-TYP  TO L449-HBL-TYPE1                          
              MOVE WS-HBL-TYPE       TO F671-NUM-HBL-TYP                        
           END-IF.                                                              
                                                                                
           WRITE P13F671A-REC FROM BP13F671-REC.                                
                                                                                
           ADD 1 TO WS-WRITE-F671.                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-PRINT-REPORT.                                                       
      *----------------------------------------------------------------*        
                                                                                
           MOVE WS-F673-REGN      TO L449-REGN-NO.                              
                                                                                
           EVALUATE WS-F673-CDE-TYPE                                            
              WHEN 'RSL'                                                        
                 MOVE '1'         TO L449-HBL-TYPE                              
              WHEN 'SMS'                                                        
                 MOVE '2'         TO L449-HBL-TYPE                              
           END-EVALUATE.                                                        
                                                                                
           MOVE WS-F673-CDE-TYPE  TO L449-PAY-TYPE.                             
                                                                                
           IF WS-F673-AMT-OPEN IS NOT NUMERIC                                   
              MOVE ZEROS             TO L449-OPENING                            
           ELSE                                                                 
              MOVE WS-F673-AMT-OPEN  TO L449-OPENING                            
           END-IF.                                                              
                                                                                
           IF WS-F673-AMT-CLOSE IS NOT NUMERIC                                  
              MOVE ZEROS             TO L449-CLOSING                            
           ELSE                                                                 
              MOVE WS-F673-AMT-CLOSE TO L449-CLOSING                            
           END-IF.                                                              
                                                                                
           PERFORM 4100-WRITE-P13L449A       THRU 4100-EXIT.                    
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4100-WRITE-P13L449A.                                                     
      *----------------------------------------------------------------*        
                                                                                
           IF WS-LINE-CNT > 53                                                  
              PERFORM 4200-PRINT-HEADING THRU 4200-EXIT                         
              MOVE 1 TO WS-LINE-CNT                                             
           END-IF.                                                              
                                                                                
           MOVE WS-CNT-PRINT-L449  TO    L449-NUMBER.                           
                                                                                
           WRITE P13L449A-REC      FROM  L449-PR-DETAILS   AFTER 1.             
                                                                                
           ADD   1                 TO    WS-LINE-CNT                            
                                         WS-CNT-PRINT-L449                      
                                         WS-CNT-WRITE-L449.                     
                                                                                
           MOVE  SPACES            TO    L449-PR-DETAILS.                       
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4200-PRINT-HEADING.                                                      
      *----------------------------------------------------------------*        
                                                                                
           ADD  1                TO     WS-PAGE-CNT.                            
           MOVE WS-PAGE-CNT      TO     L449-PAGE.                              
                                                                                
           MOVE SPACES           TO     P13L449A-REC.                           
                                                                                
           WRITE P13L449A-REC    AFTER  PAGE.                                   
           WRITE P13L449A-REC    FROM   L449-PR-HEAD-01 AFTER 1.                
           WRITE P13L449A-REC    FROM   L449-PR-HEAD-02 AFTER 1.                
           WRITE P13L449A-REC    FROM   L449-PR-HEAD-03 AFTER 2.                
           WRITE P13L449A-REC    FROM   L449-PR-HEAD-04 AFTER 1.                
                                                                                
           MOVE 5  TO WS-LINE-CNT.                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9999-CLOSE-ROUTINE.                                                      
      *----------------------------------------------------------------*        
                                                                                
           CLOSE  BP13F671                                                      
                  BP13F673                                                      
                  P13F671A                                                      
                  P13L449A.                                                     
                                                                                
           DISPLAY '*---------- BP13C449 -------------*'.                       
           DISPLAY SPACES.                                                      
           DISPLAY 'DATE : ' L449-DATE.                                         
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECORDS READ    :'.                                   
           DISPLAY '   BP13F671           : '  WS-READ-F671.                    
           DISPLAY '   BP13F673           : '  WS-READ-F673.                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF MATCH RECS     : '  WS-MATCH.                        
           DISPLAY SPACES.                                                      
           DISPLAY 'WRITE BP13F671        : '  WS-WRITE-F671                    
           DISPLAY '*---------------------------------*'.                       
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
