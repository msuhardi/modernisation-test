       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CF7Q.                                                  
       AUTHOR.       AM25.                                                      
       DATE-WRITTEN. 18/01/2024.                                                
      *****************************************************************         
      *                SOC - SYSTEM OF COMMITMENT (BP13)              *         
      *===============================================================*         
      *  OBJECTIVES : TO EXTRACT 5-ROOM SUCCESSFUL FLATS WITHOUT      *         
      *               GEN3 AFTER TAGGING                              *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP13F740 I     SEQUENTIAL 500                                 *         
      * BP13K813 I     SEQUENTIAL 1000 K813-KEY-FLD                   *         
      * P13F740A O     SEQUENTIAL 500                                 *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *                                                               *         
      * CHG-REQ#  DDMMCCYY   BY   REMARKS                             *         
      * --------  --------  ----  ----------------------------------- *         
      * BP139852  18012024  AM25  NEW PROGRAM                         *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN      TO BP13F740.                             
                                                                                
           SELECT BP13K813 ASSIGN      TO BP13K813                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K813-KEY-FLD                                     
              FILE STATUS   IS WS-K813-STATUS.                                  
                                                                                
           SELECT P13F740A ASSIGN      TO P13F740A.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F740                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K813                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K813.                                                           
                                                                                
       FD  P13F740A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F740A-REC                PIC X(500).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K813-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-UNIT-OFFER           PIC S9(4)       VALUE ZEROES.            
           05  WS-UNIT-OFFER-NUM       PIC 9(4)        VALUE ZEROES.            
           05  WS-UNIT-OFFER-GEN3-NUM  PIC 9(4)        VALUE ZEROES.            
           05  WS-NT-FT-QUEUE-NUM5     PIC 9(5)        VALUE ZEROES.            
           05  FILLER REDEFINES WS-NT-FT-QUEUE-NUM5.                            
               10 WS-NT-FT-QUEUE-NUM1  PIC 9.                                   
               10 WS-NT-FT-QUEUE-NUM4  PIC 9(4).                                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K813-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-F740A-WRITE          PIC 9(6)        VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-F740-EOF             PIC X           VALUE 'N'.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F740   THRU 2000-EXIT.                         
           PERFORM 3000-MAIN-PROCESSING THRU 3000-EXIT                          
              UNTIL WS-F740-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13K813                                                 
                OUTPUT P13F740A.                                                
                                                                                
           IF (WS-K813-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K813 FAIL STATUS=' WS-K813-STATUS               
              MOVE WS-K813-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13F740.                                                      
      *****************************************************************         
                                                                                
           READ BP13F740                                                        
              AT END                                                            
                 MOVE 'Y'              TO WS-F740-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F740-READ                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-MAIN-PROCESSING.                                                    
      *****************************************************************         
                                                                                
           IF F740-CDE-FLAT-TYPE(1:1) = '5' AND                                 
              F740-CDE-REQUEST-STATUS = 'S' AND                                 
              F740-NUM-GEN3 NOT = 'Y'                                           
              PERFORM 3100-READ-BP13K813 THRU 3100-EXIT                         
                                                                                
              IF WS-K813-STATUS = 00                                            
                 PERFORM 4000-PROCESS-TAGGING THRU 4000-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F740 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3100-READ-BP13K813.                                                      
      *****************************************************************         
                                                                                
           MOVE F740-CDE-NT1           TO K813-NUM-ZONE.                        
           MOVE F740-CDE-FLAT-TYPE     TO K813-NUM-FLAT-TYPE.                   
           MOVE F740-DTE-BALLOT        TO K813-DTE-BALLOT.                      
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K813-READ                          
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K813: '                          
                    'REGN=' F740-NUM-REGN ',KEY=' K813-KEY-FLD                  
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K813 FAIL STATUS=' WS-K813-STATUS            
                 MOVE WS-K813-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-PROCESS-TAGGING.                                                    
      *****************************************************************         
                                                                                
           IF K813-NUM-UNIT-OFFER NOT NUMERIC                                   
              MOVE ZEROES              TO K813-NUM-UNIT-OFFER                   
           END-IF.                                                              
                                                                                
           IF K813-NUM-UNIT-OFFER-GEN3 NOT NUMERIC                              
              MOVE ZEROES              TO K813-NUM-UNIT-OFFER-GEN3              
           END-IF.                                                              
                                                                                
           IF F740-NUM-NT-FT-QUEUE NOT NUMERIC                                  
              MOVE ZEROES              TO F740-NUM-NT-FT-QUEUE                  
           END-IF.                                                              
                                                                                
           MOVE K813-NUM-UNIT-OFFER    TO WS-UNIT-OFFER-NUM.                    
           MOVE K813-NUM-UNIT-OFFER-GEN3                                        
                                       TO WS-UNIT-OFFER-GEN3-NUM.               
           MOVE F740-NUM-NT-FT-QUEUE   TO WS-NT-FT-QUEUE-NUM5.                  
                                                                                
           COMPUTE WS-UNIT-OFFER = WS-UNIT-OFFER-NUM -                          
                                   WS-UNIT-OFFER-GEN3-NUM.                      
                                                                                
           IF WS-UNIT-OFFER > 0 AND WS-NT-FT-QUEUE-NUM5 > 0                     
              IF WS-NT-FT-QUEUE-NUM4 > WS-UNIT-OFFER                            
                 PERFORM 4100-WRITE-P13F740A THRU 4100-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4100-WRITE-P13F740A.                                                     
      *****************************************************************         
                                                                                
           WRITE P13F740A-REC FROM BP13F740-REC.                                
           ADD 1                       TO WS-F740A-WRITE.                       
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13F740                                                       
                 BP13K813                                                       
                 P13F740A.                                                      
                                                                                
           IF WS-K813-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K813 FAIL STATUS=' WS-K813-STATUS              
              MOVE WS-K813-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '*******************************'.                           
           DISPLAY '* PROGRAM BP13CF7Q            *'.                           
           DISPLAY '* CONTROL COUNTS              *'.                           
           DISPLAY '*-----------------------------*'.                           
           DISPLAY '* REC READ F740      : ' WS-F740-READ ' *'.                 
           DISPLAY '* REC READ K813      : ' WS-K813-READ ' *'.                 
           DISPLAY '* REC WRITTEN F740A  : ' WS-F740A-WRITE ' *'.               
           DISPLAY '*******************************'.                           
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
