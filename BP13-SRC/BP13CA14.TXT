       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CA14.                                                 
      *AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
      *DATE-WRITTEN.  17 APR 2009.                                              
      *============================================================*            
      * BP13 - SYSTEM OF COMMITMENT                                *            
      *============================================================*            
      * OBJECTIVE :  CHECK IF NO OF 2ND TIMER RECS IN BP13FB20     *            
      *              MATCHES KA00-NUM-REG-PA-UPG                   *            
      *                                                            *            
      * INPUT     :  BP13KA00                                      *            
      *              BP13FB20                                      *            
      * OUTPUT    :  BP13MAIL                                      *            
      *                                                            *            
      *----------------------------------------------------------- *            
      * CHG REF  BY    DATE      DESCRIPTION                       *            
      * -------- ----  --------  ------------------------------    *            
      * BP133530 RB13  20090417  NEW PROGRAM                       *            
      * BP133544 RB13  20090601  ADD CHECK FOR PRIORITY END DATE   *            
      *============================================================*            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13FB20 ASSIGN TO BP13FB20.                                  
                                                                                
           SELECT BP13KA00 ASSIGN TO BP13KA00                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS KA00-KEY-FLD                               
                  FILE STATUS     IS WS-KA00-STATUS.                            
                                                                                
           SELECT BP13MAIL ASSIGN TO BP13MAIL.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13FB20                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 3300 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13FB20.                                                           
                                                                                
       FD   BP13KA00                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13KA00.                                                           
                                                                                
       FD  BP13MAIL                                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING  MODE  IS F.                                               
       01  MAIL-PRTREC                   PIC X(80).                             
                                                                                
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
       01  WS-DATE                       PIC X(8)   VALUE SPACES.               
       01  WS-SNO                        PIC 9(5)   VALUE ZEROS.                
                                                                                
       01  WS-COUNT.                                                            
           05  WS-READ-FB20              PIC 9(5) COMP VALUE ZEROES.            
           05  WS-READ-KA00              PIC 9(5) COMP VALUE ZEROES.            
           05  WS-WRIT-LA10              PIC 9(5) COMP VALUE ZEROES.            
                                                                                
       01  WS-SWITCH.                                                           
           05  WS-FB20-END-FLAG          PIC X(01)   VALUE 'N'.                 
               88  WS-FB20-END                       VALUE 'Y'.                 
           05  WS-HEAD-WRITE-FLAG        PIC X(01)   VALUE 'N'.                 
               88  WS-HEAD-WRITE                     VALUE 'Y'.                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-KA00-STATUS            PIC 9(2)    VALUE ZEROES.              
           05  WS-NUM-2ND-TIMER          PIC 9(5)    VALUE ZEROES.              
           05  WS-PREV-PROJECT           PIC X(3)    VALUE spaces.              
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
      *  for testing purpose -------                                            
      *01  MAIL-HDR1.                                                           
      *    05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB3'.                 
      *    05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
      *01  MAIL-HDR2.                                                           
      *    05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
      *    05  MAIL-SENDID     PIC X(07)  VALUE 'CS91861'.                      
      *    05  FILLER          PIC X(09)  VALUE '@SGPHDB3>'.                    
      *    05  FILLER          PIC X(53)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1.                                                           
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAILRCPTTOID    PIC X(17)  VALUE SPACES.                         
           05  FILLER          PIC X(58)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2.                                                           
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(16)  VALUE                                 
               'KKL1@HDB.GOV.SG>'.                                              
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2B.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(16)  VALUE                                 
               'WLY1@HDB.GOV.SG>'.                                              
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2C.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(16)  VALUE                                 
               'LSB1@HDB.GOV.SG>'.                                              
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2D.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(16)  VALUE                                 
               'LMS1@HDB.GOV.SG>'.                                              
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2E.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(16)  VALUE                                 
               'IL5@HDB.GOV.SG>'.                                               
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2F.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(16)  VALUE                                 
               'DW5@HDB.GOV.SG>'.                                               
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4.                                                           
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(26)  VALUE                                 
               'SOC System - Email Alert'.                                      
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5.                                                           
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  MAILTOID        PIC X(17)  VALUE SPACES.                         
           05  FILLER          PIC X(58)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6.                                                           
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(16)  VALUE                                 
               'KKL1@HDB.GOV.SG>'.                                              
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6B.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(16)  VALUE                                 
               'WLY1@HDB.GOV.SG>'.                                              
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6C.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(16)  VALUE                                 
               'LSB1@HDB.GOV.SG>'.                                              
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6D.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(16)  VALUE                                 
               'LMS1@HDB.GOV.SG>'.                                              
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6E.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(16)  VALUE                                 
               'IL5@HDB.GOV.SG>'.                                               
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6F.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(16)  VALUE                                 
               'DW5@HDB.GOV.SG>'.                                               
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL7.                                                           
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  MAIL-SUBJECT    PIC X(40)  VALUE                                 
               '2ND-TIMER COUNT NOT TALLY, PLEASE CHECK '.                      
           05  FILLER          PIC X(30)  VALUE SPACES.                         
                                                                                
       01  MAIL-BODY01.                                                         
           05 FILLER                   PIC X(6)   VALUE 'DATE: '.               
           05 MAIL-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY02.                                                         
           05  FILLER                      PIC X(05)  VALUE SPACES.             
           05  MAIL-PRJECT                 PIC X(03)  VALUE SPACES.             
           05  FILLER                      PIC X(01)  VALUE SPACES.             
           05  MAIL-FB20-DES               PIC X(17)  VALUE SPACES.             
           05  MAIL-FB20-CAT               PIC Z(04)9 VALUE ZEROES.             
           05  FILLER                      PIC X(04)  VALUE SPACES.             
           05  MAIL-KA00-DES               PIC X(17)  VALUE SPACES.             
           05  MAIL-KA00-CAT               PIC Z(04)9 VALUE ZEROES.             
           05  FILLER                      PIC X(77)  VALUE SPACES.             
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
                                                                                
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
       0000-MAIN.                                                               
      *------------------------------------------------------------*            
           PERFORM 1000-INIT-ROUTINE  THRU 1000-EXIT.                           
           PERFORM 2000-READ-FB20     THRU 2000-EXIT.                           
           PERFORM 3000-MAIN-PROCESS  THRU 3000-EXIT                            
                   UNTIL WS-FB20-END.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-INIT-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT BP13FB20                                                  
                      BP13KA00                                                  
               OUTPUT BP13MAIL.                                                 
                                                                                
           IF WS-KA00-STATUS NOT = '00' AND NOT = '97'                          
              DISPLAY 'ERROR OPENING BP13KA00 - ' WS-KA00-STATUS                
              MOVE WS-KA00-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                          
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO MAIL-DATE.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-FB20.                                                          
      *-------------------------------------------------------------            
           READ BP13FB20                                                        
             AT END                                                             
                MOVE 'Y' TO WS-FB20-END-FLAG                                    
                PERFORM 4000-CHECK-KA00 THRU 4000-EXIT                          
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-READ-FB20.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------------------------------------------*            
       3000-MAIN-PROCESS.                                                       
      *------------------------------------------------------------*            
                                                                                
           IF FB20-NUM-PROJECT = WS-PREV-PROJECT                                
              IF  (FB20-CDE-ALLOC-SCH = 'PA ') AND (FB20-NUM-HH = 'G')          
              AND (FB20-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES)                
                 ADD 1 TO WS-NUM-2ND-TIMER                                      
              END-IF                                                            
           ELSE                                                                 
              IF WS-PREV-PROJECT NOT = SPACES AND LOW-VALUES                    
                 PERFORM 4000-CHECK-KA00 THRU 4000-EXIT                         
              END-IF                                                            
              MOVE FB20-NUM-PROJECT   TO   WS-PREV-PROJECT                      
              MOVE ZEROES             TO   WS-NUM-2ND-TIMER                     
              IF (FB20-CDE-ALLOC-SCH = 'PA ') AND (FB20-NUM-HH = 'G')           
              AND (FB20-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES)                
                 ADD 1 TO WS-NUM-2ND-TIMER                                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-FB20 THRU 2000-EXIT.                               
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------*            
       4000-CHECK-KA00.                                                         
      *------------------------------------------------------------*            
           MOVE SPACES           TO KA00-KEY-FLD.                               
           MOVE WS-PREV-PROJECT  TO KA00-NUM-PRJCT.                             
                                                                                
           READ BP13KA00.                                                       
                                                                                
           EVALUATE WS-KA00-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-READ-KA00                                          
                 IF KA00-DTE-PRIO-END >= WS-DATE                                
                    PERFORM 5000-CHECK-COUNT THRU 5000-EXIT                     
                 END-IF                                                         
              WHEN 23                                                           
                 DISPLAY 'KA00 KEY NOT FOUND: ' KA00-KEY-FLD                    
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13KA00: ' WS-KA00-STATUS              
                 DISPLAY 'KA00 KEY              : ' KA00-KEY-FLD                
                 MOVE WS-KA00-STATUS TO RETURN-CODE                             
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------------------------------------------------*            
       5000-CHECK-COUNT.                                                        
      *------------------------------------------------------------*            
            MOVE 'N'                TO WS-HEAD-WRITE-FLAG.                      
                                                                                
            IF KA00-NUM-REG-PA-UPG NOT NUMERIC                                  
               MOVE ZEROES TO KA00-NUM-REG-PA-UPG                               
            END-IF.                                                             
                                                                                
            IF KA00-NUM-REG-PA-UPG NOT = WS-NUM-2ND-TIMER                       
               MOVE 'KA00 REG UPG    :'  TO   MAIL-KA00-DES                     
               MOVE KA00-NUM-REG-PA-UPG  TO   MAIL-KA00-CAT                     
               MOVE 'FB20 2ND TIMERS :'  TO   MAIL-FB20-DES                     
               MOVE WS-NUM-2ND-TIMER     TO   MAIL-FB20-CAT                     
               PERFORM 5100-WRITE-REPORT THRU 5100-EXIT                         
            END-IF.                                                             
                                                                                
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------*            
       5100-WRITE-REPORT.                                                       
      *------------------------------------------------------------*            
           ADD 1 TO WS-SNO.                                                     
                                                                                
           IF WS-SNO = 1                                                        
              PERFORM 6000-OUTPUT-EMAIL-HEADER THRU 6000-EXIT                   
           END-IF.                                                              
                                                                                
           IF NOT WS-HEAD-WRITE                                                 
              MOVE WS-PREV-PROJECT    TO MAIL-PRJECT                            
              ADD +1                  TO WS-WRIT-LA10                           
              MOVE 'Y'                TO WS-HEAD-WRITE-FLAG                     
           END-IF.                                                              
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-BODY02.                                  
           MOVE SPACES                TO MAIL-BODY02                            
                                         MAIL-PRTREC.                           
                                                                                
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6000-OUTPUT-EMAIL-HEADER.                                                
      *-------------------------------------------------------------            
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-HDR1                                     
           WRITE MAIL-PRTREC FROM MAIL-HDR2.                                    
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL2.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL2B.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL2C.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL2D.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL2E.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL2F.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL3.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL4.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL6.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL6B.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL6C.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL6D.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL6E.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL6F.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL7.                                    
           WRITE MAIL-PRTREC FROM MAIL-BODY01.                                  
           MOVE  SPACES      TO   MAIL-PRTREC.                                  
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       7000-OUTPUT-EMAIL-FOOTER.                                                
      *-------------------------------------------------------------            
                                                                                
           MOVE  SPACES      TO   MAIL-PRTREC.                                  
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-END.                                     
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           IF WS-SNO > 0                                                        
              PERFORM 7000-OUTPUT-EMAIL-FOOTER THRU 7000-EXIT                   
           END-IF.                                                              
                                                                                
           DISPLAY '- BP13CA14 CONTROL TOTAL -----'.                            
           DISPLAY 'NO OF RECORDS READ    (BP13FB20) : '                        
                                               WS-READ-FB20.                    
           DISPLAY 'NO OF RECORDS READ    (BP13KA00) : '                        
                                               WS-READ-KA00.                    
           DISPLAY 'NO OF RECORDS WRITTEN (BP13MAIL) : '                        
                                               WS-WRIT-LA10.                    
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13FB20                                                       
                 BP13KA00                                                       
                 BP13MAIL.                                                      
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
