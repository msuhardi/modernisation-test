      *------------------------------------------------------------*            
       IDENTIFICATION DIVISION.                                                 
      *------------------------------------------------------------*            
       PROGRAM-ID.   BP13C005.                                                  
      *AUTHOR.       SK.                                                        
      *DATE-WRITTEN. 15/12/2004.                                                
                                                                                
      *------------------------------------------------------------*            
      * OBJECTIVE :                                                *            
      *  1. TO DEBAR THE NRIC WHERE THE LESSEE CHECK = Y           *            
      *     AND THE SPOUSE NRIC MATCH WITH ANY NRIC                *            
      *                                                            *            
      *  INPUT FILE :- BP13F740                                    *            
      *                BP13K825                                    *            
      *  I-O           BP13K665                                    *            
      *  OUTPUT FILE:- BP04K010                                    *            
      *                BP13L005                                    *            
      *                                                            *            
      *------------------------------------------------------------*            
      * CHG REQ# BY   DATE       REMARKS                           *            
      * -------- ---- ---------  ----------------------------------*            
      * BP132626 SK   15/12/2004 NEW PROGRAM.                      *            
      *               22/12/2004 INCLUDE BP17K890.                 *            
      * BP135599 PCL4 11/12/2014 TO UPDATE K010-INTERFACE-SYS      *            
      *------------------------------------------------------------*            
                                                                                
      *------------------------------------------------------------*            
       ENVIRONMENT DIVISION.                                                    
      *------------------------------------------------------------*            
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *------------------------------------------------------------*            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *------------------------------------------------------------*            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K665 ASSIGN TO BP13K665                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS WS-K665-KEY-FLD                            
                  FILE STATUS     IS WS-K665-STATUS.                            
                                                                                
           SELECT BP13K825 ASSIGN TO BP13K825                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K825-KEY-FLD                               
                  FILE STATUS     IS WS-K825-STATUS.                            
                                                                                
           SELECT BP04K010 ASSIGN TO BP04K010                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K010-PRIME-KEY                             
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
           SELECT BP17K890 ASSIGN TO BP17K890                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K890-KEY-FLD                               
                  FILE STATUS     IS WS-K890-STATUS.                            
                                                                                
           SELECT BP13L005 ASSIGN TO BP13L005.                                  
                                                                                
                                                                                
      *------------------------------------------------------------*            
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *------------------------------------------------------------*            
                                                                                
       FD  BP13F740   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 500 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD                                
                      RECORDING MODE IS F.                                      
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K665   RECORD CONTAINS 50 CHARACTERS.                            
       01  WS-BP13K665-REC.                                                     
           05  WS-K665-KEY-FLD         PIC X(08).                               
           05  FILLER                  PIC X(42).                               
                                                                                
       FD  BP04K010   RECORD CONTAINS 300 CHARACTERS.                           
       COPY P04K010.                                                            
                                                                                
       FD  BP13K825   RECORD CONTAINS 200 CHARACTERS.                           
       COPY BP13K825.                                                           
                                                                                
       FD  BP17K890   RECORD CONTAINS 80  CHARACTERS.                           
       COPY BP17K890.                                                           
                                                                                
       FD BP13L005    RECORD CONTAINS 132 CHARACTERS                            
                      RECORDING MODE IS F.                                      
       01 BP13L005-REC                   PIC X(132).                            
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF               PIC X    VALUE 'N'.                    
               88 F740-EOF                        VALUE 'Y'.                    
           05  WS-FOUND                  PIC X    VALUE SPACES.                 
           05  WS-DEBARMENT-TAG          PIC X    VALUE SPACES.                 
           05  WS-DEBAR-TAG              PIC X    VALUE SPACES.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT-F740          PIC 9(6) VALUE ZEROS.                  
           05  WS-K665-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K825-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K665-WRITE             PIC 9(6) VALUE ZEROS.                  
           05  WS-K010-WRITE             PIC 9(6) VALUE ZEROS.                  
           05  WS-LINE-CTR               PIC 9(6) VALUE 60.                     
           05  WS-PAGE-CTR               PIC 9(6) VALUE ZEROS.                  
           05  WS-SEQ-NO                 PIC 9(6) VALUE ZEROS.                  
           05  WS-PREV-REGN              PIC X(8) VALUE SPACES.                 
           05  WS-COUNT1                 PIC 9(2) VALUE ZEROS.                  
           05  WS-COUNT2                 PIC 9(2) VALUE ZEROS.                  
           05  WS-EX-HDB-REF.                                                   
               10 WS-EX-HDB-REF-ACC      PIC X(9) VALUE SPACES.                 
               10 WS-EX-HDB-REF-LESSEE   PIC X(2) VALUE SPACES.                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K665-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K010-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K825-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K890-STATUS            PIC 99   VALUE ZEROS.                  
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CURR-CC                 PIC 9(02).                             
           05 WS-YYMMDD.                                                        
              10 WS-CURR-YY              PIC 9(02).                             
              10 WS-CURR-MM              PIC 9(02).                             
              10 WS-CURR-DD              PIC 9(02).                             
                                                                                
       01  WS-DEB-TABLE.                                                        
           05 WS-DEB-REC                 OCCURS 10 TIMES.                       
              10 WS-NRIC                 PIC X(09).                             
              10 WS-NAME                 PIC X(66).                             
              10 WS-LESSEE               PIC X(01).                             
              10 WS-DEBAR                PIC X(01).                             
                                                                                
       01  WS-SPOUCE-TAB.                                                       
           05 WS-DEB-SPOUCE              OCCURS 10 TIMES.                       
              10 WS-SPC-NRIC             PIC X(09).                             
                                                                                
       COPY BP13K665.                                                           
                                                                                
      *------------------------------------------------------------*            
      *   REPORT LAYOUT                                                         
      *------------------------------------------------------------*            
       01  L005-PR-HEAD-01.                                                     
           05  FILLER              PIC X(8)      VALUE 'BP13L005'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L005-DATE           PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L005-PAGENO         PIC ZZZZ9 .                                  
                                                                                
       01  L005-PR-HEAD-02.                                                     
           05  FILLER              PIC X(44)     VALUE SPACES .                 
           05  FILLER              PIC X(56)     VALUE                          
               'DEBARMENT REPORT FOR SERS PORT TAG CASES'.                      
           05  FILLER              PIC X(30)     VALUE                          
               'SALES UNIT - APPLICATION TEAM '.                                
                                                                                
       01  L005-PR-HEAD-04.                                                     
           05  FILLER              PIC X(38)     VALUE                          
               'SEQ.    REGN      REF-NO  NAME'.                                
           05  FILLER              PIC X(20)     VALUE SPACES .                 
           05  FILLER              PIC X(54)     VALUE                          
               'I/C NO.    DEB-REF        REASON  EFF-DATE   PERIOD'.           
           05  FILLER              PIC X(12)     VALUE                          
               'ALLO  PORT'.                                                    
           05  FILLER              PIC X(03)     VALUE SPACES.                  
                                                                                
       01  L005-PR-DTL-01.                                                      
           05  L005-SEQ-NO         PIC ZZZZ.                                    
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L005-NUM-REGN       PIC X(08).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  L005-NUM-REF        PIC X(06).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L005-NME-OCCP       PIC X(30).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L005-NUM-NRIC       PIC X(09).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L005-DEB-REF-NO     PIC X(13).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L005-DEB-CODE       PIC X(02).                                   
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  L005-EFF-DATE       PIC X(10).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L005-PERIOD         PIC X(03).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  L005-ALLO           PIC X(03).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L005-PORT           PIC X(01).                                   
                                                                                
       01  L005-PR-LINE            PIC X(132)    VALUE ALL '-'.                 
                                                                                
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
                                                                                
      *------------------------------------------------------------*            
       0000-MAIN.                                                               
      *------------------------------------------------------------*            
                                                                                
            PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                           
            PERFORM 2000-READ-F740    THRU 2000-EXIT.                           
            PERFORM 3000-PROCESS      THRU 3000-EXIT                            
                    UNTIL F740-EOF.                                             
            PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1000-OPEN-FILES.                                                         
      *------------------------------------------------------------*            
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13K825                                                 
                       BP17K890                                                 
                I-O    BP13K665                                                 
                       BP04K010                                                 
                OUTPUT BP13L005.                                                
                                                                                
           IF WS-K665-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K665-STATUS ' WS-K665-STATUS              
              MOVE WS-K665-STATUS       TO RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                          
                                                                                
           IF WS-K825-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K825-STATUS ' WS-K825-STATUS              
              MOVE WS-K825-STATUS       TO RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                          
                                                                                
           IF WS-K010-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K010-STATUS ' WS-K010-STATUS              
              MOVE WS-K010-STATUS       TO RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                          
                                                                                
           IF WS-K890-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K890-STATUS ' WS-K890-STATUS              
              MOVE WS-K890-STATUS       TO RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                          
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
                                                                                
           STRING WS-CURR-DD '/'                                                
                  WS-CURR-MM '/'                                                
                  WS-CURR-CC                                                    
                  WS-CURR-YY DELIMITED BY SIZE                                  
                             INTO L005-DATE.                                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2000-READ-F740.                                                          
      *------------------------------------------------------------*            
                                                                                
           READ BP13F740 AT END                                                 
                MOVE 'Y' TO WS-F740-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD   1     TO WS-READ-CNT-F740.                                     
           MOVE SPACES TO WS-FOUND.                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3000-PROCESS.                                                            
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES             TO WS-DEB-TABLE                              
                                      WS-SPOUCE-TAB.                            
           MOVE ZEROS              TO WS-COUNT1 WS-COUNT2.                      
           MOVE LOW-VALUES         TO K825-NUM-NRIC.                            
           MOVE F740-NUM-REGN      TO K825-NUM-REGN.                            
                                                                                
           START BP13K825 KEY IS >= K825-KEY-FLD                                
                 INVALID KEY                                                    
                 DISPLAY 'READING ERROR, K825-STATUS ' WS-K825-STATUS           
                 MOVE WS-K825-STATUS       TO RETURN-CODE                       
                 PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                       
                                                                                
           PERFORM 3100-PROCESS-K825   THRU 3100-EXIT                           
                   UNTIL K825-NUM-REGN NOT = F740-NUM-REGN                      
                   OR    K825-KEY-FLD      = HIGH-VALUES.                       
                                                                                
           PERFORM 3150-PROCESS-DEBAR THRU 3150-EXIT.                           
           PERFORM 2000-READ-F740     THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3100-PROCESS-K825.                                                       
      *------------------------------------------------------------*            
                                                                                
            READ BP13K825 NEXT                                                  
                 AT END MOVE HIGH-VALUES TO K825-KEY-FLD                        
                 GO TO 3100-EXIT.                                               
                                                                                
            ADD 1 TO WS-K825-READ.                                              
            ADD 1 TO WS-COUNT1.                                                 
                                                                                
            IF K825-NUM-REGN = F740-NUM-REGN                                    
               MOVE K825-NUM-NRIC         TO WS-NRIC  (WS-COUNT1)               
               MOVE K825-NME-OCCP         TO WS-NAME  (WS-COUNT1)               
               MOVE K825-NUM-LESSEE-CHECK TO WS-LESSEE(WS-COUNT1)               
               IF K825-NUM-NRIC = F740-NUM-NRIC1 OR                             
                                  F740-NUM-NRIC2 OR                             
                                  F740-NUM-NRIC3 OR                             
                                  F740-NUM-NRIC4                                
                  MOVE K825-NUM-NRIC-SPOUSE  TO WS-SPC-NRIC(WS-COUNT1)          
                  MOVE 'Y'  TO WS-DEBAR(WS-COUNT1)                              
               END-IF                                                           
            END-IF.                                                             
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------*            
       3150-PROCESS-DEBAR.                                                      
      *------------------------------------------------------------*            
                                                                                
            PERFORM VARYING WS-COUNT1 FROM 1 BY 1                               
               UNTIL WS-COUNT1 > 10                                             
               IF WS-SPC-NRIC(WS-COUNT1) NOT = SPACES AND LOW-VALUES            
                  PERFORM VARYING WS-COUNT2 FROM 1 BY 1                         
                     UNTIL WS-COUNT2 > 10                                       
                       IF WS-SPC-NRIC(WS-COUNT1) = WS-NRIC(WS-COUNT2)           
                          MOVE 'Y'  TO WS-DEBAR(WS-COUNT2)                      
                       END-IF                                                   
                  END-PERFORM                                                   
               END-IF                                                           
            END-PERFORM.                                                        
                                                                                
            PERFORM 4000-GET-DEBARMENT-NO  THRU 4000-EXIT.                      
                                                                                
            PERFORM VARYING WS-COUNT1 FROM 1 BY 1                               
               UNTIL WS-COUNT1 > 10                                             
               IF WS-DEBAR(WS-COUNT1) = 'Y'                                     
                  PERFORM 5000-PREPARE-DEBARMENT-REC  THRU 5000-EXIT            
               END-IF                                                           
            END-PERFORM.                                                        
                                                                                
       3150-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------*            
       4000-GET-DEBARMENT-NO.                                                   
      *------------------------------------------------------------*            
                                                                                
           MOVE  LOW-VALUES    TO WS-K665-KEY-FLD.                              
           START BP13K665 KEY IS >= WS-K665-KEY-FLD                             
                 INVALID KEY                                                    
                 DISPLAY 'READING ERROR, K665-STATUS ' WS-K665-STATUS           
                 MOVE WS-K665-STATUS       TO RETURN-CODE                       
                 PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                       
                                                                                
           PERFORM 4100-READ-BP13K665 THRU 4100-EXIT                            
                   UNTIL WS-K665-KEY-FLD = HIGH-VALUES.                         
                                                                                
           IF K665-KEY-FLD = SPACES OR LOW-VALUES                               
              MOVE 0                TO  K665-NUM-LAST-DEBREF-ID                 
           END-IF.                                                              
                                                                                
           ADD   1                  TO  K665-NUM-LAST-DEBREF-ID.                
           MOVE  'SL'               TO  K665-NUM-DEBREF-AO                      
                                        K665-NUM-LAST-DEBREF-AO.                
           MOVE  WS-CURR-DATE       TO  K665-DTE-ISSUE-DEBARMENT.               
           MOVE  WS-CURR-DATE(1:4)  TO  K665-NUM-LAST-DEBREF-YR.                
           MOVE  'P13C005'          TO  K665-NUM-USERID.                        
                                                                                
           IF WS-DEBARMENT-TAG = 'Y'                                            
              REWRITE WS-BP13K665-REC FROM BP13K665-REC                         
           ELSE                                                                 
              WRITE   WS-BP13K665-REC FROM BP13K665-REC.                        
                                                                                
           EVALUATE WS-K665-STATUS                                              
           WHEN 00                                                              
              MOVE  'Y'               TO  WS-DEBARMENT-TAG                      
              ADD   1                 TO  WS-K665-WRITE                         
                                                                                
           WHEN 22                                                              
              REWRITE WS-BP13K665-REC FROM BP13K665-REC                         
                      INVALID KEY                                               
                      DISPLAY 'REWRITING ERROR, K665-STATUS '                   
                      WS-K665-STATUS                                            
                      MOVE WS-K665-STATUS       TO RETURN-CODE                  
                      PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                   
              END-REWRITE                                                       
              ADD   1                 TO  WS-K665-WRITE                         
              MOVE  'Y'               TO   WS-DEBARMENT-TAG                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITING ERROR, K665-STATUS ' WS-K665-STATUS              
              MOVE WS-K665-STATUS     TO   RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4100-READ-BP13K665.                                                      
      *------------------------------------------------------------*            
                                                                                
           READ BP13K665 NEXT                                                   
                AT END MOVE HIGH-VALUES TO WS-K665-KEY-FLD                      
                GO TO 4100-EXIT.                                                
                                                                                
           ADD 1 TO WS-K665-READ.                                               
                                                                                
           IF WS-K665-KEY-FLD NOT = SPACES AND LOW-VALUES                       
              MOVE WS-BP13K665-REC TO BP13K665-REC.                             
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       5000-PREPARE-DEBARMENT-REC.                                              
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES TO P04K010-REC.                                          
           PERFORM 5050-GET-EX-SCH       THRU 5050-EXIT.                        
           PERFORM 5100-WRITE-BP04K010   THRU 5100-EXIT.                        
           PERFORM 7000-PRINT-L005       THRU 7000-EXIT.                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       5050-GET-EX-SCH.                                                         
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES             TO WS-EX-HDB-REF.                            
           MOVE LOW-VALUES         TO K890-KEY-FLD.                             
           MOVE WS-NRIC(WS-COUNT1) TO K890-NUM-UIN.                             
                                                                                
           START BP17K890 KEY IS >= K890-KEY-FLD                                
                 INVALID KEY                                                    
                 DISPLAY 'READING ERROR, K890-STATUS ' WS-K890-STATUS           
                 MOVE WS-K890-STATUS       TO RETURN-CODE                       
                 PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                       
                                                                                
           PERFORM 5060-READNEXT       THRU 5060-EXIT                           
                   UNTIL K890-NUM-UIN NOT = WS-NRIC(WS-COUNT1)                  
                      OR K890-KEY-FLD      = HIGH-VALUES.                       
                                                                                
       5050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       5060-READNEXT.                                                           
      *------------------------------------------------------------*            
                                                                                
            READ BP17K890 NEXT                                                  
                 AT END MOVE HIGH-VALUES TO K890-KEY-FLD                        
                 GO TO 5060-EXIT.                                               
                                                                                
            IF WS-NRIC(WS-COUNT1) = K890-NUM-UIN                                
               MOVE K890-NUM-HDB-REF      TO WS-EX-HDB-REF                      
            END-IF.                                                             
                                                                                
       5060-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------*            
       5100-WRITE-BP04K010.                                                     
      *------------------------------------------------------------*            
                                                                                
           MOVE 'BP13'                TO K010-INTERFACE-SYS.                    
           MOVE 'EW'                  TO K010-REASON-CODE                       
           MOVE '999'                 TO K010-PERIOD-DEBAR                      
           MOVE WS-CURR-DATE          TO K010-EFF-DATE                          
           MOVE WS-NRIC(WS-COUNT1)       TO K010-NUM-UINFIN.                    
           MOVE WS-NAME(WS-COUNT1)       TO K010-NAME.                          
           MOVE K665-NUM-LAST-DEBARMENT  TO K010-DEB-REF-NO.                    
           MOVE '1'                      TO K010-PROP-TYPE.                     
           MOVE ZEROS                    TO K010-AMT-OWING.                     
           MOVE WS-EX-HDB-REF-ACC        TO K010-EX-SCH-ACCTNO.                 
           MOVE WS-EX-HDB-REF-LESSEE     TO K010-LSE-TEN-CODE.                  
                                                                                
           MOVE WS-CURR-DATE             TO K010-INCLUSION-DATE                 
                                            K010-LUPDATE.                       
                                                                                
           WRITE P04K010-REC.                                                   
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN 00                                                              
                ADD 1 TO WS-K010-WRITE                                          
           WHEN 22                                                              
           WHEN 02                                                              
                DISPLAY 'DUPLICATE RECORD BP04K010: ' K010-PRIME-KEY            
           WHEN OTHER                                                           
                MOVE WS-K010-STATUS       TO RETURN-CODE                        
                DISPLAY 'ERROR IN WRITING BP04K010: ' K010-PRIME-KEY            
                DISPLAY F740-NUM-REGN                                           
                DISPLAY WS-K010-STATUS                                          
                PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       7000-PRINT-L005.                                                         
      *------------------------------------------------------------*            
                                                                                
           MOVE K665-NUM-LAST-DEBARMENT  TO L005-DEB-REF-NO.                    
           MOVE K010-REASON-CODE         TO L005-DEB-CODE.                      
           MOVE WS-NRIC(WS-COUNT1)       TO L005-NUM-NRIC.                      
           MOVE WS-NAME(WS-COUNT1)       TO L005-NME-OCCP.                      
           MOVE F740-NUM-REF             TO L005-NUM-REF.                       
           MOVE K010-PERIOD-DEBAR        TO L005-PERIOD.                        
           MOVE F740-CDE-ALLOC-SCH       TO L005-ALLO.                          
           MOVE F740-NUM-PORT-ELIG-TAG   TO L005-PORT.                          
                                                                                
           STRING K010-EFF-DATE(7:2) '-'                                        
                  K010-EFF-DATE(5:2) '-'                                        
                  K010-EFF-DATE(1:4) DELIMITED BY SIZE                          
                  INTO L005-EFF-DATE.                                           
                                                                                
           IF WS-LINE-CTR > 55                                                  
              ADD  1              TO   WS-PAGE-CTR                              
              MOVE 5              TO   WS-LINE-CTR                              
              MOVE WS-PAGE-CTR    TO   L005-PAGENO                              
              WRITE BP13L005-REC  FROM L005-PR-HEAD-01 AFTER PAGE               
              WRITE BP13L005-REC  FROM L005-PR-HEAD-02                          
              WRITE BP13L005-REC  FROM L005-PR-LINE    AFTER 2                  
              WRITE BP13L005-REC  FROM L005-PR-HEAD-04                          
              WRITE BP13L005-REC  FROM L005-PR-LINE                             
           END-IF.                                                              
                                                                                
           IF F740-NUM-REGN  NOT = WS-PREV-REGN                                 
              ADD  1                        TO WS-SEQ-NO                        
              MOVE WS-SEQ-NO                TO L005-SEQ-NO                      
              MOVE F740-NUM-REGN            TO L005-NUM-REGN                    
           ELSE                                                                 
              MOVE ZEROES                   TO L005-SEQ-NO                      
              MOVE SPACES                   TO L005-NUM-REGN                    
           END-IF                                                               
                                                                                
           WRITE BP13L005-REC  FROM L005-PR-DTL-01.                             
           ADD   1             TO   WS-LINE-CTR .                               
                                                                                
           MOVE   F740-NUM-REGN             TO WS-PREV-REGN.                    
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       9999-CLOSE-FILES.                                                        
      *------------------------------------------------------------*            
                                                                                
           CLOSE BP13F740                                                       
                 BP13K665                                                       
                 BP13K825                                                       
                 BP04K010                                                       
                 BP13L005.                                                      
                                                                                
           IF WS-K665-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K665, STATUS ' WS-K665-STATUS          
              MOVE WS-K665-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K825-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K825, STATUS ' WS-K825-STATUS          
              MOVE WS-K825-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP04K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS        TO RETURN-CODE.                        
                                                                                
           DISPLAY '*------------------------------------------------'.         
           DISPLAY 'DATE : ' L005-DATE  '   ' 'BP13C005'                        
           DISPLAY '*------------------------------------------------'.         
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF REC READ        BP13F740   = '                        
                    WS-READ-CNT-F740.                                           
           DISPLAY 'NO OF REC READ        BP13K665   = '                        
                    WS-K665-READ                                                
           DISPLAY 'NO OF REC WRITTEN     BP13K665   = '                        
                    WS-K665-WRITE                                               
           DISPLAY 'NO OF REC WRITTEN     BP04K010   = '                        
                    WS-K010-WRITE                                               
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
