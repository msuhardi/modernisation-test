       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CI02.                                                 
       AUTHOR.        RJB1.                                                     
      *DATE-WRITTEN.  14/05/2015.                                               
      * ===============================================================*        
      * BP13 SYSTEM OF COMMITMENT                                      *        
      * ===============================================================*        
      * OBJECTIVE: TO CREATE A REPORT TO KEEP TRACK OF NUMBER OF CASES *        
      *            WHERE APPLICANTS CLICK OPEN THE E-APPOINTMENT LETTER*        
      *            IN MYHDBPAGE SINCE IMPLEMENTATION AND PRINT A DAILY *        
      *            REPORT                                              *        
      *                                                                *        
      *   INPUT FILES :  1. BP13KL00                                   *        
      *                                                                *        
      *   OUTPUT      :  1. BP13LI02                                   *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ------------------------------------*        
      *   BP135791  RJB1  14/05/15 NEW PROGRAM                         *        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KL00  ASSIGN        TO BP13KL00.                          
           SELECT BP13LI02  ASSIGN        TO BP13LI02.                          
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13LI02                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13LI02-REC        PIC X(132).                                      
                                                                                
       FD  BP13KL00                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13KL00.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY             PIC 9(04).                               
           05  WS-CUR-MM               PIC 9(02).                               
           05  WS-CUR-DD               PIC 9(02).                               
                                                                                
       01  WS-COUNT.                                                            
           05  WS-KL00-INPUT           PIC 9(08) VALUE 0.                       
           05  WS-LI02-OUT             PIC 9(08) VALUE 0.                       
           05  WS-SENT                 PIC 9(08) VALUE 0.                       
           05  WS-VIEWED               PIC 9(08) VALUE 0.                       
           05  WS-WRITTEN              PIC 9(08) VALUE 0.                       
           05  WS-LINE-CNT             PIC 9(02) VALUE 0.                       
                                                                                
       01  WS-TIME                     PIC 9(08) VALUE 0.                       
       01  WS-KL00-EOF                 PIC X(01) VALUE SPACES.                  
       01  WS-DATE-SEND                PIC X(08) VALUE SPACES.                  
                                                                                
       01  WS-PRINT-VARIABLES.                                                  
           05 LI02-HDG1.                                                        
              10 FILLER                   PIC X(50)   VALUE SPACES.             
              10 FILLER                   PIC X(22)   VALUE                     
                'SYSTEM  OF  COMMITMENT'.                                       
              10 FILLER                   PIC X(55)   VALUE SPACES.             
                                                                                
           05 LI02-HDG2.                                                        
              10 FILLER                   PIC X(20)   VALUE 'BP13LI02'.         
              10 FILLER                   PIC X(43)   VALUE                     
                'NUMBER OF CASES WHERE APPLICANTS CLICK OPEN'.                  
              10 FILLER                   PIC X(38)   VALUE                     
                ' THE E-APPOINTMENT LETTER IN MYHDBPAGE'.                       
              10 FILLER                   PIC X(12)   VALUE SPACES.             
              10 FILLER                   PIC X(07)   VALUE 'DATE : '.          
              10 LI02-SYS-DATE            PIC X(10).                            
                                                                                
           05 LI02-HDG3.                                                        
              10 FILLER                   PIC X(27)   VALUE                     
                'DATE E-APPPOINTMENT LETTERS'.                                  
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(17)   VALUE                     
                'NO. OF CASES SENT'.                                            
              10 FILLER                   PIC X(07)   VALUE SPACES.             
              10 FILLER                   PIC X(24)   VALUE                     
                 'NO. OF APPLICANTS VIEWED'.                                    
                                                                                
           05 LI02-HDG3A.                                                       
              10 FILLER                   PIC X(07)   VALUE SPACES.             
              10 FILLER                   PIC X(04)   VALUE 'SENT'.             
              10 FILLER                   PIC X(47)   VALUE SPACES.             
              10 FILLER                   PIC X(21)   VALUE                     
                 'E-APPOINTMENT LETTERS'.                                       
                                                                                
           05 LI02-HDG4.                                                        
              10 FILLER                   PIC X(27)   VALUE                     
                '==========================='.                                  
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(17)   VALUE                     
                '================='.                                            
              10 FILLER                   PIC X(07)   VALUE SPACES.             
              10 FILLER                   PIC X(24)   VALUE                     
                '========================'.                                     
                                                                                
           05 LI02-DTL1.                                                        
              10 FILLER                   PIC X(07)   VALUE SPACES.             
              10 LI02-DTE                 PIC X(08)   VALUE SPACES.             
              10 FILLER                   PIC X(22)   VALUE SPACES.             
              10 LI02-CNT-SENT            PIC ZZZZZ9  VALUE ZEROES.             
              10 FILLER                   PIC X(22)   VALUE SPACES.             
              10 LI02-CNT-READ            PIC ZZZZZ9  VALUE ZEROES.             
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-DATA    THRU 3000-EXIT                          
           UNTIL WS-KL00-EOF = 'Y'.                                             
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  BP13KL00                                                 
                OUTPUT BP13LI02.                                                
                                                                                
           INITIALIZE  WS-COUNT.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-TIME.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-READ-INPUT.                                                         
      *=============================================================*           
                                                                                
            READ BP13KL00                                                       
                 AT END MOVE 'Y'  TO WS-KL00-EOF                                
            END-READ.                                                           
                                                                                
            IF WS-KL00-EOF NOT = 'Y'                                            
               ADD 1 TO WS-KL00-INPUT                                           
            END-IF.                                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-PROCESS-DATA.                                                       
      *=============================================================*           
                                                                                
           IF WS-KL00-INPUT = 1                                                 
              ADD 1 TO WS-SENT                                                  
              MOVE KL00-DTE-SEND-EMAIL TO WS-DATE-SEND                          
              IF KL00-DTE-READ-EMAIL-MYHDBPAGE NOT = SPACES AND                 
                                               LOW-VALUES                       
                 ADD 1 TO WS-VIEWED                                             
              END-IF                                                            
           ELSE                                                                 
              IF KL00-DTE-SEND-EMAIL = WS-DATE-SEND                             
                 ADD 1 TO WS-SENT                                               
                 IF KL00-DTE-READ-EMAIL-MYHDBPAGE NOT = SPACES AND              
                                                  LOW-VALUES                    
                    ADD 1 TO WS-VIEWED                                          
                 END-IF                                                         
              ELSE                                                              
                 PERFORM 4000-CREATE-REPORT                                     
                    THRU 4000-EXIT                                              
                 INITIALIZE WS-SENT WS-VIEWED                                   
                 ADD 1 TO WS-SENT                                               
                 MOVE KL00-DTE-SEND-EMAIL TO WS-DATE-SEND                       
                 IF KL00-DTE-READ-EMAIL-MYHDBPAGE NOT = SPACES AND              
                                                  LOW-VALUES                    
                    ADD 1 TO WS-VIEWED                                          
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-INPUT                                              
              THRU 2000-EXIT.                                                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       4000-CREATE-REPORT.                                                      
      *================================================================*        
                                                                                
           ADD 1                    TO WS-LI02-OUT.                             
                                                                                
           IF WS-LI02-OUT = 1 OR (WS-LINE-CNT >= 55)                            
              MOVE 0                        TO WS-LINE-CNT                      
              STRING WS-CUR-DATE(7:2) '/'                                       
                     WS-CUR-DATE(5:2) '/'                                       
                     WS-CUR-DATE(1:4)                                           
                     DELIMITED BY SIZE                                          
                     INTO LI02-SYS-DATE                                         
              END-STRING                                                        
              WRITE BP13LI02-REC FROM LI02-HDG1 AFTER PAGE                      
              WRITE BP13LI02-REC FROM LI02-HDG2                                 
              MOVE SPACES TO BP13LI02-REC                                       
              WRITE BP13LI02-REC                                                
              WRITE BP13LI02-REC FROM LI02-HDG3                                 
              WRITE BP13LI02-REC FROM LI02-HDG3A                                
              WRITE BP13LI02-REC FROM LI02-HDG4                                 
              MOVE SPACES TO BP13LI02-REC                                       
              WRITE BP13LI02-REC                                                
              ADD 7                         TO WS-LINE-CNT                      
           END-IF.                                                              
                                                                                
           MOVE WS-DATE-SEND             TO LI02-DTE.                           
           MOVE WS-SENT                  TO LI02-CNT-SENT.                      
           MOVE WS-VIEWED                TO LI02-CNT-READ.                      
           WRITE BP13LI02-REC          FROM LI02-DTL1.                          
           ADD 1                         TO WS-WRITTEN                          
                                            WS-LINE-CNT.                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
           DISPLAY 'PROGRAM: BP13CI02'.                                         
           DISPLAY 'TOTAL INPUT RECORDS        : ' WS-KL00-INPUT.               
           DISPLAY 'TOTAL RECORD WRITTEN       : ' WS-WRITTEN.                  
           DISPLAY '        '                                                   
                                                                                
           CLOSE BP13KL00                                                       
                 BP13LI02.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
