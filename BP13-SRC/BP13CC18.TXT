       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC18.                                                 
       AUTHOR. BALA AMUTHAN.                                                    
       DATE-WRITTEN.  10/05/06.                                                 
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  THIS PROGRAMME WILL VALIDATE                *                
      *            CASH PAYMENT AC28F544 TO UPDATE STAMP FEE   *                
      *            AND CONVEY FEE TO BP13KD40.                 *                
      *            IF THE RECORD DOES NOT MATCH, THE           *                
      *            OUTPUT RECORD WILL MOVE TO THE KIV          *                
      *            FILE (ERROR FILE).                          *                
      *        2.  WRITE BP13F125 FOR ENQUIRY PANEL.           *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *           1. AC28F544 (WEEKLY CPF TRANSACTIONS)        *                
      *           2. BP13F800 (RFF)                            *                
      *           3. BP13KD40                                  *                
      *                                                        *                
      *     OUTPUT FILE:                                       *                
      *           1. BP13K140 (KIV FILE)                       *                
      *           2. BP13F125 (CPF TRANSACTIONS FILE)          *                
      *                                                        *                
      *     LISTINGS:                                          *                
      *           1. BP13LC18 (CONTROL LIST)                   *                
      *                                                        *                
      * BP132857 BA2 100506 - SAME PGM AS BP13C101             *                
      * BP132888 BA3 030107 - INCLUDE REMARKS                  *                
      * BP132888 BA3 300107 - DO NOT WRITE KIV FOR NON-DBSS    *                
      * BP133446 JB8 141108 - TO OUTPUT CORRECT SCHDL          *                
      * BP133682 JB8 280909 - TO MODIFY COMPUTATION            *                
      **********************************************************                
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AC28F544 ASSIGN TO AC28F544.                                  
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT BP13K140 ASSIGN TO BP13K140                                   
                ACCESS MODE IS RANDOM                                           
                ORGANIZATION IS INDEXED                                         
                RECORD KEY IS K140-KEY-FLD                                      
                FILE STATUS IS K140-STATUS.                                     
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                ACCESS MODE IS RANDOM                                           
                ORGANIZATION IS INDEXED                                         
                RECORD KEY IS KD00-KEY-FLD                                      
                FILE STATUS IS KD00-STATUS.                                     
                                                                                
           SELECT BP13KD40 ASSIGN TO BP13KD40                                   
                  ACCESS MODE IS DYNAMIC                                        
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY  IS KD40-KEY-FLD                                   
                  FILE STATUS IS KD40-STATUS.                                   
                                                                                
           SELECT BP13F125 ASSIGN TO BP13F125.                                  
                                                                                
           SELECT BP13LC18 ASSIGN TO BP13LC18.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  AC28F544                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 130 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY AC28F544.                                                           
                                                                                
       FD  BP13F800                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F800.                                                           
                                                                                
       FD  BP13F125                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       COPY BP13F125.                                                           
                                                                                
       FD  BP13K140                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13K140.                                                           
                                                                                
       FD  BP13KD40                                                             
           RECORD CONTAINS 1500 CHARACTERS.                                     
       COPY BP13KD40.                                                           
                                                                                
       FD  BP13KD00                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13LC18                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-CONTROL-LIST             PIC X(132).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  F800-STATUS                    PIC 99      VALUE ZEROES.             
       01  K140-STATUS                    PIC 99      VALUE ZEROES.             
       01  KD00-STATUS                    PIC 99      VALUE ZEROES.             
       01  KD40-STATUS                    PIC 99      VALUE ZEROES.             
                                                                                
       01  WS-K140-STATUS.                                                      
           05  WS-K140-STATUS1            PIC 9.                                
           05  WS-K140-STATUS2            PIC 9.                                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F544-EOF-FLAG           PIC X       VALUE 'N'.                
           05  WS-F800-EOF-FLAG           PIC X       VALUE 'N'.                
                                                                                
       01  WS-KD00-FND                    PIC X(1)    VALUE 'N'.                
           88  KD00-FND                               VALUE 'Y'.                
       01  WS-KD40-FND                    PIC X(1)    VALUE 'N'.                
       01  WS-KD40-EOF                    PIC X(1)    VALUE 'N'.                
       01  WS-CNT-AC28F544-READ           PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-BP13F800-READ           PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-K140-KIV                PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-F125-VALID              PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-KD00-READ               PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-KD40-READ               PIC 9(5)    VALUE ZEROES.             
       01  WS-K140-COUNT-HISTORY          PIC 9(2)    VALUE ZEROES.             
       01  WS-F125-COUNT-HISTORY          PIC 9(3)    VALUE ZEROES.             
       01  WS-CDE-APPLT                   PIC X       VALUE SPACES.             
       01  WS-NUM-NRIC                    PIC X(9)    VALUE SPACES.             
       01  WS-LINE-CNT                    PIC 9(5)    VALUE 00054.              
       01  WS-PAGENO                      PIC 9(5)    VALUE ZEROES.             
       01  WS-SNO                         PIC 9(5)    VALUE ZEROES.             
       01  WS-AMT-TOT                     PIC 9(7)V99 VALUE ZEROES.             
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05 WS-CCYY                     PIC 9999.                             
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
                                                                                
      ***************************************************************           
      *    WS  OR CONTROL-LIST                                      *           
      ***************************************************************           
                                                                                
       01  WS-CONTROL-HEAD.                                                     
           05  FILLER                  PIC X(8)  VALUE 'BP13LC18'.              
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'HDBCAT3'.               
           05  FILLER                  PIC X(25) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
                                       'S Y S T E M   O F   '.                  
           05  FILLER                  PIC X(19) VALUE                          
                                       'C O M M I T M E N T'.                   
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'DATE :'.                
           05  CL-DATE-HD              PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'PAGE : '.               
           05  CL-PAGENO               PIC ZZZZ9 .                              
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
                                                                                
       01  WS-CONTROL-LIST-1.                                                   
           05  FILLER              PIC X(43)  VALUE SPACES.                     
           05  FILLER              PIC X(42)  VALUE                             
               'ERROR REPORT ON STAMP FEE/CONVEYANCING FEE'.                    
           05  FILLER              PIC X(39)  VALUE SPACES.                     
                                                                                
       01  WS-CONTROL-LIST-2.                                                   
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  FILLER              PIC X(05)  VALUE ' S/NO'.                    
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  FILLER              PIC X(06)  VALUE 'REG NO'.                   
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'SCH-ACC'.                  
           05  FILLER              PIC X(06)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'PAYMENT'.                  
           05  FILLER              PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(05)  VALUE 'JR-NO'.                    
           05  FILLER              PIC X(09)  VALUE SPACES.                     
           05  FILLER              PIC X(11)  VALUE  'AMT-RECEIPT'.             
           05  FILLER              PIC X(13)  VALUE  SPACES.                    
           05  FILLER              PIC X(07)  VALUE  'REMARKS'.                 
           05  FILLER              PIC X(28)  VALUE  SPACES.                    
                                                                                
       01  WS-CONTROL-LIST-3.                                                   
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  CL-SNO              PIC Z(4)9  VALUE ZEROES.                     
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  CL-RENO             PIC X(08)  VALUE SPACES.                     
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  CL-SCH-ACC          PIC X(11)  VALUE SPACES.                     
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  CL-PAY-TY           PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE SPACES.                     
           05  CL-JR-NO            PIC X(13)  VALUE SPACES.                     
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  CL-AMT-RECPT        PIC ZZZZZZ9.99   VALUE ZEROES.               
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  CL-REMARKS          PIC X(25)  VALUE SPACES.                     
                                                                                
       01  WS-CONTROL-LIST-4.                                                   
           05  FILLER              PIC X(132) VALUE SPACES.                     
                                                                                
       01  WS-CONTROL-LIST-5.                                                   
           05  FILLER              PIC X(4)   VALUE SPACES.                     
           05  FILLER              PIC X(96)  VALUE ALL '-'.                    
           05  FILLER              PIC X(23)  VALUE SPACES.                     
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
      ******************************************************************        
      *        MAIN LINE PROCESS                                       *        
      ******************************************************************        
       MAIN-PROCESS.                                                            
           PERFORM 000-OPEN-FILE                THRU 000-EXIT.                  
           PERFORM 100-READ-F544                THRU 100-EXIT.                  
           PERFORM 200-READ-F800                THRU 200-EXIT.                  
           PERFORM 300-PROCESS-SEQUENTIAL-FILES THRU 300-EXIT                   
               UNTIL WS-F544-EOF-FLAG = 'Y'                                     
               AND   WS-F800-EOF-FLAG = 'Y'.                                    
           PERFORM 1000-CLOSE-ROUTINE           THRU 1000-EXIT.                 
                                                                                
      ***************************************************************           
      *    OPENING FILES SECTION                                    *           
      ***************************************************************           
                                                                                
       000-OPEN-FILE.                                                           
           OPEN INPUT  AC28F544                                                 
                       BP13F800                                                 
                       BP13KD40                                                 
                       BP13KD00                                                 
                I-O    BP13K140                                                 
                OUTPUT BP13F125                                                 
                       BP13LC18.                                                
                                                                                
           IF K140-STATUS NOT = 0  AND                                          
              K140-STATUS NOT = 97                                              
              DISPLAY ' OPENING ERROR, BP13K140 STATUS ' K140-STATUS            
              MOVE K140-STATUS           TO RETURN-CODE                         
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
           IF KD00-STATUS NOT = 0  AND                                          
              KD00-STATUS NOT = 97                                              
              DISPLAY ' OPENING ERROR, BP13KD00 STATUS ' KD00-STATUS            
              MOVE KD00-STATUS           TO RETURN-CODE                         
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
           IF KD40-STATUS NOT = 0  AND                                          
              KD40-STATUS NOT = 97                                              
              DISPLAY ' OPENING ERROR, BP13KD40 STATUS ' KD40-STATUS            
              MOVE KD40-STATUS           TO RETURN-CODE                         
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURRENT-DATE.                  
           STRING WS-DD '/' WS-MM '/' WS-CCYY                                   
                  DELIMITED BY SIZE INTO CL-DATE-HD.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ************************************************************              
      *    READ SEQUENTIAL FILE AC28F544                         *              
      ************************************************************              
                                                                                
       100-READ-F544.                                                           
                                                                                
           READ AC28F544                                                        
                AT END MOVE HIGH-VALUE  TO F544-NUM-HDBREF                      
                MOVE 'Y'        TO WS-F544-EOF-FLAG                             
                GO TO 100-EXIT.                                                 
           ADD  1                      TO WS-CNT-AC28F544-READ.                 
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
      *    READ SEQUENTIAL FILE BP13F800                         *              
      ************************************************************              
       200-READ-F800.                                                           
           READ BP13F800                                                        
                AT END MOVE HIGH-VALUE  TO F800-NUM-SCH-ACC                     
                MOVE 'Y'         TO WS-F800-EOF-FLAG                            
                GO TO 200-EXIT.                                                 
           ADD  1                       TO WS-CNT-BP13F800-READ.                
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-SEQUENTIAL-FILES.                                            
                                                                                
           MOVE SPACES TO WS-CDE-APPLT  WS-NUM-NRIC.                            
                                                                                
           IF F544-NUM-HDBREF = F800-NUM-SCH-ACC                                
               PERFORM 400-PROCESS-CASH THRU 400-EXIT                           
               PERFORM 100-READ-F544 THRU 100-EXIT                              
           ELSE                                                                 
               IF F544-NUM-HDBREF > F800-NUM-SCH-ACC                            
                  PERFORM 200-READ-F800 THRU 200-EXIT                           
               ELSE                                                             
                  DISPLAY 'NOTFD SOC MASTER ' F544-NUM-HDBREF                   
                          ' -' AC28F544-REC                                     
                  PERFORM 100-READ-F544 THRU 100-EXIT                           
               END-IF                                                           
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-PROCESS-CASH.                                                        
                                                                                
           PERFORM 440-READ-BP13KD00           THRU 440-EXIT.                   
           IF KD00-FND                                                          
              CONTINUE                                                          
           ELSE                                                                 
              GO TO 400-EXIT                                                    
           END-IF.                                                              
                                                                                
           IF F800-NUM-ALLO-CAT = 'DBS'                                         
              PERFORM 420-READ-KD40            THRU 420-EXIT                    
              IF WS-KD40-FND = 'Y'                                              
                 PERFORM 430-OUTPUT-FILE       THRU 430-EXIT                    
              ELSE                                                              
                 MOVE SPACES TO BP13K140-KIVTRAN                                
                 INITIALIZE     BP13K140-KIVTRAN                                
                 MOVE  F800-NUM-REGN           TO CL-RENO                       
                 MOVE 'NOT FOUND IN PP ORDER'  TO CL-REMARKS                    
                 PERFORM 500-WRITE-KIV-REC     THRU 500-EXIT                    
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'NOT DBSS FLAT=' F800-NUM-REGN                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    SEARCH F800-NUM-REGN  IN BP13KD40                          *         
      *****************************************************************         
                                                                                
       420-READ-KD40.                                                           
                                                                                
           MOVE   SPACES             TO     BP13KD40-REC.                       
           INITIALIZE                       BP13KD40-REC.                       
           MOVE  LOW-VALUES          TO     KD40-KEY-FLD.                       
           MOVE  F800-NUM-REGN       TO     KD40-NUM-REGN.                      
           MOVE 'N'                  TO     WS-KD40-FND WS-KD40-EOF             
           START BP13KD40 KEY >= KD40-KEY-FLD.                                  
                                                                                
           IF KD40-STATUS = 00 OR 02                                            
              PERFORM 425-CHECK-TALLY        THRU 425-EXIT                      
                UNTIL WS-KD40-FND = 'Y'                                         
                   OR WS-KD40-EOF = 'Y'                                         
           ELSE                                                                 
             IF KD40-STATUS = 23 OR 10                                          
                MOVE 'N' TO WS-KD40-FND                                         
             ELSE                                                               
                DISPLAY 'ERROR WITH START READ OF BP13KD40. STATUS IS '         
                       KD40-STATUS                                              
                PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
       425-CHECK-TALLY.                                                         
                                                                                
           READ BP13KD40 NEXT RECORD                                            
                                                                                
           IF KD40-STATUS = 00 OR 02                                            
              IF F800-NUM-REGN = KD40-NUM-REGN                                  
                 EVALUATE F544-NUM-RCPTCD                                       
                     WHEN '6250G'                                               
                          COMPUTE WS-AMT-TOT =                                  
                                     KD40-AMT-MORT-STAMP-CASH +                 
                                     KD40-AMT-CAVEAT-CASH   +                   
                                     KD40-AMT-SNP-STAMP-CASH +                  
                                     KD40-AMT-LSE-ESCR-STAMP-CASH +             
                                     KD40-AMT-MORT-ESCR-STAMP-CASH +            
                                     KD40-AMT-SOLICITOR-BILL-CASH +             
                                     KD40-AMT-SURVEY-CASH                       
                          IF KD40-NUM-PP-SCHDL = 'TR'                           
                             COMPUTE WS-AMT-TOT = WS-AMT-TOT +                  
                                        KD40-AMT-TRF-WCAVEAT-CASH +             
                                        KD40-AMT-TRF-DOA-STAMP-CASH             
                          END-IF                                                
                          IF F544-AMT-TXN = WS-AMT-TOT                          
                             ADD 1       TO WS-CNT-KD40-READ                    
                             MOVE 'Y'    TO WS-KD40-FND                         
                          END-IF                                                
                     WHEN '0933D'                                               
                          COMPUTE WS-AMT-TOT = KD40-AMT-CONVY-CASH              
                          IF KD40-NUM-PP-SCHDL = 'TR'                           
                             COMPUTE WS-AMT-TOT =                               
                                        WS-AMT-TOT +                            
                                        KD40-AMT-TRF-CONVY-TROR-CASH +          
                                        KD40-AMT-TRF-CONVY-TREE-CASH +          
                                        KD40-AMT-TRF-CONVY-TDM-CASH  +          
                                        KD40-AMT-TRF-CONVY-MORT-CASH            
                          END-IF                                                
                          IF F544-AMT-TXN = WS-AMT-TOT                          
                             ADD 1       TO WS-CNT-KD40-READ                    
                             MOVE 'Y'    TO WS-KD40-FND                         
                          END-IF                                                
                 END-EVALUATE                                                   
              ELSE                                                              
                 MOVE 'Y' TO WS-KD40-EOF                                        
              END-IF                                                            
           ELSE                                                                 
             IF KD40-STATUS = 23 OR 10                                          
                MOVE 'Y' TO WS-KD40-EOF                                         
             ELSE                                                               
                DISPLAY 'ERROR WITH READ FILE-BP13KD40. STATUS IS '             
                     KD40-STATUS                                                
                PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
       425-EXIT.                                                                
           EXIT.                                                                
                                                                                
       430-OUTPUT-FILE.                                                         
                                                                                
           MOVE SPACES                TO BP13F125-CASHTRAN.                     
           INITIALIZE                    BP13F125-CASHTRAN.                     
           MOVE F800-NUM-REGN         TO F125-NUM-REGN.                         
           MOVE F800-NUM-NRIC1        TO F125-NUM-NRIC.                         
           MOVE WS-CURRENT-DATE       TO F125-DTE-POST.                         
           MOVE ZEROS                 TO F125-COUNT-HISTORY.                    
           MOVE F544-AMT-TXN          TO F125-AMT-RECEIPT.                      
           MOVE F544-DTE-TXN          TO F125-DTE-TRANS.                        
           MOVE F544-NUM-HDBREF       TO F125-NUM-SCH-ACCT.                     
           MOVE F544-NUM-RCPT         TO F125-NUM-RECPT-JRNO.                   
           MOVE F544-CDE-BO           TO F125-CDE-BO.                           
           MOVE '11'                  TO F125-CDE-TRANS-TYPE.                   
                                                                                
           IF F544-NUM-RCPTCD = '6250G'                                         
              MOVE 'DSP'              TO F125-CDE-PAYMENT-TYPE                  
           ELSE                                                                 
            IF F544-NUM-RCPTCD = '0933D'                                        
               MOVE 'DCV'             TO F125-CDE-PAYMENT-TYPE                  
           END-IF.                                                              
                                                                                
           MOVE KD40-NUM-PP-SCHDL     TO F125-NUM-PP-SCHEDULE.                  
           MOVE KD40-DTE-PP-DUE       TO F125-DTE-PP-DUE.                       
                                                                                
           WRITE BP13F125-CASHTRAN.                                             
           ADD 1 TO WS-CNT-F125-VALID.                                          
                                                                                
       430-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *                                                                         
       440-READ-BP13KD00.                                                       
                                                                                
           MOVE 'N'                   TO WS-KD00-FND                            
                                                                                
           MOVE F800-NUM-REGN         TO KD00-NUM-REGN                          
                                                                                
           READ BP13KD00                                                        
                                                                                
           IF KD00-STATUS = 00 OR 02                                            
              MOVE 'Y'                TO WS-KD00-FND                            
           ELSE                                                                 
              IF KD00-STATUS = 23 OR 10                                         
                 MOVE 'N'             TO WS-KD00-FND                            
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13KD00 : ' KD00-STATUS                
                 PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       440-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      * OUTPUT ERROR RECORD TO KIV FILE                                         
                                                                                
       500-WRITE-KIV-REC.                                                       
                                                                                
           IF K140-CDE-ERROR NOT = 'A'                                          
              MOVE F800-NUM-REGN      TO K140-NUM-REGN                          
              MOVE F800-NUM-NRIC1     TO K140-NUM-NRIC                          
           END-IF.                                                              
           MOVE '11'                  TO K140-CDE-TRANS-TYPE.                   
           MOVE WS-CURRENT-DATE       TO K140-DTE-POST.                         
           ADD  1                     TO WS-K140-COUNT-HISTORY.                 
           MOVE 'N'                   TO K140-COUNT-HIST-CHAR.                  
           MOVE WS-K140-COUNT-HISTORY TO K140-COUNT-HIST-NUM.                   
           MOVE F544-AMT-TXN          TO K140-AMT-RECEIPT.                      
           MOVE F544-DTE-TXN          TO K140-DTE-TRANS.                        
           MOVE F544-NUM-HDBREF       TO K140-NUM-SCH-ACCT.                     
           MOVE F544-NUM-RCPT         TO K140-NUM-RECPT-JRNO.                   
           MOVE  'A'                  TO K140-CDE-ERROR.                        
                                                                                
           IF F544-NUM-RCPTCD = '6250G'                                         
              MOVE 'DSP'              TO K140-CDE-PAYMENT-TYPE                  
           ELSE                                                                 
            IF F544-NUM-RCPTCD = '0933D'                                        
               MOVE 'DCV'             TO K140-CDE-PAYMENT-TYPE                  
           END-IF.                                                              
                                                                                
           WRITE BP13K140-KIVTRAN.                                              
                                                                                
           MOVE K140-STATUS TO  WS-K140-STATUS.                                 
                                                                                
           IF WS-K140-STATUS1 = 0                                               
              ADD 1 TO WS-CNT-K140-KIV                                          
           ELSE                                                                 
              IF K140-STATUS = 22                                               
                 DISPLAY ' ERROR WRITING DUP REC : ' K140-KEY-FLD               
              ELSE                                                              
                 DISPLAY ' OPENING ERROR, BP13K140 STATUS '                     
                         K140-STATUS                                            
                 PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 600-MOVE-VALUE-REPORT    THRU 600-EXIT.                      
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    PRINT CONTROL-LIST                                         *         
      *****************************************************************         
                                                                                
       600-MOVE-VALUE-REPORT.                                                   
                                                                                
           IF WS-LINE-CNT > 53                                                  
              MOVE ZEROES              TO   WS-LINE-CNT                         
              PERFORM 610-PRINT-HEAD   THRU 610-EXIT                            
           END-IF.                                                              
                                                                                
           MOVE  F544-NUM-HDBREF       TO   CL-SCH-ACC.                         
           MOVE  K140-CDE-PAYMENT-TYPE TO   CL-PAY-TY.                          
           MOVE  F544-NUM-RCPT         TO   CL-JR-NO.                           
           MOVE  F544-AMT-TXN          TO   CL-AMT-RECPT.                       
           ADD +1                      TO   WS-SNO.                             
           MOVE WS-SNO                 TO   CL-SNO.                             
           PERFORM 620-PRINT-REC       THRU 620-EXIT.                           
                                                                                
        600-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       610-PRINT-HEAD.                                                          
           ADD +1            TO WS-PAGENO.                                      
           MOVE WS-PAGENO    TO CL-PAGENO.                                      
           MOVE SPACES       TO PRINT-CONTROL-LIST.                             
           WRITE PRINT-CONTROL-LIST AFTER PAGE.                                 
           WRITE PRINT-CONTROL-LIST.                                            
           WRITE PRINT-CONTROL-LIST.                                            
           WRITE PRINT-CONTROL-LIST.                                            
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-HEAD.                       
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-1.                     
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-4.                     
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-2.                     
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-5.                     
           ADD +8  TO WS-LINE-CNT.                                              
                                                                                
       610-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
       620-PRINT-REC.                                                           
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-3.                     
           ADD +1  TO WS-LINE-CNT.                                              
                                                                                
       620-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
       1000-CLOSE-ROUTINE.                                                      
                                                                                
           DISPLAY '******  BP13CC18 *************'.                            
           DISPLAY 'NO OF F544 READ      (F544) : '                             
                                             WS-CNT-AC28F544-READ.              
           DISPLAY 'NO OF RFF RECS READ  (F800) : '                             
                                             WS-CNT-BP13F800-READ.              
           DISPLAY 'NO OF KD40 REC READ  (KD40) : ' WS-CNT-KD40-READ.           
           DISPLAY 'NO OF F125 REC WRITE (F125) : ' WS-CNT-F125-VALID.          
           DISPLAY 'NO OF K140 REC WRITE (K140) : ' WS-CNT-K140-KIV.            
                                                                                
           CLOSE BP13F800                                                       
                 AC28F544                                                       
                 BP13F125                                                       
                 BP13K140                                                       
                 BP13KD40                                                       
                 BP13KD00                                                       
                 BP13LC18.                                                      
                                                                                
           IF K140-STATUS NOT = 0 AND K140-STATUS NOT = 97                      
               DISPLAY ' CLOSING ERROR, BP13K140 STATUS ' K140-STATUS           
               MOVE K140-STATUS TO RETURN-CODE.                                 
                                                                                
           IF KD00-STATUS NOT = 0 AND KD00-STATUS NOT = 97                      
               DISPLAY ' CLOSING ERROR, BP13KD00 STATUS ' KD00-STATUS           
               MOVE KD00-STATUS TO RETURN-CODE.                                 
                                                                                
           IF KD40-STATUS NOT = 0 AND KD40-STATUS NOT = 97                      
               DISPLAY ' CLOSING ERROR, BP13KD40 STATUS ' KD40-STATUS           
               MOVE KD40-STATUS TO RETURN-CODE.                                 
                                                                                
       1000-EXIT.                                                               
           STOP RUN.                                                            
