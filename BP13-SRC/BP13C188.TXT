       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C188.                                                 
      *AUTHOR.        SELVY.                                                    
      *DATE-WRITTEN.  11/02/99.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE : TO UPDATE HOUSEHOLD AS 'H' FIRST TIMER FOR     *            
      *             -DIVORCED IN RFS FILES                         *            
      *             -EX-SPOUSE IN RESALE HISTORY                   *            
      *                                                            *            
      *    INPUT  : BP13F500                                       *            
      *             BP13F558                                       *            
      *    OUTPUT : BP13F520                                       *            
      *             P13F558A                                       *            
      *             BP13L188                                       *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE      AUTHOR  REMARKS                         *            
      * -------- --------  ------  -------                         *            
      * BP130589 19990211   SRA    NEW PROGRAM                     *            
      * BP130652 19990917   LSB    EXPAND F500,F520 RECL FROM 300  *            
      *                            TO 500 CHARACTERS               *            
      * BP130897 20000801   AKT    EXPAND F558 RECL FROM 400 TO 600*            
      * BP133320 20080513   MSD1   WRITE CASES INTO NEW F558 DSN   *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
           SELECT P13F558A ASSIGN TO P13F558A.                                  
           SELECT BP13L188 ASSIGN TO BP13L188.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F500     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 500 CHARACTERS                          
                        LABEL RECORD IS STANDARD                                
                        RECORDING MODE IS F.                                    
       COPY BP13F500.                                                           
                                                                                
       FD  BP13F558     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 600 CHARACTERS                          
                        LABEL RECORD IS STANDARD                                
                        RECORDING MODE IS F.                                    
       COPY BP13F558.                                                           
                                                                                
       FD  BP13F520     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 500 CHARACTERS                          
                        LABEL RECORD IS STANDARD                                
                        RECORDING MODE IS F.                                    
       01  BP13F520-REC            PIC X(500).                                  
                                                                                
       FD  P13F558A     BLOCK CONTAINS 0 RECORDS                                
                        RECORD CONTAINS 600 CHARACTERS                          
                        LABEL RECORD IS STANDARD                                
                        RECORDING MODE IS F.                                    
       01  P13F558A-REC            PIC X(600).                                  
                                                                                
       FD  BP13L188     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 133 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01  BP13L188-REC            PIC X(133).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-FLAGS.                                                       
           05  WS-F500-EOF         PIC X         VALUE 'N'.                     
           05  WS-F558-EOF         PIC X         VALUE 'N'.                     
                                                                                
       01  WS-NRIC                 PIC X(9)      VALUE SPACES.                  
       01  WS-MAR-STATUS           PIC X(1)      VALUE SPACES.                  
       01  WS-RSL-HDB-REF          PIC X(11)     VALUE SPACES.                  
       01  WS-RSL-FLE-STATUS       PIC X(03)     VALUE SPACES.                  
       01  WS-RSL-SELLER           PIC X(01)     VALUE SPACES.                  
       01  WS-SMS-HDB-REF          PIC X(11)     VALUE SPACES.                  
       01  WS-SMS-FLE-STATUS       PIC X(03)     VALUE SPACES.                  
       01  WS-DIVORCE-FD           PIC X         VALUE 'N'.                     
       01  WS-DP-RSL               PIC X         VALUE 'N'.                     
       01  WS-DP-SMS               PIC X         VALUE 'N'.                     
       01  WS-DATE                 PIC X(8)      VALUE SPACES.                  
       01  WS-L188-SEQ-NO          PIC 9(4)      VALUE ZEROS.                   
       01  WS-F558-REGN            PIC X(8)      VALUE SPACES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F500-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-F558-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-F520-CNT-WRT     PIC 9(8)      VALUE ZEROS.                   
           05  WS-MATCH-REC        PIC 9(8)      VALUE ZEROS.                   
           05  WS-F500-LESS-F558   PIC 9(8)      VALUE ZEROS.                   
           05  WS-L188-CNT-WRT     PIC 9(8)      VALUE ZEROS.                   
           05  WS-L188-CNT-LINE    PIC 9(8)      VALUE 99.                      
           05  WS-L188-CNT-PAGE    PIC 9(8)      VALUE ZEROS.                   
                                                                                
       01  L188-HD-LINE1.                                                       
           05  FILLER              PIC X(8)     VALUE 'BP13L188'.               
           05  FILLER              PIC X(4)     VALUE SPACES.                   
           05  FILLER              PIC X(8)     VALUE 'HDB 3'.                  
           05  FILLER              PIC X(35)    VALUE SPACES.                   
           05  FILLER              PIC X(20)    VALUE                           
                     'SYSTEM OF COMMITMENT'.                                    
           05  FILLER              PIC X(34)    VALUE SPACES.                   
           05  FILLER              PIC X(7)     VALUE 'DATE :'.                 
           05  L188-DATE           PIC X(10).                                   
           05  FILLER              PIC X(6)    VALUE SPACES.                    
                                                                                
       01  L188-HEADER1.                                                        
           05  FILLER              PIC X(40)    VALUE SPACES.                   
           05  FILLER              PIC X(47)    VALUE                           
               'UPDATING HOUSEHOLD FROM UPGRADER TO FIRST TIMER'.               
           05  FILLER              PIC X(22)    VALUE SPACES.                   
           05  FILLER              PIC X(7)     VALUE 'PAGE :'.                 
           05  L188-PAGE-NO        PIC Z(3)9.                                   
           05  FILLER              PIC X(12)    VALUE SPACES.                   
                                                                                
       01  L188-HEADER2.                                                        
           05  FILLER              PIC X(35)    VALUE SPACES.                   
           05  FILLER              PIC X(56)    VALUE                           
             'FOR DIVORCED IN RFS FILE AND EX-SPOUSE IN RESALE HISTORY'.        
           05  FILLER              PIC X(18)    VALUE SPACES.                   
           05  FILLER              PIC X(7)     VALUE 'USER :'.                 
           05  FILLER              PIC X(16)    VALUE                           
                      'APPLICATION TEAM'.                                       
                                                                                
       01  L188-HEADER3.                                                        
           05  FILLER              PIC X(4)      VALUE 'S/NO'.                  
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'REGN-NO '.              
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(9)      VALUE '   NRIC  '.             
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(9)      VALUE 'HOUSEHOLD'.             
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'MAR-STATUS'.            
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE 'RSL-HDB-REF'.           
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(14)     VALUE 'RSL-FLE-STATUS'.        
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'SELLER-RSL'.            
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE 'SMS-HDB-REF'.           
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(14)     VALUE 'SMS-FLE-STATUS'.        
           05  FILLER              PIC X(5)      VALUE SPACES.                  
                                                                                
       01  L188-HEADER4.                                                        
           05  FILLER              PIC X(4)      VALUE ALL '-'.                 
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE ALL '-'.                 
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(9)      VALUE ALL '-'.                 
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(9)      VALUE ALL '-'.                 
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE ALL '-'.                 
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE ALL '-'.                 
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(14)     VALUE ALL '-'.                 
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE ALL '-'.                 
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE ALL '-'.                 
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(14)     VALUE ALL '-'.                 
           05  FILLER              PIC X(5)      VALUE SPACES.                  
                                                                                
       01  L188-REC.                                                            
           05  L188-SNO            PIC Z(03)9.                                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L188-REGN-NO        PIC X(08).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L188-NRIC           PIC X(09).                                   
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  L188-HOUSEHOLD      PIC X(02).                                   
           05  FILLER              PIC X(11)     VALUE SPACES.                  
           05  L188-MAR-STATUS     PIC X(01).                                   
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  L188-RSL-HDB-REF    PIC X(11).                                   
           05  FILLER              PIC X(09)     VALUE SPACES.                  
           05  L188-RSL-FLE-STATUS PIC X(03).                                   
           05  FILLER              PIC X(13)     VALUE SPACES.                  
           05  L188-SELLER-RSL     PIC X(01).                                   
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  L188-SMS-HDB-REF    PIC X(15).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  L188-SMS-FLE-STATUS PIC X(03).                                   
           05  FILLER              PIC X(14)     VALUE SPACES.                  
                                                                                
       01  L188-HEADER5.                                                        
           05  FILLER              PIC X(48)     VALUE SPACES.                  
           05  FILLER              PIC X(19)     VALUE                          
                                   'NO OF OCCURENCES : '.                       
           05  L188-OCCUR          PIC Z(03)9.                                  
           05  FILLER              PIC X(61)     VALUE SPACES.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 1200-F558-READ       THRU 1200-EXIT.                         
           PERFORM 1500-F500-READ       THRU 1500-EXIT.                         
           PERFORM 2000-MAIN-ROUTINE    THRU 2000-EXIT                          
                   UNTIL WS-F500-EOF = 'Y' AND WS-F558-EOF = 'Y'                
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F558                                                 
                       BP13F500                                                 
               OUTPUT  BP13F520                                                 
                       P13F558A                                                 
                       BP13L188.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO L188-DATE.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       1200-F558-READ.                                                          
      *****************************************************************         
                                                                                
           READ BP13F558 AT END                                                 
                        MOVE 'Y' TO WS-F558-EOF                                 
                        MOVE HIGH-VALUES TO F558-NUM-REGN                       
                        GO TO 1200-EXIT.                                        
                                                                                
           ADD 1 TO WS-F558-CNT-READ.                                           
                                                                                
       1200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       1500-F500-READ.                                                          
      *****************************************************************         
                                                                                
           READ BP13F500 AT END                                                 
                        MOVE 'Y' TO WS-F500-EOF                                 
                        MOVE HIGH-VALUES TO F500-NUM-REGN                       
                        GO TO 1500-EXIT.                                        
                                                                                
           ADD 1 TO WS-F500-CNT-READ.                                           
                                                                                
       1500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       2000-MAIN-ROUTINE.                                                       
      *****************************************************************         
                                                                                
           MOVE 'N' TO WS-DIVORCE-FD.                                           
           MOVE 'N' TO WS-DP-RSL.                                               
           MOVE 'N' TO WS-DP-SMS.                                               
           IF F500-NUM-REGN < F558-NUM-REGN                                     
              WRITE BP13F520-REC FROM BP13F500-REC                              
              ADD 1 TO WS-F520-CNT-WRT                                          
                       WS-F500-LESS-F558                                        
              PERFORM 1500-F500-READ THRU 1500-EXIT                             
           ELSE                                                                 
              IF F500-NUM-REGN > F558-NUM-REGN                                  
                 MOVE F558-NUM-REGN TO WS-F558-REGN                             
                 PERFORM 2005-BYPASS-REC THRU 2005-EXIT                         
                         UNTIL F558-NUM-REGN NOT = WS-F558-REGN                 
                         OR WS-F558-EOF = 'Y'                                   
              ELSE                                                              
                 ADD 1 TO WS-MATCH-REC                                          
                 PERFORM 2100-CHECK-MATCH THRU 2100-EXIT                        
                    UNTIL F558-NUM-REGN NOT = F500-NUM-REGN                     
                    OR WS-F558-EOF = 'Y'                                        
                 PERFORM 2200-UPDATE-BP13F520 THRU 2200-EXIT                    
                 PERFORM 1500-F500-READ THRU 1500-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2005-BYPASS-REC.                                                         
                                                                                
            PERFORM 1200-F558-READ THRU 1200-EXIT.                              
                                                                                
       2005-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2100-CHECK-MATCH.                                                        
      ******************************************************************        
                                                                                
           IF (F558-NUM-MARITAL-STATUS = 'D' OR '4')                            
              AND (F558-NUM-HDB-REF-RSL NOT = SPACES)                           
              AND (F558-CDE-FILE-STATUS-RSL NOT = 'OM')                         
              AND (F558-NUM-SELLER-RSL = 'P')                                   
                  MOVE 'Y' TO WS-DIVORCE-FD                                     
                  MOVE F558-NUM-NRIC            TO WS-NRIC                      
                  MOVE F558-NUM-MARITAL-STATUS  TO WS-MAR-STATUS                
                  MOVE F558-NUM-HDB-REF-RSL     TO WS-RSL-HDB-REF               
                  MOVE F558-CDE-FILE-STATUS-RSL TO WS-RSL-FLE-STATUS            
                  MOVE F558-NUM-SELLER-RSL      TO WS-RSL-SELLER                
                  MOVE F558-NUM-HDB-REF-SMS     TO WS-SMS-HDB-REF               
                  MOVE F558-CDE-FILE-STATUS-SMS TO WS-SMS-FLE-STATUS            
                                                                                
           END-IF.                                                              
                                                                                
           IF  (F558-NUM-HDB-REF-RSL NOT = SPACES)                              
              AND (F558-CDE-FILE-STATUS-RSL NOT = 'OM')                         
              AND (F558-NUM-SELLER-RSL = 'P')                                   
                  WRITE P13F558A-REC          FROM BP13F558-REC                 
           END-IF.                                                              
                                                                                
           IF F558-NUM-MARITAL-STATUS NOT = 'D' AND '4'                         
              IF (F558-NUM-HDB-REF-RSL NOT = SPACES)                            
                 AND (F558-CDE-FILE-STATUS-RSL NOT = 'OM')                      
                      MOVE 'Y' TO WS-DP-RSL                                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF (F558-NUM-HDB-REF-SMS NOT = SPACES)                               
              AND (F558-CDE-FILE-STATUS-SMS NOT = 'OM')                         
                  MOVE 'Y' TO WS-DP-SMS                                         
           END-IF.                                                              
                                                                                
                                                                                
           PERFORM 1200-F558-READ THRU 1200-EXIT.                               
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2200-UPDATE-BP13F520.                                                    
                                                                                
           IF WS-DIVORCE-FD = 'Y' AND WS-DP-SMS = 'N'                           
              AND WS-DP-RSL = 'N'                                               
              IF F500-CDE-HOUSEHOLD = 'G'                                       
                 MOVE 'H' TO F500-CDE-HOUSEHOLD                                 
                 WRITE BP13F520-REC FROM BP13F500-REC                           
                 ADD 1 TO WS-F520-CNT-WRT                                       
                 PERFORM 2500-WRITE-REPORT THRU 2500-EXIT                       
              END-IF                                                            
           ELSE                                                                 
              WRITE BP13F520-REC FROM BP13F500-REC                              
              ADD 1 TO WS-F520-CNT-WRT                                          
           END-IF.                                                              
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       2500-WRITE-REPORT.                                                       
      *****************************************************************         
                                                                                
           IF WS-L188-CNT-LINE > 55                                             
              PERFORM 3000-WRITE-HEADER THRU 3000-EXIT                          
           END-IF.                                                              
           ADD 1                         TO WS-L188-CNT-LINE.                   
           ADD 1                         TO WS-L188-SEQ-NO.                     
           MOVE WS-L188-SEQ-NO           TO L188-SNO.                           
           MOVE F500-NUM-REGN            TO L188-REGN-NO.                       
           MOVE WS-NRIC                  TO L188-NRIC.                          
           MOVE F500-CDE-HOUSEHOLD       TO L188-HOUSEHOLD.                     
           MOVE WS-MAR-STATUS            TO L188-MAR-STATUS.                    
           MOVE WS-RSL-HDB-REF           TO L188-RSL-HDB-REF.                   
           MOVE WS-RSL-FLE-STATUS        TO L188-RSL-FLE-STATUS.                
           MOVE WS-RSL-SELLER            TO L188-SELLER-RSL.                    
           MOVE WS-SMS-HDB-REF           TO L188-SMS-HDB-REF.                   
           MOVE WS-SMS-FLE-STATUS        TO L188-SMS-FLE-STATUS.                
           WRITE BP13L188-REC            FROM L188-REC.                         
           ADD 1                         TO WS-L188-CNT-WRT.                    
                                                                                
       2500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       3000-WRITE-HEADER.                                                       
      *****************************************************************         
      *----------------------------------------------------*                    
      * WRITES HEADER IN THE BP13L188                      *                    
      *----------------------------------------------------*                    
                                                                                
           ADD  1                   TO WS-L188-CNT-PAGE.                        
           MOVE WS-L188-CNT-PAGE TO L188-PAGE-NO.                               
           WRITE BP13L188-REC FROM L188-HD-LINE1 AFTER PAGE.                    
           WRITE BP13L188-REC FROM L188-HEADER1.                                
           WRITE BP13L188-REC FROM L188-HEADER2.                                
           WRITE BP13L188-REC FROM L188-HEADER3 AFTER 2.                        
           WRITE BP13L188-REC FROM L188-HEADER4.                                
           MOVE SPACES TO BP13L188-REC.                                         
           MOVE 8                TO WS-L188-CNT-LINE.                           
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           MOVE WS-L188-CNT-WRT  TO L188-OCCUR.                                 
           MOVE SPACES TO BP13L188-REC.                                         
           WRITE BP13L188-REC FROM L188-HEADER5.                                
           DISPLAY 'TOTAL F500 REC READ     = ' WS-F500-CNT-READ.               
           DISPLAY 'TOTAL F558 REC READ     = ' WS-F558-CNT-READ.               
           DISPLAY 'TOTAL F520 REC WRITTEN  = ' WS-F520-CNT-WRT.                
           DISPLAY 'TOTAL MATCH F500 * F558 = ' WS-MATCH-REC.                   
           DISPLAY 'TOTAL LESS  F500 * F558 = ' WS-F500-LESS-F558.              
           DISPLAY 'TOTAL L188 REC WRITTEN  = ' WS-L188-CNT-WRT.                
                                                                                
           CLOSE      BP13F558                                                  
                      BP13F500                                                  
                      BP13F520                                                  
                      P13F558A.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
