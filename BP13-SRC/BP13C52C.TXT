       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C52C.                                                  
       AUTHOR.       AM25.                                                      
       DATE-WRITTEN. 25/09/2023.                                                
      *****************************************************************         
      *                SOC - SYSTEM OF COMMITMENT (BP13)              *         
      *===============================================================*         
      *  OBJECTIVES :                                                 *         
      *     TO SEPARATE FPPS RECORDS INTO 2 GROUPS                    *         
      *        (1)PPS APPLY FOR 4-ROOM AND SMALLER FLATS UNDER BTO    *         
      *           AND FTPMC                                           *         
      *        (2)RECORDS NOT SATISFIED FROM GROUP(1)                 *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP13F740 I     SEQUENTIAL 500                                 *         
      * BP13K813 I     SEQUENTIAL 1000 K813-KEY-FLD                   *         
      * P13F740A O     SEQUENTIAL 500                                 *         
      * P13F740B O     SEQUENTIAL 500                                 *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *                                                               *         
      * CHG-REQ#  DDMMCCYY   BY   REMARKS                             *         
      * --------  --------  ----  ----------------------------------- *         
      * BP139670  25092023  AM25  NEW PROGRAM                         *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN      TO BP13F740.                             
                                                                                
           SELECT BP13K813 ASSIGN      TO BP13K813                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K813-KEY-FLD                                     
              FILE STATUS   IS WS-K813-STATUS.                                  
                                                                                
           SELECT P13F740A ASSIGN      TO P13F740A.                             
           SELECT P13F740B ASSIGN      TO P13F740B.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F740                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K813                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K813.                                                           
                                                                                
       FD  P13F740A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F740A-REC                PIC X(500).                              
                                                                                
       FD  P13F740B                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F740B-REC                PIC X(500).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K813-STATUS          PIC 9(2)        VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K813-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-F740A-WRITE          PIC 9(6)        VALUE ZEROES.            
           05  WS-F740B-WRITE          PIC 9(6)        VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-F740-EOF             PIC X           VALUE 'N'.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13F740    THRU 2000-EXIT.                        
           PERFORM 3000-MAIN-PROCESSING  THRU 3000-EXIT                         
              UNTIL WS-F740-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13K813                                                 
                OUTPUT P13F740A                                                 
                       P13F740B.                                                
                                                                                
           IF (WS-K813-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K813 FAIL STATUS=' WS-K813-STATUS               
              MOVE WS-K813-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13F740.                                                      
      *****************************************************************         
                                                                                
           READ BP13F740                                                        
              AT END                                                            
                 MOVE 'Y'              TO WS-F740-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F740-READ                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-MAIN-PROCESSING.                                                    
      *****************************************************************         
                                                                                
           IF F740-NUM-ALLO-CAT = 'BTO' AND                                     
              F740-CDE-FLAT-TYPE <= '4' AND                                     
             (F740-NUM-FTPMC = 'M' OR 'C' OR 'O' OR 'A')                        
              PERFORM 3100-READ-BP13K813 THRU 3100-EXIT                         
                                                                                
              IF K813-NUM-MATURE-EST-TAG = 'N'                                  
                 PERFORM 3200-WRITE-P13F740A THRU 3200-EXIT                     
              ELSE                                                              
                 PERFORM 3300-WRITE-P13F740B THRU 3300-EXIT                     
              END-IF                                                            
           ELSE                                                                 
              PERFORM 3300-WRITE-P13F740B THRU 3300-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F740 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3100-READ-BP13K813.                                                      
      *****************************************************************         
                                                                                
           MOVE F740-CDE-NT1           TO K813-NUM-ZONE.                        
           MOVE F740-CDE-FLAT-TYPE     TO K813-NUM-FLAT-TYPE.                   
           MOVE F740-DTE-BALLOT        TO K813-DTE-BALLOT.                      
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K813-READ                          
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K813: '                          
                    'REGN=' F740-NUM-REGN ',KEY=' K813-KEY-FLD                  
                 MOVE SPACES           TO K813-NUM-MATURE-EST-TAG               
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K813 FAIL STATUS=' WS-K813-STATUS            
                 MOVE WS-K813-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3200-WRITE-P13F740A.                                                     
      *****************************************************************         
                                                                                
           WRITE P13F740A-REC FROM BP13F740-REC.                                
           ADD 1                       TO WS-F740A-WRITE.                       
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3300-WRITE-P13F740B.                                                     
      *****************************************************************         
                                                                                
           WRITE P13F740B-REC FROM BP13F740-REC.                                
           ADD 1                       TO WS-F740B-WRITE.                       
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13F740                                                       
                 BP13K813                                                       
                 P13F740A                                                       
                 P13F740B.                                                      
                                                                                
           IF WS-K813-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K813 FAIL STATUS=' WS-K813-STATUS              
              MOVE WS-K813-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '******************************'.                            
           DISPLAY '* PROGRAM BP13C52C           *'.                            
           DISPLAY '* CONTROL COUNTS             *'.                            
           DISPLAY '*----------------------------*'.                            
           DISPLAY '* REC READ F740     : ' WS-F740-READ ' *'.                  
           DISPLAY '* REC READ K813     : ' WS-K813-READ ' *'.                  
           DISPLAY '* REC WRITE F740A   : ' WS-F740A-WRITE ' *'.                
           DISPLAY '* REC WRITE F740B   : ' WS-F740B-WRITE ' *'.                
           DISPLAY '******************************'.                            
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
