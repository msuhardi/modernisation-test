       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C993.                                                 
      *AUTHOR.        LLOYD CAPARRO.                                            
      *                                                                         
      ************************************************************              
      *                   SOC SYSTEM (SOC)                       *              
      *==========================================================*              
      * DATE WRITTEN      : 22 FEB 2005                          *              
      *                                                          *              
      * 1. OBJECTIVE      : CREATE 2 FILES FROM BP13F595 WITH    *              
      *                   : FOUND AND NOT FOUND CASES IN         *              
      *                   : AB03K080 AND AB03K030 FILES          *              
      *                                                          *              
      * 2. INPUT          : BP13F595 - BP13.F595.C994.NOTFOUND   *              
      *                   : AB03K080 - AB03.K080.UINACC          *              
      *                   : AB03K030 - AB03.K030.SALEMAST        *              
      *                                                          *              
      * 3. OUTPUT         : P13F595F - BP13.F595.C993.FOUND      *              
      *                   : P13F595N - BP13.F595.C993.NOTFOUND   *              
      *==========================================================*              
      *  MODIFICATIONS:                                          *              
      *                                                          *              
      *  CHGE-NO  DATE       OIC  DESCRIPTION                    *              
      *  -------- ---------- ---- ------------------------------ *              
      *  BP132667 22/02/2005 CG7  NEW PROGRAM.                   *              
      *  BP134081 28/02/2011 PCL3 TO REMOVE CHECKING ON          *              
      *                           K030-CDE-SALES-TYP             *              
      *  BP134598 30/07/2012 LSB1 TAKE LATEST SMS FLAT           *              
      *  BP135237 19/02/2014 IMC1 ADD NEW SMS TAG                *              
      ************************************************************              
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F595     ASSIGN        TO BP13F595.                       
                                                                                
           SELECT AB03K080     ASSIGN        TO AB03K080                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K080-KEY                        
                               FILE STATUS   IS WS-K080-STATUS.                 
                                                                                
           SELECT AB03K030     ASSIGN        TO AB03K030                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K030-NUM-HDB-REF                
                               FILE STATUS   IS WS-K030-STATUS.                 
                                                                                
           SELECT P13F595F     ASSIGN        TO P13F595F.                       
                                                                                
           SELECT P13F595N     ASSIGN        TO P13F595N.                       
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F595                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       COPY BP13F595.                                                           
                                                                                
       FD  AB03K080                                                             
                               RECORD CONTAINS 50 CHARACTERS.                   
       COPY AB03K080.                                                           
                                                                                
       FD  AB03K030                                                             
                               RECORD CONTAINS 800 CHARACTERS.                  
       COPY AB03K030.                                                           
                                                                                
       FD  P13F595F                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595F-REC                  PIC X(500).                            
                                                                                
       FD  P13F595N                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595N-REC                  PIC X(500).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05  WS-K080-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K030-STATUS            PIC 9(2)  VALUE ZEROES.                
                                                                                
       01  END-OF-FILE-FLAGS.                                                   
           05  WS-F595-EOF               PIC X     VALUE 'N'.                   
           05  WS-K080-EOF               PIC X     VALUE 'N'.                   
                                                                                
       01  COUNTERS.                                                            
           05  WS-F595-READ-CNT          PIC 9(6)  VALUE ZEROES.                
           05  WS-595F-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-595N-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-F595-SMSFD             PIC 9(6)  VALUE ZEROES.                
           05  WS-NRIC-LIST-CNT          PIC 9(1)  VALUE ZEROES.                
           05  WS-CNT                    PIC 9(1)  VALUE ZERO.                  
           05  WS-SMS-FOUND              PIC X(1)  VALUE SPACES.                
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURRENT-DATE1          PIC X(8) VALUE SPACES.                 
           05  WS-CURRENT-TIME           PIC X(9) VALUE SPACES.                 
               05  WS-TIME REDEFINES WS-CURRENT-TIME.                           
                   10  WS-HHMMSSMS       PIC X(8).                              
                   10  WS-GM             PIC X(1).                              
           05  FILLER                    PIC X(4) VALUE SPACES.                 
                                                                                
       01  TEMPORARY-VARIABLES.                                                 
           05  WS-NRIC OCCURS 2 TIMES.                                          
               10 WS-NRIC-LIST           PIC X(9)  VALUE SPACES.                
           05  WS-RUN-DATE               PIC X(10) VALUE SPACES.                
           05  WS-NRIC-FOUND             PIC X(1)  VALUE 'N'.                   
           05  WS-K080-REC-PROCESS       PIC X(1)  VALUE 'N'.                   
           05  WS-K080-NRIC-PREV         PIC X(9)  VALUE SPACES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
           PERFORM 1000-OPEN-ROUTINE THRU 1000-EXIT.                            
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-F595-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       1000-OPEN-ROUTINE.                                                       
      *************************************************************             
           OPEN INPUT  BP13F595                                                 
                       AB03K080                                                 
                       AB03K030                                                 
                OUTPUT P13F595F                                                 
                       P13F595N.                                                
                                                                                
           IF WS-K080-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K080 OPEN ERROR ' WS-K080-STATUS                     
              MOVE WS-K080-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K030-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K030 OPEN ERROR ' WS-K030-STATUS                     
              MOVE WS-K030-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CURRENT-DATE.                    
                                                                                
           STRING WS-CURRENT-DATE1(7:2) '/'                                     
                  WS-CURRENT-DATE1(5:2) '/'                                     
                  WS-CURRENT-DATE1(1:4)                                         
                  DELIMITED BY SIZE                                             
             INTO WS-RUN-DATE                                                   
                                                                                
           PERFORM 1100-READ-BP13F595 THRU 1100-EXIT.                           
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       1100-READ-BP13F595.                                                      
      *************************************************************             
           READ BP13F595                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F595-EOF                         
            NOT AT END                                                          
                ADD 1                    TO WS-F595-READ-CNT                    
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       2000-PROCESS-RECORDS.                                                    
      *************************************************************             
           MOVE SPACES                   TO WS-NRIC-LIST(1)                     
                                            WS-NRIC-LIST(2).                    
           MOVE SPACES                   TO WS-SMS-FOUND.                       
                                                                                
           MOVE F595-NUM-NRIC1           TO WS-NRIC-LIST(1).                    
           MOVE 1                        TO WS-NRIC-LIST-CNT.                   
                                                                                
           IF F595-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND                    
              F595-NUM-NRIC2(1:1) NOT = '#'                                     
              MOVE F595-NUM-NRIC2        TO WS-NRIC-LIST(2)                     
              ADD 1                      TO WS-NRIC-LIST-CNT                    
           END-IF.                                                              
                                                                                
           MOVE 'N'                      TO WS-NRIC-FOUND.                      
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1                                   
             UNTIL WS-CNT > WS-NRIC-LIST-CNT                                    
                OR WS-NRIC-FOUND = 'Y'                                          
                   MOVE SPACES           TO P13F595F-REC                        
                                            P13F595N-REC                        
                                                                                
                   PERFORM 3000-START-AB03K080 THRU 3000-EXIT                   
           END-PERFORM.                                                         
                                                                                
           IF WS-NRIC-FOUND = 'N'                                               
              IF WS-SMS-FOUND = 'Y'                                             
                 MOVE 'S'          TO F595-NUM-SMS-STATUS                       
                 ADD 1 TO WS-F595-SMSFD                                         
              ELSE                                                              
                 MOVE SPACES       TO F595-NUM-SMS-STATUS                       
              END-IF                                                            
              MOVE BP13F595-REC          TO P13F595N-REC                        
                                                                                
              WRITE P13F595N-REC                                                
                                                                                
              ADD 1                      TO WS-595N-WRITE-CNT                   
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-BP13F595 THRU 1100-EXIT.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3000-START-AB03K080.                                                     
      *************************************************************             
           MOVE SPACES                   TO AB03K080-REC.                       
           INITIALIZE                       AB03K080-REC.                       
                                                                                
           MOVE WS-NRIC-LIST(WS-CNT)     TO K080-NUM-UIN                        
                                            WS-K080-NRIC-PREV.                  
           MOVE 'N'                      TO WS-K080-EOF                         
                                            WS-K080-REC-PROCESS.                
                                                                                
           START AB03K080 KEY >= K080-KEY.                                      
                                                                                
           EVALUATE WS-K080-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3100-READNEXT-AB03K080 THRU 3100-EXIT               
                      UNTIL K080-NUM-UIN NOT = WS-K080-NRIC-PREV                
                         OR WS-K080-EOF = 'Y'                                   
                         OR WS-NRIC-FOUND = 'Y'                                 
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'AB03K080 START ERROR ' WS-K080-STATUS              
                    DISPLAY 'K080 KEY: ' K080-KEY                               
                    MOVE WS-K080-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3100-READNEXT-AB03K080.                                                  
      *************************************************************             
           READ AB03K080 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                 TO WS-K080-EOF                         
           END-READ.                                                            
                                                                                
           EVALUATE WS-K080-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K080-NUM-UIN = WS-K080-NRIC-PREV                         
                       PERFORM 3500-READ-AB03K030 THRU 3500-EXIT                
                                                                                
                       MOVE 'Y'          TO WS-K080-REC-PROCESS                 
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'AB03K080 READ NEXT ERROR ' WS-K080-STATUS          
                    DISPLAY 'K080 KEY: ' K080-KEY                               
                    MOVE WS-K080-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3500-READ-AB03K030.                                                      
      *************************************************************             
           MOVE SPACES                   TO AB03K030-REC.                       
           INITIALIZE                       AB03K030-REC.                       
                                                                                
           MOVE K080-NUM-HDB-REF         TO K030-NUM-HDB-REF.                   
                                                                                
           READ AB03K030.                                                       
                                                                                
           EVALUATE WS-K030-STATUS                                              
               WHEN 00                                                          
                    IF K030-CDE-ACCT-STAT = 'C'                                 
                       MOVE 'Y'          TO WS-SMS-FOUND                        
                       IF K030-DTE-EXE-SALE-AGRMT(1:6) >=                       
                          F595-DTE-BALLOT                                       
                          MOVE K030-CDE-SALES-TYP  TO F595-NUM-SMS-FT           
                          MOVE K030-CDE-ALLOC-SCH  TO F595-NUM-PREM-CDE         
                          MOVE BP13F595-REC TO P13F595F-REC                     
                          MOVE SPACES       TO P13F595F-REC(493:8)              
                          MOVE 'S'          TO P13F595F-REC(500:1)              
                                                                                
                          WRITE P13F595F-REC                                    
                                                                                
                          ADD 1             TO WS-595F-WRITE-CNT                
                          MOVE 'Y'          TO WS-NRIC-FOUND                    
                       END-IF                                                   
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'AB03K030 READ ERROR ' WS-K030-STATUS               
                    DISPLAY 'K030 KEY: ' K030-NUM-HDB-REF                       
                    MOVE WS-K030-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       9000-CLOSE-ROUTINE.                                                      
      *************************************************************             
           DISPLAY '   '.                                                       
           DISPLAY 'RUN DATE : ' WS-RUN-DATE.                                   
           DISPLAY '*--------- BP13C993 CONTROL TOTAL ---------*'.              
           DISPLAY '   '.                                                       
           DISPLAY 'NO. OF READS FROM F595   : ' WS-F595-READ-CNT.              
           DISPLAY 'RECS WRITTEN TO P13F595F : ' WS-595F-WRITE-CNT.             
           DISPLAY 'RECS WRITTEN TO P13F595N : ' WS-595N-WRITE-CNT.             
           DISPLAY 'RECS P13F595N FD IN SMS  : ' WS-F595-SMSFD.                 
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F595                                                       
                 AB03K080                                                       
                 AB03K030                                                       
                 P13F595F                                                       
                 P13F595N.                                                      
                                                                                
           IF WS-K080-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K080 CLOSE ERROR ' WS-K080-STATUS                    
              MOVE WS-K080-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K030-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K030 CLOSE ERROR ' WS-K030-STATUS                    
              MOVE WS-K030-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
