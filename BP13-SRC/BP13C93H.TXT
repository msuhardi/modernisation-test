       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C93H.                                                 
      *AUTHOR.        ZDD1.                                                     
      *DATE-WRITTEN.  03/06/2015.                                               
      ***************************************************************           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      ***************************************************************           
      * OBJECTIVE : USING REGN-NO/CUSTOMER-ID                       *           
      *           : MATCH THIS MONTH FILE BP13FW3B FROM BP13C92F    *           
      *           : WITH LAST MONTH FILE PASS TO SQL TABLE          *           
      *           : IF MATCH REPLACE BY THIS MONTH FILE             *           
      *           : IF NOT MATCH ADD ON NEW RECORD AND KEEP OLD REC *           
      * ----------------------------------------------------------- *           
      * CHG REF  DATE       BY   DESCRIPTION                        *           
      * -------- ---------- ---- -----------                        *           
      * BP135890 11/08/2015 ZDD1 CHANGE 1:8 TO 1:17 IN MATCHING     *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13FW3B          ASSIGN BP13FW3B.                            
           SELECT BP13FW31          ASSIGN BP13FW31.                            
           SELECT BP13OUT1          ASSIGN BP13OUT1.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13FW3B                RECORD CONTAINS 400 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13FW3B.                                                           
                                                                                
       FD   BP13FW31                RECORD CONTAINS 400 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       01 BP13FW31-REC PIC X(400).                                              
                                                                                
                                                                                
       FD   BP13OUT1                RECORD CONTAINS 400 CHARACTERS              
                                    LABEL RECORDS ARE OMITTED                   
                                    RECORDING MODE IS F.                        
       01   BP13OUT1-REC            PIC X(400).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FLAG-N-STATUS.                                                    
           05  WS-FW3B-EOF-FLAG             PIC X(01)  VALUE 'N'.               
               88  WS-FW3B-EOF              VALUE 'Y'.                          
           05  WS-FW31-EOF-FLAG             PIC X(01)  VALUE 'N'.               
               88  WS-FW31-EOF              VALUE 'Y'.                          
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FW3B-READ                 PIC 9(07)  VALUE ZEROS.             
           05  WS-FW31-READ                 PIC 9(07)  VALUE ZEROS.             
           05  WS-FW3B-UNMATCH              PIC 9(07)  VALUE ZEROS.             
           05  WS-FW31-UNMATCH              PIC 9(07)  VALUE ZEROS.             
           05  WS-FW31-MATCH                PIC 9(07)  VALUE ZEROS.             
           05  WS-BACK-WRTN                 PIC 9(07)  VALUE ZEROS.             
           05  WS-FW3B-WRTN                 PIC 9(07)  VALUE ZEROS.             
           05  WS-FW31-WRTN                 PIC 9(07)  VALUE ZEROS.             
                                                                                
       01  WS-FW3B-KEY.                                                         
           05  WS-NUM-REGN                  PIC X(08)  VALUE SPACES.            
           05  WS-NUM-CUSTOMER-ID           PIC X(09)  VALUE SPACES.            
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-YY                    PIC X(04)  VALUE SPACES.            
           05  WS-SYS-MM                    PIC X(02)  VALUE SPACES.            
           05  WS-SYS-DD                    PIC X(02)  VALUE SPACES.            
                                                                                
       PROCEDURE DIVISION.                                                      
      ***************************                                               
       0000-MAIN.                                                               
      ***************************                                               
           MOVE FUNCTION CURRENT-DATE(1:8)     TO  WS-SYSTEM-DATE.              
                                                                                
           PERFORM 1000-OPEN-FILES           THRU  1000-EXIT.                   
           PERFORM 2000-READ-BP13FW3B        THRU  2000-EXIT.                   
           PERFORM 2010-READ-BP13FW31        THRU  2010-EXIT.                   
           PERFORM 3000-PROCESS              THRU  3000-EXIT                    
             UNTIL WS-FW3B-EOF AND WS-FW31-EOF.                                 
           PERFORM 9000-CLOSE-FILES          THRU  9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       1000-OPEN-FILES.                                                         
      ***************************                                               
           OPEN INPUT BP13FW3B                                                  
                      BP13FW31                                                  
               OUTPUT BP13OUT1.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       2000-READ-BP13FW3B.                                                      
      ***************************                                               
           READ BP13FW3B                                                        
             AT END                                                             
                MOVE 'Y'                    TO  WS-FW3B-EOF-FLAG                
                MOVE HIGH-VALUES            TO  WS-FW3B-KEY                     
            NOT AT END                                                          
                ADD   1                     TO  WS-FW3B-READ                    
                MOVE FW3B-NUM-REGN TO WS-NUM-REGN                               
                MOVE FW3B-NUM-CUSTOMER-ID TO WS-NUM-CUSTOMER-ID                 
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       2010-READ-BP13FW31.                                                      
      ***************************                                               
           READ BP13FW31                                                        
             AT END                                                             
                MOVE 'Y'                    TO  WS-FW31-EOF-FLAG                
                MOVE HIGH-VALUES            TO  BP13FW31-REC(1:17)              
            NOT AT END                                                          
                ADD  1                      TO  WS-FW31-READ                    
           END-READ.                                                            
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       3000-PROCESS.                                                            
      ***************************                                               
           IF WS-FW3B-KEY   = BP13FW31-REC(1:17)                                
              ADD  1                          TO  WS-FW31-MATCH                 
              PERFORM 4100-OUTPUT-FW31         THRU  4100-EXIT                  
              PERFORM 2010-READ-BP13FW31       THRU  2010-EXIT                  
              PERFORM 2000-READ-BP13FW3B       THRU  2000-EXIT                  
           ELSE                                                                 
              IF WS-FW3B-KEY   > BP13FW31-REC(1:17)                             
                 ADD  1                    TO  WS-FW31-UNMATCH                  
                 PERFORM 4100-OUTPUT-FW31         THRU  4100-EXIT               
                 PERFORM 2010-READ-BP13FW31    THRU  2010-EXIT                  
              ELSE                                                              
                 IF WS-FW3B-KEY   < BP13FW31-REC(1:17)                          
                    ADD  1                       TO  WS-FW3B-UNMATCH            
                    PERFORM 4200-OUTPUT-FW3B      THRU 4200-EXIT                
                    PERFORM 2000-READ-BP13FW3B       THRU  2000-EXIT            
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       4100-OUTPUT-FW31.                                                        
      ***************************                                               
           MOVE SPACES                       TO  BP13OUT1-REC.                  
           MOVE BP13FW31-REC                 TO  BP13OUT1-REC.                  
           WRITE BP13OUT1-REC.                                                  
           ADD  1                            TO  WS-FW31-WRTN.                  
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       4200-OUTPUT-FW3B.                                                        
      ***************************                                               
           MOVE SPACES                       TO  BP13OUT1-REC.                  
           MOVE BP13FW3B-REC                 TO  BP13OUT1-REC.                  
           WRITE BP13OUT1-REC.                                                  
           ADD  1                            TO  WS-FW31-WRTN.                  
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       9000-CLOSE-FILES.                                                        
      ***************************                                               
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-YY.         
           DISPLAY '**************** MATFW31 ************************'.         
           DISPLAY ' RECORDS READ    IN BP13FW3B :   ' WS-FW3B-READ.            
           DISPLAY ' RECORDS READ    IN BP13FW31 :   ' WS-FW31-READ.            
           DISPLAY SPACES.                                                      
           DISPLAY ' FW3B NOT FOUND IN   FW31 : ' WS-FW3B-UNMATCH.              
           DISPLAY ' FW31 NOT FOUND IN   FW3B : ' WS-FW31-UNMATCH.              
           DISPLAY ' FW31 MATCHED WITH FW3B   : ' WS-FW31-MATCH.                
           DISPLAY SPACES.                                                      
           DISPLAY ' RECORD WRITTEN IN OUTPUT : ' WS-FW31-WRTN.                 
           DISPLAY '**************** -------- ************************'.        
                                                                                
           CLOSE BP13FW31                                                       
                 BP13FW3B                                                       
                 BP13OUT1.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
