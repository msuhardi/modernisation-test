       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C25E.                                                 
      *AUTHOR.        ELAINE S. ARGA.                                           
      *DATE-WRITTEN.  06 MARCH 2018.                                            
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE   :  TO DELETE RECORD OF ALL SCHEDULED CASES    *            
      *                 INSIDE BP13.K200.PNDAPPMT                  *            
      *                                                            *            
      *  INPUT FILE  :  1.  BP13F200 - BOOKING APPMT FILE (SEQ)    *            
      *                                                            *            
      *  I-O   FILES :  1.  BP13K200 - BOOKING APPOINTMENT FILE    *            
      * ---------------------------------------------------------- *            
      * CHG-NO   BY   DATE    DESCRIPTION                          *            
      * -------- ---  ------  -----------                          *            
      * BP137114 ESA1 060318  NEW PROGRAM                          *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F200  ASSIGN       TO BP13F200.                           
                                                                                
           SELECT BP13K200  ASSIGN       TO BP13K200                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            ALTERNATE KEY IS K200-NUM-REGN                      
                            RECORD KEY   IS K200-KEY-FLD                        
                            FILE STATUS  IS WS-K200-STATUS.                     
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F200                                                            
            RECORDING  MODE IS  F                                               
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
       FD   BP13K200                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13K200.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-VARIABLES.                                                        
           05  WS-K200-STATUS               PIC 99    VALUE  00.                
           05  WS-EOF-F200                  PIC X     VALUE 'N'.                
           05  WS-READ-NEXT-F200            PIC X     VALUE 'Y'.                
           05  WS-CNT-READ-F200             PIC 9(6)  VALUE ZEROS.              
           05  WS-CNT-DEL-K200              PIC 9(6)  VALUE ZEROS.              
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM  100-OPEN-ROUTINE         THRU  100-EXIT.                    
           PERFORM  200-READ-BP13F200        THRU  200-EXIT.                    
           PERFORM  300-PROCESS-RECORD       THRU  300-EXIT                     
                        UNTIL  WS-EOF-F200 = 'Y'.                               
           PERFORM  900-CLOSE-ROUTINE        THRU  900-EXIT.                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F200                                                 
                I-O    BP13K200.                                                
                                                                                
           IF WS-K200-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K200, STATUS = ' WS-K200-STATUS        
              MOVE     WS-K200-STATUS TO RETURN-CODE                            
              PERFORM  900-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-BP13F200.                                                       
      *-------------------------------------------------------------            
           READ BP13F200                                                        
                AT END MOVE 'Y' TO WS-EOF-F200                                  
                GO TO  200-EXIT.                                                
                                                                                
           ADD  1  TO  WS-CNT-READ-F200.                                        
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-RECORD.                                                      
      *--------------------------------------------------------------*          
                                                                                
           MOVE F200-NUM-REGN TO K200-NUM-REGN.                                 
                                                                                
           READ BP13K200 KEY IS K200-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K200-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                   PERFORM 400-DELETE-BP13K200 THRU 400-EXIT                    
              WHEN 23                                                           
                   DISPLAY 'RECORD NT FOUND: ' K200-NUM-REGN                    
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13K200 ALT. STATUS IS '             
                           WS-K200-STATUS                                       
                   PERFORM 900-CLOSE-ROUTINE   THRU 900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
           PERFORM 200-READ-BP13F200      THRU 200-EXIT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-DELETE-BP13K200.                                                     
      *--------------------------------------------------------------*          
                                                                                
           DELETE BP13K200.                                                     
                                                                                
           EVALUATE WS-K200-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                   ADD 1                  TO   WS-CNT-DEL-K200                  
              WHEN OTHER                                                        
                   DISPLAY 'ERROR DELETING BP13K200. STATUS IS '                
                           WS-K200-STATUS                                       
                   PERFORM 900-CLOSE-ROUTINE   THRU 900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY '*--------- BP13C25E CONTROL LIST ----------*'.              
           DISPLAY ' '.                                                         
           DISPLAY 'BP13F200 READ    RECORDS  = ' WS-CNT-READ-F200.             
           DISPLAY 'BP13K200 DELETED RECORDS  = ' WS-CNT-DEL-K200.              
                                                                                
           CLOSE BP13F200                                                       
                 BP13K200.                                                      
                                                                                
           IF WS-K200-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K200, STATUS = ' WS-K200-STATUS        
              MOVE     WS-K200-STATUS TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
