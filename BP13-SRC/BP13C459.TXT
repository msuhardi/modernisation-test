      *------------------------------------------------------------*            
       IDENTIFICATION DIVISION.                                                 
      *------------------------------------------------------------*            
       PROGRAM-ID.   BP13C459.                                                  
      *AUTHOR.       BALA AMUTHAN.                                              
      *DATE-WRITTEN. 20/02/2006.                                                
      *------------------------------------------------------------*            
      *                SYSTEM OF COMMITMENT  (BP13)                *            
      *------------------------------------------------------------*            
      *                                                            *            
      *  OBJECTIVE                                                 *            
      *               : THIS PROGRAM WILL DELETE BANKLOAN MEMO REC *            
      *                 BP13K671 AFTER 6 MONTH KEY-ISSUE DATE.     *            
      *                                                            *            
      *  I-O         : 1.  BP13F671                                *            
      *  INPUT FILES : 1.  BP13K410                                *            
      *  OUTPUT FILE : 1.  P13F670A                                *            
      *------------------------------------------------------------*            
      * CHG REQ# BY   DATE      DESCRIPTION                        *            
      * -------- ---- --------  -----------------------------------*            
      * BP132828 BA2  20/02/05  NEW PROGRAM.                       *            
      *------------------------------------------------------------*            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *------------------------------------------------------------*            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *------------------------------------------------------------*            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *------------------------------------------------------------*            
                                                                                
           SELECT BP13F671 ASSIGN TO BP13F671.                                  
                                                                                
           SELECT P13F671A ASSIGN TO P13F671A.                                  
                                                                                
           SELECT BP13K671 ASSIGN TO BP13K671                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K671-KEY-FLD                               
                  FILE STATUS     IS WS-K671-STATUS.                            
                                                                                
           SELECT BP13K410 ASSIGN TO BP13K410                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K410-KEY-FLD                               
                  FILE STATUS     IS WS-K410-STATUS.                            
                                                                                
      *------------------------------------------------------------*            
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *------------------------------------------------------------*            
                                                                                
       FD   BP13F671                                                            
            RECORD CONTAINS 3000 CHARACTERS                                     
            BLOCK CONTAINS     0 RECORDS                                        
            RECORDING MODE    IS F                                              
            LABEL RECORDS    ARE STANDARD.                                      
                                                                                
       COPY BP13F671.                                                           
                                                                                
       FD   P13F671A                                                            
            RECORD CONTAINS 3000 CHARACTERS                                     
            BLOCK CONTAINS     0 RECORDS                                        
            RECORDING MODE    IS F                                              
            LABEL RECORDS    ARE STANDARD.                                      
                                                                                
       01   P13F671A-REC                PIC X(3000).                            
                                                                                
       FD   BP13K671                                                            
            RECORD CONTAINS 3000 CHARACTERS.                                    
                                                                                
       COPY BP13K671.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
                                                                                
       COPY BP13K410.                                                           
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
                                                                                
       01  WORK-FILE-VARS.                                                      
           05  WS-F671-EOF                PIC X(01)   VALUE 'N'.                
           05  WS-K410-FOUND              PIC X(01)   VALUE 'N'.                
                                                                                
           05  WS-K410-STATUS             PIC 9(02)   VALUE 0.                  
           05  WS-K671-STATUS             PIC 9(02)   VALUE 0.                  
                                                                                
           05  WS-F671-READ               PIC 9(08)   VALUE 0.                  
           05  WS-K410-READ               PIC 9(08)   VALUE 0.                  
           05  WS-K671-DELETE             PIC 9(08)   VALUE 0.                  
           05  WS-A671-WRITE              PIC 9(08)   VALUE 0.                  
                                                                                
       01  WS-DATE-VARS.                                                        
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY            PIC 9(04).                            
               10  WS-SYS-MM              PIC 9(02).                            
               10  WS-SYS-DD              PIC 9(02).                            
                                                                                
           05  WS-ISSU-DATE-4MONTH.                                             
               10  WS-ISSU-CCYY            PIC 9(04).                           
               10  WS-ISSU-MM              PIC 9(02).                           
                                                                                
           05  WS-K410-DTE-KEY-ISSUE-CHAR  PIC X(06).                           
                                                                                
           05  WS-K410-DTE-KEY-ISSUE REDEFINES                                  
                  WS-K410-DTE-KEY-ISSUE-CHAR PIC 9(06).                         
                                                                                
           05 WS-EDITED-DATE              PIC X(10)   VALUE SPACES.             
                                                                                
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
                                                                                
      *------------------------------------------------------------*            
       0000-MAIN.                                                               
      *------------------------------------------------------------*            
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 1200-READ-F671     THRU 1200-EXIT.                           
           PERFORM 2000-PROCESS       THRU 2000-EXIT                            
                   UNTIL WS-F671-EOF = 'Y'.                                     
                                                                                
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1000-OPEN-FILES.                                                         
      *------------------------------------------------------------*            
                                                                                
           OPEN INPUT  BP13F671                                                 
                       BP13K410                                                 
                I-O    BP13K671                                                 
                OUTPUT P13F671A.                                                
                                                                                
           EVALUATE WS-K410-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'OPEN BP13K410 FAIL. STATUS - '                        
                                            WS-K410-STATUS                      
                 MOVE  WS-K410-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K671-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'OPEN BP13K671 FAIL. STATUS - '                        
                                            WS-K671-STATUS                      
                 MOVE  WS-K671-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-SYSTEM-DATE.                  
                                                                                
           STRING  WS-SYSTEM-DATE(7:2) '/'                                      
                   WS-SYSTEM-DATE(5:2) '/'                                      
                   WS-SYSTEM-DATE(1:4)                                          
                   DELIMITED BY SIZE                                            
                   INTO WS-EDITED-DATE                                          
           END-STRING.                                                          
                                                                                
      * TO ASSIGN LESS THEN 6 MONTH.                                            
                                                                                
           MOVE WS-SYSTEM-DATE     TO WS-ISSU-DATE-4MONTH.                      
           IF WS-ISSU-MM <= 06                                                  
               COMPUTE  WS-ISSU-CCYY = WS-ISSU-CCYY - 1                         
               EVALUATE WS-ISSU-MM                                              
                 WHEN 06                                                        
                   MOVE 12 TO WS-ISSU-MM                                        
                 WHEN 05                                                        
                   MOVE 11 TO WS-ISSU-MM                                        
                 WHEN 04                                                        
                   MOVE 10 TO WS-ISSU-MM                                        
                 WHEN 03                                                        
                   MOVE 09 TO WS-ISSU-MM                                        
                 WHEN 02                                                        
                   MOVE 08 TO WS-ISSU-MM                                        
                 WHEN 01                                                        
                   MOVE 07 TO WS-ISSU-MM                                        
               END-EVALUATE                                                     
           ELSE                                                                 
              COMPUTE WS-ISSU-MM = WS-ISSU-MM - 6                               
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1200-READ-F671.                                                          
      *------------------------------------------------------------*            
                                                                                
           READ BP13F671                                                        
                AT END                                                          
                   MOVE 'Y' TO WS-F671-EOF                                      
                NOT AT END                                                      
                   ADD 1    TO WS-F671-READ                                     
           END-READ.                                                            
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2000-PROCESS.                                                            
      *------------------------------------------------------------*            
                                                                                
           PERFORM 3000-READ-K410        THRU 3000-EXIT.                        
                                                                                
           IF WS-K410-FOUND = 'Y'                                               
              MOVE K410-DTE-KEY-ISSUE TO WS-K410-DTE-KEY-ISSUE-CHAR             
              IF WS-K410-DTE-KEY-ISSUE < WS-ISSU-DATE-4MONTH                    
                  PERFORM 4000-READ-K671 THRU 4000-EXIT                         
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 1200-READ-F671        THRU 1200-EXIT.                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3000-READ-K410.                                                          
      *------------------------------------------------------------*            
                                                                                
           MOVE 'N'               TO WS-K410-FOUND.                             
           MOVE F671-NUM-REGN     TO K410-NUM-REGN.                             
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE WS-K410-STATUS                                              
              WHEN 00                                                           
                 ADD  1 TO WS-K410-READ                                         
                 MOVE 'Y'    TO WS-K410-FOUND                                   
              WHEN 10                                                           
              WHEN 23                                                           
                 DISPLAY 'REGN NOT FOUND IN K410 - ' F671-NUM-REGN              
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BP13K410. STATUS - '                   
                                            WS-K410-STATUS                      
                 MOVE WS-K410-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------------------------------------------*            
       4000-READ-K671.                                                          
      *------------------------------------------------------------*            
                                                                                
           MOVE F671-NUM-REGN  TO K671-NUM-REGN.                                
                                                                                
           READ BP13K671.                                                       
                                                                                
           EVALUATE WS-K671-STATUS                                              
              WHEN 00                                                           
                 PERFORM 4100-WRITE-F671    THRU 4100-EXIT                      
                 PERFORM 4200-DELETE-K671   THRU 4200-EXIT                      
              WHEN 23                                                           
                 DISPLAY ' NOT FOUND IN BP13K671 - ' F671-NUM-REGN              
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BP13K671. STATUS - '                   
                                            WS-K671-STATUS                      
                 MOVE WS-K671-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4100-WRITE-F671.                                                         
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES        TO P13F671A-REC.                                  
           MOVE  BP13K671-REC TO P13F671A-REC.                                  
           ADD 1         TO  WS-A671-WRITE.                                     
           WRITE P13F671A-REC.                                                  
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------------------------------------------*            
       4200-DELETE-K671.                                                        
      *------------------------------------------------------------*            
                                                                                
           DELETE BP13K671.                                                     
           EVALUATE WS-K671-STATUS                                              
              WHEN 00                                                           
                 ADD 1         TO  WS-K671-DELETE                               
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR DELETE BP13K671. STATUS - '                    
                                            WS-K671-STATUS.                     
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------*            
                                                                                
           CLOSE BP13F671                                                       
                 BP13K671                                                       
                 BP13K410                                                       
                 P13F671A.                                                      
                                                                                
           EVALUATE WS-K671-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'CLOSE BP13K671 FAIL. STATUS-'                         
                                         WS-K671-STATUS                         
                 MOVE WS-K671-STATUS  TO RETURN-CODE                            
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K410-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'CLOSE BP13K410 FAIL. STATUS-'                         
                                         WS-K410-STATUS                         
                 MOVE WS-K410-STATUS  TO RETURN-CODE                            
           END-EVALUATE.                                                        
                                                                                
           DISPLAY '*----------- BP13C459 -------------*'.                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY ' 1. BP13F671 READ      : '  WS-F671-READ.                   
           DISPLAY ' 2. BP13K410 READ      : '  WS-K410-READ.                   
           DISPLAY SPACES.                                                      
           DISPLAY ' 3. P13F671A WRITE     : '  WS-A671-WRITE.                  
           DISPLAY ' 4. BP13K671 DELETE    : '  WS-K671-DELETE.                 
           DISPLAY SPACES.                                                      
           DISPLAY '*----------------------------------*'.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
      *                      END OF PROGRAM                        *            
      *------------------------------------------------------------*            
