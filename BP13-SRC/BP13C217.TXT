       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C217.                                                  
      *DATE-WRITTEN. 10 AUG 2017.                                               
      *AUTHOR.       KRISTINE LANTIN.                                           
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) TO UPDATE PCD IN BATCH                      *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP13XXXX 20170810 KVL1  NEW PROGRAM                        *            
      * BP137091 20171030 FNP1  MATCH F217 WITH F130 POSTAL TO READ*            
      *                         BM06K100                           *            
      * BP137170 20180126 FNP1  CHANGE SIZE OF DATE FIELD TO 8 BYTES            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F217  ASSIGN       TO BP13F217.                           
           SELECT BM06F130  ASSIGN       TO BM06F130.                           
           SELECT BM06K100  ASSIGN       TO BM06K100                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K100-KEY-FLD                        
                            FILE STATUS  IS BM06K100-STATUS.                    
                                                                                
           SELECT BM06K190  ASSIGN       TO BM06K190                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K190-2-KEY                          
                            FILE STATUS  IS BM06K190-STATUS.                    
                                                                                
           SELECT BP13FOUT  ASSIGN       TO BP13FOUT.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F217                                                             
           RECORD CONTAINS 20   CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BP13F217.                                                           
                                                                                
       FD  BM06F130                                                             
           RECORD CONTAINS 600  CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BM06F130.                                                           
                                                                                
       FD  BM06K100                                                             
           RECORD CONTAINS 500  CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD  BM06K190                                                             
           RECORD CONTAINS 650  CHARACTERS.                                     
       COPY BM06K190.                                                           
                                                                                
       FD  BP13FOUT                                                             
           RECORD CONTAINS 150  CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       01 BP13FOUT-REC         PIC X(150).                                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BM06K100-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  BM06K190-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  WS-F217-CNT         PIC 9(06)  VALUE ZEROES.                         
       01  WS-F130-CNT         PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-CNT        PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-LOG-CNT    PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-FOUT       PIC 9(06)  VALUE ZEROES.                         
       01  WS-K100-NFND        PIC 9(06)  VALUE ZEROES.                         
       01  WS-MATCH            PIC 9(06)  VALUE ZEROES.                         
       01  WS-BP13F217-EOF     PIC X      VALUE 'N'.                            
       01  WS-BM06F130-EOF     PIC X      VALUE 'N'.                            
       01  WS-K100-FND         PIC X      VALUE 'N'.                            
       01  WS-VALID-TAG        PIC X      VALUE 'Y'.                            
       01  WS-DTE-PACD         PIC X(02)  VALUE SPACES.                         
       01  WS-DATE-TIME.                                                        
           05  WS-DATE         PIC 9(08)  VALUE ZEROES.                         
           05  WS-TIME         PIC 9(08)  VALUE ZEROES.                         
       01  WS-TIME-OUT.                                                         
           05  WS-TIME-OUT-HM  PIC 9(08)  VALUE ZEROES.                         
           05  WS-TIME-OUT-S   PIC 9(01)  VALUE ZEROES.                         
       01  WS-K100-REC         PIC X(600) VALUE SPACES.                         
                                                                                
       01 FOUT-HEADER.                                                          
          05 FILLER            PIC X(15) VALUE 'RECORDS UPDATED'.               
          05 FILLER            PIC X(135) VALUE SPACES.                         
                                                                                
       01 FOUT-HEADER1.                                                         
          05 FILLER            PIC X(03) VALUE 'NT'.                            
          05 FILLER            PIC X(01) VALUE ';'.                             
          05 FILLER            PIC X(20) VALUE 'STREET'.                        
          05 FILLER            PIC X(01) VALUE ';'.                             
          05 FILLER            PIC X(05) VALUE 'BLOCK'.                         
          05 FILLER            PIC X(01) VALUE ';'.                             
          05 FILLER            PIC X(06) VALUE 'POSTAL'.                        
          05 FILLER            PIC X(01) VALUE ';'.                             
          05 FILLER            PIC X(03) VALUE 'PCD'.                           
          05 FILLER            PIC X(01) VALUE ';'.                             
          05 FILLER            PIC X(08) VALUE 'PCD BEF'.                       
          05 FILLER            PIC X(01) VALUE ';'.                             
          05 FILLER            PIC X(08) VALUE 'PCD AFT'.                       
          05 FILLER            PIC X(01) VALUE ';'.                             
          05 FILLER            PIC X(50) VALUE 'COMMENT'.                       
          05 FILLER            PIC X(40) VALUE SPACES.                          
                                                                                
       01 FOUT-DETAIL.                                                          
          05 FOUT-NT           PIC X(03) VALUE SPACES.                          
          05 FIL01             PIC X(01) VALUE SPACES.                          
          05 FOUT-STREET       PIC X(20) VALUE SPACES.                          
          05 FIL02             PIC X(01) VALUE SPACES.                          
          05 FOUT-BLOCK        PIC X(05) VALUE SPACES.                          
          05 FIL03             PIC X(01) VALUE SPACES.                          
          05 FOUT-POSTAL       PIC X(06) VALUE SPACES.                          
          05 FIL04             PIC X(01) VALUE SPACES.                          
          05 FOUT-NUM-PACD     PIC X(03) VALUE SPACES.                          
          05 FIL05             PIC X(01) VALUE SPACES.                          
          05 FOUT-DTE-PACD-BEF PIC X(08) VALUE SPACES.                          
          05 FIL06             PIC X(01) VALUE SPACES.                          
          05 FOUT-DTE-PACD-AFT PIC X(08) VALUE SPACES.                          
          05 FIL07             PIC X(01) VALUE SPACES.                          
          05 FOUT-COMMENT      PIC X(50) VALUE SPACES.                          
          05 FILLER            PIC X(40) VALUE SPACES.                          
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
                                                                                
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F217       THRU 2000-EXIT.                     
           PERFORM 2100-READ-BM06F130       THRU 2100-EXIT.                     
           PERFORM 3000-PROCESS-DATA        THRU 3000-EXIT                      
                UNTIL F130-NUM-POSTAL-CODE = HIGH-VALUES AND                    
                      F217-NUM-POSTAL      = HIGH-VALUES.                       
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F217                                                 
                       BM06F130                                                 
                I-O    BM06K100                                                 
                       BM06K190                                                 
                OUTPUT BP13FOUT.                                                
                                                                                
            IF BM06K100-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPEN ERROR BM06K100 FILE, STATUS IS '                   
                        BM06K100-STATUS                                         
               MOVE BM06K100-STATUS         TO   RETURN-CODE                    
               PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            IF BM06K190-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPEN ERROR BM06K190 FILE, STATUS IS '                   
                        BM06K190-STATUS                                         
               MOVE BM06K190-STATUS         TO   RETURN-CODE                    
               PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            WRITE BP13FOUT-REC FROM FOUT-HEADER.                                
            WRITE BP13FOUT-REC FROM FOUT-HEADER1.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F217.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F217 AT END                                                 
                         MOVE HIGH-VALUES TO F217-NUM-POSTAL                    
                         GO TO 2000-EXIT.                                       
                                                                                
           ADD  1    TO WS-F217-CNT.                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BM06F130.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BM06F130 AT END                                                 
                         MOVE HIGH-VALUES TO F130-NUM-POSTAL-CODE               
                         GO TO 2100-EXIT.                                       
                                                                                
           ADD  1    TO WS-F130-CNT.                                            
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES TO BP13FOUT-REC.                                         
                                                                                
           IF F217-NUM-POSTAL = F130-NUM-POSTAL-CODE                            
              ADD 1 TO WS-MATCH                                                 
              MOVE F130-NT-CODE                TO FOUT-NT                       
              MOVE F130-BLK-NO                 TO FOUT-BLOCK                    
              MOVE F130-NME-STREET             TO FOUT-STREET                   
              PERFORM 3100-READ-BM06K100       THRU 3100-EXIT                   
              PERFORM 2000-READ-BP13F217       THRU 2000-EXIT                   
              PERFORM 2100-READ-BM06F130       THRU 2100-EXIT                   
           ELSE                                                                 
              IF F217-NUM-POSTAL < F130-NUM-POSTAL-CODE                         
                 MOVE 'RECORD NOT MATCH IN BM06F130' TO                         
                            FOUT-COMMENT                                        
                 PERFORM 3150-WRITE-BP13FOUT   THRU 3150-EXIT                   
                 PERFORM 2000-READ-BP13F217    THRU 2000-EXIT                   
              ELSE                                                              
                 IF F217-NUM-POSTAL > F130-NUM-POSTAL-CODE                      
                    PERFORM 2100-READ-BM06F130 THRU 2100-EXIT                   
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-READ-BM06K100.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                        TO K100-KEY-FLD                   
           MOVE F130-ESTATE                   TO K100-ESTATE                    
           MOVE F130-NEIGHBOURHOOD            TO K100-NEIGHBOURHOOD             
           MOVE F130-CONTRACT-NO              TO K100-CONTRACT-NO               
           MOVE F130-BLK-NO                   TO K100-BLK-NO                    
                                                                                
           READ BM06K100.                                                       
                                                                                
           EVALUATE BM06K100-STATUS                                             
           WHEN 00                                                              
              MOVE K100-REC TO WS-K100-REC                                      
              MOVE K100-DTE-PACD            TO FOUT-DTE-PACD-BEF                
              PERFORM 3160-CHECK-PACD       THRU 3160-EXIT                      
              PERFORM 3200-REWRITE-BM06K100 THRU 3200-EXIT                      
              PERFORM 3300-WRITE-BM06K190   THRU 3300-EXIT                      
              PERFORM 3150-WRITE-BP13FOUT   THRU 3150-EXIT                      
           WHEN 23                                                              
              MOVE 'RECORD NOT FOUND IN BM06K100' TO                            
                          FOUT-COMMENT                                          
              PERFORM 3160-CHECK-PACD       THRU 3160-EXIT                      
              PERFORM 3150-WRITE-BP13FOUT   THRU 3150-EXIT                      
              ADD 1 TO WS-K100-NFND                                             
           WHEN OTHER                                                           
              DISPLAY 'BM06K100 READ ERROR, STATUS IS '                         
                       BM06K100-STATUS                                          
              MOVE     BM06K100-STATUS       TO   RETURN-CODE                   
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3150-WRITE-BP13FOUT.                                                     
      *-------------------------------------------------------------            
           MOVE ';'       TO FIL01  FIL02  FIL03                                
                             FIL04  FIL05  FIL06                                
                             FIL07.                                             
                                                                                
           MOVE F217-NUM-POSTAL      TO FOUT-POSTAL.                            
                                                                                
           WRITE BP13FOUT-REC FROM FOUT-DETAIL.                                 
           ADD 1                     TO WS-WRITE-FOUT.                          
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3160-CHECK-PACD.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE F217-DTE-PACD(1:2) TO WS-DTE-PACD.                              
                                                                                
           EVALUATE WS-DTE-PACD                                                 
              WHEN 'Q1'                                                         
                 STRING  F217-DTE-PACD-CCYY '01' '01'                           
                    DELIMITED BY SIZE INTO  K100-DTE-PACD                       
                 MOVE 'Q'               TO  K100-NUM-PACD                       
              WHEN 'Q2'                                                         
                 STRING  F217-DTE-PACD-CCYY '04' '01'                           
                    DELIMITED BY SIZE INTO  K100-DTE-PACD                       
                 MOVE 'Q'               TO  K100-NUM-PACD                       
              WHEN 'Q3'                                                         
                 STRING  F217-DTE-PACD-CCYY '07' '01'                           
                    DELIMITED BY SIZE INTO  K100-DTE-PACD                       
                 MOVE 'Q'               TO  K100-NUM-PACD                       
              WHEN 'Q4'                                                         
                 STRING  F217-DTE-PACD-CCYY '10' '01'                           
                    DELIMITED BY SIZE INTO  K100-DTE-PACD                       
                 MOVE 'Q'               TO  K100-NUM-PACD                       
              WHEN OTHER                                                        
                 STRING F217-DTE-PACD-CCYY F217-DTE-PACD-MM '01'                
                    DELIMITED BY SIZE INTO  K100-DTE-PACD                       
                 MOVE 'M'               TO  K100-NUM-PACD                       
           END-EVALUATE.                                                        
                                                                                
           MOVE K100-NUM-PACD   TO FOUT-NUM-PACD.                               
           MOVE K100-DTE-PACD   TO FOUT-DTE-PACD-AFT.                           
                                                                                
       3160-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-REWRITE-BM06K100.                                                   
      *-------------------------------------------------------------            
                                                                                
           REWRITE K100-REC.                                                    
           IF BM06K100-STATUS NOT = 00                                          
              DISPLAY 'ERROR UPDATING BM06K100, STATUS IS: '                    
                  BM06K100-STATUS ' KEY IS, ' K100-KEY-FLD                      
              MOVE     BM06K100-STATUS       TO   RETURN-CODE                   
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           ADD 1                         TO WS-WRITE-CNT.                       
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3300-WRITE-BM06K190.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                   TO K190-REC2.                          
           MOVE WS-K100-REC              TO K190-REC2.                          
           MOVE FUNCTION CURRENT-DATE    TO WS-DATE-TIME.                       
           MOVE WS-TIME                  TO WS-TIME-OUT-HM.                     
           ADD  1                        TO WS-TIME-OUT-S.                      
           MOVE WS-DATE                  TO K190-2-SYS-DATE.                    
           MOVE WS-TIME-OUT              TO K190-2-SYS-TIME.                    
           MOVE 'C21700'                 TO K190-2-TRANS-TYPE.                  
           MOVE 'BP13C217'               TO K190-2-USER-ID.                     
           WRITE K190-REC2.                                                     
           ADD 1                         TO WS-WRITE-LOG-CNT.                   
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '---- BP13C217 CONTROL TOTAL -----'.                         
           DISPLAY ' NO OF RECS READ FROM BP13F217   = '                        
                     WS-F217-CNT.                                               
           DISPLAY ' NO OF RECS READ FROM BM06F130   = '                        
                     WS-F130-CNT.                                               
           DISPLAY ' NO OF RECORDS MATCHED           = '                        
                     WS-MATCH.                                                  
           DISPLAY ' NO OF PCD RECS UPDATED IN FILE  = '                        
                     WS-WRITE-CNT.                                              
           DISPLAY ' NO OF RECS WRITTEN IN FOUT      = '                        
                     WS-WRITE-FOUT.                                             
           DISPLAY ' NO OF RECS NOT FND IN BM06K100  = '                        
                     WS-K100-NFND.                                              
           DISPLAY ' NO OF PCD RECS UPDATED IN LOG FILE  = '                    
                     WS-WRITE-LOG-CNT.                                          
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13F217                                                 
                       BM06F130                                                 
                       BM06K190                                                 
                       BM06K100                                                 
                       BP13FOUT.                                                
                                                                                
            IF BM06K100-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPEN ERROR BM06K100 FILE, STATUS IS '                   
                        BM06K100-STATUS                                         
               MOVE BM06K100-STATUS         TO   RETURN-CODE                    
            END-IF.                                                             
                                                                                
            IF BM06K190-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPEN ERROR BM06K190 FILE, STATUS IS '                   
                        BM06K190-STATUS                                         
               MOVE BM06K190-STATUS         TO   RETURN-CODE                    
            END-IF.                                                             
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
