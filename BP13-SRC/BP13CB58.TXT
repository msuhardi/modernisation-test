       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB58.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  31/10/2017.                                               
      * ============================================================= *         
      *                  SYSTEM OF COMMITMENT (SOC)                   *         
      * ============================================================= *         
      *                                                               *         
      * OBJECTIVE    : GENERATE PRE-BALLOT REPORTS BY EST AND FT      *         
      *                                                               *         
      * INPUT FILE   : BP13LB57 - SUMMARY COUNT                       *         
      * OUTPUT FILE  : BP13LB58 - REPORT GENERATED                    *         
      *                                                               *         
      * ============================================================= *         
      * CHG-REF#  DATE      BY    DESCRIPTIONS                        *         
      * --------  --------  ----  ----------------------------------- *         
      * BP137052  31102017  SMR2  NEW PROGRAM                         *         
      * BP137052  04122017  SMR2  CATER FOR GRAND TOTAL               *         
      * BP137706  08032019  AM25  INCLUDE CHECKING OF FLAT TY FOR ROF *         
      * BP138214  20022020  AM25  CATER FOR BP13K762 COPYCOB CHANGES  *         
      * ============================================================= *         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13LB57 ASSIGN TO BP13LB57.                                  
                                                                                
           SELECT BP13LB58 ASSIGN TO BP13LB58.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13LB57                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 200 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORD     IS STANDARD.                                       
       01   BP13LB57-REC.                                                       
            05  LB57-NUM-ZONE               PIC X(03).                          
            05  LB57-NUM-FLAT-TYPE          PIC X(02).                          
            05  LB57-DTE-BALLOT             PIC X(06).                          
            05  LB57-NUM-SALES-MODE         PIC X(03).                          
            05  LB57-NUM-UNIT-OFFER         PIC 9(04).                          
            05  LB57-NUM-APPLN-RECV         PIC 9(05).                          
            05  LB57-NUM-APPLN-RECV-FTS     PIC 9(05).                          
            05  LB57-NUM-APPLN-RECV-TOT     PIC 9(05).                          
            05  LB57-NUM-APPLN-CAN          PIC 9(05).                          
            05  LB57-NUM-APPLN-RECV-AF-CL   PIC 9(05).                          
            05  LB57-NUM-APPLN-RAND-NO-TOT  PIC 9(05).                          
            05  LB57-NUM-RAND-NO-ASSIGN     PIC 9(05).                          
            05  LB57-NME-NEW-TOWN           PIC X(20).                          
            05  LB57-NUM-GEN3-TAG           PIC X(01).                          
            05  LB57-TOT-APPLN-RAND-NO-TOT  PIC 9(06).                          
            05  LB57-TOT-RAND-NO-ASSIGN     PIC 9(06).                          
            05  FILLER                      PIC X(114).                         
                                                                                
       FD   BP13LB58                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   BP13LB58-REC                    PIC X(132).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-TIME.                                                         
               10  WS-TIME-HH            PIC 9(02) VALUE 0.                     
               10  WS-TIME-MM            PIC 9(02) VALUE 0.                     
               10  WS-TIME-SS            PIC 9(02) VALUE 0.                     
           05  WS-LB57-READ              PIC 9(06) VALUE 0.                     
           05  WS-LB58-WRITE             PIC 9(06) VALUE 0.                     
           05  WS-CNT-PAGE               PIC 9(04) VALUE 0.                     
           05  WS-LB57-EOF               PIC X(01) VALUE SPACES.                
               88  LB57-EOF                        VALUE 'Y'.                   
           05  WS-LB58-HDR               PIC X(01) VALUE 'N'.                   
           05  WS-NUM-FT                 PIC X(02) VALUE SPACES.                
           05  WS-FLAT-TYPE              PIC X(12) VALUE SPACES.                
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-YY                     PIC X(02).                             
           05  WS-MM                     PIC X(02).                             
           05  WS-DD                     PIC X(02).                             
                                                                                
       01  WS-DTE-TODATE.                                                       
           05  WS-DD                     PIC X(02).                             
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-MM                     PIC X(02).                             
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-YY                     PIC X(02).                             
                                                                                
      ******************* REPORT LAYOUT *****************                       
                                                                                
       01  LB58-HEAD1.                                                          
           05  FILLER                    PIC X(08) VALUE 'BP13LB58'.            
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'HDBCAT3'.             
           05  FILLER                    PIC X(19) VALUE SPACES.                
           05  FILLER                    PIC X(42) VALUE                        
               'S Y S T E M     O F    C O M M I T M E N T'.                    
           05  FILLER                    PIC X(10) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'DATE : '.             
           05  LB58-DTE-CURRENT          PIC X(08).                             
           05  FILLER                    PIC X(02) VALUE ' ('.                  
           05  LB58-TME-HH               PIC 9(02).                             
           05  FILLER                    PIC X(01) VALUE ':'.                   
           05  LB58-TME-MM               PIC 9(02).                             
           05  FILLER                    PIC X(02) VALUE ') '.                  
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'PAGE : '.             
           05  LB58-CNT-PAGE             PIC ZZZ9.                              
                                                                                
       01  LB58-HEAD2.                                                          
           05  FILLER                    PIC X(39) VALUE SPACES.                
           05  FILLER                    PIC X(42) VALUE                        
               'PRE-BALLOT REPORTS BY ESTATE AND FLAT TYPE'.                    
                                                                                
       01  LB58-HEAD3.                                                          
           05  LB58-NME-NEW-TOWN         PIC X(20) VALUE SPACES.                
                                                                                
       01  LB58-HEAD4.                                                          
           05  FILLER                    PIC X(117) VALUE SPACES.               
           05  FILLER                    PIC X(02) VALUE '| '.                  
           05  LB58-NME-FT               PIC X(12) VALUE SPACES.                
                                                                                
       01  LB58-HEAD5.                                                          
           05  FILLER                    PIC X(117) VALUE SPACES.               
           05  FILLER                    PIC X(07) VALUE '|     ('.             
           05  LB58-ZONE                 PIC X(03) VALUE SPACES.                
           05  FILLER                    PIC X(01) VALUE ')'.                   
                                                                                
       01  LB58-HEAD6.                                                          
           05  FILLER                    PIC X(117) VALUE ALL '-'.              
           05  FILLER                    PIC X(01) VALUE '|'.                   
           05  FILLER                    PIC X(14) VALUE ALL '-'.               
                                                                                
       01  LB58-LINES.                                                          
           05  FILLER                    PIC X(132) VALUE ALL '='.              
                                                                                
       01  LB58-BLANK.                                                          
           05  FILLER                    PIC X(132) VALUE SPACES.               
                                                                                
       01  LB58-DETAIL.                                                         
           05  FILLER                    PIC X(02) VALUE ' ('.                  
           05  LB58-SEQ                  PIC X(01) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE ') '.                  
           05  LB58-DESC                 PIC X(112) VALUE SPACES.               
           05  FILLER                    PIC X(01) VALUE '|'.                   
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  LB58-QTY                  PIC ZZZZ9.                             
                                                                                
       01  LB58-DETAIL-TOT.                                                     
           05  FILLER                    PIC X(02) VALUE ' ('.                  
           05  LB58-SEQ-TOT              PIC X(01) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE ') '.                  
           05  LB58-DESC-TOT             PIC X(112) VALUE SPACES.               
           05  FILLER                    PIC X(01) VALUE '|'.                   
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  LB58-QTY-TOT              PIC ZZZZZ9.                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ********************                                                      
       0000-MAIN-ROUTINE.                                                       
      ********************                                                      
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13LB57       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECS        THRU 3000-EXIT                      
             UNTIL LB57-EOF.                                                    
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1000-OPEN-FILES.                                                         
      ******************                                                        
                                                                                
           OPEN INPUT  BP13LB57                                                 
                OUTPUT BP13LB58.                                                
                                                                                
           ACCEPT WS-DTE-CURRENT            FROM DATE.                          
           MOVE CORR WS-DTE-CURRENT         TO WS-DTE-TODATE.                   
           MOVE WS-DTE-TODATE               TO LB58-DTE-CURRENT.                
                                                                                
           ACCEPT WS-TIME                   FROM TIME.                          
           MOVE WS-TIME-HH                  TO LB58-TME-HH.                     
           MOVE WS-TIME-MM                  TO LB58-TME-MM.                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       2000-READ-BP13LB57.                                                      
      *********************                                                     
                                                                                
           READ BP13LB57                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-LB57-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-LB57-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       3000-PROCESS-RECS.                                                       
      ********************                                                      
                                                                                
           IF LB57-NUM-FLAT-TYPE = '99'                                         
              PERFORM 4000-WRITE-GRAND-TOT  THRU 4000-EXIT                      
              PERFORM 2000-READ-BP13LB57    THRU 2000-EXIT                      
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF WS-LB58-HDR = 'N'                                                 
              MOVE 'Y'                      TO WS-LB58-HDR                      
              ADD 1                         TO WS-CNT-PAGE                      
              MOVE WS-CNT-PAGE              TO LB58-CNT-PAGE                    
              WRITE BP13LB58-REC            FROM LB58-HEAD1 AFTER PAGE          
              WRITE BP13LB58-REC            FROM LB58-HEAD2                     
           END-IF.                                                              
                                                                                
           ADD 1                            TO WS-LB58-WRITE.                   
                                                                                
           WRITE BP13LB58-REC               FROM LB58-BLANK.                    
           WRITE BP13LB58-REC               FROM LB58-BLANK.                    
                                                                                
           MOVE LB57-NME-NEW-TOWN           TO LB58-NME-NEW-TOWN.               
           WRITE BP13LB58-REC               FROM LB58-HEAD3.                    
           WRITE BP13LB58-REC               FROM LB58-LINES.                    
                                                                                
           PERFORM 3100-DECODE-FT           THRU 3100-EXIT.                     
           MOVE WS-FLAT-TYPE                TO LB58-NME-FT.                     
           WRITE BP13LB58-REC               FROM LB58-HEAD4.                    
                                                                                
           MOVE LB57-NUM-ZONE               TO LB58-ZONE.                       
           WRITE BP13LB58-REC               FROM LB58-HEAD5.                    
                                                                                
           WRITE BP13LB58-REC               FROM LB58-HEAD6.                    
           MOVE 'A'                         TO LB58-SEQ.                        
           MOVE SPACES                      TO LB58-DESC.                       
           MOVE 'NO. OF FLATS OFFERED'      TO LB58-DESC.                       
           MOVE LB57-NUM-UNIT-OFFER         TO LB58-QTY.                        
           WRITE BP13LB58-REC               FROM LB58-DETAIL.                   
                                                                                
           WRITE BP13LB58-REC               FROM LB58-HEAD6.                    
           MOVE 'B'                         TO LB58-SEQ.                        
           MOVE SPACES                      TO LB58-DESC.                       
           MOVE 'NO. OF APPLICATIONS RECEIVED AS AT CLOSING DATE (AS SHO        
      -         'WN IN HDB INFOWEB)'        TO LB58-DESC.                       
           MOVE LB57-NUM-APPLN-RECV         TO LB58-QTY.                        
           WRITE BP13LB58-REC               FROM LB58-DETAIL.                   
                                                                                
           IF LB57-NUM-FLAT-TYPE = '2F' OR 'RF'                                 
              WRITE BP13LB58-REC            FROM LB58-HEAD6                     
              MOVE 'C'                      TO LB58-SEQ                         
              MOVE SPACES                   TO LB58-DESC                        
              MOVE 'NO. OF SECOND-TIMER APPLICATIONS RECEIVED UNDER FIRS        
      -            'T-TIMER SINGLES & NON-ELDERLY NOT REFLECTED IN HDB I        
      -            'NFOWEB'                 TO LB58-DESC                        
              MOVE LB57-NUM-APPLN-RECV-FTS  TO LB58-QTY                         
              WRITE BP13LB58-REC            FROM LB58-DETAIL                    
                                                                                
              WRITE BP13LB58-REC            FROM LB58-HEAD6                     
              MOVE 'D'                      TO LB58-SEQ                         
              MOVE SPACES                   TO LB58-DESC                        
              MOVE 'TOTAL NUMBER OF APPLICATIONS RECEIVED (B + C)'              
                                            TO LB58-DESC                        
              MOVE LB57-NUM-APPLN-RECV-TOT  TO LB58-QTY                         
              WRITE BP13LB58-REC            FROM LB58-DETAIL                    
           END-IF.                                                              
                                                                                
           WRITE BP13LB58-REC               FROM LB58-HEAD6.                    
           IF LB57-NUM-FLAT-TYPE = '2F' OR 'RF'                                 
              MOVE 'E'                      TO LB58-SEQ                         
           ELSE                                                                 
              MOVE 'C'                      TO LB58-SEQ                         
           END-IF.                                                              
           MOVE SPACES                      TO LB58-DESC.                       
           MOVE 'NO. OF CANCELLED APPLICATIONS NOT INCLUDED IN THE BALLO        
      -         'T'                         TO LB58-DESC.                       
           MOVE LB57-NUM-APPLN-CAN          TO LB58-QTY.                        
           WRITE BP13LB58-REC               FROM LB58-DETAIL.                   
                                                                                
           WRITE BP13LB58-REC               FROM LB58-HEAD6.                    
           IF LB57-NUM-FLAT-TYPE = '2F' OR 'RF'                                 
              MOVE 'F'                      TO LB58-SEQ                         
           ELSE                                                                 
              MOVE 'D'                      TO LB58-SEQ                         
           END-IF.                                                              
           MOVE SPACES                      TO LB58-DESC.                       
           MOVE 'APPLICATIONS RECEIVED AFTER CLOSING PERIOD'                    
                                            TO LB58-DESC.                       
           MOVE LB57-NUM-APPLN-RECV-AF-CL   TO LB58-QTY.                        
           WRITE BP13LB58-REC               FROM LB58-DETAIL.                   
                                                                                
           WRITE BP13LB58-REC               FROM LB58-HEAD6.                    
           MOVE SPACES                      TO LB58-DESC.                       
           IF LB57-NUM-FLAT-TYPE = '2F' OR 'RF'                                 
              MOVE 'G'                      TO LB58-SEQ                         
              MOVE 'TOTAL NUMBER OF APPLICATIONS TO BE ASSIGNED RANDOM N        
      -         'UMBER (D - E + F)'         TO LB58-DESC                        
           ELSE                                                                 
              MOVE 'E'                      TO LB58-SEQ                         
              MOVE 'TOTAL NUMBER OF APPLICATIONS TO BE ASSIGNED RANDOM N        
      -         'UMBER (B - C + D)'         TO LB58-DESC                        
           END-IF.                                                              
           MOVE LB57-NUM-APPLN-RAND-NO-TOT  TO LB58-QTY.                        
           WRITE BP13LB58-REC               FROM LB58-DETAIL.                   
                                                                                
           WRITE BP13LB58-REC               FROM LB58-HEAD6.                    
           IF LB57-NUM-FLAT-TYPE = '2F' OR 'RF'                                 
              MOVE 'H'                      TO LB58-SEQ                         
           ELSE                                                                 
              MOVE 'F'                      TO LB58-SEQ                         
           END-IF.                                                              
           MOVE SPACES                      TO LB58-DESC.                       
           MOVE 'NO. OF RANDOM NUMBERS TO BE ASSIGNED'                          
                                            TO LB58-DESC.                       
           MOVE LB57-NUM-RAND-NO-ASSIGN     TO LB58-QTY.                        
           WRITE BP13LB58-REC               FROM LB58-DETAIL.                   
                                                                                
           WRITE BP13LB58-REC               FROM LB58-LINES.                    
                                                                                
           PERFORM 2000-READ-BP13LB57       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************                                                         
       3100-DECODE-FT.                                                          
      *****************                                                         
                                                                                
           MOVE LB57-NUM-FLAT-TYPE          TO WS-NUM-FT.                       
           MOVE SPACES                      TO WS-FLAT-TYPE.                    
                                                                                
           EVALUATE WS-NUM-FT                                                   
           WHEN '1A'                                                            
           WHEN '2A'                                                            
                MOVE '     SA     '         TO WS-FLAT-TYPE                     
                                                                                
           WHEN '2 '                                                            
                MOVE '   2-ROOM   '         TO WS-FLAT-TYPE                     
                                                                                
           WHEN '2F'                                                            
                MOVE '2-ROOM FLEXI'         TO WS-FLAT-TYPE                     
                                                                                
           WHEN '3 '                                                            
                MOVE '   3-ROOM   '         TO WS-FLAT-TYPE                     
                                                                                
           WHEN '3P'                                                            
                MOVE '   3P-ROOM  '         TO WS-FLAT-TYPE                     
                                                                                
           WHEN '4 '                                                            
                MOVE '   4-ROOM   '         TO WS-FLAT-TYPE                     
                                                                                
           WHEN '5 '                                                            
                IF LB57-NUM-GEN3-TAG = 'Y'                                      
                   MOVE ' 5-ROOM/3GEN'      TO WS-FLAT-TYPE                     
                ELSE                                                            
                   MOVE '   5-ROOM   '      TO WS-FLAT-TYPE                     
                END-IF                                                          
                                                                                
           WHEN '6 '                                                            
                MOVE '  EXECUTIVE '         TO WS-FLAT-TYPE                     
                                                                                
           WHEN 'RF'                                                            
                MOVE '   RF-ROOM  '         TO WS-FLAT-TYPE                     
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'FLAT TYPE NOT FOUND : ' WS-NUM-FT                      
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       4000-WRITE-GRAND-TOT.                                                    
      ***********************                                                   
                                                                                
           ADD 1                            TO WS-LB58-WRITE.                   
                                                                                
           WRITE BP13LB58-REC               FROM LB58-BLANK.                    
           WRITE BP13LB58-REC               FROM LB58-BLANK.                    
                                                                                
           MOVE LB57-NME-NEW-TOWN           TO LB58-NME-NEW-TOWN.               
           WRITE BP13LB58-REC               FROM LB58-HEAD3.                    
           WRITE BP13LB58-REC               FROM LB58-LINES.                    
                                                                                
           MOVE 'A'                         TO LB58-SEQ-TOT.                    
           MOVE SPACES                      TO LB58-DESC-TOT.                   
           MOVE 'TOTAL NUMBER OF APPLICATIONS TO BE ASSIGNED RANDOM NUMB        
      -         'ER'                        TO LB58-DESC-TOT.                   
           MOVE LB57-TOT-APPLN-RAND-NO-TOT  TO LB58-QTY-TOT.                    
           WRITE BP13LB58-REC               FROM LB58-DETAIL-TOT.               
                                                                                
           WRITE BP13LB58-REC               FROM LB58-HEAD6.                    
           MOVE 'B'                         TO LB58-SEQ-TOT.                    
           MOVE SPACES                      TO LB58-DESC-TOT.                   
           MOVE 'NO. OF RANDOM NUMBERS TO BE ASSIGNED'                          
                                            TO LB58-DESC-TOT.                   
           MOVE LB57-TOT-RAND-NO-ASSIGN     TO LB58-QTY-TOT.                    
           WRITE BP13LB58-REC               FROM LB58-DETAIL-TOT.               
           WRITE BP13LB58-REC               FROM LB58-LINES.                    
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       9000-CLOSE-FILES.                                                        
      *******************                                                       
                                                                                
           DISPLAY '**************** BP13CB58 ****************'.                
           DISPLAY ' '.                                                         
           DISPLAY 'NO. OF RECORDS READ FROM BP13LB57 : ' WS-LB57-READ.         
           DISPLAY 'NO. OF RECORDS WRITTEN TO REPORT  : ' WS-LB58-WRITE.        
           DISPLAY ' '.                                                         
           DISPLAY '******************************************'.                
                                                                                
           CLOSE BP13LB57                                                       
                 BP13LB58.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
