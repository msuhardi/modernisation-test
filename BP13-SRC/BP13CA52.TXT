      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13CA52.                                                 
       AUTHOR.        KSJ3.                                                     
       DATE-WRITTEN.  27/02/2017.                                               
      *===============================================================*         
      *   OBJECTIVE:  TO MATCH F130 AND K410 BY REGN                  *         
      *===============================================================*         
      *     INPUT FILE :                                              *         
      *                  1. BP13F130                                  *         
      *                  2. BP13K410                                  *         
      *     OUTPUT FILE:                                              *         
      *                  1. BP13F13A                                  *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP135888 27/02/2017 KSJ3 NEW PROGRAM.                         *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K410  ASSIGN        TO BP13K410                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K410-NUM-REGN                      
                            FILE STATUS   IS WS-K410-STATUS.                    
                                                                                
           SELECT BP13F130  ASSIGN        TO BP13F130.                          
                                                                                
           SELECT BP13F13A  ASSIGN        TO BP13F13A.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13K410                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13K410.                                                       
                                                                                
       FD  BP13F130                                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
           COPY BP13F130.                                                       
                                                                                
       FD  BP13F13A                                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  BP13F13A-REC                  PIC X(150).                            
                                                                                
                                                                                
      *===============================================================*         
       WORKING-STORAGE SECTION.                                                 
      *===============================================================*         
                                                                                
       01  WS-DATE-VARS.                                                        
           05  WS-CUR-TIME             PIC X(07) VALUE ZEROES.                  
           05  WS-CUR-DATE.                                                     
               10  WS-CCYY             PIC 9(04).                               
               10  WS-MM               PIC 9(02).                               
               10  WS-DD               PIC 9(02).                               
           05  WS-SYS-DATE.                                                     
               10  WS-SYS-CC           PIC 9(02).                               
               10  WS-SYS-YY           PIC 9(02).                               
               10  WS-SYS-MM           PIC 9(02).                               
               10  WS-SYS-DD           PIC 9(02).                               
           05  WS-EDIT-DATE.                                                    
               10  WS-EDIT-DD          PIC 9(02).                               
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-EDIT-MM          PIC 9(02).                               
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-EDIT-YY          PIC 9(02).                               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F130-EOF             PIC X     VALUE SPACE.                   
           05  WS-K410-FND             PIC X     VALUE SPACE.                   
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K410-STATUS          PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F130-READ            PIC 9(08) VALUE ZEROES.                  
           05  WS-F130-SKIP            PIC 9(08) VALUE ZEROES.                  
           05  WS-F13A-WRITE           PIC 9(08) VALUE ZEROES.                  
           05  WS-K410-READ            PIC 9(08) VALUE ZEROES.                  
                                                                                
                                                                                
      *===============================================================*         
       PROCEDURE DIVISION.                                                      
      *===============================================================*         
                                                                                
      *---------------------------------------------------------------*         
       000-MAIN.                                                                
      *---------------------------------------------------------------*         
                                                                                
           PERFORM 100-OPEN-FILES          THRU 100-EXIT.                       
           PERFORM 200-BP13F130-READ       THRU 200-EXIT.                       
           PERFORM 300-PROCESS-REC         THRU 300-EXIT                        
                   UNTIL WS-F130-EOF = 'Y'.                                     
           PERFORM 900-CLOSE-ROUTINE       THRU 900-EXIT.                       
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       100-OPEN-FILES.                                                          
      *---------------------------------------------------------------*         
                                                                                
           OPEN INPUT  BP13F130                                                 
                       BP13K410                                                 
               OUTPUT  BP13F13A.                                                
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR WS-K410-STATUS IS ' WS-K410-STATUS         
              MOVE     WS-K410-STATUS      TO   RETURN-CODE                     
              PERFORM  900-CLOSE-ROUTINE   THRU 900-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-SYS-DATE.                    
                                                                                
           MOVE WS-SYS-YY                  TO   WS-EDIT-YY.                     
           MOVE WS-SYS-MM                  TO   WS-EDIT-MM.                     
           MOVE WS-SYS-DD                  TO   WS-EDIT-DD.                     
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       200-BP13F130-READ.                                                       
      *---------------------------------------------------------------*         
                                                                                
           READ BP13F130 AT END                                                 
                MOVE 'Y'                   TO   WS-F130-EOF                     
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1                           TO   WS-F130-READ.                   
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       300-PROCESS-REC.                                                         
      *---------------------------------------------------------------*         
                                                                                
           MOVE SPACES                     TO   BP13K410-REC.                   
           MOVE F130-NUM-REGN              TO   K410-NUM-REGN.                  
                                                                                
           PERFORM 310-BP13K410-READ       THRU 310-EXIT.                       
                                                                                
           IF WS-K410-FND = 'Y'                                                 
              MOVE K410-DTE-SO-EXECUTION   TO   F130-DTE-PP-DUE                 
              MOVE K410-NUM-SCH-ACC        TO   F130-NUM-SCH-ACCT               
              MOVE BP13F130-REC            TO   BP13F13A-REC                    
              WRITE BP13F13A-REC                                                
                                                                                
              ADD 1                        TO   WS-F13A-WRITE                   
           ELSE                                                                 
              ADD 1                        TO   WS-F130-SKIP                    
           END-IF.                                                              
                                                                                
           PERFORM 200-BP13F130-READ       THRU 200-EXIT.                       
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       310-BP13K410-READ.                                                       
      *---------------------------------------------------------------*         
                                                                                
           READ BP13K410 KEY IS K410-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K410-STATUS                                              
           WHEN 00                                                              
                MOVE 'Y'                   TO   WS-K410-FND                     
                                                                                
           WHEN 23                                                              
                MOVE 'N'                   TO   WS-K410-FND                     
                DISPLAY 'BP13K410 RECORD NOT FND, KEY: '                        
                         F130-NUM-REGN                                          
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READ BP13K410,STATUS ' WS-K410-STATUS            
                MOVE WS-K410-STATUS        TO   RETURN-CODE                     
                PERFORM 900-CLOSE-ROUTINE  THRU 900-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       900-CLOSE-ROUTINE.                                                       
      *---------------------------------------------------------------*         
                                                                                
           DISPLAY 'PROGRAM-ID : BP13CA52 '.                                    
           DISPLAY '**********************'.                                    
           DISPLAY ' '.                                                         
           DISPLAY 'CURR-DATE  : ' WS-SYS-DATE.                                 
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECS READ FROM  BP13F130  : ' WS-F130-READ.           
           DISPLAY 'NO OF RECS FOUND IN   BP13K410  : ' WS-K410-READ.           
           DISPLAY 'NO OF RECS WRITE TO   BP13F13A  : ' WS-F13A-WRITE.          
           DISPLAY 'NO OF RECS SKIPPED -  BP13F130  : ' WS-F130-SKIP.           
                                                                                
           CLOSE BP13F130                                                       
                 BP13K410                                                       
                 BP13F13A.                                                      
                                                                                
           IF WS-K410-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR WS-K410-STATUS IS ' WS-K410-STATUS         
              MOVE     WS-K410-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
