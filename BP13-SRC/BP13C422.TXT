      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
                                                                                
       PROGRAM-ID.    BP13C422.                                                 
       AUTHOR.        JOEL E PARRENO.                                           
       DATE-WRITTEN.  02/23/94.                                                 
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    SYSTEM NAME :   SYSTEM OF COMMITMENT  (SOC)                 *        
      *                                                                *        
      *    SYSTEM ID   :   BP13                                        *        
      *                                                                *        
      *    OBJECTIVE   :                                               *        
      *      1.   THIS IS A PROGRAM TO UPDATE ACCUM-SO FILE ON CASES   *        
      *           SENT FOR STAMPING.                                   *        
      *                                                                *        
      *    INPUT  FILE :                                               *        
      *      1.   BP13F315   (TRANSACTION FILE)                        *        
      *                                                                *        
      *    I-O    FILE :                                               *        
      *      1.   BP13K410   (ACCUM-SO FILE)                           *        
      *                                                                *        
      *    OUTPUT LIST :                                               *        
      *      1.   NIL                                                  *        
      *                                                                *        
      *                                                                *        
      *    CHGE-NO  BY    DATE   DESCRIPTION                           *        
      *    -------  ---  ------  -----------------------------------   *        
      *    BP130418 MPA  110698  Y2000 CHANGES.                        *        
      *    BP130418 RHB  060898  Y2K;FILE LENGTH                       *        
      *    BP130696 CLT  300799  CHANGE BP13F310 TO BP13F315           *        
      ******************************************************************        
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F315  ASSIGN       TO BP13F315.                           
                                                                                
           SELECT BP13K410  ASSIGN       TO BP13K410                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K410-KEY-FLD                        
                            FILE STATUS  IS WS-K410-STATUS.                     
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F315                                                            
            RECORDING MODE  IS   F                                              
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BP13F315.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-K410-STATUS                PIC 99    VALUE 00.                
           05  WS-EOF-F315                   PIC X     VALUE 'N'.               
               88  EOF-OF-F315                         VALUE 'Y'.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-F315-CNT              PIC 9(6)  VALUE 0.                 
           05  WS-READ-K410-CNT              PIC 9(6)  VALUE 0.                 
           05  WS-UPDT-K410-CNT              PIC 9(6)  VALUE 0.                 
                                                                                
       01  WS-OTHER-VARIABLES.                                                  
           05  WS-EDITED-READ-F315           PIC ZZZZZ9.                        
           05  WS-EDITED-READ-K410           PIC ZZZZZ9.                        
           05  WS-EDITED-UPDT-K410           PIC ZZZZZ9.                        
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
      ******************************************************************        
      *    MAIN LOGIC PROCESS SECTION                                  *        
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
                                                                                
           PERFORM  100-OPEN-ROUTINE          THRU   100-EXIT.                  
           PERFORM  200-READ-INPUT-BP13F315   THRU   200-EXIT.                  
           PERFORM  300-PROCESS-RECORD        THRU   300-EXIT                   
                        UNTIL   EOF-OF-F315.                                    
           PERFORM  600-DISPLAY-STATISTICS    THRU   600-EXIT.                  
           PERFORM  900-CLOSE-ROUTINE         THRU   900-EXIT.                  
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    OPENING FILES SECTION                                       *        
      ******************************************************************        
       100-OPEN-ROUTINE.                                                        
                                                                                
           OPEN INPUT   BP13F315                                                
                I-O     BP13K410.                                               
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K410, STATUS = ' WS-K410-STATUS        
              MOVE     WS-K410-STATUS    TO    RETURN-CODE                      
              PERFORM  900-CLOSE-ROUTINE THRU  900-EXIT                         
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *   READ TRANSACTION FILE (BP13F315) AT END MOVE 'Y' TO EOF-IND  *        
      ******************************************************************        
       200-READ-INPUT-BP13F315.                                                 
                                                                                
           READ BP13F315                                                        
                AT END                                                          
                   MOVE  'Y'         TO WS-EOF-F315                             
                   GO TO 200-EXIT.                                              
                                                                                
           ADD  1  TO WS-READ-F315-CNT.                                         
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    CHECK FOR F315-CDE-TRANS = 'ST' THEN READ ACCUM-SO FILE     *        
      ******************************************************************        
       300-PROCESS-RECORD.                                                      
                                                                                
           IF F315-CDE-TRANS = 'ST'                                             
              PERFORM  400-READ-BP13K410-RECORD   THRU   400-EXIT               
           END-IF.                                                              
                                                                                
           PERFORM     200-READ-INPUT-BP13F315    THRU   200-EXIT.              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    CHECK F315-REGN-NO IF EXISTING IN ACCUM-SO FILE             *        
      ******************************************************************        
       400-READ-BP13K410-RECORD.                                                
                                                                                
           MOVE F315-REGN-NO TO K410-KEY-FLD.                                   
                                                                                
           READ BP13K410.                                                       
                                                                                
           IF WS-K410-STATUS = 00                                               
              ADD     1                    TO    WS-READ-K410-CNT               
              PERFORM 500-UPDATE-BP13K410  THRU  500-EXIT                       
           ELSE                                                                 
           IF WS-K410-STATUS = 23                                               
              DISPLAY 'RECORD NOT FOUND IN K410, STAT = ' WS-K410-STATUS        
              DISPLAY '           K410-KEY-FLD        = ' K410-KEY-FLD          
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K410, STATUS = ' WS-K410-STATUS        
              DISPLAY '           K410-KEY-FLD        = ' K410-KEY-FLD          
              MOVE     WS-K410-STATUS      TO     RETURN-CODE                   
              PERFORM  900-CLOSE-ROUTINE   THRU   900-EXIT.                     
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    REWRITE SOC APPLICATION MASTER RECORD (BP13K410)            *        
      ******************************************************************        
       500-UPDATE-BP13K410.                                                     
                                                                                
           MOVE   FUNCTION CURRENT-DATE(1:8) TO   K410-DTE-UPDATE.              
           MOVE   'Y'               TO   K410-CDE-RELEASE-PAYMENT.              
                                                                                
           REWRITE BP13K410-REC.                                                
                                                                                
           IF WS-K410-STATUS =  00                                              
              ADD     1   TO WS-UPDT-K410-CNT                                   
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING BP13K410, STAT = ' WS-K410-STATUS         
              DISPLAY '               K410-KEY-FLD   = ' K410-KEY-FLD           
              MOVE     WS-K410-STATUS      TO     RETURN-CODE                   
              PERFORM  900-CLOSE-ROUTINE   THRU   900-EXIT.                     
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    DISPLAY NECESSARY STATISTICS                                *        
      ******************************************************************        
       600-DISPLAY-STATISTICS.                                                  
                                                                                
           MOVE  WS-READ-F315-CNT   TO   WS-EDITED-READ-F315.                   
           MOVE  WS-READ-K410-CNT   TO   WS-EDITED-READ-K410.                   
           MOVE  WS-UPDT-K410-CNT   TO   WS-EDITED-UPDT-K410.                   
                                                                                
           DISPLAY '***********************************************'.           
           DISPLAY ' PROGRAM-ID     :  BP13C422                    '.           
           DISPLAY '***********************************************'.           
           DISPLAY '                                               '.           
           DISPLAY ' NO. OF RECORDS READ  FROM BP13F315  =  '                   
                     WS-EDITED-READ-F315.                                       
           DISPLAY '                                               '.           
           DISPLAY ' NO. OF RECORDS READ  FROM BP13K410  =  '                   
                     WS-EDITED-READ-K410.                                       
           DISPLAY ' NO. OF RECORDS UPDATED TO BP13K410  =  '                   
                     WS-EDITED-UPDT-K410.                                       
           DISPLAY '***********************************************'.           
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    CLOSE ALL FILES   AND   STOP RUN                            *        
      ******************************************************************        
       900-CLOSE-ROUTINE.                                                       
                                                                                
           CLOSE   BP13F315                                                     
                   BP13K410.                                                    
                                                                                
           IF WS-K410-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13K410, STATUS = ' WS-K410-STATUS        
              MOVE     WS-K410-STATUS    TO    RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
********************  END OF SOURCE PROGRAM  ***************************        
