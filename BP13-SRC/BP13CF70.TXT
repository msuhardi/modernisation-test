       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF70.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  07/11/2012.                                               
      * ====================================================== *                
      * SYSTEM OF COMMITMENT  (BP13)                           *                
      * ====================================================== *                
      *                                                        *                
      *  OBJECTIVE  : TO HOUSEKEEP BP13KH25 AND WRITE LOGS TO  *                
      *               BP13KH27 (SIMILAR TO BP13CB82)           *                
      *                                                        *                
      *  INPUT      : BP13FH25 - RECORDS WITH DTE-DEBAR-EXPIRY *                
      *               SY02F001 - SY02.F001.DATE                *                
      *  I-O        : BP13KH25 - BP13.KH25.REJBOOK             *                
      *  OUTPUT     : BP13KH27 - BP13.KH27.REJBOOK.HIST        *                
      *                                                        *                
      * ====================================================== *                
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                  *                
      * --------  ----  --------  ---------------------------- *                
      * BP134688  SMR2  07112012  NEW PROGRAM                  *                
      * BP135820  FNP1  07012015  DO NOT ABEND IF KH27-STAT = 02/22*            
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FH25 ASSIGN TO BP13FH25.                                  
                                                                                
           SELECT BP13KH25 ASSIGN TO BP13KH25                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KH25-KEY-FLD                               
                  FILE STATUS     IS WS-KH25-STATUS.                            
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13KH27 ASSIGN TO BP13KH27                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KH27-KEY-FLD                               
                  FILE STATUS     IS WS-KH27-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13FH25                                                            
            RECORDING MODE   IS F                                               
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13FH25.                                                           
                                                                                
       FD   BP13KH25                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13KH25.                                                           
                                                                                
       FD   BP13KH27                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13KH27.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-KH25-STATUS               PIC X(02)  VALUE SPACES.            
           05  WS-KH27-STATUS               PIC X(02)  VALUE SPACES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-FH25-EOF                  PIC X(01)  VALUE 'N'.               
           05  WS-KH25-FLAG-FOUND           PIC X(01)  VALUE 'N'.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FH25-READ                 PIC 9(06)  VALUE ZEROES.            
           05  WS-KH25-FND                  PIC 9(06)  VALUE ZEROES.            
           05  WS-KH25-NOTFND               PIC 9(06)  VALUE ZEROES.            
           05  WS-KH27-WRITE                PIC 9(06)  VALUE ZEROES.            
           05  WS-KH25-DELETE               PIC 9(06)  VALUE ZEROES.            
           05  WS-KH25-BYPASS               PIC 9(06)  VALUE ZEROES.            
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13FH25       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-BP13FH25    THRU 3000-EXIT                      
             UNTIL WS-FH25-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT BP13FH25                                                  
                      SY02F001                                                  
                I-O   BP13KH25                                                  
                      BP13KH27.                                                 
                                                                                
           IF WS-KH25-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13KH25 OPEN ERROR, STATUS IS ' WS-KH25-STATUS          
              MOVE WS-KH25-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-KH27-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13KH27 OPEN ERROR, STATUS IS ' WS-KH27-STATUS          
              MOVE WS-KH27-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-BP13FH25.                                                      
      ****************************************************************          
                                                                                
           READ BP13FH25                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-FH25-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-FH25-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-BP13FH25.                                                   
      ****************************************************************          
                                                                                
           PERFORM 4000-READ-BP13KH25       THRU 4000-EXIT.                     
                                                                                
           IF WS-KH25-FLAG-FOUND = 'Y'                                          
              IF KH25-DTE-DEBAR NOT = SPACES AND LOW-VALUES                     
                 IF F001-DTE-CURRENT >= KH25-DTE-DEBAR-EXPIRY                   
                    PERFORM 5000-WRITE-BP13KH27  THRU 5000-EXIT                 
                    PERFORM 6000-DELETE-BP13KH25 THRU 6000-EXIT                 
                 ELSE                                                           
                    ADD 1                   TO WS-KH25-BYPASS                   
                 END-IF                                                         
              ELSE                                                              
                 ADD 1                      TO WS-KH25-BYPASS                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FH25       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4000-READ-BP13KH25.                                                      
      ****************************************************************          
                                                                                
           MOVE 'N'                         TO WS-KH25-FLAG-FOUND.              
           MOVE SPACES                      TO BP13KH25-REC.                    
           MOVE FH25-KEY-FLD                TO KH25-KEY-FLD.                    
                                                                                
           READ BP13KH25.                                                       
                                                                                
           EVALUATE WS-KH25-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-KH25-FND                      
              MOVE 'Y'                      TO WS-KH25-FLAG-FOUND               
                                                                                
           WHEN 23                                                              
              ADD 1                         TO WS-KH25-NOTFND                   
              MOVE 'N'                      TO WS-KH25-FLAG-FOUND               
              DISPLAY 'RECORD NOT FOUND - ' KH25-KEY-FLD                        
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13KH25. STATUS IS '                      
                      WS-KH25-STATUS                                            
              MOVE WS-KH25-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5000-WRITE-BP13KH27.                                                     
      ****************************************************************          
                                                                                
           WRITE BP13KH27-REC               FROM BP13KH25-REC.                  
                                                                                
           IF WS-KH27-STATUS =  ZEROES                                          
              CONTINUE                                                          
           ELSE                                                                 
           IF WS-KH27-STATUS = 02 OR 22                                         
              DISPLAY 'KH27-KEY-FLD: ' KH27-KEY-FLD                             
           ELSE                                                                 
              DISPLAY 'ERROR WRITTING BP13KH27. STATUS IS '                     
                      WS-KH27-STATUS                                            
              MOVE WS-KH27-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           ADD 1                            TO WS-KH27-WRITE.                   
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       6000-DELETE-BP13KH25.                                                    
      ****************************************************************          
                                                                                
           DELETE BP13KH25.                                                     
                                                                                
           IF WS-KH25-STATUS NOT = ZEROES                                       
              DISPLAY 'ERROR DELETING BP13KH25. STATUS IS '                     
                      WS-KH25-STATUS                                            
              MOVE WS-KH25-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           ADD 1                            TO WS-KH25-DELETE.                  
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9000-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*===========BP13CF70 CONTROL TOTALS============*'.          
           DISPLAY ' '.                                                         
           DISPLAY 'BP13FH25 RECORDS READ        = ' WS-FH25-READ.              
           DISPLAY 'NO OF REC FOUND BP13KH25     = ' WS-KH25-FND.               
           DISPLAY 'NO OF REC NOT FOUND BP13KH25 = ' WS-KH25-NOTFND.            
           DISPLAY 'NO OF REC WRITTEN BP13KH27   = ' WS-KH27-WRITE.             
           DISPLAY 'NO OF REC DELETED BP13KH25   = ' WS-KH25-DELETE.            
           DISPLAY 'NO OF REC BYPASSED BP13KH25  = ' WS-KH25-BYPASS.            
           DISPLAY ' '.                                                         
           DISPLAY '*==============================================*'.          
                                                                                
           CLOSE BP13FH25                                                       
                 SY02F001                                                       
                 BP13KH25                                                       
                 BP13KH27.                                                      
                                                                                
           IF WS-KH25-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13KH25 CLOSE ERROR, STATUS IS ' WS-KH25-STATUS         
              MOVE WS-KH25-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-KH27-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13KH27 CLOSE ERROR, STATUS IS ' WS-KH27-STATUS         
              MOVE WS-KH27-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
