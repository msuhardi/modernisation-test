       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC50.                                                 
      *AUTHOR.        SAUL SIO.                                                 
      *DATE-WRITTEN.  06/11/2006.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      * OBJECTIVE    :  PRINT LETTER ON DISBURSEMENT OF LOAN        *           
      *                 TOWARDS PROGRESS PAYMENT FOR DBSS FLAT.     *           
      *                                                             *           
      * INPUT FILES  :  1.  BP13FD38                                *           
      *                 4.  BP13K800 - SOC APPLICATION MASTER       *           
      *                 5.  BP13K820 - SOC OCCUPIER FILE            *           
      *                 6.  BP13K830 - SOC CORR ADDRESS FILE        *           
      *                                                             *           
      * OUTPUT       :  1.  P13PC50A - DISB LETTER - SUBSQ LOAN     *           
      *                 2.  P13PC50B - DISB LETTER - 1ST LOAN USING *           
      *                                              CASH           *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  OIC  DATE     DESCRIPTION                          *           
      * -------- ---  -------- ------------------------------------ *           
      * BP132936 SSS  20061106 NEW PROGRAM                          *           
      * BP132936 ED1  20070104 ADDED CONTROL REPORT BP13LC50        *           
      * BP132888 ED1  20070227 CHANGED MAXLINE,NAME & ADDR LENGTHS  *           
      * BP133033 ED1  20070424 ADDED PRINTING OF DISB LETTER FOR    *           
      *                        1ST LOAN USING CASH (P13PC50B)       *           
      * BP134245 JB8  20110611 TO PRINT PP-SCH                      *           
      * BP134507 IMC1 20120515 REALIGN FIELDS TO BP13D6 FORM        *           
      * BP134493 IMC1 20120523 REALIGN FIELDS TO BP13D1 FORM        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13FD38  ASSIGN       TO BP13FD38.                           
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS WS-K820-STATUS.                     
                                                                                
           SELECT BP13K830  ASSIGN       TO BP13K830                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K830-KEY-FLD                        
                            ALTERNATE RECORD KEY IS  K830-NUM-REGN              
                            FILE STATUS  IS WS-K830-STATUS.                     
                                                                                
           SELECT P13PC50A  ASSIGN       TO P13PC50A.                           
           SELECT P13PC50B  ASSIGN       TO P13PC50B.                           
           SELECT BP13LC50  ASSIGN       TO BP13LC50.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13FD38                                                            
            RECORDING MODE  IS    F                                             
            LABEL RECORDS   ARE   STANDARD                                      
            BLOCK  CONTAINS    0  RECORDS                                       
            RECORD CONTAINS  400  CHARACTERS.                                   
       COPY BP13FD38.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   P13PC50A                                                            
            RECORD CONTAINS 90 CHARACTERS                                       
            RECORDING MODE  IS  F.                                              
       01   PC50A-PRTREC                 PIC X(90).                             
                                                                                
       FD   P13PC50B                                                            
            RECORD CONTAINS 90 CHARACTERS                                       
            RECORDING MODE  IS  F.                                              
       01   PC50B-PRTREC                 PIC X(90).                             
                                                                                
       FD   BP13LC50                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   LC50-PRTREC                 PIC X(132).                             
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-INDICATORS.                                                       
           05  WS-K800-STATUS           PIC 99     VALUE 00.                    
           05  WS-K820-STATUS           PIC 99     VALUE 00.                    
           05  WS-K830-STATUS           PIC 99     VALUE 00.                    
           05  WS-EOF-FD38              PIC X      VALUE 'N'.                   
                                                                                
       01  WS-COUNTERS-POINTERS.                                                
           05  WS-FD38-READ             PIC 9(6)   VALUE ZEROS.                 
           05  WS-K800-READ             PIC 9(6)   VALUE ZEROS.                 
           05  WS-PIDB-READ             PIC 9(6)   VALUE ZEROS.                 
           05  WS-LETTER-PRINTED        PIC 9(6)   VALUE ZEROS.                 
           05  WS-LC50-WRT-CTR          PIC 9(7)   VALUE ZEROES.                
           05  WS-SNO                   PIC 9(5)   VALUE ZEROES.                
           05  WS-PAGE-CTR              PIC 9(4)   VALUE ZEROES.                
           05  WS-LINE-CTR              PIC 9(2)   VALUE ZEROES.                
                                                                                
       01  WS-LETTER-TYPE               PIC X.                                  
       01  WS-DATE                      PIC X(8).                               
       01  WS-TEMP-DATE                 PIC X(8).                               
       01  WS-TEMP-DATE-R REDEFINES WS-TEMP-DATE.                               
           05 WS-CCYY                   PIC 9(4).                               
           05 WS-MM                     PIC 9(2).                               
           05 WS-DD                     PIC 9(2).                               
       01 WS-MONTH-NAME.                                                        
          05 WS-STRING PIC X(36) VALUE                                          
                'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                         
          05 WS-MONTH  REDEFINES WS-STRING OCCURS 12 TIMES PIC XXX.             
                                                                                
       01  WS-NAME                      PIC X(66)  VALUE SPACES.                
       01  WS-MAXLINE                   PIC 99     VALUE 57.                    
                                                                                
      *-------------------------------------------------------------            
      * FORMAT LETTER                                                           
      *-------------------------------------------------------------            
       01  PC50-LINE01A.                                                        
           05  FILLER               PIC X(72) VALUE SPACES.                     
           05  PC50-HDB-REF         PIC X(14) VALUE SPACES.                     
                                                                                
       01  PC50-LINE01.                                                         
           05  FILLER               PIC X(72) VALUE SPACES.                     
           05  PC50-OUR-REF         PIC X(8)  VALUE SPACES.                     
                                                                                
       01  PC50-LINE02.                                                         
           05  FILLER               PIC X(72) VALUE SPACES.                     
           05  PC50-DATE            PIC X(10) VALUE SPACES.                     
                                                                                
       01  PC50-LINE03.                                                         
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC50-NAME1           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PC50-LINE03B.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC50-NAME2           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PC50-LINE03C.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC50-NAME3           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PC50-LINE03D.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC50-NAME4           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PC50-LINE04.                                                         
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC50-BLK             PIC X(16) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE SPACES.                     
           05  PC50-UNIT            PIC X(11) VALUE SPACES.                     
                                                                                
       01  PC50-LINE04B.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC50-ADDR-L2         PIC X(65) VALUE SPACES.                     
                                                                                
       01  PC50-LINE04C.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC50-ADDR-L3         PIC X(20) VALUE SPACES.                     
                                                                                
       01  PC50-LINE05.                                                         
           05  FILLER               PIC X(10) VALUE SPACES.                     
           05  PC50-BAR-CODE        PIC X(09) VALUE SPACES.                     
                                                                                
       01  PC50-LINE06.                                                         
           05  FILLER               PIC X(11) VALUE SPACES.                     
           05  PC50-FLAT-ADDRESS    PIC X(60) VALUE SPACES.                     
                                                                                
       01  PC50-LINE07.                                                         
           05  FILLER               PIC X(43) VALUE SPACES.                     
           05  PC50-AMT-LOAN-NEW    PIC $$,$$$,$$9.99  VALUE ZEROS.             
           05  FILLER               PIC X(31) VALUE SPACES.                     
           05  PC50-NUM-PP-SCH      PIC X(02) VALUE ZEROS.                      
                                                                                
       01  PC50-LINE08.                                                         
           05  FILLER               PIC X(13) VALUE SPACES.                     
           05  PC50-DTE-DUE         PIC X(11) VALUE SPACES.                     
           05  FILLER               PIC X(48) VALUE SPACES.                     
           05  PC50-AMT-INSTAL-NEW-TOT  PIC $$$$,$$9.99 VALUE ZEROS.            
           05  FILLER               PIC X(12) VALUE SPACES.                     
                                                                                
       01  PC50-LINE08B.                                                        
           05  FILLER                PIC X(13) VALUE SPACES.                    
           05  PC50-DTE-DUE-B        PIC X(11) VALUE SPACES.                    
           05  FILLER                PIC X(22) VALUE SPACES.                    
           05  PC50-AMT-INSTAL-NEW-TOT-B  PIC $$$$,$$9.99 VALUE ZEROS.          
           05  FILLER                PIC X(18) VALUE SPACES.                    
           05  PC50-DTE-INSTAL-NEW-B PIC X(11) VALUE SPACES.                    
                                                                                
       01  PC50-LINE09.                                                         
           05  FILLER               PIC X(19) VALUE SPACES.                     
           05  PC50-DTE-INSTAL-NEW  PIC X(11) VALUE SPACES.                     
           05  FILLER               PIC X(56) VALUE SPACES.                     
                                                                                
                                                                                
      *-------------------------------------------------------------            
      * CONTROL REPORT                                                          
      *-------------------------------------------------------------            
       01  LC50-HEADER01.                                                       
           05  FILLER              PIC X(08)     VALUE 'BP13LC50'.              
           05  FILLER              PIC X(09)     VALUE '     HDB3'.             
           05  FILLER              PIC X(31)     VALUE SPACES.                  
           05  FILLER              PIC X(27)     VALUE                          
               'HOUSING & DEVELOPMENT BOARD'.                                   
           05  FILLER              PIC X(23)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LC50-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LC50-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
                                                                                
       01  LC50-HEADER02.                                                       
           05  FILLER              PIC X(40)     VALUE SPACES.                  
           05  FILLER              PIC X(49)     VALUE                          
               'REPORT ON DISBURSEMENT OF NEW LOAN FOR DBSS FLATS'.             
                                                                                
       01  LC50-HEADER03.                                                       
           05  FILLER              PIC X(85)    VALUE SPACES.                   
           05  FILLER              PIC X(03)    VALUE 'NEW'.                    
           05  FILLER              PIC X(18)    VALUE SPACES.                   
           05  FILLER              PIC X(11)    VALUE ' INSTALMENT'.            
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(11)    VALUE ' INSTALMENT'.            
           05  FILLER              PIC X(04)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE 'CASH ($)'.               
                                                                                
       01  LC50-HEADER04.                                                       
           05  FILLER              PIC X(03)     VALUE 'S/N'.                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REGN NO'.               
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'NAME/ADDRESS'.          
           05  FILLER              PIC X(58)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'LOAN($)'.               
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'DUE DATE'.              
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'TOTAL($)'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'EFF-DATE'.              
                                                                                
       01 LC50-DETAIL1.                                                         
          05 LC50-DTL-SERIAL      PIC ZZZZ9      VALUE ZEROES.                  
          05 FILLER               PIC X(01)      VALUE SPACES.                  
          05 LC50-REGNO           PIC X(08)      VALUE SPACES.                  
          05 FILLER               PIC X(01)      VALUE SPACES.                  
          05 LC50-NAME1           PIC X(66)      VALUE SPACES.                  
          05 FILLER               PIC X(01)      VALUE SPACES.                  
          05 LC50-LOAN-AMT        PIC Z,ZZZ,ZZ9.99 VALUE ZEROES.                
          05 FILLER               PIC X(02)      VALUE SPACES.                  
          05 LC50-DUE-DATE        PIC X(10)      VALUE SPACES.                  
          05 FILLER               PIC X(02)      VALUE SPACES.                  
          05 LC50-INSTAL-AMT      PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
          05 FILLER               PIC X(02)      VALUE SPACES.                  
          05 LC50-INSTAL-DATE     PIC X(10)      VALUE SPACES.                  
                                                                                
       01 LC50-DETAIL2.                                                         
          05 FILLER               PIC X(15)     VALUE SPACES.                   
          05 LC50-NAME2           PIC X(66)     VALUE SPACES.                   
                                                                                
       01 LC50-DETAIL3.                                                         
          05 FILLER               PIC X(15)     VALUE SPACES.                   
          05 LC50-NAME3           PIC X(66)     VALUE SPACES.                   
                                                                                
       01 LC50-DETAIL4.                                                         
          05 FILLER               PIC X(15)     VALUE SPACES.                   
          05 LC50-NAME4           PIC X(66)     VALUE SPACES.                   
                                                                                
       01 LC50-DETAIL5.                                                         
          05 FILLER               PIC X(15)     VALUE SPACES.                   
          05 LC50-FLAT-ADDR       PIC X(70)     VALUE SPACES.                   
                                                                                
       01  WS-BLANK-LINE          PIC X(132)     VALUE SPACES.                  
       01  WS-DASH-LINE           PIC X(132)     VALUE ALL '-'.                 
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR AV02C015                                                   
      *-------------------------------------------------------------            
        01  WS-LINK-AV02C015.                                                   
            05  AV02C015-IN-POSTCODE          PIC X(6).                         
            05  AV02C015-OUT-POSTCODE-CHKSUM  PIC X(9).                         
                                                                                
       COPY AV02COMM.                                                           
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR BP13C913                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH            PIC X(04).                             
           05 WS-LINK-NUM-ACC            PIC X(04).                             
           05 WS-LINK-NUM-CHK            PIC X(01).                             
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-FILES-ROUTINE   THRU  1000-EXIT.                  
            PERFORM 2000-READ-BP13FD38        THRU  2000-EXIT.                  
            PERFORM 3000-PROCESS-RECORD       THRU  3000-EXIT                   
                    UNTIL WS-EOF-FD38 =  'Y'.                                   
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES-ROUTINE.                                                 
      *-------------------------------------------------------------            
            OPEN INPUT  BP13FD38                                                
                        BP13K800                                                
                        BP13K820                                                
                        BP13K830                                                
                 OUTPUT P13PC50A                                                
                        P13PC50B                                                
                        BP13LC50.                                               
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K800-STATUS ' WS-K800-STATUS           
              MOVE     WS-K800-STATUS   TO  RETURN-CODE                         
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K820-STATUS ' WS-K820-STATUS           
              MOVE     WS-K820-STATUS TO RETURN-CODE                            
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K830-STATUS ' WS-K830-STATUS           
              MOVE     WS-K830-STATUS   TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)    TO WS-DATE.                       
           STRING  WS-DATE(7:2) '/'                                             
                   WS-DATE(5:2) '/'                                             
                   WS-DATE(1:4)                                                 
                   DELIMITED BY SIZE INTO PC50-DATE                             
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13FD38.                                                      
      *-------------------------------------------------------------            
           READ BP13FD38                                                        
                AT END MOVE 'Y' TO WS-EOF-FD38                                  
                GO TO  2000-EXIT.                                               
                                                                                
           ADD  1  TO   WS-FD38-READ.                                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
           PERFORM 3100-INITIALIZE-FIELDS THRU  3100-EXIT.                      
                                                                                
           IF FD38-NUM-1ST-LOAN  = 'Y'                                          
              IF ( FD38-AMT-INSTAL-NEW-CASH >  0  ) AND                         
                 ( FD38-AMT-INSTAL-NEW-CPF1 =  0  ) AND                         
                 ( FD38-AMT-INSTAL-NEW-CPF2 =  0  ) AND                         
                 ( FD38-AMT-INSTAL-NEW-CPF3 =  0  ) AND                         
                 ( FD38-AMT-INSTAL-NEW-CPF4 =  0  )                             
                                                                                
                MOVE 'B'  TO WS-LETTER-TYPE                                     
              END-IF                                                            
                                                                                
           ELSE                                                                 
                MOVE 'A'  TO WS-LETTER-TYPE                                     
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 4000-READ-BP13K800    THRU 4000-EXIT.                        
                                                                                
           IF WS-K800-STATUS    = 00  AND WS-LETTER-TYPE NOT = SPACE            
                                                                                
              PERFORM 5000-GET-NAMES     THRU 5000-EXIT                         
              PERFORM 6000-GET-ADDRESS   THRU 6000-EXIT                         
              PERFORM 7000-GET-FLAT-ADDR THRU 7000-EXIT                         
                                                                                
              IF WS-LETTER-TYPE = 'A'                                           
                 PERFORM 8000-PRINT-DISB-LETTER      THRU 8000-EXIT             
              END-IF                                                            
              IF WS-LETTER-TYPE = 'B'                                           
                 PERFORM 8100-PRINT-DISB-CASH-LETTER THRU 8100-EXIT             
              END-IF                                                            
                                                                                
              PERFORM 8200-MOVE-REPORT-DTLS THRU 8200-EXIT                      
              PERFORM 8300-PRINT-REPORT     THRU 8300-EXIT                      
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD38       THRU 2000-EXIT.                     
                                                                                
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3100-INITIALIZE-FIELDS.                                                  
      *-------------------------------------------------------------            
           MOVE SPACES TO  WS-LETTER-TYPE                                       
                           PC50-HDB-REF                                         
                           PC50-OUR-REF                                         
                           PC50-NAME1                                           
                           PC50-NAME2                                           
                           PC50-NAME3                                           
                           PC50-NAME4                                           
                           PC50-BLK                                             
                           PC50-UNIT                                            
                           PC50-ADDR-L2                                         
                           PC50-ADDR-L3                                         
                           PC50-BAR-CODE                                        
                           PC50-FLAT-ADDRESS                                    
                           PC50-DTE-DUE                                         
                           PC50-DTE-DUE-B                                       
                           PC50-DTE-INSTAL-NEW                                  
                           PC50-DTE-INSTAL-NEW-B.                               
           MOVE ZEROS  TO  PC50-AMT-LOAN-NEW                                    
                           PC50-NUM-PP-SCH                                      
                           PC50-AMT-INSTAL-NEW-TOT                              
                           PC50-AMT-INSTAL-NEW-TOT-B.                           
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       4000-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
           MOVE FD38-NUM-REGN TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
                ADD  1  TO   WS-K800-READ                                       
           WHEN 23                                                              
                DISPLAY 'BP13K800 REC NOTFND, KEY ' K800-NUM-REGN               
           WHEN OTHER                                                           
                DISPLAY 'BP13K800 READ ERR, STATUS ' WS-K800-STATUS             
                MOVE WS-K800-STATUS           TO RETURN-CODE                    
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       5000-GET-NAMES.                                                          
      *-------------------------------------------------------------            
           MOVE    K800-NUM-NRIC1          TO   K820-NUM-NRIC.                  
           PERFORM 5500-READ-BP13K820      THRU 5500-EXIT.                      
           MOVE    WS-NAME                 TO   PC50-NAME1.                     
                                                                                
           IF K800-NUM-NRIC2 NOT =  SPACES AND  LOW-VALUES                      
              MOVE     K800-NUM-NRIC2       TO  K820-NUM-NRIC                   
              PERFORM  5500-READ-BP13K820   THRU 5500-EXIT                      
              MOVE     WS-NAME              TO  PC50-NAME2                      
           ELSE                                                                 
              MOVE     SPACES               TO  PC50-NAME2                      
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 NOT =  SPACES AND  LOW-VALUES                      
              MOVE     K800-NUM-NRIC3       TO  K820-NUM-NRIC                   
              PERFORM  5500-READ-BP13K820   THRU 5500-EXIT                      
              MOVE     WS-NAME              TO  PC50-NAME3                      
           ELSE                                                                 
              MOVE     SPACES               TO  PC50-NAME3                      
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 NOT =  SPACES AND  LOW-VALUES                      
              MOVE     K800-NUM-NRIC4       TO  K820-NUM-NRIC                   
              PERFORM  5500-READ-BP13K820   THRU 5500-EXIT                      
              MOVE     WS-NAME              TO  PC50-NAME4                      
           ELSE                                                                 
              MOVE     SPACES               TO  PC50-NAME4                      
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       5500-READ-BP13K820.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES        TO WS-NAME.                                       
           MOVE K800-NUM-REGN TO K820-NUM-REGN.                                 
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              MOVE  K820-NME-OCCP           TO    WS-NAME                       
           ELSE                                                                 
           IF WS-K820-STATUS = 23                                               
              DISPLAY 'RECORD NOTFND IN BP13K820 = ' WS-K820-STATUS             
                      ', KEY-FLD = ' K820-KEY-FLD                               
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K820, STAT ' WS-K820-STATUS            
                      ', KEY-FLD = ' K820-KEY-FLD                               
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
       5500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       6000-GET-ADDRESS.                                                        
      *-------------------------------------------------------------            
           MOVE FD38-NUM-REGN     TO    K830-NUM-REGN.                          
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K830-STATUS                                              
           WHEN 00                                                              
              IF (K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR =  '#') AND                           
                 (K830-NUM-MAIL-LVL-NUM =  SPACES OR LOW-VALUES))               
                 MOVE K830-NUM-BLK         TO PC50-BLK                          
                 MOVE K830-NUM-FLAT        TO PC50-UNIT                         
                 MOVE K830-NME-STREET-BLDG TO PC50-ADDR-L2                      
                 IF K830-NUM-POSTAL  = SPACES OR LOW-VALUES OR ZEROES           
                    MOVE SPACES            TO PC50-ADDR-L3                      
                 ELSE                                                           
                    STRING 'SINGAPORE '  K830-NUM-POSTAL                        
                       DELIMITED BY SIZE    INTO PC50-ADDR-L3                   
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-POSTAL TO AV02C015-IN-POSTCODE                
                    PERFORM 6500-CALL-AV02C015     THRU 6500-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC50-BAR-CODE          
                 END-IF                                                         
              ELSE                                                              
                 MOVE K830-NUM-MAIL-BLK        TO PC50-BLK                      
                 MOVE K830-NUM-MAIL-FLAT       TO PC50-UNIT                     
                 MOVE K830-NME-MAIL-STR-BLDG   TO PC50-ADDR-L2                  
                 IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES OR ZEROS        
                    MOVE SPACES                TO PC50-ADDR-L3                  
                 ELSE                                                           
                    STRING 'SINGAPORE '      K830-NUM-MAIL-POSTAL               
                           DELIMITED BY SIZE INTO PC50-ADDR-L3                  
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-MAIL-POSTAL TO AV02C015-IN-POSTCODE           
                    PERFORM 6500-CALL-AV02C015     THRU 6500-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC50-BAR-CODE          
                 END-IF                                                         
              END-IF                                                            
           WHEN 23                                                              
              MOVE SPACES TO PC50-BLK                                           
                             PC50-UNIT                                          
                             PC50-ADDR-L2                                       
                             PC50-ADDR-L3                                       
              DISPLAY 'BP13K830 RECORD NOT FOUND = ' WS-K830-STATUS             
                      ', KEY ' K830-KEY-FLD                                     
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K830, STATUS ' WS-K830-STATUS          
                      ', KEY ' K830-KEY-FLD                                     
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6500-CALL-AV02C015.                                                      
      *-------------------------------------------------------------            
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       7000-GET-FLAT-ADDR.                                                      
      *-------------------------------------------------------------*           
           MOVE SPACES                TO BP13COMM8-REC.                         
           MOVE K800-NUM-SCH-ACC(1:9) TO WS-LINK-REC.                           
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
             WHEN 000                                                           
                ADD 1 TO WS-PIDB-READ                                           
                STRING 'BLK '  COMM8-NUM-BLK  '  '                              
                       '#'  COMM8-NUM-LEVEL   '-'                               
                       COMM8-NUM-UNIT-MAIN    '  ' DELIMITED BY SIZE            
                       COMM8-NME-STREET       DELIMITED BY '   '                
                       INTO PC50-FLAT-ADDRESS                                   
             WHEN 100                                                           
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                     
                         K800-NUM-SCH-ACC ', REGN : ' FD38-NUM-REGN             
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
                        ', AT KEY ' K800-NUM-SCH-ACC                            
                        ', REGN : ' FD38-NUM-REGN                               
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8000-PRINT-DISB-LETTER.                                                  
      *-------------------------------------------------------------            
           MOVE  FD38-NUM-REGN           TO PC50-OUR-REF.                       
                                                                                
           STRING FD38-NUM-SCH-ACC(1:4) '-'                                     
                  FD38-NUM-SCH-ACC(5:4) '-'                                     
                  FD38-NUM-SCH-ACC(9:1) '-'                                     
                  FD38-NUM-SCH-ACC(10:2)                                        
                  DELIMITED BY SIZE INTO PC50-HDB-REF.                          
                                                                                
           MOVE  FD38-AMT-LOAN-NEW       TO PC50-AMT-LOAN-NEW.                  
           MOVE  FD38-NUM-SCHEDULE       TO PC50-NUM-PP-SCH.                    
           MOVE  FD38-AMT-INSTAL-NEW-TOT TO PC50-AMT-INSTAL-NEW-TOT.            
                                                                                
           INITIALIZE WS-TEMP-DATE.                                             
           MOVE  FD38-DTE-DUE            TO WS-TEMP-DATE.                       
                                                                                
           STRING WS-DD ' ' WS-MONTH(WS-MM) ' ' WS-CCYY                         
                  DELIMITED BY SIZE INTO PC50-DTE-DUE.                          
                                                                                
           INITIALIZE WS-TEMP-DATE.                                             
           MOVE   FD38-DTE-INSTAL-NEW    TO WS-TEMP-DATE.                       
                                                                                
           STRING WS-DD ' ' WS-MONTH(WS-MM) ' ' WS-CCYY                         
                  DELIMITED BY SIZE INTO PC50-DTE-INSTAL-NEW.                   
                                                                                
           MOVE   SPACES   TO PC50-DTE-DUE-B.                                   
           MOVE   SPACES   TO PC50-DTE-INSTAL-NEW-B.                            
                                                                                
           MOVE SPACES TO PC50A-PRTREC.                                         
           WRITE PC50A-PRTREC       FROM PC50-LINE01A AFTER PAGE.               
           WRITE PC50A-PRTREC       FROM PC50-LINE01.                           
           WRITE PC50A-PRTREC       FROM PC50-LINE02.                           
           WRITE PC50A-PRTREC       FROM PC50-LINE03 AFTER 4.                   
           WRITE PC50A-PRTREC       FROM PC50-LINE03B.                          
           WRITE PC50A-PRTREC       FROM PC50-LINE03C.                          
           WRITE PC50A-PRTREC       FROM PC50-LINE03D.                          
           WRITE PC50A-PRTREC       FROM PC50-LINE04.                           
           WRITE PC50A-PRTREC       FROM PC50-LINE04B.                          
           WRITE PC50A-PRTREC       FROM PC50-LINE04C.                          
           WRITE PC50A-PRTREC       FROM PC50-LINE05 AFTER 2.                   
           WRITE PC50A-PRTREC       FROM PC50-LINE06 AFTER 6.                   
           WRITE PC50A-PRTREC       FROM PC50-LINE07 AFTER 3.                   
           WRITE PC50A-PRTREC       FROM PC50-LINE08.                           
           WRITE PC50A-PRTREC       FROM PC50-LINE09.                           
                                                                                
           ADD 1 TO WS-LETTER-PRINTED.                                          
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8100-PRINT-DISB-CASH-LETTER.                                             
      *-------------------------------------------------------------            
           MOVE  FD38-NUM-REGN           TO PC50-OUR-REF.                       
                                                                                
           STRING FD38-NUM-SCH-ACC(1:4) '-'                                     
                  FD38-NUM-SCH-ACC(5:4) '-'                                     
                  FD38-NUM-SCH-ACC(9:1) '-'                                     
                  FD38-NUM-SCH-ACC(10:2)                                        
                  DELIMITED BY SIZE INTO PC50-HDB-REF.                          
                                                                                
           MOVE  FD38-AMT-LOAN-NEW       TO PC50-AMT-LOAN-NEW.                  
           MOVE  FD38-NUM-SCHEDULE       TO PC50-NUM-PP-SCH.                    
           MOVE  FD38-AMT-INSTAL-NEW-TOT TO PC50-AMT-INSTAL-NEW-TOT-B.          
                                                                                
           INITIALIZE WS-TEMP-DATE.                                             
           MOVE  FD38-DTE-DUE            TO WS-TEMP-DATE.                       
                                                                                
           STRING WS-DD ' ' WS-MONTH(WS-MM) ' ' WS-CCYY                         
                  DELIMITED BY SIZE INTO PC50-DTE-DUE-B.                        
                                                                                
           INITIALIZE WS-TEMP-DATE.                                             
           MOVE   FD38-DTE-INSTAL-NEW    TO WS-TEMP-DATE.                       
                                                                                
           STRING WS-DD ' ' WS-MONTH(WS-MM) ' ' WS-CCYY                         
                  DELIMITED BY SIZE INTO PC50-DTE-INSTAL-NEW-B.                 
                                                                                
           MOVE   SPACES   TO PC50-DTE-DUE.                                     
           MOVE   SPACES   TO PC50-DTE-INSTAL-NEW.                              
                                                                                
           MOVE SPACES TO PC50B-PRTREC.                                         
           WRITE PC50B-PRTREC       FROM PC50-LINE01A AFTER PAGE.               
           WRITE PC50B-PRTREC       FROM PC50-LINE01.                           
           WRITE PC50B-PRTREC       FROM PC50-LINE02.                           
           WRITE PC50B-PRTREC       FROM PC50-LINE03 AFTER 4.                   
           WRITE PC50B-PRTREC       FROM PC50-LINE03B.                          
           WRITE PC50B-PRTREC       FROM PC50-LINE03C.                          
           WRITE PC50B-PRTREC       FROM PC50-LINE03D.                          
           WRITE PC50B-PRTREC       FROM PC50-LINE04.                           
           WRITE PC50B-PRTREC       FROM PC50-LINE04B.                          
           WRITE PC50B-PRTREC       FROM PC50-LINE04C.                          
           WRITE PC50B-PRTREC       FROM PC50-LINE05 AFTER 2.                   
           WRITE PC50B-PRTREC       FROM PC50-LINE06 AFTER 6.                   
           WRITE PC50B-PRTREC       FROM PC50-LINE07 AFTER 3.                   
           WRITE PC50B-PRTREC       FROM PC50-LINE08B.                          
                                                                                
           ADD 1 TO WS-LETTER-PRINTED.                                          
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8200-MOVE-REPORT-DTLS.                                                   
      *-------------------------------------------------------------            
           MOVE SPACES               TO  LC50-DETAIL1                           
                                         LC50-DETAIL2                           
                                         LC50-DETAIL3                           
                                         LC50-DETAIL4                           
                                         LC50-DETAIL5.                          
                                                                                
           STRING  WS-DATE(7:2) '/'                                             
                   WS-DATE(5:2) '/'                                             
                   WS-DATE(1:4)                                                 
                   DELIMITED BY SIZE INTO LC50-DATE-HDR                         
           END-STRING.                                                          
                                                                                
           MOVE  FD38-NUM-REGN           TO LC50-REGNO.                         
           MOVE  FD38-AMT-LOAN-NEW       TO LC50-LOAN-AMT.                      
           MOVE  FD38-AMT-INSTAL-NEW-TOT TO LC50-INSTAL-AMT.                    
           MOVE  PC50-NAME1              TO LC50-NAME1.                         
           MOVE  PC50-NAME2              TO LC50-NAME2.                         
           MOVE  PC50-NAME3              TO LC50-NAME3.                         
           MOVE  PC50-NAME4              TO LC50-NAME4.                         
                                                                                
           IF PC50-FLAT-ADDRESS NOT = SPACES                                    
              STRING 'BLK '  COMM8-NUM-BLK  ' '                                 
                     '#'  COMM8-NUM-LEVEL   '-'                                 
                     COMM8-NUM-UNIT-MAIN    ' ' DELIMITED BY SIZE               
                     COMM8-NME-STREET            DELIMITED BY '   '             
               ' S('  COMM8-NUM-POSTAL-CODE ')'   DELIMITED BY SIZE             
                     INTO LC50-FLAT-ADDR                                        
           END-IF.                                                              
                                                                                
           IF FD38-DTE-DUE NOT = SPACES AND LOW-VALUES AND ZEROES               
              STRING  FD38-DTE-DUE(7:2) '/'                                     
                      FD38-DTE-DUE(5:2) '/'                                     
                      FD38-DTE-DUE(1:4)                                         
              DELIMITED BY SIZE INTO LC50-DUE-DATE                              
           END-IF.                                                              
                                                                                
           IF FD38-DTE-INSTAL-NEW NOT = SPACES AND LOW-VALUES                   
                                               AND ZEROES                       
              STRING  FD38-DTE-INSTAL-NEW(7:2) '/'                              
                      FD38-DTE-INSTAL-NEW(5:2) '/'                              
                      FD38-DTE-INSTAL-NEW(1:4)                                  
              DELIMITED BY SIZE INTO LC50-INSTAL-DATE                           
           END-IF.                                                              
                                                                                
                                                                                
       8200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8300-PRINT-REPORT.                                                       
      *--------------------------------------------------------------           
           ADD   1                      TO  WS-SNO.                             
           MOVE  WS-SNO                 TO  LC50-DTL-SERIAL.                    
                                                                                
           IF WS-LINE-CTR > WS-MAXLINE OR                                       
              WS-SNO       = 1                                                  
              ADD            1   TO WS-PAGE-CTR                                 
              MOVE ZEROES        TO LC50-PAGE-HDR                               
              MOVE WS-PAGE-CTR   TO LC50-PAGE-HDR                               
              PERFORM 8400-PRINT-HEADING THRU 8400-EXIT                         
           END-IF.                                                              
                                                                                
           WRITE LC50-PRTREC FROM LC50-DETAIL1.                                 
           WRITE LC50-PRTREC FROM LC50-DETAIL2.                                 
           WRITE LC50-PRTREC FROM LC50-DETAIL3.                                 
           WRITE LC50-PRTREC FROM LC50-DETAIL4.                                 
           WRITE LC50-PRTREC FROM LC50-DETAIL5.                                 
           WRITE LC50-PRTREC FROM WS-BLANK-LINE.                                
                                                                                
           ADD   6              TO    WS-LINE-CTR.                              
           ADD   1              TO    WS-LC50-WRT-CTR.                          
                                                                                
       8300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       8400-PRINT-HEADING.                                                      
      *--------------------------------------------------------------           
                                                                                
            WRITE LC50-PRTREC FROM LC50-HEADER01 AFTER PAGE.                    
            WRITE LC50-PRTREC FROM LC50-HEADER02.                               
            WRITE LC50-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC50-PRTREC FROM LC50-HEADER03.                               
            WRITE LC50-PRTREC FROM LC50-HEADER04.                               
            WRITE LC50-PRTREC FROM WS-DASH-LINE.                                
                                                                                
            INITIALIZE WS-LINE-CTR.                                             
            MOVE  6 TO WS-LINE-CTR.                                             
                                                                                
       8400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY ' -- BP13CC50 CONTROL TOTAL -------'.                        
           DISPLAY ' '.                                                         
           DISPLAY ' TOTAL READ FROM FD38 : ' WS-FD38-READ.                     
           DISPLAY ' TOTAL READ FROM K800 : ' WS-K800-READ.                     
           DISPLAY ' TOTAL READ FROM PIDB : ' WS-PIDB-READ.                     
           DISPLAY ' TOTAL LETTER PRINTED : ' WS-LETTER-PRINTED.                
           DISPLAY ' TOTAL REPORT PRINTED : ' WS-LC50-WRT-CTR.                  
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13FD38                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 P13PC50A                                                       
                 P13PC50B                                                       
                 BP13LC50.                                                      
                                                                                
           IF WS-K800-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K800, STAT ' WS-K800-STATUS.           
                                                                                
           IF WS-K820-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K820, STAT ' WS-K820-STATUS.           
                                                                                
           IF WS-K830-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K830, STAT ' WS-K830-STATUS.           
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
