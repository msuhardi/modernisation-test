       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C429.                                                    
       AUTHOR. AJITH KUMAR T.                                                   
       DATE-WRITTEN. 26-05-1999.                                                
                                                                                
      *---------------------------------------------------------------*         
      * SOC (BP13)                                                    *         
      *---------------------------------------------------------------*         
      * PROGRAM   : BP13C429                                          *         
      * AUTHOR    : AKT                                               *         
      * OBJECTIVE : TO PRINT REPORT ON E-STAMPING BATCH NOT PROCESSED *         
      *             BY IRAS AFTER 7 DAYS.                             *         
      *                                                               *         
      *  FILES :                                                      *         
      *  INPUT  - BP13F625                                            *         
      *  OUTPUT - BP13L429                                            *         
      *                                                               *         
      *----------------------------------------------------------------         
      *                                                               *         
      * CHG-NO   DATE     BY   DESCRIPTION                            *         
      * -------- -------- ---  -----------                            *         
      * BP130696 19071999  AKT  PRINT EXCEPTION REPORT                *         
      * BP132185 02082002  MCC  PRINT EXCEPTION REPORT ONLY IF        *         
      *                         CONTRACT IS WITHIN 2 YRS FR CURR DATE *         
      * BP134779 13/06/13  PCL4 TO CONVERT THE REPORT TO SENDMAIL     *         
      * BP135537 23/02/15  RJB1 CHANGE THE EMAIL RECIPIENT FROM       *         
      *                         NICKNAME INTO COMPLETE NAME           *         
      * BP136477 18/10/16  RJB1 TO REPLACE KIM HUAT TO YEE MIEN       *         
      * BP136625 01/02/17  RJB1 REMOVE NORMALA BUANG, INCLUDE CHANDRA *         
      *                         ARUMUGAM AND MEENACHI                 *         
      * BP137987 17/11/17  PCL4 TO UPDATE EMAIL RECIPIENTS            *         
      * BP137131 26/12/17  PCL4 TO UPDATE EMAIL RECIPIENTS            *         
      * BP137155 08/01/18  PCL4 TO UPDATE EMAIL RECIPIENTS            *         
      * BP138202 10/03/20  KV5  REPLACE MAIL FROM MOHAMED ALI BEE     *         
      *                         KATHIJAH TO HENG SC SUZANNE(HDB)      *         
      * BP139030 07/01/22 ZAR7  REPLACE CHUA YEE MIEN TO LIM KOK CHUN           
      * BP139159 13/06/22 KAC1  ADDED oh_meng_huat@hdb.gov.sg         *         
      * BP139159 27/06/22 KAC1  REMOVE AFIQAH INCLUDE KATHIJAH        *         
      * BP139276 05/04/24 KAC1  REPLACE LIM KOK CHUN BY               *         
      *                                 KATHIJAH_BEE_ALI_MOHAMED      *         
      *----------------------------------------------------------------         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F625 ASSIGN TO BP13F625                                   
           ORGANIZATION IS SEQUENTIAL                                           
           FILE STATUS IS WS-F625-STATUS.                                       
                                                                                
           SELECT BP13L429 ASSIGN TO BP13L429.                                  
                                                                                
                                                                                
      *---------------------*                                                   
       DATA DIVISION.                                                           
      *---------------------*                                                   
       FILE SECTION.                                                            
                                                                                
       FD BP13F625                                                              
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BP13F625.                                                           
                                                                                
       FD BP13L429                                                              
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01  BP13L429-REC             PIC X(132).                                 
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-F625-STATUS         PIC X(02).                                
           05  WS-L429-STATUS         PIC X(02).                                
                                                                                
       01  WS-EOF-VARIABLES.                                                    
           05  WS-F625-EOF            PIC X VALUE 'N'.                          
           05  WS-PRT-FLAG            PIC X VALUE 'Y'.                          
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F625-READ           PIC 9(08) VALUE ZEROES.                   
           05  WS-L429-WRITE          PIC 9(08) VALUE ZEROES.                   
           05  WS-LINE-CTR            PIC 9(02) VALUE 99.                       
           05  WS-PAGE-CTR            PIC 9(04) VALUE ZEROES.                   
           05  WS-NUM-CASES           PIC 9(04) VALUE ZEROES.                   
           05  WS-SLNO                PIC 9(04) VALUE 1.                        
           05  WS-TOT-REC-PRINTED     PIC 9(04) VALUE ZEROES.                   
                                                                                
       01  WS-COMP-DATE               PIC S9(09).                               
                                                                                
       01  WS-CHK-DATE                PIC 9(08).                                
                                                                                
       01  WS-DATE                    PIC 9(08) VALUE ZEROES.                   
       01  WS-BATCH-SENT-DATE         PIC 9(08) VALUE ZEROES.                   
       01  WS-2YRS-DATE               PIC 9(08) VALUE ZEROES.                   
                                                                                
       01  WS-DATE1.                                                            
           05  WS-CCYY                PIC X(04).                                
           05  WS-MM                  PIC X(02).                                
           05  WS-DD                  PIC X(02).                                
                                                                                
       01  WS-DATE-SENT.                                                        
           05  WS-CCYY-S              PIC X(04).                                
           05  WS-MM-S                PIC X(02).                                
           05  WS-DD-S                PIC X(02).                                
                                                                                
       01 WS-CUR-DATE.                                                          
           05  WS-CUR-CCYY            PIC 9(04).                                
           05  WS-CUR-MM              PIC 9(02).                                
           05  WS-CUR-DD              PIC 9(02).                                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-BATCH           PIC X(13) VALUE SPACES.                   
           05  WS-NUM-VRCH            PIC X(11) VALUE SPACES.                   
                                                                                
       01  WS-EMAIL-RPT-VARIABLES.                                              
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(60)                               
               VALUE 'RCPT TO:<BALASUBRAMANIAM_ARUNASALAM@HDB.GOV.SG>'.         
           05  WS-MAIL-RPT4             PIC X(60) VALUE                         
              'RCPT TO:<NOORJAHAN_SHAIK_MOHAMED_GULEMKADER@HDB.GOV.SG>'.        
           05  WS-MAIL-RPT5             PIC X(60)                               
               VALUE 'RCPT TO:<MUHAMMED_SHARIFF_SAHROM@HDB.GOV.SG>'.            
           05  WS-MAIL-RPT6             PIC X(45)                               
               VALUE 'RCPT TO:<CHANDRA_LAGHA_ARUMUGAM@HDB.GOV.SG>'.             
           05  WS-MAIL-RPT7             PIC X(60)                               
               VALUE 'RCPT TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                    
           05  WS-MAIL-RPT7A            PIC X(60)                               
               VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.           
           05  WS-MAIL-RPT7B            PIC X(40)                               
               VALUE 'RCPT TO:<PHNG_BEE_LUAN@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT7C            PIC X(45)                               
               VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.           
           05  WS-MAIL-RPT7D            PIC X(40)                               
               VALUE 'RCPT TO:<oh_meng_huat@hdb.gov.sg>'.                       
           05  WS-MAIL-RPT8             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT9             PIC X(40)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT10            PIC X(60)                               
               VALUE 'TO:<BALASUBRAMANIAM_ARUNASALAM@HDB.GOV.SG>'.              
           05  WS-MAIL-RPT11            PIC X(60) VALUE                         
              'TO:<NOORJAHAN_SHAIK_MOHAMED_GULEMKADER@HDB.GOV.SG>'.             
           05  WS-MAIL-RPT12            PIC X(60)                               
               VALUE 'TO:<MUHAMMED_SHARIFF_SAHROM@HDB.GOV.SG>'.                 
           05  WS-MAIL-RPT13A           PIC X(40)                               
               VALUE 'TO:<CHANDRA_LAGHA_ARUMUGAM@HDB.GOV.SG>'.                  
           05  WS-MAIL-RPT13B           PIC X(60)                               
               VALUE 'TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                         
           05  WS-MAIL-RPT13C           PIC X(60)                               
               VALUE 'TO:<KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.                
           05  WS-MAIL-RPT13D           PIC X(40)                               
               VALUE 'TO:<PHNG_BEE_LUAN@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT13E           PIC X(40)                               
               VALUE 'TO:<oh_meng_huat@hdb.gov.sg>'.                            
           05  WS-MAIL-RPT13F           PIC X(40)                               
               VALUE 'CC:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                
           05  WS-MAIL-RPT15            PIC X(100) VALUE                        
               'SUBJECT : REPORT ON E-STAMPING BATCH NOT PROCESSED BY IR        
      -                  'AS AFTER 7 DAYS'.                                     
                                                                                
       01  WS-L429-HDNG1.                                                       
           05  FILLER                 PIC X(08)  VALUE 'BP13L429'.              
           05  FILLER                 PIC X(04)  VALUE SPACES.                  
           05  FILLER                 PIC X(08)  VALUE 'HDB3'.                  
           05  FILLER                 PIC X(18)  VALUE SPACES.                  
           05  FILLER                 PIC X(39)  VALUE                          
                'S Y S T E M   O F   C O M M I T M E N T'.                      
           05  FILLER                 PIC X(15)  VALUE SPACES.                  
           05  FILLER                 PIC X(07)  VALUE 'DATE: '.                
           05  WS-L429-DATE           PIC X(11)  VALUE SPACES.                  
           05  FILLER                 PIC X(08)  VALUE SPACES.                  
           05  FILLER                 PIC X(07)  VALUE 'PAGE: '.                
           05  WS-L429-CNT-PAGE       PIC ZZZZ9  VALUE ZERO.                    
                                                                                
       01  WS-L429-HDNG2.                                                       
           05  FILLER                 PIC X(30) VALUE SPACES.                   
           05  FILLER                 PIC X(61) VALUE                           
                    'REPORT ON E-STAMPING BATCH NOT PROCESSED BY IRAS AF        
      -                  'TER 7 DAYS'.                                          
           05  FILLER                 PIC X(41) VALUE SPACES.                   
                                                                                
       01  WS-L429-HDNG3.                                                       
           05  FILLER  PIC X(04) VALUE 'S/NO'.                                  
           05  FILLER  PIC X(04) VALUE SPACES.                                  
           05  FILLER  PIC X(24) VALUE 'C O N T R A C T  D A T E'.              
           05  FILLER  PIC X(08) VALUE SPACES.                                  
           05  FILLER  PIC X(22) VALUE 'B A T C H  N U M B E R'.                
           05  FILLER  PIC X(10) VALUE SPACES.                                  
           05  FILLER  PIC X(26) VALUE 'V O U C H E R  N U M B E R'.            
           05  FILLER  PIC X(10) VALUE SPACES.                                  
           05  FILLER  PIC X(20) VALUE 'N O.  O F  C A S E S'.                  
           05  FILLER  PIC X(04) VALUE SPACES.                                  
                                                                                
       01  WS-L429-LINE               PIC X(132) VALUE ALL '-'.                 
                                                                                
       01  WS-L429-DET1.                                                        
           05  WS-L429-CNT            PIC ZZZ99  VALUE ZEROES.                  
           05  FILLER                 PIC X(08)  VALUE SPACES.                  
           05  WS-L429-DTE-SENT       PIC X(10)  VALUE SPACES.                  
           05  FILLER                 PIC X(23)  VALUE SPACES.                  
           05  WS-L429-NUM-BATCH      PIC X(13)  VALUE SPACES.                  
           05  FILLER                 PIC X(19)  VALUE SPACES.                  
           05  WS-L429-NUM-VRCH       PIC X(11)  VALUE SPACES.                  
           05  FILLER                 PIC X(29)  VALUE SPACES.                  
           05  WS-L429-NUM-CASES      PIC ZZ99   VALUE ZEROES.                  
           05  FILLER                 PIC X(10)  VALUE SPACES.                  
       01  WS-L429-NULL-REPORT.                                                 
           05  FILLER                 PIC X(30)  VALUE SPACES.                  
           05  WS-L429-MESSAGE        PIC X(50)  VALUE SPACES.                  
           05  FILLER                 PIC X(52)  VALUE SPACES.                  
       01  WS-HDNG-LINE               PIC X(132) VALUE ALL '-'.                 
       01  WS-BLANK-LINE              PIC X(132) VALUE SPACES.                  
                                                                                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *================================================================*        
       0000-CONTROL.                                                            
      *================================================================*        
                                                                                
            PERFORM 1000-OPEN-FILES       THRU  1000-EXIT.                      
                                                                                
            PERFORM 2000-READ-BP13F625    THRU  2000-EXIT.                      
            PERFORM 3000-PROCESS-BP13F625 THRU  3000-EXIT                       
                    UNTIL WS-F625-EOF = 'Y'.                                    
                                                                                
            IF WS-PRT-FLAG = 'N'                                                
               PERFORM 4000-PRINT-RTN     THRU  4000-EXIT                       
            END-IF.                                                             
                                                                                
            PERFORM 9000-CLOSE-FILES      THRU  9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       1000-OPEN-FILES.                                                         
      *================================================================*        
            OPEN INPUT     BP13F625                                             
                 OUTPUT    BP13L429.                                            
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8)   TO WS-DATE.                       
            COMPUTE WS-COMP-DATE = FUNCTION INTEGER-OF-DATE(WS-DATE).           
            SUBTRACT 7 FROM WS-COMP-DATE.                                       
            COMPUTE WS-CHK-DATE =                                               
                            FUNCTION DATE-OF-INTEGER(WS-COMP-DATE).             
            DISPLAY WS-COMP-DATE.                                               
            DISPLAY WS-CHK-DATE.                                                
            MOVE WS-DATE     TO WS-DATE1.                                       
            STRING WS-DD, '/', WS-MM, '/', WS-CCYY                              
                DELIMITED BY SIZE INTO WS-L429-DATE.                            
                                                                                
            PERFORM 5000-HEADER-EMAIL  THRU 5000-EXIT.                          
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2000-READ-BP13F625.                                                      
      *================================================================*        
                                                                                
           READ BP13F625 AT END MOVE 'Y' TO WS-F625-EOF                         
                           GO TO 2000-EXIT.                                     
                                                                                
              ADD 1 TO WS-F625-READ.                                            
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       3000-PROCESS-BP13F625.                                                   
      *================================================================*        
                                                                                
           IF F625-DTE-BATCH-SENT = SPACES                                      
              PERFORM 2000-READ-BP13F625    THRU  2000-EXIT                     
           ELSE                                                                 
              CONTINUE                                                          
           END-IF.                                                              
                                                                                
           IF F625-NUM-BATCH = WS-NUM-BATCH                                     
              MOVE F625-NUM-VRCH       TO WS-NUM-VRCH                           
              MOVE F625-DTE-BATCH-SENT TO WS-DATE-SENT                          
              ADD 1                    TO WS-NUM-CASES                          
           ELSE                                                                 
             IF WS-NUM-CASES > 0                                                
                MOVE 'Y' TO WS-PRT-FLAG                                         
                PERFORM  4000-PRINT-RTN THRU 4000-EXIT                          
             END-IF                                                             
                                                                                
             MOVE ZEROS                    TO   WS-NUM-CASES                    
             MOVE F625-NUM-BATCH           TO   WS-NUM-BATCH                    
             MOVE F625-NUM-VRCH            TO   WS-NUM-VRCH                     
             MOVE F625-DTE-BATCH-SENT      TO   WS-DATE-SENT                    
             PERFORM 3100-CHECK-BATCH-SENT THRU 3100-EXIT                       
                                                                                
             IF F625-DTE-BATCH-SENT < WS-CHK-DATE AND                           
                WS-2YRS-DATE >= WS-DATE                                         
                ADD 1              TO WS-NUM-CASES                              
                MOVE 'N'           TO WS-PRT-FLAG                               
             ELSE                                                               
                PERFORM 2000-READ-BP13F625 THRU 2000-EXIT                       
                        UNTIL WS-NUM-BATCH NOT = F625-NUM-BATCH                 
                        OR    WS-F625-EOF = 'Y'                                 
                GO TO 3000-EXIT                                                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F625 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       3100-CHECK-BATCH-SENT.                                                   
      *================================================================*        
           MOVE     FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                      
           MOVE     F625-DTE-BATCH-SENT        TO WS-BATCH-SENT-DATE.           
           COMPUTE  WS-COMP-DATE =                                              
                    FUNCTION INTEGER-OF-DATE(WS-BATCH-SENT-DATE).               
           ADD      730 TO WS-COMP-DATE.                                        
           COMPUTE  WS-2YRS-DATE =                                              
                    FUNCTION DATE-OF-INTEGER(WS-COMP-DATE).                     
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       4000-PRINT-RTN.                                                          
      *================================================================*        
            IF WS-LINE-CTR > 30                                                 
               PERFORM 4100-PRINT-HDNG THRU 4100-EXIT                           
            END-IF.                                                             
                                                                                
            ADD 1         TO WS-LINE-CTR.                                       
                                                                                
            MOVE WS-SLNO  TO WS-L429-CNT.                                       
                                                                                
            STRING WS-DD-S, '/', WS-MM-S, '/', WS-CCYY-S                        
                DELIMITED BY SIZE INTO WS-L429-DTE-SENT.                        
                                                                                
            MOVE WS-NUM-VRCH         TO WS-L429-NUM-VRCH.                       
            DISPLAY WS-NUM-VRCH.                                                
            MOVE WS-NUM-CASES        TO WS-L429-NUM-CASES.                      
            MOVE WS-NUM-BATCH        TO WS-L429-NUM-BATCH.                      
            WRITE BP13L429-REC       FROM WS-L429-DET1.                         
            ADD 1 TO WS-L429-WRITE.                                             
                                                                                
            ADD 1 TO WS-SLNO.                                                   
            ADD 1 TO WS-TOT-REC-PRINTED.                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       4100-PRINT-HDNG.                                                         
      *================================================================*        
            ADD   1            TO   WS-PAGE-CTR.                                
            MOVE  WS-PAGE-CTR  TO   WS-L429-CNT-PAGE.                           
            WRITE BP13L429-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L429-REC FROM WS-L429-HDNG1 AFTER PAGE.                   
            WRITE BP13L429-REC FROM WS-L429-HDNG2 AFTER 2.                      
            WRITE BP13L429-REC FROM WS-L429-HDNG3 AFTER 2.                      
            WRITE BP13L429-REC FROM WS-HDNG-LINE AFTER 2.                       
            WRITE BP13L429-REC FROM WS-BLANK-LINE.                              
            MOVE  12           TO   WS-LINE-CTR.                                
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5000-HEADER-EMAIL.                                                       
      *================================================================*        
                                                                                
            WRITE BP13L429-REC FROM WS-MAIL-RPT1.                               
            WRITE BP13L429-REC FROM WS-MAIL-RPT2.                               
            WRITE BP13L429-REC FROM WS-MAIL-RPT3.                               
            WRITE BP13L429-REC FROM WS-MAIL-RPT4.                               
            WRITE BP13L429-REC FROM WS-MAIL-RPT5.                               
            WRITE BP13L429-REC FROM WS-MAIL-RPT6.                               
            WRITE BP13L429-REC FROM WS-MAIL-RPT7.                               
            WRITE BP13L429-REC FROM WS-MAIL-RPT7A.                              
            WRITE BP13L429-REC FROM WS-MAIL-RPT7B.                              
            WRITE BP13L429-REC FROM WS-MAIL-RPT7C.                              
            WRITE BP13L429-REC FROM WS-MAIL-RPT7D.                              
            WRITE BP13L429-REC FROM WS-MAIL-RPT8.                               
            WRITE BP13L429-REC FROM WS-MAIL-RPT9.                               
            WRITE BP13L429-REC FROM WS-MAIL-RPT10.                              
            WRITE BP13L429-REC FROM WS-MAIL-RPT11.                              
            WRITE BP13L429-REC FROM WS-MAIL-RPT12.                              
            WRITE BP13L429-REC FROM WS-MAIL-RPT13A.                             
            WRITE BP13L429-REC FROM WS-MAIL-RPT13B.                             
            WRITE BP13L429-REC FROM WS-MAIL-RPT13C.                             
            WRITE BP13L429-REC FROM WS-MAIL-RPT13D.                             
            WRITE BP13L429-REC FROM WS-MAIL-RPT13E.                             
            WRITE BP13L429-REC FROM WS-MAIL-RPT13F.                             
            WRITE BP13L429-REC FROM WS-MAIL-RPT15.                              
            MOVE WS-L429-DATE    TO WS-DATE-RPT.                                
            WRITE BP13L429-REC FROM WS-DATE-RPT-DTL.                            
                                                                                
            MOVE SPACES TO  BP13L429-REC.                                       
            WRITE   BP13L429-REC.                                               
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-FILES.                                                        
      *================================================================*        
           IF WS-L429-WRITE = ZEROES                                            
              PERFORM 4100-PRINT-HDNG   THRU  4100-EXIT                         
              WRITE BP13L429-REC FROM WS-BLANK-LINE                             
              WRITE BP13L429-REC FROM WS-BLANK-LINE                             
              MOVE 'NO RECORDS WITH DTE-BATCH-SENT > 7 DAYS ' TO                
                 WS-L429-MESSAGE                                                
              WRITE BP13L429-REC       FROM WS-L429-NULL-REPORT                 
           END-IF.                                                              
                                                                                
           CLOSE BP13F625 BP13L429.                                             
                                                                                
           DISPLAY '** BP13C429 **'.                                            
           DISPLAY 'F625 RECORDS READ    : ' WS-F625-READ.                      
           DISPLAY 'L429 RECORDS WRITTEN : ' WS-L429-WRITE.                     
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
