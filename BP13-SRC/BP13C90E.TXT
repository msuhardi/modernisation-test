      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C90E.                                                 
      *AUTHOR.        ELGER DE GUZMAN.                                          
      *DATE-WRITTEN.  15/09/14.                                                 
                                                                                
      * ============================================================= *         
      * SYSTEM OF COMMITMENT (BP13)                                   *         
      * ============================================================= *         
      *                                                               *         
      *   OBJECTIVE   :  1.  A STANDARD ROUTINE CALLED BY BATCH       *         
      *                      PROGRAMS TO CALCULATE                    *I        
      *                      LEASE-ESCROW-FEE                                   
      *                      LEASE-REGN-FEE                                     
      *                      MORT-ESCROW-FEE                                    
      *                      MORT-REGN-FEE                                      
      *                      SURVEY-FEE                                         
      *                      SURVEY-FEE-GST                                     
      *                      PREMIUM-STAMP-FEE                                  
      *                      PREM-CNVY-FEE                                      
      *                      PREM-CNVY-FEE-GST                                  
      *                                                               *         
      *   CHGE-NO  BY    DATE   DESCRIPTION                           *         
      *   -------  ---  ------  ------------------------------------  *         
      *   BP135420 ESD1 160914  NEW PROGRAM                           *         
      *   BP139211 KAC1 020123  TO IMPLEMENT 8% GST                   *         
      *                            REPLACED BP13C90D BY AV02C025      *         
      *   BP139485 KAC1 020124  TO IMPLEMENT 9% GST                   *         
      *                            REPLACED BP13C90D BY AV02C025      *         
      * ============================================================= *         
                                                                                
      *-------------------------------------------------------------            
       ENVIRONMENT DIVISION.                                                    
      *-------------------------------------------------------------            
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       DATA DIVISION.                                                           
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01 WS-RESP                          PIC 9(2)      VALUE ZEROES.          
       01 WS-AMT-SP-CONVEY                 PIC S9(8)V99  VALUE ZEROES.          
       01 WS-AMT-SP-PR-CONVEY              PIC S9(8)V99  VALUE ZEROES.          
       01 WS-AMT-SP-LEASE                  PIC S9(8)V99  VALUE ZEROES.          
       01 WS-AMT-SP-PR-LEASE               PIC S9(8)V99  VALUE ZEROES.          
       01 WS-TEMP-INT-BASE                 PIC 9(8)      VALUE ZEROES.          
       01 WS-AMT-THOUSANDS                 PIC 9(6)      VALUE ZEROES.          
       01 WS-AMT-REMAIN                    PIC 9(4)      VALUE ZEROES.          
       01 WS-ROUND-SP                      PIC S9(6)     VALUE ZEROES.          
       01 WS-ROUND-CONVFEE                 PIC S9(5)V99  VALUE ZEROES.          
       01 WS-TOTAL-FEE                     PIC 9(8)V99   VALUE ZEROES.          
       01 WS-TEMP-STAMP-FEE                PIC 9(5)V99   VALUE ZEROES.          
       01 WS-TEMP-CONVEY-FEE               PIC 9(5)V99   VALUE ZEROES.          
       01 WS-TEMP-CONVEY-FEE-GST           PIC 9(5)V99   VALUE ZEROES.          
       01 WS-TEMP-MORT-ST-FEE              PIC 9(5) VALUE ZEROES.               
                                                                                
       01 WS-SERS-VARS.                                                         
           05 WS-SCH-FND                  PIC X(01)    VALUE SPACES.            
              88 SERS-SCH                              VALUE 'Y'.               
           05 WS-ALLOC-SCH                PIC X(03)    VALUE SPACES.            
                                                                                
      *-----------------------------------------------------------*             
      *    WS VARIABLES PASSED TO SUB-PROGRAM "BP13C90B"          *             
      *-----------------------------------------------------------*             
       COPY SERSCH.                                                             
                                                                                
      *-----------------------------------------------------------*             
      *    WS VARIABLES PASSED TO SUB-PROGRAM "AV02C025"          *             
      *-----------------------------------------------------------*             
       01  C025-AREA.                                                           
           05  C025-DTE-EFF.                                                    
               10  C025-DTE-CCYY          PIC 9(4).                             
               10  C025-DTE-MM            PIC 9(2).                             
               10  C025-DTE-DD            PIC 9(2).                             
           05  FILLER                     PIC X(34).                            
           05  C025-NUM-GST-RATE          PIC 9(2)V99.                          
           05  C025-NUM-ERROR             PIC X(4).                             
                                                                                
      *-----------------------------------------------------------*             
      *    WS VARIABLES PASSED TO SUB-PROGRAM "AV02C021"          *             
      *-----------------------------------------------------------*             
       COPY P13GST01.                                                           
                                                                                
      *-----------------------------------------------------------*             
      *    WS VARIABLES PASSED TO SUB-PROGRAM "BP13C90B"          *             
      *-----------------------------------------------------------*             
       COPY P13GST02.                                                           
                                                                                
      *-------------------------------------------------------------            
       LINKAGE SECTION.                                                         
      *-------------------------------------------------------------            
       01 WS-COMM-AREA.                                                         
          05 WS-COMM-NUM-REGN             PIC X(08)    VALUE SPACES.            
          05 WS-COMM-AMT-LOAN             PIC 9(8)V99  VALUE ZEROES.            
          05 WS-COMM-SELLING-PRICE        PIC 9(8)V99  VALUE ZEROES.            
          05 WS-COMM-PREMIUM              PIC 9(7)V99  VALUE ZEROES.            
          05 WS-COMM-FLAT-DISCOUNT        PIC 9(6)V99  VALUE ZEROES.            
          05 WS-COMM-ALLO-SCHEME          PIC X(03)    VALUE SPACES.            
          05 WS-COMM-SCH-ACC              PIC X(11)    VALUE SPACES.            
          05 WS-COMM-FLAT-TYPE            PIC X(02)    VALUE SPACES.            
          05 WS-COMM-FLAT-STATUS          PIC X(02)    VALUE SPACES.            
          05 WS-COMM-ALLOCN-TYPE          PIC X(01)    VALUE SPACES.            
          05 WS-COMM-REPOSSESSED-CODE     PIC X(02)    VALUE SPACES.            
          05 WS-COMM-SCCC-ELGBTY          PIC X(01)    VALUE SPACES.            
          05 WS-COMM-CDE-FLAT-ACCEPT      PIC X(01)    VALUE SPACES.            
          05 WS-COMM-LEASE-REGN-FEE       PIC 9(3)V99  VALUE ZEROES.            
          05 WS-COMM-LEASE-ESCROW-FEE     PIC 9(3)V99  VALUE ZEROES.            
          05 WS-COMM-LEASE-STAMP-FEE      PIC 9(5)V99  VALUE ZEROES.            
          05 WS-COMM-PREMIUM-STAMP-FEE    PIC 9(5)V99  VALUE ZEROES.            
          05 WS-COMM-TOT-LSE-STAMP-FEE    PIC 9(5)V99  VALUE ZEROES.            
          05 WS-COMM-SURVEY-FEE           PIC 9(3)V99  VALUE ZEROES.            
          05 WS-COMM-SURVEY-FEE-GST       PIC 9(3)V99  VALUE ZEROES.            
          05 WS-COMM-CONVEY-FEE           PIC 9(5)V99  VALUE ZEROES.            
          05 WS-COMM-CONVEY-FEE-GST       PIC 9(5)V99  VALUE ZEROES.            
          05 WS-COMM-PREM-CNVY-FEE        PIC 9(5)V99  VALUE ZEROES.            
          05 WS-COMM-PREM-CNVY-FEE-GST    PIC 9(5)V99  VALUE ZEROES.            
          05 WS-COMM-TOT-CNVY-FEE         PIC 9(5)V99  VALUE ZEROES.            
          05 WS-COMM-TOT-CNVY-FEE-GST     PIC 9(5)V99  VALUE ZEROES.            
          05 WS-COMM-MORT-ST-FEE          PIC 9(5)V99  VALUE ZEROES.            
          05 WS-COMM-MORT-REGN-FEE        PIC 9(3)V99  VALUE ZEROES.            
          05 WS-COMM-MORT-ESCROW-FEE      PIC 9(3)V99  VALUE ZEROES.            
          05 WS-COMM-SALES-TYPE           PIC X(02)    VALUE SPACES.            
          05 FILLER                       PIC X(87)    VALUE SPACES.            
          05 WS-COMM-RETCODE              PIC X(03)    VALUE SPACES.            
          05 WS-COMM-ERRMSG               PIC X(40)    VALUE SPACES.            
                                                                                
       PROCEDURE DIVISION USING WS-COMM-AREA.                                   
      *----------------------------------------------------------------*        
       0000-MAIN.                                                               
      *----------------------------------------------------------------*        
                                                                                
           MOVE ZEROES                       TO   WS-COMM-RETCODE.              
           MOVE SPACES                       TO   WS-COMM-ERRMSG.               
                                                                                
           PERFORM 1000-COMP-SP-AMOUNT       THRU 1000-EXIT.                    
                                                                                
           MOVE SPACES                       TO   C025-AREA.                    
           INITIALIZE                             C025-AREA.                    
           MOVE FUNCTION CURRENT-DATE(1:8)   TO   C025-DTE-EFF.                 
           CALL 'AV02C025'                USING   C025-AREA.                    
                                                                                
           MOVE C025-NUM-GST-RATE            TO   WS-NUM-GST-RATE.              
                                                                                
           PERFORM 4000-COMP-CONVEY-FEES     THRU 4000-EXIT.                    
                                                                                
           PERFORM 5000-COMP-SURVEY-FEES     THRU 5000-EXIT.                    
                                                                                
           PERFORM 6000-COMP-LEASE-STAMPFEE  THRU 6000-EXIT.                    
                                                                                
           IF WS-COMM-AMT-LOAN > ZEROES                                         
              PERFORM 8000-COMP-MORT-FEES    THRU 8000-EXIT                     
           END-IF.                                                              
                                                                                
           GO TO 999-EXIT-PROGRAM.                                              
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-COMP-SP-AMOUNT.                                                     
      *----------------------------------------------------------------*        
      *-----------------------------------*                                     
      * CHECK FOR SERS SCHEME             *                                     
      *-----------------------------------*                                     
           MOVE WS-COMM-ALLO-SCHEME       TO   WS-ALLOC-SCH.                    
           PERFORM 2000-CHECK-SERS-SCH    THRU 2000-EXIT.                       
                                                                                
      *-----------------------------------------------*                         
      * COMPUTE BASE AMOUNT FOR CONVEY AND LEASE FEES *                         
      *-----------------------------------------------*                         
           PERFORM 2200-COMP-SP-CONVEY    THRU 2200-EXIT.                       
                                                                                
           PERFORM 2400-COMP-SP-LEASE     THRU 2400-EXIT.                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-CHECK-SERS-SCH.                                                     
      *----------------------------------------------------------------*        
      *-----------------------------------*                                     
      * SEARCH SCHEME IN ALLOC SCH TABLE  *                                     
      *-----------------------------------*                                     
           IF WS-ALLOC-SCH  NOT =  ALL ' ' AND LOW-VALUES                       
               SET WS-SERS-PTR  TO 1                                            
               SEARCH WS-SERS-SCH                                               
                   AT END                                                       
                   MOVE  'N'        TO   WS-SCH-FND                             
                   WHEN WS-SERS-SCHEME(WS-SERS-PTR) = WS-ALLOC-SCH              
                   MOVE  'Y'        TO   WS-SCH-FND                             
               END-SEARCH                                                       
           ELSE                                                                 
               MOVE  'N'            TO   WS-SCH-FND                             
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2200-COMP-SP-CONVEY.                                                     
      *----------------------------------------------------------------*        
           MOVE WS-COMM-SELLING-PRICE      TO   WS-AMT-SP-CONVEY.               
           IF WS-COMM-PREMIUM > ZEROES                                          
              COMPUTE WS-AMT-SP-PR-CONVEY = WS-AMT-SP-CONVEY +                  
                                            WS-COMM-PREMIUM                     
           ELSE                                                                 
              MOVE WS-AMT-SP-CONVEY        TO   WS-AMT-SP-PR-CONVEY             
           END-IF.                                                              
                                                                                
      *--------------------------------------------------------*                
      * ROUND UP THE SELLING PRICE TO THE NEAREST HUNDRED      *                
      *--------------------------------------------------------*                
           ADD    49                       TO   WS-AMT-SP-CONVEY.               
           COMPUTE WS-ROUND-SP ROUNDED  =  WS-AMT-SP-CONVEY / 100.              
           COMPUTE WS-AMT-SP-CONVEY     =  WS-ROUND-SP * 100.                   
                                                                                
           ADD    49                       TO   WS-AMT-SP-PR-CONVEY.            
           COMPUTE WS-ROUND-SP ROUNDED  =  WS-AMT-SP-PR-CONVEY / 100.           
           COMPUTE WS-AMT-SP-PR-CONVEY  =  WS-ROUND-SP * 100.                   
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2400-COMP-SP-LEASE.                                                      
      *----------------------------------------------------------------*        
           IF (SERS-SCH) OR (WS-COMM-SALES-TYPE = '2B') OR                      
              (WS-COMM-ALLO-SCHEME = 'RED')                                     
              COMPUTE WS-AMT-SP-LEASE = (WS-COMM-SELLING-PRICE +                
                                         WS-COMM-FLAT-DISCOUNT)                 
              IF WS-COMM-PREMIUM > ZEROES                                       
                 COMPUTE WS-AMT-SP-PR-LEASE = WS-AMT-SP-LEASE +                 
                                              WS-COMM-PREMIUM                   
              ELSE                                                              
                 MOVE WS-AMT-SP-LEASE        TO   WS-AMT-SP-PR-LEASE            
              END-IF                                                            
           ELSE                                                                 
              MOVE WS-COMM-SELLING-PRICE     TO   WS-AMT-SP-LEASE               
              IF WS-COMM-PREMIUM > ZEROES                                       
                 COMPUTE WS-AMT-SP-PR-LEASE = WS-AMT-SP-LEASE +                 
                                              WS-COMM-PREMIUM                   
              ELSE                                                              
                 MOVE WS-AMT-SP-LEASE        TO   WS-AMT-SP-PR-LEASE            
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-COMP-CONVEY-FEES.                                                   
      *----------------------------------------------------------------*        
           PERFORM 4200-COMP-TOT-CONVEY  THRU 4200-EXIT.                        
           PERFORM 4300-COMP-CONVEY      THRU 4300-EXIT.                        
           PERFORM 4400-COMP-PREM-CONVEY THRU 4400-EXIT.                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4200-COMP-TOT-CONVEY.                                                    
      *----------------------------------------------------------------*        
      *---------------------------------------------*                           
      * CONVEY FEE FOR SELLING PRICE PLUS PREMIUM   *                           
      *---------------------------------------------*                           
           DIVIDE WS-AMT-SP-PR-CONVEY BY 100                                    
                                      GIVING WS-TEMP-INT-BASE                   
                                      REMAINDER WS-AMT-REMAIN.                  
                                                                                
           IF WS-AMT-REMAIN > ZERO                                              
              ADD    1                  TO   WS-TEMP-INT-BASE                   
           END-IF.                                                              
                                                                                
           COMPUTE WS-TEMP-INT-BASE = WS-TEMP-INT-BASE * 100.                   
                                                                                
           PERFORM 4500-COMP-CONVEY-FEE THRU 4500-EXIT.                         
                                                                                
           MOVE WS-TEMP-CONVEY-FEE      TO   WS-COMM-TOT-CNVY-FEE.              
           MOVE WS-TEMP-CONVEY-FEE-GST  TO   WS-COMM-TOT-CNVY-FEE-GST.          
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       4300-COMP-CONVEY.                                                        
      *----------------------------------------------------------------*        
      *---------------------------------------------*                           
      * CONVEY FEE FOR SELLING PRICE                *                           
      *---------------------------------------------*                           
           DIVIDE WS-AMT-SP-CONVEY BY 100                                       
                                      GIVING WS-TEMP-INT-BASE                   
                                      REMAINDER WS-AMT-REMAIN.                  
                                                                                
           IF WS-AMT-REMAIN > ZERO                                              
              ADD    1                  TO   WS-TEMP-INT-BASE                   
           END-IF.                                                              
                                                                                
           COMPUTE WS-TEMP-INT-BASE = WS-TEMP-INT-BASE * 100.                   
                                                                                
           PERFORM 4500-COMP-CONVEY-FEE THRU 4500-EXIT.                         
                                                                                
           MOVE WS-TEMP-CONVEY-FEE      TO   WS-COMM-CONVEY-FEE.                
           MOVE WS-TEMP-CONVEY-FEE-GST  TO   WS-COMM-CONVEY-FEE-GST.            
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       4400-COMP-PREM-CONVEY.                                                   
      *----------------------------------------------------------------*        
      *---------------------------------------------*                           
      * CONVEY FEE FOR PREMIUM AMOUNT               *                           
      *---------------------------------------------*                           
           IF WS-COMM-TOT-CNVY-FEE = WS-COMM-CONVEY-FEE                         
              MOVE ZEROES               TO   WS-COMM-PREM-CNVY-FEE              
                                             WS-COMM-PREM-CNVY-FEE-GST          
           ELSE                                                                 
              COMPUTE WS-TEMP-CONVEY-FEE = WS-COMM-TOT-CNVY-FEE -               
                                           WS-COMM-CONVEY-FEE                   
              PERFORM 4600-COMP-GST-AMOUNT   THRU 4600-EXIT                     
              PERFORM 4700-REMOVE-ODD-CENTS  THRU 4700-EXIT                     
              MOVE WS-TEMP-CONVEY-FEE      TO WS-COMM-PREM-CNVY-FEE             
              MOVE WS-TEMP-CONVEY-FEE-GST  TO WS-COMM-PREM-CNVY-FEE-GST         
           END-IF.                                                              
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       4500-COMP-CONVEY-FEE.                                                    
      *----------------------------------------------------------------*        
      *--------------------------------------------------------*                
      *  CONVEYANCING FEE :                                    *                
      *      FIRST $30,000   -->  0.09%                        *                
      *      NEXT  $30,000   -->  0.072%                       *                
      *      ABOVE $60,000   -->  0.06%                        *                
      *      MINIMUM CONVEYANCING FEE CHARGEABLE IS $20/-      *                
      *--------------------------------------------------------*                
           IF WS-TEMP-INT-BASE <= 30000                                         
              COMPUTE WS-TEMP-CONVEY-FEE = WS-TEMP-INT-BASE * 0.0009            
           ELSE                                                                 
              IF WS-TEMP-INT-BASE <= 60000                                      
                 COMPUTE WS-TEMP-CONVEY-FEE = (30000 * 0.0009)  +               
                       ((WS-TEMP-INT-BASE - 30000) * 0.00072)                   
              ELSE                                                              
                 COMPUTE WS-TEMP-CONVEY-FEE = (30000 * 0.0009)  +               
                                              (30000 * 0.00072) +               
                       ((WS-TEMP-INT-BASE - 60000) * 0.0006)                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *--------------------------------------------------------*                
      *  MINIMUM CONVEYANCING FEE IS $20/-                     *                
      *  ROUND IT UP TO THE NEAREST DOLLAR                     *                
      *--------------------------------------------------------*                
           IF WS-TEMP-CONVEY-FEE < 20                                           
              MOVE   20                  TO   WS-TEMP-CONVEY-FEE                
           ELSE                                                                 
              ADD  0.49                  TO   WS-TEMP-CONVEY-FEE                
              COMPUTE WS-ROUND-CONVFEE ROUNDED =                                
                                      WS-TEMP-CONVEY-FEE / 100                  
              COMPUTE WS-TEMP-CONVEY-FEE  = WS-ROUND-CONVFEE * 100              
           END-IF.                                                              
                                                                                
           PERFORM 4600-COMP-GST-AMOUNT  THRU 4600-EXIT.                        
                                                                                
           PERFORM 4700-REMOVE-ODD-CENTS THRU 4700-EXIT.                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       4600-COMP-GST-AMOUNT.                                                    
      *----------------------------------------------------------------*        
      *--------------------------------------------------------*                
      *  CALL AV02B021 SUB-ROUTINE TO GET THE GST AMOUNT       *                
      *--------------------------------------------------------*                
           MOVE WS-TEMP-CONVEY-FEE       TO   WS-AMT-NETT.                      
           CALL 'AV02C021' USING WS-COMM-AREA-GST.                              
           MOVE WS-AMT-GST               TO   WS-TEMP-CONVEY-FEE-GST.           
           COMPUTE WS-TOTAL-FEE = WS-TEMP-CONVEY-FEE +                          
                                  WS-TEMP-CONVEY-FEE-GST.                       
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4700-REMOVE-ODD-CENTS.                                                   
      *----------------------------------------------------------------*        
           MOVE SPACES                   TO   WS-COMM-AREA-ODDCENTS.            
           INITIALIZE WS-COMM-AREA-ODDCENTS.                                    
                                                                                
           MOVE WS-NUM-GST-RATE          TO   WS-COMM-ODD-GST-RATE.             
           MOVE WS-TOTAL-FEE             TO   WS-COMM-ODD-TOTAL-FEE.            
                                                                                
           CALL 'BP13C90B' USING WS-COMM-AREA-ODDCENTS.                         
                                                                                
           IF WS-COMM-ODD-CENTS-SW = 'Y'                                        
              MOVE WS-COMM-ODD-GST       TO   WS-TEMP-CONVEY-FEE-GST            
              MOVE WS-COMM-ODD-BASE-FEE  TO   WS-TEMP-CONVEY-FEE                
           END-IF.                                                              
                                                                                
       4700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5000-COMP-SURVEY-FEES.                                                   
      *----------------------------------------------------------------*        
      *-------------------------------------------------------*                 
      *  SURVEY FEE IS BASED ON FLAT TYPE                     *                 
      *  1 & 2  -->  $150.00                                  *                 
      *      3  -->  $212.50                                  *                 
      *      4  -->  $275.00                                  *                 
      *      5  -->  $325.00                                  *                 
      *    6/E  -->  $375.00                                  *                 
      *                                                       *                 
      *  IF SALES TYPE IS '03' OR '3A', SURVEY FEE IS ZERO    *                 
      *-------------------------------------------------------*                 
           IF WS-COMM-SALES-TYPE = '03' OR '3A'                                 
              MOVE ZEROES             TO   WS-COMM-SURVEY-FEE                   
           ELSE                                                                 
              EVALUATE WS-COMM-FLAT-TYPE(1:1)                                   
                WHEN '1'                                                        
                WHEN '2'                                                        
                  MOVE   150.00       TO   WS-COMM-SURVEY-FEE                   
                WHEN '3'                                                        
                  MOVE   212.50       TO   WS-COMM-SURVEY-FEE                   
                WHEN '4'                                                        
                  MOVE   275.00       TO   WS-COMM-SURVEY-FEE                   
                WHEN '5'                                                        
                  MOVE   325.00       TO   WS-COMM-SURVEY-FEE                   
                WHEN '6'                                                        
                WHEN 'E'                                                        
                  MOVE   375.00       TO   WS-COMM-SURVEY-FEE                   
                WHEN OTHER                                                      
                  MOVE  ZEROES        TO   WS-COMM-SURVEY-FEE                   
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
      *--------------------------------------------------------*                
      *  CALL AV02B021 SUB-ROUTINE TO GET THE GST AMOUNT       *                
      *--------------------------------------------------------*                
            MOVE WS-COMM-SURVEY-FEE      TO   WS-AMT-NETT.                      
            CALL 'AV02C021' USING WS-COMM-AREA-GST.                             
            MOVE WS-AMT-GST              TO   WS-COMM-SURVEY-FEE-GST.           
            COMPUTE WS-TOTAL-FEE = WS-COMM-SURVEY-FEE +                         
                                   WS-COMM-SURVEY-FEE-GST.                      
                                                                                
           MOVE SPACES                   TO   WS-COMM-AREA-ODDCENTS.            
           INITIALIZE WS-COMM-AREA-ODDCENTS.                                    
                                                                                
           MOVE WS-NUM-GST-RATE          TO   WS-COMM-ODD-GST-RATE.             
                                                                                
           COMPUTE WS-COMM-ODD-TOTAL-FEE = WS-TOTAL-FEE                         
                                                                                
           CALL 'BP13C90B' USING WS-COMM-AREA-ODDCENTS.                         
                                                                                
           IF WS-COMM-ODD-CENTS-SW = 'Y'                                        
              MOVE WS-COMM-ODD-GST       TO   WS-COMM-SURVEY-FEE-GST            
              MOVE WS-COMM-ODD-BASE-FEE  TO   WS-COMM-SURVEY-FEE                
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       6000-COMP-LEASE-STAMPFEE.                                                
      *----------------------------------------------------------------*        
      *---------------------------------------------*                           
      *  LEASE AND PREMIUM STAMP FEES               *                           
      *---------------------------------------------*                           
           PERFORM 6200-COMP-TOT-LSF     THRU 6200-EXIT.                        
           PERFORM 6300-COMP-LSF         THRU 6300-EXIT.                        
           PERFORM 6400-COMP-PREM-SF     THRU 6400-EXIT.                        
                                                                                
      *---------------------------------------------*                           
      *  LEASE ESCROW FEE                           *                           
      *---------------------------------------------*                           
           MOVE   0        TO   WS-COMM-LEASE-ESCROW-FEE.                       
      *    MOVE   4        TO   WS-COMM-LEASE-ESCROW-FEE.                       
                                                                                
      *---------------------------------------------*                           
      *  LEASE REGN FEE                             *                           
      *---------------------------------------------*                           
           MOVE  38.30     TO   WS-COMM-LEASE-REGN-FEE.                         
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       6200-COMP-TOT-LSF.                                                       
      *----------------------------------------------------------------*        
           MOVE WS-AMT-SP-PR-LEASE      TO   WS-TEMP-INT-BASE                   
                                                                                
           PERFORM 6800-COMP-STAMP-FEE  THRU 6800-EXIT.                         
                                                                                
           MOVE WS-TEMP-STAMP-FEE       TO   WS-COMM-TOT-LSE-STAMP-FEE.         
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       6300-COMP-LSF.                                                           
      *----------------------------------------------------------------*        
           MOVE WS-AMT-SP-LEASE         TO   WS-TEMP-INT-BASE                   
                                                                                
           PERFORM 6800-COMP-STAMP-FEE  THRU 6800-EXIT.                         
                                                                                
           MOVE WS-TEMP-STAMP-FEE       TO   WS-COMM-LEASE-STAMP-FEE.           
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       6400-COMP-PREM-SF.                                                       
      *----------------------------------------------------------------*        
      *---------------------------------------------*                           
      * STAMP FEE FOR PREMIUM AMOUNT                *                           
      *---------------------------------------------*                           
           COMPUTE WS-COMM-PREMIUM-STAMP-FEE =                                  
                 WS-COMM-TOT-LSE-STAMP-FEE - WS-COMM-LEASE-STAMP-FEE.           
                                                                                
       6400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       6800-COMP-STAMP-FEE.                                                     
      *----------------------------------------------------------------*        
      *--------------------------------------------------------*                
      *  LEASE STAMP FEE :                                     *                
      *      FIRST $180,000    -->  1%                         *                
      *      NEXT  $180,000    -->  2%                         *                
      *      ABOVE $360,000    -->  3%                         *                
      *--------------------------------------------------------*                
           IF WS-TEMP-INT-BASE <= 180000                                        
              COMPUTE WS-TEMP-STAMP-FEE = (WS-TEMP-INT-BASE * 0.01)             
           ELSE                                                                 
              IF WS-TEMP-INT-BASE <= 360000                                     
                 COMPUTE WS-TEMP-STAMP-FEE = (180000 * 0.01)  +                 
                        ((WS-TEMP-INT-BASE - 180000) * 0.02)                    
              ELSE                                                              
                 COMPUTE WS-TEMP-STAMP-FEE = (180000 * 0.01) +                  
                                             (180000 * 0.02) +                  
                        ((WS-TEMP-INT-BASE - 360000) * 0.03)                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       8000-COMP-MORT-FEES.                                                     
      *----------------------------------------------------------------*        
      *---------------------------------------------*                           
      *  MORTAGE ESCROW FEE                         *                           
      *---------------------------------------------*                           
           IF WS-COMM-AMT-LOAN NOT NUMERIC                                      
              MOVE  ZEROES              TO   WS-COMM-AMT-LOAN                   
           END-IF.                                                              
                                                                                
           IF WS-COMM-AMT-LOAN = ZEROES                                         
              MOVE  ZEROES              TO   WS-COMM-MORT-ESCROW-FEE            
                                             WS-COMM-MORT-ST-FEE                
                                             WS-COMM-MORT-REGN-FEE              
           ELSE                                                                 
              MOVE  0                   TO   WS-COMM-MORT-ESCROW-FEE            
              MOVE  38.30               TO   WS-COMM-MORT-REGN-FEE              
              IF WS-COMM-AMT-LOAN < 1000                                        
                 MOVE  16               TO   WS-COMM-MORT-ST-FEE                
              ELSE                                                              
                 IF WS-COMM-AMT-LOAN >= 125000                                  
                    MOVE  500           TO   WS-TEMP-MORT-ST-FEE                
                 ELSE                                                           
                    COMPUTE WS-TEMP-MORT-ST-FEE =                               
                      (WS-COMM-AMT-LOAN * .4) / 100                             
                    IF WS-TEMP-MORT-ST-FEE < 1                                  
                       MOVE 1 TO WS-TEMP-MORT-ST-FEE                            
                    END-IF                                                      
                 END-IF                                                         
                 IF WS-TEMP-MORT-ST-FEE > 500                                   
                    MOVE  500           TO   WS-COMM-MORT-ST-FEE                
                 ELSE                                                           
                    MOVE WS-TEMP-MORT-ST-FEE TO WS-COMM-MORT-ST-FEE             
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       999-EXIT-PROGRAM.                                                        
      ******************************************************************        
           GOBACK.                                                              
