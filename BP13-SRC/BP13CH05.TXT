       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH05.                                                 
      *AUTHOR.        MSD1.                                                     
      *DATE-WRITTEN.  25/06/2008.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES:  TO REMOVE REC WHERE QUEUE NO NOT IN SEQUENCE  *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133367  MSD1  20080625  NEW PGM                          *            
      * BP135037  IMC1  20130912  MOVE BF BOOK STATUS              *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13F268  ASSIGN        TO BP13F268.                          
           SELECT P13F268A  ASSIGN        TO P13F268A.                          
           SELECT P13F268B  ASSIGN        TO P13F268B.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F268                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F268.                                                           
                                                                                
       FD   P13F268A                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01 P13F268A-REC            PIC X(200).                                   
                                                                                
       FD   P13F268B                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01 P13F268B-REC            PIC X(200).                                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-COUNTERS.                                                          
          05 WS-F268-READ         PIC 9(08) VALUE ZEROES.                       
          05 WS-F268-WRITE        PIC 9(08) VALUE ZEROES.                       
          05 WS-F268-DROP         PIC 9(08) VALUE ZEROES.                       
       01 WS-SWITCHES.                                                          
          05 WS-F268-EOF          PIC X     VALUE 'N'.                          
       01 WS-VARIABLES.                                                         
          05 WS-F268-MAIN-KEY.                                                  
             10 WS-F268-PART-KEY  PIC X(47) VALUE SPACES.                       
             10 FILLER            PIC X(7)  VALUE SPACES.                       
          05 WS-HOLD-PART-KEY     PIC X(47) VALUE SPACES.                       
          05 WS-HOLD-QUEU-NO      PIC X(5)  VALUE SPACES.                       
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F268                                                 
                OUTPUT P13F268A                                                 
                       P13F268B.                                                
                                                                                
           PERFORM 1000-READ-BP13F268  THRU 1000-EXIT.                          
           PERFORM 2000-PROCESS-DATA   THRU 2000-EXIT                           
                 UNTIL WS-F268-EOF = 'Y'.                                       
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-READ-BP13F268.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F268 AT END                                                 
             MOVE 'Y'  TO WS-F268-EOF                                           
             GO TO 1000-EXIT.                                                   
                                                                                
           ADD 1       TO WS-F268-READ.                                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE F268-KEY-FLD            TO WS-F268-MAIN-KEY.                    
                                                                                
           IF WS-F268-PART-KEY > WS-HOLD-PART-KEY                               
              MOVE WS-F268-PART-KEY     TO WS-HOLD-PART-KEY                     
              MOVE F268-NUM-QUEUE       TO WS-HOLD-QUEU-NO                      
              WRITE P13F268A-REC      FROM BP13F268-REC                         
              ADD 1                     TO WS-F268-WRITE                        
                                                                                
           ELSE                                                                 
              IF F268-NUM-QUEUE > WS-HOLD-QUEU-NO                               
                 MOVE F268-NUM-QUEUE    TO WS-HOLD-QUEU-NO                      
                 WRITE P13F268A-REC   FROM BP13F268-REC                         
                 ADD 1                  TO WS-F268-WRITE                        
                                                                                
              ELSE                                                              
                 MOVE 'Y'               TO F268-NUM-BOOK                        
                 MOVE 'BF'              TO F268-NUM-BOOK-STATUS                 
                 WRITE P13F268B-REC   FROM BP13F268-REC                         
                 ADD 1                  TO WS-F268-DROP.                        
                                                                                
           PERFORM 1000-READ-BP13F268 THRU 1000-EXIT.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13CH05 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ FROM BP13F268  : ' WS-F268-READ.            
           DISPLAY 'NO OF RECS WRITTEN   P13F268A  : ' WS-F268-WRITE.           
           DISPLAY 'NO OF RECS DROPPED   BP13F268  : ' WS-F268-DROP.            
                                                                                
           CLOSE BP13F268                                                       
                 P13F268A                                                       
                 P13F268B.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
