      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13C3A6.                                                 
       AUTHOR.        KV5.                                                      
       DATE-WRITTEN.  08/02/2019.                                               
                                                                                
      *************************************************************             
      * SYSTEM NAME : SYSTEM OF COMMITTMENT - BP13                *             
      *===========================================================*             
      * OBJECTIVE   : IF THE REGISTRAION FOUND DELETING THE RECORD*             
      *               IN BP13K300                                 *             
      *                                                           *             
      * FILES USED  : BP13F300 - INPUT FILE.                      *             
      *               BP13K300 - VALIDATE K300-KEY-FLD.           *             
      *===========================================================*             
      * CHGEREQ  DATE       BY   AMMENDMENTS                      *             
      * -------- ---------- ---- -----------                      *             
      * BP136685 22/02/2019 KV5  NEW PROGRAM                      *             
      *************************************************************             
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F300 ASSIGN           TO BP13F300.                        
           SELECT BP13K300 ASSIGN           TO BP13K300                         
                           ORGANIZATION     IS INDEXED                          
                           ACCESS MODE      IS RANDOM                           
                           RECORD KEY       IS K300-KEY-FLD                     
                           FILE STATUS      IS WS-K300-STATUS.                  
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F300                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F300.                                                           
                                                                                
       FD   BP13K300                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K300.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 WS-K300-STATUS                 PIC 9(02) VALUE ZEROS.              
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-F300-READ                   PIC 9(08) VALUE ZEROS.              
          05 WS-K300-READ                   PIC 9(08) VALUE ZEROS.              
          05 WS-K300-DELETE                 PIC 9(08) VALUE ZEROS.              
          05 WS-K300-NOT-DEL                PIC 9(08) VALUE ZEROS.              
                                                                                
       01 WS-SWITCHES.                                                          
          05 WS-F300-EOF                    PIC X(01) VALUE SPACES.             
                                                                                
       01 WS-DTE-TME.                                                           
          05 WS-DTE                         PIC X(08) VALUE SPACES.             
          05 WS-TME                         PIC X(08) VALUE SPACES.             
          05 FILLER                         PIC X(05) VALUE SPACES.             
                                                                                
       01 WS-DISPLAY-DATE.                                                      
          05 WS-DD                          PIC X(02) VALUE SPACES.             
          05 FILLER                         PIC X(01) VALUE '/'.                
          05 WS-MM                          PIC X(02) VALUE SPACES.             
          05 FILLER                         PIC X(01) VALUE '/'.                
          05 WS-CCYY                        PIC X(04) VALUE SPACES.             
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *-----------------*                                                       
       0000-MAIN-ROUTINE.                                                       
      *-----------------*                                                       
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F300       THRU 2000-EXIT.                     
           PERFORM 3000-READ-BP13K300       THRU 3000-EXIT                      
             UNTIL WS-F300-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
                                                                                
      *---------------*                                                         
       1000-OPEN-FILES.                                                         
      *---------------*                                                         
           OPEN INPUT BP13F300                                                  
                I-O   BP13K300.                                                 
                                                                                
           IF WS-K300-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K300 OPENING ERROR, STATUS : ' WS-K300-STATUS        
              MOVE WS-K300-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-DTE-TME.                      
           MOVE WS-DTE(1:4)                 TO WS-CCYY.                         
           MOVE WS-DTE(5:2)                 TO WS-MM.                           
           MOVE WS-DTE(7:2)                 TO WS-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       2000-READ-BP13F300.                                                      
      *------------------*                                                      
           READ BP13F300                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F300-EOF                      
             NOT AT END                                                         
                ADD 1                       TO WS-F300-READ                     
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3000-READ-BP13K300.                                                      
      *-------------------*                                                     
           MOVE SPACES                      TO K300-KEY-FLD.                    
           MOVE F300-KEY-FLD                TO K300-KEY-FLD.                    
                                                                                
           READ BP13K300.                                                       
                                                                                
           EVALUATE WS-K300-STATUS                                              
               WHEN 00                                                          
                    ADD 1                    TO WS-K300-READ                    
                    PERFORM 4000-K300-DELETE THRU 4000-EXIT                     
               WHEN 23                                                          
                    ADD 1                    TO WS-K300-NOT-DEL                 
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'READ ERROR BP13K300 : ' WS-K300-STATUS             
                    MOVE WS-K300-STATUS      TO RETURN-CODE                     
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
           END-EVALUATE                                                         
           PERFORM 2000-READ-BP13F300        THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4000-K300-DELETE.                                                        
      *-------------------*                                                     
                                                                                
           DELETE BP13K300.                                                     
                                                                                
           EVALUATE WS-K300-STATUS                                              
               WHEN 00                                                          
                    ADD 1 TO WS-K300-DELETE                                     
               WHEN OTHER                                                       
                    DISPLAY 'DELETE ERROR BP13K300 : ' WS-K300-STATUS           
                    MOVE WS-K300-STATUS  TO RETURN-CODE                         
                    PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       9000-CLOSE-FILES.                                                        
      *----------------*                                                        
           CLOSE BP13F300                                                       
                 BP13K300.                                                      
                                                                                
           IF WS-K300-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K300 CLOSING ERROR, STATUS : ' WS-K300-STATUS        
              MOVE WS-K300-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'PROGRAM NAME   : BP13C3A6'.                                 
           DISPLAY 'DATE OF RUN    : ' WS-DISPLAY-DATE.                         
           DISPLAY ' '.                                                         
           DISPLAY 'RECORDS READ FROM BP13F300    : ' WS-F300-READ.             
           DISPLAY 'RECORDS READ FROM BP13K300    : ' WS-K300-READ.             
           DISPLAY 'RECODRS DELETED IN BP13K300   : ' WS-K300-DELETE            
           DISPLAY 'RECODRS NOT DELETED IN BP13K300 : ' WS-K300-NOT-DEL         
           DISPLAY ' '.                                                         
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
