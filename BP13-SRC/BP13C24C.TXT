      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13C24C.                                                 
      *AUTHOR.        ELAINE S. ARGA.                                           
      *DATE-WRITTEN.  09/09/2009.                                               
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      * OBJECTIVE :                                                   *         
      *  1. PRINT REPORT FOR NPL APPLICATIONS W/ SELECTED FLATS IN    *         
      *             PAYA LEBAR OR CHAI CHEE AFFECTED BY HIP           *         
      *---------------------------------------------------------------*         
      * CHG REF  BY    DATE    DESCRIPTION                            *         
      * -------- ---  -------- -----------                            *         
      * BP133706 ESA1 09/09/09 NEW PROGRAM.                           *         
      *===============================================================*         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F120  ASSIGN    TO BM06F120.                              
                                                                                
           SELECT BB24K010  ASSIGN    TO BB24K010                               
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE IS DYNAMIC                                        
                  RECORD KEY IS K010-KEY-FLD                                    
                  ALTERNATE KEY IS K010-ALT-KEY1                                
                  FILE STATUS IS K010-STATUS.                                   
                                                                                
           SELECT BP13L24C  ASSIGN    TO BP13L24C.                              
                                                                                
      *---------------*                                                         
       DATA DIVISION.                                                           
      *---------------*                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BM06F120                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 700 CHARACTERS.                                      
       COPY BM06F120.                                                           
                                                                                
       FD  BB24K010                                                             
           RECORD CONTAINS 5000 CHARACTERS.                                     
       COPY BB24K010.                                                           
                                                                                
       FD  BP13L24C                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01 BP13L24C-REC            PIC X(132).                                   
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
      *------------------------------*                                          
      *   WORKING STORAGE VARIABLES  *                                          
      *------------------------------*                                          
       01 WS-VARIABLES.                                                         
          05 WS-F120-EOF             PIC X(01) VALUE 'N'.                       
             88 F120-END-OF-FILE   VALUE 'Y'.                                   
          05 WS-F120-READ-CNT        PIC 9(08) VALUE ZEROES.                    
          05 WS-L24C-WRITE-CNT       PIC 9(08) VALUE ZEROES.                    
          05 WS-EDT-DATE             PIC X(10) VALUE SPACES.                    
          05 WS-CUR-DATE             PIC X(08) VALUE SPACES.                    
          05 WS-EDIT1                PIC Z(07)9.                                
          05 K010-STATUS             PIC 9(02) VALUE ZEROES.                    
          05 WS-HANDOVER-KEY         PIC X(10) VALUE SPACES.                    
          05 WS-EFFECTIVE-DATE       PIC X(10) VALUE SPACES.                    
          05 WS-COMPLETE-DATE        PIC X(08) VALUE SPACES.                    
          05 WS-COMPLETION-DATE      PIC X(10) VALUE SPACES.                    
                                                                                
       01 WS-COUNTER.                                                           
          05 WS-LINE-CNT             PIC 9(05) VALUE ZEROES.                    
          05 WS-PAGE-CNT             PIC 9(06) VALUE ZEROES.                    
          05 WS-MAX-LINES            PIC 9(05) VALUE ZEROES.                    
          05 WS-SLNO                 PIC 9(05) VALUE ZEROES.                    
                                                                                
       01  WS-F120-ADDRESS.                                                     
           05 FILLER                      PIC X(03)  VALUE 'BLK'.               
           05 FILLER                      PIC X      VALUE SPACE.               
           05 WS-F120-BLK-NO              PIC X(05).                            
           05 FILLER                      PIC X      VALUE SPACE.               
           05 WS-CHAR                     PIC X      VALUE '#'.                 
           05 WS-F120-LEVEL-NO            PIC X(02).                            
           05 WS-DASH                     PIC X      VALUE '-'.                 
           05 WS-F120-MAIN-UNIT           PIC X(04).                            
           05 FILLER                      PIC X      VALUE SPACE.               
           05 WS-F120-SUB-UNIT            PIC X(03).                            
           05 FILLER                      PIC X      VALUE SPACE.               
           05 WS-F120-STREET-NAME         PIC X(32).                            
                                                                                
       01  WS-PARSING.                                                          
           05 WS-STRING-TO-PARSE            PIC X(80).                          
           05 WS-PARSED-STRING              PIC X(80).                          
           05 WS-STRING-LENGTH              PIC 9(03).                          
           05 WS-CUR-POS-SP                 PIC 9(03).                          
           05 WS-CUR-POS-PS                 PIC 9(03).                          
           05 WS-SPACE-FLAG                 PIC X    VALUE 'N'.                 
           05 WS-INSERT-SPACE-FLAG          PIC X    VALUE 'N'.                 
                                                                                
      *-------------------------------------------------------------            
      *    REPORT LAYOUT                                                        
      *-------------------------------------------------------------            
       01 L24C-HDR01.                                                           
          05 FILLER                     PIC X(14) VALUE 'BP13L24C'.             
          05 FILLER                     PIC X(30) VALUE 'HDB3'.                 
          05 FILLER                     PIC X(42) VALUE                         
            'S Y S T E M   O F   C O M M I T M E N T'.                          
          05 FILLER                     PIC X(13) VALUE SPACES.                 
          05 FILLER                     PIC X(07) VALUE 'DATE: '.               
          05 L24C-DATE                  PIC X(10) VALUE SPACES.                 
          05 FILLER                     PIC X(10) VALUE '   PAGE: '.            
          05 L24C-PAGE                  PIC ZZZZ9.                              
                                                                                
       01 L24C-HDR02.                                                           
          05 FILLER                     PIC X(46) VALUE SPACES.                 
          05 FILLER                     PIC X(36) VALUE                         
            'NPL APPLICATIONS WITH SELECTED FLATS'.                             
          05 FILLER                     PIC X(56) VALUE SPACES.                 
                                                                                
       01 L24C-HDR03.                                                           
          05 FILLER                     PIC X(43) VALUE SPACES.                 
          05 FILLER                     PIC X(42) VALUE                         
            'IN PAYA LEBAR OR CHAI CHEE AFFECTED BY HIP'.                       
          05 FILLER                     PIC X(50) VALUE SPACES.                 
                                                                                
       01 L24C-HDR04.                                                           
          05 FILLER                     PIC X(03) VALUE SPACE.                  
          05 FILLER                     PIC X(04) VALUE 'S/NO'.                 
          05 FILLER                     PIC X(02) VALUE SPACES.                 
          05 FILLER                     PIC X(08) VALUE 'REGN NO.'.             
          05 FILLER                     PIC X(02) VALUE SPACES.                 
          05 FILLER                     PIC X(04) VALUE 'NRIC'.                 
          05 FILLER                     PIC X(07) VALUE SPACES.                 
          05 FILLER                     PIC X(19) VALUE                         
            'BOOKED FLAT ADDRESS'.                                              
          05 FILLER                     PIC X(26) VALUE SPACES.                 
          05 FILLER                     PIC X(14) VALUE                         
            'EFFECTIVE DATE'.                                                   
          05 FILLER                     PIC X(03) VALUE SPACES.                 
          05 FILLER                     PIC X(17) VALUE                         
            'KEY HANDOVER DATE'.                                                
          05 FILLER                     PIC X(03) VALUE SPACES.                 
          05 FILLER                     PIC X(15) VALUE                         
            'COMPLETION DATE'.                                                  
                                                                                
       01  L24C-DETAIL.                                                         
           05 FILLER                     PIC X(02) VALUE SPACES.                
           05 L24C-SNO                   PIC ZZZZ9.                             
           05 FILLER                     PIC X(02) VALUE SPACES.                
           05 L24C-REGN-NO               PIC X(08) VALUE SPACES.                
           05 FILLER                     PIC X(02) VALUE SPACES.                
           05 L24C-NUM-NRIC-HA1          PIC X(09) VALUE SPACES.                
           05 FILLER                     PIC X(02) VALUE SPACES.                
           05 L24C-ADDR                  PIC X(40) VALUE SPACES.                
           05 FILLER                     PIC X(07) VALUE SPACES.                
           05 L24C-EFFECTIVE-DATE        PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(08) VALUE SPACES.                
           05 L24C-HANDOVER-KEY          PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE SPACES.                
           05 L24C-COMPLETION-DATE       PIC X(10) VALUE SPACES.                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BM06F120      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-DATA       THRU 3000-EXIT                       
                       UNTIL F120-END-OF-FILE                                   
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN  INPUT  BM06F120                                                
                        BB24K010                                                
                 OUTPUT BP13L24C.                                               
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4)  DELIMITED BY SIZE                           
                  INTO WS-EDT-DATE.                                             
                                                                                
           MOVE  '55'         TO WS-MAX-LINES.                                  
           MOVE  '60'         TO WS-LINE-CNT.                                   
           MOVE WS-EDT-DATE   TO L24C-DATE.                                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BM06F120.                                                      
      *---------------------------------------------------------------*         
           READ BM06F120 AT END                                                 
                          MOVE 'Y' TO WS-F120-EOF                               
                          GO TO 2000-EXIT.                                      
                                                                                
           ADD    1    TO WS-F120-READ-CNT.                                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-DATA.                                                       
      *---------------------------------------------------------------*         
           MOVE SPACES         TO  K010-ALT-KEY1.                               
           MOVE F120-KEY-FLD   TO  K010-ALT-KEY1.                               
                                                                                
           READ BB24K010 KEY IS K010-ALT-KEY1.                                  
                                                                                
           IF K010-STATUS = 00                                                  
              IF K010-DTE-RESL-EFF NOT = SPACES AND LOW-VALUES                  
                 STRING K010-DTE-RESL-EFF(7:2) '/'                              
                        K010-DTE-RESL-EFF(5:2) '/'                              
                        K010-DTE-RESL-EFF(1:4)                                  
                        DELIMITED BY SIZE                                       
                        INTO WS-EFFECTIVE-DATE                                  
              ELSE                                                              
                 MOVE  SPACES   TO WS-EFFECTIVE-DATE                            
              END-IF                                                            
              IF K010-DTE-HANDOVER-KEY-APPTM NOT = SPACES AND LOW-VALUES        
                 STRING K010-DTE-HANDOVER-KEY-APPTM(7:2) '/'                    
                        K010-DTE-HANDOVER-KEY-APPTM(5:2) '/'                    
                        K010-DTE-HANDOVER-KEY-APPTM(1:4)                        
                        DELIMITED BY SIZE                                       
                        INTO WS-HANDOVER-KEY                                    
              ELSE                                                              
                 MOVE  SPACES   TO WS-HANDOVER-KEY                              
              END-IF                                                            
              IF K010-DTE-STATUS-UPDATE(17) NOT = SPACES AND LOW-VALUES         
                 MOVE K010-DTE-STATUS-UPDATE(17)  TO WS-COMPLETE-DATE           
                 STRING WS-COMPLETE-DATE(7:2) '/'                               
                        WS-COMPLETE-DATE(5:2) '/'                               
                        WS-COMPLETE-DATE(1:4)                                   
                        DELIMITED BY SIZE                                       
                        INTO WS-COMPLETION-DATE                                 
              ELSE                                                              
                 MOVE  SPACES   TO WS-COMPLETION-DATE                           
              END-IF                                                            
           ELSE                                                                 
              IF K010-STATUS = 23                                               
                 MOVE SPACES    TO WS-EFFECTIVE-DATE                            
                 MOVE SPACES    TO WS-HANDOVER-KEY                              
                 MOVE SPACES    TO WS-COMPLETION-DATE                           
              ELSE                                                              
                 DISPLAY 'ERROR READING - BB24K010 : ' K010-STATUS              
                 MOVE K010-STATUS     TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 4000-PRINT-REPORT   THRU 4000-EXIT                           
                                                                                
           PERFORM 2000-READ-BM06F120  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-PRINT-REPORT.                                                       
      *---------------------------------------------------------------*         
           IF (WS-LINE-CNT  >  WS-MAX-LINES)                                    
              ADD      1           TO WS-PAGE-CNT                               
              MOVE     0           TO WS-LINE-CNT                               
              MOVE WS-PAGE-CNT     TO L24C-PAGE                                 
              WRITE BP13L24C-REC   FROM L24C-HDR01 AFTER PAGE                   
              WRITE BP13L24C-REC   FROM L24C-HDR02                              
              WRITE BP13L24C-REC   FROM L24C-HDR03                              
              WRITE BP13L24C-REC   FROM L24C-HDR04 AFTER 2                      
              MOVE ALL '='         TO BP13L24C-REC                              
              WRITE BP13L24C-REC           AFTER 1                              
              ADD     05           TO WS-LINE-CNT                               
           END-IF.                                                              
                                                                                
           INITIALIZE   L24C-DETAIL.                                            
           MOVE   SPACES           TO L24C-DETAIL.                              
           ADD    1                TO WS-SLNO.                                  
           MOVE  WS-SLNO           TO L24C-SNO.                                 
           MOVE F120-REGN-NO       TO L24C-REGN-NO.                             
           MOVE F120-NUM-NRIC-HA1  TO L24C-NUM-NRIC-HA1                         
           MOVE WS-EFFECTIVE-DATE  TO L24C-EFFECTIVE-DATE.                      
           MOVE WS-HANDOVER-KEY    TO L24C-HANDOVER-KEY.                        
           MOVE WS-COMPLETION-DATE TO L24C-COMPLETION-DATE.                     
                                                                                
           MOVE F120-BLK-NO        TO WS-F120-BLK-NO.                           
           MOVE F120-LEVEL-NO      TO WS-F120-LEVEL-NO.                         
           MOVE F120-MAIN-UNIT     TO WS-F120-MAIN-UNIT.                        
           MOVE WS-F120-MAIN-UNIT  TO WS-STRING-TO-PARSE.                       
           MOVE LENGTH OF WS-F120-MAIN-UNIT                                     
                                   TO WS-STRING-LENGTH.                         
           MOVE 'Y'                TO WS-INSERT-SPACE-FLAG.                     
           PERFORM 4010-PARSE-ADDRESS   THRU 4010-EXIT.                         
           MOVE WS-PARSED-STRING   TO WS-F120-MAIN-UNIT.                        
           MOVE F120-SUB-UNIT      TO WS-F120-SUB-UNIT.                         
           MOVE F120-STREET-NAME   TO WS-F120-STREET-NAME.                      
           MOVE SPACES             TO WS-STRING-TO-PARSE.                       
           MOVE WS-F120-ADDRESS    TO WS-STRING-TO-PARSE.                       
           MOVE LENGTH OF WS-F120-ADDRESS                                       
                                   TO WS-STRING-LENGTH.                         
           MOVE 'Y'                TO WS-INSERT-SPACE-FLAG.                     
           PERFORM 4010-PARSE-ADDRESS   THRU 4010-EXIT.                         
           MOVE WS-PARSED-STRING   TO  L24C-ADDR.                               
                                                                                
           MOVE     SPACES         TO BP13L24C-REC.                             
           WRITE BP13L24C-REC      FROM L24C-DETAIL.                            
           ADD    1                TO WS-LINE-CNT.                              
           ADD    1                TO WS-L24C-WRITE-CNT.                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       4010-PARSE-ADDRESS.                                                      
      *--------------------------------------------------------------           
           MOVE SPACES                    TO   WS-PARSED-STRING.                
           MOVE 1                         TO   WS-CUR-POS-PS.                   
           ADD  1                         TO   WS-STRING-LENGTH.                
           PERFORM 4020-GET-CHAR          THRU 4020-EXIT                        
              VARYING WS-CUR-POS-SP FROM 1 BY 1                                 
              UNTIL WS-CUR-POS-SP = WS-STRING-LENGTH.                           
                                                                                
       4010-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4020-GET-CHAR.                                                           
      *---------------------------------------------------------------          
           IF WS-STRING-TO-PARSE(WS-CUR-POS-SP:1) NOT = SPACES                  
              IF WS-SPACE-FLAG = 'Y' AND                                        
                 WS-INSERT-SPACE-FLAG = 'Y' AND                                 
                 WS-PARSED-STRING NOT = SPACES                                  
                 MOVE ' ' TO WS-PARSED-STRING(WS-CUR-POS-PS:1)                  
                 ADD 1           TO  WS-CUR-POS-PS                              
              END-IF                                                            
              MOVE WS-STRING-TO-PARSE(WS-CUR-POS-SP:1)                          
                              TO  WS-PARSED-STRING(WS-CUR-POS-PS:1)             
              ADD 1           TO  WS-CUR-POS-PS                                 
              MOVE    'N'      TO WS-SPACE-FLAG                                 
           ELSE                                                                 
              MOVE 'Y'     TO WS-SPACE-FLAG                                     
           END-IF.                                                              
                                                                                
       4020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13C24C       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F120-READ-CNT    TO WS-EDIT1.                                
           DISPLAY 'NO OF RECS READ FROM BM06F120  : ' WS-EDIT1.                
           MOVE WS-L24C-WRITE-CNT  TO WS-EDIT1.                                 
           DISPLAY 'NO OF RECS WRITTEN TO BP13L24C : ' WS-EDIT1.                
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           CLOSE   BM06F120                                                     
                   BB24K010                                                     
                   BP13L24C.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
