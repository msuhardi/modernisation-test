       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C32E.                                                    
      *DATE-WRITTEN. 31 JAN 2019.                                               
      *AUTHOR.       DEEPIKA NALLAM.                                            
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) CREATE A MONTHLY REPORT ON THE CANCELLATION *            
      *                CASES WITH CANCELLATION REASON "LB".        *            
      *                                                            *            
      *  INPUT    :    BP13F800, BP13K022, BP13K024                *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP137652 31012019 DN8   NEW PROGRAM                        *            
      * BP138757 23072021 KV8   REPLACEING KATHIJAH TO SUZANNE     *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT BP13F800  ASSIGN          TO BP13F800                         
                            ACCESS MODE     IS SEQUENTIAL                       
                            FILE STATUS     IS WS-F800-STATUS.                  
      *                                                                         
           SELECT F800RPT   ASSIGN          TO F800RPT                          
                            ACCESS MODE     IS SEQUENTIAL                       
                            FILE STATUS     IS WS-RPT-STATUS.                   
      *                                                                         
           SELECT BP13K022  ASSIGN          TO BP13K022                         
                            ORGANIZATION    IS INDEXED                          
                            ACCESS MODE     IS DYNAMIC                          
                            RECORD KEY      IS K022-KEY-FLD                     
                            ALTERNATE KEY   IS K022-NUM-REGN                    
                            FILE STATUS     IS WS-K022-STATUS.                  
      *                                                                         
           SELECT BP13K024  ASSIGN          TO BP13K024                         
                            ORGANIZATION    IS INDEXED                          
                            ACCESS MODE     IS DYNAMIC                          
                            RECORD KEY      IS K024-KEY-FLD                     
                            FILE STATUS     IS WS-K024-STATUS.                  
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *                                                                         
       FD  BP13F800                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13F800.                                                           
      *                                                                         
       FD BP13K022.                                                             
       COPY BP13K022.                                                           
      *                                                                         
       FD BP13K024.                                                             
       COPY BP13K024.                                                           
      *                                                                         
       FD  F800RPT                                                              
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 100  CHARACTERS.                                     
       01 F800RPT-RECORD.                                                       
         05 RPT-SNO              PIC Z(05).                                     
         05 FILER               PIC X(01).                                      
         05 RPT-REGN-NUM        PIC X(08).                                      
         05 FILER1              PIC X(01).                                      
         05 RPT-CRM             PIC X(50).                                      
         05 FILER2              PIC X(01).                                      
         05 RPT-CANCEL-DTE      PIC X(08).                                      
         05 FILLER              PIC X(26).                                      
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *-----------------------------------------------------------------        
       01 WS-K022-STATUS        PIC 9(02).                                      
       01 WS-F800-STATUS        PIC 9(02).                                      
       01 WS-K024-STATUS        PIC 9(02).                                      
       01 WS-RPT-STATUS         PIC 9(02).                                      
       01 WS-MESSAGE            PIC X(80) VALUE SPACES.                         
       01 WS-RETURN-CODE        PIC X(02) VALUE SPACES.                         
       01 WS-KEY                PIC X(11) VALUE SPACES.                         
       01 WS-EOF                PIC X(01) VALUE 'N'.                            
       01 WS-FIRST-REPORT       PIC X(01) VALUE 'Y'.                            
       01 WS-RECORD-FOUND       PIC X(01) VALUE SPACES.                         
       01 REC-NOT-FOUND         PIC 9(08) VALUE ZEROES.                         
       01 WS-RPT-COUNT          PIC 9(05) VALUE ZEROES.                         
      *                                                                         
       01 WS-CUR-DATE.                                                          
          05  WS-CUR-CCYY                  PIC 9(04).                           
          05  WS-CUR-MM                    PIC 9(02).                           
          05  WS-CUR-DD                    PIC 9(02).                           
       01 WS-MONTH                         PIC X(3)  VALUE SPACES.              
       01 WS-MONTHS-IN-DIGITS-AND-WORDS.                                        
          05 FILLER                        PIC X(06) VALUE                      
             '01 JAN'.                                                          
          05 FILLER                        PIC X(06) VALUE                      
             '02 FEB'.                                                          
          05 FILLER                        PIC X(06) VALUE                      
             '03 MAR'.                                                          
          05 FILLER                        PIC X(06) VALUE                      
             '04 APR'.                                                          
          05 FILLER                        PIC X(06) VALUE                      
             '05 MAY'.                                                          
          05 FILLER                        PIC X(06) VALUE                      
             '06 JUN'.                                                          
          05 FILLER                        PIC X(06) VALUE                      
             '07 JUL'.                                                          
          05 FILLER                        PIC X(06) VALUE                      
             '08 AUG'.                                                          
          05 FILLER                        PIC X(06) VALUE                      
             '09 SEP'.                                                          
          05 FILLER                        PIC X(06) VALUE                      
             '10 OCT'.                                                          
          05 FILLER                        PIC X(06) VALUE                      
             '11 NOV'.                                                          
          05 FILLER                        PIC X(06) VALUE                      
             '12 DEC'.                                                          
       01 FILLER  REDEFINES  WS-MONTHS-IN-DIGITS-AND-WORDS.                     
          05 WS-MONTH-TABLE               OCCURS 12                             
                                           INDEXED BY WS-MTH-IND.               
              10 WS-MONTH-IN-DIGITS         PIC X(02).                          
              10 FILLER                     PIC X(01).                          
              10 WS-MONTH-IN-WORDS          PIC X(03).                          
      *                                                                         
       01 WS-HEADER1-RPT                PIC X(100) VALUE SPACES.                
      *                                                                         
       01 WS-MAIL-HEADER.                                                       
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(60)                               
      *        VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.           
               VALUE 'RCPT TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                    
           05  WS-MAIL-RPT4             PIC X(60)                               
               VALUE 'RCPT TO:<SERENA_JM_TEH@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT6             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT7             PIC X(29)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT8             PIC X(60)                               
      *        VALUE 'TO:<KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.                
               VALUE 'TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                         
           05  WS-MAIL-RPT9             PIC X(60)                               
               VALUE 'TO:<SERENA_JM_TEH@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT10            PIC X(90) VALUE SPACES.                 
           05  WS-MAIL-RPT11            PIC X(41)                               
               VALUE 'Mime-Version: 1.0                        '.               
           05  WS-MAIL-RPT12            PIC X(80)                               
               VALUE 'Content-type: multipart/mixed; boundary="Mime.Part        
      -         '.Boundary"'.                                                   
           05  WS-MAIL-RPT13            PIC X(80)                               
               VALUE 'This is a multi-part message in MIME format.'.            
           05  WS-MAIL-RPT14            PIC X(80)                               
               VALUE '--Mime.Part.Boundary                        '.            
           05  WS-MAIL-RPT15            PIC X(80)                               
               VALUE 'Content-Type: text/plain; charset="iso-8859-1"'.          
           05  WS-MAIL-RPT16            PIC X(80)                               
               VALUE 'Content-Transfer-Encoding: 7bit               '.          
           05  WS-MAIL-RPT17            PIC X(80)                               
               VALUE 'Content-disposition: attachment;              '.          
           05  WS-MAIL-RPT18            PIC X(80)                               
               VALUE '           FILENAME="CANCELLATION_LBS.TXT"    '.          
      *                                                                         
      *===================*                                                     
       PROCEDURE DIVISION.                                                      
      *===================*                                                     
       0000-MAIN-PARA.                                                          
      *-------------------*                                                     
      *                                                                         
           PERFORM 1000-OPEN-PARA.                                              
           PERFORM 2000-PROCESS-PARA  UNTIL WS-EOF = 'Y'.                       
           PERFORM 4000-CLOSE-PARA.                                             
      *                                                                         
           STOP RUN.                                                            
      *                                                                         
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------               
       1000-OPEN-PARA.                                                          
      *----------------------------------------------------------               
           OPEN INPUT BP13F800                                                  
                      BP13K022                                                  
                      BP13K024                                                  
               OUTPUT F800RPT                                                   
      *                                                                         
           DISPLAY WS-K022-STATUS ';' WS-K024-STATUS ';'                        
      *                                                                         
           MOVE FUNCTION CURRENT-DATE(1:8)        TO WS-CUR-DATE                
           SEARCH WS-MONTH-TABLE                                                
               AT END                                                           
               MOVE '   ' TO WS-MONTH                                           
            WHEN WS-MONTH-IN-DIGITS(WS-MTH-IND) = WS-CUR-MM                     
               MOVE WS-MONTH-IN-WORDS(WS-MTH-IND) TO WS-MONTH                   
           END-SEARCH.                                                          
      *                                                                         
           EVALUATE TRUE                                                        
      *                                                                         
             WHEN  WS-K022-STATUS NOT EQUAL TO 00                               
              MOVE 'ERROR OPENING THE BP13K022   ' TO WS-MESSAGE                
              MOVE WS-K022-STATUS TO WS-RETURN-CODE                             
              PERFORM 5000-ERROR-PARA THRU 5000-EXIT                            
      *                                                                         
             WHEN  WS-K024-STATUS NOT EQUAL TO 00                               
              MOVE 'ERROR OPENING THE BP13K024   ' TO WS-MESSAGE                
              MOVE WS-K024-STATUS TO WS-RETURN-CODE                             
              PERFORM 5000-ERROR-PARA THRU 5000-EXIT                            
      *                                                                         
             WHEN  OTHER                                                        
              DISPLAY 'SUCCESSFUL IN OPENING THE FILES'                         
      *                                                                         
           END-EVALUATE.                                                        
      *                                                                         
       1000-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------               
       2000-PROCESS-PARA.                                                       
      *----------------------------------------------------------               
           READ BP13F800                                                        
              AT END MOVE 'Y' TO WS-EOF                                         
              NOT AT END                                                        
              IF F800-NUM-CANCEL-REASON = 'LB'                                  
                ADD     1                     TO WS-RPT-COUNT                   
                PERFORM 2200-READ-BP13K022  THRU 2200-EXIT                      
                PERFORM 2300-READ-BP13K024  THRU 2300-EXIT                      
                PERFORM 2400-CREATE-REPORT  THRU 2400-EXIT                      
              END-IF                                                            
           END-READ.                                                            
      *                                                                         
       2000-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------               
       2200-READ-BP13K022.                                                      
      *----------------------------------------------------------               
              INITIALIZE BP13K022-REC                                           
              MOVE F800-NUM-REGN       TO K022-NUM-REGN                         
              READ BP13K022                                                     
               KEY IS K022-NUM-REGN                                             
               MOVE   K022-NUM-REGN    TO  WS-KEY                               
      *                                                                         
            EVALUATE TRUE                                                       
              WHEN WS-K022-STATUS = 00                                          
                CONTINUE                                                        
                                                                                
                WHEN WS-K022-STATUS = 23                                        
                 MOVE 'N' TO WS-RECORD-FOUND                                    
                 ADD   1  TO REC-NOT-FOUND                                      
                 DISPLAY 'RECORD NOT FOUND FOR REGN IN K022 : ' WS-KEY          
      *                                                                         
                WHEN OTHER                                                      
                 MOVE 'ERROR READING THE BP13K022' TO WS-MESSAGE                
                 MOVE WS-K022-STATUS TO WS-RETURN-CODE                          
                 PERFORM 5000-ERROR-PARA THRU 5000-EXIT                         
      *                                                                         
            END-EVALUATE.                                                       
      *                                                                         
       2200-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------               
       2300-READ-BP13K024.                                                      
      *----------------------------------------------------------               
              INITIALIZE BP13K024-REC                                           
              MOVE K022-NUM-OIC        TO K024-NUM-OIC                          
              READ BP13K024                                                     
               KEY IS K024-KEY-FLD                                              
               MOVE   K024-NUM-OIC     TO  WS-KEY                               
      *                                                                         
            EVALUATE TRUE                                                       
              WHEN WS-K024-STATUS = 00                                          
                CONTINUE                                                        
                                                                                
                WHEN WS-K024-STATUS = 23                                        
                 MOVE 'N' TO WS-RECORD-FOUND                                    
                 ADD   1  TO REC-NOT-FOUND                                      
                 DISPLAY 'RECORD NOT FOUND FOR REGN IN K024 : ' WS-KEY          
      *                                                                         
                WHEN OTHER                                                      
                 MOVE 'ERROR READING THE BP13K024' TO WS-MESSAGE                
                 MOVE WS-K024-STATUS TO WS-RETURN-CODE                          
                 PERFORM 5000-ERROR-PARA THRU 5000-EXIT                         
      *                                                                         
            END-EVALUATE.                                                       
      *                                                                         
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------               
       2400-CREATE-REPORT.                                                      
      *----------------------------------------------------------               
            INITIALIZE  F800RPT-RECORD                                          
            IF WS-FIRST-REPORT = 'Y'                                            
               WRITE F800RPT-RECORD FROM WS-MAIL-RPT1                           
               WRITE F800RPT-RECORD FROM WS-MAIL-RPT2                           
               WRITE F800RPT-RECORD FROM WS-MAIL-RPT3                           
               WRITE F800RPT-RECORD FROM WS-MAIL-RPT4                           
               WRITE F800RPT-RECORD FROM WS-MAIL-RPT6                           
               WRITE F800RPT-RECORD FROM WS-MAIL-RPT7                           
               WRITE F800RPT-RECORD FROM WS-MAIL-RPT8                           
               WRITE F800RPT-RECORD FROM WS-MAIL-RPT9                           
               STRING "SUBJECT : MONTHLY REPORT ON CANCELLATION CASE"           
                  "S WITH CANCEL REASON LB   "                                  
                    DELIMITED BY SIZE INTO WS-MAIL-RPT10                        
               END-STRING                                                       
               WRITE F800RPT-RECORD FROM WS-MAIL-RPT10                          
               STRING WS-CUR-DD '/' WS-CUR-MM '/' WS-CUR-CCYY                   
                      DELIMITED BY SIZE INTO WS-DATE-RPT                        
               WRITE F800RPT-RECORD FROM WS-DATE-RPT-DTL                        
               WRITE F800RPT-RECORD FROM WS-MAIL-RPT11                          
               WRITE F800RPT-RECORD FROM WS-MAIL-RPT12                          
               MOVE SPACES TO  F800RPT-RECORD                                   
               WRITE   F800RPT-RECORD                                           
               WRITE F800RPT-RECORD FROM WS-MAIL-RPT13                          
               WRITE F800RPT-RECORD FROM WS-MAIL-RPT14                          
               WRITE F800RPT-RECORD FROM WS-MAIL-RPT15                          
               WRITE F800RPT-RECORD FROM WS-MAIL-RPT16                          
               WRITE F800RPT-RECORD FROM WS-MAIL-RPT17                          
               WRITE F800RPT-RECORD FROM WS-MAIL-RPT18                          
               MOVE    'N'             TO WS-FIRST-REPORT                       
               MOVE SPACES             TO F800RPT-RECORD                        
               WRITE F800RPT-RECORD                                             
               STRING "REPORT ON CANCELLATION OF FLAT APPLICATION TO"           
                  " TAKE UP LBS FOR MONTH OF " WS-MONTH                         
                    DELIMITED BY SIZE INTO WS-HEADER1-RPT                       
               END-STRING                                                       
               WRITE F800RPT-RECORD FROM WS-HEADER1-RPT                         
               MOVE SPACES             TO F800RPT-RECORD                        
               WRITE F800RPT-RECORD                                             
               MOVE 'S.NO ;REGN NUM;         CRM                                
      -      '              ;CANCEL DTE' TO F800RPT-RECORD                      
               WRITE F800RPT-RECORD                                             
             END-IF.                                                            
               MOVE WS-RPT-COUNT       TO RPT-SNO.                              
               MOVE ';'                TO FILER FILER1 FILER2.                  
               MOVE F800-NUM-REGN      TO RPT-REGN-NUM.                         
               MOVE F800-DTE-CANCEL    TO RPT-CANCEL-DTE.                       
               STRING '(' K022-NUM-OIC ') ' K024-NME-OIC                        
                    '(T' K024-CDE-TEAM ')'                                      
                   DELIMITED BY SIZE INTO RPT-CRM                               
               END-STRING.                                                      
               WRITE F800RPT-RECORD.                                            
      *                                                                         
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------               
       4000-CLOSE-PARA.                                                         
      *----------------------------------------------------------               
           CLOSE BP13K022                                                       
                 BP13K024                                                       
                 BP13F800                                                       
                 F800RPT                                                        
      *                                                                         
           DISPLAY WS-K022-STATUS ';' WS-K024-STATUS ';'                        
      *                                                                         
                EVALUATE TRUE                                                   
      *                                                                         
                  WHEN  WS-K022-STATUS NOT EQUAL TO 00                          
                   MOVE 'ERROR CLOSING THE BP13K022   ' TO WS-MESSAGE           
                   MOVE WS-K022-STATUS TO WS-RETURN-CODE                        
                   PERFORM 5000-ERROR-PARA THRU 5000-EXIT                       
      *                                                                         
                  WHEN  WS-K024-STATUS NOT EQUAL TO 00                          
                   MOVE 'ERROR CLOSING THE BP13K024   ' TO WS-MESSAGE           
                   MOVE WS-K024-STATUS TO WS-RETURN-CODE                        
                   PERFORM 5000-ERROR-PARA THRU 5000-EXIT                       
      *                                                                         
                  WHEN  OTHER                                                   
                   DISPLAY 'SUCCESSFUL IN CLOSING THE FILES'                    
      *                                                                         
                END-EVALUATE.                                                   
      *                                                                         
       4000-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------               
       5000-ERROR-PARA.                                                         
      *----------------------------------------------------------               
            DISPLAY WS-MESSAGE ';' WS-RETURN-CODE.                              
            MOVE 'Y' TO WS-EOF.                                                 
      *                                                                         
       5000-EXIT.                                                               
           EXIT.                                                                
      *                                                                         
