      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CY92.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  09/09/08.                                                 
      *****************************************************************         
      *SIMILAR TO BP13CB71, ADD CUT-OFF-DATE = 200901                 *         
      *           USE IN BP13AYOH, BP13AY0J                           *         
      *OBJECTIVE - COMPUTE NUMBER OF UNSUCC ATTEMPT BEFORE THE        *         
      *            APPLICATION IS SUCCESSFUL.                         *         
      *            SUCCESSFUL - SCH-ACC-NO NOT = BLANK                *         
      *            K800-ALLOC-TAG = FN, NI, SI, RI, RG, NE            *         
      *            K595-BOOK-STATUS = WF, BF, LF                      *         
      *                                                               *         
      * E.G XX/XXX - NF                                               *         
      *     XX/XXX - NF                                               *         
      *     XX/XXX - FN OR WF                                         *         
      *     XX/XXX - NF                                               *         
      *          ======> SYSTEM WILL COUNT AS 3 UNSUCC ATTEMPTS       *         
      *                                                               *         
      *     INPUT FILE : 1. BP13F595                                  *         
      *                                                               *         
      *     OUTPUT FILE: 1. P13F595A                                  *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP133792 09/09/2008 JB8  NEW PROGRAM.                         *         
      * BP133835 02/07/2010 JB8  TO RECTIFY OUTPUT                    *         
      * BP133954 12/08/2010 PCL3 TO CATER FOR BP13AY0F JOB            *         
      * BP134011 21/10/2010 LSB3 TO CATER FOR BP13AY0F JOB            *         
      * BP134255 07/07/2011 LSB3 CATER FOR FN CODE, ALLOC-TAG NOT FD  *         
      *                          GET BOOK-STATUS, BK-CNT, NE-CNT      *         
      *                          FN-CNT                               *         
      * BP134598 17/07/2012 LSB1 CHANGE 2009 TO 2011                  *         
      * BP134780 08/01/2013 IMC1 REMOVE CUTOFF HARDCODING             *         
      * BP134980 02/07/2013 IMC1 MOVE THE 2009-BKFD TAG TO OUTPUT     *         
      * BP134980 15/07/2013 IMC1 CATER FOR 'YK' JOB                   *         
      * BP135039 16/12/2013 IMC1 ADD NEW BOOK-STATUS CODES            *         
      * BP135190 04/12/2013 SMR2 EXPAND BP13K813 TO 1000              *         
      * BP135237 02/03/2014 LSB1 CHG CUTOFF TO 201101                 *         
      * BP135410 07/07/2014 LSB1 CHG HARDCODE 2011 TO F205-DTE-ALLOCN *         
      * BP135635 28/01/2015 LSB1 CATER FOR FTS - BF, LB, WB BOOK-ST   *         
      * BP135944 15/09/2015 SMR2 REMOVE REFERENCE TO CDE-NT6/7/8      *         
      * BP136740 11/08/2017 LSB1 INCLUDE SFS FOR BOOK-STATUS = LB, WB *         
      * BP136866 14/08/2017 FNP1 CATER FOR SAP CASES                  *         
      * BP137247 26/03/2018 LSB1 ADD 'YL'                                       
      * BP137918 10/09/2019 KV6  CATER FOR REMOVAL OF DTE-DEFER-EXPIRY*         
      *                          AND NUM-DEFER-ZONE-FT                *         
      * BP138080 04/11/2019 SMP3 CATER FOR BOOK STATUS = 'HL'         *         
      * BP138080 28/01/2020 SMP3 CATER FOR BOOK STATUS = 'HS'         *         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205  ASSIGN      TO BP13F205.                            
           SELECT BP13F595  ASSIGN      TO BP13F595.                            
                                                                                
           SELECT BP13K813  ASSIGN        TO BP13K813                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K813-KEY-FLD                       
                            FILE STATUS   IS WS-K813-STATUS.                    
                                                                                
           SELECT P13F595A ASSIGN       TO P13F595A.                            
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F205                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F205.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13F595                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01  BP13F595-REC.                                                        
           05  F595-NUM-RANDOM                    PIC X(28).                    
           05  F595-NUM-RANDOM-PRIORITY           PIC X(2).                     
           05  FILLER                             PIC X(2).                     
           05  F595-NUM-REF                       PIC X(8).                     
           05  F595-ALT1-KEY.                                                   
               10  F595-NUM-NRIC1                 PIC X(9).                     
           05  F595-ALT3-KEY.                                                   
               10  F595-NUM-NRIC3                 PIC X(9).                     
           05  F595-ALT4-KEY.                                                   
               10  F595-NUM-NRIC4                 PIC X(9).                     
           05  F595-NUM-MTH-RESIDE                PIC 9(3).                     
           05  F595-NUM-LOW-FLOOR                 PIC X(1).                     
           05  F595-NUM-PREV-REF                  PIC X(6).                     
           05  F595-CDE-ZONE                      PIC X(2).                     
           05  F595-CDE-COHORT                    PIC X(2).                     
           05  F595-ALT2-KEY.                                                   
               10  F595-NUM-NRIC2                 PIC X(9).                     
           05  F595-NUM-SHORTLIST-PA-HHTY         PIC X(5).                     
           05  F595-NUM-SHORTLIST-PA-CAT          PIC X(5).                     
           05  F595-TME-QUEUE                     PIC X(2).                     
           05  F595-NUM-SHORTLIST-PRIO-HHTY       PIC X(5).                     
           05  F595-NUM-SHORTLIST-PRIO-CAT        PIC X(5).                     
           05  F595-CDE-NT-WITH-LAND              PIC X(1).                     
           05  F595-NUM-WAIT-TIME                 PIC X(2).                     
           05  F595-CDE-HOUSEHOLD                 PIC X(1).                     
           05  F595-NUM-TWO-ROOMER                PIC X(1).                     
           05  F595-NUM-MCPS                      PIC X(1).                     
           05  F595-CDE-SERVE                     PIC X(1).                     
           05  F595-NUM-HOUSEHOLD-FREEZE          PIC X(1).                     
           05  F595-CDE-RESIDENT-HA3              PIC X(1).                     
           05  F595-CDE-RESIDENT-HA4              PIC X(1).                     
           05  F595-NUM-REGN                      PIC X(8).                     
           05  F595-NUM-SEQUENCE                  PIC X(8).                     
           05  F595-NUM-CUR-REGN                  PIC X(8).                     
           05  F595-NUM-EXIST-REGN                PIC X(8).                     
           05  F595-NUM-JB-PARENT-REGN            PIC X(8).                     
           05  F595-NUM-GRO-RANK                  PIC X(2).                     
           05  F595-TME-PIGEONHOLE                PIC 9(2).                     
           05  F595-TME-QUEUE-PRIO-IFPA           PIC X(2).                     
           05  F595-NUM-HHTY-QUEUE.                                             
               10  F595-NUM-HHTY-QUEUE-APLHA      PIC X(1).                     
               10  F595-NUM-HHTY-QUEUE-SERIAL     PIC X(5).                     
           05  F595-NUM-5YR-OCCUPN                PIC X(1).                     
           05  F595-CDE-JB-PARENT                 PIC X(1).                     
           05  F595-NUM-JB-REGN                   PIC X(8).                     
           05  F595-CDE-EXIST-FLAT-TYPE           PIC X(2).                     
           05  F595-CDE-NT1                       PIC X(3).                     
           05  F595-CDE-NT2                       PIC X(3).                     
           05  F595-CDE-NT3                       PIC X(3).                     
           05  F595-CDE-NT4                       PIC X(3).                     
           05  F595-CDE-NT5                       PIC X(3).                     
           05  FILLER                             PIC X(9).                     
           05  F595-CDE-FLAT-TYPE                 PIC X(2).                     
           05  F595-CDE-ALLOC-SCH                 PIC X(3).                     
           05  F595-CDE-JB                        PIC X(1).                     
           05  F595-NUM-MTH-RES-JB-IND            PIC 9(3).                     
           05  F595-NUM-MTH-SUBLET                PIC 9(3).                     
           05  F595-NUM-JB-CNT                    PIC 9(2).                     
           05  F595-CDE-CATEGORY                  PIC X(1).                     
           05  F595-CDE-SUCC-NT-ZONE              PIC X(3).                     
           05  F595-CDE-UNSUCC                    PIC X(1).                     
           05  F595-NUM-BALLOT-POSITION           PIC 9(5).                     
           05  F595-NUM-NT-FT-QUEUE               PIC X(5).                     
           05  F595-NUM-PROV-TAG                  PIC X(1).                     
           05  F595-NUM-PROV-REASON               PIC X(2).                     
           05  FILLER                             PIC X(5).                     
           05  F595-CDE-ELIG-SCH                  PIC X(3).                     
           05  F595-NUM-UNSUCC-REQUEST            PIC X(2).                     
           05  F595-CDE-RESIDENT-HA1              PIC X(1).                     
           05  F595-CDE-RESIDENT-HA2              PIC X(1).                     
           05  F595-CDE-PARENT-CHILD              PIC X(1).                     
           05  F595-CDE-BO                        PIC X(2).                     
           05  F595-CDE-ELIG-TAG                  PIC X(2).                     
           05  F595-CDE-REQUEST-STATUS            PIC X.                        
           05  F595-DTE-REQUEST                   PIC X(8).                     
           05  FILLER                             PIC X(8).                     
           05  F595-NUM-2ND-ALLOC-SCH             PIC X(3).                     
           05  F595-NUM-JB-QUEUE-SEQUENCE         PIC X(8).                     
           05  F595-NUM-REQUEST-REASON            PIC X(2).                     
           05  F595-NUM-BOOK-STATUS               PIC X(02).                    
           05  F595-NUM-USER-ID                   PIC X(8).                     
           05  F595-CDE-PRINT-STATUS              PIC X(1).                     
           05  F595-NUM-SHORTLIST-QUEUE.                                        
               10  F595-NUM-SLIST-PA-ZNFT         PIC X(05).                    
               10  F595-NUM-SLIST-PA-ZNFT-CAT     PIC X(05).                    
               10  F595-NUM-SLIST-PRIO-ZNFT       PIC X(05).                    
               10  F595-NUM-SLIST-PRIO-ZNFT-CAT   PIC X(05).                    
           05  F595-NUM-ASSESSMENT.                                             
               10  F595-NUM-CA-TAG                PIC X(01).                    
               10  F595-NUM-SPR-TAG               PIC X(01).                    
               10  F595-NUM-SUBSIDISED-LOAN       PIC X(01).                    
               10  F595-NUM-CAP                   PIC X(01).                    
           05  F595-NUM-RES-NRIC1                 PIC X(09).                    
           05  F595-DTE-BALLOT                    PIC X(6).                     
           05  F595-NUM-JB-WITHIN-PA-RANGE        PIC X(1).                     
           05  F595-NUM-JB-BETTER-QUEUE           PIC X(1).                     
           05  F595-CDE-IDP-SCH                   PIC X(3).                     
           05  F595-TME-QUEUE-PREVQTR             PIC X(02).                    
           05  F595-NUM-PPO                       PIC X(01).                    
           05  F595-CDE-NT-ASSIGN                 PIC X(3).                     
           05  F595-CDE-REJECT                    PIC X(1).                     
           05  F595-CDE-UPD-AFTER-BALLOT          PIC X(1).                     
           05  F595-NUM-HA-OPT                    PIC X(01).                    
           05  F595-NUM-JB-REF                    PIC X(06).                    
           05  F595-NUM-DEBAR                     PIC X(2).                     
           05  F595-DTE-1ST-LETTER                PIC X(8).                     
           05  F595-NUM-OWN-PTE-PRTY              PIC X(1).                     
           05  F595-NUM-DISPOSED-PTE-PRTY         PIC X(1).                     
           05  F595-NUM-APPLN-TYPE                PIC X(1).                     
           05  F595-NUM-WARD                      PIC X(3).                     
           05  F595-NUM-ALLO-CAT                  PIC X(3).                     
           05  F595-NUM-RES-NRIC2                 PIC X(9).                     
           05  F595-NUM-LOAN-TAG                  PIC X(1).                     
           05  F595-NUM-PREM-CDE                  PIC X(3).                     
           05  F595-NUM-MORT-LOAN-POLICY          PIC X(2).                     
           05  F595-NUM-ELIG-LOAN-POLICY          PIC X(2).                     
           05  F595-NUM-ORIG-LOAN-TAG             PIC X(1).                     
           05  F595-NUM-SMS-STATUS                PIC X(1).                     
           05  F595-NUM-SMS-FT                    PIC X(2).                     
           05  F595-NUM-RSL-STATUS                PIC X(1).                     
           05  F595-NUM-RSL-FT                    PIC X(2).                     
           05  F595-NUM-RDB-STATUS                PIC X(1).                     
           05  F595-NUM-RDB-FT                    PIC X(2).                     
           05  F595-NUM-APPLN-SERIAL              PIC X(5).                     
           05  F595-NUM-JB-NRIC                   PIC X(9).                     
           05  F595-CDE-REQ-ALLOC-SCH             PIC X(3).                     
           05  F595-NUM-BUYBACK                   PIC X(1).                     
           05  F595-CDE-DIFF-ETHNIC               PIC X(1).                     
           05  F595-DTE-PRPSD-SALE                PIC X(8).                     
           05  F595-NUM-PORT-ELIG-TAG             PIC X(1).                     
           05  F595-NUM-MCPS-POSTAL               PIC X(6).                     
           05  F595-CDE-SPL-APPRV-TYPE            PIC X(3).                     
           05  F595-CDE-BTO-REOPT                 PIC X(1).                     
           05  F595-NUM-AHG-REQ                   PIC X(1).                     
           05  F595-NUM-HSGNT-REQ                 PIC X(1).                     
           05  F595-NUM-APPL-LOAN-REQ             PIC X(1).                     
           05  F595-DTE-BK-APPT                   PIC X(8).                     
           05  F595-TME-BK-APPT                   PIC X(4).                     
           05  F595-NUM-RESERVE-STATUS            PIC X(1).                     
           05  F595-NUM-ABORT                     PIC X(1).                     
           05  F595-CDE-BALLOT-HOUSEHOLD          PIC X(1).                     
           05  F595-CDE-CITIZENSHIP-HOUSEHOLD     PIC X(3).                     
           05  FILLER                             PIC X(11).                    
           05  F595-NUM-SCH-ACC                   PIC X(09).                    
           05  F595-DTE-UPDATE                    PIC X(08).                    
           05  F595-NUM-ALLOC-TAG                 PIC X(02).                    
           05  FILLER                             PIC X(06).                    
                                                                                
       FD   P13F595A                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   P13F595A-REC.                                                       
            05  FILLER                    PIC X(464).                           
            05  F595A-NUM-BE-CNT          PIC 9(02).                            
            05  F595A-NUM-BTO-CNT         PIC 9(02).                            
            05  F595A-NUM-BK-TAG          PIC X(01).                            
            05  F595A-NUM-BK-CNT          PIC 9(02).                            
            05  F595A-NUM-REST-CNT        PIC 9(02).                            
            05  F595A-NUM-BTO-ADD         PIC X(02).                            
            05  F595A-NUM-SCH-ACC         PIC X(09).                            
            05  F595A-NUM-FN-CNT          PIC X(02).                            
            05  F595A-NUM-STAT-S          PIC X(02).                            
            05  F595A-NUM-2009-BKFND      PIC X(01).                            
            05  F595A-NUM-BTO-CNT-COMP    PIC X(02).                            
            05  F595A-NUM-BUY-STATUS      PIC X(01).                            
            05  F595A-NUM-ALLOC-TAG       PIC X(02).                            
            05  F595A-NUM-NE-CNT          PIC 9(02).                            
            05  F595A-NUM-BK-ADD          PIC 9(02).                            
            05  F595A-NUM-HARDLUCK        PIC 9(02).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F595             PIC X(1)  VALUE 'N'.                     
           05  WS-K813-STATUS          PIC 9(2)  VALUE ZEROS.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F595-READ            PIC 9(07) VALUE ZEROES.                  
           05  WS-F595A-WRITE          PIC 9(07) VALUE ZEROES.                  
           05  WS-NRIC-CNT             PIC 9(02) VALUE ZEROES.                  
           05  WS-BK-CNT               PIC 9(02) VALUE ZEROES.                  
           05  WS-BE-CNT               PIC 9(02) VALUE ZEROES.                  
           05  WS-BTO-CNT              PIC 9(02) VALUE ZEROES.                  
           05  WS-REST-CNT             PIC 9(02) VALUE ZEROES.                  
           05  WS-NE-CNT               PIC 9(02) VALUE ZEROES.                  
           05  WS-FN-CNT               PIC 9(02) VALUE ZEROES.                  
           05  WS-TOTAL-CNT            PIC S9(02) VALUE ZEROES.                 
           05  WS-HARDLUCK             PIC 9(02) VALUE ZEROES.                  
           05  WS-NRIC-NOTBK-CNT       PIC 9(07) VALUE ZEROES.                  
           05  WS-NRIC-BK-CNT          PIC 9(07) VALUE ZEROES.                  
                                                                                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-SYSTEM-DATE          PIC 9(08) VALUE ZEROES.                  
           05  WS-NUM-NRIC             PIC X(09) VALUE SPACES.                  
           05  WS-BK-TAG               PIC X(01) VALUE 'N'.                     
           05  WS-BYPASS-NOTBK         PIC X(01) VALUE SPACES.                  
           05  WS-K813-BYPASS          PIC X(01) VALUE SPACES.                  
           05  WS-BUY-STATUS           PIC X(01) VALUE SPACES.                  
           05  WS-F595-CUR-REC         PIC X(500).                              
           05  WS-F595-BK-REC          PIC X(500).                              
           05  WS-F595-NOTBK-REC       PIC X(500).                              
           05  WS-A2009-BKFD           PIC X(1)  VALUE SPACES.                  
           05  WS-A2009-NOTBK-FD       PIC X(1)  VALUE SPACES.                  
           05  WS-SUCC                 PIC X(1)  VALUE SPACES.                  
           05  WS-HH                   PIC X(1)  VALUE SPACES.                  
           05  WS-HH-B                 PIC X(1)  VALUE SPACES.                  
           05  WS-BOOKED               PIC X(1)  VALUE SPACE.                   
               88  BOOKED                        VALUE 'Y'.                     
           05  WS-1ST-UNSUCC           PIC X(6)  VALUE SPACES.                  
           05  WS-RESET-FOUND          PIC X(1)  VALUE SPACES.                  
           05  WS-SBF-HH               PIC X(1)  VALUE SPACES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-READ-BP13F205      THRU 1100-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F595      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F595 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F595                                                 
                       BP13F205                                                 
                       BP13K813                                                 
              OUTPUT   P13F595A.                                                
                                                                                
           IF WS-K813-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'ERROR OPENING - BP13K813 : ' WS-K813-STATUS              
              MOVE     WS-K813-STATUS            TO RETURN-CODE                 
              PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT                   
           END-IF.                                                              
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1100-READ-BP13F205.                                                      
      ******************************************************************        
                                                                                
           READ BP13F205           AT END                                       
                GO                 TO 9999-CLOSE-ROUTINE.                       
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F595.                                                      
      ******************************************************************        
                                                                                
           READ BP13F595           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F595                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-F595-READ.                           
                                                                                
           IF F595-NUM-ALLOC-TAG = SPACES OR '00'                               
              IF F595-NUM-BOOK-STATUS NOT = SPACES                              
                 MOVE F595-NUM-BOOK-STATUS TO                                   
                      F595-NUM-ALLOC-TAG                                        
              END-IF                                                            
           END-IF.                                                              
           MOVE ZEROS  TO F595-DTE-UPDATE.                                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE F595-NUM-NRIC1                 TO WS-NUM-NRIC.                  
           MOVE 'N'                            TO WS-BK-TAG.                    
           MOVE ZEROES                         TO WS-BK-CNT                     
                                                  WS-BE-CNT                     
                                                  WS-BTO-CNT                    
                                                  WS-REST-CNT                   
                                                  WS-NRIC-CNT.                  
           MOVE SPACES                         TO WS-HH WS-HH-B.                
           MOVE ZEROES                         TO WS-NE-CNT.                    
           MOVE ZEROES                         TO WS-FN-CNT.                    
           MOVE ZEROES                         TO WS-HARDLUCK.                  
           MOVE SPACES                         TO WS-F595-NOTBK-REC.            
           MOVE SPACES                         TO WS-BUY-STATUS.                
           MOVE SPACES                         TO WS-A2009-BKFD.                
           MOVE SPACES                         TO WS-A2009-NOTBK-FD.            
           MOVE SPACES                         TO WS-1ST-UNSUCC.                
           MOVE SPACES                         TO WS-RESET-FOUND.               
           MOVE ZEROES                         TO WS-TOTAL-CNT.                 
                                                                                
           PERFORM 3500-PROCEED-SAME-NRIC    THRU 3500-EXIT                     
             UNTIL F595-NUM-NRIC1 NOT = WS-NUM-NRIC                             
                OR WS-EOF-F595 = 'Y'.                                           
      *                                                                         
      * AY0H                                                                    
      * IF FOUND LAST SBF AS 'H', LAST BTO AS 'G', TO INCLUDE ALL BTO           
      * IF FOUND LAST SBF AS 'G', LAST BTO AS 'H', TO DROP ALL BTO              
      *                                                                         
      * AY0J                                                                    
      * IF FOUND LAST SBF AS 'G', LAST BTO AS 'H', TO INCLUDE ALL BTO           
      * IF FOUND LAST SBF AS 'H', LAST BTO AS 'G', TO DROP ALL BTO              
      *                                                                         
      *                                                                         
           IF F205-NUM-OPTION = 'YA' OR 'YH' OR 'YL'                            
              IF WS-HH = 'G' OR WS-HH-B = 'G'                                   
                 IF WS-SBF-HH = 'H' OR 'T'                                      
                    MOVE 'H' TO WS-HH                                           
                    MOVE 'H' TO WS-HH-B                                         
                 END-IF                                                         
              ELSE                                                              
                 IF WS-SBF-HH = 'G'                                             
                    MOVE 'G' TO WS-HH                                           
                    MOVE 'G' TO WS-HH-B                                         
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F205-NUM-OPTION = 'YF' OR 'YJ'                                    
              IF (WS-HH = 'H' OR 'T') OR (WS-HH-B = 'H' OR 'T')                 
                 IF WS-SBF-HH = 'G'                                             
                    MOVE 'G' TO WS-HH                                           
                    MOVE 'G' TO WS-HH-B                                         
                 END-IF                                                         
              ELSE                                                              
                 IF WS-SBF-HH = 'H'                                             
                    MOVE 'H' TO WS-HH                                           
                    MOVE 'H' TO WS-HH-B                                         
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F205-NUM-OPTION = 'YA' OR 'YH' OR 'YL'                            
              IF WS-HH = 'G' OR WS-HH-B = 'G'                                   
                 GO TO 3000-EXIT                                                
              ELSE                                                              
                 IF WS-BK-TAG = 'Y'                                             
                    PERFORM 5000-WRITE-BK       THRU 5000-EXIT                  
                 ELSE                                                           
                    PERFORM 4000-WRITE-NOTBK    THRU 4000-EXIT                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F205-NUM-OPTION = 'YF' OR 'YJ'                                    
              IF WS-HH-B = SPACES OR LOW-VALUES                                 
                 IF (WS-HH = 'H' OR 'T')                                        
                    GO TO 3000-EXIT                                             
                 END-IF                                                         
              ELSE                                                              
                 IF WS-HH-B = 'H' OR 'T'                                        
                    GO TO 3000-EXIT                                             
                 END-IF                                                         
              END-IF                                                            
              IF WS-BK-TAG = 'Y'                                                
                 PERFORM 5000-WRITE-BK       THRU 5000-EXIT                     
              ELSE                                                              
                 PERFORM 4000-WRITE-NOTBK    THRU 4000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F205-NUM-OPTION = 'YK'                                            
              IF WS-BK-TAG = 'Y'                                                
                  PERFORM 5000-WRITE-BK       THRU 5000-EXIT                    
              ELSE                                                              
                  PERFORM 4000-WRITE-NOTBK    THRU 4000-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3500-PROCEED-SAME-NRIC.                                                  
      ****************************************************************          
                                                                                
           MOVE F595-CDE-HOUSEHOLD             TO WS-HH.                        
           MOVE F595-CDE-BALLOT-HOUSEHOLD      TO WS-HH-B.                      
           MOVE BP13F595-REC                   TO WS-F595-CUR-REC.              
           MOVE SPACES                         TO WS-BYPASS-NOTBK.              
           MOVE SPACES                         TO WS-SUCC.                      
           MOVE SPACES                         TO WS-SBF-HH.                    
           MOVE BP13F595-REC(463:1)            TO WS-SBF-HH.                    
                                                                                
           PERFORM 3600-READ-BP13K813 THRU 3600-EXIT.                           
                                                                                
           IF F205-NUM-OPTION = 'YA' OR 'YH'                                    
              IF WS-HH = 'G' OR WS-HH-B = 'G'                                   
                  PERFORM 2000-READ-BP13F595     THRU 2000-EXIT                 
                  GO TO 3500-EXIT                                               
              END-IF                                                            
           ELSE                                                                 
              IF F205-NUM-OPTION = 'YF' OR 'YJ'                                 
                 IF WS-HH = 'H' OR WS-HH-B = 'H'                                
                     PERFORM 2000-READ-BP13F595     THRU 2000-EXIT              
                     GO TO 3500-EXIT                                            
                 END-IF                                                         
              ELSE                                                              
                 IF F205-NUM-OPTION = 'YK'                                      
                    IF WS-HH = 'G' OR WS-HH-B = 'G' OR                          
                       F595-DTE-BALLOT >= F205-DTE-BALLOT                       
                       PERFORM 2000-READ-BP13F595   THRU 2000-EXIT              
                       GO TO 3500-EXIT                                          
                    END-IF                                                      
                 END-IF                                                         
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE 'N'                         TO WS-BOOKED.                       
                                                                                
           IF (F595-CDE-ALLOC-SCH = 'FTS' OR 'STS') OR                          
              ((F595-CDE-ALLOC-SCH = 'SAP') AND                                 
              (F595-CDE-ELIG-SCH   = 'SSC' OR 'JSS' OR 'NCS' OR                 
                                         'SSE' OR 'JSE' OR 'NSE'))              
               PERFORM 3520-CHECK-FTS-BOOK-STATUS THRU 3520-EXIT                
           ELSE                                                                 
               PERFORM 3510-CHECK-PA-BOOK-STATUS THRU 3510-EXIT                 
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F595        THRU 2000-EXIT.                    
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3510-CHECK-PA-BOOK-STATUS.                                               
                                                                                
           IF F595-DTE-BALLOT >= F205-DTE-REQUEST(1:6)                          
              IF (F595-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES)                 
                  OR                                                            
                 (F595-NUM-BOOK-STATUS = 'BF' OR 'LF' OR 'WF' OR                
                                         'LA' OR 'WA' OR                        
                                         'LG' OR 'WG' OR                        
                                         'LS' OR 'WS' OR 'HS')                  
                 MOVE 'Y'                         TO WS-BOOKED                  
                 MOVE 'Y'                         TO WS-A2009-BKFD              
                 IF F205-NUM-OPTION = 'YK'                                      
                     MOVE ZEROS                  TO WS-BTO-CNT                  
                     MOVE ZEROES                 TO WS-HARDLUCK                 
                     MOVE SPACES                 TO WS-1ST-UNSUCC               
                     MOVE 'Y'                    TO WS-RESET-FOUND              
                     MOVE 'Y'                    TO WS-SUCC                     
                     PERFORM 2000-READ-BP13F595  THRU 2000-EXIT                 
                     GO TO 3500-EXIT                                            
                 END-IF                                                         
              ELSE                                                              
                 IF (F595-NUM-BOOK-STATUS = 'NC' OR 'NF' OR 'NV' OR             
                                            'NS' OR 'SC' OR 'SF' OR             
                                            'HL')                               
                    MOVE 'N'                      TO WS-BOOKED                  
                    MOVE 'Y'                      TO WS-A2009-NOTBK-FD          
                                                                                
                 ELSE                                                           
                  IF ((F595-NUM-SCH-ACC = SPACES OR LOW-VALUES) AND             
                      (F595-NUM-ALLOC-TAG = 'NI' OR 'SI' OR                     
                                        'RI' OR 'RG') )                         
                  OR                                                            
                                                                                
                     ((F595-NUM-SCH-ACC = SPACES OR LOW-VALUES) AND             
                      (F595-NUM-ALLOC-TAG = 'FN') AND                           
                      (F595-DTE-BALLOT < '200805'))                             
                                                                                
                      MOVE 'Y'                        TO WS-BOOKED              
                      MOVE 'Y'                        TO WS-A2009-BKFD          
                      IF F205-NUM-OPTION = 'YK'                                 
                         MOVE ZEROS                  TO WS-BTO-CNT              
                         MOVE ZEROES                 TO WS-HARDLUCK             
                         MOVE SPACES                 TO WS-1ST-UNSUCC           
                         MOVE 'Y'                    TO WS-RESET-FOUND          
                         MOVE 'Y'                    TO WS-SUCC                 
                         PERFORM 2000-READ-BP13F595  THRU 2000-EXIT             
                         GO TO 3500-EXIT                                        
                      END-IF                                                    
                 ELSE                                                           
                      MOVE 'N'                        TO WS-BOOKED              
                 END-IF                                                         
               END-IF                                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
           IF WS-BK-TAG = 'Y'                                                   
              IF BOOKED                                                         
                 ADD 1                         TO WS-REST-CNT                   
              END-IF                                                            
           ELSE                                                                 
              IF BOOKED                                                         
                 MOVE 'Y'                      TO WS-BK-TAG                     
                 MOVE 00                       TO WS-REST-CNT                   
                 IF F595-NUM-ALLO-CAT = 'BE '                                   
                    ADD 1                      TO WS-BE-CNT                     
                    ADD 1                      TO WS-BK-CNT                     
                 ELSE                                                           
                    IF F595-NUM-ALLO-CAT = 'BTO' AND                            
                      (K813-NUM-MATURE-EST-TAG NOT = 'Y')                       
                       ADD 1                   TO WS-BTO-CNT                    
                       ADD 1                   TO WS-BK-CNT                     
                       IF WS-1ST-UNSUCC = SPACES                                
                          MOVE F595-DTE-BALLOT TO WS-1ST-UNSUCC                 
                       END-IF                                                   
                       MOVE BP13F595-REC TO WS-F595-NOTBK-REC                   
                    END-IF                                                      
                 END-IF                                                         
                 MOVE BP13F595-REC             TO WS-F595-BK-REC                
              ELSE                                                              
                 PERFORM 3550-COULD-NOT-BOOK THRU 3550-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F595-NUM-SCH-ACC NOT = SPACES                                     
              MOVE 'B'                       TO WS-BUY-STATUS                   
           END-IF.                                                              
                                                                                
       3510-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3520-CHECK-FTS-BOOK-STATUS.                                              
                                                                                
           IF F595-DTE-BALLOT >= F205-DTE-REQUEST(1:6)                          
              IF (F595-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES)                 
                  OR                                                            
                 (F595-NUM-BOOK-STATUS = 'BF' OR 'LB' OR 'WB' OR 'HS')          
                 MOVE 'Y'                         TO WS-BOOKED                  
                 MOVE 'Y'                         TO WS-A2009-BKFD              
                 IF F205-NUM-OPTION = 'YK'                                      
                     MOVE ZEROS                  TO WS-BTO-CNT                  
                     MOVE ZEROES                 TO WS-HARDLUCK                 
                     MOVE SPACES                 TO WS-1ST-UNSUCC               
                     MOVE 'Y'                    TO WS-RESET-FOUND              
                     MOVE 'Y'                    TO WS-SUCC                     
                     PERFORM 2000-READ-BP13F595  THRU 2000-EXIT                 
                     GO TO 3500-EXIT                                            
                 END-IF                                                         
              ELSE                                                              
                 IF (F595-NUM-BOOK-STATUS = 'NC' OR 'NF' OR 'NV' OR             
                                            'NS' OR 'SC' OR 'SF' OR             
                                            'HL')                               
                    MOVE 'N'                      TO WS-BOOKED                  
                    MOVE 'Y'                      TO WS-A2009-NOTBK-FD          
                                                                                
                 ELSE                                                           
                  IF ((F595-NUM-SCH-ACC = SPACES OR LOW-VALUES) AND             
                      (F595-NUM-ALLOC-TAG = 'NI' OR 'SI' OR                     
                                        'RI' OR 'RG') )                         
                  OR                                                            
                                                                                
                     ((F595-NUM-SCH-ACC = SPACES OR LOW-VALUES) AND             
                      (F595-NUM-ALLOC-TAG = 'FN') AND                           
                      (F595-DTE-BALLOT < '200805'))                             
                                                                                
                      MOVE 'Y'                        TO WS-BOOKED              
                      MOVE 'Y'                        TO WS-A2009-BKFD          
                      IF F205-NUM-OPTION = 'YK'                                 
                         MOVE ZEROS                  TO WS-BTO-CNT              
                         MOVE ZEROES                 TO WS-HARDLUCK             
                         MOVE SPACES                 TO WS-1ST-UNSUCC           
                         MOVE 'Y'                    TO WS-RESET-FOUND          
                         MOVE 'Y'                    TO WS-SUCC                 
                         PERFORM 2000-READ-BP13F595  THRU 2000-EXIT             
                         GO TO 3500-EXIT                                        
                      END-IF                                                    
                 ELSE                                                           
                      MOVE 'N'                        TO WS-BOOKED              
                 END-IF                                                         
               END-IF                                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
           IF (F595-DTE-BALLOT >= F205-DTE-ALLOCN(1:6)) AND                     
              (F595-DTE-BALLOT <  F205-DTE-REQUEST(1:6))                        
             IF (F595-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES) OR               
                (F595-NUM-BOOK-STATUS = 'BF' OR 'LB' OR 'WB' OR 'HS')           
                   MOVE ZEROS                  TO WS-BTO-CNT                    
                   MOVE ZEROES                 TO WS-HARDLUCK                   
                   MOVE SPACES                 TO WS-1ST-UNSUCC                 
                   MOVE 'Y'                    TO WS-RESET-FOUND                
                   MOVE 'Y'                    TO WS-SUCC                       
             ELSE                                                               
                IF F595-NUM-BOOK-STATUS = 'NC' OR 'NF' OR 'NV' OR               
                                          'NS' OR 'SC' OR 'SF' OR               
                                          'HL'                                  
                   CONTINUE                                                     
                ELSE                                                            
                  IF ((F595-NUM-SCH-ACC = SPACES OR LOW-VALUES) AND             
                      (F595-NUM-ALLOC-TAG = 'FN' OR 'NI' OR 'SI' OR             
                                          'RI' OR 'RG' OR 'RQ') )               
                   MOVE ZEROS                  TO WS-BTO-CNT                    
                   MOVE ZEROES                 TO WS-HARDLUCK                   
                   MOVE SPACES                 TO WS-1ST-UNSUCC                 
                   MOVE 'Y'                    TO WS-SUCC                       
                   MOVE 'Y'                    TO WS-RESET-FOUND                
                  END-IF                                                        
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
           IF WS-BK-TAG = 'Y'                                                   
              IF BOOKED                                                         
                 ADD 1                         TO WS-REST-CNT                   
              END-IF                                                            
           ELSE                                                                 
              IF BOOKED                                                         
                 MOVE 'Y'                      TO WS-BK-TAG                     
                 MOVE 00                       TO WS-REST-CNT                   
                 IF F595-NUM-ALLO-CAT = 'BE '                                   
                    ADD 1                      TO WS-BE-CNT                     
                    ADD 1                      TO WS-BK-CNT                     
                 ELSE                                                           
                    IF F595-NUM-ALLO-CAT = 'BTO' AND                            
                      (K813-NUM-MATURE-EST-TAG NOT = 'Y')                       
                       ADD 1                   TO WS-BTO-CNT                    
                       ADD 1                   TO WS-BK-CNT                     
                       IF WS-1ST-UNSUCC = SPACES                                
                          MOVE F595-DTE-BALLOT TO WS-1ST-UNSUCC                 
                       END-IF                                                   
                       MOVE BP13F595-REC TO WS-F595-NOTBK-REC                   
                    END-IF                                                      
                 END-IF                                                         
                 MOVE BP13F595-REC             TO WS-F595-BK-REC                
              ELSE                                                              
                 PERFORM 3550-COULD-NOT-BOOK THRU 3550-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F595-NUM-SCH-ACC NOT = SPACES                                     
              MOVE 'B'                       TO WS-BUY-STATUS                   
           END-IF.                                                              
                                                                                
       3520-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3550-COULD-NOT-BOOK.                                                     
                                                                                
           IF WS-SUCC = 'Y'                                                     
              GO TO 3550-EXIT.                                                  
                                                                                
           IF F595-NUM-ALLO-CAT NOT = 'BE ' AND 'SBF' AND 'BTO'                 
              GO TO 3550-EXIT.                                                  
                                                                                
           ADD 1                         TO WS-NRIC-CNT.                        
           IF F595-NUM-ALLO-CAT = 'BE ' OR 'SBF'                                
              ADD 1                      TO WS-BE-CNT                           
              ADD 1                         TO WS-BK-CNT                        
           ELSE                                                                 
              IF F595-NUM-ALLO-CAT = 'BTO'                                      
                IF (F595-NUM-SCH-ACC = SPACES AND                               
                    F595-NUM-BOOK-STATUS = SPACES AND                           
                   (F595-NUM-ALLOC-TAG = SPACES OR '00'))                       
                   OR                                                           
                                                                                
                   (F595-NUM-SCH-ACC = SPACES) AND                              
                   (F595-NUM-ALLOC-TAG = 'FN'  OR 'RQ') AND                     
                   (F595-NUM-BOOK-STATUS = SPACES) AND                          
                   (F595-DTE-BALLOT > '200805')                                 
                   OR                                                           
                                                                                
                   (F595-CDE-REQUEST-STATUS = 'W' AND                           
                    F595-NUM-BOOK-STATUS = SPACES)                              
                   OR                                                           
                     (K813-NUM-MATURE-EST-TAG = 'Y')                            
                                                                                
                     IF (F595-NUM-SCH-ACC = SPACES OR LOW-VALUES) AND           
                        (F595-NUM-ALLOC-TAG = 'FN' OR 'RQ') AND                 
                        (F595-NUM-BOOK-STATUS = SPACES) AND                     
                        (F595-DTE-BALLOT >= '200805')                           
                          ADD 1                   TO WS-FN-CNT                  
                          MOVE 'Y'                TO WS-BYPASS-NOTBK            
                     END-IF                                                     
                ELSE                                                            
                 ADD 1                   TO WS-BTO-CNT                          
                 ADD 1                   TO WS-BK-CNT                           
                 IF WS-1ST-UNSUCC = SPACES                                      
                    MOVE F595-DTE-BALLOT TO WS-1ST-UNSUCC                       
                 END-IF                                                         
                                                                                
                 IF F595-NUM-BOOK-STATUS = 'NE' OR                              
                    F595-NUM-ALLOC-TAG   = 'NE'                                 
                   ADD 1                   TO WS-NE-CNT                         
                   MOVE 'Y'                TO WS-BYPASS-NOTBK                   
                 ELSE                                                           
                                                                                
                   IF WS-K813-BYPASS = 'Y'                                      
                     IF WS-RESET-FOUND = 'Y'                                    
                        IF F595-DTE-BALLOT >= WS-1ST-UNSUCC                     
                           ADD 1               TO WS-HARDLUCK                   
                           MOVE 'Y'            TO WS-BYPASS-NOTBK               
                        END-IF                                                  
                     ELSE                                                       
                        ADD 1               TO WS-HARDLUCK                      
                        MOVE 'Y'            TO WS-BYPASS-NOTBK                  
                     END-IF                                                     
                   END-IF                                                       
                                                                                
                 END-IF                                                         
                                                                                
                 IF WS-BYPASS-NOTBK = SPACES                                    
                    MOVE BP13F595-REC TO WS-F595-NOTBK-REC                      
                 END-IF                                                         
                                                                                
                END-IF                                                          
                                                                                
             END-IF.                                                            
                                                                                
       3550-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3600-READ-BP13K813.                                                      
                                                                                
           MOVE SPACES                          TO BP13K813-REC.                
           INITIALIZE                              BP13K813-REC.                
                                                                                
           MOVE 'N'                             TO WS-K813-BYPASS.              
           MOVE F595-CDE-NT1                    TO K813-NUM-ZONE.               
           IF F595-CDE-FLAT-TYPE(2:1)  = 'P'                                    
             EVALUATE F595-CDE-FLAT-TYPE(1:1)                                   
                WHEN 'S'                                                        
                   MOVE '1A'                TO F595-CDE-FLAT-TYPE               
                WHEN '3'                                                        
                   MOVE '3 '                TO F595-CDE-FLAT-TYPE               
                WHEN '4'                                                        
                   MOVE '4 '                TO F595-CDE-FLAT-TYPE               
                WHEN '5'                                                        
                   MOVE '5 '                TO F595-CDE-FLAT-TYPE               
             END-EVALUATE                                                       
           END-IF                                                               
           MOVE F595-CDE-FLAT-TYPE              TO K813-NUM-FLAT-TYPE           
           MOVE F595-DTE-BALLOT                 TO K813-DTE-BALLOT.             
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    IF K813-NUM-HARDLUCK-BYPASS = 'Y'                           
                       MOVE 'Y'                 TO WS-K813-BYPASS               
                    END-IF                                                      
                                                                                
               WHEN 23                                                          
                    MOVE 'N'                    TO WS-K813-BYPASS               
                                                                                
               WHEN OTHER                                                       
                    MOVE WS-K813-STATUS               TO RETURN-CODE            
                    DISPLAY 'ERROR READING BP13K813: ' WS-K813-STATUS           
                            ' KEY : ' K813-KEY-FLD                              
                    PERFORM  9999-CLOSE-ROUTINE THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-WRITE-NOTBK.                                                        
      ******************************************************************        
                                                                                
           IF WS-F595-NOTBK-REC NOT = SPACES                                    
               MOVE WS-F595-NOTBK-REC TO P13F595A-REC                           
           ELSE                                                                 
               MOVE WS-F595-CUR-REC   TO P13F595A-REC                           
           END-IF.                                                              
                                                                                
           MOVE 'N'                      TO F595A-NUM-BK-TAG.                   
           MOVE WS-NRIC-CNT              TO F595A-NUM-BK-CNT.                   
           MOVE 00                       TO F595A-NUM-REST-CNT.                 
           MOVE WS-BE-CNT                TO F595A-NUM-BE-CNT.                   
           MOVE WS-NE-CNT                TO F595A-NUM-NE-CNT.                   
           MOVE WS-FN-CNT                TO F595A-NUM-FN-CNT.                   
           MOVE WS-HARDLUCK              TO F595A-NUM-HARDLUCK.                 
           MOVE WS-A2009-BKFD            TO F595A-NUM-2009-BKFND.               
           MOVE WS-BTO-CNT               TO F595A-NUM-BTO-CNT-COMP.             
           COMPUTE WS-TOTAL-CNT = WS-BTO-CNT - WS-NE-CNT                        
                                             - WS-HARDLUCK.                     
           IF WS-TOTAL-CNT < ZEROS                                              
               MOVE ZEROS                TO F595A-NUM-BTO-CNT                   
           ELSE                                                                 
               MOVE WS-TOTAL-CNT         TO F595A-NUM-BTO-CNT                   
           END-IF.                                                              
           WRITE P13F595A-REC.                                                  
           ADD 1                         TO WS-NRIC-NOTBK-CNT                   
                                            WS-F595A-WRITE.                     
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-WRITE-BK.                                                           
      ******************************************************************        
                                                                                
           MOVE WS-F595-BK-REC           TO P13F595A-REC                        
           MOVE 'Y'                      TO F595A-NUM-BK-TAG.                   
           MOVE WS-BK-CNT                TO F595A-NUM-BK-CNT.                   
           MOVE WS-REST-CNT              TO F595A-NUM-REST-CNT.                 
           MOVE WS-BE-CNT                TO F595A-NUM-BE-CNT.                   
           MOVE WS-NE-CNT                TO F595A-NUM-NE-CNT.                   
           MOVE WS-FN-CNT                TO F595A-NUM-FN-CNT.                   
           MOVE WS-HARDLUCK              TO F595A-NUM-HARDLUCK.                 
           MOVE WS-A2009-BKFD            TO F595A-NUM-2009-BKFND.               
           MOVE WS-BTO-CNT               TO F595A-NUM-BTO-CNT-COMP.             
           COMPUTE WS-TOTAL-CNT = WS-BTO-CNT - WS-NE-CNT                        
                                             - WS-HARDLUCK.                     
           IF WS-TOTAL-CNT < ZEROS                                              
               MOVE ZEROS                TO F595A-NUM-BTO-CNT                   
           ELSE                                                                 
               MOVE WS-TOTAL-CNT         TO F595A-NUM-BTO-CNT                   
           END-IF.                                                              
           WRITE P13F595A-REC.                                                  
           ADD 1                         TO WS-NRIC-BK-CNT                      
                                            WS-F595A-WRITE.                     
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CY92'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F595 RECORDS READ ........ : '                
                    WS-F595-READ.                                               
           DISPLAY '(2) NO OF P13F595A RECORDS WRITTEN ..... : '                
                    WS-F595A-WRITE.                                             
           DISPLAY '(3) NO OF BOOKED NRICS ................. : '                
                    WS-NRIC-BK-CNT.                                             
           DISPLAY '(4) NO OF NOT BOOKED NRICS ............. : '                
                    WS-NRIC-NOTBK-CNT.                                          
                                                                                
           CLOSE BP13F595                                                       
                 BP13F205                                                       
                 BP13K813                                                       
                 P13F595A.                                                      
                                                                                
           IF WS-K813-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'ERROR CLOSING - BP13K813 : ' WS-K813-STATUS              
              MOVE     WS-K813-STATUS            TO RETURN-CODE                 
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
