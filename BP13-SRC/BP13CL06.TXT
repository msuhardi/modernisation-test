       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CL06.                                                 
      *AUTHOR.        BALA AMUTHAN.                                             
      *DATE-WRITTEN.  16/08/2006.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT  (SOC)                                 *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE  :                                               *           
      *    1. LOAN TAG 'H' & 'B' CASES FOR SUCCESSFULLY SHORTLISTED *           
      *       DBSS CASES.                                           *           
      *                                                             *           
      *  INPUT FILES:  1. BP13LC01 (INFO FOR OFFER LETTERS)         *           
      *                                                             *           
      *     REPORT  :  1. P13LL06A.                                 *           
      *                2. P13LL06B.                                 *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  BY   ON      DESCRIPTION                           *           
      * -------- ---- ------  -----------                           *           
      * BP132873 BA2  160806  NEW PROGRAM                           *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13LC01 ASSIGN TO BP13LC01.                                  
                                                                                
           SELECT P13LL06A ASSIGN TO P13LL06A.                                  
                                                                                
           SELECT P13LL06B ASSIGN TO P13LL06B.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13LC01                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13LC01.                                                           
                                                                                
       FD  P13LL06A                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  PRINT-REC-L06A      PIC X(132).                                      
                                                                                
       FD  P13LL06B                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  PRINT-REC-L06B      PIC X(132).                                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-SWITCH.                                                           
           05  WS-LC01-END           PIC X(1)       VALUE 'N'.                  
               88 CS-LC01-END                       VALUE 'Y'.                  
                                                                                
       01  WS-COUNT.                                                            
           05  WS-LC01-READ          PIC 9(5) COMP  VALUE ZEROS.                
           05  WS-L06A-WRITE         PIC 9(5) COMP  VALUE ZEROS.                
           05  WS-L06B-WRITE         PIC 9(5) COMP  VALUE ZEROS.                
                                                                                
       01  WS-DATE.                                                             
           05 WS-DATE-CHAR           PIC X(8).                                  
           05 WS-DATE-NUM REDEFINES WS-DATE-CHAR                                
                                     PIC 9(8).                                  
                                                                                
       01  WS-UNIT-INFO.                                                        
           05 LVL-NO                 PIC X(02).                                 
           05 FILLER                 PIC X(01) VALUE '-'.                       
           05 MAIN-NO                PIC X(04).                                 
           05 WS-SLASH               PIC X(01) VALUE '-'.                       
           05 SUB-NO                 PIC X(03).                                 
                                                                                
       01  WS-PREV-VALUES.                                                      
           05 WS-PREV-PRJCT          PIC X(03) VALUE SPACES.                    
           05 WS-PREV-FLAT           PIC X(02) VALUE SPACES.                    
           05 WS-PREV-LOAN-TAG       PIC X(01) VALUE SPACES.                    
                                                                                
       77  WS-LINE-CNT-L06A          PIC 9(2)      VALUE 60.                    
       77  WS-LINE-CNT-L06B          PIC 9(2)      VALUE 60.                    
       77  WS-PAGE-NO-L06A           PIC 9(4) COMP VALUE ZEROS.                 
       77  WS-PAGE-NO-L06B           PIC 9(4) COMP VALUE ZEROS.                 
       77  WS-SNO-L06A               PIC 9(4) COMP VALUE ZEROS.                 
       77  WS-SNO-L06B               PIC 9(4) COMP VALUE ZEROS.                 
       77  WS-CNT                    PIC 9(1)      VALUE ZEROS.                 
                                                                                
      *---------------------------------------------------------------*         
      *       HEADINGS FOR REPORT                                     *         
      *---------------------------------------------------------------*         
       01  LL06-HEAD-01.                                                        
           05  FILLER                PIC X(8)  VALUE 'P13LL06A'.                
           05  FILLER                PIC X(4)  VALUE SPACES.                    
           05  FILLER                PIC X(8)  VALUE 'HDB3    '.                
           05  FILLER                PIC X(24) VALUE SPACES.                    
           05  FILLER                PIC X(20) VALUE                            
                                      'S Y S T E M   O F   '.                   
           05  FILLER                PIC X(19) VALUE                            
                                       'C O M M I T M E N T'.                   
           05  FILLER                PIC X(15) VALUE SPACES.                    
           05  FILLER                PIC X(7)  VALUE 'DATE :'.                  
           05  LL06A-DATE            PIC X(10) VALUE SPACES.                    
           05  FILLER                PIC X(4)  VALUE SPACES.                    
           05  FILLER                PIC X(7)  VALUE 'PAGE : '.                 
           05  LL06A-PAGE            PIC ZZZ9  VALUE ZEROS.                     
                                                                                
       01  LL06-HEAD-01B.                                                       
           05  FILLER                PIC X(8)  VALUE 'P13LL06B'.                
           05  FILLER                PIC X(4)  VALUE SPACES.                    
           05  FILLER                PIC X(8)  VALUE 'HDB3    '.                
           05  FILLER                PIC X(24) VALUE SPACES.                    
           05  FILLER                PIC X(20) VALUE                            
                                      'S Y S T E M   O F   '.                   
           05  FILLER                PIC X(19) VALUE                            
                                       'C O M M I T M E N T'.                   
           05  FILLER                PIC X(15) VALUE SPACES.                    
           05  FILLER                PIC X(7)  VALUE 'DATE :'.                  
           05  LL06B-DATE            PIC X(10) VALUE SPACES.                    
           05  FILLER                PIC X(4)  VALUE SPACES.                    
           05  FILLER                PIC X(7)  VALUE 'PAGE : '.                 
           05  LL06B-PAGE            PIC ZZZ9  VALUE ZEROS.                     
                                                                                
                                                                                
       01  LL06-HEAD-02.                                                        
           05  FILLER                PIC X(20)   VALUE SPACES.                  
           05  FILLER                PIC X(88)   VALUE                          
               'DETAILED MONITORING REPORT OF LOAN TAG "H" CASES FOR SUC        
      -        'CESSFULLY SHORTLISTED DBSS CASES'.                              
           05  FILLER                PIC X(24)   VALUE SPACES.                  
                                                                                
       01  LL06-HEAD-02B.                                                       
           05  FILLER                PIC X(20)   VALUE SPACES.                  
           05  FILLER                PIC X(88)   VALUE                          
               'DETAILED MONITORING REPORT OF LOAN TAG "B" CASES FOR SUC        
      -        'CESSFULLY SHORTLISTED DBSS CASES'.                              
           05  FILLER                PIC X(24)   VALUE SPACES.                  
                                                                                
       01  LL06-HEAD-03.                                                        
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(15)   VALUE                          
               'PROJECT      : '.                                               
           05  LL06-PROJECT          PIC X(30)   VALUE SPACES.                  
           05  FILLER                PIC X(85)   VALUE SPACES.                  
                                                                                
       01  LL06-HEAD-04.                                                        
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(15)   VALUE                          
               'FLAT TYPE    : '.                                               
           05  LL06-FLT-TYP          PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(113)  VALUE SPACES.                  
                                                                                
       01  LL06-HEAD-05.                                                        
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(04)   VALUE                          
               'S/NO'.                                                          
           05  FILLER                PIC X(04)   VALUE SPACES.                  
           05  FILLER                PIC X(12)   VALUE                          
               'BOOKING DATE'.                                                  
           05  FILLER                PIC X(04)   VALUE SPACES.                  
           05  FILLER                PIC X(15)   VALUE                          
               'HLE EXPIRY DATE'.                                               
           05  FILLER                PIC X(04)   VALUE SPACES.                  
           05  FILLER                PIC X(10)   VALUE                          
               'HLE REF NO'.                                                    
           05  FILLER                PIC X(04)   VALUE SPACES.                  
           05  FILLER                PIC X(11)   VALUE                          
               'DBSS REF NO'.                                                   
           05  FILLER                PIC X(04)   VALUE SPACES.                  
           05  FILLER                PIC X(06)   VALUE                          
               'CA OIC'.                                                        
           05  FILLER                PIC X(52)   VALUE SPACES.                  
                                                                                
       01  LL06-HEAD-06.                                                        
           05  FILLER                PIC X(10)   VALUE SPACES.                  
           05  FILLER                PIC X(07)   VALUE                          
               'ADDRESS'.                                                       
           05  FILLER                PIC X(115)  VALUE SPACES.                  
                                                                                
       01  LL06-HEAD-07.                                                        
           05  FILLER                PIC X(10)   VALUE SPACES.                  
           05  FILLER                PIC X(04)   VALUE                          
               'NRIC'.                                                          
           05  FILLER                PIC X(12)   VALUE SPACES.                  
           05  FILLER                PIC X(04)   VALUE                          
               'NAME'.                                                          
           05  FILLER                PIC X(102)  VALUE SPACES.                  
                                                                                
       01  LL06-DETAIL01.                                                       
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  LL06-SNO              PIC ZZZ9    VALUE SPACES.                  
           05  FILLER                PIC X(04)   VALUE SPACES.                  
           05  LL06-BOOK-DTE         PIC X(10)   VALUE SPACES.                  
           05  FILLER                PIC X(06)   VALUE SPACES.                  
           05  LL06-HLE-EXPIRY-DTE   PIC X(10)   VALUE SPACES.                  
           05  FILLER                PIC X(09)   VALUE SPACES.                  
           05  LL06-HLE-REF-NO       PIC X(09)   VALUE SPACES.                  
           05  FILLER                PIC X(06)   VALUE SPACES.                  
           05  LL06-DBSS-REF-NO      PIC X(09)   VALUE SPACES.                  
           05  FILLER                PIC X(06)   VALUE SPACES.                  
           05  LL06-CA-OIC           PIC X(05)   VALUE SPACES.                  
           05  FILLER                PIC X(51)   VALUE SPACES.                  
                                                                                
       01  LL06-DETAIL02.                                                       
           05  FILLER                PIC X(10)   VALUE SPACES.                  
           05  LL06-ADDRESS          PIC X(60)   VALUE SPACES.                  
           05  FILLER                PIC X(62)   VALUE SPACES.                  
                                                                                
       01  LL06-DETAIL03.                                                       
           05  FILLER                PIC X(10)   VALUE SPACES.                  
           05  LL06-NRIC             PIC X(09)   VALUE SPACES.                  
           05  FILLER                PIC X(07)   VALUE SPACES.                  
           05  LL06-NAME             PIC X(66)   VALUE SPACES.                  
           05  FILLER                PIC X(39)   VALUE SPACES.                  
                                                                                
       01  LL06-HEAD-ROLL.                                                      
           05  FILLER                PIC X(2)    VALUE SPACES.                  
           05  FILLER                PIC X(86)   VALUE ALL '-'.                 
           05  FILLER                PIC X(44)   VALUE SPACES.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPENING-ROUTINE THRU 1000-EXIT.                         
           PERFORM 2000-READ-LC01       THRU 2000-EXIT.                         
           PERFORM 3000-MAIN-PROCESS   THRU 3000-EXIT                           
                   UNTIL CS-LC01-END.                                           
                                                                                
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT BP13LC01                                                  
               OUTPUT P13LL06A                                                  
                      P13LL06B.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE-CHAR(7:2) '/' WS-DATE-CHAR(5:2) '/'                   
                  WS-DATE(1:4) DELIMITED BY SIZE INTO LL06A-DATE.               
                                                                                
           MOVE LL06A-DATE TO LL06B-DATE.                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-LC01.                                                          
      *-------------------------------------------------------------            
           READ BP13LC01 AT END MOVE 'Y' TO WS-LC01-END                         
                                GO TO 2000-EXIT.                                
           ADD 1 TO WS-LC01-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
           IF LC01-NUM-LOAN-TAG = 'H'                                           
              PERFORM 3100-PROCESS-L06A     THRU 3100-EXIT                      
           ELSE                                                                 
             IF LC01-NUM-LOAN-TAG = 'B'                                         
                PERFORM 3200-PROCESS-L06B   THRU 3200-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-LC01           THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-PROCESS-L06A.                                                       
      *-------------------------------------------------------------            
      *LOAN TAG = H                                                             
           MOVE LC01-NME-PRJCT              TO LL06-PROJECT.                    
           MOVE LC01-NUM-FT                 TO LL06-FLT-TYP.                    
           ADD 1                            TO WS-SNO-L06A.                     
           PERFORM 6000-HEAD-CHECK-L06A     THRU 6000-EXIT.                     
                                                                                
           MOVE WS-SNO-L06A                 TO LL06-SNO.                        
           PERFORM 3300-MOVE-DETAIL         THRU 3300-EXIT.                     
           WRITE   PRINT-REC-L06A FROM LL06-DETAIL01 AFTER 2.                   
           ADD 2                   TO WS-LINE-CNT-L06A.                         
                                                                                
           PERFORM 3400-MOVE-ADDRESS        THRU 3400-EXIT.                     
           MOVE LL06-DETAIL02               TO PRINT-REC-L06A.                  
           PERFORM 6100-WRITE-REPORT-L06A   THRU 6100-EXIT.                     
           ADD 1                            TO WS-L06A-WRITE.                   
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1 UNTIL WS-CNT > 4 OR               
              (LC01-NUM-NRIC(WS-CNT) = SPACES OR LOW-VALUES OR ZEROES)          
             PERFORM 3500-MOVE-NAME-IC      THRU 3500-EXIT                      
             MOVE LL06-DETAIL03             TO PRINT-REC-L06A                   
             PERFORM 6100-WRITE-REPORT-L06A THRU 6100-EXIT                      
           END-PERFORM.                                                         
                                                                                
           MOVE LC01-NUM-PRJCT              TO WS-PREV-PRJCT.                   
           MOVE LC01-NUM-FT                 TO WS-PREV-FLAT.                    
           MOVE LC01-NUM-LOAN-TAG           TO WS-PREV-LOAN-TAG.                
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-PROCESS-L06B.                                                       
      *-------------------------------------------------------------            
      *LOAN TAG = B                                                             
           MOVE LC01-NME-PRJCT              TO LL06-PROJECT.                    
           MOVE LC01-NUM-FT                 TO LL06-FLT-TYP.                    
           ADD 1                            TO WS-SNO-L06B.                     
           PERFORM 6050-HEAD-CHECK-L06B     THRU 6050-EXIT.                     
                                                                                
           MOVE WS-SNO-L06B                 TO LL06-SNO.                        
           PERFORM 3300-MOVE-DETAIL         THRU 3300-EXIT.                     
           WRITE   PRINT-REC-L06B FROM LL06-DETAIL01 AFTER 2.                   
           ADD 2                   TO WS-LINE-CNT-L06A.                         
                                                                                
           PERFORM 3400-MOVE-ADDRESS        THRU 3400-EXIT.                     
           MOVE LL06-DETAIL02               TO PRINT-REC-L06B.                  
           PERFORM 6150-WRITE-REPORT-L06B   THRU 6150-EXIT.                     
           ADD 1                            TO WS-L06B-WRITE.                   
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1 UNTIL WS-CNT > 4 OR               
              (LC01-NUM-NRIC(WS-CNT) = SPACES OR LOW-VALUES OR ZEROES)          
             PERFORM 3500-MOVE-NAME-IC      THRU 3500-EXIT                      
             MOVE LL06-DETAIL03             TO PRINT-REC-L06B                   
             PERFORM 6150-WRITE-REPORT-L06B THRU 6150-EXIT                      
           END-PERFORM.                                                         
                                                                                
           MOVE LC01-NUM-PRJCT              TO WS-PREV-PRJCT.                   
           MOVE LC01-NUM-FT                 TO WS-PREV-FLAT.                    
           MOVE LC01-NUM-LOAN-TAG           TO WS-PREV-LOAN-TAG.                
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3300-MOVE-DETAIL.                                                        
      *-------------------------------------------------------------            
           IF LC01-DTE-ACCEPT NOT = SPACES AND ZEROES AND LOW-VALUES            
              STRING LC01-DTE-ACCEPT(7:2) '/' LC01-DTE-ACCEPT(5:2) '/'          
                     LC01-DTE-ACCEPT(1:4)                                       
                     DELIMITED BY SIZE INTO LL06-BOOK-DTE                       
           ELSE                                                                 
              MOVE SPACES                TO LL06-BOOK-DTE                       
           END-IF.                                                              
                                                                                
           IF LC01-DTE-HLE-APPROVAL NOT = SPACES AND ZEROES AND                 
                                          LOW-VALUES                            
              STRING LC01-DTE-HLE-APPROVAL(7:2) '/'                             
                     LC01-DTE-HLE-APPROVAL(5:2) '/'                             
                     LC01-DTE-HLE-APPROVAL(1:4)                                 
                     DELIMITED BY SIZE INTO LL06-HLE-EXPIRY-DTE                 
           ELSE                                                                 
              MOVE SPACES                TO LL06-HLE-EXPIRY-DTE                 
           END-IF.                                                              
                                                                                
           MOVE LC01-NUM-HLE             TO LL06-HLE-REF-NO.                    
           MOVE LC01-NUM-REGN            TO LL06-DBSS-REF-NO.                   
           MOVE LC01-NUM-OIC-CA          TO LL06-CA-OIC.                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3400-MOVE-ADDRESS.                                                       
      *-------------------------------------------------------------            
      * PRINTING ADDRESS.                                                       
                                                                                
           MOVE LC01-NUM-LEVEL         TO   LVL-NO.                             
           MOVE LC01-NUM-MAIN-UNIT     TO   MAIN-NO.                            
           IF LC01-NUM-SUB-UNIT = SPACES OR LOW-VALUES                          
              MOVE SPACES              TO   WS-SLASH                            
                                            SUB-NO                              
           ELSE                                                                 
              MOVE LC01-NUM-SUB-UNIT   TO   SUB-NO                              
              MOVE '-'                 TO   WS-SLASH                            
           END-IF.                                                              
                                                                                
           STRING  'BLK ' LC01-NUM-BLK                                          
                   ' # '  WS-UNIT-INFO                                          
                   '   '  LC01-NME-STREET                                       
                     DELIMITED BY SIZE INTO LL06-ADDRESS.                       
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3500-MOVE-NAME-IC.                                                       
      *-------------------------------------------------------------            
           MOVE LC01-NUM-NRIC(WS-CNT)    TO LL06-NRIC.                          
           MOVE LC01-NME-HA(WS-CNT)      TO LL06-NAME.                          
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-HEAD-CHECK-L06A.                                                    
      *-------------------------------------------------------------            
           IF WS-LINE-CNT-L06A > 51                                             
              PERFORM 6200-WRITE-HEAD  THRU 6200-EXIT                           
           ELSE                                                                 
              IF WS-PREV-PRJCT NOT = LC01-NUM-PRJCT                             
                 PERFORM 6200-WRITE-HEAD  THRU 6200-EXIT                        
              ELSE                                                              
                IF WS-PREV-FLAT  NOT = LC01-NUM-FT                              
                   PERFORM 6200-WRITE-HEAD  THRU 6200-EXIT                      
                ELSE                                                            
                   IF WS-PREV-LOAN-TAG NOT = LC01-NUM-LOAN-TAG                  
                      PERFORM 6200-WRITE-HEAD  THRU 6200-EXIT                   
                   END-IF                                                       
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6050-HEAD-CHECK-L06B.                                                    
      *-------------------------------------------------------------            
           IF WS-LINE-CNT-L06B > 51                                             
              PERFORM 6250-WRITE-HEAD  THRU 6250-EXIT                           
           ELSE                                                                 
              IF WS-PREV-PRJCT NOT = LC01-NUM-PRJCT                             
                 PERFORM 6250-WRITE-HEAD  THRU 6250-EXIT                        
              ELSE                                                              
                IF WS-PREV-FLAT  NOT = LC01-NUM-FT                              
                   PERFORM 6250-WRITE-HEAD  THRU 6250-EXIT                      
                ELSE                                                            
                   IF WS-PREV-LOAN-TAG NOT = LC01-NUM-LOAN-TAG                  
                      PERFORM 6250-WRITE-HEAD  THRU 6250-EXIT                   
                   END-IF                                                       
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6050-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6100-WRITE-REPORT-L06A.                                                  
      *-------------------------------------------------------------            
           WRITE   PRINT-REC-L06A.                                              
           ADD +1                  TO WS-LINE-CNT-L06A.                         
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6150-WRITE-REPORT-L06B.                                                  
      *-------------------------------------------------------------            
           WRITE   PRINT-REC-L06B.                                              
           ADD +1                  TO WS-LINE-CNT-L06B.                         
                                                                                
       6150-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6200-WRITE-HEAD.                                                         
      *-------------------------------------------------------------            
           ADD +1                  TO WS-PAGE-NO-L06A.                          
           MOVE WS-PAGE-NO-L06A    TO LL06A-PAGE.                               
                                                                                
           WRITE   PRINT-REC-L06A  FROM LL06-HEAD-01 AFTER PAGE.                
           WRITE   PRINT-REC-L06A  FROM LL06-HEAD-02.                           
           WRITE   PRINT-REC-L06A  FROM LL06-HEAD-03.                           
           WRITE   PRINT-REC-L06A  FROM LL06-HEAD-04.                           
           WRITE   PRINT-REC-L06A  FROM LL06-HEAD-05.                           
           WRITE   PRINT-REC-L06A  FROM LL06-HEAD-06.                           
           WRITE   PRINT-REC-L06A  FROM LL06-HEAD-07.                           
           WRITE   PRINT-REC-L06A  FROM LL06-HEAD-ROLL.                         
                                                                                
           MOVE  8                 TO WS-LINE-CNT-L06A.                         
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6250-WRITE-HEAD.                                                         
      *-------------------------------------------------------------            
           ADD +1                  TO WS-PAGE-NO-L06B.                          
           MOVE WS-PAGE-NO-L06B    TO LL06B-PAGE.                               
                                                                                
           WRITE   PRINT-REC-L06B  FROM LL06-HEAD-01B AFTER PAGE.               
           WRITE   PRINT-REC-L06B  FROM LL06-HEAD-02B.                          
           WRITE   PRINT-REC-L06B  FROM LL06-HEAD-03.                           
           WRITE   PRINT-REC-L06B  FROM LL06-HEAD-04.                           
           WRITE   PRINT-REC-L06B  FROM LL06-HEAD-05.                           
           WRITE   PRINT-REC-L06B  FROM LL06-HEAD-06.                           
           WRITE   PRINT-REC-L06B  FROM LL06-HEAD-07.                           
           WRITE   PRINT-REC-L06B  FROM LL06-HEAD-ROLL.                         
                                                                                
           MOVE  8                 TO WS-LINE-CNT-L06B.                         
                                                                                
       6250-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           IF WS-L06A-WRITE <= 0                                                
              PERFORM 6200-WRITE-HEAD        THRU 6200-EXIT                     
              MOVE 'NO RECORDS FOUND'   TO   PRINT-REC-L06A                     
              PERFORM 6100-WRITE-REPORT-L06A THRU 6100-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-L06B-WRITE <= 0                                                
              PERFORM 6250-WRITE-HEAD        THRU 6250-EXIT                     
              MOVE 'NO RECORDS FOUND'   TO   PRINT-REC-L06A                     
              PERFORM 6150-WRITE-REPORT-L06B THRU 6150-EXIT                     
           END-IF.                                                              
                                                                                
           DISPLAY '------- BP13LL06 -------------'.                            
           DISPLAY ' NO OF LC01 READ   : ' WS-LC01-READ.                        
           DISPLAY ' NO OF L06A WRITTEN: ' WS-L06A-WRITE.                       
           DISPLAY ' NO OF L06B WRITTEN: ' WS-L06B-WRITE.                       
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13LC01                                                       
                 P13LL06A                                                       
                 P13LL06B.                                                      
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
