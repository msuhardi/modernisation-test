       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C580.                                                  
       AUTHOR.       LKH.                                                       
      *DATE-WRITTEN. 24.05.90.                                                  
      ************************************************************              
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                      *              
      *  SYSTEM ID   : BP13                                      *              
      *  OBJECTIVE   : TO MATCH THE NRIC NO IN THE BP13REQ FILE  *              
      *                WITH THE NRIC NO IN P10K020.              *              
      *                                                          *              
      *  BP13MR      : NRIC1/NRIC2 OUTREC FROM RFF               *              
      *  BP13REQ     : NRIC1/NRIC2 OUTREC FROM REQ FILE(BP13F580)*              
      *                                                          *              
      * CHG-NO  BY   ON        DESCRIPTION                       *              
      * ----------------------------------                       *              
      * C920206 LSB 170292 - WIS NEED TO CHK NRIC2 FOR ELIG CHK  *              
      *                      CHG BP13REQ TO BP13REQ FILE.        *              
      * C920    YPH 240492 - TO INCLUDE REF-NO FOR SOC ELIG CHK  *              
      * N930012 YPH 040693 - SOC PH8                             *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13MR ASSIGN TO BP13MR.                                      
           SELECT BP13REQ ASSIGN TO BP13REQ.                                    
           SELECT BP13F587 ASSIGN TO BP13F587.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13MR                                                               
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 30  CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  BP13MR-REC.                                                          
           05  P13MR-NUM-REGN              PIC X(8).                            
           05  P13MR-NUM-NRIC              PIC X(9).                            
           05  FILLER                      PIC X(13).                           
                                                                                
       FD  BP13REQ                                                              
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 30  CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  BP13REQ-REC.                                                         
           05  FILLER                      PIC X(8).                            
           05  P13REQ-NUM-NRIC             PIC X(9).                            
           05  FILLER                      PIC X(13).                           
                                                                                
       FD  BP13F587                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 30  CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       COPY BP13F587.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-P13MR-CNT            PIC 9(8)    VALUE ZERO.                      
       01  WS-F587-CNT             PIC 9(8)    VALUE ZERO.                      
       01  WS-P13REQ-CNT           PIC 9(8)    VALUE ZERO.                      
       01  WS-EOF-P13MR            PIC X(1).                                    
           88  EOF-OF-P13MR                    VALUE 'Y'.                       
       01  WS-EOF-P13REQ           PIC X(1).                                    
           88  EOF-OF-P13REQ                   VALUE 'Y'.                       
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
       10-MAIN-LOGIC.                                                           
           PERFORM 20-INITIALIZATION  THRU 20-EXIT.                             
           PERFORM 200-READ-P13REQ    THRU 200-EXIT.                            
           PERFORM 300-READ-P13MR     THRU 300-EXIT.                            
           PERFORM 400-F587-ROUTINE   THRU 400-EXIT                             
                   UNTIL EOF-OF-P13REQ OR  EOF-OF-P13MR.                        
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       20-INITIALIZATION.                                                       
           OPEN INPUT  BP13MR                                                   
                       BP13REQ                                                  
               OUTPUT  BP13F587.                                                
                                                                                
           MOVE ZEROS TO WS-P13MR-CNT.                                          
           MOVE ZEROS TO WS-P13REQ-CNT.                                         
           MOVE ZEROS TO WS-F587-CNT.                                           
           MOVE 'N' TO WS-EOF-P13MR.                                            
           MOVE 'N' TO WS-EOF-P13REQ.                                           
                                                                                
       20-EXIT.                                                                 
           EXIT.                                                                
                                                                                
       200-READ-P13REQ.                                                         
           READ BP13REQ                                                         
               AT END MOVE 'Y' TO WS-EOF-P13REQ                                 
               MOVE HIGH-VALUE TO P13REQ-NUM-NRIC                               
               GO TO 200-EXIT.                                                  
                                                                                
           ADD 1 TO WS-P13REQ-CNT.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-P13MR.                                                          
           READ BP13MR                                                          
                AT END MOVE 'Y' TO WS-EOF-P13MR                                 
                MOVE HIGH-VALUE TO P13MR-NUM-NRIC                               
                GO TO 300-EXIT.                                                 
                                                                                
           ADD 1 TO WS-P13MR-CNT.                                               
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      * F587 THE TWO FILE WITH THE ESTATE/NEIGHBOURHOOD/CONTRACT/BLK *          
      *****************************************************************         
       400-F587-ROUTINE.                                                        
           IF P13REQ-NUM-NRIC  = P13MR-NUM-NRIC                                 
              PERFORM 500-WRITE-F587 THRU 500-EXIT                              
              PERFORM 300-READ-P13MR THRU 300-EXIT                              
           ELSE                                                                 
              IF P13REQ-NUM-NRIC  > P13MR-NUM-NRIC                              
                 PERFORM 300-READ-P13MR THRU 300-EXIT                           
              ELSE                                                              
                 PERFORM 200-READ-P13REQ THRU 200-EXIT.                         
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-WRITE-F587.                                                          
                                                                                
           MOVE BP13REQ-REC    TO BP13F587-TRANS-REC.                           
           MOVE P13MR-NUM-REGN TO F587-NUM-REGN.                                
                                                                                
           WRITE BP13F587-TRANS-REC.                                            
                                                                                
           MOVE SPACES         TO BP13F587-TRANS-REC.                           
           ADD 1               TO WS-F587-CNT.                                  
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
           DISPLAY '  '.                                                        
           DISPLAY '***** SUMMARY REPORT FOR BP13C580 *****'.                   
           DISPLAY 'TOTAL NO OF REC READ FROM BP13REQ  = '                      
                   WS-P13REQ-CNT.                                               
           DISPLAY 'TOTAL NO OF REC READ FROM BP13MR    = '                     
                   WS-P13MR-CNT.                                                
           DISPLAY 'TOTAL NO OF MATCHED RECORDS         = '                     
                   WS-F587-CNT.                                                 
                                                                                
           CLOSE      BP13MR                                                    
                      BP13REQ                                                   
                      BP13F587.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
