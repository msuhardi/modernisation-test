       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C509.                                                 
       AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
       DATE-WRITTEN.  20/03/2007.                                               
      * ========================================================= *             
      *             CREATE APPOINTMENT SCHEDULES IN K225          *             
      * ========================================================= *             
      *  PURPOSE OF THIS PROGRAM: CREATE APPOINTMENT SCHEDULES    *             
      *                           IN BP13K225 FROM BOOKING APPMT  *             
      *                           DETAILS IN BP13K223             *             
      *                                                           *             
      *  INPUT        :  BP13K223                                 *             
      *                  BP13K764                                 *             
      *  OUTPUT       :  BP13K225                                 *             
      *                                                           *             
      * ----------------------------------------------------------*             
      * CHG REF  DATE     BY   DESCRIPTION                        *             
      * -------- -------- --- ------------------------------------*             
      * BP133026 20/03/07 RB12 NEW PROGRAM                        *             
      * BP133114 02/08/07 RB12 START STAGE 2 FOR 3R ON 20070820   *             
      * BP133129 16/08/07 RB12 REMOVE PREFIX FOR WIC STAGE 1      *             
      * BP133136 03/09/07 RB12 REMOVE DATE OVERRIDE FOR STG 2 3R  *             
      * BP133071 17/09/08 RB12 CATER FOR SALES MODE 3R1           *             
      * BP133430 15/10/08 RB12 CATER FOR SALES MODE WIH           *             
      * ==========================================================*             
                                                                                
       ENVIRONMENT    DIVISION.                                                 
       CONFIGURATION  SECTION.                                                  
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13K223  ASSIGN        TO BP13K223                           
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K223-KEY-FLD                       
                            ACCESS MODE   IS DYNAMIC                            
                            FILE STATUS   IS K223-FILE-STATUS.                  
                                                                                
           SELECT BP13K225  ASSIGN        TO BP13K225                           
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K225-KEY-FLD                       
                            ACCESS MODE   IS RANDOM                             
                            FILE STATUS   IS K225-FILE-STATUS.                  
                                                                                
           SELECT BP13K764  ASSIGN        TO BP13K764                           
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K764-KEY-FLD                       
                            ACCESS MODE   IS RANDOM                             
                            FILE STATUS   IS K764-FILE-STATUS.                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13K223                                                            
            RECORD CONTAINS 80 CHARACTERS.                                      
       COPY BP13K223.                                                           
                                                                                
       FD   BP13K225                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K225.                                                           
                                                                                
       FD   BP13K764                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K764.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  K223-FILE-STATUS               PIC 9(2) VALUE 0.                     
       01  K225-FILE-STATUS               PIC 9(2) VALUE 0.                     
       01  K764-FILE-STATUS               PIC 9(2) VALUE 0.                     
                                                                                
       01  WS-K764-FOUND                  PIC X    VALUE SPACES.                
       01  WS-SAVE-DATE                   PIC X(8) VALUE SPACES.                
       01  WS-NUM-FIRST-BATCH             PIC 9(6) VALUE ZEROES.                
       01  WS-PREFIX                      PIC X    VALUE SPACES.                
       01  WS-NEXT-BATCH                  PIC X    VALUE 'N'.                   
       01  WS-CTR                         PIC 9(2) VALUE ZEROES.                
                                                                                
       01  WS-SAVE-SALES-MODE             PIC X(3) VALUE SPACES.                
       01  WS-SAVE-SALES-MODE-CNT         PIC 9(5) VALUE ZEROES.                
                                                                                
       01  WS-QUEUE-NUM REDEFINES WS-SAVE-SALES-MODE-CNT.                       
           05 FILLER                      PIC X.                                
           05 WS-QUEUE-NUM-DIGITS         PIC X(4).                             
                                                                                
       01  WS-K223-NUM-CASE-ALPHA         PIC X(2).                             
       01  WS-K223-NUM-CASE REDEFINES WS-K223-NUM-CASE-ALPHA                    
                                          PIC 9(2).                             
                                                                                
       01  WS-K223-READ-CNT               PIC 9(8) VALUE ZEROES.                
       01  WS-K764-READ-CNT               PIC 9(8) VALUE ZEROES.                
       01  WS-K764-READ-ERR               PIC 9(8) VALUE ZEROES.                
       01  WS-K225-WRITE-CNT              PIC 9(8) VALUE ZEROES.                
       01  WS-K225-WRITE-ERR              PIC 9(8) VALUE ZEROES.                
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-DTE-CCYY                PIC 9(4) VALUE ZEROES.                
           05  WS-DTE-MM                  PIC 9(2) VALUE ZEROES.                
           05  WS-DTE-DD                  PIC 9(2) VALUE ZEROES.                
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-K223-EOF-FLAG           PIC X VALUE 'N'.                      
               88 WS-K223-EOF-YES               VALUE 'Y'.                      
               88 WS-K223-EOF-NO                VALUE 'N'.                      
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-PRE-PROCESS         THRU 1000-EXIT.                     
           PERFORM 2000-PROCESS-MASTER      THRU 2000-EXIT.                     
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-PRE-PROCESS.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT  BP13K223                                                 
                       BP13K764                                                 
                I-O    BP13K225.                                                
                                                                                
           IF K223-FILE-STATUS NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING BP13K223 : ' K223-FILE-STATUS              
              MOVE     K223-FILE-STATUS    TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
           IF K225-FILE-STATUS NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING BP13K225 : ' K225-FILE-STATUS              
              MOVE     K225-FILE-STATUS    TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
           IF K764-FILE-STATUS NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING BP13K764 : ' K764-FILE-STATUS              
              MOVE     K764-FILE-STATUS    TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-PROCESS-MASTER.                                                     
      *-------------------------------------------------------------            
                                                                                
           SET WS-K223-EOF-NO        TO TRUE.                                   
                                                                                
           MOVE SPACES               TO K223-KEY-FLD.                           
                                                                                
           START BP13K223 KEY IS GREATER OR EQUAL K223-KEY-FLD                  
              END-START.                                                        
                                                                                
           EVALUATE K223-FILE-STATUS                                            
                                                                                
           WHEN 00                                                              
              PERFORM 2100-PROCESS-K223 THRU 2100-EXIT                          
                UNTIL WS-K223-EOF-YES                                           
                                                                                
           WHEN 10                                                              
              DISPLAY 'K223 END OF FILE'                                        
                                                                                
           WHEN 23                                                              
              DISPLAY 'K223 KEY NOT FOUND IN START OF K223: '                   
                          K223-KEY-FLD                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR STARTING BP13K223'                                 
              DISPLAY 'K223-FILE-STATUS: ' K223-FILE-STATUS                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2100-PROCESS-K223.                                                       
      *-------------------------------------------------------------            
                                                                                
           READ BP13K223 NEXT RECORD                                            
             AT END                                                             
                SET WS-K223-EOF-YES  TO   TRUE                                  
                GO TO 2100-EXIT.                                                
                                                                                
           IF K223-NUM-SALES-MODE = SPACES OR LOW-VALUES                        
              GO TO 2100-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF WS-SAVE-SALES-MODE NOT EQUAL K223-NUM-SALES-MODE                  
              MOVE K223-NUM-SALES-MODE   TO   WS-SAVE-SALES-MODE                
              MOVE ZEROES                TO   WS-SAVE-SALES-MODE-CNT            
              MOVE 'N'                   TO   WS-NEXT-BATCH                     
              MOVE 'N'                   TO   WS-K764-FOUND                     
              PERFORM 2110-READ-BP13K764 THRU 2110-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K764-FOUND = 'Y'                                               
              MOVE K223-NUM-CASES TO WS-K223-NUM-CASE-ALPHA                     
                                                                                
              IF K223-NUM-SALES-MODE = '3R1'                                    
                 COMPUTE WS-NUM-FIRST-BATCH =                                   
                  K764-NUM-APPLICATION-SA + K764-NUM-SETASIDE-QUEUE-SA          
              ELSE                                                              
                 COMPUTE WS-NUM-FIRST-BATCH =                                   
                  K764-NUM-APPLICATION + K764-NUM-SETASIDE-QUEUE                
              END-IF                                                            
                                                                                
              PERFORM 3000-CREATE-K225 VARYING WS-CTR FROM 1 BY 1               
                UNTIL WS-CTR > WS-K223-NUM-CASE                                 
                                                                                
              ADD 1 TO WS-K223-READ-CNT                                         
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-CREATE-K225.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES TO BP13K225-REC.                                         
           INITIALIZE     BP13K225-REC.                                         
                                                                                
           ADD 1 TO WS-SAVE-SALES-MODE-CNT.                                     
                                                                                
           IF (WS-SAVE-SALES-MODE-CNT <= WS-NUM-FIRST-BATCH) AND                
              (WS-NEXT-BATCH = 'N')                                             
              PERFORM 3100-CREATE-QUEUE-NUM1  THRU 3100-EXIT                    
           ELSE                                                                 
              IF WS-NEXT-BATCH = 'N'                                            
                 MOVE 'Y'                     TO WS-NEXT-BATCH                  
                 IF K223-NUM-SALES-MODE = 'WIH'                                 
                    MOVE 0001                 TO WS-SAVE-SALES-MODE-CNT         
                 ELSE                                                           
                    MOVE 6001                 TO WS-SAVE-SALES-MODE-CNT         
                 END-IF                                                         
              END-IF                                                            
              PERFORM 3110-CREATE-QUEUE-NUM2  THRU 3110-EXIT                    
           END-IF.                                                              
                                                                                
           STRING '##' K225-NUM-QUEUE                                           
              DELIMITED BY SIZE     INTO K225-NUM-NRIC.                         
                                                                                
           MOVE K223-NUM-SALES-MODE   TO K225-NUM-SALES-MODE.                   
           MOVE K223-DTE-APPOINTMENT  TO K225-DTE-APPOINTMENT                   
                                         WS-SAVE-DATE.                          
           MOVE K223-TME-APPOINTMENT  TO K225-TME-APPOINTMENT.                  
                                                                                
           MOVE 'BP13C509'            TO K225-NUM-USERID.                       
           MOVE FUNCTION CURRENT-DATE TO K225-DTE-UPDATE.                       
           ACCEPT K225-TME-UPDATE   FROM TIME.                                  
                                                                                
           WRITE BP13K225-REC.                                                  
                                                                                
           IF K225-FILE-STATUS EQUAL 00 OR 02                                   
              ADD 1 TO WS-K225-WRITE-CNT                                        
           ELSE                                                                 
              DISPLAY 'ERROR WRITING K225 - ' K225-FILE-STATUS                  
                      ' APPMT NOT WRITTEN: ' K225-KEY-FLD K225-ALT-KEY          
              ADD 1 TO WS-K225-WRITE-ERR                                        
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3100-CREATE-QUEUE-NUM1.                                                  
      *-------------------------------------------------------------            
            EVALUATE K223-NUM-SALES-MODE                                        
                WHEN '3R '                                                      
                   MOVE 'R' TO WS-PREFIX                                        
                WHEN '3R1'                                                      
                   MOVE 'S' TO WS-PREFIX                                        
            END-EVALUATE.                                                       
                                                                                
            IF K223-NUM-SALES-MODE = 'WIH'                                      
               MOVE WS-SAVE-SALES-MODE-CNT TO K225-NUM-QUEUE                    
            ELSE                                                                
               STRING WS-PREFIX WS-QUEUE-NUM-DIGITS                             
                  DELIMITED BY SIZE INTO K225-NUM-QUEUE                         
            END-IF.                                                             
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3110-CREATE-QUEUE-NUM2.                                                  
      *-------------------------------------------------------------            
            EVALUATE K223-NUM-SALES-MODE                                        
                WHEN 'WIH'                                                      
                   MOVE '2' TO WS-PREFIX                                        
                WHEN '3R '                                                      
                   MOVE 'T' TO WS-PREFIX                                        
                WHEN '3R1'                                                      
                   MOVE 'Y' TO WS-PREFIX                                        
            END-EVALUATE.                                                       
                                                                                
            STRING WS-PREFIX WS-QUEUE-NUM-DIGITS                                
                DELIMITED BY SIZE INTO K225-NUM-QUEUE.                          
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2110-READ-BP13K764.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                 TO WS-K764-FOUND.                           
           MOVE K223-NUM-SALES-MODE TO K764-NUM-SALES-MODE.                     
           IF K223-NUM-SALES-MODE = '3R1'                                       
              MOVE '3R '            TO K764-NUM-SALES-MODE                      
           END-IF.                                                              
                                                                                
           READ BP13K764.                                                       
                                                                                
           EVALUATE K764-FILE-STATUS                                            
                                                                                
              WHEN 00                                                           
                ADD 1 TO WS-K764-READ-CNT                                       
                MOVE 'Y' TO WS-K764-FOUND                                       
                                                                                
              WHEN 23                                                           
                DISPLAY 'KEY NOT FOUND IN READING K764: '                       
                         K764-KEY-FLD                                           
                ADD 1 TO WS-K764-READ-ERR                                       
                MOVE 'N' TO WS-K764-FOUND                                       
                                                                                
              WHEN OTHER                                                        
                DISPLAY 'ERROR READING BP13K764: ' K764-FILE-STATUS             
                MOVE     K764-FILE-STATUS TO RETURN-CODE                        
                PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       2110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '******  BP13C509  *************'.                           
           DISPLAY 'RECS READ   FROM BP13K223   : ' WS-K223-READ-CNT.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ   FROM BP13K764   : ' WS-K764-READ-CNT.           
           DISPLAY 'READ NOT FND  IN BP13K764   : ' WS-K764-READ-ERR.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN  IN BP13K225   : ' WS-K225-WRITE-CNT.          
           DISPLAY 'RECS ERROR WRITE BP13K225   : ' WS-K225-WRITE-ERR.          
                                                                                
           CLOSE   BP13K223                                                     
                   BP13K225                                                     
                   BP13K764.                                                    
                                                                                
           IF K223-FILE-STATUS NOT = 00                                         
              DISPLAY 'ERROR CLOSING BP13K223 : ' K223-FILE-STATUS              
              MOVE     K223-FILE-STATUS    TO RETURN-CODE.                      
                                                                                
           IF K225-FILE-STATUS NOT = 00                                         
              DISPLAY 'ERROR CLOSING BP13K225 : ' K225-FILE-STATUS              
              MOVE     K225-FILE-STATUS    TO RETURN-CODE.                      
                                                                                
           IF K764-FILE-STATUS NOT = 00                                         
              DISPLAY 'ERROR CLOSING BP13K764 : ' K764-FILE-STATUS              
              MOVE     K764-FILE-STATUS    TO RETURN-CODE.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
