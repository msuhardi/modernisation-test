       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF25.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  13/12/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  TO HOUSEKEEP BP13K730 AND BP13K825          *            
      *                                                            *            
      *  INPUT      :  BP13F740  CANCELLED CASES                   *            
      *  I-O        :  BP13K730  REQUEST FILE                      *            
      *                BP13K595  DATA ENTRY FILE FOR REQUEST       *            
      *                BP13K825  REQUEST OCCUPIER FILE             *            
      *                                                            *            
      * ========================================================== *            
      * CHG-NO   DATE     BY   DESCRIPTION                         *            
      * -------- -------- ---- ----------------------------------- *            
      * BP135148 13122013 SMR2 NEW PROGRAM                         *            
      * BP135373 10062014 SMR2 WRITE THE RECORDS INTO BP13K590     *            
      *                        BEFORE DELETION IN BP13K825         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740  ASSIGN       TO BP13F740.                           
                                                                                
           SELECT BP13K730  ASSIGN       TO BP13K730                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K730-NUM-REGN                       
                            FILE STATUS  IS WS-K730-STATUS.                     
                                                                                
           SELECT BP13K595  ASSIGN       TO BP13K595                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K595-KEY-FLD                        
                            FILE STATUS  IS WS-K595-STATUS.                     
                                                                                
           SELECT BP13K825  ASSIGN       TO BP13K825                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K825-KEY-FLD                        
                            FILE STATUS  IS WS-K825-STATUS.                     
                                                                                
           SELECT BP13K590  ASSIGN       TO BP13K590                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K590-KEY-FLD                        
                            FILE STATUS  IS WS-K590-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F740                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K730.                                                           
                                                                                
       FD   BP13K595                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K595.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K825.                                                           
                                                                                
       FD   BP13K590                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K590.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K730-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K595-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K825-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K590-STATUS             PIC 9(02) VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ               PIC 9(06) VALUE ZEROES.               
           05  WS-K730-FND                PIC 9(06) VALUE ZEROES.               
           05  WS-K730-NTFND              PIC 9(06) VALUE ZEROES.               
           05  WS-K595-WRITE              PIC 9(06) VALUE ZEROES.               
           05  WS-K595-DUPL               PIC 9(06) VALUE ZEROES.               
           05  WS-K590-WRITE              PIC 9(06) VALUE ZEROES.               
           05  WS-K590-DUPL               PIC 9(06) VALUE ZEROES.               
           05  WS-K730-DELETE             PIC 9(06) VALUE ZEROES.               
           05  WS-K825-DELETE             PIC 9(06) VALUE ZEROES.               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-K825-EOF                PIC X(01) VALUE 'N'.                  
                                                                                
       01  WS-DATE-TIME-VARS.                                                   
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY            PIC 9(04).                            
               10  WS-SYS-MM              PIC 9(02).                            
               10  WS-SYS-DD              PIC 9(02).                            
           05  WS-SYSTEM-TIME             PIC 9(08).                            
           05  WS-EDITED-DATE             PIC X(10).                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECORDS     THRU 3000-EXIT                      
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F740                                                 
                  I-O  BP13K730                                                 
                       BP13K595                                                 
                       BP13K825                                                 
                       BP13K590.                                                
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K730 OPEN ERROR ' WS-K730-STATUS                
              MOVE WS-K730-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K595-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K595 OPEN ERROR ' WS-K595-STATUS                
              MOVE WS-K595-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K825 OPEN ERROR ' WS-K825-STATUS                
              MOVE WS-K825-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K590-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K590 OPEN ERROR ' WS-K590-STATUS                
              MOVE WS-K590-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-SYSTEM-DATE.                  
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
             INTO WS-EDITED-DATE.                                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F740.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F740-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F740-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORDS.                                                    
      *-------------------------------------------------------------            
                                                                                
           IF F740-NUM-REGN NOT = SPACES AND LOW-VALUES                         
              PERFORM 4000-READ-BP13K730    THRU 4000-EXIT                      
              PERFORM 5000-STARTBR-K825     THRU 5000-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-READ-BP13K730.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                      TO BP13K730-REC.                    
           INITIALIZE                          BP13K730-REC.                    
                                                                                
           MOVE F740-NUM-REGN               TO K730-NUM-REGN.                   
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE WS-K730-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-K730-FND                      
              PERFORM 4100-WRITE-BP13K595   THRU 4100-EXIT                      
              PERFORM 4200-DELETE-K730      THRU 4200-EXIT                      
                                                                                
           WHEN 23                                                              
              ADD 1                         TO WS-K730-NTFND                    
              DISPLAY 'BP13K730 - RECORD NOT FOUND, KEY : '                     
                      K730-NUM-REGN                                             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K730 READ ERROR: ' WS-K730-STATUS                    
              MOVE WS-K730-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-WRITE-BP13K595.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                      TO BP13K595-REC.                    
           INITIALIZE                          BP13K595-REC.                    
                                                                                
           MOVE BP13K730-REC                TO BP13K595-REC.                    
           MOVE '01'                        TO K595-CDE-ELIG-TAG.               
           MOVE 'C'                         TO K595-CDE-REQUEST-STATUS.         
           MOVE K730-NUM-REGN               TO K595-NUM-REGN-REF.               
           MOVE WS-SYSTEM-DATE              TO K595-DTE-UPDATE.                 
           MOVE FUNCTION CURRENT-DATE(9:8)  TO WS-SYSTEM-TIME.                  
           MOVE WS-SYSTEM-TIME              TO K595-TME-SYSTEM.                 
                                                                                
           WRITE BP13K595-REC.                                                  
                                                                                
           EVALUATE WS-K595-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              ADD 1                         TO WS-K595-WRITE                    
                                                                                
           WHEN 22                                                              
              ADD 1                         TO WS-K595-DUPL                     
              DISPLAY 'DUPLICATE KEY FOR BP13K595. REGN : '                     
                      K595-NUM-REGN-REF                                         
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K595 WRITE ERROR: ' WS-K595-STATUS                   
              MOVE WS-K595-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4200-DELETE-K730.                                                        
      *-------------------------------------------------------------            
                                                                                
           DELETE BP13K730.                                                     
                                                                                
           EVALUATE WS-K730-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-K730-DELETE                   
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K730 DELETE ERROR: ' WS-K730-STATUS                  
              MOVE WS-K730-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-STARTBR-K825.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                      TO BP13K825-REC.                    
           INITIALIZE                          BP13K825-REC.                    
                                                                                
           MOVE 'N'                         TO WS-K825-EOF.                     
                                                                                
           MOVE F740-NUM-REGN               TO K825-NUM-REGN.                   
                                                                                
           START BP13K825 KEY IS >= K825-KEY-FLD.                               
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              PERFORM 5100-READNEXT-K825    THRU 5100-EXIT                      
                UNTIL K825-NUM-REGN NOT = F740-NUM-REGN                         
                   OR WS-K825-EOF = 'Y'                                         
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              DISPLAY 'BP13K825 - RECORD NOT FOUND, KEY : '                     
                      K825-NUM-REGN                                             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K825 STARTBR ERROR: ' WS-K825-STATUS                 
              MOVE WS-K825-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5100-READNEXT-K825.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K825 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                    TO WS-K825-EOF.                     
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              IF F740-NUM-REGN = K825-NUM-REGN                                  
                 PERFORM 5110-WRITE-K590    THRU 5110-EXIT                      
                 PERFORM 5200-DELETE-K825   THRU 5200-EXIT                      
              END-IF                                                            
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                      TO WS-K825-EOF                      
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K825 READNEXT ERROR: ' WS-K825-STATUS                
              MOVE WS-K825-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5110-WRITE-K590.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                      TO BP13K590-REC.                    
           INITIALIZE                          BP13K590-REC.                    
                                                                                
           MOVE BP13K825-REC                TO BP13K590-REC.                    
                                                                                
           WRITE BP13K590-REC.                                                  
                                                                                
           EVALUATE WS-K590-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              ADD 1                         TO WS-K590-WRITE                    
                                                                                
           WHEN 22                                                              
              ADD 1                         TO WS-K590-DUPL                     
              DISPLAY 'DUPLICATE KEY FOR BP13K590. KEY-FLD : '                  
                      K590-KEY-FLD                                              
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K590 WRITE ERROR: ' WS-K590-STATUS                   
              MOVE WS-K590-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5200-DELETE-K825.                                                        
      *-------------------------------------------------------------            
                                                                                
           DELETE BP13K825.                                                     
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-K825-DELETE                   
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K825 DELETE ERROR: ' WS-K825-STATUS                  
              MOVE WS-K825-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '==============================================='.           
           DISPLAY '      PROGRAM : BP13CF25       ' WS-EDITED-DATE.            
           DISPLAY '==============================================='.           
           DISPLAY ' '.                                                         
           DISPLAY 'BP13F740 RECORDS READ      : ' WS-F740-READ.                
           DISPLAY 'BP13K730 RECORDS FOUND     : ' WS-K730-FND.                 
           DISPLAY 'BP13K730 RECORDS NOT FOUND : ' WS-K730-NTFND.               
           DISPLAY 'BP13K595 RECORDS WRITTEN   : ' WS-K595-WRITE.               
           DISPLAY 'DUPLICATE BP13K595 RECORDS : ' WS-K595-DUPL.                
           DISPLAY 'BP13K730 RECORDS DELETED   : ' WS-K730-DELETE.              
           DISPLAY 'BP13K590 RECORDS WRITTEN   : ' WS-K590-WRITE.               
           DISPLAY 'DUPLICATE BP13K590 RECORDS : ' WS-K590-DUPL.                
           DISPLAY 'BP13K825 RECORDS DELETED   : ' WS-K825-DELETE.              
           DISPLAY ' '.                                                         
           DISPLAY '==============================================='.           
                                                                                
           CLOSE BP13F740                                                       
                 BP13K730                                                       
                 BP13K595                                                       
                 BP13K825                                                       
                 BP13K590.                                                      
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K730 CLOSE ERROR ' WS-K730-STATUS               
              MOVE WS-K730-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K595-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K595 CLOSE ERROR ' WS-K595-STATUS               
              MOVE WS-K595-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K825 CLOSE ERROR ' WS-K825-STATUS               
              MOVE WS-K825-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K590-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K590 CLOSE ERROR ' WS-K590-STATUS               
              MOVE WS-K590-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
