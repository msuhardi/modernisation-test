       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C22L.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  25 JULY 2017.                                             
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE   :  PROGRAM TO OUTPUT                           *            
      *                                                            *            
      *                1)  CREATE INTV FILE FOR ROF                *            
      *                    FORMAT WITH 3 REC TYPE                  *            
      *                    - HEADER,  REC-TY = 01                  *            
      *                    - DETAIL,  REC-TY = 02                  *            
      *                    - TRAILER, REC-TY = 03                  *            
      *                    - BLOCK           = 06                  *            
      *                                                            *            
      *  INPUT FILES :  1)  BP13F240  -  FLAT AVAILABILITY BLK FILE*            
      *                 2)  BP13K230  -  FLAT AVAILABILITY UNIT FILE            
      *  OUTPUT FILE :  1)  BP13F230  -  FLATLIST FILE(01,06,02,03)*            
      * ---------------------------------------------------------- *            
      * CHGE NO  OIC   DATE     DESCRIPTION                        *            
      * -------- ----  ------   -----------                        *            
      * BP136835 ESA1  250717   NEW PROGRAM.                       *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F240 ASSIGN TO BP13F240.                                  
                                                                                
           SELECT BP13K230 ASSIGN TO BP13K230                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K230-KEY-FLD                               
                  ALTERNATE KEY   IS K230-ALT-KEY4                              
                  FILE STATUS     IS K230-STATUS.                               
                                                                                
           SELECT BP13F230 ASSIGN TO BP13F230.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F240                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K240.                                                           
                                                                                
       FD   BP13K230                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K230.                                                           
                                                                                
       FD   BP13F230                                                            
            RECORDING MODE  IS  F                                               
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 450 CHARACTERS                                      
            LABEL  RECORDS  ARE STANDARD.                                       
       COPY BP13F230.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05 K230-STATUS                 PIC 9(2)    VALUE ZEROES.             
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-EOF-F240                PIC X       VALUE 'N'.                
           05  WS-EOF-K230                PIC X       VALUE 'N'.                
           05  WS-CNT-F240-READ           PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-HEADER-F230   PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-BLOCK-F230    PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-DETAIL-F230   PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-TRAILER-F230  PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-RT05-F230     PIC 9(5)    VALUE ZEROS.              
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM  100-OPEN-FILES        THRU   100-EXIT.                      
           PERFORM  200-READ-BP13F240     THRU   200-EXIT                       
           PERFORM  300-PROCESS-RECORD    THRU   300-EXIT                       
                    UNTIL WS-EOF-F240 = 'Y'.                                    
           PERFORM  9999-CLOSE-ROUTINE     THRU   9999-EXIT.                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F240                                                 
                       BP13K230                                                 
                OUTPUT BP13F230.                                                
                                                                                
           IF K230-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K230 - OPEN ERROR, K230-STATUS ' K230-STATUS         
              MOVE     K230-STATUS      TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE   THRU 9999-EXIT                      
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-BP13F240.                                                       
      *-------------------------------------------------------------            
           READ BP13F240                                                        
                AT END MOVE 'Y'        TO WS-EOF-F240                           
                GO TO 200-EXIT.                                                 
                                                                                
           ADD     1                   TO WS-CNT-F240-READ.                     
                                                                                
           MOVE SPACES                 TO F230-HEADER-REC.                      
           MOVE K240-HEADER-REC        TO F230-HEADER-REC.                      
           WRITE F230-HEADER-REC.                                               
           ADD 1                       TO WS-WRITE-CNT-HEADER-F230.             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-RECORD.                                                      
      *-------------------------------------------------------------            
                                                                                
           PERFORM 500-WRITE-BLOCK        THRU 500-EXIT                         
           PERFORM 400-STARTBR-BP13K230   THRU 400-EXIT.                        
           PERFORM 600-WRITE-TRAILER      THRU 600-EXIT.                        
                                                                                
           PERFORM 200-READ-BP13F240      THRU 200-EXIT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-STARTBR-BP13K230.                                                    
      *-------------------------------------------------------------            
           MOVE 'N'                 TO WS-EOF-K230.                             
           MOVE SPACES              TO K230-ALT-KEY4.                           
           MOVE K240-ALT-KEY4       TO K230-ALT-KEY4.                           
                                                                                
           START BP13K230 KEY >= K230-ALT-KEY4.                                 
           EVALUATE K230-STATUS                                                 
              WHEN 00                                                           
                 PERFORM 410-READNEXT-K230  THRU 410-EXIT                       
                    UNTIL WS-EOF-K230 = 'Y'                                     
              WHEN 10                                                           
              WHEN 23                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K230. STATUS - '                    
                                            K230-STATUS                         
                 MOVE K230-STATUS           TO RETURN-CODE                      
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       410-READNEXT-K230.                                                       
      *------------------------------------------------------------*            
                                                                                
            READ BP13K230 NEXT RECORD                                           
                 AT END MOVE 'Y' TO WS-EOF-K230                                 
            END-READ.                                                           
                                                                                
            IF K240-ALT-KEY4     =  K230-ALT-KEY4(1:45)                         
               MOVE SPACES            TO F230-DETAIL-REC                        
               MOVE K230-DETAIL-REC   TO F230-DETAIL-REC                        
               WRITE F230-DETAIL-REC                                            
               ADD 1                  TO WS-WRITE-CNT-DETAIL-F230               
             ELSE                                                               
               MOVE 'Y' TO WS-EOF-K230                                          
             END-IF.                                                            
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       500-WRITE-BLOCK.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE       SPACES                  TO    F230-BLOCK-REC.             
           INITIALIZE F230-BLOCK-REC.                                           
           MOVE    'INTV'                     TO                                
                       F230-CDE-INTV          OF    F230-BLOCK-REC.             
           MOVE    '06'                       TO    F230-CDE-BLOCK              
                                              OF    F230-BLOCK-REC.             
           MOVE    K240-DTE-ALLOC             TO    F230-DTE-ALLOC              
                                              OF    F230-BLOCK-REC.             
           MOVE    K240-NUM-NEW-TOWN          TO    F230-NUM-NEW-TOWN           
                                              OF    F230-BLOCK-REC.             
           MOVE    K240-DTE-BALLOT            TO    F230-DTE-BALLOT             
                                              OF    F230-BLOCK-REC.             
           MOVE    K240-NUM-SELECTION         TO                                
                       F230-NUM-SELECTION     OF    F230-BLOCK-REC.             
           MOVE    K240-NUM-BLK               TO                                
                       F230-NUM-BLK           OF    F230-BLOCK-REC.             
                                                                                
           MOVE    K240-NME-STREET            TO F230-NME-STREET                
                                              OF F230-BLOCK-REC.                
           MOVE  SPACES                       TO F230-NME-PROJECT               
                                              OF F230-BLOCK-REC.                
           IF K240-NUM-SELECTION = 'BTO' OR 'SER'                               
              MOVE 'A'                        TO F230-NUM-DISPLAY               
                                              OF F230-BLOCK-REC                 
           ELSE                                                                 
              MOVE 'B'                        TO F230-NUM-DISPLAY               
                                              OF F230-BLOCK-REC                 
           END-IF.                                                              
                                                                                
           WRITE F230-BLOCK-REC.                                                
                                                                                
           ADD    1   TO  WS-WRITE-CNT-BLOCK-F230.                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       600-WRITE-TRAILER.                                                       
      *-------------------------------------------------------------            
           MOVE  SPACES               TO F230-TRAILER-REC.                      
           MOVE 'INTV'                TO                                        
                    F230-CDE-INTV     OF F230-TRAILER-REC.                      
           MOVE '03'                  TO F230-CDE-TRAILER.                      
           MOVE K240-DTE-ALLOC        TO                                        
                    F230-DTE-ALLOC     OF F230-TRAILER-REC.                     
           MOVE K240-NUM-NEIGH        TO F230-NUM-NEIGH                         
                                       OF F230-TRAILER-REC.                     
           MOVE K240-NUM-CNTRCT       TO F230-NUM-CNTRCT                        
                                       OF F230-TRAILER-REC.                     
           MOVE K240-NUM-FLAT-TYPE    TO                                        
                 F230-NUM-FLAT-TYPE    OF F230-TRAILER-REC.                     
           MOVE K240-NUM-FLAT-TYPE2   TO                                        
                F230-NUM-FLAT-TYPE2   OF F230-TRAILER-REC                       
                F230-NUM-FLAT-TYPE3   OF F230-TRAILER-REC                       
                F230-NUM-FLAT-TYPE4   OF F230-TRAILER-REC                       
                F230-NUM-FLAT-TYPE5   OF F230-TRAILER-REC.                      
           MOVE K240-NUM-BLK2         TO                                        
                F230-NUM-BLK          OF F230-TRAILER-REC                       
                F230-NUM-BLK2         OF F230-TRAILER-REC                       
                F230-NUM-BLK3         OF F230-TRAILER-REC                       
                F230-NUM-BLK4         OF F230-TRAILER-REC.                      
           MOVE K240-NUM-NEW-TOWN2    TO                                        
                F230-NUM-NEW-TOWN     OF F230-TRAILER-REC                       
                F230-NUM-NEW-TOWN2    OF F230-TRAILER-REC                       
                F230-NUM-NEW-TOWN3    OF F230-TRAILER-REC                       
                F230-NUM-NEW-TOWN4    OF F230-TRAILER-REC                       
                F230-NUM-NEW-TOWN5    OF F230-TRAILER-REC.                      
           MOVE K240-NUM-ZONE         TO                                        
                F230-NUM-ZONE         OF F230-TRAILER-REC                       
                F230-NUM-ZONE2        OF F230-TRAILER-REC                       
                F230-NUM-ZONE3        OF F230-TRAILER-REC.                      
                                                                                
           MOVE SPACES                TO                                        
                F230-NUM-DESIGN       OF F230-DETAIL-REC.                       
           MOVE K240-NUM-DESIGN2      TO                                        
                F230-NUM-DESIGN2      OF F230-DETAIL-REC.                       
           MOVE K240-NUM-SELECTION    TO                                        
                F230-NUM-SELECTION    OF F230-TRAILER-REC                       
                F230-NUM-SELECTION2   OF F230-TRAILER-REC                       
                F230-NUM-SELECTION3   OF F230-TRAILER-REC                       
                F230-NUM-SELECTION4   OF F230-TRAILER-REC                       
                F230-NUM-SELECTION5   OF F230-TRAILER-REC.                      
                                                                                
           MOVE K240-DTE-BALLOT       TO                                        
                F230-DTE-BALLOT       OF F230-TRAILER-REC                       
                F230-DTE-BALLOT5      OF F230-TRAILER-REC.                      
                                                                                
           MOVE K240-NUM-NEIGH4       TO F230-NUM-NEIGH4                        
                                      OF F230-TRAILER-REC.                      
           MOVE K240-NUM-CNTRCT4      TO F230-NUM-CNTRCT4                       
                                      OF F230-TRAILER-REC.                      
           WRITE F230-TRAILER-REC.                                              
                                                                                
           ADD   1 TO WS-WRITE-CNT-TRAILER-F230.                                
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY '*----------- BP13C22L CONTROL LIST ----------*'.            
           DISPLAY '  '.                                                        
           DISPLAY 'NO OF BP13F240 READ        = ' WS-CNT-F240-READ.            
           DISPLAY 'NO OF HEADER RECORDS       = '                              
                    WS-WRITE-CNT-HEADER-F230.                                   
           DISPLAY 'NO OF BLOCK RECORDS        = '                              
                    WS-WRITE-CNT-BLOCK-F230.                                    
           DISPLAY 'NO OF DETAIL RECORDS       = '                              
                    WS-WRITE-CNT-DETAIL-F230.                                   
           DISPLAY 'NO OF TRAILER RECORDS      = '                              
                    WS-WRITE-CNT-TRAILER-F230.                                  
           DISPLAY 'NO OF INTV05  RECORDS      = '                              
                    WS-WRITE-CNT-RT05-F230.                                     
                                                                                
           CLOSE BP13F240                                                       
                 BP13K230                                                       
                 BP13F230.                                                      
                                                                                
           IF K230-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K230 CLOSE ERROR, K230-STATUS ' K230-STATUS          
              MOVE     K230-STATUS      TO       RETURN-CODE                    
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
