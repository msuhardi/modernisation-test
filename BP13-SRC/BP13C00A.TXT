      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C00A.                                                 
      *AUTHOR.        TEREZA SALONGA.                                           
                                                                                
      * ========================================================== *            
      *               SYSTEM OF COMMITMENT (BP13)                  *            
      * ========================================================== *            
      *  OBJECTIVES :                                              *            
      *                                                            *            
      *    1.  TO PULL OUT ALL RECORDS IN BP13K870 BASED ON        *            
      *        THE NUM-OPTION OF F205                              *            
      *                                                            *            
      *    INPUT FILES : BP13K870 - PANEL ACCESS RIGHTS FILE       *            
      *                  BP13F205 - BP13.F205.CTLREC               *            
      *    OUTPUT FILES: BP13F870 - PANEL ACCESS RIGHTS OUTPUT FILE*            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY    DATE        DESCRIPTION                     *            
      * -------- ----  ----------  ----------------------------    *            
      * BP132919 MTSR1 06/07/07    NEW PROGRAM                     *            
      * ========================================================== *            
                                                                                
      *---------------------------*                                             
       ENVIRONMENT DIVISION.                                                    
      *---------------------------*                                             
       CONFIGURATION SECTION.                                                   
      *---------------------------*                                             
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *---------------------------*                                             
           SELECT BP13F205 ASSIGN      TO   BP13F205.                           
                                                                                
           SELECT BP13K870 ASSIGN      TO   BP13K870                            
                  ACCESS MODE          IS   SEQUENTIAL                          
                  ORGANIZATION         IS   INDEXED                             
                  RECORD KEY           IS   K870-KEY-FLD                        
                  FILE STATUS          IS   WS-K870-STATUS.                     
                                                                                
           SELECT BP13F870 ASSIGN      TO   BP13F870.                           
                                                                                
                                                                                
      *---------------------------*                                             
       DATA DIVISION.                                                           
      *---------------------------*                                             
       FILE SECTION.                                                            
      *---------------------------*                                             
       FD   BP13F205                                                            
            BLOCK      CONTAINS 0    RECORDS                                    
            RECORD     CONTAINS 80   CHARACTERS                                 
            LABEL      RECORDS  ARE  STANDARD                                   
            RECORDING  MODE     IS   F.                                         
       COPY BP13F205.                                                           
                                                                                
       FD   BP13K870                                                            
            RECORD CONTAINS 30 CHARACTERS.                                      
       COPY BP13K870.                                                           
                                                                                
       FD   BP13F870                                                            
            BLOCK      CONTAINS 0    RECORDS                                    
            RECORD     CONTAINS 30   CHARACTERS                                 
            LABEL      RECORDS  ARE  STANDARD                                   
            RECORDING  MODE     IS   F.                                         
       COPY BP13F870.                                                           
                                                                                
                                                                                
      *---------------------------*                                             
       WORKING-STORAGE SECTION.                                                 
      *---------------------------*                                             
       01  WS-VARIABLES.                                                        
           05  WS-K870-STATUS             PIC 9(2).                             
           05  WS-F205-EOF                PIC X(1)    VALUE 'N'.                
           05  WS-K870-EOF                PIC X(1)    VALUE 'N'.                
                                                                                
           05  WS-SYSTEM-DATE             PIC X(8)    VALUE SPACES.             
                                                                                
           05  WS-F205-READ-CTR           PIC 9(6)    VALUE 0.                  
           05  WS-F870-WRITE-CTR          PIC 9(6)    VALUE 0.                  
                                                                                
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
                                                                                
      *-----------------------*                                                 
       0000-CONTROL.                                                            
      *-----------------------*                                                 
           PERFORM  1000-OPEN-FILES         THRU 1000-EXIT.                     
           PERFORM  2000-MAIN-ROUTINE       THRU 2000-EXIT                      
              UNTIL WS-F205-EOF = 'Y'.                                          
           PERFORM  9000-CLOSE-FILES        THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------*                                                 
       1000-OPEN-FILES.                                                         
      *-----------------------*                                                 
           OPEN INPUT  BP13F205                                                 
                       BP13K870                                                 
                OUTPUT BP13F870.                                                
                                                                                
           IF WS-K870-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K870 - OPEN ERROR '  WS-K870-STATUS                  
              MOVE    WS-K870-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-SYSTEM-DATE                  
           PERFORM 3000-READ-BP13F205      THRU 3000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------*                                                 
       2000-MAIN-ROUTINE.                                                       
      *-----------------------*                                                 
           PERFORM 3100-START-BP13K870      THRU   3100-EXIT.                   
                                                                                
           PERFORM 3000-READ-BP13F205       THRU   3000-EXIT.                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------*                                            
       3000-READ-BP13F205.                                                      
      *----------------------------*                                            
           READ BP13F205                                                        
              AT END MOVE 'Y'              TO   WS-F205-EOF                     
              GO TO  3000-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1                           TO   WS-F205-READ-CTR.               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------*                                                 
       3100-START-BP13K870.                                                     
      *-----------------------*                                                 
           MOVE 'N'                            TO   WS-K870-EOF.                
           MOVE LOW-VALUES                     TO   K870-KEY-FLD.               
                                                                                
           START BP13K870 KEY IS >= K870-KEY-FLD END-START.                     
                                                                                
           EVALUATE WS-K870-STATUS                                              
           WHEN 00                                                              
                PERFORM 3110-READNEXT-BP13K870 THRU 3110-EXIT                   
                   UNTIL WS-K870-EOF = 'Y'                                      
           WHEN 10                                                              
           WHEN 23                                                              
                CONTINUE                                                        
           WHEN OTHER                                                           
                DISPLAY 'BP13K870 - STARTBR ERROR ' WS-K870-STATUS              
                MOVE WS-K870-STATUS            TO   RETURN-CODE                 
                PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------*                                                 
       3110-READNEXT-BP13K870.                                                  
      *-----------------------*                                                 
           READ BP13K870 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K870-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
                EVALUATE F205-NUM-OPTION                                        
                WHEN 'A1'                                                       
                     PERFORM 4000-GET-DATA-BY-TEAM  THRU 4000-EXIT              
                WHEN 'A2'                                                       
                     PERFORM 4100-GET-DATA-BY-USER  THRU 4100-EXIT              
                WHEN 'A3'                                                       
                     PERFORM 4200-GET-DATA-BY-PANEL THRU 4200-EXIT              
                WHEN OTHER                                                      
                     DISPLAY 'INVALID F205-NUM-OPTION '                         
                                      F205-NUM-OPTION                           
                     MOVE 'Y'                       TO   WS-K870-EOF            
                END-EVALUATE                                                    
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y'                            TO   WS-K870-EOF            
           WHEN OTHER                                                           
                DISPLAY 'BP13K870 - READNEXT ERROR ' WS-K870-STATUS             
                        ' KEY = ' K870-KEY-FLD                                  
                MOVE WS-K870-STATUS                 TO   RETURN-CODE            
                PERFORM 9000-CLOSE-FILES            THRU 9000-EXIT              
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------*                                                 
       4000-GET-DATA-BY-TEAM.                                                   
      *-----------------------*                                                 
           IF K870-NUM-TEAMID = F205-NUM-NT-ZONE(1:1)                           
              WRITE BP13F870-REC FROM BP13K870-REC                              
              ADD 1              TO   WS-F870-WRITE-CTR                         
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------*                                                 
       4100-GET-DATA-BY-USER.                                                   
      *-----------------------*                                                 
           IF K870-NUM-USERID = F205-DTE-ALLOCN                                 
              WRITE BP13F870-REC FROM BP13K870-REC                              
              ADD 1              TO   WS-F870-WRITE-CTR                         
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------*                                                 
       4200-GET-DATA-BY-PANEL.                                                  
      *-----------------------*                                                 
           IF K870-NUM-PANELID = F205-DTE-ALLOCN(1:4)                           
              WRITE BP13F870-REC FROM BP13K870-REC                              
              ADD 1              TO   WS-F870-WRITE-CTR                         
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------*                                                 
       9000-CLOSE-FILES.                                                        
      *-----------------------*                                                 
           CLOSE BP13F205                                                       
                 BP13K870                                                       
                 BP13F870.                                                      
                                                                                
           IF WS-K870-STATUS NOT = 00                                           
              DISPLAY 'BP13K870 - CLOSE ERROR ' WS-K870-STATUS                  
              MOVE WS-K870-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           DISPLAY '*************************************'                      
           DISPLAY 'PROGRAM - BP13C00A'                                         
           DISPLAY 'PROCESSING DATE - ' WS-SYSTEM-DATE                          
           DISPLAY '*************************************'                      
           DISPLAY 'NO. OF RECS READ FROM F205   : ' WS-F205-READ-CTR.          
           DISPLAY 'NO. OF RECS WRITTEN IN F870  : ' WS-F870-WRITE-CTR.         
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
