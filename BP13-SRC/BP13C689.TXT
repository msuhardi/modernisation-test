       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C689.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  10/03/2017.                                               
      * ========================================================== *            
      * OBJECTIVE    : CONVERT AB02F875 TO BP13F680                *            
      * ========================================================== *            
      * INPUT  FILES :  1.) AB02F875                               *            
      * OUTPUT FILES :  1.) BP13F680                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136460 KSJ3 10/03/2017 NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AB02F875  ASSIGN        TO AB02F875.                          
                                                                                
           SELECT BP13F680  ASSIGN        TO BP13F680.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  AB02F875                                                             
           RECORD CONTAINS 1500 CHARACTERS.                                     
           COPY AB02F875.                                                       
                                                                                
       FD  BP13F680                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
           COPY BP13F680.                                                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F875-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F680-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-I                      PIC 9(2)   VALUE ZEROES.               
           05  WS-I-MAX                  PIC 9(2)   VALUE 50.                   
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F875-EOF               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE ZEROES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 2000-AB02F875-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F875-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  AB02F875                                                 
                OUTPUT BP13F680.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET CURRENT DATE                                                     
      *-------------------------------------------------------------            
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-AB02F875-READ.                                                      
      *=============================================================            
                                                                                
           READ AB02F875 NEXT                                                   
              AT END MOVE 'Y'              TO   WS-F875-EOF.                    
                                                                                
           IF WS-F875-EOF NOT = 'Y'                                             
              ADD 1                        TO   WS-CNT-F875-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13F680-REC.                   
           MOVE F875-NUM-SYSTEM            TO   F680-NUM-SYSTEM                 
           MOVE F875-NUM-HDB-REF           TO   F680-NUM-HDB-REF                
           MOVE F875-NUM-REGN              TO   F680-NUM-REGN                   
           MOVE F875-NUM-CASE              TO   F680-NUM-CASE                   
           MOVE F875-AMT-LOAN-GRANT        TO   F680-AMT-LOAN-GRANT             
           MOVE F875-DTE-LOAN-COMM         TO   F680-DTE-LOAN-COMM              
           MOVE F875-NUM-LOAN-SCHEME       TO   F680-NUM-LOAN-SCHEME            
           MOVE F875-AMT-VALUATION-LIMIT   TO   F680-AMT-VALUATION-LIMIT        
           MOVE F875-AMT-VALUATION-LIMIT-TAG                                    
                                       TO  F680-AMT-VALUATION-LIMIT-TAG         
           MOVE F875-LSE1-DETAILS(1:294)   TO   F680-LSE1-DETAILS(1:294)        
           MOVE F875-LSE2-DETAILS(1:294)   TO   F680-LSE2-DETAILS(1:294)        
           MOVE F875-LSE3-DETAILS(1:294)   TO   F680-LSE3-DETAILS(1:294)        
           MOVE F875-LSE4-DETAILS(1:294)   TO   F680-LSE4-DETAILS(1:294)        
           MOVE F875-NUM-USERID            TO   F680-NUM-USERID                 
           MOVE F875-NUM-OFFICE            TO   F680-NUM-OFFICE                 
           MOVE F875-NUM-ORIGINATOR        TO   F680-NUM-ORIGINATOR             
                                                                                
           MOVE ZEROES                TO   F680-AMT-ORIGIN-VAL-LIMIT            
                                           F680-MLP-VL-PCT                      
                                           F680-AMT-AHWL1                       
                                           F680-EARMK-OA-AMT1                   
                                           F680-AMT-AHWL2                       
                                           F680-EARMK-OA-AMT2                   
                                           F680-AMT-AHWL3                       
                                           F680-EARMK-OA-AMT3                   
                                           F680-AMT-AHWL4                       
                                           F680-EARMK-OA-AMT4.                  
                                                                                
           WRITE BP13F680-REC.                                                  
           ADD 1                           TO   WS-CNT-F680-WRITE.              
                                                                                
           PERFORM 2000-AB02F875-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE AB02F875                                                       
                 BP13F680.                                                      
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C689 CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF AB02F875 READ      : ' WS-CNT-F875-READ.          
           DISPLAY ' 2. NO OF BP13F680 WRITTEN   : ' WS-CNT-F680-WRITE.         
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
