       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CF7G.                                                  
       AUTHOR.       AM25.                                                      
       DATE-WRITTEN. 15/02/2023.                                                
      *****************************************************************         
      *                SOC - SYSTEM OF COMMITMENT (BP13)              *         
      *===============================================================*         
      *  OBJECTIVES :                                                 *         
      *     TO PROCESS BP13K762 RECORDS MEETING FOLLOWING CONDITIONS: *         
      *         - NTS FOR 3-ROOM AND BIGGER FLATS UNDER BTO AND       *         
      *         - FT FAMILY RATE IS LESS THAN 3 TIMES AND             *         
      *         - OVERALL APPLICATION RATE > 1 TIME                   *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP13K762 I     SEQUENTIAL 200  K762-KEY-FLD                   *         
      * BP13F762 O     SEQUENTIAL 200                                 *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *                                                               *         
      * CHG-REQ#  DDMMCCYY   BY   REMARKS                             *         
      * --------  --------  ----  ----------------------------------- *         
      * BP139516  15022023  AM25  NEW PROGRAM                         *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K762 ASSIGN      TO BP13K762                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS SEQUENTIAL                                       
              RECORD KEY    IS K762-KEY-FLD                                     
              FILE STATUS   IS WS-K762-STATUS.                                  
                                                                                
           SELECT BP13F762 ASSIGN      TO BP13F762.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K762                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K762.                                                           
                                                                                
       FD  BP13F762                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F762.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K762-STATUS          PIC 9(2)        VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-K762-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-F762-WRITE           PIC 9(6)        VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-K762-EOF             PIC X           VALUE 'N'.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13K762    THRU 2000-EXIT                         
              UNTIL WS-K762-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13K762                                                 
                OUTPUT BP13F762.                                                
                                                                                
           IF (WS-K762-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K762 FAIL STATUS=' WS-K762-STATUS               
              MOVE WS-K762-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13K762.                                                      
      *****************************************************************         
                                                                                
           READ BP13K762.                                                       
                                                                                
           EVALUATE WS-K762-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K762-READ                          
                                                                                
                 IF (K762-KEY-FLD NOT = SPACES AND LOW-VALUES)                  
                    PERFORM 3000-MAIN-PROCESSING THRU 3000-EXIT                 
                 END-IF                                                         
                                                                                
              WHEN 10                                                           
                 MOVE 'Y'              TO WS-K762-EOF                           
                                                                                
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K762 FAIL STATUS=' WS-K762-STATUS            
                 MOVE WS-K762-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
                                                                                
           END-EVALUATE.                                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-MAIN-PROCESSING.                                                    
      *****************************************************************         
                                                                                
           IF K762-NUM-FT(1:1) >= '3' AND                                       
              K762-NUM-DEMAND > K762-NUM-SUPPLY AND                             
              K762-NUM-1ST-TIMER < K762-NUM-1ST-TIMER-CNT * 3                   
              PERFORM 4000-WRITE-BP13F762 THRU 4000-EXIT                        
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-WRITE-BP13F762.                                                     
      *****************************************************************         
                                                                                
           WRITE BP13F762-REC FROM BP13K762-REC.                                
           ADD 1                       TO WS-F762-WRITE.                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13K762                                                       
                 BP13F762.                                                      
                                                                                
           IF WS-K762-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K762 FAIL STATUS=' WS-K762-STATUS              
              MOVE WS-K762-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '******************************'.                            
           DISPLAY '* PROGRAM BP13CF7G           *'.                            
           DISPLAY '* CONTROL COUNTS             *'.                            
           DISPLAY '*----------------------------*'.                            
           DISPLAY '* REC READ K762     : ' WS-K762-READ ' *'.                  
           DISPLAY '* REC WRITE F762    : ' WS-F762-WRITE ' *'.                 
           DISPLAY '******************************'.                            
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
