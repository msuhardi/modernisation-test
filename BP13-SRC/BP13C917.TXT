       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C917.                                                  
      *AUTHOR.       SONNY A.                                                   
      *DATE-WRITTEN. 30/11/96.                                                  
      ***********************************************************               
      *   SYSTEM NAME : SYSTEM OF COMMITMENT (SOC)              *               
      ***********************************************************               
      *                                                         *               
      *  INPUT FILE :- BE02F100                                 *               
      *  OUTPUT FILE:- BP13K820                                 *               
      *                                                         *               
      ***********************************************************               
      * CHG REQ    DATE    OIC   DESCRIPTION                    *               
      *--------- --------  ---   -----------                    *               
      * BP130383 20/01/98  LMS   TO EXPAND STREET NAME TO X(80).*               
      * BP130418 29/09/98  SIA   Y2K COMPLIANCE CHANGES         *               
      ***********************************************************               
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BE02F200 ASSIGN TO BE02F200.                                  
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                        ACCESS MODE          IS RANDOM                          
                        ORGANIZATION         IS INDEXED                         
                        RECORD KEY           IS K820-KEY-FLD                    
                        ALTERNATE RECORD KEY IS K820-NUM-NRIC                   
                                                WITH DUPLICATES                 
                        FILE STATUS          IS WS-K820-STATUS.                 
                                                                                
           SELECT BP13L917 ASSIGN TO BP13L917.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY BE02F200.                                                           
                                                                                
       FD  BP13K820   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 400 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13K820.                                                           
                                                                                
       FD  BP13L917                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  BP13L917-REC                PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F200-EOF               PIC X    VALUE 'N'.                    
               88 F200-EOF                        VALUE 'Y'.                    
           05  WS-FOUND                  PIC X    VALUE SPACES.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT-F200          PIC 9(6) VALUE ZEROS.                  
           05  WS-K820-MATCHED           PIC 9(6) VALUE ZEROS.                  
           05  WS-K820-BYPASS            PIC 9(6) VALUE ZEROS.                  
           05  WS-PAGE-CTR               PIC 9(4) VALUE ZEROS.                  
           05  WS-LINE-CTR               PIC 9(6) VALUE 65.                     
           05  WS-SEQN-CTR               PIC 9(6) VALUE ZEROS.                  
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K820-STATUS            PIC 99   VALUE ZEROS.                  
       01  WS-CURR-DATE.                                                        
           05 WS-CURR-CC           PIC 9(02).                                   
           05 WS-YYMMDD.                                                        
              10 WS-CURR-YY        PIC 9(02).                                   
              10 WS-CURR-MM        PIC 9(02).                                   
              10 WS-CURR-DD        PIC 9(02).                                   
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01  L917-PR-HEAD-01.                                                     
           05  FILLER              PIC X(8)      VALUE 'BP13L917'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L917-DATE           PIC X(10).                                   
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L917-PAGENO         PIC ZZZZ9 .                                  
                                                                                
       01  L917-PR-HEAD-02.                                                     
           05  FILLER              PIC X(45)     VALUE SPACES .                 
           05  FILLER              PIC X(37)     VALUE                          
               'REPORT ON MATCHED OSCARS UIN/FIN WITH'.                         
           05  FILLER              PIC X(43)     VALUE SPACES.                  
                                                                                
       01  L917-PR-HEAD-03.                                                     
           05  FILLER              PIC X(51)     VALUE SPACES .                 
           05  FILLER              PIC X(26)     VALUE                          
               'CANCELLED RFS APPLICATIONS'.                                    
           05  FILLER              PIC X(53)     VALUE SPACES.                  
                                                                                
       01  L917-PR-HEAD-04.                                                     
           05  FILLER              PIC X(45)     VALUE                          
               'S/N   UIN/FIN    NEW ADDRESS (FROM OSCARS)'.                    
           05  FILLER              PIC X(28)     VALUE SPACES.                  
                                                                                
       01  L917-PR-DTL-01.                                                      
           05  L917-SNO                 PIC ZZZ9.                               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L917-NRIC-NO             PIC X(9)    VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L917-BLK-RES-N           PIC X(10)   VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE SPACES.               
           05  L917-NUM-FLAT-N          PIC X(10)   VALUE SPACE.                
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L917-NME-STREET          PIC X(80)   VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L917-CDE-POSTAL          PIC X(09)   VALUE SPACES.               
                                                                                
       01  L917-PR-LINE                 PIC X(132)  VALUE ALL '-'.              
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F200    THRU 200-EXIT.                             
            PERFORM 300-PROCESS-K820 THRU 300-EXIT                              
                    UNTIL F200-EOF.                                             
            PERFORM 999-CLOSE-FILES  THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------                                                        
       100-OPEN-FILES.                                                          
      *-----------------                                                        
           OPEN INPUT  BE02F200                                                 
                       BP13K820                                                 
                OUTPUT BP13L917.                                                
                                                                                
           IF WS-K820-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K820-STATUS ' WS-K820-STATUS              
              MOVE WS-K820-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
                                                                                
           STRING WS-CURR-DD '/'                                                
                  WS-CURR-MM '/'                                                
                  WS-CURR-CC                                                    
                  WS-CURR-YY DELIMITED BY SIZE                                  
                             INTO L917-DATE.                                    
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------                                                        
       200-READ-F200.                                                           
      *-----------------                                                        
           READ BE02F200 AT END                                                 
           MOVE 'Y'                      TO WS-F200-EOF                         
           GO TO 200-EXIT.                                                      
           ADD 1                         TO WS-READ-CNT-F200.                   
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------                                                        
       300-PROCESS-K820.                                                        
      *-----------------                                                        
                                                                                
           MOVE BE02F200-NUM-NRIC TO K820-NUM-NRIC.                             
           READ BP13K820 KEY IS K820-NUM-NRIC.                                  
                                                                                
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
                PERFORM 400-PRINT-L917         THRU 400-EXIT                    
                ADD     1                      TO   WS-K820-MATCHED             
                                                                                
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K820 : ' K820-NUM-NRIC         
                ADD     1                      TO   WS-K820-BYPASS              
           WHEN OTHER                                                           
                DISPLAY 'READING ERROR, K820-STATUS ' WS-K820-STATUS            
                MOVE WS-K820-STATUS            TO RETURN-CODE                   
                PERFORM 999-CLOSE-FILES THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
           PERFORM 200-READ-F200    THRU 200-EXIT.                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------                                                     
       400-PRINT-L917.                                                          
      *--------------------                                                     
           IF WS-LINE-CTR > 55                                                  
              ADD  1              TO   WS-PAGE-CTR                              
              MOVE 5              TO   WS-LINE-CTR                              
              MOVE WS-PAGE-CTR    TO   L917-PAGENO                              
              WRITE BP13L917-REC  FROM L917-PR-HEAD-01 AFTER PAGE               
              WRITE BP13L917-REC  FROM L917-PR-HEAD-02                          
              WRITE BP13L917-REC  FROM L917-PR-HEAD-03                          
              WRITE BP13L917-REC  FROM L917-PR-LINE    AFTER 2                  
              WRITE BP13L917-REC  FROM L917-PR-HEAD-04                          
              WRITE BP13L917-REC  FROM L917-PR-LINE.                            
                                                                                
                                                                                
           ADD  1                            TO  WS-SEQN-CTR.                   
           MOVE WS-SEQN-CTR                  TO  L917-SNO.                      
           MOVE K820-NUM-NRIC                TO  L917-NRIC-NO.                  
           MOVE BE02F200-NEW-ADDR-TEXT-LINE1 TO  L917-NME-STREET(1:40).         
           MOVE BE02F200-NEW-ADDR-TEXT-LINE2 TO  L917-NME-STREET(41:40).        
                                                                                
           IF BE02F200-HDB-PTY-INDICATOR  = 'H'                                 
              MOVE 'BLK'                     TO  L917-BLK-RES-N                 
              MOVE BE02F200-NEW-ADDR-NUM-BLK TO  L917-BLK-RES-N(5:6)            
           ELSE                                                                 
              MOVE BE02F200-NEW-ADDR-NUM-BLK TO  L917-BLK-RES-N(1:10)           
           END-IF.                                                              
                                                                                
           IF BE02F200-NEW-ADDR-NUM-LEVEL  > SPACES  AND                        
              BE02F200-NEW-ADDR-NUM-UNIT   > SPACES                             
                                                                                
              STRING '#' BE02F200-NEW-ADDR-NUM-LEVEL                            
                     '-' BE02F200-NEW-ADDR-NUM-UNIT                             
                         DELIMITED BY SIZE                                      
                         INTO L917-NUM-FLAT-N                                   
              END-STRING                                                        
           ELSE                                                                 
              MOVE SPACES  TO L917-NUM-FLAT-N                                   
           END-IF.                                                              
                                                                                
           IF BE02F200-NEW-ADDR-POSTAL-CDE = SPACES                             
              MOVE SPACES TO L917-CDE-POSTAL                                    
           ELSE                                                                 
              STRING 'S(' BE02F200-NEW-ADDR-POSTAL-CDE ')'                      
                         DELIMITED BY SIZE                                      
                         INTO L917-CDE-POSTAL                                   
              END-STRING                                                        
           END-IF.                                                              
                                                                                
           WRITE BP13L917-REC  FROM L917-PR-DTL-01.                             
           ADD   1             TO   WS-LINE-CTR .                               
                                                                                
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       999-CLOSE-FILES.                                                         
                                                                                
           CLOSE BE02F200                                                       
                 BP13K820                                                       
                 BP13L917.                                                      
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K820, STATUS ' WS-K820-STATUS          
              MOVE WS-K820-STATUS        TO RETURN-CODE.                        
                                                                                
                                                                                
           DISPLAY '*------------------ BP13C917 -------------------'.          
           DISPLAY '                  ' WS-CURR-DATE                            
           DISPLAY 'NO OF REC READ        BE02F200   = '                        
                    WS-READ-CNT-F200.                                           
           DISPLAY 'NO OF REC READ        BP13K820   = '                        
                    WS-K820-MATCHED                                             
           DISPLAY 'NO OF REC NOTFOUND    BP13K820   = '                        
                    WS-K820-BYPASS.                                             
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
