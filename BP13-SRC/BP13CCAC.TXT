      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CCAC.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  13/02/2009.                                               
      *****************************************************************         
      * OBJECTIVE - PRINT MONTHLY REPORT ON ADDTIONAL LOAN DISBURSED            
      *                                                                         
      *      INPUT FILE : 1. BP13F136                                           
      *                   2. SY02F001                                           
      *     OUTPUT FILE : 1. BP13LCAC                                           
      *----------------------------------------------------------------         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS                        
      * -------   --------  ---  ------------------------                       
      * BP133545  20090213  JB8  NEW PROGRAM.                                   
      * BP134089  20110126  JB8  TO PRINT SEPARATE PP-SCH                       
      *                          TO PRINT CORRECT DATE                          
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  SY02F001  ASSIGN        TO SY02F001.                         
           SELECT  BP13F136  ASSIGN        TO BP13F136.                         
           SELECT  BP13LCAC  ASSIGN        TO BP13LCAC.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F136                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 150 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F136.                                                           
                                                                                
       FD  BP13LCAC                    BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 132 CHARACTERS           
                                       LABEL RECORDS ARE OMITTED                
                                       RECORDING MODE IS F.                     
       01  BP13LCAC-REC                PIC X(132).                              
                                                                                
       COPY SY02F001.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
                                                                                
       01  WS-VARIBLES.                                                         
           05  WS-F136-READ              PIC 9(06) VALUE ZEROS.                 
           05  WS-F136A-WRITE            PIC 9(06) VALUE ZEROS.                 
           05  CNT-F120-NTFND            PIC 9(06) VALUE ZEROS.                 
           05  WS-PAGE-CNT               PIC 9(06) VALUE ZEROS.                 
           05  WS-REC-CNT                PIC 9(06) VALUE 60.                    
           05  WS-NUM-FT                 PIC X(01) VALUE SPACES.                
           05  WS-NUM-REGN               PIC X(08) VALUE SPACES.                
           05  WS-FIRST-REGN             PIC X(01) VALUE 'N'.                   
           05  WS-AMT-PAYMENT-DVLPR      PIC 9(06)V99 VALUE ZEROES.             
           05  WS-F136-EOF               PIC X(01) VALUE SPACE.                 
               88  F136-EOF              VALUE 'Y'.                             
           05  WS-CNT-CASES              PIC 9(05) VALUE ZEROES.                
           05  WS-TOT-CASES              PIC 9(05) VALUE ZEROES.                
           05  WS-CNT-SPACE              PIC 9(05) VALUE ZEROES.                
           05  WS-AMT-SUB-TOT            PIC S9(9)V99 VALUE ZEROES.             
           05  WS-AMT-TOT                PIC S9(10)V99 VALUE ZEROES.            
           05  WS-CURR-DATE              PIC X(08) VALUE SPACES.                
                                                                                
       01  BP13LCAC-MAIN-TITLE.                                                 
           05  FILLER              PIC X(8)      VALUE 'BP13LCAC'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  LCAC-CURR-DATE      PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  LCAC-NUM-PAGE       PIC ZZZZ9     VALUE ZEROES.                  
                                                                                
       01  BP13LCAC-TITLE.                                                      
           05  FILLER                    PIC X(40) VALUE SPACES.                
           05  FILLER                    PIC X(48) VALUE                        
               'MONTHLY REPORT ON ADDITIONAL LOAN DISBURSED FOR '.              
           05  LCAC-PREV-MTH             PIC X(08) VALUE SPACES.                
                                                                                
       01  BP13LCAC-SUB-TITLE.                                                  
           05  FILLER                    PIC X(55) VALUE SPACES.                
           05  FILLER                    PIC X(21) VALUE                        
               '(GL 54-0810 VIA EWPC)'.                                         
           05  FILLER                    PIC X(56) VALUE SPACES.                
                                                                                
       01  BP13LCAC-FLAT-TYPE.                                                  
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(12) VALUE 'FLAT TYPE : '.        
           05  LCAC-FLAT-TYPE            PIC X(01) VALUE SPACE.                 
           05  FILLER                    PIC X(05) VALUE ' ROOM'.               
           05  FILLER                    PIC X(112) VALUE SPACES.               
                                                                                
       01  BP13LCAC-HEADER.                                                     
           05  FILLER                    PIC X(15) VALUE SPACES.                
           05  FILLER                    PIC X(06) VALUE 'S/N   '.              
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  FILLER                    PIC X(08) VALUE 'REGN'.                
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  FILLER                    PIC X(14) VALUE 'HDB REF'.             
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  FILLER                    PIC X(05) VALUE 'SCHDL'.               
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  FILLER                    PIC X(14) VALUE                        
                                                   'LOAN DISBURSED'.            
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  FILLER                    PIC X(10) VALUE 'DUE DATE'.            
                                                                                
       01  BP13LCAC-DASH.                                                       
           05  FILLER                    PIC X(15) VALUE SPACES.                
           05  FILLER                    PIC X(06) VALUE ALL '-'.               
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  FILLER                    PIC X(08) VALUE ALL '-'.               
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  FILLER                    PIC X(14) VALUE ALL '-'.               
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  FILLER                    PIC X(05) VALUE ALL '-'.               
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  FILLER                    PIC X(14) VALUE ALL '-'.               
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  FILLER                    PIC X(10) VALUE ALL '-'.               
                                                                                
       01  BP13LCAC-DETAIL.                                                     
           05  FILLER                    PIC X(15) VALUE SPACES.                
           05  LCAC-SN                   PIC ZZZZZ9 VALUE ZEROES.               
           05  LCAC-SN-CHAR REDEFINES LCAC-SN PIC X(06).                        
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  LCAC-REGN-NO              PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  LCAC-HDB-REF              PIC X(14) VALUE SPACES.                
           05  FILLER                    PIC X(10) VALUE SPACES.                
           05  LCAC-NUM-SCHDL            PIC X(02) VALUE ZEROES.                
           05  FILLER                    PIC X(11) VALUE SPACES.                
           05  LCAC-AMT-LOAN-GRANT    PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.          
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  LCAC-DTE-DUE              PIC X(10) VALUE SPACES.                
                                                                                
       01  BP13LCAC-SUB-TOTAL.                                                  
           05  FILLER                    PIC X(72) VALUE SPACES.                
           05  FILLER                    PIC X(13) VALUE 'SUB-TOTAL : '.        
           05  LCAC-AMT-SUB-TOTAL        PIC ZZ,ZZZ,ZZ9.99 VALUE ZEROS.         
                                                                                
       01  BP13LCAC-TOTAL.                                                      
           05  FILLER                    PIC X(70) VALUE SPACES.                
           05  FILLER                    PIC X(14) VALUE                        
               'GRANT-TOTAL : '.                                                
           05  LCAC-AMT-TOTAL            PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROS.        
                                                                                
       01  BP13LCAC-DASH2.                                                      
           05  FILLER                    PIC X(84) VALUE SPACES.                
           05  FILLER                    PIC X(14) VALUE ALL '-'.               
                                                                                
       01  BP13LCAC-SUMMARY.                                                    
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'SUMMARY'.             
                                                                                
       01  BP13LCAC-TOT-CASE.                                                   
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  FILLER                    PIC X(39) VALUE                        
               'TOTAL NO. OF CASES          : '.                                
           05  LCAC-NUM-TOT-CASE         PIC ZZZZ9 VALUE ZEROES.                
                                                                                
       01  BP13LCAC-TOT-AMT.                                                    
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  FILLER                    PIC X(30) VALUE                        
               'TOTAL AMT OF LOAN DISBURSED : '.                                
           05  LCAC-NUM-TOT-AMT          PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROS.        
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN-PARA.                                                          
      ******************************************************************        
                                                                                
           PERFORM 1000-START-UP           THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F136      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-RECORD     THRU 3000-EXIT                       
             UNTIL F136-EOF.                                                    
           PERFORM 4200-PRINT-GRT-TOT      THRU 4200-EXIT.                      
           PERFORM 4300-PRINT-SUMMARY      THRU 4300-EXIT.                      
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-START-UP.                                                           
      ******************************************************************        
                                                                                
           OPEN INPUT  SY02F001                                                 
                       BP13F136                                                 
                OUTPUT BP13LCAC.                                                
                                                                                
           READ SY02F001.                                                       
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2000-READ-BP13F136.                                                      
      ******************************************************************        
                                                                                
           READ BP13F136           AT   END                                     
                MOVE 'Y'           TO   WS-F136-EOF                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-F136-READ.                           
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORD.                                                     
      ******************************************************************        
                                                                                
           MOVE F136-NUM-FLAT-TYPE-SA(2:1)     TO WS-NUM-FT                     
           MOVE F136-NUM-REGN                  TO WS-NUM-REGN                   
           MOVE 60                             TO WS-REC-CNT                    
           MOVE 'Y'                            TO WS-FIRST-REGN                 
           MOVE ZEROES                         TO WS-AMT-SUB-TOT                
                                                  WS-CNT-CASES                  
                                                  WS-AMT-PAYMENT-DVLPR.         
                                                                                
           PERFORM 3100-PROCESS-CASES        THRU 3100-EXIT                     
             UNTIL F136-NUM-FLAT-TYPE-SA(2:1) NOT = WS-NUM-FT                   
                OR F136-EOF.                                                    
                                                                                
           PERFORM 4100-PRINT-SUB-TOT        THRU 4100-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-PROCESS-CASES.                                                      
      ******************************************************************        
                                                                                
           IF F136-NUM-REGN = WS-NUM-REGN                                       
              MOVE F136-AMT-RECEIPT        TO LCAC-AMT-LOAN-GRANT               
              COMPUTE WS-AMT-SUB-TOT = WS-AMT-SUB-TOT +                         
                                       F136-AMT-RECEIPT                         
              COMPUTE WS-AMT-TOT = WS-AMT-TOT +                                 
                                   F136-AMT-RECEIPT                             
                                                                                
              STRING F136-DTE-PP-DUE(7:2) '/'                                   
                     F136-DTE-PP-DUE(5:2) '/'                                   
                     F136-DTE-PP-DUE(1:4)                                       
                     DELIMITED BY SIZE INTO LCAC-DTE-DUE                        
                                                                                
              MOVE F136-NUM-PP-SCHEDULE    TO LCAC-NUM-SCHDL                    
                                                                                
              IF WS-FIRST-REGN = 'Y'                                            
                 ADD 1                     TO WS-CNT-CASES                      
                 ADD 1                     TO WS-TOT-CASES                      
                 MOVE WS-CNT-CASES         TO LCAC-SN                           
                                                                                
                 MOVE F136-NUM-REGN        TO LCAC-REGN-NO                      
                 STRING F136-NUM-SCH-ACCT(1:4) '-'                              
                        F136-NUM-SCH-ACCT(5:4) '-'                              
                        F136-NUM-SCH-ACCT(9:1) '-'                              
                        F136-NUM-SCH-ACCT(10:2)                                 
                    DELIMITED BY SIZE INTO LCAC-HDB-REF                         
                 MOVE 'N'                  TO WS-FIRST-REGN                     
              ELSE                                                              
                 MOVE SPACES               TO LCAC-REGN-NO                      
                                              LCAC-HDB-REF                      
                                              LCAC-SN-CHAR                      
              END-IF                                                            
                                                                                
              PERFORM 3200-PRINT-REPORT  THRU 3200-EXIT                         
              PERFORM 2000-READ-BP13F136 THRU 2000-EXIT                         
           ELSE                                                                 
              MOVE F136-NUM-REGN           TO WS-NUM-REGN                       
              MOVE 'Y'                     TO WS-FIRST-REGN                     
              MOVE ZEROES                  TO WS-AMT-PAYMENT-DVLPR              
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
           ADD 1                         TO WS-REC-CNT                          
           IF WS-REC-CNT > 60                                                   
              PERFORM 4000-PRINT-HEADER THRU 4000-EXIT                          
              ADD 1                      TO WS-REC-CNT                          
           END-IF.                                                              
                                                                                
           WRITE BP13LCAC-REC FROM BP13LCAC-DETAIL.                             
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           ADD 1 TO WS-PAGE-CNT                                                 
           STRING F001-DTE-CURRENT(7:2) '/'                                     
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
              DELIMITED BY SIZE INTO LCAC-CURR-DATE                             
           STRING F001-TXT-PREV-MTH(1:3) ' '                                    
                  F001-LST-PREV-CC F001-LST-PREV-YY                             
              DELIMITED BY SIZE INTO LCAC-PREV-MTH                              
           MOVE WS-PAGE-CNT              TO LCAC-NUM-PAGE.                      
                                                                                
           MOVE SPACES                   TO BP13LCAC-REC.                       
           WRITE BP13LCAC-REC AFTER PAGE.                                       
           WRITE BP13LCAC-REC FROM BP13LCAC-MAIN-TITLE AFTER 2.                 
           WRITE BP13LCAC-REC FROM BP13LCAC-TITLE.                              
           WRITE BP13LCAC-REC FROM BP13LCAC-SUB-TITLE.                          
                                                                                
           MOVE WS-NUM-FT                TO LCAC-FLAT-TYPE                      
                                                                                
           WRITE BP13LCAC-REC FROM BP13LCAC-FLAT-TYPE AFTER 2                   
           WRITE BP13LCAC-REC FROM BP13LCAC-HEADER AFTER 2                      
           WRITE BP13LCAC-REC FROM BP13LCAC-DASH                                
                                                                                
           MOVE 10                       TO WS-REC-CNT.                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4100-PRINT-SUB-TOT.                                                      
      ******************************************************************        
                                                                                
           ADD 4                         TO WS-REC-CNT.                         
           IF WS-REC-CNT > 60                                                   
              PERFORM 4000-PRINT-HEADER THRU 4000-EXIT                          
              ADD 4                      TO WS-REC-CNT                          
           END-IF.                                                              
                                                                                
           MOVE WS-AMT-SUB-TOT           TO LCAC-AMT-SUB-TOTAL.                 
           WRITE BP13LCAC-REC FROM BP13LCAC-DASH2 AFTER 2.                      
           WRITE BP13LCAC-REC FROM BP13LCAC-SUB-TOTAL.                          
           WRITE BP13LCAC-REC FROM BP13LCAC-DASH2.                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4200-PRINT-GRT-TOT.                                                      
      ******************************************************************        
                                                                                
           ADD 3                         TO WS-REC-CNT.                         
           IF WS-REC-CNT > 60                                                   
              PERFORM 4000-PRINT-HEADER THRU 4000-EXIT                          
              ADD 3                      TO WS-REC-CNT                          
           END-IF.                                                              
                                                                                
           MOVE WS-AMT-TOT               TO LCAC-AMT-TOTAL                      
           WRITE BP13LCAC-REC FROM BP13LCAC-TOTAL AFTER 2.                      
           WRITE BP13LCAC-REC FROM BP13LCAC-DASH2.                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4300-PRINT-SUMMARY.                                                      
      ******************************************************************        
                                                                                
           ADD 5                         TO WS-REC-CNT.                         
           IF WS-REC-CNT > 60                                                   
              PERFORM 4000-PRINT-HEADER THRU 4000-EXIT                          
              ADD 5                      TO WS-REC-CNT                          
           END-IF.                                                              
                                                                                
           COMPUTE WS-CNT-SPACE = 60 - WS-REC-CNT + 1 - 5                       
           MOVE WS-TOT-CASES             TO LCAC-NUM-TOT-CASE                   
           MOVE WS-AMT-TOT               TO LCAC-NUM-TOT-AMT                    
           WRITE BP13LCAC-REC FROM BP13LCAC-SUMMARY AFTER WS-CNT-SPACE          
           WRITE BP13LCAC-REC FROM BP13LCAC-TOT-CASE AFTER 2.                   
           WRITE BP13LCAC-REC FROM BP13LCAC-TOT-AMT AFTER 2.                    
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13LCAC'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13F136 RECORDS READ..........: '                    
                    WS-F136-READ.                                               
           DISPLAY 'NO OF BP13LCAC RECORDS WRITTEN.......: '                    
                    WS-F136A-WRITE.                                             
                                                                                
           CLOSE SY02F001                                                       
                 BP13F136                                                       
                 BP13LCAC.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
