       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C23B.                                                 
      *AUTHOR.        LMS.                                                      
      *DATE-WRITTEN.  24/04/2007.                                               
      * ========================================================== *            
      *             SYSTEM OF COMMITMENT  (BP13)                   *            
      * ========================================================== *            
      *                                                            *            
      *  DESCRIPTION :  TO CHECK FOR UTS TAG                       *            
      *  INPUT FILE  :  BP13F820                                   *            
      *              :  BR11K010                                   *            
      * ---------------------------------------------------------- *            
      * CHG-NO.  BY  ON          DESCRIPTION                       *            
      * -------- --- ----------  -----------                       *            
      * BP133046 LMS 24/04/2006  NEW PROGRAM.                      *            
      * BP133911 YCH 29/05/2010  CHANGE IN BR11K010 AND BR11K020   *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F820  ASSIGN       TO BP13F820.                           
                                                                                
           SELECT BR11K010  ASSIGN       TO BR11K010                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K010-NUM-KEY                        
                            FILE STATUS  IS WS-K010-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *---------------------------------------------------------------*         
       FD  BP13F820                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 400  CHARACTERS.                                     
       01  BP13F820-REC.                                                        
           05  F820-NUM-REGN         PIC X(8).                                  
           05  F820-NUM-NRIC         PIC X(9).                                  
           05  FILLER                PIC X(382).                                
           05  F820-NUM-UTS          PIC X(1).                                  
                                                                                
       FD  BR11K010                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BR11K010.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-VARIABLES.                                                        
           05  WS-K010-STATUS      PIC 9(02)  VALUE ZEROES.                     
           05  WS-F820-EOF         PIC X(01)  VALUE SPACES.                     
               88 F820-END-OF-FILE   VALUE 'Y'.                                 
           05  WS-F820-READ-CNT    PIC 9(08)  VALUE ZEROES.                     
           05  WS-K010-READ-CNT    PIC 9(08)  VALUE ZEROES.                     
           05  WS-F820-UPD-CNT     PIC 9(08)  VALUE ZEROES.                     
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05 BR11K010-EOF         PIC X(01)  VALUE 'N'.                        
                                                                                
       01  WS-BR11K010-FOUND       PIC X(01)  VALUE SPACES.                     
           88 K010-KEY-FOUND         VALUE 'Y'.                                 
                                                                                
       01  WS-EDT-CNT              PIC Z(7)9.                                   
                                                                                
       01  WS-SYS-DATE             PIC 9(08)  VALUE ZEROES.                     
                                                                                
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13F820     THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-F820      THRU 3000-EXIT                        
                                 UNTIL F820-END-OF-FILE.                        
           PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BR11K010                                                 
                I-O    BP13F820.                                                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BR11K010 OPEN ERROR, STATUS : ' WS-K010-STATUS           
              MOVE     WS-K010-STATUS       TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYS-DATE.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F820.                                                      
      *---------------------------------------------------------------*         
           READ BP13F820 AT END                                                 
                          MOVE 'Y'  TO WS-F820-EOF                              
                         NOT AT END                                             
                          ADD  1    TO WS-F820-READ-CNT                         
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-F820.                                                       
      *---------------------------------------------------------------*         
                                                                                
           MOVE  SPACE                  TO   WS-BR11K010-FOUND.                 
           MOVE  SPACES                 TO   K010-NUM-KEY.                      
           MOVE  F820-NUM-NRIC          TO   K010-NUM-UIN.                      
           PERFORM 3100-START-BR11K010  THRU 3100-EXIT                          
                                                                                
           PERFORM 2000-READ-BP13F820   THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3100-START-BR11K010.                                                     
      *---------------------------------------------------------------*         
           START BR11K010 KEY IS >= K010-NUM-KEY.                               
           PERFORM 3150-READNEXT-BR11K010 THRU 3150-EXIT                        
             UNTIL K010-NUM-KEY NOT = F820-NUM-NRIC                             
                OR BR11K010-EOF = 'Y'.                                          
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3150-READNEXT-BR11K010.                                                  
      *---------------------------------------------------------------*         
                                                                                
           READ BR11K010  NEXT RECORD                                           
             AT END MOVE 'Y'      TO BR11K010-EOF                               
             GO TO 3150-EXIT.                                                   
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN 00                                                              
             IF K010-NUM-UIN (1:9) = F820-NUM-NRIC                              
                IF K010-NUM-PRGRM-TYPE = 'MUP'                                  
                   ADD 1 TO WS-K010-READ-CNT                                    
                   MOVE 'Y' TO F820-NUM-UTS                                     
                   REWRITE BP13F820-REC                                         
                   ADD 1 TO WS-F820-UPD-CNT                                     
                END-IF                                                          
             END-IF                                                             
           WHEN 23                                                              
             DISPLAY 'RECORD NOT FOUND IN BR11K010, KEY : '                     
                      F820-NUM-NRIC                                             
           WHEN OTHER                                                           
             DISPLAY 'ERROR READING BR11K010, STATUS = ' WS-K010-STATUS         
             MOVE WS-K010-STATUS            TO RETURN-CODE                      
             PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3150-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY ' '.                                                         
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
           DISPLAY '   PROGRAM : BP13C23B   ' .                                 
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
           DISPLAY ' '.                                                         
           MOVE WS-F820-READ-CNT     TO WS-EDT-CNT.                             
           DISPLAY 'RECS READ FROM BP13F820    : ' WS-EDT-CNT.                  
           MOVE WS-K010-READ-CNT     TO WS-EDT-CNT.                             
           DISPLAY 'RECS READ FROM BR11K010    : ' WS-EDT-CNT.                  
           MOVE WS-F820-UPD-CNT      TO WS-EDT-CNT.                             
           DISPLAY 'RECS UPDATED IN BR11K010   : ' WS-EDT-CNT.                  
           DISPLAY ' '.                                                         
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
                                                                                
           CLOSE BP13F820                                                       
                 BR11K010.                                                      
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BR11K010 CLOSE ERROR, STATUS : ' WS-K010-STATUS          
              MOVE     WS-K010-STATUS       TO  RETURN-CODE                     
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
