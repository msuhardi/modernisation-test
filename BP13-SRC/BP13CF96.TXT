       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF96.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  22/07/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    : OUTPUT CASES WHERE AGE OF HA1-HA4 IS < 35   *            
      *                YEARS OLD AND ALLOC-SCH IS FTS              *            
      *                                                            *            
      * INPUT FILES  : BP13F730 - CASES PER REGN                   *            
      *                BP13K825 - BP13.K825.REQOCCP                *            
      * OUTPUT FILE  : P13F730A - CASES THAT COMPLY WITH THE       *            
      *                           CONDITIONS ABOVE                 *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP134996  SMR2  22072013  NEW PROGRAM                      *            
      * BP135748  SMR2  04062015  REVISE COMPUTATION OF AGE        *            
      * BP137048  SMR2  27102017  OUTPUT CASES WHERE APPLICANT OR  *            
      *                           FIANCEE/FIANCE UNDER FS SCHEME   *            
      *                           IS LESS THAN 18 YRS OLD          *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN       TO BP13F730.                            
                                                                                
           SELECT BP13K825 ASSIGN       TO BP13K825                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K825-KEY-FLD                         
                           FILE STATUS  IS WS-K825-STATUS.                      
                                                                                
           SELECT P13F730A ASSIGN       TO P13F730A.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE   IS F.                                              
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K825.                                                           
                                                                                
       FD   P13F730A                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE   IS F.                                              
       01   P13F730A-REC           PIC X(500).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K825-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ                PIC 9(08)   VALUE ZEROES.            
           05  WS-F730A-WRITE              PIC 9(08)   VALUE ZEROES.            
           05  WS-F730B-WRITE              PIC 9(08)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K825-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-FOUND                    PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-BDATE                    PIC 9(08)   VALUE ZEROES.            
           05  WS-SYSTEM-DATE              PIC 9(08)   VALUE ZEROES.            
           05  WS-DTE-REQUEST              PIC 9(08)   VALUE ZEROES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-AGE                      PIC 9(08)   VALUE ZEROES.            
           05  WS-NUM-AGE REDEFINES WS-AGE.                                     
               10  WS-YEAR-AGE             PIC 9(04).                           
               10  FILLER                  PIC 9(04).                           
           05  WS-NRIC                     PIC X(09)   VALUE SPACES.            
                                                                                
       PROCEDURE DIVISION.                                                      
      ****************************                                              
       0000-MAIN-CONTROL.                                                       
      ****************************                                              
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F730       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECORDS     THRU 3000-EXIT                      
             UNTIL WS-F730-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       1000-OPEN-FILES.                                                         
      ****************************                                              
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13K825                                                 
                OUTPUT P13F730A.                                                
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K825 FILE ERROR ' WS-K825-STATUS             
              MOVE WS-K825-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-SYSTEM-DATE.                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       2000-READ-BP13F730.                                                      
      ****************************                                              
                                                                                
           READ BP13F730 AT END                                                 
                MOVE 'Y'                    TO WS-F730-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           MOVE F730-DTE-REQUEST            TO WS-DTE-REQUEST.                  
           ADD 1                            TO WS-F730-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3000-PROCESS-RECORDS.                                                    
      ****************************                                              
                                                                                
           MOVE 'N'                         TO WS-FOUND.                        
           INITIALIZE                          WS-AGE.                          
                                                                                
           IF F730-CDE-ALLOC-SCH = 'FTS'                                        
              PERFORM 4000-CHECK-HA         THRU 4000-EXIT                      
              IF WS-FOUND = 'Y'                                                 
                 MOVE 'A'                   TO BP13F730-REC(22:1)               
                 WRITE P13F730A-REC         FROM BP13F730-REC                   
                 ADD 1                      TO WS-F730A-WRITE                   
              END-IF                                                            
           ELSE                                                                 
              IF F730-CDE-ELIG-SCH = 'FS '                                      
                 PERFORM 6000-STARTBR-K825  THRU 6000-EXIT                      
                 IF WS-FOUND = 'Y'                                              
                    MOVE 'Y'                TO BP13F730-REC(24:1)               
                    WRITE P13F730A-REC      FROM BP13F730-REC                   
                    ADD 1                   TO WS-F730B-WRITE                   
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F730       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       4000-CHECK-HA.                                                           
      ****************************                                              
                                                                                
           MOVE F730-NUM-NRIC1              TO WS-NRIC.                         
           PERFORM 5000-READ-BP13K825       THRU 5000-EXIT.                     
                                                                                
           IF WS-FOUND = 'N' AND                                                
             (F730-NUM-NRIC2(1:1) NOT = '#' AND SPACES AND LOW-VALUES)          
              MOVE F730-NUM-NRIC2           TO WS-NRIC                          
              PERFORM 5000-READ-BP13K825    THRU 5000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-FOUND = 'N' AND                                                
             (F730-NUM-NRIC3(1:1) NOT = '#' AND SPACES AND LOW-VALUES)          
              MOVE F730-NUM-NRIC3           TO WS-NRIC                          
              PERFORM 5000-READ-BP13K825    THRU 5000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-FOUND = 'N' AND                                                
             (F730-NUM-NRIC4(1:1) NOT = '#' AND SPACES AND LOW-VALUES)          
              MOVE F730-NUM-NRIC4           TO WS-NRIC                          
              PERFORM 5000-READ-BP13K825    THRU 5000-EXIT                      
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       5000-READ-BP13K825.                                                      
      ****************************                                              
                                                                                
           MOVE SPACES                      TO BP13K825-REC.                    
           INITIALIZE                          BP13K825-REC.                    
                                                                                
           MOVE F730-NUM-REGN               TO K825-NUM-REGN.                   
           MOVE WS-NRIC                     TO K825-NUM-NRIC.                   
                                                                                
           READ BP13K825.                                                       
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
              MOVE K825-DTE-BIRTH           TO WS-BDATE                         
              COMPUTE WS-AGE = WS-DTE-REQUEST - WS-BDATE                        
              IF WS-YEAR-AGE < 35                                               
                 MOVE 'Y'                   TO WS-FOUND                         
              END-IF                                                            
                                                                                
           WHEN 23                                                              
              MOVE 'N'                      TO WS-FOUND                         
              DISPLAY 'BP13K825 REC NOT FND: ' K825-KEY-FLD                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K825'                               
              MOVE WS-K825-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       6000-STARTBR-K825.                                                       
      ****************************                                              
                                                                                
           MOVE SPACES                      TO BP13K825-REC.                    
           INITIALIZE                          BP13K825-REC.                    
                                                                                
           MOVE 'N'                         TO WS-K825-EOF.                     
                                                                                
           MOVE F730-NUM-REGN               TO K825-NUM-REGN.                   
                                                                                
           START BP13K825 KEY IS >= K825-KEY-FLD.                               
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              PERFORM 6100-READNEXT-K825    THRU 6100-EXIT                      
                UNTIL K825-NUM-REGN NOT = F730-NUM-REGN                         
                   OR WS-K825-EOF = 'Y'                                         
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              DISPLAY 'BP13K825 - RECORD NOT FOUND, KEY : '                     
                      K825-NUM-REGN                                             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K825 STARTBR ERROR: ' WS-K825-STATUS                 
              MOVE WS-K825-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       6100-READNEXT-K825.                                                      
      ****************************                                              
                                                                                
           READ BP13K825 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              IF K825-NUM-REGN = F730-NUM-REGN                                  
                 IF K825-NUM-RELATIONSHIP = '00' OR '09'                        
                    MOVE K825-DTE-BIRTH     TO WS-BDATE                         
                    COMPUTE WS-AGE = WS-DTE-REQUEST - WS-BDATE                  
                    IF WS-YEAR-AGE < 18                                         
                       MOVE 'Y'             TO WS-FOUND                         
                                               WS-K825-EOF                      
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'Y'                   TO WS-K825-EOF                      
              END-IF                                                            
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                      TO WS-K825-EOF                      
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K825 READNEXT ERROR: ' WS-K825-STATUS                
              MOVE WS-K825-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       9000-CLOSE-FILES.                                                        
      ****************************                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-SYSTEM-DATE.                                
           DISPLAY '*================ BP13CF96 ================*'.              
           DISPLAY '  NO OF F730 RECS READ        : ' WS-F730-READ.             
           DISPLAY '  NO OF HA1-HA4 IS < 35 Y/O   : ' WS-F730A-WRITE.           
           DISPLAY '  NO OF FS SCHEME IS < 18 Y/O : ' WS-F730B-WRITE.           
           DISPLAY '*==========================================*'.              
                                                                                
           CLOSE BP13F730                                                       
                 P13F730A                                                       
                 BP13K825.                                                      
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K825 FILE ERROR ' WS-K825-STATUS             
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
