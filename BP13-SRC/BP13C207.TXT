       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C207.                                                 
      *AUTHOR.        MJ16.                                                     
      *DATE-WRITTEN.  26/12/03                                                  
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE   :  PROGRAM TO UPDATE BB14K580 & BB14K500       *            
      *                BY LEASE-COMM-DTE AND DTE-DELV-POS          *            
      *                                                            *            
      *  INPUT FILES :  1)  BP13F205                               *            
      *                 2)  BM06F800                               *            
      *                 3)  BM06K100                               *            
      *                                                            *            
      *  I-O    FILE :  1)  BB14K580                               *            
      *                 2)  BB14K500                               *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHGE NO  OIC   DATE     DESCRIPTION                        *            
      * -------- ----  ------   -----------                        *            
      * BP132452 MJ16  261203   NEW PROGRAM.                       *            
      * BP132500 MTD   050504   MOVE F800-NUM-LEASE-PERIOD-FLAT TO *            
      *                         K500-NUM-LEASE-PERIOD-FLAT         *            
      * BP134731 PCL3  121112   TO CATER FOR MODL-DESIGN 89        *            
      * BP135838 ESA1  010715   TO CATER FOR MODL-DESIGN 92        *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205  ASSIGN        TO  BP13F205.                         
                                                                                
           SELECT BM06F800  ASSIGN        TO  BM06F800.                         
                                                                                
           SELECT BM06K100  ASSIGN        TO  BM06K100                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K100-KEY-FLD                      
                            FILE STATUS   IS  WS-K100-STATUS.                   
                                                                                
           SELECT BB14K580  ASSIGN        TO  BB14K580                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K580-KEY-FLD                      
                            FILE STATUS   IS  WS-K580-STATUS.                   
                                                                                
           SELECT BB14K500  ASSIGN        TO  BB14K500                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K500-KEY-FLD                      
                            FILE STATUS   IS  WS-K500-STATUS.                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F205                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 80  CHARACTERS.                                     
       COPY BP13F205.                                                           
                                                                                
       FD   BM06F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 800 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BM06F800.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BB14K580                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BB14K580.                                                           
                                                                                
       FD   BB14K500                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BB14K500.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-EOF-F205                PIC X       VALUE 'N'.                
           05  WS-EOF-F800                PIC X       VALUE 'N'.                
           05  WS-K100-FND                PIC X       VALUE 'N'.                
           05  WS-K100-STATUS             PIC 9(2)    VALUE  00.                
           05  WS-K580-STATUS             PIC 9(2)    VALUE  00.                
           05  WS-K500-STATUS             PIC 9(2)    VALUE  00.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-TOTAL-CNT          PIC 9(5)    VALUE ZEROS.              
           05  WS-UPD-K580-CNT            PIC 9(5)    VALUE ZEROS.              
           05  WS-UPD-K500-CNT            PIC 9(5)    VALUE ZEROS.              
                                                                                
       01 OTHER-VARIABLES.                                                      
           05 WS-F800-AIX1                PIC X(16)   VALUE SPACES.             
           05 WS-F800-FT                  PIC X(2)    VALUE SPACES.             
           05 WS-F800-NUM-FLAT-TYPE       PIC X(2)    VALUE SPACES.             
           05 WS-PREV-CDE-BLK             PIC X(5)    VALUE SPACES.             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM  100-OPEN-FILES        THRU   100-EXIT.                      
           PERFORM  200-READ-BP13F205     THRU   200-EXIT                       
                        UNTIL WS-EOF-F205 = 'Y'.                                
           PERFORM  9999-CLOSE-ROUTINE     THRU   9999-EXIT.                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F205                                                 
                       BM06K100                                                 
                I-O    BB14K580                                                 
                       BB14K500.                                                
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K100 - OPEN ERROR :' WS-K100-STATUS                  
              MOVE     WS-K100-STATUS   TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE THRU   9999-EXIT.                     
                                                                                
           IF WS-K580-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BB14K580 - OPEN ERROR :' WS-K580-STATUS                  
              MOVE     WS-K580-STATUS   TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE THRU   9999-EXIT.                     
                                                                                
           IF WS-K580-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BB14K580 - OPEN ERROR :' WS-K580-STATUS                  
              MOVE     WS-K580-STATUS   TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE THRU   9999-EXIT.                     
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-BP13F205.                                                       
      *-------------------------------------------------------------            
           READ BP13F205                                                        
                AT END MOVE 'Y'         TO     WS-EOF-F205                      
                       GO TO 200-EXIT.                                          
                                                                                
           ADD     1                    TO     WS-READ-TOTAL-CNT.               
                                                                                
              PERFORM 300-PROCESS-RECORD THRU  300-EXIT.                        
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-RECORD.                                                      
      *-------------------------------------------------------------            
           OPEN    INPUT BM06F800.                                              
           PERFORM 320-READ-BM06F800      THRU   320-EXIT.                      
           MOVE        'N'                TO     WS-EOF-F800.                   
           PERFORM 400-CHECK-NT-ZONE-ALLOCDTE THRU 400-EXIT                     
                       UNTIL WS-EOF-F800  = 'Y'.                                
                                                                                
           CLOSE   BM06F800.                                                    
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       320-READ-BM06F800.                                                       
      *-------------------------------------------------------------            
           READ BM06F800                                                        
                AT END MOVE 'Y'  TO  WS-EOF-F800                                
                       GO TO 320-EXIT.                                          
                                                                                
           MOVE F800-AIX1 TO WS-F800-AIX1.                                      
                                                                                
           PERFORM 350-DECODE-FLAT-TYPE                                         
              THRU 350-EXIT.                                                    
                                                                                
           MOVE WS-F800-NUM-FLAT-TYPE TO WS-F800-FT.                            
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
       350-DECODE-FLAT-TYPE.                                                    
      *-------------------------------------------------------------            
           EVALUATE F800-CDE-TYP-ACTUSE                                         
           WHEN '01'                                                            
                 IF F800-CDE-MODL-DESG  = '80' OR '89' OR '92'                  
                    MOVE '1A'       TO WS-F800-NUM-FLAT-TYPE                    
                 ELSE                                                           
                    MOVE '1 '       TO WS-F800-NUM-FLAT-TYPE                    
                 END-IF                                                         
           WHEN '02'                                                            
                 IF F800-CDE-MODL-DESG  = '80' OR '89' OR '92'                  
                    MOVE '2A'       TO WS-F800-NUM-FLAT-TYPE                    
                 ELSE                                                           
                    MOVE '2 '       TO WS-F800-NUM-FLAT-TYPE                    
                 END-IF                                                         
           WHEN '03'                                                            
                 MOVE '3 '       TO WS-F800-NUM-FLAT-TYPE                       
           WHEN '04'                                                            
                 MOVE '4 '       TO WS-F800-NUM-FLAT-TYPE                       
           WHEN '05'                                                            
                 MOVE '5 '       TO WS-F800-NUM-FLAT-TYPE                       
           WHEN '06'                                                            
                 MOVE 'E '       TO WS-F800-NUM-FLAT-TYPE                       
           WHEN OTHER                                                           
                 MOVE '  '       TO WS-F800-NUM-FLAT-TYPE                       
           END-EVALUATE.                                                        
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-CHECK-NT-ZONE-ALLOCDTE.                                              
      *-------------------------------------------------------------            
                                                                                
           IF ((F205-NUM-NT-ZONE        = F800-CDE-NT)           OR             
               (F205-NUM-NT-ZONE (1:2)  = F800-CDE-ZONE))        AND            
               (F205-DTE-ALLOCN         = F800-DTE-ALLOCN)       AND            
              ((F205-NUM-FLAT-TYPE      = WS-F800-NUM-FLAT-TYPE) OR             
              ((F205-NUM-FLAT-TYPE      = 'SE')                  AND            
               (WS-F800-NUM-FLAT-TYPE   = '3 ' OR '4 ' OR '5 ')))               
              PERFORM 420-UPD-K580-K500    THRU   420-EXIT                      
           ELSE                                                                 
              IF (F205-NUM-NT-ZONE   = F800-NUM-BTO-OPTION)   AND               
                 (F205-DTE-ALLOCN    = F800-DTE-ALLOCN)                         
                 PERFORM 420-UPD-K580-K500    THRU   420-EXIT                   
              ELSE                                                              
                 IF (F205-NUM-NT-ZONE   = F800-CDE-ZONE)   AND                  
                    (F205-DTE-ALLOCN    = F800-DTE-ALLOCN) AND                  
                    (F205-NUM-FLAT-TYPE = '00')                                 
                    PERFORM 420-UPD-K580-K500    THRU   420-EXIT                
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM    320-READ-BM06F800    THRU   320-EXIT.                     
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       420-UPD-K580-K500.                                                       
      *-------------------------------------------------------------            
            MOVE SPACES TO K580-KEY-FLD.                                        
            MOVE F800-SCH-ACC-NO(1:9)  TO  K580-NUM-SCH-ACCNT                   
            READ BB14K580                                                       
                                                                                
            EVALUATE WS-K580-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                MOVE F800-DTE-DLVRY-POSSESSION TO                               
                                            K580-DTE-UNIT-DELIVERY              
                REWRITE BB14K580-REC                                            
                IF WS-K580-STATUS = 00                                          
                  ADD  1 TO WS-UPD-K580-CNT                                     
                ELSE                                                            
                  DISPLAY ' ERROR IN REWRITE BB14K580:'                         
                                                   WS-K580-STATUS               
                END-IF                                                          
              WHEN 23                                                           
                DISPLAY 'RECORD NOT FOUND IN K580:' F800-SCH-ACC-NO(1:9)        
              WHEN OTHER                                                        
                DISPLAY 'ERROR READING BB14K580 FILE:' WS-K580-STATUS           
                PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                      
            END-EVALUATE.                                                       
                                                                                
            PERFORM 421-READ-BM06K100  THRU   421-EXIT.                         
                                                                                
            IF WS-K100-FND = 'Y' THEN                                           
               IF K100-CDE-BLK = WS-PREV-CDE-BLK                                
                  MOVE K100-CDE-BLK TO WS-PREV-CDE-BLK                          
                  GO TO 420-EXIT                                                
               ELSE                                                             
                  CONTINUE                                                      
               END-IF                                                           
               MOVE SPACES       TO K500-KEY-FLD                                
               MOVE K100-CDE-BLK TO K500-KEY-FLD                                
                                    WS-PREV-CDE-BLK                             
               READ BB14K500                                                    
                                                                                
               EVALUATE WS-K500-STATUS                                          
                 WHEN 00                                                        
                 WHEN 02                                                        
                  IF K500-DTE-LEASE-COMM-FLAT NOT = SPACES AND                  
                                       LOW-VALUES AND '00000000'                
                   CONTINUE                                                     
                  ELSE                                                          
                   MOVE F800-DTE-LEASE-COMM TO K500-DTE-LEASE-COMM-FLAT         
                   MOVE F800-NUM-LEASE-PERIOD-FLAT TO                           
                        K500-NUM-LEASE-PERIOD-FLAT                              
                   REWRITE BB14K500-REC                                         
                   IF WS-K500-STATUS = 00                                       
                       ADD 1 TO WS-UPD-K500-CNT                                 
                   ELSE                                                         
                       DISPLAY ' ERROR IN REWRITE BB14K500:'                    
                                         WS-K500-STATUS                         
                   END-IF                                                       
                  END-IF                                                        
                 WHEN 23                                                        
                  DISPLAY 'RECORD NOT FOUND IN K500:' K100-CDE-BLK              
                 WHEN OTHER                                                     
                  DISPLAY 'ERROR READING BB14K500 FILE:' WS-K500-STATUS         
                  PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                    
               END-EVALUATE                                                     
            ELSE                                                                
               CONTINUE                                                         
            END-IF.                                                             
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       421-READ-BM06K100.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES TO K100-KEY-FLD.                                         
           MOVE 'N'    TO WS-K100-FND.                                          
           MOVE F800-AIX1 TO K100-KEY-FLD.                                      
                                                                                
           READ BM06K100.                                                       
                                                                                
           EVALUATE WS-K100-STATUS                                              
            WHEN 00                                                             
                 MOVE 'Y' TO  WS-K100-FND                                       
            WHEN 23                                                             
                 MOVE 'N' TO  WS-K100-FND                                       
            WHEN OTHER                                                          
                 DISPLAY 'ERROR IN READING BM06K100' WS-K100-STATUS             
                 PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       421-EXIT.                                                                
           EXIT.                                                                
                                                                                
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY '*----------- BP13C207 SUMMARY --------------*'.             
           DISPLAY '  '.                                                        
           DISPLAY 'NO OF BP13F205 READ        = ' WS-READ-TOTAL-CNT.           
           DISPLAY 'NO OF BB14K580 UPDATED     = ' WS-UPD-K580-CNT.             
           DISPLAY 'NO OF BB14K500 UPDATED     = ' WS-UPD-K500-CNT.             
                                                                                
                                                                                
           CLOSE BP13F205                                                       
                 BM06K100                                                       
                 BB14K580                                                       
                 BB14K500.                                                      
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
             DISPLAY 'BM06K100 CLOSE ERROR: ' WS-K100-STATUS                    
             MOVE  WS-K100-STATUS      TO       RETURN-CODE.                    
                                                                                
           IF WS-K580-STATUS NOT = 00 AND 97                                    
             DISPLAY 'BB14K580 CLOSE ERROR: ' WS-K580-STATUS                    
             MOVE  WS-K580-STATUS      TO       RETURN-CODE.                    
                                                                                
           IF WS-K500-STATUS NOT = 00 AND 97                                    
             DISPLAY 'BB14K500 CLOSE ERROR: ' WS-K500-STATUS                    
             MOVE  WS-K580-STATUS      TO       RETURN-CODE.                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
