       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY95.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *                                                                         
      ************************************************************              
      *                   SOC SYSTEM (SOC)                       *              
      *==========================================================*              
      * DATE WRITTEN      : 08 AUG 2011                          *              
      *                                                          *              
      * 1. OBJECTIVE      : MATCH BP13F730 WITH BP13F595 BY NRIC.*              
      *                                                          *              
      * 2. INPUT          : BP13F595                             *              
      *                   : BP13F730                             *              
      *                                                          *              
      * 3. OUTPUT         : P13F595F                             *              
      *                   : P13F595N                             *              
      *==========================================================*              
      *  MODIFICATIONS:                                          *              
      *                                                          *              
      *  CHGE-NO  DATE       OIC  DESCRIPTION                    *              
      *  -------- ---------- ---- ------------------------------ *              
      *  BP133434 29/10/2008 JB8  NEW PROGRAM                    *              
      ************************************************************              
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F595     ASSIGN        TO BP13F595.                       
           SELECT BP13F730     ASSIGN        TO BP13F730.                       
                                                                                
           SELECT P13F595F     ASSIGN        TO P13F595F.                       
           SELECT P13F595N     ASSIGN        TO P13F595N.                       
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F595                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       COPY BP13F595.                                                           
                                                                                
       FD  BP13F730                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       COPY BP13F730.                                                           
                                                                                
       FD  P13F595F                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595F-REC.                                                        
           05  FILLER                    PIC X(500).                            
                                                                                
       FD  P13F595N                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595N-REC        PIC X(500).                                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  END-OF-FILE-FLAGS.                                                   
           05  WS-F595-EOF               PIC X     VALUE 'N'.                   
           05  WS-F730-EOF               PIC X     VALUE 'N'.                   
                                                                                
       01  COUNTERS.                                                            
           05  WS-F595-READ-CNT          PIC 9(6)  VALUE ZEROES.                
           05  WS-F730-READ-CNT          PIC 9(6)  VALUE ZEROES.                
           05  WS-F595F-WRITE            PIC 9(6)  VALUE ZEROES.                
           05  WS-F595N-WRITE            PIC 9(6)  VALUE ZEROES.                
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURRENT-DATE1          PIC X(8) VALUE SPACES.                 
           05  WS-CURRENT-TIME           PIC X(9) VALUE SPACES.                 
               05  WS-TIME REDEFINES WS-CURRENT-TIME.                           
                   10  WS-HHMMSSMS       PIC X(8).                              
                   10  WS-GM             PIC X(1).                              
           05  FILLER                    PIC X(4) VALUE SPACES.                 
           05  WS-RUN-DATE               PIC X(8) VALUE SPACES.                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F730-FND               PIC X(01) VALUE 'N'.                   
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
                                                                                
           PERFORM 1000-OPEN-ROUTINE THRU 1000-EXIT.                            
           PERFORM 1100-READ-BP13F595 THRU 1100-EXIT.                           
           PERFORM 1200-READ-BP13F730 THRU 1200-EXIT.                           
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-F595-EOF = 'Y' AND                                        
                   WS-F730-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       1000-OPEN-ROUTINE.                                                       
      *************************************************************             
           OPEN INPUT  BP13F595                                                 
                       BP13F730                                                 
                OUTPUT P13F595F                                                 
                       P13F595N.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CURRENT-DATE.                    
                                                                                
           STRING WS-CURRENT-DATE1(7:2) '/'                                     
                  WS-CURRENT-DATE1(5:2) '/'                                     
                  WS-CURRENT-DATE1(1:4)                                         
                  DELIMITED BY SIZE                                             
             INTO WS-RUN-DATE.                                                  
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       1100-READ-BP13F595.                                                      
      *************************************************************             
                                                                                
           READ BP13F595                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F595-EOF                         
                MOVE HIGH-VALUES         TO F595-NUM-NRIC1                      
            NOT AT END                                                          
                ADD 1                    TO WS-F595-READ-CNT                    
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       1200-READ-BP13F730.                                                      
      *************************************************************             
                                                                                
           READ BP13F730                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F730-EOF                         
                MOVE HIGH-VALUES         TO F730-NUM-NRIC1                      
           END-READ.                                                            
                ADD 1                    TO WS-F730-READ-CNT.                   
                                                                                
       1200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       2000-PROCESS-RECORDS.                                                    
      *************************************************************             
                                                                                
           IF F595-NUM-NRIC1 = F730-NUM-NRIC1                                   
              PERFORM 3000-PROCESS-CASES     THRU 3000-EXIT                     
                UNTIL F595-NUM-NRIC1 > F730-NUM-NRIC1                           
                   OR F595-NUM-NRIC1 = HIGH-VALUES                              
              PERFORM 1200-READ-BP13F730  THRU 1200-EXIT                        
           ELSE                                                                 
              IF F595-NUM-NRIC1 < F730-NUM-NRIC1                                
                 PERFORM 5000-WRITE-NOTFND   THRU 5000-EXIT                     
                 PERFORM 1100-READ-BP13F595  THRU 1100-EXIT                     
              ELSE                                                              
                 PERFORM 1200-READ-BP13F730  THRU 1200-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------                  
       3000-PROCESS-CASES.                                                      
      *-------------------------------------------------------                  
                                                                                
           WRITE P13F595F-REC FROM BP13F595-REC.                                
           ADD 1                            TO WS-F595F-WRITE.                  
                                                                                
           PERFORM 1100-READ-BP13F595     THRU 1100-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------               
       5000-WRITE-NOTFND.                                                       
      *----------------------------------------------------------               
                                                                                
           WRITE P13F595F-REC FROM BP13F595-REC.                                
           ADD 1                               TO WS-F595N-WRITE.               
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       9000-CLOSE-ROUTINE.                                                      
      *************************************************************             
           DISPLAY '   '.                                                       
           DISPLAY 'RUN DATE : ' WS-RUN-DATE.                                   
           DISPLAY '*--------- BP13CY95 CONTROL TOTAL ---------*'.              
           DISPLAY '   '.                                                       
           DISPLAY 'NO. OF READS FROM F595   : ' WS-F595-READ-CNT.              
           DISPLAY 'NO. OF READS FROM F730   : ' WS-F730-READ-CNT.              
           DISPLAY 'NO. OF WRITES TO F595F   : ' WS-F595F-WRITE.                
           DISPLAY 'NO. OF WRITES TO F595N   : ' WS-F595N-WRITE.                
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F595                                                       
                 BP13F730                                                       
                 P13F595F                                                       
                 P13F595N.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
