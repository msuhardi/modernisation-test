      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C23E.                                                 
       AUTHOR.        LMS.                                                      
       DATE-WRITTEN.  15/05/07.                                                 
                                                                                
      * ====================================================== *                
      *    ONE-TIME PROGRAM.                                   *                
      *--------------------------------------------------------*                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO PASS THE CASES THAT REQUIRE        *                
      *                  CPF STATEMENT.                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                         *                
      * -------  ---  ------   -----------------------------   *                
      * ====================================================== *                
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
      *---------------------*                                                   
       INPUT-OUTPUT SECTION.                                                    
      *---------------------*                                                   
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS K800-STATUS.                               
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS K820-STATUS.                               
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K110-KEY-FLD                               
                  ALTERNATE RECORD KEY IS K110-REGN-NO                          
                  FILE STATUS     IS K110-STATUS.                               
                                                                                
           SELECT AB02F870 ASSIGN TO AB02F870.                                  
                                                                                
                                                                                
      *-------------*                                                           
       DATA DIVISION.                                                           
      *-------------*                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F800                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F800.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  AB02F870                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY AB02F870.                                                           
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
       01  WS-FILE-STATUS.                                                      
           05 K800-STATUS                 PIC 99    VALUE ZEROS.                
           05 K820-STATUS                 PIC 99    VALUE ZEROS.                
           05 K110-STATUS                 PIC 99    VALUE ZEROS.                
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F800-READ                PIC 9(5)  VALUE 0.                    
           05 WS-F870-WRITE               PIC 9(5)  VALUE 0.                    
                                                                                
       01  WS-EOF-F800                    PIC 9     VALUE 0.                    
           88 EOF-F800                              VALUE 1.                    
                                                                                
       01  WS-CONTROL-DATE.                                                     
           05 WS-CTRL-CC                  PIC XX.                               
           05 WS-CTRL-YY                  PIC XX.                               
           05 WS-CTRL-MM                  PIC XX.                               
           05 WS-CTRL-DD                  PIC XX.                               
                                                                                
       01  WS-DATE.                                                             
           05 WS-CCYY.                                                          
              10 WS-CC                    PIC 99.                               
              10 WS-YY                    PIC 99.                               
           05 WS-CCYY1 REDEFINES WS-CCYY  PIC 9(4).                             
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
       01  WS-DATE9 REDEFINES WS-DATE     PIC 9(8).                             
                                                                                
       01  WS-INTEGER-CUR-DATE2           PIC S9(9) VALUE ZEROES.               
       01  WS-INTEGER-CUR-DATE4           PIC S9(9) VALUE ZEROES.               
       01  WS-DATE2-N                     PIC  9(8) VALUE ZEROES.               
       01  WS-DATE2 REDEFINES WS-DATE2-N  PIC  X(8).                            
       01  WS-DATE4-N                     PIC  9(8) VALUE ZEROES.               
       01  WS-DATE4 REDEFINES WS-DATE4-N  PIC  X(8).                            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *------------------*                                                      
       000-MAIN-ROUTINE.                                                        
      *------------------*                                                      
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 150-READ-SY02F001  THRU 150-EXIT.                            
           PERFORM 220-READ-BP13F800  THRU 220-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL EOF-F800.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       100-INITIALIZATION.                                                      
      *------------------*                                                      
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BM06K110                                                 
                       SY02F001                                                 
                OUTPUT AB02F870.                                                
                                                                                
           MOVE ZEROS  TO WS-COUNTERS.                                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       150-READ-SY02F001.                                                       
      *-------------------------------------------------------------            
           READ SY02F001                                                        
                AT END                                                          
                   GO TO 150-EXIT.                                              
                                                                                
           MOVE F001-DTE-CURRENT TO WS-DATE.                                    
                                                                                
           COMPUTE WS-INTEGER-CUR-DATE2 =                                       
              FUNCTION INTEGER-OF-DATE(WS-DATE9) + 2.                           
           COMPUTE WS-INTEGER-CUR-DATE4 =                                       
              FUNCTION INTEGER-OF-DATE(WS-DATE9) + 4.                           
                                                                                
           COMPUTE WS-DATE2-N =                                                 
              FUNCTION DATE-OF-INTEGER(WS-INTEGER-CUR-DATE2).                   
           COMPUTE WS-DATE4-N =                                                 
              FUNCTION DATE-OF-INTEGER(WS-INTEGER-CUR-DATE4).                   
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       220-READ-BP13F800.                                                       
      *------------------*                                                      
           READ BP13F800 AT END                                                 
                MOVE 1   TO WS-EOF-F800                                         
                GO TO 220-EXIT.                                                 
                                                                                
           ADD  1  TO WS-F800-READ.                                             
                                                                                
       220-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       300-PROCESS-RTN.                                                         
      *------------------*                                                      
           PERFORM 400-WRITE-OUTPUT  THRU 400-EXIT.                             
           PERFORM 220-READ-BP13F800 THRU 220-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       400-WRITE-OUTPUT.                                                        
      *------------------*                                                      
           MOVE SPACES                  TO AB02F870-REC.                        
           MOVE F800-NUM-REGN           TO K800-NUM-REGN.                       
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS = 00                                                  
              CONTINUE                                                          
           ELSE                                                                 
              IF K800-STATUS = 23                                               
                 DISPLAY 'RECORD NOT FOUND IN BP13K800, ' K800-NUM-REGN         
                 GO TO 400-EXIT                                                 
              ELSE                                                              
                 MOVE K800-STATUS       TO RETURN-CODE                          
                 DISPLAY 'ERROR READING BP13K800, ' K800-NUM-REGN               
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
           IF K800-DTE-BK-APPT >= WS-DATE2 AND                                  
              K800-DTE-BK-APPT <= WS-DATE4                                      
              CONTINUE                                                          
           ELSE                                                                 
              GO TO 400-EXIT                                                    
           END-IF.                                                              
                                                                                
           MOVE 'HPI'                   TO F870-NUM-SYSTEM.                     
           MOVE K800-NUM-REGN           TO F870-NUM-REGN                        
                                           F870-NUM-HDB-REF.                    
                                                                                
           MOVE 'B1'                    TO F870-NUM-TRANS.                      
           MOVE ZEROS                   TO F870-AMT-LOAN-GRANT.                 
           MOVE ZEROS                   TO F870-DTE-LOAN-COMM.                  
                                                                                
           MOVE K800-NUM-REGN           TO K820-NUM-REGN.                       
           MOVE K800-NUM-NRIC1          TO K820-NUM-NRIC.                       
           PERFORM 410-READ-BP13K820    THRU 410-EXIT.                          
           MOVE K820-NUM-NRIC           TO F870-NUM-UIN-LSE1.                   
           MOVE K820-NUM-CPF            TO F870-NUM-CPF1.                       
                                                                                
           IF K800-NUM-NRIC2          = LOW-VALUES OR SPACES                    
              CONTINUE                                                          
           ELSE                                                                 
              MOVE K800-NUM-REGN        TO K820-NUM-REGN                        
              MOVE K800-NUM-NRIC2       TO K820-NUM-NRIC                        
              PERFORM 410-READ-BP13K820 THRU 410-EXIT                           
              MOVE K820-NUM-NRIC        TO F870-NUM-UIN-LSE2                    
              MOVE K820-NUM-CPF         TO F870-NUM-CPF2.                       
                                                                                
           IF K800-NUM-NRIC3          = LOW-VALUES OR SPACES                    
              CONTINUE                                                          
           ELSE                                                                 
              MOVE K800-NUM-REGN        TO K820-NUM-REGN                        
              MOVE K800-NUM-NRIC3       TO K820-NUM-NRIC                        
              PERFORM 410-READ-BP13K820 THRU 410-EXIT                           
              MOVE K820-NUM-NRIC        TO F870-NUM-UIN-LSE3                    
              MOVE K820-NUM-CPF         TO F870-NUM-CPF3.                       
                                                                                
           IF K800-NUM-NRIC4          = LOW-VALUES OR SPACES                    
              CONTINUE                                                          
           ELSE                                                                 
              MOVE K800-NUM-REGN        TO K820-NUM-REGN                        
              MOVE K800-NUM-NRIC4       TO K820-NUM-NRIC                        
              PERFORM 410-READ-BP13K820 THRU 410-EXIT                           
              MOVE K820-NUM-NRIC        TO F870-NUM-UIN-LSE4                    
              MOVE K820-NUM-CPF         TO F870-NUM-CPF4.                       
                                                                                
           MOVE 'SS'                    TO F870-NUM-OFFICE                      
                                           F870-NUM-ORIGINATOR.                 
           MOVE 'P13C23E'               TO F870-NUM-USERID.                     
                                                                                
           IF K800-NUM-LOAN-TAG = 'P' OR 'H'                                    
              MOVE 'P' TO F870-NUM-LOAN-SCHEME                                  
           ELSE                                                                 
              IF K800-NUM-LOAN-TAG = 'B' OR 'N'                                 
                 MOVE 'M' TO F870-NUM-LOAN-SCHEME                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           WRITE AB02F870-REC.                                                  
           ADD 1     TO  WS-F870-WRITE.                                         
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       410-READ-BP13K820.                                                       
      *------------------*                                                      
           READ BP13K820.                                                       
                                                                                
           IF K820-STATUS = 00                                                  
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF K820-STATUS = 23                                               
                 DISPLAY 'RECORD NOT FOUND IN BP13K820, ' K820-KEY-FLD          
                 MOVE SPACES            TO K820-NUM-NRIC                        
                                           K820-NUM-CPF                         
              ELSE                                                              
                 MOVE K820-STATUS       TO RETURN-CODE                          
                 DISPLAY 'ERROR READING BP13K820, ' K820-KEY-FLD                
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       420-READ-BM06K110.                                                       
      *------------------*                                                      
           READ BM06K110 KEY IS K110-REGN-NO.                                   
                                                                                
           IF K110-STATUS = 00 OR 02                                            
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF K110-STATUS = 23                                               
                 DISPLAY 'RECORD NOT FOUND IN BM06K110, ' K110-REGN-NO          
                 MOVE ZEROS             TO K110-CURR-SELLING-PRICE              
                                           K110-DTE-SO-EXECUTING                
              ELSE                                                              
                 MOVE K110-STATUS       TO RETURN-CODE                          
                 DISPLAY 'ERROR READING BM06K110, ' K110-REGN-NO                
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------*                                                          
       999-CLOSE-FILES.                                                         
      *--------------*                                                          
           CLOSE BP13F800                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BM06K110                                                       
                 AB02F870.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C23E ================*'.              
           DISPLAY '    NO. OF F800 RECORDS READ     = ' WS-F800-READ.          
           DISPLAY '    NO. OF F870 RECORDS WRITTEN  = ' WS-F870-WRITE.         
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
