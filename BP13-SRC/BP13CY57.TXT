       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY57.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  06/07/2009.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE :                                                    *        
      *    GET DETAIL OF LATEST APPLN AND NEW APPLNS                   *        
      *    1) LATEST APPLN : AGE , INCOME , ETHNIC                     *        
      *                      ELIG SCHEME , ALLOC SCHEME                *        
      *    2) NEW APPLNS   : REGN , MODE , NT , DTE-BALLOT             *        
      *                                                                *        
      * INPUT  FILES : BP13F555                                        *        
      *                BP13F820                                        *        
      *                BP13K800                                        *        
      *                BP13K820                                        *        
      *                BP13K893                                        *        
      *                BP13K895                                        *        
      *                BP13K813                                        *        
      * OUTPUT FILES : P13F555A                                        *        
      ******************************************************************        
      * CHG REQ   DATE      BY   DETAIL                                *        
      * ========  ========  ==== ===================================== *        
      * BP133653  06/07/09  JB8  NEW PROGRAM.                          *        
      * BP135340  19/05/14  IMC1 REPLACE BP13K816 WITH BP13K813        *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT   BP13F555      ASSIGN TO BP13F555.                           
           SELECT   BP13F820      ASSIGN TO BP13F820.                           
                                                                                
           SELECT   BP13K060      ASSIGN TO BP13K060                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS RANDOM                              
                              RECORD KEY IS K060-KEY-FLD                        
                             FILE STATUS IS WS-K060-STATUS.                     
                                                                                
           SELECT   BP13K813      ASSIGN TO BP13K813                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS DYNAMIC                             
                              RECORD KEY IS K813-KEY-FLD                        
                             FILE STATUS IS WS-K813-STATUS.                     
                                                                                
           SELECT   BP13K800      ASSIGN TO BP13K800                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS RANDOM                              
                              RECORD KEY IS K800-NUM-REGN                       
                             FILE STATUS IS WS-K800-STATUS.                     
                                                                                
           SELECT   BP13K820      ASSIGN TO BP13K820                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS RANDOM                              
                              RECORD KEY IS K820-KEY-FLD                        
                             FILE STATUS IS WS-K820-STATUS.                     
                                                                                
           SELECT   BP13K893      ASSIGN TO BP13K893                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS DYNAMIC                             
                              RECORD KEY IS K893-KEY-FLD                        
                             FILE STATUS IS WS-K893-STATUS.                     
                                                                                
           SELECT   BP13K895      ASSIGN TO BP13K895                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS DYNAMIC                             
                              RECORD KEY IS K895-KEY-FLD                        
                             FILE STATUS IS WS-K895-STATUS.                     
                                                                                
           SELECT   P13F555A      ASSIGN TO P13F555A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F555                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F555.                                                           
                                                                                
       FD   BP13F820                                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F820.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25 CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BP13K895                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K895.                                                           
                                                                                
       FD   P13F555A                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   P13F555A-REC                 PIC X(600).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  CNT-F555-READ             PIC 9(7)  VALUE ZEROES.                
           05  CNT-F820-READ             PIC 9(7)  VALUE ZEROES.                
           05  CNT-K820-READ             PIC 9(7)  VALUE ZEROES.                
           05  CNT-K800-READ             PIC 9(7)  VALUE ZEROES.                
           05  CNT-MATCHED               PIC 9(7)  VALUE ZEROES.                
           05  CNT-F555A-WRITE           PIC 9(7)  VALUE ZEROES.                
           05  CNT-LATEST                PIC 9(7)  VALUE ZEROES.                
           05  CNT-LATEST-K800-FND       PIC 9(7)  VALUE ZEROES.                
           05  CNT-LATEST-K820-FND       PIC 9(7)  VALUE ZEROES.                
           05  CNT-NEW-APPLN             PIC 9(7)  VALUE ZEROES.                
           05  CNT-NEW-APPLN-K800-FND    PIC 9(7)  VALUE ZEROES.                
           05  CNT-NA                    PIC 9(7)  VALUE ZEROES.                
           05  CNT-K800-NOTFND           PIC 9(7)  VALUE ZEROES.                
           05  CNT-K820-NOTFND           PIC 9(7)  VALUE ZEROES.                
           05  WS-K060-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K813-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K800-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K820-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K893-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K895-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K800-FND               PIC X(1)  VALUE SPACE.                 
               88  K800-FND              VALUE 'Y'.                             
           05  WS-K820-FND               PIC X(1)  VALUE SPACE.                 
               88  K820-FND              VALUE 'Y'.                             
           05  WS-K893-FND               PIC X(1)  VALUE SPACE.                 
               88  K893-FND              VALUE 'Y'.                             
           05  WS-K895-FND               PIC X(1)  VALUE SPACE.                 
               88  K895-FND              VALUE 'Y'.                             
           05  WS-F820-EOF               PIC X(1)  VALUE SPACE.                 
               88  F820-EOF              VALUE 'Y'.                             
           05  WS-F555-EOF               PIC X(1)  VALUE SPACE.                 
               88  F555-EOF              VALUE 'Y'.                             
           05  WS-K893-EOF               PIC X(1)  VALUE SPACE.                 
               88  K893-EOF              VALUE 'Y'.                             
           05  WS-K895-EOF               PIC X(1)  VALUE SPACE.                 
               88  K895-EOF              VALUE 'Y'.                             
           05  WS-NUM-AGE                PIC 9(08) VALUE ZEROES.                
           05  WS-AGE REDEFINES WS-NUM-AGE.                                     
               10  WS-AGE-CY             PIC 9(04).                             
               10  WS-AGE-MM             PIC 9(02).                             
               10  WS-AGE-DD             PIC 9(02).                             
           05  WS-NUM-ETHNIC             PIC X(10) VALUE SPACES.                
           05  WS-CDE-RACE               PIC X(01) VALUE SPACES.                
           05  WS-NUM-BIRTH              PIC 9(08) VALUE ZEROES.                
           05  WS-NUM-REGN               PIC 9(08) VALUE ZEROES.                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *==========*                                                              
       0000-MAIN.                                                               
      *==========*                                                              
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F555       THRU 2000-EXIT.                     
           PERFORM 2100-READ-BP13F820       THRU 2100-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL F555-NUM-UIN  = HIGH-VALUES                                  
               AND F820-NUM-NRIC = HIGH-VALUES.                                 
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================*                                                        
       1000-OPEN-FILES.                                                         
      *================*                                                        
                                                                                
           OPEN INPUT BP13F555                                                  
                      BP13F820                                                  
                      BP13K060                                                  
                      BP13K813                                                  
                      BP13K800                                                  
                      BP13K820                                                  
                      BP13K893                                                  
                      BP13K895                                                  
               OUTPUT P13F555A.                                                 
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K060 , STATUS = '                      
                      WS-K060-STATUS                                            
              MOVE WS-K060-STATUS          TO RETURN-CODE                       
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K813 , STATUS = '                      
                      WS-K813-STATUS                                            
              MOVE WS-K813-STATUS          TO RETURN-CODE                       
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K800 , STATUS = '                      
                      WS-K800-STATUS                                            
              MOVE WS-K800-STATUS          TO RETURN-CODE                       
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K820 , STATUS = '                      
                      WS-K820-STATUS                                            
              MOVE WS-K820-STATUS          TO RETURN-CODE                       
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K893 , STATUS = '                      
                      WS-K893-STATUS                                            
              MOVE WS-K893-STATUS          TO RETURN-CODE                       
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF WS-K895-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K895 , STATUS = '                      
                      WS-K895-STATUS                                            
              MOVE WS-K895-STATUS          TO RETURN-CODE                       
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       2000-READ-BP13F555.                                                      
      *===================*                                                     
                                                                                
           READ BP13F555                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-F555-EOF                       
                MOVE HIGH-VALUES           TO F555-NUM-UIN                      
                GO                         TO 2000-EXIT                         
           END-READ.                                                            
                                                                                
           ADD  1                          TO CNT-F555-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       2100-READ-BP13F820.                                                      
      *===================*                                                     
                                                                                
           READ BP13F820                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-F820-EOF                       
                MOVE HIGH-VALUES           TO F820-NUM-NRIC                     
                GO                         TO 2100-EXIT                         
           END-READ.                                                            
                                                                                
           ADD  1                          TO CNT-F820-READ.                    
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *====================*                                                    
       3000-PROCESS-RECORD.                                                     
      *====================*                                                    
                                                                                
           IF F555-NUM-UIN = F820-NUM-NRIC                                      
              MOVE ZEROES                       TO CNT-NA                       
              PERFORM 4000-PROCESS-NRIC       THRU 4000-EXIT                    
                UNTIL F820-NUM-NRIC NOT = F555-NUM-UIN                          
                   OR F820-NUM-NRIC = HIGH-VALUES                               
                                                                                
              WRITE P13F555A-REC              FROM BP13F555-REC                 
              ADD 1                             TO CNT-F555A-WRITE              
                                                                                
              PERFORM 2000-READ-BP13F555      THRU 2000-EXIT                    
           ELSE                                                                 
              IF F555-NUM-UIN < F820-NUM-NRIC                                   
                                                                                
                 WRITE P13F555A-REC           FROM BP13F555-REC                 
                 ADD 1                          TO CNT-F555A-WRITE              
                                                                                
                 PERFORM 2000-READ-BP13F555   THRU 2000-EXIT                    
              ELSE                                                              
                 PERFORM 2100-READ-BP13F820   THRU 2100-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=================*                                                       
       4000-PROCESS-NRIC.                                                       
      *=================*                                                       
                                                                                
           IF F820-NUM-REGN = F555-LATEST-NUM-REGN                              
              ADD 1                                  TO CNT-LATEST              
              PERFORM 5000-GET-LATEST-INFO         THRU 5000-EXIT               
           ELSE                                                                 
              IF F820-NUM-REGN NOT = F555-NUM-REGN(1) AND                       
                 F820-NUM-REGN NOT = F555-NUM-REGN(2) AND                       
                 F820-NUM-REGN NOT = F555-NUM-REGN(3)                           
                 ADD 1                               TO CNT-NEW-APPLN           
                 PERFORM 6000-GET-NEW-APPLN        THRU 6000-EXIT               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-BP13F820              THRU 2100-EXIT.              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=====================*                                                   
       5000-GET-LATEST-INFO.                                                    
      *=====================*                                                   
                                                                                
           MOVE F555-LATEST-NUM-REGN             TO K800-NUM-REGN.              
           PERFORM 7000-READ-BP13K800          THRU 7000-EXIT                   
                                                                                
           IF K800-FND                                                          
              ADD 1                         TO CNT-LATEST-K800-FND              
              MOVE K800-NUM-ELIG-SCHEME     TO F555-LATEST-NUM-ELIG-SCH         
              MOVE K800-NUM-ALLO-SCHEME     TO F555-LATEST-NUM-ALLO-SCH         
              IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES                     
                 MOVE K800-NUM-REGN         TO K820-NUM-REGN                    
                 MOVE K800-NUM-NRIC1        TO K820-NUM-NRIC                    
                 PERFORM 7100-READ-BP13K820    THRU 7100-EXIT                   
                 IF K820-FND                                                    
                    ADD 1                   TO CNT-LATEST-K820-FND              
                    MOVE K820-AMT-INCOME    TO F555-LATEST-HA1-INCOME           
                                                                                
                    PERFORM 7200-COMPUTE-AGE   THRU 7200-EXIT                   
                    MOVE WS-AGE-CY          TO F555-LATEST-HA1-AGE              
                                                                                
                    PERFORM 7300-DECODE-ETHNIC THRU 7300-EXIT                   
                    MOVE WS-NUM-ETHNIC      TO F555-LATEST-NUM-ETHNIC           
                 END-IF                                                         
              END-IF                                                            
              IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                     
                 MOVE K800-NUM-REGN         TO K820-NUM-REGN                    
                 MOVE K800-NUM-NRIC2        TO K820-NUM-NRIC                    
                 PERFORM 7100-READ-BP13K820    THRU 7100-EXIT                   
                 IF K820-FND                                                    
                    ADD 1                   TO CNT-LATEST-K820-FND              
                    MOVE K820-AMT-INCOME    TO F555-LATEST-HA2-INCOME           
                                                                                
                    PERFORM 7200-COMPUTE-AGE   THRU 7200-EXIT                   
                    MOVE WS-AGE-CY          TO F555-LATEST-HA2-AGE              
                 END-IF                                                         
              END-IF                                                            
              IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                     
                 MOVE K800-NUM-REGN         TO K820-NUM-REGN                    
                 MOVE K800-NUM-NRIC3        TO K820-NUM-NRIC                    
                 PERFORM 7100-READ-BP13K820    THRU 7100-EXIT                   
                 IF K820-FND                                                    
                    ADD 1                   TO CNT-LATEST-K820-FND              
                    MOVE K820-AMT-INCOME    TO F555-LATEST-HA3-INCOME           
                                                                                
                    PERFORM 7200-COMPUTE-AGE   THRU 7200-EXIT                   
                    MOVE WS-AGE-CY          TO F555-LATEST-HA3-AGE              
                 END-IF                                                         
              END-IF                                                            
              IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                     
                 MOVE K800-NUM-REGN         TO K820-NUM-REGN                    
                 MOVE K800-NUM-NRIC4        TO K820-NUM-NRIC                    
                 PERFORM 7100-READ-BP13K820    THRU 7100-EXIT                   
                 IF K820-FND                                                    
                    ADD 1                   TO CNT-LATEST-K820-FND              
                    MOVE K820-AMT-INCOME    TO F555-LATEST-HA4-INCOME           
                                                                                
                    PERFORM 7200-COMPUTE-AGE   THRU 7200-EXIT                   
                    MOVE WS-AGE-CY          TO F555-LATEST-HA4-AGE              
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       6000-GET-NEW-APPLN.                                                      
      *===================*                                                     
                                                                                
           MOVE F820-NUM-REGN               TO K800-NUM-REGN.                   
           PERFORM 7000-READ-BP13K800     THRU 7000-EXIT                        
                                                                                
           IF K800-FND                                                          
              ADD 1                        TO CNT-NEW-APPLN-K800-FND            
              IF K800-DTE-BALLOT > F555-NA-DTE-BALLOT OR                        
                 (K800-DTE-BALLOT = F555-NA-DTE-BALLOT AND                      
                  K800-DTE-REGN > F555-NA-DTE-APPLN)                            
                 MOVE K800-NUM-REGN        TO F555-NA-NUM-REGN                  
                 MOVE K800-NUM-ALLO-CAT    TO F555-NA-NUM-MODE                  
                 IF K800-NUM-NEW-TOWN = SPACES OR LOW-VALUES                    
                    INITIALIZE BP13K813-REC                                     
                    MOVE K800-NUM-ZONE     TO K813-NUM-ZONE                     
                    PERFORM 6100-READ-BP13K813 THRU 6100-EXIT                   
                    MOVE K813-CDE-NT       TO F555-NA-NUM-NT                    
                 ELSE                                                           
                    MOVE K800-NUM-NEW-TOWN TO F555-NA-NUM-NT                    
                 END-IF                                                         
                 MOVE K800-NUM-FLAT-TYPE   TO F555-NA-NUM-FT                    
                 MOVE K800-DTE-BALLOT      TO F555-NA-DTE-BALLOT                
                 MOVE K800-DTE-REGN        TO F555-NA-DTE-APPLN                 
                 MOVE K800-NUM-SCH-ACC     TO F555-NA-SCHACC                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       6100-READ-BP13K813.                                                      
      *===================*                                                     
                                                                                
           START BP13K813 KEY >= K813-KEY-FLD                                   
                                                                                
           IF WS-K813-STATUS = 00 OR 02                                         
              READ BP13K813 NEXT RECORD                                         
              IF K813-NUM-ZONE = K800-NUM-ZONE                                  
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE SPACES               TO K813-CDE-NT                       
              END-IF                                                            
           ELSE                                                                 
              IF WS-K813-STATUS = 23                                            
                 MOVE SPACES               TO K813-CDE-NT                       
              ELSE                                                              
                 DISPLAY 'ERROR READNG BP13K813 : ' WS-K813-STATUS              
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       7000-READ-BP13K800.                                                      
      *===================*                                                     
                                                                                
           MOVE 'N'                               TO WS-K800-FND.               
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              MOVE 'Y'                            TO WS-K800-FND                
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 PERFORM 7001-STARTBR-BP13K893    THRU 7001-EXIT                
                 IF NOT K893-EOF                                                
                    PERFORM 7002-READNXT-BP13K893 THRU 7002-EXIT                
                       UNTIL K893-FND OR K893-EOF                               
                 END-IF                                                         
                 IF K893-FND                                                    
                    MOVE BP13K893-MASTER(1:2000)  TO BP13K800-MASTER            
                    MOVE 'Y'                      TO WS-K800-FND                
                 ELSE                                                           
                    ADD 1                         TO CNT-K800-NOTFND            
                    DISPLAY 'RECORD NOT FND IN BP13K800 & BP13K893 , '          
                            'KEY IS ' K800-NUM-REGN                             
                 END-IF                                                         
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K800 , STATUS IS '                  
                         WS-K800-STATUS                                         
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       7001-STARTBR-BP13K893.                                                   
      ***********************                                                   
                                                                                
           MOVE SPACES                  TO BP13K893-MASTER.                     
           INITIALIZE                      BP13K893-MASTER.                     
                                                                                
           MOVE K800-NUM-REGN           TO K893-KEY-FLD.                        
           MOVE 'N'                     TO WS-K893-EOF                          
                                           WS-K893-FND.                         
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           IF WS-K893-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K893-STATUS = 23 OR 10                                      
                 MOVE 'Y'               TO WS-K893-EOF                          
              ELSE                                                              
                 DISPLAY 'BP13K893 - ERROR READING : ' WS-K893-STATUS           
                         ',KEY : ' K893-NUM-REGN-HIST                           
                 MOVE WS-K893-STATUS    TO RETURN-CODE                          
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       7002-READNXT-BP13K893.                                                   
      ***********************                                                   
                                                                                
           READ BP13K893 NEXT                                                   
             AT END                                                             
                MOVE 'Y'      TO WS-K893-EOF                                    
                GO            TO 7002-EXIT.                                     
                                                                                
           IF WS-K893-STATUS = 00 OR 02                                         
              IF K893-NUM-REGN-HIST = K800-NUM-REGN                             
                 MOVE 'Y'               TO WS-K893-FND                          
              ELSE                                                              
                 MOVE 'Y'               TO WS-K893-EOF                          
              END-IF                                                            
           ELSE                                                                 
              IF WS-K893-STATUS = 23 OR 10                                      
                 MOVE 'Y'               TO WS-K893-EOF                          
              ELSE                                                              
                 DISPLAY 'BP13K893 - ERROR READ NEXT : ' WS-K893-STATUS         
                         ',KEY : ' K893-NUM-REGN-HIST                           
                 MOVE WS-K893-STATUS    TO RETURN-CODE                          
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7002-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       7100-READ-BP13K820.                                                      
      **********************                                                    
                                                                                
           MOVE 'N'                        TO WS-K820-FND                       
                                              WS-K895-FND.                      
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              MOVE 'Y'                     TO WS-K820-FND                       
           ELSE                                                                 
              IF WS-K820-STATUS = 23                                            
                 PERFORM 7101-STARTBR-BP13K895    THRU 7101-EXIT                
                 IF NOT K895-EOF                                                
                    PERFORM 7102-READNXT-BP13K895 THRU 7102-EXIT                
                       UNTIL K895-FND OR K895-EOF                               
                 END-IF                                                         
                 IF K895-FND                                                    
                    MOVE BP13K895-REC(1:400)        TO BP13K820-REC             
                    MOVE 'Y'                        TO WS-K820-FND              
                 ELSE                                                           
                    ADD 1                           TO CNT-K820-NOTFND          
                    MOVE 'Y'                        TO WS-K820-FND              
                    DISPLAY 'BP13K820 - RECORD NOT FND : ' K820-KEY-FLD         
                 END-IF                                                         
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K820'                                  
                         ',STATUS = ' WS-K820-STATUS                            
                         ',KEY = ' K820-KEY-FLD                                 
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       7101-STARTBR-BP13K895.                                                   
      ***********************                                                   
                                                                                
           MOVE SPACES                  TO BP13K895-REC.                        
           INITIALIZE                      BP13K895-REC.                        
           MOVE K820-KEY-FLD            TO K895-KEY-FLD.                        
                                                                                
           START BP13K895 KEY >= K895-KEY-FLD.                                  
                                                                                
           IF WS-K895-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K895-STATUS = 23 OR 10                                      
                 MOVE 'Y'               TO WS-K895-EOF                          
              ELSE                                                              
                 DISPLAY 'BP13K895 - ERROR READING : ' WS-K895-STATUS           
                         ',KEY : ' K895-NUM-REGN-HIST                           
                 MOVE WS-K895-STATUS    TO RETURN-CODE                          
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7101-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       7102-READNXT-BP13K895.                                                   
      ***********************                                                   
                                                                                
           READ BP13K895 NEXT                                                   
             AT END                                                             
                MOVE 'Y'      TO WS-K895-EOF                                    
                GO            TO 7102-EXIT.                                     
                                                                                
           IF WS-K895-STATUS = 00 OR 02                                         
              IF (K895-NUM-REGN-HIST = K820-NUM-REGN) AND                       
                 (K895-NUM-NRIC-HIST = K820-NUM-NRIC)                           
                 MOVE 'Y'               TO WS-K895-FND                          
              ELSE                                                              
                 MOVE 'Y'               TO WS-K895-EOF                          
                 GO TO 7102-EXIT                                                
              END-IF                                                            
           ELSE                                                                 
              IF WS-K895-STATUS = 23 OR 10                                      
                 MOVE 'Y'               TO WS-K895-EOF                          
              ELSE                                                              
                 DISPLAY 'BP13K895 - ERROR READ NEXT : ' WS-K895-STATUS         
                         ',KEY : ' K895-NUM-REGN-HIST                           
                 MOVE WS-K895-STATUS    TO RETURN-CODE                          
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7102-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=================*                                                       
       7200-COMPUTE-AGE.                                                        
      *=================*                                                       
                                                                                
           IF K820-DTE-BIRTH NOT NUMERIC                                        
              MOVE ZEROES               TO K820-DTE-BIRTH                       
           END-IF.                                                              
           IF K800-DTE-REGN NOT NUMERIC                                         
              MOVE ZEROES               TO K800-DTE-REGN                        
           END-IF.                                                              
                                                                                
           MOVE K820-DTE-BIRTH          TO WS-NUM-BIRTH                         
           MOVE K800-DTE-REGN           TO WS-NUM-REGN                          
                                                                                
           IF WS-NUM-REGN > WS-NUM-BIRTH                                        
              COMPUTE WS-NUM-AGE = WS-NUM-REGN - WS-NUM-BIRTH                   
           ELSE                                                                 
              COMPUTE WS-NUM-AGE = 0                                            
           END-IF.                                                              
                                                                                
           IF WS-AGE-MM > 0                                                     
              COMPUTE WS-AGE-CY = WS-AGE-CY + 1                                 
           END-IF.                                                              
                                                                                
       7200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       7300-DECODE-ETHNIC.                                                      
      ********************                                                      
                                                                                
           IF K820-NUM-ETHNIC = 0 OR 1 OR 2 OR 3 OR 4                           
              MOVE 2                        TO WS-CDE-RACE                      
           ELSE IF K820-NUM-ETHNIC = 5                                          
              MOVE 1                        TO WS-CDE-RACE                      
           ELSE IF K820-NUM-ETHNIC = 6                                          
              MOVE 3                        TO WS-CDE-RACE                      
           ELSE IF K820-NUM-ETHNIC = 7 OR 9 OR ' '                              
              MOVE 4                        TO WS-CDE-RACE                      
           END-IF.                                                              
                                                                                
           MOVE 09                          TO K060-SERIAL-NO                   
           MOVE WS-CDE-RACE                 TO K060-CODE                        
           PERFORM 7400-READ-BP13K060     THRU 7400-EXIT                        
           MOVE K060-DESC(1:10)             TO WS-NUM-ETHNIC.                   
                                                                                
       7300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       7400-READ-BP13K060.                                                      
      *===================*                                                     
                                                                                
           READ BP13K060.                                                       
                                                                                
           IF WS-K060-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K060-STATUS = 23                                            
                 MOVE SPACES                   TO K060-DESC                     
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K060 , STATUS IS '                  
                         WS-K060-STATUS                                         
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=================*                                                       
       9000-CLOSE-FILES.                                                        
      *=================*                                                       
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CY57.'                                
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BP13F555 READ     : ' CNT-F555-READ.                 
           DISPLAY 'NO. OF LATEST CASES      : ' CNT-LATEST.                    
           DISPLAY 'NO. OF LATEST CASES(K800): ' CNT-LATEST-K800-FND.           
           DISPLAY 'NO. OF LATEST CASES(K820): ' CNT-LATEST-K820-FND.           
           DISPLAY 'NO. OF NEW APPLN         : ' CNT-NEW-APPLN.                 
           DISPLAY 'NO. OF NEW APPLN(K800)   : ' CNT-NEW-APPLN-K800-FND.        
           DISPLAY 'NO. OF P13F555A WRITTEN  : ' CNT-F555A-WRITE.               
                                                                                
           CLOSE BP13F555                                                       
                 BP13K060                                                       
                 BP13K813                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K893                                                       
                 BP13K895                                                       
                 P13F555A.                                                      
                                                                                
           IF WS-K060-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13K060 , STATUS = '                      
                      WS-K060-STATUS                                            
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13K813 , STATUS = '                      
                      WS-K813-STATUS                                            
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13K800 , STATUS = '                      
                      WS-K800-STATUS                                            
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13K820 , STATUS = '                      
                      WS-K820-STATUS                                            
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13K893 , STATUS = '                      
                      WS-K893-STATUS                                            
           END-IF.                                                              
                                                                                
           IF WS-K895-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13K895 , STATUS = '                      
                      WS-K895-STATUS                                            
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
