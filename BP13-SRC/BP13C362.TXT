       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C362.                                                 
       AUTHOR.        NOR.                                                      
       DATE-WRITTEN.  09/02/1999.                                               
      *****************************************************************         
      *                 SYSTEM OF COMMITMENT (BP13)                   *         
      *****************************************************************         
      * OBJECTIVE : OUTPUT NRIC 1 - 4 FROM SOC MASTER BP13K800        *         
      *           : TO USE NRIC TO MATCH PPOS (BR10K010). IF MATCHED, *         
      *             IF THE PPTY CODE NOT EQUAL TO COMMERCIAL MARKET   *         
      *             OR STALL, UPDATE 'N' TO SOC MASTER IN             *         
      *             SCCC-ELIGIBITY-TAG                                *         
      *                                                               *         
      * SCCC-ELGBTY = Y : ELIGIBLE                                    *         
      *             = N : NOT ELIGIBLE                                *         
      *             = Z : NOT ELIGIBLE FORCE THRU BY SALES UNIT       *         
      *                                                               *         
      * INELIGIBLE CODE : CHSIM - COM STALL IN MARKET                 *         
      * IN PPOS           CSTAL - COM STALL IN FOOD CTE               *         
      *****************************************************************         
      * CHGE REQ    DATE     BY  DESCRIPTION                          *         
      * -------- ---------- ---- ------------------------------------ *         
      * BP130636 09/02/1999 NOR  NEW PGRM                             *         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BR10K010            ASSIGN BR10K010                           
                                      ORGANIZATION IS INDEXED                   
                                      ACCESS MODE  IS DYNAMIC                   
                                      RECORD KEY   IS K010-KEY-FLD              
                                      FILE STATUS  IS WS-K010-STATUS.           
                                                                                
           SELECT BP13INP             ASSIGN BP13INP.                           
                                                                                
           SELECT BP13OUT             ASSIGN BP13OUT.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BR10K010                  RECORD CONTAINS 300 CHARACTERS.           
       COPY BR10K010.                                                           
                                                                                
       FD   BP13INP                   RECORD CONTAINS 30 CHARACTERS             
                                      LABEL RECORDS ARE STANDARD                
                                      RECORDING MODE IS F.                      
       01   BP13INP-REC.                                                        
            05  INP-REGN              PIC X(08).                                
            05  INP-NRIC              PIC X(09).                                
            05  FILLER                PIC X(13).                                
                                                                                
       FD   BP13OUT                   RECORD CONTAINS 30 CHARACTERS             
                                      LABEL RECORDS ARE STANDARD                
                                      RECORDING MODE IS F.                      
       01   BP13OUT-REC.                                                        
            05  OUT-REGN              PIC X(08).                                
            05  OUT-NRIC              PIC X(09).                                
            05  OUT-SCCC-TAG          PIC X(01).                                
            05  FILLER                PIC X(12).                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FLAG-N-SWITCH.                                                    
           05  WS-INP-EOF-SW          PIC X(01)  VALUE 'N'.                     
               88  WS-INP-EOF         VALUE 'Y'.                                
           05  WS-K010-FOUND-SW       PIC X(01)  VALUE 'N'.                     
               88  WS-K010-FOUND      VALUE 'Y'.                                
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K010-STATUS         PIC 9(02)  VALUE ZEROS.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-K010-FND-CTR        PIC 9(08)  VALUE ZEROS.                   
           05  WS-K010-NOTFND-CTR     PIC 9(08)  VALUE ZEROS.                   
           05  WS-INP-READ            PIC 9(08)  VALUE ZEROS.                   
           05  WS-OUT-WRITE           PIC 9(08)  VALUE ZEROS.                   
           05  WS-BYPASS              PIC 9(08)  VALUE ZEROS.                   
           05  WS-LINE-CTR            PIC 9(02)  VALUE ZEROS.                   
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-YY              PIC X(04)  VALUE SPACES.                  
           05  WS-SYS-MM              PIC X(02)  VALUE SPACES.                  
           05  WS-SYS-DD              PIC X(02)  VALUE SPACES.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      **************************                                                
       0000-MAIN.                                                               
      **************************                                                
           PERFORM 1000-OPEN-FILES               THRU  1000-EXIT.               
           PERFORM 2000-READ-BP13INP             THRU  2000-EXIT.               
           PERFORM 3000-PROCESS                  THRU  3000-EXIT                
             UNTIL WS-INP-EOF.                                                  
           PERFORM 9000-CLOSE-FILES              THRU  9000-EXIT.               
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************                                                
       1000-OPEN-FILES.                                                         
      **************************                                                
           OPEN INPUT BP13INP                                                   
                      BR10K010                                                  
               OUTPUT BP13OUT.                                                  
                                                                                
           IF WS-K010-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING BR10K010, FILE STATUS ('                   
                      WS-K010-STATUS ')'                                        
              MOVE WS-K010-STATUS          TO  RETURN-CODE                      
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************                                                
       2000-READ-BP13INP.                                                       
      **************************                                                
           READ BP13INP                                                         
             AT END                                                             
                MOVE 'Y'            TO  WS-INP-EOF-SW                           
            NOT AT END                                                          
                ADD   1             TO  WS-INP-READ                             
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************                                                
       2010-READNEXT-BR10K010.                                                  
      **************************                                                
           READ BR10K010 NEXT RECORD                                            
             AT END                                                             
                MOVE HIGH-VALUES        TO  K010-KEY-FLD                        
            NOT AT END                                                          
                CONTINUE                                                        
           END-READ.                                                            
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **************************                                                
       3000-PROCESS.                                                            
      **************************                                                
           MOVE SPACES              TO  K010-KEY-FLD.                           
           MOVE INP-NRIC            TO  K010-NUM-UIN.                           
           START BR10K010 KEY >= K010-KEY-FLD.                                  
           EVALUATE WS-K010-STATUS                                              
           WHEN ZEROS                                                           
              PERFORM 2010-READNEXT-BR10K010     THRU 2010-EXIT                 
              IF K010-NUM-UIN = INP-NRIC                                        
                 ADD 1                     TO  WS-K010-FND-CTR                  
                 IF K010-CDE-PPTY-TYPE NOT = 'CHSIM' AND 'CSTAL'                
                    PERFORM 4000-WRITE-OUTPUT THRU 4000-EXIT                    
                 ELSE                                                           
                    ADD 1                  TO  WS-BYPASS                        
                 END-IF                                                         
              ELSE                                                              
                 ADD 1              TO  WS-K010-NOTFND-CTR                      
              END-IF                                                            
           WHEN 23                                                              
              MOVE 'N'              TO  WS-K010-FOUND-SW                        
              ADD 1                 TO  WS-K010-NOTFND-CTR                      
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BR10K010, FILE STATUS ('                   
                      WS-K010-STATUS ')'                                        
              MOVE WS-K010-STATUS   TO  RETURN-CODE                             
              GO TO 9000-CLOSE-FILES                                            
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-READ-BP13INP            THRU 2000-EXIT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************                                                
       4000-WRITE-OUTPUT.                                                       
      **************************                                                
           MOVE  SPACES          TO  BP13OUT-REC.                               
           MOVE  BP13INP-REC     TO  BP13OUT-REC.                               
           MOVE 'N'              TO  OUT-SCCC-TAG.                              
           WRITE BP13OUT-REC.                                                   
           ADD 1              TO  WS-OUT-WRITE.                                 
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************                                                
       9000-CLOSE-FILES.                                                        
      **************************                                                
           DISPLAY SPACES.                                                      
           DISPLAY '****************  BP13C362  **********************'.        
           DISPLAY ' NO. OF BP13INP RECORDS READ          : '                   
                   WS-INP-READ.                                                 
           DISPLAY ' NO. OF RECORDS FOUND IN BR10K010     : '                   
                   WS-K010-FND-CTR.                                             
           DISPLAY ' NO. OF RECORDS NOT FOUND IN BR10K010 : '                   
                   WS-K010-NOTFND-CTR.                                          
           DISPLAY ' NO. OF OUTPUT RECORDS                : '                   
                   WS-OUT-WRITE.                                                
           DISPLAY ' NO. OF RECORDS BYPASS (PPTY-TYPE)    : '                   
                   WS-BYPASS.                                                   
           DISPLAY '****************  --------  **********************'.        
                                                                                
           CLOSE BP13INP                                                        
                 BR10K010                                                       
                 BP13OUT.                                                       
                                                                                
           IF WS-K010-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR CLOSING BR10K010, FILE STATUS ('                   
                      WS-K010-STATUS ')'                                        
              MOVE WS-K010-STATUS        TO  RETURN-CODE                        
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
      **********************  END OF PROGRAM LISTING  *****************         
