       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C778                                                  
      *AUTHOR.        JK.                                                       
      *DATE-WRITTEN.  16/11/93.                                                 
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  TO REPLACE REFERENCE NUMBER FOR APPLICATIONS  *                
      *          WHICH MAY HAVE CHANGED DURING THE LAPSE       *                
      *          BETWEEN MHA DATA TRANSFER                     *                
      *                                                        *                
      *    TASK :                                              *                
      *      1.  TO ENSURE FOR EVERY MATCHING NRIC, THE        *                
      *          REFERENCE NUMBER ALSO MATCHES.                *                
      *                                                        *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *      1  BP13F825                                       *                
      *      2  BP13F725                                       *                
      *                                                        *                
      *     OUTPUT FILE :                                      *                
      *      1  BP13OUT                                        *                
      *                                                        *                
      * CHG-NO  BY   ON      DESCRIPTION                       *                
      * --------------------------------                       *                
      * C950106 LSB 190195 - CHG F558 TO F825, CHG LRECL F725  *                
      *BP130082 LSB 170196 - CHG BP13F725 FR 205 TO 211  RECL  *                
      *BP132376 MJ16290703 - USE REGNNO INSTEAD OF REFNO OF    *                
      *                      FILE BP13F725                     *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT BP13F725 ASSIGN TO BP13F725.                                  
           SELECT BP13F825 ASSIGN TO BP13F825.                                  
           SELECT BP13OUT ASSIGN TO BP13OUT.                                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F725                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 211 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F725.                                                           
                                                                                
       FD   BP13F825                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F825.                                                           
                                                                                
       FD   BP13OUT                                                             
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 211 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       01   BP13OUT-REC            PIC X(211).                                  
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F725-CNT             PIC 9(8)    VALUE ZERO.                      
       01  WS-F825-CNT             PIC 9(8)    VALUE ZERO.                      
       01  WS-MATCH                PIC 9(8)    VALUE ZERO.                      
       01  WS-CHGE-REF             PIC 9(8)    VALUE ZERO.                      
       01  WS-F725-FD              PIC 9(8)    VALUE ZERO.                      
       01  WS-EOF-F725             PIC X(3).                                    
           88  EOF-OF-F725                     VALUE 'YES'.                     
       01  WS-EOF-F825             PIC X(3).                                    
           88  EOF-OF-F825                     VALUE 'YES'.                     
                                                                                
       01  WS-F725-REC.                                                         
           05  WS-NUM-REF                  PIC X(8).                            
           05  WS-NUM-NRIC                 PIC X(9).                            
           05  FILLER                      PIC X(194).                          
                                                                                
       PROCEDURE DIVISION.                                                      
       10-MAIN-LOGIC.                                                           
           PERFORM 20-INITIALIZATION  THRU 20-EXIT.                             
           PERFORM 200-READ-F725      THRU 200-EXIT.                            
           PERFORM 300-READ-F825      THRU 300-EXIT.                            
           PERFORM 400-MATCHING-ROUTINE THRU 400-EXIT                           
                   UNTIL EOF-OF-F825 AND EOF-OF-F725.                           
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       20-INITIALIZATION.                                                       
           OPEN INPUT  BP13F725                                                 
                       BP13F825                                                 
               OUTPUT  BP13OUT.                                                 
                                                                                
           MOVE 'NO' TO WS-EOF-F725.                                            
           MOVE 'NO' TO WS-EOF-F825.                                            
                                                                                
       20-EXIT.                                                                 
           EXIT.                                                                
                                                                                
       200-READ-F725.                                                           
           READ BP13F725 INTO WS-F725-REC                                       
                AT END MOVE 'YES' TO WS-EOF-F725                                
                MOVE HIGH-VALUE TO WS-NUM-NRIC                                  
                GO TO 200-EXIT.                                                 
           ADD 1 TO WS-F725-CNT.                                                
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-F825.                                                           
           READ BP13F825                                                        
               AT END MOVE 'YES' TO WS-EOF-F825                                 
               MOVE HIGH-VALUE TO F825-NUM-NRIC                                 
               GO TO 300-EXIT.                                                  
           ADD 1 TO WS-F825-CNT.                                                
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *  MATCH F725 WITH F825 FILE (NUM-NRIC)                         *         
      *****************************************************************         
                                                                                
       400-MATCHING-ROUTINE.                                                    
           IF WS-NUM-NRIC = F825-NUM-NRIC                                       
               PERFORM 450-MATCH-REF  THRU 450-EXIT                             
                 UNTIL WS-NUM-NRIC NOT = F825-NUM-NRIC                          
              PERFORM 300-READ-F825 THRU 300-EXIT                               
           ELSE                                                                 
               IF WS-NUM-NRIC > F825-NUM-NRIC                                   
                   PERFORM 300-READ-F825 THRU 300-EXIT                          
           ELSE                                                                 
               IF WS-NUM-NRIC < F825-NUM-NRIC                                   
                   ADD 1 TO WS-F725-FD                                  .       
                   WRITE BP13OUT-REC FROM WS-F725-REC                           
                   PERFORM 200-READ-F725  THRU 200-EXIT.                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       450-MATCH-REF.                                                           
              IF F725-NUM-REGN = F825-NUM-REGN                                  
                 ADD 1 TO WS-MATCH                                      .       
                 WRITE BP13OUT-REC FROM WS-F725-REC                             
              ELSE                                                              
                 ADD 1 TO WS-CHGE-REF                                   .       
                 DISPLAY 'CHGED REF FROM  '                                     
                       WS-NUM-REF ' TO ' F825-NUM-REGN ' ' WS-NUM-NRIC          
                 MOVE F825-NUM-REGN TO WS-NUM-REF                               
                 WRITE BP13OUT-REC FROM WS-F725-REC                             
              END-IF.                                                           
              PERFORM 200-READ-F725  THRU 200-EXIT.                             
                                                                                
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
           DISPLAY ' '.                                                         
           DISPLAY '---- BP13C778 --------------------------'.                  
           DISPLAY ' '.                                                         
           DISPLAY '1. RECORDS READ FROM BP13F725    = ' WS-F725-CNT.           
           DISPLAY '2. RECORDS READ FROM BP13F825    = ' WS-F825-CNT.           
           DISPLAY '3. NO. OF CASES WITH SAME REF    = '                .       
                                              WS-MATCH.                 .       
           DISPLAY '4. NO. OF CASES WITH CHGED REF   = '                .       
                                              WS-CHGE-REF.              .       
           DISPLAY '5. NO. OF UNMTCHED CASES FR F725 = '                .       
                                              WS-F725-FD.               .       
           DISPLAY ' '.                                                         
                                                                                
           CLOSE      BP13F825                                                  
                      BP13F725                                                  
                      BP13OUT.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
