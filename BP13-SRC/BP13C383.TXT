      *****************************************************************         
       IDENTIFICATION DIVISION.                                                 
      *****************************************************************         
       PROGRAM-ID.    BP13C383.                                                 
      *AUTHOR.        EG1.                                                      
      *DATE-WRITTEN.  FEB 06 1996.                                              
                                                                                
      *****************************************************************         
      * OBJECTIVE  : TO EXTRACT SMS RECORDS FOR ALL OCCUPIERS OF AN   *         
      *              APPLICATION AFTER BOOKING.                       *         
      * INPUT FILES:  BP13F310              AB03K080                  *         
      *               BP13K820              AB03K030                  *         
      *                                                               *         
      * OUTPUT FILES: BP13F313                                        *         
      * REPORT:       - NIL                                           *         
      * SUBROUTINES   -                                               *         
      * IDENTICAL PGM -                                               *         
      *---------------------------------------------------------------*         
      * REF NO   DATE        BY   AMENDMENTS/ENHANCEMENTS             *         
      * -------  ----        ---- -------------------                 *         
      * BP130068 03/02/1996. EG1 LASER PRINTING OF SOCA LETTER        *         
      * BP130418 18/08/1998. LJL TO CATER FOR Y2K.                    *         
      *****************************************************************         
                                                                                
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
      *===============================================================*         
       CONFIGURATION SECTION.                                                   
      *===============================================================*         
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *===============================================================*         
       INPUT-OUTPUT SECTION.                                                    
      *===============================================================*         
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310 ASSIGN        TO BP13F310.                           
                                                                                
           SELECT BP13K820 ASSIGN        TO BP13K820                            
                  ACCESS MODE  IS        DYNAMIC                                
                  ORGANIZATION IS        INDEXED                                
                  RECORD KEY   IS        K820-KEY-FLD                           
                  FILE STATUS  IS        BP13K820-STATUS.                       
                                                                                
           SELECT AB03K080 ASSIGN        TO AB03K080                            
                  ACCESS MODE  IS        DYNAMIC                                
                  ORGANIZATION IS        INDEXED                                
                  RECORD KEY   IS        K080-KEY                               
                  FILE STATUS  IS        AB03K080-STATUS.                       
                                                                                
           SELECT AB03K030 ASSIGN        TO AB03K030                            
                  ACCESS MODE  IS        RANDOM                                 
                  ORGANIZATION IS        INDEXED                                
                  RECORD KEY   IS        K030-NUM-HDB-REF                       
                  FILE STATUS  IS        AB03K030-STATUS.                       
                                                                                
           SELECT BP13F313 ASSIGN        TO BP13F313.                           
                                                                                
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
      *===============================================================*         
       FILE SECTION.                                                            
      *===============================================================*         
                                                                                
       FD   BP13F310                                                            
            RECORDING  MODE IS F                                                
            RECORD CONTAINS 150 CHARACTERS                                      
            BLOCK  CONTAINS   0 RECORDS.                                        
       COPY BP13F310.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   AB03K030                                                            
            RECORD CONTAINS 800 CHARACTERS.                                     
       COPY AB03K030.                                                           
                                                                                
       FD   AB03K080                                                            
            RECORD CONTAINS  50 CHARACTERS.                                     
       COPY AB03K080.                                                           
                                                                                
       FD   BP13F313                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 50  CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS.                                        
       01   BP13F313-REC.                                                       
            05 F313-NUM-REGN             PIC X(8).                              
            05 F313-NUM-NRIC             PIC X(9).                              
            05 F313-NUM-SCH-ACC          PIC X(11).                             
            05 FILLER                    PIC X(22).                             
                                                                                
      *===============================================================*         
       WORKING-STORAGE SECTION.                                                 
      *===============================================================*         
      *---------------------------------------------------------------*         
      *   COUNTER VARIABLES                                           *         
      *---------------------------------------------------------------*         
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F310-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-F313-WRITE          PIC 9(07) VALUE 0.                     
                                                                                
      *---------------------------------------------------------------*         
      *   FLAG    VARIABLES                                           *         
      *---------------------------------------------------------------*         
       01  WS-F310-EOF                   PIC X(01) VALUE 'N'.                   
       01  WS-K820-EOF                   PIC X(01) VALUE 'N'.                   
       01  WS-K080-EOF                   PIC X(01) VALUE 'N'.                   
                                                                                
       01  BP13K820-STATUS               PIC X(2)  VALUE SPACES.                
       01  AB03K030-STATUS               PIC X(2)  VALUE SPACES.                
       01  AB03K080-STATUS               PIC X(2)  VALUE SPACES.                
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
      *===============================================================*         
       0000-CONTROL.                                                            
      *===============================================================*         
                                                                                
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
                                                                                
           PERFORM 2000-MAIN-ROUTINE     THRU 2000-EXIT                         
                UNTIL WS-F310-EOF = 'Y'.                                        
                                                                                
           PERFORM 9999-WRAP-UP.                                                
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       1000-START-UP.                                                           
      *===============================================================*         
                                                                                
            OPEN INPUT  BP13F310                                                
                        BP13K820                                                
                        AB03K030                                                
                        AB03K080                                                
                OUTPUT  BP13F313.                                               
                                                                                
            IF BP13K820-STATUS NOT = '00'                                       
               DISPLAY 'BP13K820 OPENING ERROR: ' BP13K820-STATUS               
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF AB03K030-STATUS NOT = '00'                                       
               DISPLAY 'AB03K030 OPENING ERROR: ' AB03K030-STATUS               
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF AB03K080-STATUS NOT = '00'                                       
               DISPLAY 'AB03K080 OPENING ERROR: ' AB03K030-STATUS               
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            PERFORM 8100-READ-BP13F310   THRU 8100-EXIT.                        
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       2000-MAIN-ROUTINE.                                                       
      *===============================================================*         
      *---------------------------------------------------------------*         
      * GET ALL THE OCCUPIER FOR THE APPLICATION AND PROCESS EACH.    *         
      *---------------------------------------------------------------*         
            MOVE LOW-VALUES                TO K820-KEY-FLD.                     
            MOVE F310-REGN-NO              TO K820-NUM-REGN.                    
            PERFORM 8200-START-BP13K820    THRU 8200-EXIT.                      
            IF WS-K820-EOF = 'N'                                                
               PERFORM 8250-READNEXT-BP13K820 THRU 8250-EXIT                    
            END-IF.                                                             
            IF WS-K820-EOF = 'Y'                                                
               DISPLAY 'K820 NOT FOUND: ' F310-REGN-NO                          
            END-IF.                                                             
            PERFORM 2100-CHECK-OCCUPIERS   THRU 2100-EXIT                       
              UNTIL WS-K820-EOF = 'Y'.                                          
                                                                                
            PERFORM 8100-READ-BP13F310     THRU 8100-EXIT.                      
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       2100-CHECK-OCCUPIERS.                                                    
      *===============================================================*         
           MOVE LOW-VALUES                TO K080-KEY.                          
           MOVE K820-NUM-NRIC             TO K080-NUM-UIN.                      
           PERFORM 8300-START-AB03K080    THRU 8300-EXIT.                       
           IF WS-K080-EOF = 'N'                                                 
              PERFORM 8350-READNEXT-AB03K080 THRU 8350-EXIT                     
           END-IF.                                                              
           IF WS-K080-EOF = 'Y'                                                 
              DISPLAY 'K080 NOT FOUND: ' K820-NUM-NRIC                          
           END-IF.                                                              
           PERFORM 2200-CHECK-SALEMAST    THRU 2200-EXIT                        
             UNTIL WS-K080-EOF = 'Y'.                                           
                                                                                
           PERFORM 8250-READNEXT-BP13K820 THRU 8250-EXIT.                       
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       2200-CHECK-SALEMAST.                                                     
      *===============================================================*         
           MOVE K080-NUM-HDB-REF          TO K030-NUM-HDB-REF.                  
           PERFORM 8400-READ-AB03K030     THRU 8400-EXIT.                       
           IF AB03K030-STATUS = '00'                                            
              IF K030-CDE-ACCT-STAT = 'C'                                       
                 MOVE SPACES              TO BP13F313-REC                       
                 MOVE F310-REGN-NO        TO F313-NUM-REGN                      
                 MOVE K820-NUM-NRIC       TO F313-NUM-NRIC                      
                 MOVE K080-NUM-HDB-REF    TO F313-NUM-SCH-ACC                   
                 PERFORM 8500-WRITE-BP13F313 THRU 8500-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 8350-READNEXT-AB03K080 THRU 8350-EXIT.                       
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       8100-READ-BP13F310.                                                      
      *===============================================================*         
                                                                                
           READ BP13F310                                                        
                AT END MOVE 'Y'  TO  WS-F310-EOF                                
                       GO TO 8100-EXIT.                                         
                                                                                
           ADD 1                 TO WS-CNT-F310-READ.                           
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       8200-START-BP13K820.                                                     
      *===============================================================*         
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD                                   
                 INVALID KEY MOVE 'Y' TO WS-K820-EOF                            
                             GO TO 8200-EXIT.                                   
                                                                                
           IF BP13K820-STATUS NOT = '00'                                        
              DISPLAY 'BP13K820 START ERROR: ' BP13K820-STATUS                  
              PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE 'N'              TO WS-K820-EOF.                                
                                                                                
       8200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       8250-READNEXT-BP13K820.                                                  
      *===============================================================*         
                                                                                
           READ BP13K820 NEXT                                                   
                AT END MOVE 'Y'      TO WS-K820-EOF                             
                       GO TO 8250-EXIT.                                         
                                                                                
           IF BP13K820-STATUS NOT = '00'                                        
              DISPLAY 'BP13K820 READNEXT ERROR: ' BP13K820-STATUS               
              PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                       
           END-IF.                                                              
                                                                                
           IF K820-NUM-REGN NOT = F310-REGN-NO                                  
              MOVE 'Y'              TO WS-K820-EOF                              
           END-IF.                                                              
                                                                                
       8250-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       8300-START-AB03K080.                                                     
      *===============================================================*         
                                                                                
           START AB03K080 KEY >= K080-KEY                                       
                 INVALID KEY MOVE 'Y' TO WS-K080-EOF                            
                             GO TO 8300-EXIT.                                   
                                                                                
           IF AB03K080-STATUS NOT = '00'                                        
              DISPLAY 'AB03K080 START ERROR: ' AB03K080-STATUS                  
              PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE 'N'              TO WS-K080-EOF.                                
                                                                                
       8300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       8350-READNEXT-AB03K080.                                                  
      *===============================================================*         
                                                                                
           READ AB03K080 NEXT                                                   
                AT END MOVE 'Y'      TO WS-K080-EOF                             
                       GO TO 8350-EXIT.                                         
                                                                                
           IF AB03K080-STATUS NOT = '00'                                        
              DISPLAY 'AB03K080 READNEXT ERROR: ' AB03K080-STATUS               
              PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                       
           END-IF.                                                              
                                                                                
           IF K080-NUM-UIN NOT = K820-NUM-NRIC                                  
              MOVE 'Y'              TO WS-K080-EOF                              
           END-IF.                                                              
                                                                                
       8350-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       8400-READ-AB03K030.                                                      
      *===============================================================*         
                                                                                
           READ AB03K030                                                        
           INVALID KEY DISPLAY 'K030 NOT FOUND: ' K080-NUM-HDB-REF              
                       MOVE SPACE      TO K030-CDE-ACCT-STAT                    
                       GO TO 8400-EXIT.                                         
                                                                                
           IF AB03K030-STATUS NOT = '00'                                        
              DISPLAY 'AB03K030 READ ERROR: ' AB03K030-STATUS                   
              PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                       
           END-IF.                                                              
                                                                                
       8400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       8500-WRITE-BP13F313.                                                     
      *===============================================================*         
                                                                                
           WRITE BP13F313-REC.                                                  
           ADD   1             TO WS-CNT-F313-WRITE.                            
                                                                                
       8500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *******************                                                       
       9900-HANDLE-ABEND.                                                       
      *******************                                                       
                                                                                
           DISPLAY  SPACES.                                                     
           DISPLAY '******* BP13C383 ABEND TOTALS ********'.                    
           DISPLAY 'PROGRAM-ID : BP13C383'.                                     
           DISPLAY  SPACES.                                                     
           DISPLAY 'TOTAL BP13F310 RECS READ     = ' WS-CNT-F310-READ.          
           DISPLAY 'TOTAL BP13F313 RECS WRITTEN  = ' WS-CNT-F313-WRITE.         
                                                                                
           CLOSE BP13F310 BP13F313 BP13K820 AB03K080 AB03K030.                  
                                                                                
           MOVE '4096'               TO RETURN-CODE.                            
                                                                                
           STOP RUN.                                                            
                                                                                
       9900-EXIT.                                                               
            EXIT.                                                               
                                                                                
      **************                                                            
       9999-WRAP-UP.                                                            
      **************                                                            
                                                                                
                                                                                
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13C383'.                                     
           DISPLAY  SPACES.                                                     
           DISPLAY 'TOTAL BP13F310 RECS READ     = ' WS-CNT-F310-READ.          
           DISPLAY 'TOTAL BP13F313 RECS WRITTEN  = ' WS-CNT-F313-WRITE.         
                                                                                
           CLOSE BP13F310 BP13F313 BP13K820 AB03K080 AB03K030.                  
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
