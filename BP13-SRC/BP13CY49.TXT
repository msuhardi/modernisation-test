       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY49.                                                 
      *AUTHOR.        IRVIN CHING.                                              
      *                                                                         
      ************************************************************              
      *                   SOC SYSTEM (SOC)                       *              
      *==========================================================*              
      * DATE WRITTEN      : 27 JAN 2013                          *              
      *                                                          *              
      * 1. OBJECTIVE      : CREATE 2 FILES FROM BP13F595 WITH    *              
      *                   : FOUND AND NOT FOUND CASES IN         *              
      *                   : BP13K803                             *              
      *                                                          *              
      * 2. INPUT          : BP13F595 - BP13.F595.C996.NOTFOUND   *              
      *                   : BP13K803 - BP13.K803.OCCUP           *              
      *                                                          *              
      * 3. OUTPUT         : P13F595F - BP13.F595.C995.FOUND      *              
      *                   : P13F595N - BP13.F595.C995.NOTFOUND   *              
      *==========================================================*              
      *  MODIFICATIONS:                                          *              
      *                                                          *              
      *  CHGE-NO  DATE       OIC  DESCRIPTION                    *              
      *  -------- ---------- ---- ------------------------------ *              
      *  BP134780 27/01/2013 IMC1 NEW PROGRAM                    *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F595     ASSIGN        TO BP13F595.                       
                                                                                
           SELECT BP13K803     ASSIGN        TO BP13K803                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K803-KEY-FLD                    
                               ALTERNATE RECORD KEY IS K803-NUM-NRIC            
                                                WITH DUPLICATES                 
                               FILE STATUS   IS WS-K803-STATUS.                 
                                                                                
           SELECT P13F595F     ASSIGN        TO P13F595F.                       
                                                                                
           SELECT P13F595N     ASSIGN        TO P13F595N.                       
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F595                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       COPY BP13F595.                                                           
                                                                                
       FD  BP13K803                                                             
                               RECORD CONTAINS 100 CHARACTERS.                  
       COPY BP13K803.                                                           
                                                                                
       FD  P13F595F                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595F-REC                  PIC X(500).                            
                                                                                
       FD  P13F595N                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595N-REC                  PIC X(500).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05  WS-K803-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K800-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K893-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K895-STATUS            PIC 9(2)  VALUE ZEROES.                
                                                                                
       01  END-OF-FILE-FLAGS.                                                   
           05  WS-F595-EOF               PIC X     VALUE 'N'.                   
           05  WS-K803-EOF               PIC X     VALUE 'N'.                   
           05  WS-K895-EOF               PIC X     VALUE 'N'.                   
                                                                                
       01  COUNTERS.                                                            
           05  WS-F595-READ-CNT          PIC 9(6)  VALUE ZEROES.                
           05  WS-595F-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-595N-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-NRIC-LIST-CNT          PIC 9(1)  VALUE ZEROES.                
           05  WS-CNT                    PIC 9(1)  VALUE ZERO.                  
           05  WS-CANCEL-NOTELIG         PIC 9(6)  VALUE ZERO.                  
           05  WS-CANCEL-NOTBK           PIC 9(6)  VALUE ZERO.                  
           05  WS-CANCEL-BALLOT          PIC 9(6)  VALUE ZERO.                  
           05  WS-BE-AUG                 PIC 9(6)  VALUE ZERO.                  
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURRENT-DATE1          PIC X(8) VALUE SPACES.                 
           05  WS-CURRENT-TIME           PIC X(9) VALUE SPACES.                 
               05  WS-TIME REDEFINES WS-CURRENT-TIME.                           
                   10  WS-HHMMSSMS       PIC X(8).                              
                   10  WS-GM             PIC X(1).                              
           05  FILLER                    PIC X(4) VALUE SPACES.                 
                                                                                
       01  TEMPORARY-VARIABLES.                                                 
           05  WS-NRIC OCCURS 2 TIMES.                                          
               10 WS-NRIC-LIST           PIC X(9)  VALUE SPACES.                
           05  WS-RUN-DATE               PIC X(10) VALUE SPACES.                
           05  WS-NRIC-FOUND             PIC X(1)  VALUE 'N'.                   
           05  WS-K803-REC-PROCESS       PIC X(1)  VALUE 'N'.                   
           05  WS-K803-NRIC-PREV         PIC X(9)  VALUE SPACES.                
           05  WS-K895-REC-PROCESS       PIC X(1)  VALUE 'N'.                   
           05  WS-K895-NRIC-PREV         PIC X(9)  VALUE SPACES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
           PERFORM 1000-OPEN-ROUTINE THRU 1000-EXIT.                            
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-F595-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       1000-OPEN-ROUTINE.                                                       
      *************************************************************             
           OPEN INPUT  BP13F595                                                 
                       BP13K803                                                 
                OUTPUT P13F595F                                                 
                       P13F595N.                                                
                                                                                
           IF WS-K803-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K803 OPEN ERROR ' WS-K803-STATUS                     
              MOVE WS-K803-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CURRENT-DATE.                    
                                                                                
           STRING WS-CURRENT-DATE1(7:2) '/'                                     
                  WS-CURRENT-DATE1(5:2) '/'                                     
                  WS-CURRENT-DATE1(1:4)                                         
                  DELIMITED BY SIZE                                             
             INTO WS-RUN-DATE                                                   
                                                                                
           PERFORM 1100-READ-BP13F595 THRU 1100-EXIT.                           
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       1100-READ-BP13F595.                                                      
      *************************************************************             
           READ BP13F595                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F595-EOF                         
            NOT AT END                                                          
                ADD 1                    TO WS-F595-READ-CNT                    
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       2000-PROCESS-RECORDS.                                                    
      *************************************************************             
           MOVE SPACES                   TO WS-NRIC-LIST(1)                     
                                            WS-NRIC-LIST(2).                    
                                                                                
           MOVE F595-NUM-NRIC1           TO WS-NRIC-LIST(1).                    
           MOVE 1                        TO WS-NRIC-LIST-CNT.                   
                                                                                
           IF F595-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND                    
              F595-NUM-NRIC2(1:1) NOT = '#'                                     
              MOVE F595-NUM-NRIC2        TO WS-NRIC-LIST(2)                     
              ADD 1                      TO WS-NRIC-LIST-CNT                    
           END-IF.                                                              
                                                                                
           MOVE 'N'                      TO WS-NRIC-FOUND.                      
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1                                   
             UNTIL WS-CNT > WS-NRIC-LIST-CNT                                    
                OR WS-NRIC-FOUND = 'Y'                                          
                   MOVE SPACES           TO P13F595F-REC                        
                                            P13F595N-REC                        
                                                                                
                   PERFORM 3000-START-BP13K803 THRU 3000-EXIT                   
           END-PERFORM.                                                         
                                                                                
           IF WS-NRIC-FOUND = 'N'                                               
              MOVE BP13F595-REC          TO P13F595N-REC                        
                                                                                
              WRITE P13F595N-REC                                                
                                                                                
              ADD 1                      TO WS-595N-WRITE-CNT                   
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-BP13F595 THRU 1100-EXIT.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3000-START-BP13K803.                                                     
      *************************************************************             
           MOVE SPACES                   TO BP13K803-REC.                       
           INITIALIZE                       BP13K803-REC.                       
                                                                                
           MOVE WS-NRIC-LIST(WS-CNT)     TO K803-NUM-NRIC                       
                                            WS-K803-NRIC-PREV.                  
           MOVE 'N'                      TO WS-K803-EOF                         
                                            WS-K803-REC-PROCESS.                
                                                                                
           START BP13K803 KEY >= K803-NUM-NRIC.                                 
                                                                                
           EVALUATE WS-K803-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3100-READNEXT-BP13K803 THRU 3100-EXIT               
                      UNTIL K803-NUM-NRIC NOT = WS-K803-NRIC-PREV               
                         OR WS-K803-EOF = 'Y'                                   
                         OR WS-K803-REC-PROCESS = 'Y'                           
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K803 START ERROR ' WS-K803-STATUS              
                    DISPLAY 'K803 KEY: ' K803-KEY-FLD                           
                    MOVE WS-K803-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3100-READNEXT-BP13K803.                                                  
      *************************************************************             
                                                                                
           READ BP13K803 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                 TO WS-K803-EOF                         
           END-READ.                                                            
                                                                                
           EVALUATE WS-K803-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K803-NUM-NRIC = WS-K803-NRIC-PREV                        
                       IF K803-DTE-CREATE-SOCHIST = SPACES OR LOW-VALUES        
                          PERFORM 4000-PROCESS-K800-CASES                       
                             THRU 4000-EXIT                                     
                           MOVE 'Y'          TO WS-K803-REC-PROCESS             
                       ELSE                                                     
                           PERFORM 4100-PROCESS-K893-CASES                      
                              THRU 4100-EXIT                                    
                       END-IF                                                   
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K803 READ NEXT ERROR ' WS-K803-STATUS          
                    DISPLAY 'K803 KEY: ' K803-KEY-FLD                           
                    MOVE WS-K803-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       4000-PROCESS-K800-CASES.                                                 
      *************************************************************             
                                                                                
           IF K803-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                      
              IF WS-NRIC-LIST(WS-CNT) = K803-NUM-NRIC                           
                 PERFORM 6000-WRITE-BP13F595  THRU 6000-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       4100-PROCESS-K893-CASES.                                                 
      ******************************************************************        
                                                                                
           IF K803-DTE-SO-APPMT(1:6) >= F595-DTE-BALLOT                         
               IF WS-NRIC-LIST(WS-CNT) = K803-NUM-NRIC                          
                   PERFORM 6000-WRITE-BP13F595     THRU 6000-EXIT               
               END-IF                                                           
           ELSE                                                                 
               IF K803-DTE-CREATE-SOCHIST(1:6) >= F595-DTE-BALLOT               
                  IF WS-NRIC-LIST(WS-CNT) = K803-NUM-NRIC                       
                     PERFORM 6000-WRITE-BP13F595   THRU 6000-EXIT               
                  END-IF                                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       6000-WRITE-BP13F595.                                                     
      *************************************************************             
                                                                                
            MOVE SPACES                  TO P13F595F-REC.                       
            INITIALIZE                      P13F595F-REC.                       
                                                                                
            WRITE P13F595F-REC         FROM BP13F595-REC.                       
                                                                                
            ADD 1                        TO WS-595F-WRITE-CNT.                  
            MOVE 'Y'                     TO WS-NRIC-FOUND.                      
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       9000-CLOSE-ROUTINE.                                                      
      *************************************************************             
           DISPLAY '   '.                                                       
           DISPLAY 'RUN DATE : ' WS-RUN-DATE.                                   
           DISPLAY '*--------- BP13CY49 CONTROL TOTAL ---------*'.              
           DISPLAY '   '.                                                       
           DISPLAY 'NO. OF READS FROM F595   : ' WS-F595-READ-CNT.              
           DISPLAY 'RECS WRITTEN TO P13F595F : ' WS-595F-WRITE-CNT.             
           DISPLAY 'RECS WRITTEN TO P13F595N : ' WS-595N-WRITE-CNT.             
           DISPLAY 'RECS CANCEL NOT BOOK     : ' WS-CANCEL-NOTBK.               
           DISPLAY 'RECS CANCEL OTHER        : ' WS-CANCEL-NOTELIG.             
           DISPLAY 'RECS CANCEL -THIS BALLOT : ' WS-CANCEL-BALLOT.              
           DISPLAY 'RECS AUG BE              : ' WS-BE-AUG.                     
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F595                                                       
                 BP13K803                                                       
                 P13F595F                                                       
                 P13F595N.                                                      
                                                                                
           IF WS-K803-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K803 CLOSE ERROR ' WS-K803-STATUS                    
              MOVE WS-K803-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
