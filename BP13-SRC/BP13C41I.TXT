       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C41I.                                                  
      *AUTHOR.       RJB1.                                                      
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO ENHANCE BP13K482 FILE TO CATER FOR 2ND   *           
      *                 CON LOAN CASES WITH SALES CONTRA            *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13K480                                              *           
      *   2.  BP13F482                                              *           
      *                                                             *           
      *   INPUT-OUTPUT FILES:                                       *           
      *   1.  BP13K482                                              *           
      *                                                             *           
      * REF      DATE      BY   DESCRIPTIONS                        *           
      * -------  --------  ---- ------------                        *           
      * BP137105 28/11/17  RJB1 NEW PROGRAM                         *           
      * =========================================================== *           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K480  ASSIGN TO BP13K480.                                 
           SELECT BP13F482  ASSIGN TO BP13F482.                                 
                                                                                
           SELECT BP13K482  ASSIGN TO BP13K482                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K482-KEY-FLD                              
                  ACCESS           IS RANDOM                                    
                  FILE STATUS      IS WS-K482-STATUS.                           
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13K480   BLOCK CONTAINS 0 RECORDS                                  
                      RECORDING MODE  IS  F                                     
                      RECORD CONTAINS 1000 CHARACTERS                           
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13K480.                                                           
                                                                                
       FD  BP13F482   BLOCK CONTAINS 0 RECORDS                                  
                      RECORDING MODE  IS  F                                     
                      RECORD CONTAINS 1000 CHARACTERS                           
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13F482.                                                           
                                                                                
       FD  BP13K482                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K482.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-VARIABLES.                                                        
           05  WS-K482-STATUS            PIC 99    VALUE ZEROS.                 
           05  WS-K480-EOF               PIC X     VALUE 'N'.                   
               88 K480-EOF                         VALUE 'Y'.                   
           05  WS-F482-EOF               PIC X     VALUE 'N'.                   
               88 F482-EOF                         VALUE 'Y'.                   
           05  WS-K480-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-F482-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K482-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K482-REWRITE           PIC 9(6)  VALUE ZEROS.                 
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES      THRU 100-EXIT.                          
            PERFORM 200-READ-K480       THRU 200-EXIT.                          
            PERFORM 300-READ-F482       THRU 300-EXIT.                          
            PERFORM 500-PROCESS-MATCH   THRU 500-EXIT                           
              UNTIL K480-EOF OR F482-EOF                                        
            PERFORM 999-CLOSE-FILES     THRU 999-EXIT.                          
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13K480                                                 
                       BP13F482                                                 
                I-O    BP13K482.                                                
                                                                                
           IF WS-K482-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, K482-STATUS ' WS-K482-STATUS              
              MOVE WS-K482-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-K480.                                                           
      *-------------------------------------------------------------            
           READ BP13K480 AT END                                                 
           MOVE 'Y'                      TO WS-K480-EOF                         
           GO TO 200-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-K480-READ.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-F482.                                                           
      *-------------------------------------------------------------            
           READ BP13F482 AT END                                                 
           MOVE 'Y'                      TO WS-F482-EOF                         
           GO TO 300-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-F482-READ.                       
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       500-PROCESS-MATCH.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF K480-NUM-REGN = F482-NUM-REGN                                     
              PERFORM 600-PROC-K482    THRU 600-EXIT                            
                                                                                
              PERFORM 200-READ-K480    THRU 200-EXIT                            
              PERFORM 300-READ-F482    THRU 300-EXIT                            
           ELSE                                                                 
              IF K480-NUM-REGN > F482-NUM-REGN                                  
                 PERFORM 300-READ-F482 THRU 300-EXIT                            
              ELSE                                                              
                 PERFORM 200-READ-K480 THRU 200-EXIT                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       600-PROC-K482.                                                           
      *-------------------------------------------------------------            
           MOVE SPACES              TO K482-KEY-FLD.                            
           MOVE F482-KEY-FLD        TO K482-KEY-FLD.                            
                                                                                
           READ BP13K482.                                                       
                                                                                
           EVALUATE WS-K482-STATUS                                              
           WHEN 00                                                              
              ADD 1                 TO WS-K482-READ                             
              IF K482-CDE-CONTRA-TAG NOT = 'Y'                                  
                 PERFORM 700-REWRITE-BP13K482                                   
                    THRU 700-EXIT                                               
              ELSE                                                              
                 DISPLAY 'CONTRA TAG IS ALREADY Y : '                           
                         K482-KEY-FLD ' ' K482-NUM-REGN                         
              END-IF                                                            
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND. KEY IS: ' K482-KEY-FLD                 
              DISPLAY 'WS-K482-STATUS = ' WS-K482-STATUS                        
           WHEN OTHER                                                           
              DISPLAY 'BP13K482 - READ ERROR (' WS-K482-STATUS                  
                      '), KEY IS ('  K482-KEY-FLD ')'                           
              PERFORM 999-CLOSE-FILES                                           
                 THRU 999-EXIT                                                  
           END-EVALUATE.                                                        
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       700-REWRITE-BP13K482.                                                    
      *-------------------------------------------------------------            
                                                                                
           DISPLAY 'PREV CONTRA-TAG OF ' K482-NUM-HDBREF-NEW ' '                
                    K482-NUM-REGN ' = ' K482-CDE-CONTRA-TAG.                    
                                                                                
           MOVE 'Y'                 TO  K482-CDE-CONTRA-TAG.                    
                                                                                
           REWRITE BP13K482-REC.                                                
                                                                                
           IF WS-K482-STATUS = 00                                               
              ADD 1                 TO WS-K482-REWRITE                          
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13K482 STATUS '                        
                                      WS-K482-STATUS                            
              MOVE WS-K482-STATUS      TO RETURN-CODE                           
              PERFORM 999-CLOSE-FILES  THRU 999-EXIT                            
           END-IF.                                                              
                                                                                
           DISPLAY 'NEW  CONTRA-TAG OF ' K482-NUM-HDBREF-NEW ' '                
                    K482-NUM-REGN ' = ' K482-CDE-CONTRA-TAG.                    
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '* ------------ BP13C41I ------------- * '.                  
           DISPLAY 'NO OF REC READ    BP13K480 = ' WS-K480-READ.                
           DISPLAY 'NO OF REC READ    BP13F482 = ' WS-F482-READ.                
           DISPLAY 'NO OF REC READ    BP13K482 = ' WS-K482-READ.                
           DISPLAY 'NO OF REC REWRITE BP13K482 = ' WS-K482-REWRITE.             
                                                                                
           CLOSE BP13K480                                                       
                 BP13F482                                                       
                 BP13K482.                                                      
                                                                                
           IF WS-K482-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K482, STATUS ' WS-K482-STATUS          
              MOVE WS-K482-STATUS        TO RETURN-CODE.                        
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
