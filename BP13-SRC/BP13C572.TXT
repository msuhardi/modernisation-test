       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C572.                                                    
      *AUTHOR. RHB.                                                             
      *DATE-WRITTEN. 11/07/97.                                                  
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *                                                        *                
      *      AT BP13A525 - BEFORE SHORTLIST FOR GRO :                           
      *      1.  TO ADJUST BALANCE FLAT FOR UPGRADER FROM      *                
      *          TCP/MTF  TO GRO (CCC).                        *                
      *                                                        *                
      *      AT BP13ABAL - BEFORE SHORTLIST FOR PA  :                           
      *      2.  TO ADJUST BALANCE FLAT FOR UPGRADER FROM      *                
      *          FRO/REN/RH/RDC TO PA.                         *                
      *                                                        *                
      * INPUT : BP13F712, BP13F713                             *                
      *                                                        *                
      * I-O FILE : BP13K712                                    *                
      *                                                        *                
      * CHG-NO   BY   ON     DESCRIPTION                       *                
      * --------------------------------                       *                
      * BP130341 RHB 11/07/97 NEW PRG                          *                
      * BP135458 SMR 04/08/14 EXPAND BP13K712 TO 350           *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F712   ASSIGN       TO BP13F712.                          
           SELECT BP13F713   ASSIGN       TO BP13F713.                          
                                                                                
           SELECT BP13K712   ASSIGN       TO BP13K712                           
                             ACCESS       IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K712-KEY-FLD                       
                             FILE STATUS  IS WS-K712-STATUS.                    
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F712                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F712.                                                           
                                                                                
       FD   BP13F713                                                            
            RECORD CONTAINS 20 CHARACTERS                                       
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F713.                                                           
                                                                                
       FD   BP13K712                                                            
            RECORD CONTAINS 350 CHARACTERS.                                     
                                                                                
       COPY BP13K712.                                                           
                                                                                
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-K712-STATUS          PIC 9(02) VALUE ZEROES.                      
       01  WS-K712-EOF             PIC X     VALUE SPACE.                       
       01  WS-K712-UPDATE          PIC X     VALUE SPACE.                       
       01  WS-F712-READ-FLD.                                                    
           05  WS-F712-CDE-NT-ZONE    PIC X(3) VALUE SPACES.                    
           05  WS-F712-CDE-FLAT-TY    PIC X(2) VALUE SPACES.                    
       01  WS-CUR-TIME             PIC X(07) VALUE ZEROES.                      
       01  WS-F713-READ            PIC 9(08) VALUE ZEROES.                      
       01  WS-F713-EOF             PIC X     VALUE SPACE.                       
       01  WS-F712-READ            PIC 9(08) VALUE ZEROES.                      
       01  WS-F712-EOF             PIC X     VALUE SPACE.                       
       01  WS-K712-REWRITE         PIC 9(08) VALUE ZEROES.                      
       01  WS-K712-SUCC            PIC 9(08) VALUE ZEROES.                      
       01  WS-K712-UNSUCC          PIC 9(08) VALUE ZEROES.                      
       01  WS-K712-READ            PIC 9(08) VALUE ZEROES.                      
       01  WS-K712-NOT-FOUND       PIC 9(08) VALUE ZEROES.                      
       01  WS-K712-FOUND           PIC X(01) VALUE SPACES.                      
       01  WS-UPDATE               PIC X(01) VALUE SPACES.                      
       01  WS-UPDATE-ALLOC-SCH     PIC X(01) VALUE SPACES.                      
       01  WS-NH                   PIC S9(5) VALUE ZEROES.                      
       01  WS-UG                   PIC S9(5) VALUE ZEROES.                      
       01  WS-BAL-NH               PIC S9(5) VALUE ZEROES.                      
       01  WS-BAL-UG               PIC S9(5) VALUE ZEROES.                      
       01  BAL-NH                  PIC ZZZZ9 VALUE ZEROES.                      
       01  BAL-UG                  PIC ZZZZ9 VALUE ZEROES.                      
                                                                                
           COPY PRIOSCH.                                                        
                                                                                
       01  WS-CUR-DATE.                                                         
           05 WS-YY                PIC 9(02).                                   
           05 WS-MM                PIC 9(02).                                   
           05 WS-DD                PIC 9(02).                                   
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-SYS-CC            PIC 9(02).                                   
           05 WS-SYS-YY            PIC 9(02).                                   
           05 WS-SYS-MM            PIC 9(02).                                   
           05 WS-SYS-DD            PIC 9(02).                                   
                                                                                
       01  WS-EDIT-DATE.                                                        
           05 WS-EDIT-DD           PIC 9(02).                                   
           05 FILLER               PIC X(01) VALUE '/'.                         
           05 WS-EDIT-MM           PIC 9(02).                                   
           05 FILLER               PIC X(01) VALUE '/'.                         
           05 WS-EDIT-YY           PIC 9(02).                                   
                                                                                
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *--------*                                                                
       000-MAIN.                                                                
      *--------*                                                                
                                                                                
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
                                                                                
           PERFORM 200-READ-F712-REC THRU 200-EXIT.                             
                                                                                
           PERFORM 250-READ-F713-REC THRU 250-EXIT.                             
                                                                                
           PERFORM 300-PROCESS-REC   THRU 300-EXIT                              
                   UNTIL WS-F712-EOF = 'Y'.                                     
                                                                                
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       100-OPEN-FILES.                                                          
      *--------------*                                                          
                                                                                
           OPEN INPUT  BP13F712                                                 
                       BP13F713                                                 
                I-O    BP13K712.                                                
                                                                                
           IF WS-K712-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'K712 OPEN ERROR '                                        
              DISPLAY 'FILE STATUS IS :' WS-K712-STATUS                         
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                           
           END-IF.                                                              
                                                                                
           ACCEPT WS-CUR-DATE FROM DATE.                                        
                                                                                
           MOVE WS-YY   TO WS-EDIT-YY  WS-SYS-YY                                
           MOVE WS-MM   TO WS-EDIT-MM  WS-SYS-MM                                
           MOVE WS-DD   TO WS-EDIT-DD  WS-SYS-DD.                               
                                                                                
           IF WS-SYS-YY > 93                                                    
              MOVE  19  TO WS-SYS-CC                                            
           ELSE                                                                 
              MOVE  20  TO WS-SYS-CC                                            
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       200-READ-F712-REC.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13F712 AT END                                                 
                MOVE 'Y' TO WS-F712-EOF                                         
                GO TO 200-EXIT.                                                 
           ADD 1 TO WS-F712-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       250-READ-F713-REC.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13F713 AT END                                                 
                MOVE 'Y' TO WS-F713-EOF                                         
                GO TO 250-EXIT.                                                 
           ADD 1 TO WS-F713-READ.                                               
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *==============================================*                          
      *       DOCUMENTATION                          *                          
      *==============================================*                          
                                                                                
      *---------------*                                                         
       300-PROCESS-REC.                                                         
      *---------------*                                                         
                                                                                
           MOVE ZEROES TO WS-NH                                                 
                          WS-UG                                                 
                          WS-BAL-NH                                             
                          WS-BAL-UG.                                            
                                                                                
           MOVE F712-CDE-NT-ZONE TO WS-F712-CDE-NT-ZONE.                        
           MOVE F712-CDE-FLAT-TY TO WS-F712-CDE-FLAT-TY.                        
                                                                                
           PERFORM 320-PROCESS-BAL-NHHUPG THRU 320-EXIT                         
               UNTIL F712-CDE-NT-ZONE NOT = WS-F712-CDE-NT-ZONE OR              
                     F712-CDE-FLAT-TY NOT = WS-F712-CDE-FLAT-TY OR              
                     WS-F712-EOF = 'Y'.                                         
                                                                                
           IF F713-CDE-COHORT = 'CC'                                            
               IF WS-BAL-NH > 0 OR WS-BAL-UG > 0                                
                  PERFORM 350-ADD-UPG-TO-CCC THRU 350-EXIT.                     
                                                                                
           IF F713-CDE-COHORT = 'BA'                                            
                  PERFORM 380-ADJUST-UPG-TO-PA THRU 380-EXIT.                   
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       320-PROCESS-BAL-NHHUPG.                                                  
                                                                                
           EVALUATE F713-CDE-COHORT                                             
           WHEN 'CC'                                                            
              IF F712-CDE-ALLOC-SCH = 'MTF' OR 'TCP'                            
                    MOVE 'N' TO WS-K712-UPDATE                                  
                    COMPUTE WS-NH = F712-NUM-FLAT-NH - F712-NUM-REG-NH          
                    COMPUTE WS-UG = F712-NUM-FLAT-UG - F712-NUM-REG-UG          
                    IF WS-NH > 0                                                
                       COMPUTE WS-BAL-NH = WS-BAL-NH + WS-NH                    
                    END-IF                                                      
                    IF WS-UG > 0                                                
                    COMPUTE WS-BAL-UG = WS-BAL-UG + WS-UG                       
                    END-IF                                                      
              END-IF                                                            
                                                                                
           WHEN 'BA'                                                            
              IF F712-CDE-ALLOC-SCH = 'RH ' OR 'RDC' OR 'REN'                   
                    COMPUTE WS-NH = F712-NUM-FLAT-NH - F712-NUM-REG-NH          
                    COMPUTE WS-UG = F712-NUM-FLAT-UG - F712-NUM-REG-UG          
                    IF WS-NH > 0                                                
                        COMPUTE WS-BAL-NH = WS-BAL-NH + WS-NH                   
                    END-IF                                                      
                    IF WS-UG > 0                                                
                        COMPUTE WS-BAL-UG = WS-BAL-UG + WS-UG                   
                    END-IF                                                      
              END-IF                                                            
              IF F712-CDE-ALLOC-SCH = 'CCC'                                     
                    COMPUTE WS-NH = F712-NUM-FLAT-NH - F712-NUM-REG-NH          
                    COMPUTE WS-UG = F712-NUM-FLAT-UG - F712-NUM-REG-UG          
                    COMPUTE WS-BAL-NH = WS-BAL-NH + WS-NH                       
                    COMPUTE WS-BAL-UG = WS-BAL-UG + WS-UG                       
              END-IF                                                            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'INVALID BP13K713-REC : ' F713-CDE-COHORT                 
              GO TO 900-CLOSE-ROUTINE                                           
              MOVE 'Y' TO WS-F712-EOF                                           
           END-EVALUATE.                                                        
                                                                                
           MOVE WS-BAL-NH TO BAL-NH.                                            
           MOVE WS-BAL-UG TO BAL-UG.                                            
                                                                                
           PERFORM 200-READ-F712-REC THRU 200-EXIT.                             
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       350-ADD-UPG-TO-CCC.                                                      
      *-----------------*                                                       
                                                                                
           MOVE SPACES               TO K712-KEY-FLD.                           
           MOVE WS-F712-CDE-NT-ZONE  TO K712-CDE-NT-ZONE.                       
           MOVE WS-F712-CDE-FLAT-TY  TO K712-CDE-FLAT-TY.                       
           MOVE 'CC'                 TO K712-CDE-COHORT.                        
           MOVE 'CCC'                TO K712-CDE-ALLOC-SCH.                     
           PERFORM 400-READ-BP13K712 THRU 400-EXIT.                             
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       380-ADJUST-UPG-TO-PA.                                                    
      *-----------------*                                                       
                                                                                
            MOVE SPACES               TO K712-KEY-FLD.                          
            MOVE WS-F712-CDE-NT-ZONE  TO K712-CDE-NT-ZONE.                      
            MOVE WS-F712-CDE-FLAT-TY  TO K712-CDE-FLAT-TY.                      
            MOVE 'PA'                 TO K712-CDE-COHORT.                       
            MOVE 'PA '                TO K712-CDE-ALLOC-SCH.                    
            PERFORM 400-READ-BP13K712 THRU 400-EXIT.                            
                                                                                
       380-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       400-READ-BP13K712.                                                       
      *-----------------*                                                       
                                                                                
           MOVE 'Y'                  TO WS-K712-FOUND.                          
           READ  BP13K712.                                                      
           EVALUATE WS-K712-STATUS                                              
           WHEN 00                                                              
                ADD  1         TO WS-K712-READ                                  
                COMPUTE K712-NUM-FLAT-NH = K712-NUM-FLAT-NH + WS-BAL-NH         
                COMPUTE K712-NUM-FLAT-UG = K712-NUM-FLAT-UG + WS-BAL-UG         
                COMPUTE K712-NUM-TOT-UNIT = K712-NUM-TOT-UNIT +                 
                                            WS-BAL-NH         +                 
                                            WS-BAL-UG                           
                PERFORM 430-UPDATE-K712  THRU 430-EXIT                          
           WHEN 23                                                              
                MOVE 'N'       TO WS-K712-FOUND                                 
                DISPLAY 'K712-FILE STATUS IS  : ' WS-K712-STATUS                
           WHEN OTHER                                                           
                MOVE 'N'       TO WS-K712-FOUND                                 
                DISPLAY 'ERROR READING K712  : ' WS-K712-STATUS                 
                GO TO 900-CLOSE-ROUTINE                                         
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
       430-UPDATE-K712.                                                         
           REWRITE BP13K712-REC.                                                
           ADD 1 TO WS-K712-REWRITE.                                            
       430-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       900-CLOSE-ROUTINE.                                                       
      *-----------------*                                                       
                                                                                
           ACCEPT WS-CUR-DATE FROM DATE.                                        
                                                                                
           DISPLAY 'PROGRAM-ID : BP13C572 '.                                    
           DISPLAY '**********************'.                                    
           DISPLAY ' '.                                                         
           DISPLAY 'CURR-DATE  : ' WS-CUR-DATE.                                 
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM F712   ' WS-F712-READ.              
           DISPLAY 'NO OF RECORDS FOUND IN  K712   ' WS-K712-READ.              
           DISPLAY 'NO OF RECORDS NOT FOUND K712   ' WS-K712-NOT-FOUND.         
           DISPLAY 'NO OF RECORDS RE-WRITTEN K712  ' WS-K712-REWRITE.           
                                                                                
           CLOSE BP13K712                                                       
                 BP13F713                                                       
                 BP13F712.                                                      
                                                                                
           IF WS-K712-STATUS EQUAL 00 OR 97                                     
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'K712 CLOSE ERROR '                                       
              DISPLAY 'FILE STATUS IS : ' WS-K712-STATUS                        
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
