  /****************************************************************/            
  /*  SYSTEM OF COMMITMENT  (SOC)                                 */            
  /*==============================================================*/            
  /*  OBJECTIVES : TAB THE RESULT OF BALLOTING ON THE UPGRADER    */            
  /*               AND NEW HH FOR BOOKING SYSTEM                  */            
  /*--------------------------------------------------------------*/            
  /* CHG REF  DATE     BY   DESCRIPTION                           */            
  /* -------- -------- ---- -----------                           */            
  /* BP132235 18022003 ANC1 NEW PGM                               */            
  /* BP132322 04032003 ANC1 ADD BTO & WIS 4RM                     */            
  /* BP132353 05062003 ANC1 ADD RFS CASES                         */            
  /* BP132384 01082003 ANC1 CAPTURE EXISTING/EX-DP FLAT TYPE      */            
  /*                        FROM BP13F800 FILE                    */            
  /* BP133203 03122007 SSS2 CHANGE REPORT TITLE                   */            
  /****************************************************************/            
  PROC FORMAT;                                                                  
                                                                                
   VALUE $HH_FMT  "G1"="UPG EXISTING LESSEES IN SMS     "                       
                  "G2"="UPG EXISTING RESALE FLAT LESSEES"                       
                  "G3"="UPG NON-LESSEES                 "                       
                  "H1"="NHH EXISTING RESALE FLAT LESSEES"                       
                  "H2"="NHH EX-RESALE FLAT LESSEES      "                       
                  "NT"="TENANTS   "                                             
                  "O "="OCCUPIERS "                                             
                  "OT"="OTHERS    ";                                            
                                                                                
   VALUE $RM_FMT  "1 ","01"="1R"                                                
                  "2 ","02"="2R"                                                
                  "3 ","03"="3R"                                                
                  "4 ","04"="4R"                                                
                  "5 ","05"="5R"                                                
                  "6 ","06"="EF"                                                
                  "7 ","07"="OTHERS"                                            
                  ;                                                             
                                                                                
   VALUE $FT_FMT  "1"="1R"                                                      
                  "2"="2R"                                                      
                  "3"="3R"                                                      
                  "4"="4R"                                                      
                  "5"="5R"                                                      
                  "E","6"="EF"                                                  
                  ;                                                             
                                                                                
   %GLOBAL DATE TITLE3;                                                         
   OPTIONS NODATE;                                                              
                                                                                
   DATA EXIST_GP EX_GP EX_GP1;                                                  
   INFILE BP13UPG END=EOF;                                                      
   INPUT @4    REG_NO   $CHAR8.                                                 
         @30   RM_REQ   $CHAR1.                                                 
         @51   NT       $CHAR3.                                                 
         @57   ALLOSCH  $CHAR3.                                                 
         @82   INCOME   5.                                                      
         @155  SCHACCT  $CHAR11.                                                
         @525  HH_CDE   $CHAR1.                                                 
         @785  DTE_BAL  $CHAR6.                                                 
         @924  SELTYP   $CHAR3.                                                 
         @1099 SMS_ST   $CHAR1.                                                 
         @1100 SMS_FT   $CHAR2.                                                 
         @1102 RSL_ST   $CHAR1.                                                 
         @1103 RSL_FT   $CHAR2.                                                 
         @1105 RDB_ST   $CHAR1.                                                 
         @1106 RDB_FT   $CHAR2.                                                 
         ;                                                                      
                                                                                
   IF _N_ = 1 THEN                                                              
      DO;                                                                       
        CUR_DTE=PUT(TODAY(),DDMMYY8.);                                          
        CALL SYMPUT('DATE',CUR_DTE);                                            
      END;                                                                      
                                                                                
   IF SCHACCT ^= " ";                                                           
                                                                                
   IF SMS_ST="D" THEN                                                           
   DO;                                                                          
      CDE_TYPE="G1";                                                            
      RM_RES=SMS_FT;                                                            
   END;                                                                         
   ELSE                                                                         
   IF SMS_ST="M" AND RSL_ST="D" THEN                                            
   DO;                                                                          
      CDE_TYPE="G2";                                                            
      RM_RES=RSL_FT;                                                            
   END;                                                                         
   ELSE                                                                         
   IF SMS_ST=" " AND RSL_ST="D" THEN                                            
   DO;                                                                          
      CDE_TYPE="G3";                                                            
      RM_RES=RSL_FT;                                                            
   END;                                                                         
   ELSE                                                                         
   IF SMS_ST="M" THEN                                                           
   DO;                                                                          
      CDE_TYPE="H1";                                                            
      RM_RES=SMS_FT;                                                            
   END;                                                                         
   ELSE                                                                         
   IF SMS_ST=" " AND RSL_ST="M" THEN                                            
   DO;                                                                          
      CDE_TYPE="H2";                                                            
      RM_RES=RSL_FT;                                                            
   END;                                                                         
   ELSE                                                                         
   IF SMS_ST=" " AND RSL_ST=" " AND RDB_ST="T" THEN                             
   DO;                                                                          
      CDE_TYPE="NT";                                                            
      RM_RES=RDB_FT;                                                            
   END;                                                                         
   ELSE                                                                         
   IF SMS_ST=" " AND RSL_ST=" " AND RDB_ST="O" THEN                             
   DO;                                                                          
      CDE_TYPE="O ";                                                            
      RM_RES=RDB_FT;                                                            
   END;                                                                         
   ELSE                                                                         
   IF SMS_ST=" " AND RSL_ST=" " AND RDB_ST=" " THEN                             
   DO;                                                                          
      CDE_TYPE="OT";                                                            
      RM_RES=RDB_FT;                                                            
   END;                                                                         
   ELSE                                                                         
   DO;                                                                          
      CDE_TYPE="OT";                                                            
      RM_RES=RDB_FT;                                                            
   END;                                                                         
                                                                                
   IF RM_RES="0E" OR                                                            
      RM_RES="6 " OR                                                            
      RM_RES="06" OR                                                            
      RM_RES="E " THEN                                                          
      RM_RES="06";                                                              
   IF RM_RES="0H" OR                                                            
      RM_RES="H " OR                                                            
      RM_RES="0G" OR                                                            
      RM_RES="  " THEN                                                          
      RM_RES="07";                                                              
                                                                                
   LENGTH  TITLE $40.;                                                          
   IF SELTYP="BE1" THEN TITLE="JAN 02 BALLOTING EXERCISE";                      
   ELSE                                                                         
   IF SELTYP="BE2" THEN TITLE="NOV 02 BALLOTING EXERCISE";                      
   ELSE                                                                         
   IF SELTYP="WIS" THEN TITLE="WALK IN SELECTION EXERCISE";                     
   ELSE                                                                         
   IF SELTYP="BTO" THEN TITLE="BUILD-TO-ORDER EXERCISE";                        
   ELSE                                                                         
   IF SELTYP="WI4" THEN TITLE="WALK IN SELECTION EXERCISE(4RM)";                
   ELSE                                                                         
   IF SELTYP="RFS" THEN TITLE="RFS SELECTION EXERCISE";                         
                                                                                
   CALL SYMPUT('TITLE3',TITLE);                                                 
                                                                                
   IF HH_CDE="H" OR HH_CDE="T" THEN OUTPUT EXIST_GP;                            
   ELSE                                                                         
   IF HH_CDE="G" OR HH_CDE="3" THEN OUTPUT EX_GP;                               
   ELSE                                                                         
     OUTPUT EX_GP1;                                                             
                                                                                
   RUN;                                                                         
                                                                                
   /*------------------------------------------------*/                         
   /*    TABULATE ON EXISTING FLAT INFORMATION       */                         
   /*------------------------------------------------*/                         
                                                                                
   OPTIONS MISSING=0 NOCENTER;                                                  
   PROC PRINTTO UNIT=18 NEW;                                                    
   PROC TABULATE DATA=EXIST_GP MISSING FORMAT=7.;                               
   TITLE1 "P13L092A                                SYSTEM OF COMMITMENT         
                                             PAGE : ";                          
   TITLE2 "                        EXISTING FLAT TYPE OF FIRST-TIMER APPLICANTS 
WHO BOOKED FLATS                        DATE : &DATE";                          
   TITLE3 "                                       &TITLE3                       
                                                    ";                          
                                                                                
   CLASS DTE_BAL HH_CDE CDE_TYPE RM_REQ RM_RES ;                                
                                                                                
   FORMAT RM_REQ   $FT_FMT.                                                     
          RM_RES   $RM_FMT.                                                     
          CDE_TYPE $HH_FMT.;                                                    
                                                                                
   KEYLABEL N  =" "                                                             
            ALL="TOTAL";                                                        
                                                                                
   TABLE DTE_BAL="DATE BALLOT: ",(CDE_TYPE=" "*                                 
         (RM_REQ="FLAT TYPE BOOKED" ALL="TOTAL") ALL),                          
         (N="EXISTING FLAT TYPE"*RM_RES=" ") ALL                                
         /RTS=25 BOX="CATEGORY OF FT APPLICANTS";                               
                                                                                
   /*------------------------------------------------*/                         
   /*    TABULATE ON EX-RESALE FLAT INFORMATION      */                         
   /*------------------------------------------------*/                         
                                                                                
   PROC TABULATE DATA=EX_GP MISSING FORMAT=7.;                                  
   TITLE1 "P13L092B                                SYSTEM OF COMMITMENT         
                                             PAGE : ";                          
   TITLE2 "                       EXISTING FLAT TYPE OF SECOND-TIMER APPLICANTS 
WHO BOOKED FLATS                        DATE : &DATE";                          
   TITLE3 "                                       &TITLE3                       
                                                    ";                          
                                                                                
                                                                                
   CLASS DTE_BAL HH_CDE CDE_TYPE RM_REQ RM_RES ;                                
                                                                                
   FORMAT RM_REQ   $FT_FMT.                                                     
          RM_RES   $RM_FMT.                                                     
          CDE_TYPE $HH_FMT.;                                                    
                                                                                
   KEYLABEL N  =" "                                                             
            ALL="TOTAL";                                                        
                                                                                
   TABLE DTE_BAL="DATE BALLOT: ", (CDE_TYPE=" "*                                
         (RM_REQ="FLAT TYPE BOOKED" ALL="TOTAL") ALL),                          
         (N="EXISTING FLAT TYPE"*RM_RES=" ") ALL                                
         /RTS=25 BOX="CATEGORY OF 2T APPLICANTS";                               
                                                                                
   /*------------------------------------------------*/                         
   /*    TABULATE ON EX-RESALE FLAT INFORMATION      */                         
   /*------------------------------------------------*/                         
                                                                                
   PROC TABULATE DATA=EX_GP1 MISSING FORMAT=7.;                                 
   TITLE1 "P13L092C                               SYSTEM  OF  COMMITMENT        
                                             PAGE : ";                          
   TITLE2 "                        EXISTING FLAT TYPE OF NO HH TYPE APPLICANTS  
WHO BOOKED FLATS                        DATE : &DATE";                          
   TITLE3 "                                       &TITLE3                       
                                                    ";                          
                                                                                
   CLASS DTE_BAL HH_CDE CDE_TYPE RM_REQ RM_RES ;                                
                                                                                
   FORMAT RM_REQ   $FT_FMT.                                                     
          RM_RES   $RM_FMT.                                                     
          CDE_TYPE $HH_FMT.;                                                    
                                                                                
   KEYLABEL N  =" "                                                             
            ALL="TOTAL";                                                        
                                                                                
   TABLE DTE_BAL="DATE BALLOT: ",(CDE_TYPE=" "*                                 
         (RM_REQ="FLAT TYPE BOOKED" ALL="TOTAL") ALL),                          
         (N="EXISTING FLAT TYPE"*RM_RES=" ") ALL                                
         /RTS=25 BOX="CATEGORY OF NO HH TYPE APPLICANTS";                       
                                                                                
   PROC PRINT DATA=EX_GP;                                                       
      VAR REG_NO NT ALLOSCH RM_REQ INCOME DTE_BAL SMS_ST SMS_FT                 
          RSL_ST RSL_FT RDB_ST RDB_FT;                                          
                                                                                
