      *--------------------------------------------------------*                
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------*                
       PROGRAM-ID.   BP13C797.                                                  
      *AUTHOR.       SK.                                                        
      *DATE-WRITTEN. 13-01-2004.                                                
      *--------------------------------------------------------*                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES  : TO MATCH BP13F200 & BP13K740          *                
      *                                                        *                
      *    INPUT FILES  : BP13F200                             *                
      *                   BP13K740.                            *                
      *    OUTPUT FILES : BP13F740.                            *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE      DETAILS                         *                
      *-------- ---- --------  --------------------------------*                
      *BP132463 SK   13012004  NEW PGM                         *                
      *BP132973 CKK  02112006  REMOVE CHECKING ON INTERNET APPL*                
      *BP134521 ESA1 03052012  CATER FOR MGPS                  *                
      *--------------------------------------------------------*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
                                                                                
           SELECT BP13K740 ASSIGN TO BP13K740                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K740-KEY-FLD                               
                  ALTERNATE KEY   IS K740-NUM-REGN                              
                  FILE STATUS     IS WS-K740-STATUS.                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
                                                                                
       FD   BP13K740                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
                                                                                
       COPY BP13K740.                                                           
                                                                                
       FD   BP13F200                                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F200.                                                           
                                                                                
       FD   BP13F740                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F740.                                                           
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
                                                                                
       01  WORK-AREAS.                                                          
           05 WS-READ-F200              PIC 9(8)    VALUE 0.                    
           05 WS-F200-EOF               PIC X(1)    VALUE SPACES.               
           05 WS-K740-EOF               PIC X(1)    VALUE SPACES.               
           05 WS-WRITE-F740             PIC 9(8)    VALUE 0.                    
                                                                                
           05 WS-NOTFND                 PIC 9(8)    VALUE 0.                    
           05 WS-K740-STATUS            PIC 9(2)    VALUE 00.                   
           05 WS-K740-FOUND             PIC X(1)    VALUE SPACES.               
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY            PIC 9(4).                                 
           05  WS-SYS-MM              PIC 9(2).                                 
           05  WS-SYS-DD              PIC 9(2).                                 
                                                                                
       01  WS-EDITED-DATE             PIC X(10).                                
                                                                                
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
      *--------------------------------------------------------*                
       0000-MAIN.                                                               
      *--------------------------------------------------------*                
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F200     THRU 2000-EXIT.                           
                                                                                
           PERFORM 3000-PROCESS       THRU 3000-EXIT                            
                   UNTIL WS-F200-EOF = 'Y'.                                     
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILES.                                                         
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  BP13F200                                                 
                       BP13K740                                                 
           OUTPUT      BP13F740.                                                
                                                                                
           EVALUATE WS-K740-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K740-STATUS  TO RETURN-CODE                            
                 DISPLAY 'OPEN BP13K740 FAIL. STATUS=' WS-K740-STATUS           
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-READ-F200.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F200                                                        
                AT END                                                          
                   MOVE 'Y' TO WS-F200-EOF                                      
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F200                                        
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3000-PROCESS.                                                            
      *--------------------------------------------------------*                
           MOVE 'N'                   TO WS-K740-EOF.                           
           PERFORM 3500-START-K740    THRU 3500-EXIT.                           
           PERFORM 3600-READNXT-K740  THRU 3600-EXIT                            
              UNTIL WS-K740-EOF = 'Y'                                           
                                                                                
           PERFORM 2000-READ-F200  THRU 2000-EXIT.                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3500-START-K740.                                                         
      *------------------------------------------------------------*            
           MOVE SPACES                     TO K740-NUM-REGN.                    
           MOVE F200-NUM-REGN              TO K740-NUM-REGN.                    
                                                                                
           START BP13K740   KEY >= K740-NUM-REGN                                
           IF WS-K740-STATUS NOT = 00                                           
              DISPLAY 'START ERROR WITH BP13K740. STATUS IS - '                 
                       WS-K740-STATUS                                           
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3600-READNXT-K740.                                                       
      *------------------------------------------------------------*            
           READ BP13K740 NEXT                                                   
              AT END   MOVE 'Y'  TO WS-K740-EOF.                                
                                                                                
           EVALUATE WS-K740-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 IF F200-NUM-REGN = K740-NUM-REGN                               
                    PERFORM 4000-WRITE-F740 THRU 4000-EXIT                      
                 ELSE                                                           
                    MOVE 'Y'   TO   WS-K740-EOF                                 
                 END-IF                                                         
              WHEN 23                                                           
                 ADD 1     TO WS-NOTFND                                         
                 MOVE 'Y'  TO WS-K740-EOF                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BP13K740, STATUS = '                   
                         WS-K740-STATUS                                         
                PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-WRITE-F740.                                                         
      *--------------------------------------------------------*                
                                                                                
           IF (K740-CDE-SUCC-NT-ZONE NOT = SPACES AND LOW-VALUES )              
                                                                                
              MOVE SPACES        TO   BP13F740-REC                              
              INITIALIZE              BP13F740-REC                              
                                                                                
              WRITE BP13F740-REC FROM BP13K740-REC                              
              ADD 1 TO WS-WRITE-F740                                            
                                                                                
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           DISPLAY '*********** BP13C797 **************'.                       
           DISPLAY SPACES.                                                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECORDS READ    :'.                                   
           DISPLAY '   BP13F200           : '  WS-READ-F200.                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECORDS WRITE   : '.                                  
           DISPLAY '   BP13F740           : '  WS-WRITE-F740.                   
           DISPLAY SPACES.                                                      
           DISPLAY 'NOT FOUND ON BP13K740 : '  WS-NOTFND.                       
           DISPLAY '***********************************'.                       
                                                                                
           CLOSE BP13K740                                                       
                 BP13F200                                                       
                 BP13F740.                                                      
                                                                                
           EVALUATE WS-K740-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K740-STATUS  TO RETURN-CODE                            
                 DISPLAY 'CLOSE BP13K740 FAIL. STATUS=' WS-K740-STATUS          
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
