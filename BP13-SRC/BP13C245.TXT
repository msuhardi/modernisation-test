       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C245.                                                 
      *AUTHOR.        JOEL E PARRENO.                                           
      *DATE-WRITTEN.  03/24/94.                                                 
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      * OBJECTIVE    :  1. PRINT INVITATION LETTERS TO APPLICANT    *           
      *                    INVITING THEM FOR A SELECTION FOR SERS   *           
      *                                                             *           
      * INPUT FILES  :  1.  BP13F205 - CONTROL RECORDS FILE         *           
      *                 2.  BP13K060 - CODE TRANSCRIPTION FILE      *           
      *                 3.  BP13K200 - BOOKING APPOINTMENT FILE     *           
      *                 4.  BP13K800 - SOC APPLICATION MASTER       *           
      *                 5.  BP13K820 - SOC OCCUPIER FILE            *           
      *                 6.  BP13K830 - SOC CORR ADDRESS FILE        *           
      *                                                             *           
      * OUTPUT LISTS :  1.  P13L245A - INVITATION LETTER            *           
      *                 2.  P13L245D - INVITATION LETTER (COPY)     *           
      *                 3.  P13L245B - CONTROL LIST                 *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  OIC  DATE    DESCRIPTION                           *           
      * -------- ---  ------  ------------------------------------- *           
      * CXXXXXX  LSB  020787  MT-JB POLICY CHANGE                   *           
      * C890401  YCH  200489  CHG CLAUSE FOR RESETTLEMENT CASE      *           
      * CXXXXXX  LME  100789  CHANGE LAYOUT OF BN06NAME             *           
      *                       READ P10K060 FOR NEW TOWN NAME        *           
      * C900893  LSB  030990  PRINT BOOKING FEE CLAUSE              *           
      * C901161  LSB  061290  PRINT ADDITION CLAUSE FOR URO         *           
      * C901227  LSB  311290  CHG FOR STEADY STAGE, DELETE BM06K440 *           
      * C910576  LSB  300591  TAKE OUT SELECTION COUNT CLAUSE       *           
      * C910859  LSB  300891  REFORMAT LETTERS & INCL BOOKING FEE   *           
      *                       FOR 2 ROOM                            *           
      * C920205  LSB  140292  ADDITIONAL CLAUSE FOR DEBARMENT       *           
      *                       $1000, EXCLUDE RHO/REN/URO CASES      *           
      * C9208C4  RAZ  240892  INCL CD CLAUSE & SELECT FORM TYPE     *           
      * C920883  YPH  080992  INCL D&B TYPE FLAT                    *           
      * C930144  JK   010393  ADDITIONAL 'CC TO ' CLAUSE            *           
      * C930642  LMS  090693  CATER FOR RDC CASES                   *           
      * I920718  LMS  290693  CONVERT TO COBOL II                   *           
      * SOC-PH8  TON  130793  RETRIEVE ADDRESS FROM BP13K830        *           
      * SOC-PH8  RML  200893  REFER TO BP13K800/BP13K820/BP13K060   *           
      * SOC-PH8  JK   200394  CORRECTION FOR REGN-NO                *           
      * SOC-PH8  JEP  240394  CHG BM06CTL  TO  BP13F205             *           
      *                       CHG BM06K010 TO  BP13K200             *           
      *                       (BM06640A RENAMED TO BP13C245)        *           
      * C940936  BAL  090994  TAKE OUT DEBARMENT FILE (BP13K542)    *           
      *                       ADD 'COPY' ON THE DUPLICATE COPY   *              
      * SOC-PH9  GD   071194  CATER FOR FLATS GROUPED BY ZONE       *           
      *                       (F205-NUM-NT-ZONE-SW = 'Z')           *           
      * SOC-PH9  GD   291194  CHG BP13K200 TO BP13F200              *           
      * C9504B5  GD   180495  CHG FORM FORMAT FOR THE FOLLOWING :   *           
      *                       .MATURE NT (W DEBAR) - FORMID BP13C4  *           
      *                       .JB CASES FOR ZONE  - FORMID BP13C5   *           
      *                       .NON-JB CASES FOR ZONE - FORMID BP13C6*           
      * C9504B5  GD   190595  CHG PRINTING OF ZONE TEXT             *           
      * C9504B5  GD   190595  INCL NEW LETTER:                      *           
      *                       .BP13C7- TENANT SCH FOR ZONE          *           
      *                       .BP13C8- MATURE NT (W/O DEBAR)        *           
      * C9504B5  GD   240595  PUT BACK DEBARMENT FILE (BP13K542)    *           
      * C9505C5  GD   240595  INCL NEW LETTER:                      *           
      *                       .BP13CA- 3RM FLAT (REOPT)             *           
      *                       .BP13CB- 3RM FLAT (1ST TIMER)         *           
      *                       .BP13CC- 3RM FLAT (TENANT)            *           
      * C9506F9  LMS  250895  INCL DECODING OF HHTY FOR CGO.        *           
      * BP130029 LMS  070995  CHG CD FOR 3 RM TO 5%.                *           
      *                       (EXCEPT FOR JSS SCHEME).              *           
      * BP130052 LMS  081295  CHG CD FORM 3 RM WITH ELIG            *           
      *                       SCH = 'OR ' TO 20%.                   *           
      * BP130127 LJL  300496  CHG FORM BP13C6 TO BP13CD.            *           
      * BP130127 SIA  140596  ALIGMENT FOR FORM BP13CA, CB & CC.    *           
      * BP130164 LMS  310796  INCL FORM BP13CE FOR SERS.            *           
      * BP130196 LMS  051096  CHG IN FORM BP13CE, ADD FORM BP13CF   *           
      * BP130237 SIA  301296  INCL QUEUE NO                         *           
      * BP130297 LMS  190597  INCL CUR-REGN                         *           
      * BP130297 LMS  240597  INCL NUM-SEQUENCE, REMOVE CUR-REGN    *           
      * BP130330 LMS  230697  CATER FOR JB IN SERS                  *           
      * BP130367 SSS  040897  ALIGNMENT FOR FORM BP13CB, BP13CC     *           
      * BP130385 LMS  081097  ALIGNMENT FOR FORM BP13C5, BP13C6     *           
      * BP130385 LMS  081097  ALIGNMENT FOR FORM BP13CB             *           
      * BP130385 LMS  111097  ALIGNMENT FOR CC COPY                 *           
      * BP130488 SIA  080598  CATER FOR OCS AT BOOKING              *           
      * BP130488 SIA  200798  NEW FORM BP13CN, BP13CP               *           
      * BP130488 LMS  220798  NEW FORM BP13CQ, BP13CR               *           
      * BP130556 SSS  270898  REMOVE FORM BP13CQ & BP13CR           *           
      * BP130418 LMS  170998  CHG IN BP13K800 AND BP13K820          *           
      * BP130488 LMS  141198  ALIGNMENT FOR FORM BP13CA             *           
      * BP130606 LMS  091298  CATER FOR 3-RM REN.                   *           
      * BP130609 LMS  161298  CATER FOR E-FLAT 1998/10.             *           
      * BP130618 SRN  120498  READ BP13K555 TO GET OFFICER NAME.    *           
      * BP130618 SRN  100399  INCL BP13CT.                          *           
      * BP130618 SRN  240399  ALIGNMENT FOR BP13CE                  *           
      * BP130618 LMS  070499  ALIGNMENT FOR BP13CL                  *           
      * BP130673 LMS  070699  CATER FOR 2-ROOM RE.                  *           
      * BP130676 LMS  140699  PRINT ONLY FORM BP13CA FOR 3-ROOM.    *           
      * BP130701 SSS  220699  USE FORM BP13CM INSTEAD OF BP13CK     *           
      * BP130677 MGLR 130599  MOVE 4 BYTES OF BALLOT YEAR           *           
      * BP130677 MGLR 190599  INCL CENTURY TO LETTER DATE AND       *           
      *                       APPOINTMENT DATE.                     *           
      * BP130768 GBR  071299  INCL NEW LETTER:                      *           
      *                       .BP13CV- NON-MATURE FOR ZONE=ZN       *           
      *                       .BP13CU- MATURE FOR ZONE=ME           *           
      * BP130768 LMS  231299  ALIGN BP13CU AND BP13CV.              *           
      *                       HANDLE EOF OF BP13K278.               *           
      * BP130790 SCK  020200  ADD IN IN-SOURCE DOCUMENTATION        *           
      * BP130867 LMS  300500  PRINT BP13CQ FOR RE 2-ROOM.           *           
      * BP130950 GBR  081100  ALIGNMENT FOR FORMS BP13C5, BP13C7,   *           
      *                       BP13CD, BP13CL, BP13CI & BP13CJ.      *           
      * BP130863 LMS  301100  REVISED FORMAT FOR BP13CP AND BP13CR. *           
      * BP131009 JAM  230401  PRINT SECOND QUARTER INVITATION       *           
      *                       LETTERS FOR THE FF: FORMS:            *           
      *                                BP13CJ, BP13CI, BP13CL,      *           
      *                                BP13C5, BP13C7, BP13CD,      *           
      *                                BP13CN, BP13CP, BP13CQ,      *           
      *                                BP13CR                       *           
      *                    1. HARD CODE SCHEDULE DATE & SHOW FLAT   *           
      *                       DATES FOR 2ND QTR, 2001               *           
      *                    2. CHG PRINT FORMAT FOR RM TT & LOCATION *           
      *                    3. CHG PRINT FORMAT FOR ALLOWING PRINTING*           
      *                       OF 2 SHOW FLAT DATE IN A SINGLE FORM. *           
      * BP131018 LMS  110501  CATER FOR ALLOCATION SCHEME "RED".    *           
      * BP131036 RAS  140601  CATER FOR ALLOC CODES: SFC AND SRC    *           
      * BP131059 ED   270701  PRINT APPLICATION INVITATION LETTER   *           
      *                       FOR BUILD-TO-ORDER (BTO) CASES.       *           
      * BP132031 MALC 031001  CATER FOR SALE OF SERS FLAT TO SINGLES*           
      * BP132036 LMS  161001  PRINT LETTER BP13CQ FOR REN AND RED 2 *           
      *                       AND 3-ROOM FLATS.                     *           
      * BP132045 JAM  131101  PRINT DESIGNATION OF OIC              *           
      * BP132094 MTD  070202  PRINT BP13CA, BP13CB AND BP13CC       *           
      *                       FOR NUM-ALLO-CAT = 'BE'               *           
      * BP132095 MTD  200302  SPLIT LETTER PRINTING INTO SUBROUTINE *           
      * BP132276 JF4  070103  CATER FOR CHANGE OF BALLOT MONTH      *           
      *                       PRINT ADDRESS TO L245B.               *           
      * BP132293 JF4  270103  CATER FOR PRINTING OF APPMNT LETTER   *           
      *                       FOR BE RESERVE LIST.                  *           
      * BP132360 JF4  290703  CHG CHECKING FOR BE CASES TO PRINT    *           
      *                       INVITATION FOR PA AND MCPS CASES.     *           
      *                       MOVE ALL 20 CHARS OF NT/ZONE NAME TO  *           
      *                       C245-NT-ZONE-NAME FOR 'BE' CASES.     *           
      * BP132482 JF4  130204  CATER FOR PRINTING OF BTO REOPT CASES *           
      * BP132500 JF4  290304  CATER FOR PRINTING OF INVITATION LTR  *           
      *                       FOR SALES UNDER SERS.                 *           
      * BP132500 MTD  050504  ADD LOAN TAG TO C245-PARAMETERS       *           
      * BP132560 LMS  210704  NEW FORM                              *           
      * BP132560 LMS  090904  CATER FOR RESERVE LIST                *           
      * BP132582 LMS  300904  CATER FOR FERNVALE GROVE              *           
      * BP132627 SSS2 301104  REMOVE READING K542                   *           
      * BP132626 LMS  111204  CATER FOR BE (10/2004)                *           
      * BP132670 LMS  230205  CATER FOR CORAL GREEN                 *           
      * BP132693 GAN  270505  INCL PRINTING OF BALLOT MO/YR FOR     *           
      *                       LETTER TYPE F /FORM R4                *           
      * BP132708 GAN1 070905  USE OF SERS TABLE (SERSCH)            *           
      * BP133085 SSS2 150607  DELETE OBSOLETE LETTERS               *           
      * BP133085 SSS2 220607  DELETE SA LETTER                      *           
      * BP133085 SSS2 240707  DEL CALLING C297                      *           
      * BP133610 ESA1 280409  TO REPLACE BP13K767 W/ BP13K816       *           
      *                       TO CATER FOR SELECTION TYPE = 'BTO'   *           
      * BP133703 ESA1 100909  TO REMOVE COPY                        *           
      * BP133816 ESA1 210110  TO ADD PRINTING OF REGN & DATE IN     *           
      *                       ANNEX 1                               *           
      *               290110  TO CATER FOR SERS ONLY                *           
      * BP134755 ESA1 060613  TO CATER FOR WS-C245-NUM-ALLO-CAT     *           
      * BP135068 ESA1 041013  TO CATER FOR SERS UNAPPROVED CASES RPT*           
      * BP135177 ESA1 160114  TO UPDATE READING OF BP17K580         *           
      * BP135533 ESA1 131114  TO CATER FOR ALLOC SCHEMES            *           
      *                       'REG' & 'RNG'                         *           
      * BP136074 ESA1 280116  TO ADD CALLING OF BP13C20N FOR SERS   *           
      * BP136199 ESA1 140416  TO CALL BP13C20N FOR REN LETTERS      *           
      * =========================================================== *           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F205  ASSIGN       TO BP13F205.                           
           SELECT BP13F200  ASSIGN       TO BP13F200.                           
                                                                                
           SELECT BP13K060  ASSIGN       TO BP13K060                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K060-KEY-FLD                        
                            FILE STATUS  IS BP13K060-STATUS.                    
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS BP13K800-STATUS.                    
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS BP13K820-STATUS.                    
                                                                                
           SELECT BP13K830  ASSIGN       TO BP13K830                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K830-KEY-FLD                        
                            ALTERNATE RECORD KEY IS  K830-NUM-REGN              
                            FILE STATUS  IS BP13K830-STATUS.                    
                                                                                
           SELECT BP13K278  ASSIGN       TO BP13K278                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K278-KEY-FLD                        
                            FILE STATUS  IS BP13K278-STATUS.                    
                                                                                
           SELECT BP13K555  ASSIGN       TO BP13K555                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K555-KEY-FLD                        
                            FILE STATUS  IS BP13K555-STATUS.                    
                                                                                
           SELECT BP17K000  ASSIGN       TO BP17K000                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K000-KEY-FLD                        
                            ALTERNATE KEY IS K000-NUM-REGNO                     
                            FILE STATUS  IS BP17K000-STATUS.                    
                                                                                
           SELECT BP17K580  ASSIGN       TO BP17K580                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K580-APPRV-KEY-FLD                  
                            FILE STATUS  IS BP17K580-STATUS.                    
                                                                                
           SELECT P13L245B  ASSIGN       TO P13L245B.                           
           SELECT P13F245A  ASSIGN       TO P13F245A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F205                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            BLOCK CONTAINS  0   RECORDS                                         
            RECORD CONTAINS 80  CHARACTERS.                                     
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F200                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            BLOCK CONTAINS  0   RECORDS                                         
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25  CHARACTERS.                                     
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13K278                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K278.                                                           
                                                                                
       FD   BP13K555                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K555.                                                           
                                                                                
       FD   BP17K000                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP17K000.                                                           
                                                                                
       FD   BP17K580                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP17K580.                                                           
                                                                                
       FD   P13L245B                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   P13L245B-REC                PIC X(132).                             
                                                                                
       FD   P13F245A                                                            
            RECORD CONTAINS 70 CHARACTERS                                       
            RECORDING MODE  IS F.                                               
       01   P13F245A-REC.                                                       
            05 F245A-REGN-NO        PIC X(08).                                  
            05 F245A-SERS-SCH-ACC   PIC X(09).                                  
            05 F245A-SERS-FT-ADDR   PIC X(50).                                  
            05 FILLER               PIC X(03).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-INDICATORS.                                                       
           05  BP13K060-STATUS          PIC 99     VALUE 00.                    
           05  BP13K800-STATUS          PIC 99     VALUE 00.                    
           05  BP13K820-STATUS          PIC 99     VALUE 00.                    
           05  BP13K830-STATUS          PIC 99     VALUE 00.                    
           05  BP13K278-STATUS          PIC 99     VALUE 00.                    
           05  BP13K555-STATUS          PIC 99     VALUE 00.                    
           05  BP17K000-STATUS          PIC 99     VALUE 00.                    
           05  BP17K580-STATUS          PIC 99     VALUE 00.                    
           05  WS-EOF-F205              PIC X      VALUE 'N'.                   
           05  WS-EOF-F200              PIC X      VALUE 'N'.                   
           05  WS-EOF-K278              PIC X      VALUE 'N'.                   
           05  WS-K580-FND              PIC X      VALUE 'N'.                   
           05  WS-K580-EOF              PIC X      VALUE 'N'.                   
           05  WS-K278-FOUND            PIC X      VALUE 'N'.                   
           05  WS-PRINT-TOT-IND         PIC X      VALUE 'N'.                   
                                                                                
       01  WS-COUNTERS-POINTERS.                                                
           05  WS-CNT-READ-F200         PIC 9(6)   VALUE ZEROS.                 
           05  WS-CNT-LETTER            PIC 9(6)   VALUE ZEROS.                 
           05  WS-CNT-RH-LETTER         PIC 9(6)   VALUE ZEROS.                 
           05  WS-CNT-WRITE-F245A       PIC 9(6)   VALUE ZEROS.                 
           05  WS-CNT-DETAIL            PIC 9(2)   VALUE 99.                    
           05  WS-CNT-PAGENO            PIC 9(5)   VALUE ZEROS.                 
           05  WS-PTR                   PIC 9(2)   VALUE ZEROS.                 
                                                                                
       01  WS-MATCH-KEYS.                                                       
           05  WS-F200-MATCH-KEY.                                               
               10  WS-F200-DTE-ALLOC    PIC X(8).                               
               10  WS-F200-NUM-NT-ZONE  PIC X(3).                               
               10  WS-F200-NUM-FT       PIC X(2).                               
           05  WS-F205-MATCH-KEY.                                               
               10  WS-F205-DTE-ALLOCN   PIC X(8).                               
               10  WS-F205-NUM-NT-ZONE  PIC X(3).                               
               10  WS-F205-NUM-FT       PIC X(2).                               
                                                                                
           05  WS-NUM-ZONE              PIC X(3) VALUE SPACES.                  
                                                                                
       01  WS-PARSING.                                                          
           05 WS-STRING-TO-PARSE            PIC X(70).                          
           05 WS-PARSED-STRING              PIC X(50).                          
           05 WS-STRING-LENGTH              PIC 9(03).                          
           05 WS-CUR-POS-SP                 PIC 9(03).                          
           05 WS-CUR-POS-PS                 PIC 9(03).                          
           05 WS-SPACE-FLAG                 PIC X    VALUE 'N'.                 
           05 WS-INSERT-SPACE-FLAG          PIC X    VALUE 'N'.                 
                                                                                
       01  WS-SCHEDULE-RECS.                                                    
           05 FILLER                    PIC X(51)  VALUE                        
              '200105062001051401/05/2001 - 06/05/2001'.                        
           05 FILLER                    PIC X(51)  VALUE                        
              '200105152001052007/05/2001 - 13/05/2001'.                        
           05 FILLER                    PIC X(51)  VALUE                        
              '200105212001052707/05/01-13/05/01,26/05/01-27/05/01'.            
           05 FILLER                    PIC X(51)  VALUE                        
              '200105282001060326/05/01-27/05/01,01/06/01-03/06/01'.            
           05 FILLER                    PIC X(51)  VALUE                        
              '200106042001061001/06/2001 - 10/06/2001'.                        
           05 FILLER                    PIC X(51)  VALUE                        
              '200106112001061704/06/2001 - 10/06/2001'.                        
           05 FILLER                    PIC X(51)  VALUE                        
              '200106182001062411/06/2001 - 17/06/2001'.                        
           05 FILLER                    PIC X(51)  VALUE                        
              '200106242001070218/06/2001 - 24/06/2001'.                        
           05 FILLER                    PIC X(51)  VALUE                        
              '200107012001071625/06/2001 - 30/06/2001'.                        
                                                                                
       01  WS-SCHEDULE-TABLE   REDEFINES  WS-SCHEDULE-RECS.                     
           02  WS-SCHEDULE-DTE          OCCURS 09 TIMES                         
                                        INDEXED BY WS-SCH.                      
               05 WS-START-DTE          PIC X(08).                              
               05 WS-END-DTE            PIC X(08).                              
               05 WS-SHOW-DTE           PIC X(35).                              
                                                                                
       01  WS-OTHER-VARIABLES.                                                  
           05  WS-NAME                  PIC X(66)  VALUE SPACES.                
           05  WS-NT-ZONE-NAME          PIC X(20)  VALUE SPACES.                
           05  FILLER                   REDEFINES  WS-NT-ZONE-NAME.             
               10  WS-TEXT-IN           PIC X(3).                               
               10  WS-ZONE-NAME         PIC X(17).                              
                                                                                
           05  WS-ALLOC-SCH.                                                    
               10  FILLER               PIC XX     VALUE ' ('.                  
               10  WS-AL-SCH            PIC X(3)   VALUE SPACES.                
               10  FILLER               PIC XX     VALUE ') '.                  
                                                                                
           05  WS-CURRENT-DATE.                                                 
               10  WS-CURRENT-CCYY      PIC X(4)   VALUE SPACES.                
               10  WS-CURRENT-MM        PIC X(2)   VALUE SPACES.                
               10  WS-CURRENT-DD        PIC X(2)   VALUE SPACES.                
                                                                                
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD         PIC 99     VALUE 00.                    
               10  FILLER               PIC X      VALUE '/'.                   
               10  WS-EDITED-MM         PIC 99     VALUE 00.                    
               10  FILLER               PIC X      VALUE '/'.                   
               10  WS-EDITED-CCYY       PIC 9999   VALUE 00.                    
                                                                                
           05  WS-COMP-DATE.                                                    
               10  WS-COMP-CCYY         PIC 9999   VALUE 00.                    
               10  WS-COMP-MM           PIC 99     VALUE 00.                    
               10  WS-COMP-DD           PIC 99     VALUE 00.                    
                                                                                
           05  WS-APPMT-TIME.                                                   
               10  WS-APPMT-HR          PIC 9(2)   VALUE 00.                    
               10  WS-APPMT-MIN         PIC 9(2)   VALUE 00.                    
                                                                                
       01  SW-SERS-ALLO-SCHEME          PIC X(01) VALUE SPACES.                 
           88  C-SERS-ALLO-SCHEME       VALUE 'Y'.                              
                                                                                
       01  WS-ROOM-TABLE.                                                       
           05  ROOM-1                   PIC X(30)  VALUE                        
               '1 1 ROOM FLAT                 '.                                
           05  ROOM-2                   PIC X(30)  VALUE                        
               '2 2 ROOM FLAT                 '.                                
           05  ROOM-2                   PIC X(30)  VALUE                        
               '2F2 ROOM FLEXI FLAT           '.                                
           05  ROOM-3                   PIC X(30)  VALUE                        
               '3 3 ROOM FLAT                 '.                                
           05  ROOM-4                   PIC X(30)  VALUE                        
               '4 4 ROOM FLAT                 '.                                
           05  ROOM-4S                  PIC X(30)  VALUE                        
               '4S4 ROOM SIMPLIFIED           '.                                
           05  ROOM-4D                  PIC X(30)  VALUE                        
               '4DDESIGN & BUILD TYPE A       '.                                
           05  ROOM-5                   PIC X(30)  VALUE                        
               '5 5 ROOM FLAT                 '.                                
           05  ROOM-5D                  PIC X(30)  VALUE                        
               '5DDESIGN & BUILD TYPE B       '.                                
           05  ROOM-E                   PIC X(30)  VALUE                        
               'E EXECUTIVE FLAT              '.                                
           05  ROOM-ED                  PIC X(30)  VALUE                        
               'EDDESIGN & BUILD TYPE C       '.                                
       01  WS-ROOM-TABLE-DES  REDEFINES WS-ROOM-TABLE.                          
           05  WS-ROOM-VALUE  OCCURS 11 TIMES INDEXED BY ROOM-PTR.              
               10  ROOM-CD              PIC XX.                                 
               10  ROOM-DES             PIC X(28).                              
                                                                                
       COPY SERSCH.                                                             
                                                                                
      *-----------------------------------------------------------*             
      *       PARAMETERS FOR CALLING SUBROUTINES                  *             
      *-----------------------------------------------------------*             
       01  WS-F200-RECORD                 PIC X(300).                           
       COPY P13SC245.                                                           
                                                                                
      *---------------------------------------------------------------*         
      *        LINKAGE FOR BP13C913                                   *         
      *---------------------------------------------------------------*         
        01  WS-LINK-REC.                                                        
            05  WS-LINK-NUM-SCH-ACC.                                            
                10  WS-LINK-NUM-SCH   PIC X(4).                                 
                10  WS-LINK-NUM-ACC   PIC X(5).                                 
        COPY P13COMM8.                                                          
                                                                                
      *-----------------------------------------------------------*             
      *       LAYOUT FOR CONTROL REGISTER                         *             
      *-----------------------------------------------------------*             
       01  WS-L245B-PRT-VARS.                                                   
           05  L245B-PRT-HDG-1.                                                 
               10  FILLER               PIC X(9)   VALUE '  PAGE : '.           
               10  L245B-PAGE           PIC ZZZZ9.                              
               10  FILLER               PIC X(29)  VALUE SPACES.                
               10  FILLER               PIC X(32)  VALUE                        
                   'SYSTEM OF COMMITMENT'.                                      
               10  FILLER               PIC X(30)  VALUE SPACES.                
               10  L245B-DATE           PIC X(10)  VALUE SPACES.                
               10  FILLER               PIC X(14)  VALUE SPACES.                
                                                                                
           05  L245B-PRT-HDG-2.                                                 
               10  FILLER               PIC X(33)  VALUE SPACES.                
               10  FILLER               PIC X(64)  VALUE                        
                  'CONTROL LIST FOR PRINTING INVITATION LETTERS'.               
                                                                                
           05  L245B-PRT-HDG-3.                                                 
               10  FILLER               PIC X(46)  VALUE                        
                   '             REGN-NO           ALLO  MT-JB '.               
               10  FILLER               PIC X(60)  VALUE                        
                   ' APPLN-NAME             ADDRESS  '.                         
                                                                                
           05  L245B-PRT-HDG-4.                                                 
               10  FILLER               PIC X(46)  VALUE                        
                   '                               SCH   CODE'.                 
               10  FILLER               PIC X(60)  VALUE                        
                   ' COAPPLN-NAME'.                                             
                                                                                
           05  L245B-PRT-HDG-5.                                                 
               10  L245B-PRT-HDG-T.                                             
                   15  L245B-NT             PIC X(3) VALUE SPACES.              
                   15  L245B-ROOM           PIC X(2) VALUE SPACES.              
                   15  L245B-SLASH-1        PIC X(1) VALUE SPACE.               
                   15  L245B-REGN-NO        PIC X(8) VALUE SPACES.              
                   15  FILLER               PIC X(17) VALUE SPACES.             
               10  L245B-PRT-HDG-Z          REDEFINES  L245B-PRT-HDG-T.         
                   15  L245B-ZONE-Z         PIC X(3).                           
                   15  L245B-FLAT-TYPE-Z    PIC X(2).                           
                   15  L245B-SLASH-1-Z      PIC X(1).                           
                   15  L245B-HT-Z           PIC X(1).                           
                   15  L245B-SLASH-2-Z      PIC X(1).                           
                   15  L245B-SEQ-NO-Z       PIC X(7).                           
                   15  L245B-SLASH-3-Z      PIC X(1).                           
                   15  L245B-REGN-NO-Z      PIC X(8).                           
                   15  FILLER               PIC X(7).                           
               10  L245B-ALLOC-SCH      PIC X(3)   VALUE SPACES.                
               10  FILLER               PIC X(4)   VALUE SPACES.                
               10  L245B-JB-CD          PIC X(1)   VALUE SPACE.                 
               10  FILLER               PIC X(7)   VALUE SPACES.                
               10  L245B-APPL-NAME      PIC X(22)  VALUE SPACES.                
               10  FILLER               PIC X(1)   VALUE SPACE.                 
               10  L245B-BLK-NO         PIC X(10)  VALUE SPACES.                
               10  FILLER               PIC X(2)   VALUE SPACES.                
               10  L245B-NUM-FLAT.                                              
                   15  L245B-CHAR       PIC X      VALUE SPACE.                 
                   15  L245B-LEVEL      PIC X(2)   VALUE SPACES.                
                   15  L245B-DASH       PIC X      VALUE SPACE.                 
                   15  L245B-HSE-MAIN   PIC X(4)   VALUE SPACES.                
                   15  L245B-HSE-SUB    PIC X(3)   VALUE SPACES.                
                                                                                
           05  L245B-PRT-HDG-6.                                                 
               10  FILLER               PIC X(46)  VALUE SPACES.                
               10  L245B-COAPPL-NAME    PIC X(22)  VALUE SPACES.                
               10  FILLER               PIC X      VALUE SPACES.                
               10  L245B-STREET-NAME    PIC X(65)  VALUE SPACES.                
                                                                                
           05  L245B-PRT-HDG-7.                                                 
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  FILLER               PIC X(35)  VALUE                        
                   'NO OF BP13F200 READ             = '.                        
               10  L245B-READ-F200      PIC ZZZZZ9.                             
                                                                                
           05  L245B-PRT-HDG-8.                                                 
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  FILLER               PIC X(35)  VALUE                        
                   'TOTAL PA/RH LETTERS PRINTED     = '.                        
               10  L245B-TOT-LETTER     PIC ZZZZZ9.                             
                                                                                
           05  L245B-PRT-HDG-9.                                                 
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  FILLER               PIC X(35)  VALUE                        
                   'ADDITIONAL RH LETTERS PRINTED   = '.                        
               10  L245B-RH-LETTER      PIC ZZZZZ9.                             
                                                                                
           05  L245B-PRT-HDG-10.                                                
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  FILLER               PIC X(35)  VALUE                        
                   'TOTAL NUMBER OF UNAPPROVED CASE = '.                        
               10  L245B-WRITE-F245A    PIC ZZZZZ9.                             
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-FILES-ROUTINE   THRU  1000-EXIT.                  
            PERFORM 2000-INITIALIZE-ROUTINE   THRU  2000-EXIT.                  
            PERFORM 3000-READ-BP13F205-FILE   THRU  3000-EXIT.                  
            PERFORM 4000-PROCESS-CONTROL-REC  THRU  4000-EXIT                   
                         UNTIL WS-EOF-F205 =  'Y'.                              
            PERFORM 8000-PRINT-TOTAL          THRU  8000-EXIT.                  
            PERFORM 9000-CLOSE-FILES-ROUTINE  THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES-ROUTINE.                                                 
      *-------------------------------------------------------------            
            OPEN INPUT  BP13F205                                                
                        BP13F200                                                
                        BP13K060                                                
                        BP13K800                                                
                        BP13K820                                                
                        BP13K830                                                
                        BP13K278                                                
                        BP13K555                                                
                        BP17K000                                                
                        BP17K580                                                
                 OUTPUT P13L245B                                                
                        P13F245A.                                               
                                                                                
           IF BP13K060-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K060-STATUS ' BP13K060-STATUS         
              MOVE     BP13K060-STATUS  TO  RETURN-CODE                         
              PERFORM  9000-CLOSE-FILES-ROUTINE.                                
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K800-STATUS ' BP13K800-STATUS         
              MOVE     BP13K800-STATUS  TO  RETURN-CODE                         
              PERFORM  9000-CLOSE-FILES-ROUTINE.                                
                                                                                
           IF BP13K820-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K820-STATUS ' BP13K820-STATUS         
              MOVE     BP13K820-STATUS TO RETURN-CODE                           
              PERFORM  9000-CLOSE-FILES-ROUTINE.                                
                                                                                
           IF BP13K830-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K830-STATUS ' BP13K830-STATUS         
              MOVE     BP13K830-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-FILES-ROUTINE.                                
                                                                                
           IF BP13K278-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K278-STATUS ' BP13K278-STATUS         
              MOVE     BP13K278-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-FILES-ROUTINE.                                
                                                                                
           IF BP17K000-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP17K000-STATUS ' BP17K000-STATUS         
              MOVE     BP17K000-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-FILES-ROUTINE.                                
                                                                                
           IF BP17K580-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP17K580-STATUS ' BP17K580-STATUS         
              MOVE     BP17K580-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-FILES-ROUTINE.                                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-INITIALIZE-ROUTINE.                                                 
      *-------------------------------------------------------------            
            MOVE ZEROS  TO WS-CNT-READ-F200                                     
                           WS-CNT-LETTER                                        
                           WS-CNT-RH-LETTER                                     
                           WS-CNT-PAGENO.                                       
            MOVE 99     TO WS-CNT-DETAIL.                                       
            MOVE 'N'    TO WS-EOF-F205                                          
                           WS-EOF-F200.                                         
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3000-READ-BP13F205-FILE.                                                 
      *-------------------------------------------------------------            
            READ BP13F205                                                       
                 AT END MOVE 'Y' TO WS-EOF-F205                                 
                 GO TO  3000-EXIT.                                              
                                                                                
            MOVE F205-DTE-ALLOCN    TO WS-F205-DTE-ALLOCN.                      
            MOVE F205-NUM-NT-ZONE   TO WS-F205-NUM-NT-ZONE.                     
            MOVE F205-NUM-FLAT-TYPE TO WS-F205-NUM-FT.                          
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-PROCESS-CONTROL-REC.                                                
      *-------------------------------------------------------------            
            PERFORM  4100-READ-BP13F200       THRU    4100-EXIT                 
              UNTIL  WS-F200-MATCH-KEY >= WS-F205-MATCH-KEY                     
                     OR WS-EOF-F200 = 'Y'.                                      
                                                                                
            PERFORM  6000-PROCESS-ALLOC-PREP  THRU   6000-EXIT                  
              UNTIL  WS-F200-MATCH-KEY NOT = WS-F205-MATCH-KEY                  
                 OR  WS-EOF-F200 = 'Y'.                                         
                                                                                
            PERFORM  3000-READ-BP13F205-FILE  THRU   3000-EXIT.                 
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       4100-READ-BP13F200.                                                      
      *-------------------------------------------------------------            
            READ BP13F200                                                       
               AT END   MOVE  'Y'  TO  WS-EOF-F200                              
               GO TO    4100-EXIT                                               
            END-READ.                                                           
                                                                                
            MOVE F200-DTE-ALLOC     TO  WS-F200-DTE-ALLOC.                      
            MOVE F200-NUM-NT-ZONE   TO  WS-F200-NUM-NT-ZONE.                    
            MOVE F200-NUM-FLAT-TYPE TO  WS-F200-NUM-FT.                         
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       6000-PROCESS-ALLOC-PREP.                                                 
      *-------------------------------------------------------------            
            ADD  1                 TO  WS-CNT-READ-F200.                        
                                                                                
            MOVE SPACES            TO  WS-C245-PARAMETERS.                      
                                                                                
            IF WS-CNT-READ-F200 = 1                                             
               MOVE 'Y'            TO  WS-C245-FIRST-FLAG                       
            ELSE                                                                
               MOVE 'N'            TO  WS-C245-FIRST-FLAG                       
            END-IF.                                                             
                                                                                
            STRING F205-DTE-REQUEST(7:2) '/'                                    
                   F205-DTE-REQUEST(5:2) '/'                                    
                   F205-DTE-REQUEST(1:4)                                        
                   DELIMITED BY SIZE INTO WS-C245-PRT-DATE                      
            END-STRING.                                                         
                                                                                
            PERFORM 6100-READ-BP13K060-FILE       THRU   6100-EXIT.             
            PERFORM 6200-READ-BP13K800-FILE       THRU   6200-EXIT.             
                                                                                
            IF BP13K800-STATUS = 00                                             
               PERFORM  6300-GET-OCCUPIER-NAMES   THRU   6300-EXIT              
               PERFORM  6400-PRINT-LETTER         THRU   6400-EXIT              
               PERFORM  7000-PROCESS-PRINT        THRU   7000-EXIT              
            END-IF.                                                             
                                                                                
            PERFORM 4100-READ-BP13F200            THRU   4100-EXIT.             
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       6100-READ-BP13K060-FILE.                                                 
      *-------------------------------------------------------------            
      *     READ THE CODE TAB FILE (BP13K060 ) TO GET THE FULL                  
      *     DESCRIPTION OF NEW TOWN/ZONE NAME.                                  
      *-------------------------------------------------------------            
            MOVE SPACES         TO WS-NT-ZONE-NAME.                             
            IF F205-NUM-NT-ZONE-SW = 'T'                                        
               MOVE 02          TO K060-SERIAL-NO                               
            ELSE                                                                
            IF F205-NUM-NT-ZONE-SW = 'Z'                                        
               MOVE 45          TO K060-SERIAL-NO                               
            END-IF.                                                             
                                                                                
            MOVE F200-NUM-NT-ZONE TO K060-CODE.                                 
            READ BP13K060.                                                      
                                                                                
            IF BP13K060-STATUS = 00                                             
               MOVE K060-DESC TO WS-NT-ZONE-NAME                                
            ELSE                                                                
            IF BP13K060-STATUS NOT = 23                                         
               DISPLAY 'RECORD NOTFND IN BP13K060 = ' BP13K060-STATUS           
                       ', F200-NUM-NT-ZONE = ' F200-NUM-NT-ZONE                 
            ELSE                                                                
               DISPLAY 'ERROR READING BP13K060, STAT = ' BP13K060-STATUS        
                       ', K060-KEY-FLD = ' K060-KEY-FLD.                        
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       6200-READ-BP13K800-FILE.                                                 
      *-------------------------------------------------------------            
            MOVE SPACES        TO WS-C245-BO.                                   
            MOVE F200-NUM-REGN TO K800-NUM-REGN.                                
                                                                                
            READ BP13K800.                                                      
                                                                                
            IF BP13K800-STATUS  =  00                                           
               MOVE K800-NUM-BO        TO WS-C245-BO                            
               MOVE K800-NUM-REGN      TO WS-C245-REGN-NO                       
               MOVE K800-NUM-ALLO-CAT  TO WS-C245-NUM-ALLO-CAT                  
               MOVE K800-NUM-SALE-BROCHURE-SCPY                                 
                                       TO WS-C245-BROCHURE-SCPY                 
            ELSE                                                                
               DISPLAY 'REC NOTFND IN BP13K800 = ' BP13K800-STATUS              
                       ', K800-NUM-REGN = ' K800-NUM-REGN.                      
                                                                                
       6200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       6300-GET-OCCUPIER-NAMES.                                                 
      *-------------------------------------------------------------            
            MOVE    K800-NUM-NRIC1          TO   K820-NUM-NRIC                  
                                                 WS-C245-APPLN-NRIC.            
            PERFORM 6310-READ-BP13K820-FILE THRU 6310-EXIT.                     
            MOVE    WS-NAME                 TO   WS-C245-APPLN-NAME.            
                                                                                
            IF K800-NUM-NRIC2 NOT =  SPACES AND  LOW-VALUES                     
               MOVE     K800-NUM-NRIC2       TO  K820-NUM-NRIC                  
                                                 WS-C245-COAPPLN-NRIC           
               PERFORM  6310-READ-BP13K820-FILE  THRU 6310-EXIT                 
               MOVE     WS-NAME              TO  WS-C245-COAPPLN-NAME           
            ELSE                                                                
               MOVE     SPACES               TO  WS-C245-COAPPLN-NAME           
            END-IF.                                                             
                                                                                
            IF K800-NUM-NRIC3 NOT =  SPACES AND  LOW-VALUES                     
               MOVE     K800-NUM-NRIC3       TO  K820-NUM-NRIC                  
                                                 WS-C245-COAPPLN2-NRIC          
               PERFORM  6310-READ-BP13K820-FILE  THRU 6310-EXIT                 
               MOVE     WS-NAME              TO  WS-C245-COAPPLN2-NAME          
            ELSE                                                                
               MOVE     SPACES               TO  WS-C245-COAPPLN2-NAME          
            END-IF.                                                             
                                                                                
            IF K800-NUM-NRIC4 NOT =  SPACES AND  LOW-VALUES                     
               MOVE     K800-NUM-NRIC4       TO  K820-NUM-NRIC                  
                                                 WS-C245-COAPPLN3-NRIC          
               PERFORM  6310-READ-BP13K820-FILE  THRU 6310-EXIT                 
               MOVE     WS-NAME              TO  WS-C245-COAPPLN3-NAME          
            ELSE                                                                
               MOVE     SPACES               TO  WS-C245-COAPPLN3-NAME          
            END-IF.                                                             
                                                                                
       6300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       6310-READ-BP13K820-FILE.                                                 
      *-------------------------------------------------------------            
      *     USE F200-NUM-REGN & NRIC (1-2) TO ACCESS SOC OCCU-                  
      *     PIER MASTER FILE (BP13K820).                                        
      *-------------------------------------------------------------            
            MOVE SPACES        TO WS-C245-TITLE.                                
            MOVE SPACES        TO WS-NAME.                                      
            MOVE K800-NUM-REGN TO K820-NUM-REGN.                                
            READ BP13K820.                                                      
                                                                                
            IF BP13K820-STATUS = 00                                             
               MOVE    SPACES                   TO    WS-C245-TITLE             
               MOVE     K820-NME-OCCP           TO    WS-NAME                   
            ELSE                                                                
            IF BP13K820-STATUS = 23                                             
               DISPLAY 'REC NOTFND IN BP13K820 = ' BP13K820-STATUS              
                       ', K820-KEY-FLD = ' K820-KEY-FLD                         
            ELSE                                                                
               DISPLAY 'ERROR READING BP13K820, STAT = ' BP13K820-STATUS        
                       ', K820-KEY-FLD = ' K820-KEY-FLD.                        
                                                                                
       6310-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       6400-PRINT-LETTER.                                                       
      *-------------------------------------------------------------            
           IF F200-NUM-REGN NOT = SPACES                                        
              PERFORM  6410-MOVE-VAL-TO-PARMS   THRU   6410-EXIT                
              PERFORM  6600-CALL-PRINT-SUBR     THRU   6600-EXIT                
           END-IF.                                                              
                                                                                
       6400-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       6410-MOVE-VAL-TO-PARMS.                                                  
      *-------------------------------------------------------------            
      *-------------------------------------------------------------            
      *    GET INFORMATION FOR PRINTING LETTER TO PUBLIC CASES                  
      *-------------------------------------------------------------            
           MOVE 'SR'                  TO K555-NUM-ZONE-NT                       
                                         K555-NUM-FLAT-TYPE.                    
                                                                                
           PERFORM 6500-READ-BP13K555-FILE  THRU 6500-EXIT.                     
                                                                                
           MOVE K800-NUM-ALLO-SCHEME  TO WS-C245-NUM-ALLO-SCHEME.               
                                                                                
      *--------> ROOM                                                           
           IF F205-NUM-NT-ZONE-SW = 'T'                                         
              IF F200-NUM-FLAT-TYPE = 'SE'                                      
                 MOVE  F200-NUM-ACTUAL-FT        TO  WS-C245-ROOM               
              ELSE                                                              
                 MOVE  F200-NUM-FLAT-TYPE        TO  WS-C245-ROOM               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F205-NUM-NT-ZONE-SW = 'Z'                                         
              MOVE F200-NUM-FLAT-TYPE  TO WS-C245-ROOM                          
           END-IF.                                                              
                                                                                
      *-------> HT                                                              
           EVALUATE F200-NUM-HHTY-QUEUE-ALPHA                                   
             WHEN 'H'                                                           
                  MOVE 'F' TO WS-C245-HT                                        
             WHEN 'G'                                                           
                  MOVE 'U' TO WS-C245-HT                                        
             WHEN 'C'                                                           
                  MOVE 'G' TO WS-C245-HT                                        
             WHEN OTHER                                                         
                  MOVE F200-NUM-HHTY-QUEUE-ALPHA TO WS-C245-HT                  
           END-EVALUATE.                                                        
                                                                                
      *-------> Q-NO                                                            
           IF (F205-NUM-NT-ZONE   = 'WW' OR 'NE' OR 'NN') AND                   
              (F200-NUM-FLAT-TYPE = 'E')                  AND                   
              (F200-DTE-BALLOT >= '199810')                                     
              MOVE SPACES            TO WS-C245-Q-NO                            
           ELSE                                                                 
              MOVE F200-NUM-HHTY-QUEUE-SERIAL TO WS-C245-Q-NO                   
           END-IF.                                                              
                                                                                
      *------> ALLOC-SCH                                                        
           PERFORM 8800-CHK-SERS-ALSCH  THRU 8800-EXIT.                         
           IF C-SERS-ALLO-SCHEME OR K800-NUM-ALLO-SCHEME = 'PA '                
              MOVE SPACES               TO WS-C245-ALLOC-SCH                    
            ELSE                                                                
              MOVE K800-NUM-ALLO-SCHEME TO WS-AL-SCH                            
              MOVE WS-ALLOC-SCH         TO WS-C245-ALLOC-SCH.                   
                                                                                
      *------> ALLOC-DATE                                                       
            MOVE  F200-DTE-ALLOC  TO   WS-COMP-DATE.                            
                                                                                
            IF WS-COMP-DD NOT >   10                                            
               ADD      30      TO     WS-COMP-DD                               
               SUBTRACT 1       FROM   WS-COMP-MM                               
               IF WS-COMP-MM = 0                                                
                  MOVE  12      TO   WS-COMP-MM                                 
                  SUBTRACT 1    FROM WS-COMP-CCYY                               
               END-IF                                                           
            END-IF.                                                             
                                                                                
            SUBTRACT 10              FROM  WS-COMP-DD.                          
            MOVE     WS-COMP-CCYY    TO    WS-EDITED-CCYY.                      
            MOVE     WS-COMP-MM      TO    WS-EDITED-MM.                        
            MOVE     WS-COMP-DD      TO    WS-EDITED-DD.                        
            MOVE     WS-EDITED-DATE  TO    WS-C245-ALLOC-DATE.                  
                                                                                
      *-----> SELECT-ROOM                                                       
            IF F200-NUM-FLAT-TYPE = 'SE'                                        
               SET ROOM-PTR TO 1                                                
               SEARCH WS-ROOM-VALUE                                             
                   AT END MOVE SPACES TO WS-C245-SELECT-ROOM                    
                      WHEN F200-NUM-ACTUAL-FT = ROOM-CD (ROOM-PTR)              
                          MOVE ROOM-DES (ROOM-PTR)                              
                            TO WS-C245-SELECT-ROOM                              
            ELSE                                                                
               SET ROOM-PTR TO 1                                                
               SEARCH WS-ROOM-VALUE                                             
                   AT END MOVE SPACES TO WS-C245-SELECT-ROOM                    
                      WHEN F200-NUM-FLAT-TYPE = ROOM-CD (ROOM-PTR)              
                          MOVE ROOM-DES (ROOM-PTR)                              
                            TO WS-C245-SELECT-ROOM                              
            END-IF.                                                             
                                                                                
      *------> ZONE/NT-ZONE-NAME                                                
            IF F205-NUM-NT-ZONE-SW = 'Z'                                        
               IF F200-NUM-NT-ZONE = '3R' OR 'RE' OR 'ME' OR 'ZN'               
                  MOVE SPACES              TO WS-C245-NT-ZONE-NAME              
               ELSE                                                             
                  MOVE 'IN '               TO WS-TEXT-IN                        
                  MOVE K060-DESC           TO WS-ZONE-NAME                      
                  MOVE K060-DESC(1:10)     TO WS-C245-ZONE                      
                  MOVE K060-DESC           TO WS-C245-NT-ZONE-NAME              
               END-IF                                                           
            ELSE                                                                
               MOVE 'IN '               TO WS-TEXT-IN                           
               MOVE K060-DESC           TO WS-ZONE-NAME                         
               MOVE K060-DESC(1:10)     TO WS-C245-ZONE                         
               MOVE K060-DESC           TO WS-C245-NT-ZONE-NAME                 
            END-IF.                                                             
                                                                                
           IF ((K060-DESC(1:8) = 'BIDADARI' OR                                  
                K060-DESC(1:9) = 'TOA PAYOH') AND                               
                F200-DTE-BALLOT >= '201511')                                    
              MOVE 'TOA PAYOH(BIDADARI)'    TO WS-C245-NT-ZONE-NAME             
           END-IF.                                                              
                                                                                
      *------> APPMT DATE/TIME                                                  
            MOVE    F200-DTE-BKAPPMT-DATE  TO  WS-COMP-DATE.                    
            MOVE    WS-COMP-CCYY           TO  WS-EDITED-CCYY.                  
            MOVE    WS-COMP-MM             TO  WS-EDITED-MM.                    
            MOVE    WS-COMP-DD             TO  WS-EDITED-DD.                    
            MOVE    WS-EDITED-DATE         TO  WS-C245-APPMT-DATE.              
            MOVE    F200-TME-BKAPPMT       TO  WS-APPMT-TIME.                   
                                                                                
            IF WS-APPMT-TIME NOT = SPACES                                       
               IF WS-APPMT-HR > 11                                              
                  MOVE 'PM'  TO WS-C245-APPMT-AM-PM                             
               ELSE                                                             
                  MOVE 'AM'  TO WS-C245-APPMT-AM-PM                             
            ELSE                                                                
               MOVE SPACES   TO WS-C245-APPMT-AM-PM.                            
                                                                                
            IF WS-APPMT-TIME NOT = SPACES                                       
               IF WS-APPMT-HR > 12                                              
                  COMPUTE WS-APPMT-HR = WS-APPMT-HR - 12                        
                  MOVE WS-APPMT-HR TO   WS-C245-APPMT-HR                        
               ELSE                                                             
                  MOVE WS-APPMT-HR TO   WS-C245-APPMT-HR                        
            ELSE                                                                
               MOVE    SPACES      TO   WS-C245-APPMT-HR.                       
                                                                                
            MOVE  WS-APPMT-MIN     TO   WS-C245-APPMT-MIN.                      
            MOVE ':'               TO   WS-C245-APPMT-SEP.                      
                                                                                
      *-------> PERCENT DESC/PERCENT/REMAINDER                                  
            IF (K800-NUM-ALLO-SCHEME   =  'MT ' OR 'MTS' OR 'RHA' OR            
               'RHL' OR 'RHO' OR 'RHP' OR 'RHS' OR 'RJO' OR 'RJ1' OR            
               'URA' OR 'URL' OR 'URO' OR 'URP' OR 'URS' OR 'RDC' OR            
               'BCA' OR 'BCE' OR 'BCL' OR 'BCS' OR 'BDC' OR                     
               'REA' OR 'REE' OR 'REN' OR 'RES' OR 'RED' OR                     
               'SMF' OR 'SMS' OR 'MCP' OR 'REG' OR 'RNG')                       
                MOVE ' TEN  '          TO  WS-C245-PERCENT-DESC                 
                MOVE 10                TO  WS-C245-PERCENT                      
                MOVE '90 %'            TO  WS-C245-REMAINDER                    
            ELSE                                                                
            IF (K800-NUM-ALLO-SCHEME   =  'JB ') OR                             
               (K800-NUM-JT-BALLOT-MTF =   1 OR 2 OR 3 OR 5)                    
                MOVE ' TEN  '          TO  WS-C245-PERCENT-DESC                 
                MOVE 10                TO  WS-C245-PERCENT                      
                MOVE '90 %'            TO  WS-C245-REMAINDER                    
            ELSE                                                                
                MOVE 20                TO  WS-C245-PERCENT                      
                MOVE '80 %'            TO  WS-C245-REMAINDER                    
                MOVE 'TWENTY'          TO  WS-C245-PERCENT-DESC.                
                                                                                
                                                                                
           PERFORM 6412-GET-ADDRESS THRU  6412-EXIT.                            
                                                                                
           MOVE K800-NUM-FLAT-TYPE  TO WS-C245-ROOM.                            
                                                                                
           MOVE K800-NUM-JT-BALLOT-MTF    TO WS-C245-NUM-JB-MTF.                
           MOVE K800-NUM-LOW-FLOOR        TO WS-C245-NUM-LOW-FLOOR.             
           MOVE K800-NUM-LOAN-TAG         TO WS-C245-NUM-LOAN-TAG.              
                                                                                
       6410-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       6412-GET-ADDRESS.                                                        
      *-------------------------------------------------------------            
            MOVE SPACES     TO WS-C245-STREET-NAME L245B-STREET-NAME.           
            MOVE F200-NUM-REGN TO K830-NUM-REGN.                                
            READ BP13K830 KEY IS K830-NUM-REGN.                                 
                                                                                
            IF BP13K830-STATUS = 00 OR 02                                       
               IF (K830-NUM-MAIL-ADDR  = SPACES OR LOW-VALUES) OR               
                  ((K830-NUM-MAIL-LVL-CHAR = '#') AND                           
                   (K830-NUM-MAIL-LVL-NUM = SPACES OR LOW-VALUES))              
                  IF K830-NUM-ADDR-TYPE  = 'H'                                  
                     MOVE K830-NUM-BLK         TO WS-C245-BLK-NO                
                     MOVE K830-NME-STREET      TO WS-C245-STREET-NAME           
                     MOVE K830-NUM-LVL-CHAR    TO WS-C245-CHAR                  
                     MOVE K830-NUM-LVL-NUM     TO WS-C245-LEVEL                 
                     MOVE K830-NUM-LVL-DASH    TO WS-C245-DASH                  
                     MOVE K830-NUM-HSEMAIN     TO WS-C245-HSE-MAIN              
                     MOVE K830-NUM-HSESUB      TO WS-C245-HSE-SUB               
                     STRING 'SINGAPORE ' K830-NUM-POSTAL                        
                             DELIMITED BY SIZE INTO WS-C245-CODE                
                  ELSE                                                          
                     MOVE K830-NUM-BLK         TO WS-C245-BLK-NO                
                     MOVE K830-NUM-FLAT        TO WS-C245-NUM-FLAT              
                     MOVE K830-NME-STREET-BLDG TO WS-C245-STREET-NAME           
                     IF K830-NUM-POSTAL = SPACES OR LOW-VALUES OR               
                                          ZEROES                                
                        MOVE SPACES TO WS-C245-CODE                             
                     ELSE                                                       
                        STRING 'SINGAPORE ' K830-NUM-POSTAL                     
                                DELIMITED BY SIZE INTO WS-C245-CODE             
                     END-IF                                                     
                  END-IF                                                        
               ELSE                                                             
                  IF K830-NUM-MAIL-ADDR-TYPE =  'H'                             
                     MOVE K830-NUM-MAIL-BLK      TO WS-C245-BLK-NO              
                     MOVE K830-NME-MAIL-STREET   TO WS-C245-STREET-NAME         
                     MOVE K830-NUM-MAIL-LVL-CHAR TO WS-C245-CHAR                
                     MOVE K830-NUM-MAIL-LVL-NUM  TO WS-C245-LEVEL               
                     MOVE K830-NUM-MAIL-LVL-DASH TO WS-C245-DASH                
                     MOVE K830-NUM-MAIL-HSEMAIN  TO WS-C245-HSE-MAIN            
                     MOVE K830-NUM-MAIL-HSESUB   TO WS-C245-HSE-SUB             
                     STRING 'SINGAPORE '   K830-NUM-MAIL-POSTAL                 
                             DELIMITED BY SIZE INTO WS-C245-CODE                
                  ELSE                                                          
                     MOVE K830-NUM-MAIL-BLK      TO WS-C245-BLK-NO              
                     MOVE K830-NME-MAIL-STR-BLDG TO WS-C245-STREET-NAME         
                     MOVE K830-NUM-MAIL-FLAT     TO WS-C245-NUM-FLAT            
                     STRING 'SINGAPORE '   K830-NUM-MAIL-POSTAL                 
                             DELIMITED BY SIZE INTO WS-C245-CODE                
                  END-IF                                                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF BP13K830-STATUS = 23                                             
               MOVE SPACES  TO WS-C245-STREET-NAME                              
                               WS-C245-BLK-NO                                   
                               WS-C245-NUM-FLAT                                 
                               WS-C245-CODE                                     
               DISPLAY 'RECORD NOTFND IN BP13K830 = '  BP13K830-STATUS          
                       ', K830-NUM-REGN = '  K830-NUM-REGN.                     
                                                                                
            IF BP13K830-STATUS NOT = 00 AND 02 AND 23                           
               MOVE SPACES  TO WS-C245-STREET-NAME                              
                               WS-C245-BLK-NO                                   
                               WS-C245-NUM-FLAT                                 
                               WS-C245-CODE                                     
               DISPLAY 'ERROR READING BP13K830 = ' BP13K830-STATUS              
                       ', K830-NUM-REGN = ' K830-NUM-REGN                       
               GO TO 9000-CLOSE-FILES-ROUTINE.                                  
                                                                                
       6412-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       6500-READ-BP13K555-FILE.                                                 
      *-------------------------------------------------------------            
            READ BP13K555.                                                      
            EVALUATE BP13K555-STATUS                                            
            WHEN 0                                                              
               MOVE K555-NME-OIC           TO WS-C245-NME-OIC                   
               MOVE SPACES                 TO WS-C245-OIC-DESIG                 
               EVALUATE K555-NME-OIC-DESG                                       
               WHEN 'EO  '                                                      
                 MOVE 'ESTATES OFFICER          ' TO WS-C245-OIC-DESIG          
               WHEN 'EEO '                                                      
                 MOVE 'EXECUTIVE ESTATES OFFICER' TO WS-C245-OIC-DESIG          
               WHEN 'SEO '                                                      
                 MOVE 'SENIOR ESTATES OFFICER   ' TO WS-C245-OIC-DESIG          
               WHEN OTHER                                                       
                 MOVE 'OTHER OFFICER            ' TO WS-C245-OIC-DESIG          
               END-EVALUATE                                                     
            WHEN 23                                                             
               DISPLAY 'RECORD NOT FOUND IN BP13K555'                           
                       ', KEY-FLD IS ' K555-KEY-FLD                             
               MOVE SPACES                 TO WS-C245-NME-OIC,                  
                                              WS-C245-OIC-DESIG                 
            WHEN OTHER                                                          
               DISPLAY 'ERROR READING BP13K555, STATUS '                        
                                BP13K555-STATUS                                 
               MOVE BP13K555-STATUS        TO RETURN-CODE                       
               GO TO 9000-CLOSE-FILES-ROUTINE                                   
            END-EVALUATE.                                                       
                                                                                
       6500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       6600-CALL-PRINT-SUBR.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE BP13F200-REC          TO  WS-F200-RECORD.                       
                                                                                
           CALL 'BP13C20N'   USING WS-F200-RECORD                               
                             WS-C245-PARAMETERS.                                
                                                                                
           ADD  1                   TO    WS-CNT-LETTER.                        
                                                                                
           IF (K800-NUM-ALLO-SCHEME = 'RHO' OR 'RH1' OR 'RHA' OR                
              'RHP' OR 'REN' OR 'RHL' OR 'RHS' OR 'RJO' OR 'RJ1' OR             
              'REA' OR 'REE' OR 'REL' OR 'RES' OR 'RDC' OR 'URA' OR             
              'URL' OR 'URO' OR 'URP' OR 'URS' OR 'RED' OR                      
              'REG' OR 'RNG')                                                   
               ADD  1               TO     WS-CNT-RH-LETTER                     
           END-IF.                                                              
                                                                                
       6600-EXIT.                                                               
           EXIT.                                                                
                                                                                
       7000-PROCESS-PRINT.                                                      
      *-------------------------------------------------------------            
      *     PRINT CONTROL LIST                                                  
      *-------------------------------------------------------------            
            PERFORM 8910-READ-BP17K000       THRU   8910-EXIT.                  
                                                                                
            IF (WS-CNT-DETAIL > 16)                                             
                PERFORM  7100-PRINT-HEADING   THRU   7100-EXIT                  
            END-IF.                                                             
                                                                                
            PERFORM 7200-PRINT-DETAIL   THRU   7200-EXIT.                       
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       7100-PRINT-HEADING.                                                      
      *-------------------------------------------------------------            
            ADD    1               TO    WS-CNT-PAGENO.                         
            MOVE   WS-CNT-PAGENO   TO    L245B-PAGE.                            
            MOVE   FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                    
            MOVE   WS-CURRENT-DD   TO    WS-EDITED-DD.                          
            MOVE   WS-CURRENT-MM   TO    WS-EDITED-MM.                          
            MOVE   WS-CURRENT-CCYY TO    WS-EDITED-CCYY.                        
            MOVE   WS-EDITED-DATE  TO    L245B-DATE.                            
            WRITE  P13L245B-REC    FROM  L245B-PRT-HDG-1   AFTER PAGE.          
                                                                                
            WRITE  P13L245B-REC    FROM  L245B-PRT-HDG-2  AFTER 2.              
            WRITE  P13L245B-REC    FROM  L245B-PRT-HDG-3  AFTER 3.              
            WRITE  P13L245B-REC    FROM  L245B-PRT-HDG-4  AFTER 1.              
            MOVE   SPACES          TO    P13L245B-REC.                          
            WRITE  P13L245B-REC    AFTER 2.                                     
            MOVE   0               TO    WS-CNT-DETAIL.                         
                                                                                
       7100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       7200-PRINT-DETAIL.                                                       
      *-------------------------------------------------------------            
            ADD   1                         TO  WS-CNT-DETAIL.                  
            MOVE SPACES                     TO  L245B-PRT-HDG-T.                
                                                                                
            IF F205-NUM-NT-ZONE-SW = 'T'                                        
               MOVE  F200-NUM-NT-ZONE          TO  L245B-NT                     
               MOVE  F200-NUM-REGN             TO  L245B-REGN-NO                
                                                                                
               IF F200-NUM-FLAT-TYPE = 'SE'                                     
                  MOVE  F200-NUM-ACTUAL-FT        TO  L245B-ROOM                
               ELSE                                                             
                  MOVE  F200-NUM-FLAT-TYPE        TO  L245B-ROOM                
               END-IF                                                           
                                                                                
               MOVE  '/'                       TO  L245B-SLASH-1                
            ELSE                                                                
            IF F205-NUM-NT-ZONE-SW = 'Z' AND                                    
               F205-NUM-NT-ZONE    = 'RE'                                       
               MOVE  F200-NUM-NT-ZONE          TO  L245B-NT                     
               MOVE  F200-NUM-REGN             TO  L245B-REGN-NO                
               MOVE  F200-NUM-FLAT-TYPE        TO  L245B-ROOM                   
               MOVE  '/'                       TO  L245B-SLASH-1                
            ELSE                                                                
            IF F205-NUM-NT-ZONE-SW = 'Z'                                        
               MOVE  F200-NUM-NT-ZONE          TO  L245B-ZONE-Z                 
               MOVE  '/'                       TO  L245B-SLASH-1-Z              
                                                   L245B-SLASH-2-Z              
                                                   L245B-SLASH-3-Z              
               MOVE  F200-NUM-REGN             TO  L245B-REGN-NO-Z              
               MOVE  F200-NUM-FLAT-TYPE        TO  L245B-FLAT-TYPE-Z            
                                                                                
               EVALUATE F200-NUM-HHTY-QUEUE-ALPHA                               
                 WHEN 'H'                                                       
                      MOVE 'F' TO L245B-HT-Z                                    
                 WHEN 'G'                                                       
                      MOVE 'U' TO L245B-HT-Z                                    
                 WHEN 'C'                                                       
                      MOVE 'G' TO L245B-HT-Z                                    
                 WHEN OTHER                                                     
                      MOVE F200-NUM-HHTY-QUEUE-ALPHA TO L245B-HT-Z              
               END-EVALUATE                                                     
                                                                                
               MOVE F200-NUM-SEQUENCE TO L245B-SEQ-NO-Z.                        
                                                                                
            MOVE  K800-NUM-ALLO-SCHEME      TO  L245B-ALLOC-SCH.                
            MOVE  K800-NUM-JT-BALLOT-MTF   TO  L245B-JB-CD.                     
            MOVE  WS-C245-APPLN-NAME       TO  L245B-APPL-NAME.                 
                                                                                
      ******CHANGES MADE TO DISPLAY ADDRESS IN L245B                            
            MOVE WS-C245-BLK-NO            TO L245B-BLK-NO.                     
            MOVE WS-C245-CHAR              TO L245B-CHAR.                       
            MOVE WS-C245-LEVEL             TO L245B-LEVEL.                      
            MOVE WS-C245-DASH              TO L245B-DASH.                       
            MOVE WS-C245-HSE-MAIN          TO L245B-HSE-MAIN.                   
            MOVE WS-C245-HSE-SUB           TO L245B-HSE-SUB.                    
            MOVE WS-C245-STREET-NAME       TO L245B-STREET-NAME.                
                                                                                
            WRITE P13L245B-REC            FROM  L245B-PRT-HDG-5 AFTER 2.        
            MOVE  WS-C245-COAPPLN-NAME     TO  L245B-COAPPL-NAME.               
            WRITE P13L245B-REC            FROM  L245B-PRT-HDG-6 AFTER 1.        
                                                                                
       7200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       8000-PRINT-TOTAL.                                                        
      *-------------------------------------------------------------            
      *     OUTPUT TOTAL NUMBERS OF CASES PRINTED                               
      *-------------------------------------------------------------            
            MOVE   WS-CNT-READ-F200 TO    L245B-READ-F200.                      
            WRITE  P13L245B-REC     FROM  L245B-PRT-HDG-7  AFTER PAGE.          
            MOVE   WS-CNT-LETTER    TO    L245B-TOT-LETTER.                     
            WRITE  P13L245B-REC     FROM  L245B-PRT-HDG-8  AFTER 3.             
            MOVE   WS-CNT-RH-LETTER TO    L245B-RH-LETTER.                      
            WRITE  P13L245B-REC     FROM  L245B-PRT-HDG-9  AFTER 3.             
            MOVE   WS-CNT-WRITE-F245A TO  L245B-WRITE-F245A.                    
            WRITE  P13L245B-REC     FROM  L245B-PRT-HDG-10 AFTER 3.             
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8800-CHK-SERS-ALSCH.                                                     
      *-------------------------------------------------------------            
      * SEARCH SERS TABLE TO CHECK ALLOC SCHEME                                 
      *-------------------------------------------------------------            
           SET WS-SERS-PTR TO 1.                                                
           SEARCH WS-SERS-SCH                                                   
              AT END                                                            
                 MOVE 'N'      TO SW-SERS-ALLO-SCHEME                           
              WHEN WS-SERS-SCHEME (WS-SERS-PTR) = K800-NUM-ALLO-SCHEME          
                 MOVE 'Y'      TO SW-SERS-ALLO-SCHEME                           
           END-SEARCH.                                                          
                                                                                
       8800-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       8910-READ-BP17K000.                                                      
      *-------------------------------------------------------------            
            MOVE F200-NUM-REGN             TO K000-NUM-REGNO.                   
                                                                                
            READ BP17K000 KEY IS K000-NUM-REGNO.                                
            EVALUATE BP17K000-STATUS                                            
            WHEN 00                                                             
            WHEN 02                                                             
               PERFORM 8920-START-BP17K580 THRU 8920-EXIT                       
            WHEN 23                                                             
               MOVE SPACES                 TO P13F245A-REC                      
               MOVE F200-NUM-REGN          TO F245A-REGN-NO                     
               MOVE SPACES                 TO F245A-SERS-SCH-ACC                
               WRITE P13F245A-REC                                               
               ADD 1   TO WS-CNT-WRITE-F245A                                    
            WHEN OTHER                                                          
               DISPLAY 'ERROR READING BP17K000, STATUS '                        
                                BP17K000-STATUS                                 
               MOVE BP17K000-STATUS        TO RETURN-CODE                       
               GO TO 9000-CLOSE-FILES-ROUTINE                                   
            END-EVALUATE.                                                       
                                                                                
       8910-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------------------------------------------------*         
       8920-START-BP17K580.                                                     
      *---------------------------------------------------------------*         
           MOVE 'N'                       TO WS-K580-EOF                        
                                             WS-K580-FND.                       
           MOVE SPACES                    TO K580-APPRV-KEY-FLD.                
           MOVE K000-NUM-SCH-ACC          TO K580-NUM-SCH-ACC-APPRV.            
                                                                                
           START BP17K580 KEY >= K580-APPRV-KEY-FLD.                            
                                                                                
           EVALUATE BP17K580-STATUS                                             
           WHEN 00                                                              
                PERFORM 8925-READNEXT-BP17K580 THRU 8925-EXIT                   
                  UNTIL WS-K580-EOF = 'Y'                                       
                IF WS-K580-FND = 'N'                                            
                   MOVE SPACES                 TO P13F245A-REC                  
                   MOVE F200-NUM-REGN          TO F245A-REGN-NO                 
                   MOVE K000-NUM-SCH-ACC       TO F245A-SERS-SCH-ACC            
                   PERFORM 8930-GET-PIDB-INFO  THRU 8930-EXIT                   
                   WRITE P13F245A-REC                                           
                   ADD 1   TO WS-CNT-WRITE-F245A                                
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
                DISPLAY 'BP17K580 RECORD NOT FOUND: ' K580-APPRV-KEY-FLD        
                MOVE 'N'                  TO WS-K580-FND                        
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'BP17K580 READ ERROR: ' BP17K580-STATUS                 
                MOVE BP17K580-STATUS      TO RETURN-CODE                        
                GO TO 9000-CLOSE-FILES-ROUTINE                                  
           END-EVALUATE.                                                        
                                                                                
       8920-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       8925-READNEXT-BP17K580.                                                  
      *---------------------------------------------------------------*         
           READ BP17K580 NEXT RECORD.                                           
                                                                                
           EVALUATE BP17K580-STATUS                                             
           WHEN 00                                                              
                IF K580-NUM-SCH-ACC-APPRV = K000-NUM-SCH-ACC                    
                   IF K580-NUM-CASE-APPRV(1:1) = '#'                            
                      CONTINUE                                                  
                   ELSE                                                         
                      MOVE 'Y'            TO WS-K580-FND                        
                   END-IF                                                       
                ELSE                                                            
                   MOVE 'Y'               TO WS-K580-EOF                        
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y'                  TO WS-K580-EOF                        
           WHEN OTHER                                                           
                DISPLAY 'BP17K580 READ ERROR: ' BP17K580-STATUS                 
                MOVE BP17K580-STATUS      TO RETURN-CODE                        
                PERFORM 9000-CLOSE-FILES-ROUTINE THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       8925-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8930-GET-PIDB-INFO.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES               TO BP13COMM8-REC.                          
           INITIALIZE                   BP13COMM8-REC.                          
           MOVE K000-NUM-SCH-ACC     TO WS-LINK-REC.                            
           CALL 'BP13C913'  USING WS-LINK-REC,                                  
                                  BP13COMM8-REC.                                
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0 AND 100                                  
              MOVE SPACES              TO F245A-SERS-FT-ADDR                    
              DISPLAY 'ERROR IN READING PIDB TABLE  '                           
                       COMM8-CDE-SYSERR                                         
           ELSE                                                                 
              IF COMM8-CDE-SYSERR =  100                                        
                 MOVE SPACES              TO F245A-SERS-FT-ADDR                 
              ELSE                                                              
                 MOVE SPACES    TO WS-STRING-TO-PARSE                           
                 STRING 'BLK ' COMM8-NUM-BLK ' #' COMM8-NUM-LEVEL '-'           
                        COMM8-NUM-UNIT-MAIN  ' '  COMM8-NUM-UNIT-SUB            
                        ' '  COMM8-NME-STREET ' ' COMM8-NUM-POSTAL-CODE         
                        DELIMITED BY SIZE INTO WS-STRING-TO-PARSE               
                 END-STRING                                                     
                 MOVE LENGTH OF WS-STRING-TO-PARSE                              
                                         TO WS-STRING-LENGTH                    
                          MOVE 'Y'       TO WS-INSERT-SPACE-FLAG                
                 PERFORM 8940-PARSE-STRING     THRU 8940-EXIT                   
                 MOVE WS-PARSED-STRING   TO F245A-SERS-FT-ADDR                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8930-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       8940-PARSE-STRING.                                                       
      *--------------------------------------------------------------           
           MOVE SPACES                    TO   WS-PARSED-STRING.                
           MOVE 1                         TO   WS-CUR-POS-PS.                   
           ADD  1                         TO   WS-STRING-LENGTH.                
           PERFORM 8950-GET-CHAR          THRU 8950-EXIT                        
              VARYING WS-CUR-POS-SP FROM 1 BY 1                                 
              UNTIL WS-CUR-POS-SP = WS-STRING-LENGTH.                           
                                                                                
                                                                                
       8940-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------          
       8950-GET-CHAR.                                                           
      *---------------------------------------------------------------          
           IF WS-STRING-TO-PARSE(WS-CUR-POS-SP:1) NOT = SPACES                  
              IF WS-SPACE-FLAG = 'Y' AND                                        
                 WS-INSERT-SPACE-FLAG = 'Y' AND                                 
                 WS-PARSED-STRING NOT = SPACES                                  
                 MOVE ' ' TO WS-PARSED-STRING(WS-CUR-POS-PS:1)                  
                 ADD 1           TO  WS-CUR-POS-PS                              
              END-IF                                                            
              MOVE WS-STRING-TO-PARSE(WS-CUR-POS-SP:1)                          
                              TO  WS-PARSED-STRING(WS-CUR-POS-PS:1)             
              ADD 1           TO  WS-CUR-POS-PS                                 
              MOVE    'N'      TO WS-SPACE-FLAG                                 
           ELSE                                                                 
              MOVE 'Y'     TO WS-SPACE-FLAG                                     
           END-IF.                                                              
                                                                                
       8950-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES-ROUTINE.                                                
      *-------------------------------------------------------------            
            CLOSE BP13F205                                                      
                  BP13K060                                                      
                  BP13F200                                                      
                  BP13K800                                                      
                  BP13K820                                                      
                  BP13K830                                                      
                  BP13K278                                                      
                  BP13K555                                                      
                  BP17K000                                                      
                  BP17K580                                                      
                  P13L245B                                                      
                  P13F245A.                                                     
                                                                                
            IF BP13K060-STATUS  NOT = 00 AND 97                                 
               DISPLAY 'CLOSING ERROR BP13K060, STAT ' BP13K060-STATUS.         
                                                                                
            IF BP13K800-STATUS  NOT = 00 AND 97                                 
               DISPLAY 'CLOSING ERROR BP13K800, STAT ' BP13K800-STATUS.         
                                                                                
            IF BP13K820-STATUS  NOT = 00 AND 97                                 
               DISPLAY 'CLOSING ERROR BP13K820, STAT ' BP13K820-STATUS.         
                                                                                
            IF BP13K830-STATUS  NOT = 00 AND 97                                 
               DISPLAY 'CLOSING ERROR BP13K830, STAT ' BP13K830-STATUS.         
                                                                                
            IF BP13K555-STATUS  NOT = 00 AND 97                                 
               DISPLAY 'CLOSING ERROR BP13K555, STAT ' BP13K555-STATUS.         
                                                                                
            IF BP13K278-STATUS  NOT = 00 AND 97                                 
               DISPLAY 'CLOSING ERROR BP13K278, STAT ' BP13K278-STATUS.         
                                                                                
            IF BP17K000-STATUS  NOT = 00 AND 97                                 
               DISPLAY 'CLOSING ERROR BP17K000, STAT ' BP17K000-STATUS.         
                                                                                
            IF BP17K580-STATUS  NOT = 00 AND 97                                 
               DISPLAY 'CLOSING ERROR BP17K580, STAT ' BP17K580-STATUS.         
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
