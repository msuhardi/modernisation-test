      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13CE10.                                                 
       AUTHOR.        MRN1.                                                     
       DATE-WRITTEN.  15032012.                                                 
      *   OBJECTIVE:  TO UPDATE CPFGRANT MASTER FOR DBSS SUBTRANSFER  *         
      *               DATE, REPUDIATION DATE, AND KEY-ISSUED CASES    *         
      *===============================================================*         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13FC38                                  *         
      *                  2. BP13K022                                  *         
      *                                                               *         
      *     I/O FILE:                                                 *         
      *                  1. BP13KD09                                  *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP134970 15/03/2012 MRN1 NEW PROGRAM.                         *         
      *===============================================================*         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13FC38  ASSIGN        TO BP13FC38.                         
                                                                                
           SELECT  BP13KD09  ASSIGN        TO BP13KD09                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD09-KEY-FLD                      
                             FILE STATUS   IS BP13KD09-STATUS.                  
                                                                                
           SELECT  BP13K022  ASSIGN       TO BP13K022                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K022-KEY-FLD                       
                             ALTERNATE RECORD KEY IS K022-NUM-REGN              
                             FILE STATUS  IS BP13K022-STATUS.                   
                                                                                
           SELECT BP13KD00   ASSIGN       TO BP13KD00                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS KD00-KEY-FLD                       
                             FILE STATUS  IS BP13KD00-STATUS.                   
                                                                                
           SELECT  BP13FD09  ASSIGN        TO BP13FD09.                         
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13FC38                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   WS-BP13FC38-REC.                                                    
            05 WS-FC38-REGNO   PIC X(8).                                        
            05 FILLER          PIC X(52).                                       
            05 WS-FC38-DTE-REP PIC X(10).                                       
            05 FILLER          PIC X(30).                                       
                                                                                
       FD   BP13KD09                                                            
            BLOCK CONTAINS   0  RECORDS                                         
            RECORD CONTAINS  500  CHARACTERS                                    
            LABEL RECORD IS STANDARD.                                           
       COPY BP13KD09.                                                   00150000
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13FD09                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FD09.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  BP13KD09-STATUS         PIC 99       VALUE ZEROS.                    
       01  BP13K022-STATUS         PIC 99       VALUE ZEROS.                    
       01  BP13KD00-STATUS         PIC 99       VALUE ZEROS.                    
       01  CNT-FC38-READ           PIC 9(5)     VALUE ZEROS.                    
       01  CNT-KD09-REWRITE        PIC 9(5)     VALUE ZEROS.                    
       01  CNT-FD09-WRITE          PIC 9(5)     VALUE ZEROS.                    
       01  AMT-GRANT-TOTAL         PIC 9(08)V99 VALUE ZEROS.                    
       01  AMT-RECOV-TOTAL         PIC 9(08)V99 VALUE ZEROS.                    
       01  AMT-GRANT-RECOV-DIFF    PIC 9(08)V99 VALUE ZEROS.                    
                                                                                
       01  WS-SWITCHES.                                                         
           05  FC38-EOF            PIC X       VALUE 'N'.                       
           05  WS-KD09-FOUND-TAG   PIC X       VALUE 'N'.                       
           05  WS-K022-FOUND-TAG   PIC X       VALUE 'N'.                       
           05  WS-KD00-FOUND-TAG   PIC X       VALUE 'N'.                       
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       MAIN-PROCESS.                                                            
      ******************************************************************        
           PERFORM 100-OPEN-FILES          THRU 100-EXIT.                       
           PERFORM 200-READ-FC38           THRU 200-EXIT.                       
           PERFORM 300-PROCESS-RECORD      THRU 300-EXIT                        
                   UNTIL FC38-EOF = 'Y'.                                        
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       100-OPEN-FILES.                                                          
      ******************************************************************        
                                                                                
           OPEN   INPUT  BP13FC38                                               
                         BP13K022                                               
                         BP13KD00                                               
                  I-O    BP13KD09                                               
                  OUTPUT BP13FD09.                                              
                                                                                
           IF BP13KD09-STATUS NOT = '00' AND NOT = '97'                         
               DISPLAY ' OPENING ERROR,BP13KD09-STATUS' BP13KD09-STATUS         
               MOVE BP13KD09-STATUS TO RETURN-CODE                              
               PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                       
                                                                                
           IF BP13K022-STATUS NOT = '00' AND NOT = '97'                         
               DISPLAY ' OPENING ERROR,BP13K022-STATUS' BP13K022-STATUS         
               MOVE BP13K022-STATUS TO RETURN-CODE                              
               PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                       
                                                                                
           IF BP13KD00-STATUS NOT = '00' AND NOT = '97'                         
               DISPLAY ' OPENING ERROR,BP13KD00-STATUS' BP13KD00-STATUS         
               MOVE BP13KD00-STATUS TO RETURN-CODE                              
               PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                       
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       200-READ-FC38.                                                           
      ******************************************************************        
           READ BP13FC38  AT END                                                
                MOVE 'Y' TO FC38-EOF                                            
                GO TO 200-EXIT.                                                 
                                                                                
           IF WS-FC38-REGNO(1:1) NOT = '5'                                      
              GO TO 200-READ-FC38                                               
           ELSE                                                                 
              ADD 1 TO CNT-FC38-READ                                            
           END-IF.                                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-RECORD.                                                      
      ******************************************************************        
                                                                                
           PERFORM 420-READ-BP17KD09        THRU 420-EXIT.                      
                                                                                
           IF WS-KD09-FOUND-TAG = 'Y'                                           
              MOVE WS-FC38-DTE-REP(1:2)  TO KD09-DTE-CANCEL(7:2)                
              MOVE WS-FC38-DTE-REP(4:2)  TO KD09-DTE-CANCEL(5:2)                
              MOVE WS-FC38-DTE-REP(7:4)  TO KD09-DTE-CANCEL(1:4)                
              PERFORM 700-GET-GRANT-CODE THRU 700-EXIT                          
              IF WS-FC38-DTE-REP NOT = SPACES AND LOW-VALUES                    
                 PERFORM 500-GET-OIC           THRU 500-EXIT                    
              ELSE                                                              
                 MOVE SPACES TO KD09-NUM-OIC                                    
              END-IF                                                            
              PERFORM 350-CHECK-CLOSED         THRU 350-EXIT                    
              PERFORM 430-REWRITE-BP13KD09     THRU 430-EXIT                    
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-FC38            THRU 200-EXIT.                      
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       350-CHECK-CLOSED.                                                        
      ******************************************************************        
                                                                                
           IF KD09-AMT-SHG-GRANT IS NOT NUMERIC                                 
              MOVE ZEROES TO KD09-AMT-SHG-GRANT.                                
           IF KD09-AMT-AHG-GRANT IS NOT NUMERIC                                 
              MOVE ZEROES TO KD09-AMT-AHG-GRANT.                                
           IF KD09-AMT-SHG-RECOVER IS NOT NUMERIC                               
              MOVE ZEROES TO KD09-AMT-SHG-RECOVER.                              
           IF KD09-AMT-AHG-RECOVER IS NOT NUMERIC                               
              MOVE ZEROES TO KD09-AMT-AHG-RECOVER.                              
                                                                                
           COMPUTE AMT-GRANT-TOTAL      = KD09-AMT-SHG-GRANT +                  
                                          KD09-AMT-AHG-GRANT.                   
           COMPUTE AMT-RECOV-TOTAL      = KD09-AMT-SHG-RECOVER +                
                                          KD09-AMT-AHG-RECOVER.                 
           COMPUTE AMT-GRANT-RECOV-DIFF = AMT-GRANT-TOTAL -                     
                                          AMT-RECOV-TOTAL.                      
                                                                                
           IF AMT-GRANT-RECOV-DIFF = ZEROES                                     
              MOVE 'Y' TO KD09-NUM-CLOSED-STATUS                                
           ELSE                                                                 
              MOVE 'N' TO KD09-NUM-CLOSED-STATUS                                
              WRITE BP13FD09-REC FROM BP13KD09-REC                              
              ADD 1 TO CNT-FD09-WRITE                                           
           END-IF.                                                              
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       420-READ-BP17KD09.                                                       
      ******************************************************************        
                                                                                
           MOVE WS-FC38-REGNO    TO KD09-NUM-REGN.                              
                                                                                
           READ BP13KD09.                                                       
                                                                                
           EVALUATE BP13KD09-STATUS                                             
               WHEN 00                                                          
                    MOVE 'Y' TO WS-KD09-FOUND-TAG                               
               WHEN 23                                                          
                    MOVE 'N' TO WS-KD09-FOUND-TAG                               
                    MOVE SPACES          TO KD09-DTE-CANCEL                     
                    MOVE SPACES          TO KD09-NUM-OIC                        
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13KD09 : ' BP13KD09-STATUS         
                            ' REGN = ' KD09-NUM-REGN                            
                    PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       430-REWRITE-BP13KD09.                                                    
      ******************************************************************        
                                                                                
           REWRITE BP13KD09-REC.                                                
           ADD   1   TO  CNT-KD09-REWRITE.                                      
                                                                                
           IF BP13KD09-STATUS NOT = ZEROS                                       
               DISPLAY 'WRITE BP13KD09-STATUS IS ' BP13KD09-STATUS              
               MOVE BP13KD09-STATUS TO RETURN-CODE                              
               PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                       
                                                                                
       430-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       500-GET-OIC.                                                             
      ******************************************************************        
                                                                                
            MOVE SPACES                               TO BP13K022-REC.          
            INITIALIZE                                   BP13K022-REC.          
                                                                                
            MOVE WS-FC38-REGNO                        TO K022-NUM-REGN.         
                                                                                
            READ BP13K022 KEY IS K022-NUM-REGN.                                 
                                                                                
            EVALUATE BP13K022-STATUS                                            
                WHEN 00                                                         
                     MOVE 'Y' TO WS-K022-FOUND-TAG                              
                     MOVE K022-NUM-OIC TO KD09-NUM-OIC                          
                WHEN 23                                                         
                     MOVE 'N' TO WS-K022-FOUND-TAG                              
                     MOVE SPACES       TO KD09-NUM-OIC                          
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K022 : ' BP13K022-STATUS        
                             ' REGN NO = ' K022-NUM-REGN                        
                     PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT            
            END-EVALUATE.                                                       
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       700-GET-GRANT-CODE.                                                      
      ******************************************************************        
                                                                                
            MOVE SPACES                               TO BP13KD00-REC.          
            INITIALIZE                                   BP13KD00-REC.          
                                                                                
            MOVE WS-FC38-REGNO                        TO KD00-KEY-FLD.          
                                                                                
            READ BP13KD00 KEY IS KD00-KEY-FLD.                                  
                                                                                
            EVALUATE BP13KD00-STATUS                                            
                WHEN 00                                                         
                     MOVE 'Y' TO WS-KD00-FOUND-TAG                              
                     MOVE KD00-NUM-HSGNT-ELIG TO KD09-CODE-GRANT                
                WHEN 23                                                         
                     MOVE 'N' TO WS-KD00-FOUND-TAG                              
                     MOVE SPACES       TO KD09-CODE-GRANT                       
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13KD00 : ' BP13KD00-STATUS        
                             ' REGN NO = ' KD00-NUM-REGN                        
                     PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT            
            END-EVALUATE.                                                       
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
           DISPLAY 'NO OF FC38 RECS READ      : ' CNT-FC38-READ.                
           DISPLAY 'NO OF KD09 RECS REWRITTEN : ' CNT-KD09-REWRITE.             
           DISPLAY 'NO OF FD09 RECS WRITTEN   : ' CNT-FD09-WRITE.               
                                                                                
           CLOSE BP13FC38                                                       
                 BP13K022                                                       
                 BP13KD00                                                       
                 BP13KD09                                                       
                 BP13FD09.                                                      
                                                                                
           IF BP13KD09-STATUS NOT = 0                                           
              DISPLAY ' CLOSING ERROR, BP13KD09-STATUS '                        
                       BP13KD09-STATUS                                          
              MOVE BP13KD09-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13K022-STATUS NOT = 0                                           
              DISPLAY ' CLOSING ERROR, BP13K022-STATUS '                        
                       BP13K022-STATUS                                          
              MOVE BP13K022-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
