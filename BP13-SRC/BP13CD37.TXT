      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13CD37.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  JUNE 05, 2009.                                            
      *===============================================================*         
      *   OBJECTIVE:  MONTHLY REPORT ON EC CASES W/ GRANT REQUEST DATE*         
      *===============================================================*         
      *                                                               *         
      *     INPUT FILE : 1. BP13FD07 (REPLACED WITH GRANT_ASMNT)      *         
      *                                                               *         
      *     OUTPUT FILE: 1. BP13LD37                                  *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP134070 04/11/2010 PCL3 NEW PROGRAM.                         *         
      * BP134364 05/10/2011 JB8  RE-FORMAT REPORT                     *         
      * BP134364 07/10/2011 JB8  TO CATER FOR 10,000 HALF-GRANT       *         
      * BP134338 02/11/2011 JB8  TO RECTIFY READ OF BP13K730          *         
      * BP134641 03/08/2012 MDT2 CHANGE LRECL OF BP13KD07 FROM 500 TO *         
      *                          700 IN FD                            *         
      * BP136492 02/11/2016 MRN1 READ DB2 INSTEAD OF BP13KD07         *         
      *                          USE DTE-SUBMIT INSTEAD OF GRANT-RQST *         
      *===============================================================*         
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13KA30  ASSIGN        TO BP13KA30                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KA30-KEY-FLD                      
                      ALTERNATE RECORD KEY IS KA30-NUM-REGN                     
                             FILE STATUS   IS WS-KA30-STATUS.                   
                                                                                
           SELECT  BP13K730  ASSIGN        TO BP13K730                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K730-KEY-NEW                      
                             FILE STATUS   IS WS-K730-STATUS.                   
                                                                                
           SELECT  BP18K300  ASSIGN        TO BP18K300                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K300-NUM-REGN                     
                             FILE STATUS   IS WS-K300-STATUS.                   
                                                                                
           SELECT  BP13LD37  ASSIGN        TO BP13LD37.                         
           SELECT  SY02F001  ASSIGN        TO SY02F001.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13KA30                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13KA30.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K730.                                                           
                                                                                
       FD   BP18K300                                                            
            RECORD CONTAINS 800 CHARACTERS.                                     
       COPY BP18K300.                                                           
                                                                                
       FD   BP13LD37                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   BP13LD37-REC               PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-F001-EOF             PIC X(1)  VALUE 'N'.                     
               88 EOF-F001                       VALUE 'Y'.                     
           05  WS-KA30-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K730-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K300-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K300-FND             PIC X(01) VALUE SPACES.                  
               88 K300-FND                       VALUE 'Y'.                     
           05  WS-K730-FND             PIC X(01) VALUE SPACES.                  
               88 K730-FND                       VALUE 'Y'.                     
           05  WS-HH-TYPE              PIC X(01) VALUE SPACES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SYS-DATE              PIC 9(8)  VALUE ZEROES.                  
           05 WS-DTE-START             PIC X(08) VALUE SPACES.                  
           05 WS-DTE-END               PIC X(08) VALUE SPACES.                  
           05 WS-DTE-SUBMIT            PIC X(10) VALUE SPACES.                  
           05 WS-DTE-SUB               PIC X(08) VALUE SPACES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  CNT-DB2-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE 60.                      
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-LD37             PIC 9(7)  VALUE ZEROES.                  
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
           05  WS-CTR                  PIC 9(2)  VALUE 00.                      
                                                                                
       01  WS-GRANT-SUMMARY.                                                    
           05  WS-GR-1-CNT             PIC 9(05) VALUE ZEROES.                  
           05  WS-GR-2A-CNT            PIC 9(05) VALUE ZEROES.                  
           05  WS-GR-2B-CNT            PIC 9(05) VALUE ZEROES.                  
           05  WS-GR-3-CNT             PIC 9(05) VALUE ZEROES.                  
           05  WS-GR-4A-CNT            PIC 9(05) VALUE ZEROES.                  
           05  WS-GR-4B-CNT            PIC 9(05) VALUE ZEROES.                  
           05  WS-GR-4C-CNT            PIC 9(05) VALUE ZEROES.                  
           05  WS-GR-4D-CNT            PIC 9(05) VALUE ZEROES.                  
           05  WS-GR-5-CNT             PIC 9(05) VALUE ZEROES.                  
           05  WS-GR-OTHER             PIC 9(05) VALUE ZEROES.                  
                                                                                
      ******************************************************************        
      * BP13LD37 - REPORT FORMAT                                       *        
      ******************************************************************        
       01  LD37-MAIN-TITLE.                                                     
           05  FILLER              PIC X(8)      VALUE 'BP13LD37'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  LD37-CURR-DATE      PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  LD37-PAGE-NO        PIC ZZZZ9     VALUE ZEROES.                  
                                                                                
       01  LD37-SUB-TITLE.                                                      
           05  FILLER                    PIC X(29) VALUE SPACES.                
           05  FILLER                    PIC X(49) VALUE                        
               'REPORT ON EC CASES WITH GRANT REQUEST DATE (FROM '.             
           05  LD37-DTE-START            PIC X(10) VALUE SPACES.                
           05  FILLER                    PIC X(04) VALUE ' TO '.                
           05  LD37-DTE-END              PIC X(10) VALUE SPACES.                
           05  FILLER                    PIC X(01) VALUE ')'.                   
                                                                                
       01  LD37-HEADER.                                                         
           05  FILLER                    PIC X(132) VALUE                       
               '  S/NO   PROJECT   REGN NO    REQ DATE     GRANT TYPE'.         
                                                                                
       01  LD37-DASH.                                                           
           05  FILLER                    PIC X(132) VALUE                       
               ' -----   -------   --------   ----------   -------------        
      -        '-------'.                                                       
                                                                                
       01  LD37-DETAIL.                                                         
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LD37-SNO            PIC ZZZZ9        VALUE ZEROES.               
           05  FILLER              PIC X(03)        VALUE SPACES.               
           05  LD37-NUM-PRJCT      PIC X(03)        VALUE SPACES.               
           05  FILLER              PIC X(07)        VALUE SPACES.               
           05  LD37-REGN-NO        PIC X(08)        VALUE SPACES.               
           05  FILLER              PIC X(03)        VALUE SPACES.               
           05  LD37-DTE-REQ        PIC X(10)        VALUE SPACES.               
           05  FILLER              PIC X(03)        VALUE SPACES.               
           05  LD37-GRNT-TYPE      PIC X(20)        VALUE SPACES.               
                                                                                
       01  LD37-SUM-TITLE.                                                      
           05  FILLER                    PIC X(132) VALUE                       
               'GRANT TYPE    DESCRIPTION            NO. OF RECORDS'.           
                                                                                
       01  LD37-SUM-DASH.                                                       
           05  FILLER                    PIC X(132) VALUE                       
               '----------    --------------------   --------------'.           
                                                                                
       01  LD37-SUM-DETAIL.                                                     
           05  FILLER              PIC X(04)        VALUE SPACES.               
           05  LD37-SUM-GRANT-TYPE PIC X(01)        VALUE SPACES.               
           05  FILLER              PIC X(09)        VALUE SPACES.               
           05  LD37-SUM-GRANT-DESC PIC X(20)        VALUE SPACES.               
           05  FILLER              PIC X(07)        VALUE SPACES.               
           05  LD37-SUM-GRANT-CNT  PIC ZZZZ9        VALUE ZEROES.               
                                                                                
       01  WS-END-CURSOR1            PIC X(01) VALUE 'N'.                       
       01  WS-QUERY-NO               PIC X(02) VALUE SPACES.                    
                                                                                
       01  WS-SQL-CODES.                                                        
           05  WS-SQL-OK                PIC S9(4) COMP VALUE +000.              
           05  WS-SQL-NFND              PIC S9(4) COMP VALUE +100.              
           05  WS-SQL-MORE-ROWS         PIC S9(4) COMP VALUE -811.              
                                                                                
       01  WS-SQLCODE                   PIC ---9.                               
                                                                                
             EXEC SQL INCLUDE  SQLCA    END-EXEC.                               
             EXEC SQL INCLUDE  P13VGASM END-EXEC.                               
                                                                                
      *===============================================================          
      *     CURSOR TO EXTRACT GRANT_ASMNT                                       
      *===============================================================          
           EXEC SQL                                                             
              DECLARE CURSOR1 CURSOR WITH HOLD FOR                              
               SELECT NUM_RGSTRN,                                               
                      NUM_GRANT_TYPE,                                           
                      NUM_ELGBL_GRANT,                                          
                      NUM_GRANT_AMOUNT_ELGBL,                                   
                      NUM_GRANT_STATUS,                                         
                      DTE_APRV,                                                 
                      DTE_CANCEL,                                               
                      DTE_REJECT,                                               
                      DTE_SUBMIT,                                               
                      DTE_WTHDRW,                                               
                      NUM_RJCTN_REASON,                                         
                      NUM_WTDRWL_REASON,                                        
                      TXT_CNCLTN_REASON,                                        
                      DTE_GRANT_RQST,                                           
                      NUM_GRANT_DOC_RCV,                                        
                      DTE_ACK,                                                  
                      DTE_GRANT_FORM_ISSUE,                                     
                      DTE_GRANT_OPT_OUT_FORM_ISSUE,                             
                      NUM_GRANT_OPT_OUT_REASON,                                 
                      DTE_GRANT_OPT_OUT_REASON,                                 
                      NUM_ALCTN_SCHEME,                                         
                      NUM_HSHLD_CTZNSP,                                         
                      NUM_WRKNG_CHILD_ASMNT,                                    
                      QTY_MONTH_WORK,                                           
                      NUM_APPEAL_ATCHMT,                                        
                      NUM_PARENT_ADRS_BLDNG,                                    
                      NUM_PARENT_ADRS_LEVEL,                                    
                      NUM_PARENT_ADRS_UNIT,                                     
                      TXT_PARENT_ADRS1,                                         
                      TXT_PARENT_ADRS2,                                         
                      NUM_PARENT_ADRS_POSTAL,                                   
                      DTE_PYMNT,                                                
                      DTE_CPF_ACK_GRANT_RCV,                                    
                      NUM_GRANT_CREDIT,                                         
                      TME_APNTMT,                                               
                      DTE_APNTMT,                                               
                      TME_CREATE_ASMNT,                                         
                      NUM_UPDATE_USERID,                                        
                      TME_UPDATE,                                               
                      DTE_UPDATE,                                               
                      NUM_SOURCE_SYSTEM,                                        
                      NUM_ACNT,                                                 
                      NUM_SCHEME,                                               
                      NUM_TNCY,                                                 
                      NUM_HALF_GRANT                                            
                FROM  GRANT_ASMNT                                               
                WHERE                                                           
                   (NUM_RGSTRN >= '60000000' AND                                
                    NUM_RGSTRN <  '70000000') AND                               
                   (DTE_SUBMIT NOT IN ('0001-01-01'))                           
                ORDER BY NUM_RGSTRN, NUM_GRANT_TYPE                             
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-OPEN-CURSOR1       THRU 1100-EXIT.                      
           PERFORM 1200-FETCH-CURSOR1      THRU 1200-EXIT                       
                   UNTIL WS-END-CURSOR1 = 'Y'                                   
           PERFORM 1300-CLOSE-CURSOR1 THRU 1300-EXIT.                           
           PERFORM 4200-OUTPUT-SUMMARY     THRU 4200-EXIT                       
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  SY02F001                                                 
                       BP13KA30                                                 
                       BP13K730                                                 
                       BP18K300                                                 
                OUTPUT BP13LD37.                                                
                                                                                
           IF WS-KA30-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KA30 - ERROR OPENING : ' WS-KA30-STATUS              
              MOVE WS-KA30-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT                
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K730 - ERROR OPENING : ' WS-K730-STATUS              
              MOVE WS-K730-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT                
           END-IF.                                                              
                                                                                
           IF WS-K300-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP18K300 - ERROR OPENING : ' WS-K300-STATUS              
              MOVE WS-K300-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT                
           END-IF.                                                              
                                                                                
           READ SY02F001 AT END MOVE 'Y'  TO WS-F001-EOF.                       
           IF EOF-F001                                                          
              DISPLAY 'RECORD NOT FOUND IN SY01F001 '                           
              DISPLAY 'PROCESSING TERMINATED '                                  
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           ELSE                                                                 
              MOVE F001-DTE-CURRENT          TO WS-SYS-DATE                     
              MOVE F001-DTE-LAST-DAY-PREV    TO WS-DTE-END                      
              STRING F001-DTE-LAST-DAY-PREV(1:6) '01'                           
                     DELIMITED BY SIZE INTO WS-DTE-START                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       1100-OPEN-CURSOR1.                                                       
      ***************************************************************           
                                                                                
           MOVE '01'           TO WS-QUERY-NO                                   
           MOVE 'N'            TO WS-END-CURSOR1.                               
                                                                                
           EXEC SQL                                                             
                OPEN CURSOR1                                                    
           END-EXEC.                                                            
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
                CONTINUE                                                        
           WHEN OTHER                                                           
                PERFORM 9998-DB2-ERROR    THRU 9998-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       1200-FETCH-CURSOR1.                                                      
      ***************************************************************           
                                                                                
           MOVE '02'           TO WS-QUERY-NO                                   
           EXEC SQL                                                             
                FETCH CURSOR1                                                   
                INTO  :DCLGRANT-ASMNT.NUM-RGSTRN,                               
                      :DCLGRANT-ASMNT.NUM-GRANT-TYPE,                           
                      :DCLGRANT-ASMNT.NUM-ELGBL-GRANT,                          
                      :DCLGRANT-ASMNT.NUM-GRANT-AMOUNT-ELGBL,                   
                      :DCLGRANT-ASMNT.NUM-GRANT-STATUS,                         
                      :DCLGRANT-ASMNT.DTE-APRV,                                 
                      :DCLGRANT-ASMNT.DTE-CANCEL,                               
                      :DCLGRANT-ASMNT.DTE-REJECT,                               
                      :DCLGRANT-ASMNT.DTE-SUBMIT,                               
                      :DCLGRANT-ASMNT.DTE-WTHDRW,                               
                      :DCLGRANT-ASMNT.NUM-RJCTN-REASON,                         
                      :DCLGRANT-ASMNT.NUM-WTDRWL-REASON,                        
                      :DCLGRANT-ASMNT.TXT-CNCLTN-REASON,                        
                      :DCLGRANT-ASMNT.DTE-GRANT-RQST,                           
                      :DCLGRANT-ASMNT.NUM-GRANT-DOC-RCV,                        
                      :DCLGRANT-ASMNT.DTE-ACK,                                  
                      :DCLGRANT-ASMNT.DTE-GRANT-FORM-ISSUE,                     
                      :DCLGRANT-ASMNT.DTE-GRANT-OPT-OUT-FORM-ISSUE,             
                      :DCLGRANT-ASMNT.NUM-GRANT-OPT-OUT-REASON,                 
                      :DCLGRANT-ASMNT.DTE-GRANT-OPT-OUT-REASON,                 
                      :DCLGRANT-ASMNT.NUM-ALCTN-SCHEME,                         
                      :DCLGRANT-ASMNT.NUM-HSHLD-CTZNSP,                         
                      :DCLGRANT-ASMNT.NUM-WRKNG-CHILD-ASMNT,                    
                      :DCLGRANT-ASMNT.QTY-MONTH-WORK,                           
                      :DCLGRANT-ASMNT.NUM-APPEAL-ATCHMT,                        
                      :DCLGRANT-ASMNT.NUM-PARENT-ADRS-BLDNG,                    
                      :DCLGRANT-ASMNT.NUM-PARENT-ADRS-LEVEL,                    
                      :DCLGRANT-ASMNT.NUM-PARENT-ADRS-UNIT,                     
                      :DCLGRANT-ASMNT.TXT-PARENT-ADRS1,                         
                      :DCLGRANT-ASMNT.TXT-PARENT-ADRS2,                         
                      :DCLGRANT-ASMNT.NUM-PARENT-ADRS-POSTAL,                   
                      :DCLGRANT-ASMNT.DTE-PYMNT,                                
                      :DCLGRANT-ASMNT.DTE-CPF-ACK-GRANT-RCV,                    
                      :DCLGRANT-ASMNT.NUM-GRANT-CREDIT,                         
                      :DCLGRANT-ASMNT.TME-APNTMT,                               
                      :DCLGRANT-ASMNT.DTE-APNTMT,                               
                      :DCLGRANT-ASMNT.TME-CREATE-ASMNT,                         
                      :DCLGRANT-ASMNT.NUM-UPDATE-USERID,                        
                      :DCLGRANT-ASMNT.TME-UPDATE,                               
                      :DCLGRANT-ASMNT.DTE-UPDATE,                               
                      :DCLGRANT-ASMNT.NUM-SOURCE-SYSTEM,                        
                      :DCLGRANT-ASMNT.NUM-ACNT,                                 
                      :DCLGRANT-ASMNT.NUM-SCHEME,                               
                      :DCLGRANT-ASMNT.NUM-TNCY,                                 
                      :DCLGRANT-ASMNT.NUM-HALF-GRANT                            
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
                PERFORM 3000-PROCESS-RECORDS THRU 3000-EXIT                     
           WHEN 100                                                             
                MOVE 'Y'  TO WS-END-CURSOR1                                     
           WHEN OTHER                                                           
                PERFORM 9998-DB2-ERROR    THRU 9998-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       1300-CLOSE-CURSOR1.                                                      
      ***************************************************************           
                                                                                
           MOVE '03'           TO WS-QUERY-NO                                   
           EXEC SQL                                                             
                CLOSE CURSOR1                                                   
           END-EXEC.                                                            
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
                CONTINUE                                                        
           WHEN OTHER                                                           
                PERFORM 9998-DB2-ERROR    THRU 9998-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       1300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE DTE-SUBMIT OF DCLGRANT-ASMNT TO WS-DTE-SUBMIT.                  
                                                                                
           STRING WS-DTE-SUBMIT(1:4)                                            
                  WS-DTE-SUBMIT(6:2)                                            
                  WS-DTE-SUBMIT(9:2)                                            
                  DELIMITED BY SIZE INTO WS-DTE-SUB.                            
                                                                                
           IF WS-DTE-SUB >= WS-DTE-START AND                                    
              WS-DTE-SUB <= WS-DTE-END                                          
              PERFORM 4000-PROCESS-BP13LD37         THRU 4000-EXIT              
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PROCESS-BP13LD37.                                                   
      ******************************************************************        
                                                                                
           ADD 1                                   TO WS-LINE-CTR               
           IF WS-LINE-CTR > WS-MAX-LINE                                         
              PERFORM 4100-OUTPUT-HEADER         THRU 4100-EXIT                 
              ADD 1                                TO WS-LINE-CTR               
           END-IF.                                                              
                                                                                
           MOVE SPACES                             TO LD37-DETAIL.              
           INITIALIZE                                 LD37-DETAIL.              
                                                                                
           ADD 1                                   TO WS-SNO.                   
           MOVE WS-SNO                             TO LD37-SNO.                 
                                                                                
           MOVE NUM-RGSTRN OF DCLGRANT-ASMNT       TO KA30-NUM-REGN             
           PERFORM 8001-READ-BP13KA30            THRU 8001-EXIT                 
           MOVE KA30-NUM-PRJCT                     TO LD37-NUM-PRJCT            
                                                                                
           MOVE NUM-RGSTRN OF DCLGRANT-ASMNT       TO LD37-REGN-NO.             
                                                                                
           STRING WS-DTE-SUBMIT(9:2) '/'                                        
                  WS-DTE-SUBMIT(6:2) '/'                                        
                  WS-DTE-SUBMIT(1:4)                                            
                  DELIMITED BY SIZE INTO LD37-DTE-REQ.                          
                                                                                
           IF NUM-GRANT-AMOUNT-ELGBL OF DCLGRANT-ASMNT = '4' AND                
              NUM-ELGBL-GRANT OF DCLGRANT-ASMNT  = 'Y' AND                      
              NUM-GRANT-TYPE OF DCLGRANT-ASMNT  = 'TUG'                         
              MOVE '$10,000 (CTZ TOP-UP)'          TO LD37-GRNT-TYPE            
              ADD 1                                TO WS-GR-4A-CNT              
           ELSE                                                                 
              EVALUATE NUM-GRANT-AMOUNT-ELGBL OF DCLGRANT-ASMNT                 
                  WHEN '1'                                                      
                       MOVE "$30,000 (SC/SC)  "    TO LD37-GRNT-TYPE            
                       ADD 1                       TO WS-GR-1-CNT               
                  WHEN '2'                                                      
                       IF NUM-HSHLD-CTZNSP OF DCLGRANT-ASMNT = 'SCP'            
                          MOVE "$20,000 (SCP)    " TO LD37-GRNT-TYPE            
                          ADD 1                    TO WS-GR-2A-CNT              
                       ELSE                                                     
                          IF NUM-HSHLD-CTZNSP OF DCLGRANT-ASMNT = 'SCH'         
                             MOVE "$20,000 (SCH) " TO LD37-GRNT-TYPE            
                             ADD 1                 TO WS-GR-2B-CNT              
                          END-IF                                                
                       END-IF                                                   
                  WHEN '3'                                                      
                       MOVE "$15,000 (HALF-GRANT)" TO LD37-GRNT-TYPE            
                       ADD 1                       TO WS-GR-3-CNT               
                  WHEN '4'                                                      
                       PERFORM 4001-GET-HH-TYPE  THRU 4001-EXIT                 
                       IF WS-HH-TYPE = 'G'                                      
                          MOVE "$10,000 (HALF-GRANT)" TO LD37-GRNT-TYPE         
                          ADD 1                       TO WS-GR-4B-CNT           
                       ELSE                                                     
                          IF NUM-HSHLD-CTZNSP OF DCLGRANT-ASMNT = 'SCP'         
                             MOVE "$10,000 (SCP) " TO LD37-GRNT-TYPE            
                             ADD 1                      TO WS-GR-4C-CNT         
                          ELSE                                                  
                           IF NUM-HSHLD-CTZNSP OF DCLGRANT-ASMNT = 'SCH'        
                              MOVE "$10,000 (SCH) " TO LD37-GRNT-TYPE           
                              ADD 1                 TO WS-GR-4D-CNT             
                           END-IF                                               
                          END-IF                                                
                       END-IF                                                   
                  WHEN '5'                                                      
                       MOVE "$5,000  (SC/SC)  "    TO LD37-GRNT-TYPE            
                       ADD 1                       TO WS-GR-5-CNT               
                  WHEN OTHER                                                    
                       STRING 'INVALID TYPE ('                                  
                            NUM-GRANT-AMOUNT-ELGBL OF DCLGRANT-ASMNT ')'        
                              DELIMITED BY SIZE  INTO LD37-GRNT-TYPE            
                       ADD 1                       TO WS-GR-OTHER               
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           WRITE BP13LD37-REC FROM LD37-DETAIL.                                 
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4001-GET-HH-TYPE.                                                        
      ******************************************************************        
                                                                                
            MOVE SPACES                       TO WS-HH-TYPE.                    
                                                                                
            MOVE NUM-RGSTRN OF DCLGRANT-ASMNT TO K730-KEY-NEW                   
            PERFORM 8002-READ-BP13K730      THRU 8002-EXIT                      
            IF K730-FND                                                         
               MOVE K730-CDE-HOUSEHOLD        TO WS-HH-TYPE                     
            ELSE                                                                
               MOVE NUM-RGSTRN OF DCLGRANT-ASMNT TO K300-NUM-REGN               
               PERFORM 8003-READ-BP18K300   THRU 8003-EXIT                      
               IF K300-FND                                                      
                  MOVE K300-NUM-HSHLD         TO WS-HH-TYPE                     
               END-IF                                                           
            END-IF.                                                             
                                                                                
       4001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4100-OUTPUT-HEADER.                                                      
      ******************************************************************        
                                                                                
           ADD 1                                   TO WS-PAGE-CTR               
           MOVE WS-PAGE-CTR                        TO LD37-PAGE-NO              
                                                                                
           STRING F001-DTE-CURRENT(7:2) '/'                                     
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
                  DELIMITED BY SIZE INTO LD37-CURR-DATE.                        
                                                                                
           STRING WS-DTE-START(7:2) '/'                                         
                  WS-DTE-START(5:2) '/'                                         
                  WS-DTE-START(1:4)                                             
                  DELIMITED BY SIZE INTO LD37-DTE-START.                        
                                                                                
           STRING WS-DTE-END(7:2) '/'                                           
                  WS-DTE-END(5:2) '/'                                           
                  WS-DTE-END(1:4)                                               
                  DELIMITED BY SIZE INTO LD37-DTE-END.                          
                                                                                
           MOVE SPACES                      TO BP13LD37-REC.                    
           WRITE BP13LD37-REC AFTER PAGE.                                       
           WRITE BP13LD37-REC FROM LD37-MAIN-TITLE AFTER 3.                     
           WRITE BP13LD37-REC FROM LD37-SUB-TITLE.                              
           WRITE BP13LD37-REC FROM LD37-HEADER AFTER 2.                         
           WRITE BP13LD37-REC FROM LD37-DASH.                                   
                                                                                
           MOVE 8                           TO WS-LINE-CTR.                     
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4200-OUTPUT-SUMMARY.                                                     
      ******************************************************************        
                                                                                
           ADD 9                            TO WS-LINE-CTR.                     
           IF WS-LINE-CTR > WS-MAX-LINE                                         
              PERFORM 4100-OUTPUT-HEADER  THRU 4100-EXIT                        
              ADD 9                         TO WS-LINE-CTR                      
           END-IF.                                                              
                                                                                
           WRITE BP13LD37-REC FROM LD37-SUM-TITLE AFTER 2.                      
           WRITE BP13LD37-REC FROM LD37-SUM-DASH.                               
      **                                                                        
           MOVE '1'                         TO LD37-SUM-GRANT-TYPE              
           MOVE WS-GR-1-CNT                 TO LD37-SUM-GRANT-CNT               
           MOVE '$30,000 (SC/SC)     '      TO LD37-SUM-GRANT-DESC              
           WRITE BP13LD37-REC FROM LD37-SUM-DETAIL                              
      **                                                                        
           MOVE '2'                         TO LD37-SUM-GRANT-TYPE              
           MOVE WS-GR-2A-CNT                TO LD37-SUM-GRANT-CNT               
           MOVE '$20,000 (SCP)       '      TO LD37-SUM-GRANT-DESC              
           WRITE BP13LD37-REC FROM LD37-SUM-DETAIL                              
      **                                                                        
           MOVE '2'                         TO LD37-SUM-GRANT-TYPE              
           MOVE WS-GR-2B-CNT                TO LD37-SUM-GRANT-CNT               
           MOVE '$20,000 (SCH)       '      TO LD37-SUM-GRANT-DESC              
           WRITE BP13LD37-REC FROM LD37-SUM-DETAIL                              
      **                                                                        
           MOVE '3'                         TO LD37-SUM-GRANT-TYPE              
           MOVE WS-GR-3-CNT                 TO LD37-SUM-GRANT-CNT               
           MOVE '$15,000 (HALF-GRANT)'      TO LD37-SUM-GRANT-DESC              
           WRITE BP13LD37-REC FROM LD37-SUM-DETAIL.                             
      **                                                                        
           MOVE '4'                         TO LD37-SUM-GRANT-TYPE              
           MOVE WS-GR-4A-CNT                TO LD37-SUM-GRANT-CNT               
           MOVE '$10,000 (CTZ TOP-UP)'      TO LD37-SUM-GRANT-DESC              
           WRITE BP13LD37-REC FROM LD37-SUM-DETAIL.                             
      **                                                                        
           MOVE '4'                         TO LD37-SUM-GRANT-TYPE              
           MOVE WS-GR-4B-CNT                TO LD37-SUM-GRANT-CNT               
           MOVE '$10,000 (HALF-GRANT)'      TO LD37-SUM-GRANT-DESC              
           WRITE BP13LD37-REC FROM LD37-SUM-DETAIL.                             
      **                                                                        
           MOVE '4'                         TO LD37-SUM-GRANT-TYPE              
           MOVE WS-GR-4C-CNT                TO LD37-SUM-GRANT-CNT               
           MOVE '$10,000 (SCP)       '      TO LD37-SUM-GRANT-DESC              
           WRITE BP13LD37-REC FROM LD37-SUM-DETAIL.                             
      **                                                                        
           MOVE '4'                         TO LD37-SUM-GRANT-TYPE              
           MOVE WS-GR-4D-CNT                TO LD37-SUM-GRANT-CNT               
           MOVE '$10,000 (SCH)       '      TO LD37-SUM-GRANT-DESC              
           WRITE BP13LD37-REC FROM LD37-SUM-DETAIL.                             
      **                                                                        
           MOVE '5'                         TO LD37-SUM-GRANT-TYPE              
           MOVE WS-GR-5-CNT                 TO LD37-SUM-GRANT-CNT               
           MOVE '$5,000  (SC/SC)     '      TO LD37-SUM-GRANT-DESC              
           WRITE BP13LD37-REC FROM LD37-SUM-DETAIL.                             
      **                                                                        
           MOVE '*'                         TO LD37-SUM-GRANT-TYPE              
           MOVE WS-GR-OTHER                 TO LD37-SUM-GRANT-CNT               
           MOVE 'INVALID GRANT TYPE  '      TO LD37-SUM-GRANT-DESC              
           WRITE BP13LD37-REC FROM LD37-SUM-DETAIL.                             
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       8001-READ-BP13KA30.                                                      
      ******************************************************************        
                                                                                
            READ BP13KA30 KEY IS KA30-NUM-REGN.                                 
                                                                                
            EVALUATE WS-KA30-STATUS                                             
                WHEN 00                                                         
                     CONTINUE                                                   
                                                                                
                WHEN 23                                                         
                     MOVE SPACES                      TO KA30-NUM-PRJCT         
                                                                                
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13KA30 : ' WS-KA30-STATUS         
                             ' REGN NO = ' KA30-NUM-REGN                        
                     PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT            
            END-EVALUATE.                                                       
                                                                                
       8001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       8002-READ-BP13K730.                                                      
      ******************************************************************        
                                                                                
            READ BP13K730.                                                      
                                                                                
            EVALUATE WS-K730-STATUS                                             
                WHEN 00                                                         
                     MOVE 'Y'                 TO WS-K730-FND                    
                WHEN 23                                                         
                     MOVE 'N'                 TO WS-K730-FND                    
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K730 : ' WS-K730-STATUS         
                             ' REGN NO = ' K730-KEY-NEW                         
                     PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT            
            END-EVALUATE.                                                       
                                                                                
       8002-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       8003-READ-BP18K300.                                                      
      ******************************************************************        
                                                                                
            READ BP18K300.                                                      
                                                                                
            EVALUATE WS-K300-STATUS                                             
                WHEN 00                                                         
                     MOVE 'Y'                 TO WS-K300-FND                    
                WHEN 23                                                         
                     MOVE 'N'                 TO WS-K300-FND                    
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP18K300 : ' WS-K300-STATUS         
                             ' REGN NO = ' K300-NUM-REGN                        
                     PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT            
            END-EVALUATE.                                                       
                                                                                
       8003-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CD37'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1)  NO OF GRANT_ASMNT RECORDS READ............. : '        
                    CNT-DB2-READ.                                               
                                                                                
           CLOSE    SY02F001                                                    
                    BP13KA30                                                    
                    BP13K730                                                    
                    BP18K300                                                    
                    BP13LD37.                                                   
                                                                                
           IF WS-KA30-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KA30 - ERROR CLOSING : ' WS-KA30-STATUS              
              MOVE WS-KA30-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K730 - ERROR CLOSING : ' WS-K730-STATUS              
              MOVE WS-K730-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K300-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP18K300 - ERROR CLOSING : ' WS-K300-STATUS              
              MOVE WS-K300-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9998-DB2-ERROR.                                                          
      *****************************************************************         
           CALL 'DBATIAR' USING SQLCA.                                          
           DISPLAY 'QUERY NO. THAT CAUSED ABEND = ' WS-QUERY-NO                 
           PERFORM 9999-CLOSE-ROUTINE   THRU 9999-EXIT                          
           MOVE 12 TO RETURN-CODE                                               
           STOP RUN.                                                            
                                                                                
       9998-EXIT.                                                               
           EXIT.                                                                
                                                                                
