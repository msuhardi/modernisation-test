       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CK08.                                                 
       AUTHOR.        TRM1.                                                     
       DATE-WRITTEN.  07/11/2008.                                               
                                                                                
      *************************************************************             
      *  SYSTEM NAME : SYSTEM OF COMMITMENT (BP13)                *             
      * ========================================================= *             
      *  PROGRAM : BP13CK08                                       *             
      *  PURPOSE : EXTRACT DETAIL RECORDS FOR THE EXERCISES       *             
      *                                                           *             
      *  INPUT   : BP13K205                                       *             
      *            BP13KB78                                       *             
      *            BP13KB86                                       *             
      *            BP13K757                                       *             
      *            BP13K816                                       *             
      *            BP13K730                                       *             
      *            BP13K764                                       *             
      *                                                           *             
      *  OUTPUT  : BP13LK08                                       *             
      *            BP13F730                                       *             
      * ========================================================= *             
      *  REVISION HISTORY :                                       *             
      *  CHGE-NO   OIC   DATE        DESCRIPTION                  *             
      *  --------  ----  ----------  ---------------------------  *             
      *  BP133484  TRM1  07/11/2008  NEW PROGRAM                  *             
      *  BP135254  SMR2  21/04/2014  REPLACE BP13K816 W/ BP13K813 *             
      *  BP139280  EAA2  01/11/2023  EXPAND BP13K757 FROM 3000    *             
      *                                  TO 4000                  *             
      *************************************************************             
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K205     ASSIGN       TO BP13K205                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS SEQUENTIAL                       
                               RECORD KEY   IS K205-KEY-FLD                     
                               FILE STATUS  IS WS-K205-STATUS.                  
                                                                                
           SELECT BP13KB78     ASSIGN       TO BP13KB78                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS SEQUENTIAL                       
                               RECORD KEY   IS KB78-KEY-FLD                     
                               FILE STATUS  IS WS-KB78-STATUS.                  
                                                                                
           SELECT BP13KB86     ASSIGN       TO BP13KB86                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS RANDOM                           
                               RECORD KEY   IS KB86-KEY-FLD                     
                               FILE STATUS  IS WS-KB86-STATUS.                  
                                                                                
           SELECT BP13K757     ASSIGN       TO BP13K757                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS SEQUENTIAL                       
                               RECORD KEY   IS K757-KEY-FLD                     
                               FILE STATUS  IS WS-K757-STATUS.                  
                                                                                
           SELECT BP13K813     ASSIGN       TO BP13K813                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS DYNAMIC                          
                               RECORD KEY   IS K813-KEY-FLD                     
                               FILE STATUS  IS WS-K813-STATUS.                  
                                                                                
           SELECT BP13K730     ASSIGN       TO BP13K730                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS RANDOM                           
                               RECORD KEY   IS K730-KEY-NEW                     
                               FILE STATUS  IS WS-K730-STATUS.                  
                                                                                
           SELECT BP13K764     ASSIGN       TO BP13K764                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS RANDOM                           
                               RECORD KEY   IS K764-KEY-FLD                     
                               FILE STATUS  IS WS-K764-STATUS.                  
                                                                                
           SELECT BP13LK08     ASSIGN       TO BP13LK08.                        
           SELECT BP13F730     ASSIGN       TO BP13F730.                        
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13K205                                                            
            RECORD             CONTAINS 80 CHARACTERS.                          
       COPY BP13K205.                                                           
                                                                                
       FD   BP13KB78                                                            
            RECORD             CONTAINS 3300 CHARACTERS.                        
       COPY BP13KB78.                                                           
                                                                                
       FD   BP13KB86                                                            
            RECORD             CONTAINS 150 CHARACTERS.                         
       COPY BP13KB86.                                                           
                                                                                
       FD   BP13K757                                                            
            RECORD             CONTAINS 4000 CHARACTERS.                        
       COPY BP13K757.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD             CONTAINS 1000 CHARACTERS.                        
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD             CONTAINS 500 CHARACTERS.                         
       COPY BP13K730.                                                           
                                                                                
       FD   BP13K764                                                            
            RECORD             CONTAINS 2000 CHARACTERS.                        
       COPY BP13K764.                                                           
                                                                                
       FD   BP13LK08                                                            
            BLOCK              CONTAINS 0   RECORDS                             
            RECORD             CONTAINS 132 CHARACTERS                          
            LABEL              RECORDS  ARE OMITTED                             
            RECORDING          MODE     IS  F.                                  
       01   BP13LK08-REC                PIC X(132).                             
                                                                                
       FD   BP13F730                                                            
            BLOCK              CONTAINS 0   RECORDS                             
            RECORD             CONTAINS 500 CHARACTERS                          
            LABEL              RECORDS  ARE STANDARD                            
            RECORDING          MODE     IS  F.                                  
       COPY BP13F730.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-VARIABLES.                                                        
           05  WS-START                 PIC 9(8)    VALUE ZEROES.               
           05  WS-END                   PIC 9(8)    VALUE ZEROES.               
           05  WS-DATE                  PIC 9(8)    VALUE ZEROES.               
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CURR-CC                PIC 99      VALUE ZEROES.               
           05 WS-CURR-YY                PIC 99      VALUE ZEROES.               
           05 WS-CURR-MM                PIC 99      VALUE ZEROES.               
           05 WS-CURR-DD                PIC 99      VALUE ZEROES.               
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K205-STATUS           PIC XX      VALUE SPACES.               
           05  WS-KB78-STATUS           PIC XX      VALUE SPACES.               
           05  WS-KB86-STATUS           PIC XX      VALUE SPACES.               
           05  WS-K757-STATUS           PIC XX      VALUE SPACES.               
           05  WS-K813-STATUS           PIC XX      VALUE SPACES.               
           05  WS-K730-STATUS           PIC XX      VALUE SPACES.               
           05  WS-K764-STATUS           PIC XX      VALUE SPACES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-KB78-EOF              PIC X       VALUE 'N'.                  
           05  WS-K757-EOF              PIC X       VALUE 'N'.                  
           05  WS-K813-EOF              PIC X       VALUE 'N'.                  
           05  WS-KB86-VALID            PIC X       VALUE 'N'.                  
           05  WS-K757-FOUND            PIC X       VALUE 'N'.                  
           05  WS-K730-FOUND            PIC X       VALUE 'N'.                  
           05  WS-EFT-VALID             PIC X       VALUE 'N'.                  
           05  WS-DTE-BALLOT-VALID      PIC X       VALUE 'N'.                  
                                                                                
       01  WS-CTRS.                                                             
           05  WS-K205-READ-CTR         PIC 9(8)    VALUE ZEROES.               
           05  WS-KB78-READ-CTR         PIC 9(8)    VALUE ZEROES.               
           05  WS-K757-READ-CTR         PIC 9(8)    VALUE ZEROES.               
           05  WS-KB78-VALID-CTR        PIC 9(8)    VALUE ZEROES.               
           05  WS-K757-FOUND-CTR        PIC 9(8)    VALUE ZEROES.               
           05  WS-K730-FOUND-CTR        PIC 9(8)    VALUE ZEROES.               
           05  WS-K730-NOTFOUND-CTR     PIC 9(8)    VALUE ZEROES.               
           05  WS-EI-CTR                PIC 99      VALUE ZEROES.               
           05  WS-PAGE-CTR              PIC 9(4)    VALUE ZEROES.               
           05  WS-LINE-CTR              PIC 9(2)    VALUE 60.                   
           05  WS-SNO-CTR               PIC 9(5)    VALUE ZEROES.               
           05  WS-LK08-WRITE-CTR        PIC 9(8)    VALUE ZEROES.               
           05  WS-F730-WRITE-CTR        PIC 9(8)    VALUE ZEROES.               
                                                                                
       01  WS-H0.                                                               
           05  FILLER                   PIC X(8)    VALUE 'BP13LK08'.           
           05  FILLER                   PIC X(38)   VALUE SPACES.               
           05  FILLER                   PIC X(39)   VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(30)   VALUE SPACES.               
           05  FILLER                   PIC X(7)    VALUE 'DATE : '.            
           03  WS-H0-CURR-DD            PIC X(2)    VALUE SPACES.               
           03  FILLER                   PIC X(1)    VALUE '/'.                  
           03  WS-H0-CURR-MM            PIC X(2)    VALUE SPACES.               
           03  FILLER                   PIC X(1)    VALUE '/'.                  
           03  WS-H0-CURR-CC            PIC X(2)    VALUE SPACES.               
           03  WS-H0-CURR-YY            PIC X(2)    VALUE SPACES.               
                                                                                
       01  WS-H1.                                                               
           05  FILLER                   PIC X(41)   VALUE SPACES.               
           05  FILLER                   PIC X(46)   VALUE                       
               'EXCEPTIONAL REPORT BEFORE BALLOT COMMENCE FOR '.                
           05  WS-H1-XXX                PIC X(3)    VALUE SPACES.               
           05  FILLER                   PIC X(25)   VALUE SPACES.               
           05  FILLER                   PIC X(7)    VALUE 'PAGE : '.            
           05  WS-H1-PAGE               PIC ZZZ9    VALUE SPACES.               
           05  FILLER                   PIC X(6)    VALUE SPACES.               
                                                                                
       01  WS-H2.                                                               
           05  FILLER                   PIC X(46)   VALUE                       
               'INVALID DTE BALLOT/ZONE/ESTATE/FLAT TYPE FOUND'.                
           05  FILLER                   PIC X(86)   VALUE SPACES.               
                                                                                
       01  WS-H3.                                                               
           05  FILLER                   PIC X(46)   VALUE                       
               ' SNO     REGNO       DTE BALLOT    ZONE/ESTATE'.                
           05  FILLER                   PIC X(24)   VALUE                       
               '    FLAT TYPE    REMARKS'.                                      
           05  FILLER                   PIC X(62)   VALUE SPACES.               
                                                                                
       01  WS-LINE                      PIC X(132)  VALUE ALL '-'.              
       01  WS-BLANK                     PIC X(132)  VALUE SPACES.               
                                                                                
       01  WS-DETAIL.                                                           
           05  WS-SNO                   PIC X(5)    VALUE SPACES.               
           05  FILLER                   PIC X(4)    VALUE SPACES.               
           05  WS-REGNO                 PIC X(8)    VALUE SPACES.               
           05  FILLER                   PIC X(6)    VALUE SPACES.               
           05  WS-DTE-BALLOT            PIC X(6)    VALUE SPACES.               
           05  FILLER                   PIC X(10)   VALUE SPACES.               
           05  WS-ESTATE-ZONE           PIC X(3)    VALUE SPACES.               
           05  FILLER                   PIC X(11)   VALUE SPACES.               
           05  WS-FLAT-TYPE             PIC X(2)    VALUE SPACES.               
           05  FILLER                   PIC X(8)    VALUE SPACES.               
           05  WS-REMARKS               PIC X(69)   VALUE SPACES.               
                                                                                
       PROCEDURE DIVISION.                                                      
      ************                                                              
       0000-MAIN.                                                               
      ************                                                              
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-MAIN-ROUTINE    THRU 2000-EXIT                          
             UNTIL WS-K757-EOF = 'Y' OR WS-KB78-EOF = 'Y'.                      
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1000-OPEN-FILES.                                                         
      ******************                                                        
           OPEN INPUT  BP13K205                                                 
                       BP13KB78                                                 
                       BP13KB86                                                 
                       BP13K757                                                 
                       BP13K813                                                 
                       BP13K730                                                 
                       BP13K764                                                 
                OUTPUT BP13LK08                                                 
                       BP13F730.                                                
                                                                                
           IF WS-K205-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN OPENING BP13K205: STATUS - '                    
              WS-K205-STATUS                                                    
              MOVE WS-K205-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-KB78-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN OPENING BP13KB78: STATUS - '                    
              WS-KB78-STATUS                                                    
              MOVE WS-KB78-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-KB86-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN OPENING BP13KB86: STATUS - '                    
              WS-KB86-STATUS                                                    
              MOVE WS-KB86-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K757-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN OPENING BP13K757: STATUS - '                    
              WS-K757-STATUS                                                    
              MOVE WS-K757-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN OPENING BP13K813: STATUS - '                    
              WS-K813-STATUS                                                    
              MOVE WS-K813-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN OPENING BP13K730: STATUS - '                    
              WS-K730-STATUS                                                    
              MOVE WS-K730-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K764-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN OPENING BP13K764: STATUS - '                    
              WS-K764-STATUS                                                    
              MOVE WS-K764-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO BP13K205-REC                      
                                              BP13KB78-REC                      
                                              BP13KB86-REC                      
                                              BP13K757-REC                      
                                              BP13K813-REC                      
                                              BP13K730-REC                      
                                              BP13K764-REC                      
                                              BP13LK08-REC                      
                                              BP13F730-REC.                     
                                                                                
           INITIALIZE                         BP13K205-REC                      
                                              BP13KB78-REC                      
                                              BP13KB86-REC                      
                                              BP13K757-REC                      
                                              BP13K813-REC                      
                                              BP13K730-REC                      
                                              BP13K764-REC                      
                                              BP13LK08-REC                      
                                              BP13F730-REC.                     
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CURR-DATE.                     
           MOVE WS-CURR-DD                 TO WS-H0-CURR-DD.                    
           MOVE WS-CURR-MM                 TO WS-H0-CURR-MM.                    
           MOVE WS-CURR-CC                 TO WS-H0-CURR-CC.                    
           MOVE WS-CURR-YY                 TO WS-H0-CURR-YY.                    
                                                                                
           PERFORM 3000-READ-BP13K205      THRU 3000-EXIT                       
             UNTIL K205-NUM-OPTION = 'S5'.                                      
           IF K205-NUM-SELECTION = 'DBS'                                        
              PERFORM 4000-READ-BP13KB78   THRU 4000-EXIT                       
           ELSE                                                                 
              PERFORM 5000-READ-BP13K757   THRU 5000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       2000-MAIN-ROUTINE.                                                       
      ********************                                                      
           IF K205-NUM-SELECTION = 'DBS'                                        
              IF K205-DTE-RESELECTION(1:3) = KB78-NUM-PROJECT AND               
                 K205-DTE-ALLOCN = KB78-DTE-REQUESTED                           
                 MOVE 'N'                        TO WS-KB86-VALID               
                 MOVE 'N'                        TO WS-K730-FOUND               
                 MOVE KB78-NUM-REGN              TO KB86-NUM-REGN               
                 MOVE KB78-NUM-APPLT1-UIN        TO KB86-NUM-UIN                
                 PERFORM 4100-READ-BP13KB86    THRU 4100-EXIT                   
                 IF WS-KB86-VALID = 'Y'                                         
                    ADD 1                        TO WS-KB78-VALID-CTR           
                    MOVE KB78-NUM-REGN           TO K730-NUM-REGN               
                    PERFORM 5300-READ-BP13K730   THRU 5300-EXIT                 
                    IF WS-K730-FOUND = 'N'                                      
                       ADD 1                  TO WS-K730-NOTFOUND-CTR           
                       MOVE                                                     
                       'RECORD FOUND IN BP13KB78 BUT NOT IN BP13K730'           
                                              TO WS-REMARKS                     
                       PERFORM 7000-WRITE-BP13LK08    THRU 7000-EXIT            
                    ELSE                                                        
                       ADD 1                     TO WS-K730-FOUND-CTR           
                       IF K730-CDE-NT1 = KB78-NUM-NEW-TOWN                      
                         IF K730-CDE-FLAT-TYPE NOT = KB78-NUM-FLAT-TYPE         
                          MOVE 'INVALID FLAT TYPE'        TO WS-REMARKS         
                          PERFORM 7000-WRITE-BP13LK08   THRU 7000-EXIT          
                         END-IF                                                 
                       ELSE                                                     
                          MOVE 'INVALID ZONE'             TO WS-REMARKS         
                         IF K730-CDE-FLAT-TYPE NOT = KB78-NUM-FLAT-TYPE         
                            MOVE 'INVALID ZONE AND FLAT TYPE'                   
                                                          TO WS-REMARKS         
                         END-IF                                                 
                          PERFORM 7000-WRITE-BP13LK08   THRU 7000-EXIT          
                       END-IF                                                   
                       PERFORM 8000-WRITE-BP13F730      THRU 8000-EXIT          
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
              PERFORM 4000-READ-BP13KB78              THRU 4000-EXIT            
           ELSE                                                                 
              IF K757-NUM-CANCEL NOT = 'Y'                                      
                 MOVE 'N'                        TO WS-K757-FOUND               
                 IF K205-NUM-SELECTION = K757-NUM-SALES-MODE AND                
                    K205-DTE-BALLOT    = K757-DTE-BALLOT                        
                    MOVE K205-DTE-ALLOCN         TO WS-START                    
                    IF K205-DTE-ALLOCN-NEW = SPACES                             
                       MOVE WS-CURR-DATE         TO WS-END                      
                    ELSE                                                        
                       MOVE K205-DTE-ALLOCN-NEW  TO WS-END                      
                    END-IF                                                      
                    MOVE K757-DTE-REQUESTED      TO WS-DATE                     
                                                                                
                    IF WS-DATE >= WS-START AND WS-DATE <= WS-END                
                       IF K205-NUM-SELECTION = 'BTO'                            
                          PERFORM 5100-START-BP13K813   THRU 5100-EXIT          
                       ELSE                                                     
                          MOVE 'Y'               TO WS-K757-FOUND               
                       END-IF                                                   
                    END-IF                                                      
                                                                                
                    IF WS-K757-FOUND = 'Y'                                      
                       ADD 1                     TO WS-K757-FOUND-CTR           
                       MOVE 'N'                  TO WS-K730-FOUND               
                       MOVE K757-NUM-REGN        TO K730-NUM-REGN               
                       PERFORM 5300-READ-BP13K730   THRU 5300-EXIT              
                                                                                
                       IF WS-K730-FOUND = 'N'                                   
                          ADD 1                 TO WS-K730-NOTFOUND-CTR         
                          MOVE                                                  
                         'RECORD FOUND IN BP13K757 BUT NOT IN BP13K730'         
                                                TO WS-REMARKS                   
                          PERFORM 7000-WRITE-BP13LK08    THRU 7000-EXIT         
                       ELSE                                                     
                          ADD 1                  TO WS-K730-FOUND-CTR           
                          MOVE SPACES            TO K730-CDE-ELIG-TAG           
                          IF K730-NUM-ALLO-CAT = 'BTO'                          
                             PERFORM 6000-VALIDATE-ZFT   THRU 6000-EXIT         
                          ELSE                                                  
                             PERFORM 6100-VALIDATE-EFT   THRU 6100-EXIT         
                          END-IF                                                
                          PERFORM 8000-WRITE-BP13F730    THRU 8000-EXIT         
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
              PERFORM 5000-READ-BP13K757              THRU 5000-EXIT            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       3000-READ-BP13K205.                                                      
      *********************                                                     
           READ BP13K205                                                        
             AT END                                                             
             PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                          
           END-READ.                                                            
                                                                                
           ADD 1                          TO WS-K205-READ-CTR.                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4000-READ-BP13KB78.                                                      
      *********************                                                     
           READ BP13KB78                                                        
             AT END MOVE 'Y'              TO WS-KB78-EOF                        
             GO TO 4000-EXIT                                                    
           END-READ.                                                            
                                                                                
           ADD 1                          TO WS-KB78-READ-CTR.                  
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4100-READ-BP13KB86.                                                      
      *********************                                                     
           READ BP13KB86.                                                       
           EVALUATE WS-KB86-STATUS                                              
             WHEN '00'                                                          
               IF KB86-NUM-STATUS NOT = 'C'                                     
                  MOVE 'Y'                  TO WS-KB86-VALID                    
               END-IF                                                           
             WHEN '10'                                                          
             WHEN '23'                                                          
               MOVE 'RECORD NOT FOUND IN BP13KB86'                              
                                               TO WS-REMARKS                    
               PERFORM 7000-WRITE-BP13LK08   THRU 7000-EXIT                     
             WHEN OTHER                                                         
               DISPLAY 'BP13KB86 READ ERROR: STATUS - '                         
               WS-KB86-STATUS                                                   
               MOVE WS-KB86-STATUS          TO RETURN-CODE                      
               PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       5000-READ-BP13K757.                                                      
      *********************                                                     
           READ BP13K757                                                        
             AT END MOVE 'Y'              TO WS-K757-EOF                        
             GO TO 5000-EXIT                                                    
           END-READ.                                                            
                                                                                
           ADD 1                          TO WS-K757-READ-CTR.                  
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       5100-START-BP13K813.                                                     
      **********************                                                    
           MOVE 'N'                               TO WS-K813-EOF.               
           MOVE SPACES                            TO BP13K813-REC.              
           INITIALIZE                                BP13K813-REC.              
           MOVE K205-DTE-RESELECTION(1:3)         TO K813-NUM-ZONE.             
           START BP13K813 KEY >= K813-NUM-ZONE.                                 
           EVALUATE WS-K813-STATUS                                              
             WHEN '00'                                                          
               PERFORM 5200-READNEXT-BP13K813   THRU 5200-EXIT                  
                 UNTIL K813-NUM-ZONE = K205-DTE-RESELECTION(4:3) OR             
                 WS-K813-EOF = 'Y'                                              
             WHEN '10'                                                          
             WHEN '23'                                                          
               MOVE 'Y'                           TO WS-K813-EOF                
             WHEN OTHER                                                         
               DISPLAY 'BP13K813 START ERROR: STATUS - '                        
               WS-K813-STATUS                                                   
               MOVE WS-K813-STATUS                TO RETURN-CODE                
               PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************                                                 
       5200-READNEXT-BP13K813.                                                  
      *************************                                                 
           READ BP13K813 NEXT RECORD.                                           
           EVALUATE WS-K813-STATUS                                              
             WHEN '00'                                                          
               IF K813-NUM-ZONE = K757-NUM-ESTATE-ZONE                          
                  MOVE 'Y'                  TO WS-K757-FOUND                    
               END-IF                                                           
             WHEN '10'                                                          
             WHEN '23'                                                          
               MOVE 'Y'                     TO WS-K813-EOF                      
             WHEN OTHER                                                         
               DISPLAY 'ERROR IN READING BP13K813: STATUS - '                   
               WS-K813-STATUS                                                   
               MOVE WS-K813-STATUS          TO RETURN-CODE                      
               PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       5300-READ-BP13K730.                                                      
      *********************                                                     
           READ BP13K730.                                                       
           EVALUATE WS-K730-STATUS                                              
             WHEN '00'                                                          
               MOVE 'Y'                     TO WS-K730-FOUND                    
             WHEN '10'                                                          
             WHEN '23'                                                          
               CONTINUE                                                         
             WHEN OTHER                                                         
               DISPLAY 'BP13K730 READ ERROR: STATUS - '                         
               WS-K730-STATUS                                                   
               MOVE WS-K730-STATUS          TO RETURN-CODE                      
               PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       6000-VALIDATE-ZFT.                                                       
      ********************                                                      
           MOVE SPACES                         TO BP13K813-REC.                 
           INITIALIZE                             BP13K813-REC.                 
           MOVE K730-CDE-NT1                   TO K813-NUM-ZONE.                
           MOVE K730-CDE-FLAT-TYPE             TO K813-NUM-FLAT-TYPE.           
           MOVE K730-DTE-BALLOT                TO K813-DTE-BALLOT.              
                                                                                
           READ BP13K813.                                                       
           EVALUATE WS-K813-STATUS                                              
             WHEN '00'                                                          
               CONTINUE                                                         
             WHEN '10'                                                          
             WHEN '23'                                                          
               PERFORM 6010-STARTBR-K813     THRU 6010-EXIT                     
             WHEN OTHER                                                         
               DISPLAY 'ERROR IN READING BP13K813: STATUS - '                   
               WS-K813-STATUS                                                   
               MOVE WS-K813-STATUS             TO RETURN-CODE                   
               PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       6010-STARTBR-K813.                                                       
      ********************                                                      
           MOVE SPACES                      TO BP13K813-REC.                    
           INITIALIZE                          BP13K813-REC.                    
           MOVE K730-CDE-NT1                TO K813-NUM-ZONE.                   
                                                                                
           START BP13K813 KEY >= K813-NUM-ZONE.                                 
           EVALUATE WS-K813-STATUS                                              
           WHEN '00'                                                            
              PERFORM 6020-READNEXT-K813    THRU 6020-EXIT                      
                                                                                
           WHEN '10'                                                            
           WHEN '23'                                                            
              MOVE 'INVALID ZONE'           TO WS-REMARKS                       
              PERFORM 7000-WRITE-BP13LK08   THRU 7000-EXIT                      
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K813 START ERROR: STATUS - ' WS-K813-STATUS          
              MOVE WS-K813-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       6010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       6020-READNEXT-K813.                                                      
      *********************                                                     
           READ BP13K813 NEXT RECORD.                                           
           EVALUATE WS-K813-STATUS                                              
           WHEN '00'                                                            
              IF K813-NUM-ZONE = K730-CDE-NT1                                   
                 IF K813-DTE-BALLOT = K730-DTE-BALLOT                           
                    IF K813-NUM-FLAT-TYPE(1:1) NOT =                            
                       K730-CDE-FLAT-TYPE(1:1)                                  
                       MOVE 'INVALID FLAT TYPE'    TO WS-REMARKS                
                       PERFORM 7000-WRITE-BP13LK08 THRU 7000-EXIT               
                    END-IF                                                      
                 ELSE                                                           
                    MOVE 'INVALID DATE BALLOT'     TO WS-REMARKS                
                    IF K813-NUM-FLAT-TYPE(1:1) NOT =                            
                       K730-CDE-FLAT-TYPE(1:1)                                  
                       MOVE 'INVALID DATE BALLOT AND FLAT TYPE'                 
                                                   TO WS-REMARKS                
                    END-IF                                                      
                    PERFORM 7000-WRITE-BP13LK08    THRU 7000-EXIT               
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'INVALID ZONE'               TO WS-REMARKS                
                 PERFORM 7000-WRITE-BP13LK08       THRU 7000-EXIT               
              END-IF                                                            
                                                                                
           WHEN '10'                                                            
           WHEN '23'                                                            
              MOVE 'INVALID ZONE'           TO WS-REMARKS                       
              PERFORM 7000-WRITE-BP13LK08   THRU 7000-EXIT                      
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN READING BP13K813: STATUS - '                    
                       WS-K813-STATUS                                           
              MOVE WS-K813-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       6020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       6100-VALIDATE-EFT.                                                       
      ********************                                                      
           MOVE 'N'                            TO WS-EFT-VALID                  
                                                  WS-DTE-BALLOT-VALID.          
           MOVE K730-NUM-ALLO-CAT              TO K764-NUM-SALES-MODE.          
           READ BP13K764.                                                       
           EVALUATE WS-K764-STATUS                                              
             WHEN '00'                                                          
               IF K730-DTE-BALLOT = K764-DTE-BALLOT                             
                  MOVE 'Y'                     TO WS-DTE-BALLOT-VALID           
                  IF K730-NUM-ALLO-CAT = 'BE '                                  
                     MOVE 0                    TO WS-EI-CTR                     
                     PERFORM 6110-CHECK-EI   THRU 6110-EXIT                     
                       UNTIL WS-EI-CTR = 30 OR WS-EFT-VALID = 'Y'               
                  ELSE                                                          
                     MOVE 'Y'                  TO WS-EFT-VALID                  
                  END-IF                                                        
               END-IF                                                           
             WHEN '10'                                                          
             WHEN '23'                                                          
               MOVE 'INVALID SALES MODE'       TO WS-REMARKS                    
               PERFORM 7000-WRITE-BP13LK08   THRU 7000-EXIT                     
               GO TO 6100-EXIT                                                  
             WHEN OTHER                                                         
               DISPLAY 'ERROR IN READING BP13K764: STATUS - '                   
               WS-K764-STATUS                                                   
               MOVE WS-K764-STATUS             TO RETURN-CODE                   
               PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
           IF WS-EFT-VALID = 'N'                                                
              IF WS-DTE-BALLOT-VALID = 'N'                                      
                 MOVE 'INVALID DATE BALLOT'     TO WS-REMARKS                   
              ELSE                                                              
                 MOVE 'INVALID ESTATE/FLAT TYPE'  TO WS-REMARKS                 
              END-IF                                                            
              PERFORM 7000-WRITE-BP13LK08    THRU 7000-EXIT                     
           END-IF.                                                              
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************                                                          
       6110-CHECK-EI.                                                           
      ****************                                                          
           IF K730-CDE-NT1 = K764-NUM-ESTATE-ZONE(WS-EI-CTR)     AND            
              K730-CDE-FLAT-TYPE(1:1) = K764-NUM-ROOM(WS-EI-CTR) AND            
              K730-CDE-FLAT-TYPE(2:1) = K764-NUM-ROOM-TYPE(WS-EI-CTR)           
              MOVE 'Y'                  TO WS-EFT-VALID                         
           END-IF.                                                              
           ADD 1                        TO WS-EI-CTR.                           
                                                                                
       6110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       7000-WRITE-BP13LK08.                                                     
      **********************                                                    
           IF WS-LINE-CTR > 55                                                  
              PERFORM 7100-WRITE-HEADER   THRU 7100-EXIT                        
           END-IF.                                                              
                                                                                
           ADD 1                            TO WS-SNO-CTR.                      
           MOVE WS-SNO-CTR                  TO WS-SNO.                          
                                                                                
           IF WS-K730-FOUND = 'N'                                               
              IF K205-NUM-SELECTION = 'DBS'                                     
                 MOVE KB78-NUM-REGN            TO WS-REGNO                      
                 MOVE KB78-NUM-NEW-TOWN        TO WS-ESTATE-ZONE                
                 MOVE KB78-NUM-FLAT-TYPE       TO WS-FLAT-TYPE                  
              ELSE                                                              
                 MOVE K757-NUM-REGN            TO WS-REGNO                      
                 MOVE K757-DTE-BALLOT          TO WS-DTE-BALLOT                 
                 MOVE K757-NUM-ESTATE-ZONE     TO WS-ESTATE-ZONE                
                 MOVE K757-NUM-FLAT-TYPE       TO WS-FLAT-TYPE                  
              END-IF                                                            
           ELSE                                                                 
              MOVE K730-NUM-REGN            TO WS-REGNO                         
              MOVE K730-DTE-BALLOT          TO WS-DTE-BALLOT                    
              MOVE K730-CDE-NT1             TO WS-ESTATE-ZONE                   
              MOVE K730-CDE-FLAT-TYPE       TO WS-FLAT-TYPE                     
           END-IF.                                                              
                                                                                
           WRITE BP13LK08-REC             FROM WS-DETAIL.                       
           ADD 1                            TO WS-LINE-CTR                      
                                               WS-LK08-WRITE-CTR.               
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       7100-WRITE-HEADER.                                                       
      ********************                                                      
           ADD 1                          TO WS-PAGE-CTR.                       
           MOVE WS-PAGE-CTR               TO WS-H1-PAGE.                        
           MOVE K205-NUM-SELECTION        TO WS-H1-XXX.                         
           WRITE BP13LK08-REC           FROM WS-H0 AFTER PAGE.                  
           WRITE BP13LK08-REC           FROM WS-H1.                             
           WRITE BP13LK08-REC           FROM WS-H2.                             
           WRITE BP13LK08-REC           FROM WS-BLANK.                          
           WRITE BP13LK08-REC           FROM WS-H3.                             
           WRITE BP13LK08-REC           FROM WS-LINE.                           
           MOVE 6                         TO WS-LINE-CTR.                       
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8000-WRITE-BP13F730.                                                     
      **********************                                                    
           WRITE BP13F730-REC           FROM BP13K730-REC.                      
           ADD 1                          TO WS-F730-WRITE-CTR.                 
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************                                                         
       9000-CLOSE-FILES.                                                        
      *****************                                                         
           CLOSE BP13K205                                                       
                 BP13KB78                                                       
                 BP13KB86                                                       
                 BP13K757                                                       
                 BP13K813                                                       
                 BP13K730                                                       
                 BP13K764                                                       
                 BP13LK08                                                       
                 BP13F730.                                                      
                                                                                
           IF WS-K205-STATUS NOT = '00'                                         
              DISPLAY 'ERROR IN CLOSING BP13K205: STATUS - '                    
              WS-K205-STATUS                                                    
              MOVE WS-K205-STATUS         TO RETURN-CODE                        
           END-IF.                                                              
                                                                                
           IF WS-KB78-STATUS NOT = '00'                                         
              DISPLAY 'ERROR IN CLOSING BP13KB78: STATUS - '                    
              WS-KB78-STATUS                                                    
              MOVE WS-KB78-STATUS         TO RETURN-CODE                        
           END-IF.                                                              
                                                                                
           IF WS-KB86-STATUS NOT = '00'                                         
              DISPLAY 'ERROR IN CLOSING BP13KB86: STATUS - '                    
              WS-KB86-STATUS                                                    
              MOVE WS-KB86-STATUS         TO RETURN-CODE                        
           END-IF.                                                              
                                                                                
           IF WS-K757-STATUS NOT = '00'                                         
              DISPLAY 'ERROR IN CLOSING BP13K757: STATUS - '                    
              WS-K757-STATUS                                                    
              MOVE WS-K757-STATUS         TO RETURN-CODE                        
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = '00'                                         
              DISPLAY 'ERROR IN CLOSING BP13K813: STATUS - '                    
              WS-K813-STATUS                                                    
              MOVE WS-K813-STATUS         TO RETURN-CODE                        
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = '00'                                         
              DISPLAY 'ERROR IN CLOSING BP13K730: STATUS - '                    
              WS-K730-STATUS                                                    
              MOVE WS-K730-STATUS         TO RETURN-CODE                        
           END-IF.                                                              
                                                                                
           IF WS-K764-STATUS NOT = '00'                                         
              DISPLAY 'ERROR IN CLOSING BP13K764: STATUS - '                    
              WS-K764-STATUS                                                    
              MOVE WS-K764-STATUS         TO RETURN-CODE                        
           END-IF.                                                              
                                                                                
           DISPLAY '**********************************************'.            
           DISPLAY '              PROGRAM : BP13CK08              '.            
           DISPLAY '**********************************************'.            
           DISPLAY ' BP13K205 RECORDS READ             : '                      
                     WS-K205-READ-CTR.                                          
           DISPLAY ' BP13KB78 RECORDS READ             : '                      
                     WS-KB78-READ-CTR.                                          
           DISPLAY ' BP13K757 RECORDS READ             : '                      
                     WS-K757-READ-CTR.                                          
           DISPLAY ' NO. OF BP13KB78 RECORDS VALID     : '                      
                     WS-KB78-VALID-CTR.                                         
           DISPLAY ' NO. OF BP13K757 RECORDS FOUND     : '                      
                     WS-K757-FOUND-CTR.                                         
           DISPLAY ' NO. OF BP13K730 RECORDS FOUND     : '                      
                     WS-K730-FOUND-CTR.                                         
           DISPLAY ' NO. OF BP13K730 RECORDS NOT FOUND : '                      
                     WS-K730-NOTFOUND-CTR.                                      
           DISPLAY ' BP13LK08 RECORDS WROTE            : '                      
                     WS-LK08-WRITE-CTR.                                         
           DISPLAY ' BP13F730 RECORDS WROTE            : '                      
                     WS-F730-WRITE-CTR.                                         
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
