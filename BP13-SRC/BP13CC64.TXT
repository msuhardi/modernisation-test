       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC64.                                                 
       AUTHOR.        BALA AMUTHAN.                                             
       DATE-WRITTEN.                                                            
      *****************************************************************         
      * OBJECTIVE  - TO PRINT MONTHLY AGING OF                                  
      *              PRINCIPAL BALANCE REPORTS                                  
      *                                                                         
      * NOTE:      - THE PROGRAM PRINTS EACH REPORT ON ONLY 1 PAGE AS           
      *              ALL AVAILABLE DESG TYPE CAN BE LISTED ON THE SAME          
      *              PAGE. FOR FUTURE EXPANSION OF DESG TYPE, THE               
      *              PROGRAM NEEDS TO BE CHANGED TO CATER FOR MULTIPLE          
      *              PAGING.                                                    
      *                                                                         
      *              NUMBER OF CASES FOR DESIGN TYPE 103 MAY NOT BE             
      *              ACCURATE DUE TO INCLUSION OF DESIGN CLASS '2'.             
      *                                                                         
      * SUBROUTINE - BP13C950                                                   
      *                                                                         
      * IDENTICAL PGM - AB03C251, AB03C276, AB03C277                            
      *                                                                         
      *-----------------------------------------------------------------        
      * REF NO   DATE        BY    AMENDMENTS/ENHANCEMENTS                      
      * -------- ----------  ----  -----------------------                      
      * BP132934 14/02/2007  BA2   NEW PROGRAM                                  
      * BP133173 22/10/2007  BA3   USE CODE LOAN SCHEME TO GET                  
      *                            INTEREST RATE                                
      * BP133175 15/11/2007  BA3   CREATE NON-CONCESSIONARY REPORT              
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13FD79 ASSIGN  TO BP13FD79.                                 
           SELECT P13LC64A ASSIGN  TO P13LC64A.                                 
           SELECT P13LC64B ASSIGN  TO P13LC64B.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FD79                      BLOCK CONTAINS 0 RECORDS               
                                         RECORD CONTAINS 100 CHARACTERS         
                                         LABEL RECORDS ARE STANDARD             
                                         RECORDING MODE IS F.                   
       COPY BP13FD79.                                                           
                                                                                
       FD  P13LC64A                      LABEL RECORDS ARE OMITTED              
                                         RECORDING MODE IS F.                   
       01  LC64A-PRINT-REC               PIC X(132).                            
                                                                                
       FD  P13LC64B                      LABEL RECORDS ARE OMITTED              
                                         RECORDING MODE IS F.                   
       01  LC64B-PRINT-REC               PIC X(132).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      *COPY WSCODES.                                                            
                                                                                
       01  WS-COND-VAR.                                                         
           05  WS-CDE-ACTLUSE-INFO.                                             
               88  VALID-TYP-ACTLUSE             VALUE '101' THRU '105'.        
               88  ONE-ROOM                      VALUE '101'.                   
               88  TWO-ROOM                      VALUE '102'.                   
               88  THREE-ROOM                    VALUE '103'.                   
               88  FOUR-ROOM                     VALUE '104'.                   
               88  FIVE-ROOM                     VALUE '105'.                   
               10  WS-CDE-CLS-ACTLUSE            PIC X.                         
               10  WS-CDE-TYP-ACTLUSE            PIC X(2).                      
                                                                                
      *COPY AB03914C.                                                           
      *-----------------------------------------------------------*             
      *         FOR SUBROUTINE BP13C950                                         
      *-----------------------------------------------------------*             
       01  C950-FIELD.                                                          
           05  C950-PROCESS-DATE       PIC X(08).                               
           05  C950-PROCESS-DATE-NUM REDEFINES C950-PROCESS-DATE.               
               10  C950-PROCESS-CC     PIC 99.                                  
               10  C950-PROCESS-YY     PIC 99.                                  
               10  C950-PROCESS-MM     PIC 99.                                  
               10  C950-PROCESS-DD     PIC 99.                                  
           05  C950-INTREST-RATE       PIC 9(02)V9(4).                          
           05  C950-FACT               PIC 9(02).                               
           05  C950-REPYMT-PERIOD      PIC 9(02).                               
           05  C950-CDE-LOAN-SCH       PIC X(01).                               
                                                                                
       01  WS-MISC.                                                             
           05  WS-RETURN-CODE            PIC 9(02).                             
           05  WS-FD79-EOF               PIC X VALUE 'N'.                       
           05  WS-CNT-FD79-READ          PIC 9(7) VALUE 0.                      
           05  WS-CTR                    PIC 9(2).                              
           05  WS-ARREAR-FLAG            PIC X.                                 
           05  WS-INTREST-FLAG           PIC X     VALUE 'Y'.                   
           05  WS-DTE-CURR-CCYY-PREV     PIC 9(04) VALUE 0.                     
                                                                                
       01  WS-AMT-FIELDS.                                                       
           05  WS-AMT-INST               PIC S9(10)V99 VALUE +0.                
           05  WS-AMT-LOAN-BAL           PIC S9(10)V99 VALUE +0.                
           05  WS-RPT-LOAN-BAL           PIC S9(10)V99 VALUE +0.                
           05  WS-AMT-TOTAL-INST         PIC S9(10)V99 VALUE +0.                
           05  WS-AMT-INT                PIC S9(07)V99 VALUE +0.                
           05  WS-PCT-RATE               PIC 9(02)V9999.                        
           05  WS-FACTOR                 PIC 9(02).                             
                                                                                
       01  SUBS.                                                                
           03  SUB-1                     PIC 9(2).                              
           03  SUB-2                     PIC 9(2).                              
                                                                                
       01  WS-DTE-CURR.                                                         
           03  WS-DTE-CURR-CCYY          PIC 9(4).                              
           03  WS-DTE-CURR-MM            PIC 9(2).                              
           03  WS-DTE-CURR-DD            PIC 9(2).                              
                                                                                
       01  WS-DATE-C950.                                                        
           03  WS-C950-CCYY              PIC 9(4).                              
           03  WS-C950-MM                PIC 9(2).                              
           03  WS-C950-DD                PIC 9(2).                              
                                                                                
       01  WS-ARRAY.                                                            
           03 WS-MONTHS                  PIC X(36) VALUE                        
              'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                           
           03 WS-ARR-MMM REDEFINES WS-MONTHS OCCURS 12.                         
              10 WS-MMM                  PIC X(03).                             
                                                                                
       01  LC64A-FLAT-AMT-CNT-TABLE.                                            
           03  LC64A-FLAT-DETAILS        OCCURS 6  TIMES.                       
               05  LC64A-AMT-CNT-ARR     OCCURS 6 TIMES.                        
                   07  LC64A-AMT-ARR     PIC S9(11)V99.                         
                   07  LC64A-CNT-ARR     PIC 9(6).                              
                                                                                
       01  LC64B-FLAT-AMT-CNT-TABLE.                                            
           03  LC64B-FLAT-DETAILS        OCCURS 6  TIMES.                       
               05  LC64B-AMT-CNT-ARR     OCCURS 6 TIMES.                        
                   07  LC64B-AMT-ARR     PIC S9(11)V99.                         
                   07  LC64B-CNT-ARR     PIC 9(6).                              
                                                                                
       01  FLAT-TABLE.                                                          
           03  FLAT-DESC-LINE            OCCURS 5 TIMES.                        
               05 FLAT-DESC              PIC X(3).                              
                                                                                
       01  LC64-HDG1.                                                           
           03  LC64-NUM-REPORT-ID        PIC X(10).                             
           03  FILLER                    PIC X(04) VALUE SPACES.                
           03  FILLER                    PIC X(04) VALUE 'HDB3'.                
           03  FILLER                    PIC X(22) VALUE SPACES.                
           03  FILLER                    PIC X(50) VALUE                        
               'H O U S I N G  &  D E V E L O P M E N T  B O A R D'.            
           03  FILLER                    PIC X(25) VALUE SPACES.                
           03  FILLER                    PIC X(07) VALUE 'DATE : '.             
           03  LC64-DTE-HDR.                                                    
               05  LC64-DTE-HDR-DD       PIC 9(02).                             
               05  FILLER                PIC X     VALUE '/'.                   
               05  LC64-DTE-HDR-MM       PIC 9(02).                             
               05  FILLER                PIC X     VALUE '/'.                   
               05  LC64-DTE-HDR-CCYY     PIC 9(04).                             
                                                                                
       01  LC64-HDG2.                                                           
           03  FILLER                    PIC X(45) VALUE SPACES.                
           03  FILLER                    PIC X(39) VALUE                        
               ' S Y S T E M  OF  C O M M I T M E N T  '.                       
           03  FILLER                    PIC X(31) VALUE SPACES.                
           03  FILLER                    PIC X(07) VALUE 'PAGE : '.             
           03  LC64-NUM-PAGE             PIC ZZZ,ZZ9.                           
           03  FILLER                    PIC X(03) VALUE SPACES.                
                                                                                
       01  LC64-HDG3.                                                           
           03  FILLER                    PIC X(17) VALUE                        
                                         'TO : FINANCE DEPT'.                   
           03  FILLER                    PIC X(27) VALUE SPACES.                
           03  FILLER                    PIC X(37) VALUE                        
               'AGING OF PRINCIPAL BALANCE AS AT END'.                          
           03  LC64-DTE-REPT.                                                   
               05  LC64-DTE-REPT-MMM     PIC X(03).                             
               05  FILLER                PIC X     VALUE SPACE.                 
               05  LC64-DTE-REPT-CCYY    PIC 9(04).                             
           03  FILLER                    PIC X(43) VALUE SPACES.                
                                                                                
       01  LC64-HDG4.                                                           
           03  FILLER                    PIC X(48) VALUE SPACES.                
           03  LC64-SUBHDG               PIC X(50).                             
           03  FILLER                    PIC X(32) VALUE SPACES.                
                                                                                
       01  LC64-HDG5.                                                           
           03  FILLER                    PIC X(12) VALUE SPACES.                
           03  FILLER                    PIC X(43) VALUE                        
           '<-----------------------------------------'.                        
           03  FILLER                    PIC X(12) VALUE 'LOAN BALANCE'.        
           03  FILLER                    PIC X(45) VALUE                        
           ' ------------------------------------------->'.                     
           03  FILLER                    PIC X(21) VALUE SPACES.                
                                                                                
       01  LC64-HDG6.                                                           
           03  FILLER                   PIC X(06) VALUE 'DESIGN'.               
           03  FILLER                   PIC X(28) VALUE SPACES.                 
           03  FILLER                   PIC X(13) VALUE 'PRINCIPAL BAL'.        
           03  FILLER                   PIC X(03) VALUE SPACES.                 
           03  FILLER                   PIC X(13) VALUE 'PRINCIPAL BAL'.        
           03  FILLER                   PIC X(09) VALUE SPACES.                 
           03  FILLER                   PIC X(13) VALUE 'PRINCIPAL BAL'.        
           03  FILLER                   PIC X(12) VALUE SPACES.                 
           03  FILLER                   PIC X(13) VALUE 'PRINCIPAL BAL'.        
           03  FILLER                   PIC X(03) VALUE SPACES.                 
           03  FILLER                   PIC X(13) VALUE 'TOTAL LOAN'.           
           03  FILLER                   PIC X(09) VALUE SPACES.                 
                                                                                
       01  LC64-HDG7.                                                           
           03  FILLER                    PIC X(04) VALUE 'TYPE'.                
           03  FILLER                    PIC X(08) VALUE SPACES.                
           03  FILLER                    PIC X(15) VALUE                        
                                         'ADVANCE PAYMENT'.                     
           03  FILLER                    PIC X(07) VALUE SPACES.                
           03  FILLER                    PIC X(10) VALUE '<= 12 MTHS'.          
           03  FILLER                    PIC X(06) VALUE SPACES.                
           03  FILLER                    PIC X(20) VALUE                        
                                         '> 12 MTHS <= 24 MTHS'.                
           03  FILLER                    PIC X(02) VALUE SPACES.                
           03  FILLER                    PIC X(20) VALUE                        
                                         '> 24 MTHS <= 60 MTHS'.                
           03  FILLER                    PIC X(05) VALUE SPACES.                
           03  FILLER                    PIC X(09) VALUE '> 60 MTHS'.           
           03  FILLER                    PIC X(07) VALUE SPACES.                
           03  FILLER                    PIC X(11) VALUE 'OUTSTANDING'.         
           03  FILLER                    PIC X(08) VALUE SPACES.                
                                                                                
       01  LC64-HDG8.                                                           
           03  FILLER                    PIC X(10) VALUE '----------'.          
           03  FILLER                    PIC X(02) VALUE SPACES.                
           03  FILLER                    PIC X(18) VALUE                        
                                         '------------------'.                  
           03  FILLER                    PIC X(01) VALUE SPACE.                 
           03  FILLER                    PIC X(18) VALUE                        
                                         '------------------'.                  
           03  FILLER                    PIC X(01) VALUE SPACE.                 
           03  FILLER                    PIC X(20) VALUE                        
                                         '--------------------'.                
           03  FILLER                    PIC X(02) VALUE SPACES.                
           03  FILLER                    PIC X(20) VALUE                        
                                         '--------------------'.                
           03  FILLER                    PIC X(02) VALUE SPACES.                
           03  FILLER                    PIC X(18) VALUE                        
                                         '------------------'.                  
           03  FILLER                    PIC X(01) VALUE SPACE.                 
           03  FILLER                    PIC X(19) VALUE                        
                                         '-------------------'.                 
                                                                                
       01  LC64-DETAIL-LINE-1.                                                  
           03  LC64-TYP-DESG             PIC X(06).                             
           03  FILLER                    PIC X(06) VALUE SPACES.                
           03  LC64-ADV-PYT              PIC ZZ,ZZZ,ZZZ,ZZ9.99-.                
           03  FILLER                    PIC X(01) VALUE SPACE.                 
           03  LC64-INSTW12              PIC ZZ,ZZZ,ZZZ,ZZ9.99-.                
           03  FILLER                    PIC X(03) VALUE SPACE.                 
           03  LC64-INSTW24              PIC ZZ,ZZZ,ZZZ,ZZ9.99-.                
           03  FILLER                    PIC X(04) VALUE SPACES.                
           03  LC64-INSTW60              PIC ZZ,ZZZ,ZZZ,ZZ9.99-.                
           03  FILLER                    PIC X(02) VALUE SPACES.                
           03  LC64-INSTA60              PIC ZZ,ZZZ,ZZZ,ZZ9.99-.                
           03  FILLER                    PIC X(01) VALUE SPACE.                 
           03  LC64-TOT-LOAN-OS          PIC ZZZ,ZZZ,ZZZ,ZZ9.99-.               
                                                                                
       01  LC64-DETAIL-LINE-2.                                                  
           03  FILLER                    PIC X(10) VALUE 'NO OF A/CS'.          
           03  FILLER                    PIC X(12) VALUE SPACES.                
           03  LC64-ADV-PYT-CNT          PIC ZZZ,ZZ9.                           
           03  FILLER                    PIC X(12) VALUE SPACES.                
           03  LC64-INSTW12-CNT          PIC ZZZ,ZZ9.                           
           03  FILLER                    PIC X(14) VALUE SPACES.                
           03  LC64-INSTW24-CNT          PIC ZZZ,ZZ9.                           
           03  FILLER                    PIC X(15) VALUE SPACES.                
           03  LC64-INSTW60-CNT          PIC ZZZ,ZZ9.                           
           03  FILLER                    PIC X(13) VALUE SPACES.                
           03  LC64-INSTA60-CNT          PIC ZZZ,ZZ9.                           
           03  FILLER                    PIC X(13) VALUE SPACES.                
           03  LC64-TOT-LOAN-OS-CNT      PIC ZZZ,ZZ9.                           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ************************************                                      
       0000-MAIN-LOGIC.                                                         
      ************************************                                      
                                                                                
           PERFORM 1000-START-UP          THRU 1000-EXIT.                       
           PERFORM 2000-INITIALIZE-TABLES THRU 2000-EXIT.                       
           PERFORM 3000-READ-BP13FD79     THRU 3000-EXIT                        
             UNTIL WS-FD79-EOF = 'Y'.                                           
           PERFORM 4000-GEN-REPT          THRU 4000-EXIT.                       
           PERFORM 9999-WRAP-UP.                                                
                                                                                
                                                                                
      ************************************                                      
       1000-START-UP.                                                           
      ************************************                                      
                                                                                
           OPEN INPUT  BP13FD79                                                 
                OUTPUT P13LC64A                                                 
                       P13LC64B.                                                
                                                                                
           ACCEPT WS-DTE-CURR FROM DATE YYYYMMDD.                               
           MOVE   WS-DTE-CURR   TO WS-DATE-C950.                                
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ************************************                                      
       2000-INITIALIZE-TABLES.                                                  
      ************************************                                      
                                                                                
           INITIALIZE  LC64A-FLAT-AMT-CNT-TABLE                                 
                       LC64B-FLAT-AMT-CNT-TABLE.                                
                                                                                
       2000-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       3000-READ-BP13FD79.                                                      
      ************************************                                      
                                                                                
           READ  BP13FD79 AT END                                                
                 MOVE 'Y' TO WS-FD79-EOF                                        
                 GO TO 3000-EXIT.                                               
                                                                                
           ADD 1 TO WS-CNT-FD79-READ.                                           
                                                                                
           STRING FD79-CDE-CLS-ACTLUSE                                          
                  FD79-CDE-TYP-ACTLUSE                                          
                  DELIMITED BY SIZE                                             
                  INTO WS-CDE-ACTLUSE-INFO.                                     
                                                                                
           MOVE WS-DATE-C950      TO C950-PROCESS-DATE.                         
           MOVE FD79-CDE-LOAN-SCH TO C950-CDE-LOAN-SCH.                         
           CALL 'BP13C950' USING C950-FIELD.                                    
                                                                                
           PERFORM 3200-PROCESS-BP13FD79 THRU 3200-EXIT.                        
                                                                                
       3000-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       3200-PROCESS-BP13FD79.                                                   
      ************************************                                      
                                                                                
           MOVE ZEROES TO WS-AMT-INST                                           
                          WS-AMT-LOAN-BAL.                                      
                                                                                
           EVALUATE TRUE                                                        
              WHEN ONE-ROOM                                                     
                MOVE 1 TO SUB-1                                                 
                MOVE WS-CDE-ACTLUSE-INFO TO FLAT-DESC(1)                        
              WHEN TWO-ROOM                                                     
                MOVE 2 TO SUB-1                                                 
                MOVE WS-CDE-ACTLUSE-INFO TO FLAT-DESC(2)                        
              WHEN THREE-ROOM                                                   
                MOVE 3 TO SUB-1                                                 
                MOVE WS-CDE-ACTLUSE-INFO TO FLAT-DESC(3)                        
              WHEN FOUR-ROOM                                                    
                MOVE 4 TO SUB-1                                                 
                MOVE WS-CDE-ACTLUSE-INFO TO FLAT-DESC(4)                        
              WHEN FIVE-ROOM                                                    
                MOVE 5 TO SUB-1                                                 
                MOVE WS-CDE-ACTLUSE-INFO TO FLAT-DESC(5)                        
           END-EVALUATE.                                                        
                                                                                
           PERFORM 3220-START-REPORT THRU 3220-EXIT.                            
                                                                                
       3200-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       3220-START-REPORT.                                                       
      ************************************                                      
                                                                                
           PERFORM 3224-CONTINUE-REPORT THRU 3224-EXIT.                         
                                                                                
       3220-EXIT. EXIT.                                                         
                                                                                
                                                                                
                                                                                
      ************************************                                      
       3224-CONTINUE-REPORT.                                                    
      ************************************                                      
                                                                                
           IF FD79-AMT-LOAN-BAL < 0                                             
              IF FD79-CDE-LOAN-SCH = 'A'                                        
                 MOVE 1 TO SUB-2                                                
                 PERFORM 5500-ACCUMULATE-LOAN-BAL THRU 5500-EXIT                
                 MOVE 6 TO SUB-2                                                
                 PERFORM 5500-ACCUMULATE-LOAN-BAL THRU 5500-EXIT                
              ELSE                                                              
                 MOVE 1 TO SUB-2                                                
                 PERFORM 3500-ACCUMULATE-LOAN-BAL THRU 3500-EXIT                
                 MOVE 6 TO SUB-2                                                
                 PERFORM 3500-ACCUMULATE-LOAN-BAL THRU 3500-EXIT                
              END-IF                                                            
           ELSE                                                                 
              IF FD79-AMT-LOAN-BAL > 0                                          
                  PERFORM 3225-IDENTIFY-TERMS      THRU 3225-EXIT               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3224-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       3225-IDENTIFY-TERMS.                                                     
      ************************************                                      
                                                                                
      *================================================================         
      *    GET CORRESPONDING INTEREST RATE FOR LOAN SCHEME.                     
      *================================================================         
                                                                                
           MOVE 'Y' TO WS-ARREAR-FLAG                                           
                       WS-INTREST-FLAG.                                         
           INITIALIZE  WS-AMT-TOTAL-INST                                        
                       WS-AMT-LOAN-BAL                                          
                       WS-RPT-LOAN-BAL                                          
                       WS-AMT-INST                                              
                       WS-PCT-RATE                                              
                       WS-FACTOR                                                
                       WS-AMT-INT.                                              
                                                                                
                                                                                
           MOVE C950-INTREST-RATE               TO WS-PCT-RATE                  
           MOVE C950-FACT                       TO WS-FACTOR                    
                                                                                
           IF FD79-CDE-LOAN-SCH = 'A'                                           
      **      FOR NON-CONCESSIONARY LOAN                                        
              PERFORM 5227-COMPUTE-INSTALMENT THRU 5227-EXIT                    
           ELSE                                                                 
      **      FOR CONCESSIONARY LOAN                                            
              PERFORM 3227-COMPUTE-INSTALMENT THRU 3227-EXIT                    
           END-IF.                                                              
                                                                                
       3225-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       3227-COMPUTE-INSTALMENT.                                                 
      ************************************                                      
      *-----------------------------------------*                               
      ***  INSTALMENT <= 12 MTHS                                                
      *-----------------------------------------*                               
           MOVE 2  TO SUB-2.                                                    
           MOVE FD79-AMT-LOAN-BAL TO WS-AMT-LOAN-BAL.                           
                                                                                
           PERFORM 3230-COMPUTE-INTEREST THRU 3230-EXIT VARYING WS-CTR          
           FROM 1 BY 1 UNTIL WS-CTR > 12 OR WS-AMT-LOAN-BAL <= 0.               
                                                                                
           IF WS-AMT-TOTAL-INST <= 0 OR WS-AMT-LOAN-BAL <= 0                    
              PERFORM 3500-ACCUMULATE-LOAN-BAL THRU 3500-EXIT                   
              MOVE 6 TO SUB-2                                                   
              PERFORM 3500-ACCUMULATE-LOAN-BAL THRU 3500-EXIT                   
              GO TO 3227-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 3600-ACCUMULATE-INST-AMT THRU 3600-EXIT.                     
           MOVE 6 TO SUB-2.                                                     
           PERFORM 3600-ACCUMULATE-INST-AMT THRU 3600-EXIT.                     
                                                                                
      *-----------------------------------------*                               
      ***  INSTALMENT > 12 MTHS BUT <= 24 MTHS                                  
      *-----------------------------------------*                               
           MOVE WS-AMT-LOAN-BAL TO WS-RPT-LOAN-BAL.                             
           MOVE 3  TO SUB-2.                                                    
           INITIALIZE WS-AMT-TOTAL-INST.                                        
           PERFORM 3230-COMPUTE-INTEREST THRU 3230-EXIT VARYING WS-CTR          
           FROM 1 BY 1 UNTIL WS-CTR > 12 OR WS-AMT-LOAN-BAL <= 0.               
                                                                                
           IF WS-AMT-TOTAL-INST <= 0                                            
              PERFORM 3800-ACCUMULATE-BAL-AMT  THRU 3800-EXIT                   
              MOVE 6 TO SUB-2                                                   
              PERFORM 3900-ACCUMULATE-BAL-AMT  THRU 3900-EXIT                   
              GO TO 3227-EXIT                                                   
           ELSE                                                                 
              PERFORM 3600-ACCUMULATE-INST-AMT THRU 3600-EXIT                   
              MOVE 6 TO SUB-2                                                   
              PERFORM 3700-ACCUMULATE-INST-AMT THRU 3700-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-AMT-LOAN-BAL <= 0                                              
              GO TO 3227-EXIT                                                   
           END-IF.                                                              
      *-----------------------------------------*                               
      ***  INSTALMENT > 24 MTHS BUT <= 60 MTHS                                  
      *-----------------------------------------*                               
           MOVE WS-AMT-LOAN-BAL TO WS-RPT-LOAN-BAL.                             
           MOVE 4  TO SUB-2.                                                    
           INITIALIZE WS-AMT-TOTAL-INST.                                        
                                                                                
           PERFORM 3230-COMPUTE-INTEREST THRU 3230-EXIT VARYING WS-CTR          
           FROM 1 BY 1 UNTIL WS-CTR > 36 OR WS-AMT-LOAN-BAL <= 0.               
                                                                                
           IF WS-AMT-TOTAL-INST <= 0                                            
              PERFORM 3800-ACCUMULATE-BAL-AMT    THRU 3800-EXIT                 
              MOVE 6 TO SUB-2                                                   
              PERFORM 3900-ACCUMULATE-BAL-AMT  THRU 3900-EXIT                   
              GO TO 3227-EXIT                                                   
           ELSE                                                                 
              PERFORM 3600-ACCUMULATE-INST-AMT THRU 3600-EXIT                   
              MOVE 6 TO SUB-2                                                   
              PERFORM 3700-ACCUMULATE-INST-AMT THRU 3700-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-AMT-LOAN-BAL <= 0                                              
              GO TO 3227-EXIT                                                   
           END-IF.                                                              
                                                                                
      *-----------------------------------------*                               
      ***  INSTALMENT > 60 MTHS                                                 
      *-----------------------------------------*                               
           MOVE WS-AMT-LOAN-BAL TO WS-RPT-LOAN-BAL.                             
           MOVE 5 TO SUB-2.                                                     
           PERFORM 3800-ACCUMULATE-BAL-AMT  THRU 3800-EXIT.                     
           MOVE 6 TO SUB-2.                                                     
           PERFORM 3900-ACCUMULATE-BAL-AMT  THRU 3900-EXIT.                     
                                                                                
       3227-EXIT. EXIT.                                                         
                                                                                
      ************************************                                      
       3230-COMPUTE-INTEREST.                                                   
      ************************************                                      
                                                                                
           COMPUTE WS-AMT-INT ROUNDED                                           
                       = (WS-AMT-LOAN-BAL * WS-PCT-RATE ) / 12.                 
           IF (WS-CTR = 1 AND WS-INTREST-FLAG    = 'Y') OR                      
              (WS-CTR > 1 AND FD79-AMT-INST-NEXT = 0)                           
              MOVE 'N'  TO WS-INTREST-FLAG                                      
              IF WS-AMT-LOAN-BAL < FD79-AMT-INST                                
                 COMPUTE WS-AMT-INST = WS-AMT-LOAN-BAL - WS-AMT-INT             
              ELSE                                                              
                 COMPUTE WS-AMT-INST = FD79-AMT-INST   - WS-AMT-INT             
              END-IF                                                            
           ELSE                                                                 
              IF WS-AMT-LOAN-BAL < FD79-AMT-INST-NEXT                           
                 COMPUTE WS-AMT-INST = WS-AMT-LOAN-BAL    - WS-AMT-INT          
              ELSE                                                              
                 COMPUTE WS-AMT-INST = FD79-AMT-INST-NEXT - WS-AMT-INT          
              END-IF                                                            
           END-IF.                                                              
                                                                                
      **   IF WS-ARREAR-FLAG = 'Y'                                              
      *       MOVE 'N' TO WS-ARREAR-FLAG                                        
      *       ADD FD79-AMT-ARR-INST TO WS-AMT-INST                              
      **   END-IF.                                                              
           ADD WS-AMT-INST TO WS-AMT-TOTAL-INST.                                
           COMPUTE WS-AMT-LOAN-BAL = WS-AMT-LOAN-BAL - WS-AMT-INST.             
                                                                                
       3230-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       3500-ACCUMULATE-LOAN-BAL.                                                
      ************************************                                      
                                                                                
      **-- ALL CASES (LC64A) --**                                               
           COMPUTE LC64A-AMT-ARR(SUB-1, SUB-2) =                                
                   LC64A-AMT-ARR(SUB-1, SUB-2) + FD79-AMT-LOAN-BAL.             
           COMPUTE LC64A-CNT-ARR(SUB-1, SUB-2) =                                
                   LC64A-CNT-ARR(SUB-1, SUB-2) + 1.                             
                                                                                
       3500-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       3600-ACCUMULATE-INST-AMT.                                                
      ************************************                                      
      **-- ALL CASES (LC64A) --**                                               
           COMPUTE LC64A-AMT-ARR(SUB-1, SUB-2) =                                
                   LC64A-AMT-ARR(SUB-1, SUB-2) + WS-AMT-TOTAL-INST              
           COMPUTE LC64A-CNT-ARR(SUB-1, SUB-2) =                                
                   LC64A-CNT-ARR(SUB-1, SUB-2) + 1.                             
                                                                                
      **-- MARKET SCHM (LC64C) --**                                             
                                                                                
       3600-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       3700-ACCUMULATE-INST-AMT.                                                
      ************************************                                      
                                                                                
      **-- ALL CASES (LC64A) --**                                               
           COMPUTE LC64A-AMT-ARR(SUB-1, SUB-2) =                                
                   LC64A-AMT-ARR(SUB-1, SUB-2) + WS-AMT-TOTAL-INST.             
                                                                                
       3700-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       3800-ACCUMULATE-BAL-AMT.                                                 
      ************************************                                      
                                                                                
      **-- ALL CASES (LC64A) --**                                               
           COMPUTE LC64A-AMT-ARR(SUB-1, SUB-2) =                                
                   LC64A-AMT-ARR(SUB-1, SUB-2) + WS-RPT-LOAN-BAL                
           COMPUTE LC64A-CNT-ARR(SUB-1, SUB-2) =                                
                   LC64A-CNT-ARR(SUB-1, SUB-2) + 1.                             
                                                                                
       3800-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       3900-ACCUMULATE-BAL-AMT.                                                 
      ************************************                                      
                                                                                
      **-- ALL CASES (LC64A) --**                                               
           COMPUTE LC64A-AMT-ARR(SUB-1, SUB-2) =                                
                   LC64A-AMT-ARR(SUB-1, SUB-2) + WS-RPT-LOAN-BAL.               
                                                                                
       3900-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       4000-GEN-REPT.                                                           
      ************************************                                      
                                                                                
           MOVE WS-DTE-CURR-DD TO LC64-DTE-HDR-DD.                              
           MOVE WS-DTE-CURR-MM TO LC64-DTE-HDR-MM.                              
                                                                                
           IF WS-DTE-CURR-MM = 01                                               
              MOVE WS-MMM (12) TO LC64-DTE-REPT-MMM                             
              COMPUTE WS-DTE-CURR-CCYY-PREV = WS-DTE-CURR-CCYY - 1              
              MOVE WS-DTE-CURR-CCYY-PREV TO LC64-DTE-REPT-CCYY                  
           ELSE                                                                 
              MOVE WS-MMM (WS-DTE-CURR-MM - 1) TO LC64-DTE-REPT-MMM             
              MOVE WS-DTE-CURR-CCYY            TO LC64-DTE-REPT-CCYY            
           END-IF.                                                              
                                                                                
           MOVE WS-DTE-CURR-CCYY             TO LC64-DTE-HDR-CCYY.              
                                                                                
           MOVE 1 TO LC64-NUM-PAGE.                                             
           PERFORM 4200-TOTAL-TYP-DESG-AMT  THRU 4200-EXIT                      
                   VARYING SUB-1 FROM 1 BY 1 UNTIL SUB-1 > 5.                   
                                                                                
           MOVE 'P13LC64A'       TO   LC64-NUM-REPORT-ID.                       
           WRITE LC64A-PRINT-REC FROM LC64-HDG1 AFTER PAGE.                     
           WRITE LC64A-PRINT-REC FROM LC64-HDG2 AFTER 1.                        
           WRITE LC64A-PRINT-REC FROM LC64-HDG3 AFTER 2.                        
      *    MOVE 'TOTAL ( CGO, PUBLIC, MARKET - ALL )' TO LC64-SUBHDG.           
           MOVE '    TOTAL (CONCESSIONARY RATE)     ' TO LC64-SUBHDG.           
           WRITE LC64A-PRINT-REC FROM LC64-HDG4 AFTER 1.                        
           WRITE LC64A-PRINT-REC FROM LC64-HDG5 AFTER 2.                        
           WRITE LC64A-PRINT-REC FROM LC64-HDG6 AFTER 1.                        
           WRITE LC64A-PRINT-REC FROM LC64-HDG7 AFTER 1.                        
           WRITE LC64A-PRINT-REC FROM LC64-HDG8 AFTER 1.                        
                                                                                
           MOVE 'P13LC64B'       TO   LC64-NUM-REPORT-ID.                       
           WRITE LC64B-PRINT-REC FROM LC64-HDG1 AFTER PAGE.                     
           WRITE LC64B-PRINT-REC FROM LC64-HDG2 AFTER 1.                        
           WRITE LC64B-PRINT-REC FROM LC64-HDG3 AFTER 2.                        
           MOVE '  TOTAL (NON-CONCESSIONARY RATE)   ' TO LC64-SUBHDG.           
           WRITE LC64B-PRINT-REC FROM LC64-HDG4 AFTER 1.                        
           WRITE LC64B-PRINT-REC FROM LC64-HDG5 AFTER 2.                        
           WRITE LC64B-PRINT-REC FROM LC64-HDG6 AFTER 1.                        
           WRITE LC64B-PRINT-REC FROM LC64-HDG7 AFTER 1.                        
           WRITE LC64B-PRINT-REC FROM LC64-HDG8 AFTER 1.                        
                                                                                
           PERFORM 4400-REPT-DETAILS THRU 4400-EXIT                             
                   VARYING SUB-1 FROM 1 BY 1 UNTIL SUB-1 > 6.                   
                                                                                
       4000-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       4200-TOTAL-TYP-DESG-AMT.                                                 
      ************************************                                      
                                                                                
           PERFORM VARYING SUB-2 FROM 1 BY 1 UNTIL SUB-2 > 6                    
              COMPUTE LC64A-AMT-ARR(6, SUB-2) = LC64A-AMT-ARR(6, SUB-2)         
                                          + LC64A-AMT-ARR(SUB-1, SUB-2)         
              COMPUTE LC64A-CNT-ARR(6, SUB-2) = LC64A-CNT-ARR(6, SUB-2)         
                                          + LC64A-CNT-ARR(SUB-1, SUB-2)         
           END-PERFORM.                                                         
                                                                                
           PERFORM VARYING SUB-2 FROM 1 BY 1 UNTIL SUB-2 > 6                    
              COMPUTE LC64B-AMT-ARR(6, SUB-2) = LC64B-AMT-ARR(6, SUB-2)         
                                          + LC64B-AMT-ARR(SUB-1, SUB-2)         
              COMPUTE LC64B-CNT-ARR(6, SUB-2) = LC64B-CNT-ARR(6, SUB-2)         
                                          + LC64B-CNT-ARR(SUB-1, SUB-2)         
           END-PERFORM.                                                         
                                                                                
       4200-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       4400-REPT-DETAILS.                                                       
      ************************************                                      
           EVALUATE SUB-1                                                       
              WHEN 1                                                            
                MOVE '101'        TO LC64-TYP-DESG                              
              WHEN 2                                                            
                MOVE '102'        TO LC64-TYP-DESG                              
              WHEN 3                                                            
                MOVE '103'        TO LC64-TYP-DESG                              
              WHEN 4                                                            
                MOVE '104'        TO LC64-TYP-DESG                              
              WHEN 5                                                            
                MOVE '105'        TO LC64-TYP-DESG                              
              WHEN 6                                                            
                MOVE 'TOTAL' TO LC64-TYP-DESG                                   
           END-EVALUATE.                                                        
                                                                                
           MOVE LC64A-AMT-ARR(SUB-1, 1) TO LC64-ADV-PYT.                        
           MOVE LC64A-AMT-ARR(SUB-1, 2) TO LC64-INSTW12.                        
           MOVE LC64A-AMT-ARR(SUB-1, 3) TO LC64-INSTW24.                        
           MOVE LC64A-AMT-ARR(SUB-1, 4) TO LC64-INSTW60.                        
           MOVE LC64A-AMT-ARR(SUB-1, 5) TO LC64-INSTA60.                        
           MOVE LC64A-AMT-ARR(SUB-1, 6) TO LC64-TOT-LOAN-OS.                    
           WRITE LC64A-PRINT-REC FROM LC64-DETAIL-LINE-1 AFTER 2.               
                                                                                
           MOVE LC64A-CNT-ARR(SUB-1, 1) TO LC64-ADV-PYT-CNT.                    
           MOVE LC64A-CNT-ARR(SUB-1, 2) TO LC64-INSTW12-CNT.                    
           MOVE LC64A-CNT-ARR(SUB-1, 3) TO LC64-INSTW24-CNT.                    
           MOVE LC64A-CNT-ARR(SUB-1, 4) TO LC64-INSTW60-CNT.                    
           MOVE LC64A-CNT-ARR(SUB-1, 5) TO LC64-INSTA60-CNT.                    
           MOVE LC64A-CNT-ARR(SUB-1, 6) TO LC64-TOT-LOAN-OS-CNT.                
           WRITE LC64A-PRINT-REC FROM LC64-DETAIL-LINE-2 AFTER 1.               
                                                                                
           MOVE LC64B-AMT-ARR(SUB-1, 1) TO LC64-ADV-PYT.                        
           MOVE LC64B-AMT-ARR(SUB-1, 2) TO LC64-INSTW12.                        
           MOVE LC64B-AMT-ARR(SUB-1, 3) TO LC64-INSTW24.                        
           MOVE LC64B-AMT-ARR(SUB-1, 4) TO LC64-INSTW60.                        
           MOVE LC64B-AMT-ARR(SUB-1, 5) TO LC64-INSTA60.                        
           MOVE LC64B-AMT-ARR(SUB-1, 6) TO LC64-TOT-LOAN-OS.                    
           WRITE LC64B-PRINT-REC FROM LC64-DETAIL-LINE-1 AFTER 2.               
                                                                                
           MOVE LC64B-CNT-ARR(SUB-1, 1) TO LC64-ADV-PYT-CNT.                    
           MOVE LC64B-CNT-ARR(SUB-1, 2) TO LC64-INSTW12-CNT.                    
           MOVE LC64B-CNT-ARR(SUB-1, 3) TO LC64-INSTW24-CNT.                    
           MOVE LC64B-CNT-ARR(SUB-1, 4) TO LC64-INSTW60-CNT.                    
           MOVE LC64B-CNT-ARR(SUB-1, 5) TO LC64-INSTA60-CNT.                    
           MOVE LC64B-CNT-ARR(SUB-1, 6) TO LC64-TOT-LOAN-OS-CNT.                
           WRITE LC64B-PRINT-REC FROM LC64-DETAIL-LINE-2 AFTER 1.               
                                                                                
       4400-EXIT. EXIT.                                                         
                                                                                
      ************************************                                      
       5227-COMPUTE-INSTALMENT.                                                 
      ************************************                                      
      *-----------------------------------------*                               
      ***  INSTALMENT <= 12 MTHS                                                
      *-----------------------------------------*                               
           MOVE 2  TO SUB-2.                                                    
           MOVE FD79-AMT-LOAN-BAL TO WS-AMT-LOAN-BAL.                           
                                                                                
           PERFORM 5230-COMPUTE-INTEREST THRU 5230-EXIT VARYING WS-CTR          
           FROM 1 BY 1 UNTIL WS-CTR > 12 OR WS-AMT-LOAN-BAL <= 0.               
                                                                                
           IF WS-AMT-TOTAL-INST <= 0 OR WS-AMT-LOAN-BAL <= 0                    
              PERFORM 5500-ACCUMULATE-LOAN-BAL THRU 5500-EXIT                   
              MOVE 6 TO SUB-2                                                   
              PERFORM 5500-ACCUMULATE-LOAN-BAL THRU 5500-EXIT                   
              GO TO 5227-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 5600-ACCUMULATE-INST-AMT THRU 5600-EXIT.                     
           MOVE 6 TO SUB-2.                                                     
           PERFORM 5600-ACCUMULATE-INST-AMT THRU 5600-EXIT.                     
                                                                                
      *-----------------------------------------*                               
      ***  INSTALMENT > 12 MTHS BUT <= 24 MTHS                                  
      *-----------------------------------------*                               
           MOVE WS-AMT-LOAN-BAL TO WS-RPT-LOAN-BAL.                             
           MOVE 3  TO SUB-2.                                                    
           INITIALIZE WS-AMT-TOTAL-INST.                                        
           PERFORM 5230-COMPUTE-INTEREST THRU 5230-EXIT VARYING WS-CTR          
           FROM 1 BY 1 UNTIL WS-CTR > 12 OR WS-AMT-LOAN-BAL <= 0.               
                                                                                
           IF WS-AMT-TOTAL-INST <= 0                                            
              PERFORM 5800-ACCUMULATE-BAL-AMT  THRU 5800-EXIT                   
              MOVE 6 TO SUB-2                                                   
              PERFORM 5900-ACCUMULATE-BAL-AMT  THRU 5900-EXIT                   
              GO TO 5227-EXIT                                                   
           ELSE                                                                 
              PERFORM 5600-ACCUMULATE-INST-AMT THRU 5600-EXIT                   
              MOVE 6 TO SUB-2                                                   
              PERFORM 5700-ACCUMULATE-INST-AMT THRU 5700-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-AMT-LOAN-BAL <= 0                                              
              GO TO 5227-EXIT                                                   
           END-IF.                                                              
      *-----------------------------------------*                               
      ***  INSTALMENT > 24 MTHS BUT <= 60 MTHS                                  
      *-----------------------------------------*                               
           MOVE WS-AMT-LOAN-BAL TO WS-RPT-LOAN-BAL.                             
           MOVE 4  TO SUB-2.                                                    
           INITIALIZE WS-AMT-TOTAL-INST.                                        
                                                                                
           PERFORM 5230-COMPUTE-INTEREST THRU 5230-EXIT VARYING WS-CTR          
           FROM 1 BY 1 UNTIL WS-CTR > 36 OR WS-AMT-LOAN-BAL <= 0.               
                                                                                
           IF WS-AMT-TOTAL-INST <= 0                                            
              PERFORM 5800-ACCUMULATE-BAL-AMT    THRU 5800-EXIT                 
              MOVE 6 TO SUB-2                                                   
              PERFORM 5900-ACCUMULATE-BAL-AMT  THRU 5900-EXIT                   
              GO TO 5227-EXIT                                                   
           ELSE                                                                 
              PERFORM 5600-ACCUMULATE-INST-AMT THRU 5600-EXIT                   
              MOVE 6 TO SUB-2                                                   
              PERFORM 5700-ACCUMULATE-INST-AMT THRU 5700-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-AMT-LOAN-BAL <= 0                                              
              GO TO 5227-EXIT                                                   
           END-IF.                                                              
                                                                                
      *-----------------------------------------*                               
      ***  INSTALMENT > 60 MTHS                                                 
      *-----------------------------------------*                               
           MOVE WS-AMT-LOAN-BAL TO WS-RPT-LOAN-BAL.                             
           MOVE 5 TO SUB-2.                                                     
           PERFORM 5800-ACCUMULATE-BAL-AMT  THRU 5800-EXIT.                     
           MOVE 6 TO SUB-2.                                                     
           PERFORM 5900-ACCUMULATE-BAL-AMT  THRU 5900-EXIT.                     
                                                                                
       5227-EXIT. EXIT.                                                         
                                                                                
      ************************************                                      
       5230-COMPUTE-INTEREST.                                                   
      ************************************                                      
                                                                                
           COMPUTE WS-AMT-INT ROUNDED                                           
                       = (WS-AMT-LOAN-BAL * WS-PCT-RATE ) / 12.                 
           IF (WS-CTR = 1 AND WS-INTREST-FLAG    = 'Y') OR                      
              (WS-CTR > 1 AND FD79-AMT-INST-NEXT = 0)                           
              MOVE 'N'  TO WS-INTREST-FLAG                                      
              IF WS-AMT-LOAN-BAL < FD79-AMT-INST                                
                 COMPUTE WS-AMT-INST = WS-AMT-LOAN-BAL - WS-AMT-INT             
              ELSE                                                              
                 COMPUTE WS-AMT-INST = FD79-AMT-INST   - WS-AMT-INT             
              END-IF                                                            
           ELSE                                                                 
              IF WS-AMT-LOAN-BAL < FD79-AMT-INST-NEXT                           
                 COMPUTE WS-AMT-INST = WS-AMT-LOAN-BAL    - WS-AMT-INT          
              ELSE                                                              
                 COMPUTE WS-AMT-INST = FD79-AMT-INST-NEXT - WS-AMT-INT          
              END-IF                                                            
           END-IF.                                                              
                                                                                
      **   IF WS-ARREAR-FLAG = 'Y'                                              
      *       MOVE 'N' TO WS-ARREAR-FLAG                                        
      *       ADD FD79-AMT-ARR-INST TO WS-AMT-INST                              
      **   END-IF.                                                              
           ADD WS-AMT-INST TO WS-AMT-TOTAL-INST.                                
           COMPUTE WS-AMT-LOAN-BAL = WS-AMT-LOAN-BAL - WS-AMT-INST.             
                                                                                
       5230-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       5500-ACCUMULATE-LOAN-BAL.                                                
      ************************************                                      
                                                                                
      **-- ALL CASES (LC64B) --**                                               
           COMPUTE LC64B-AMT-ARR(SUB-1, SUB-2) =                                
                   LC64B-AMT-ARR(SUB-1, SUB-2) + FD79-AMT-LOAN-BAL.             
           COMPUTE LC64B-CNT-ARR(SUB-1, SUB-2) =                                
                   LC64B-CNT-ARR(SUB-1, SUB-2) + 1.                             
                                                                                
       5500-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       5600-ACCUMULATE-INST-AMT.                                                
      ************************************                                      
      **-- ALL CASES (LC64B) --**                                               
           COMPUTE LC64B-AMT-ARR(SUB-1, SUB-2) =                                
                   LC64B-AMT-ARR(SUB-1, SUB-2) + WS-AMT-TOTAL-INST              
           COMPUTE LC64B-CNT-ARR(SUB-1, SUB-2) =                                
                   LC64B-CNT-ARR(SUB-1, SUB-2) + 1.                             
                                                                                
       5600-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       5700-ACCUMULATE-INST-AMT.                                                
      ************************************                                      
                                                                                
      **-- ALL CASES (LC64B) --**                                               
           COMPUTE LC64B-AMT-ARR(SUB-1, SUB-2) =                                
                   LC64B-AMT-ARR(SUB-1, SUB-2) + WS-AMT-TOTAL-INST.             
                                                                                
       5700-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       5800-ACCUMULATE-BAL-AMT.                                                 
      ************************************                                      
                                                                                
      **-- ALL CASES (LC64B) --**                                               
           COMPUTE LC64B-AMT-ARR(SUB-1, SUB-2) =                                
                   LC64B-AMT-ARR(SUB-1, SUB-2) + WS-RPT-LOAN-BAL                
           COMPUTE LC64B-CNT-ARR(SUB-1, SUB-2) =                                
                   LC64B-CNT-ARR(SUB-1, SUB-2) + 1.                             
                                                                                
       5800-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       5900-ACCUMULATE-BAL-AMT.                                                 
      ************************************                                      
                                                                                
      **-- ALL CASES (LC64A) --**                                               
           COMPUTE LC64B-AMT-ARR(SUB-1, SUB-2) =                                
                   LC64B-AMT-ARR(SUB-1, SUB-2) + WS-RPT-LOAN-BAL.               
                                                                                
       5900-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       9999-WRAP-UP.                                                            
      ************************************                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*********** CONTROL TOTALS ***********'.                    
           DISPLAY 'PROGRAM-ID: BP13CC64'.                                      
           DISPLAY SPACES.                                                      
           DISPLAY 'TOTAL BP13FD79 RECORDS READ : ' WS-CNT-FD79-READ.           
                                                                                
           CLOSE BP13FD79                                                       
                 P13LC64A                                                       
                 P13LC64B.                                                      
                                                                                
           MOVE RETURN-CODE TO WS-RETURN-CODE.                                  
                                                                                
           IF WS-RETURN-CODE NOT = 00                                           
              MOVE WS-RETURN-CODE TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
