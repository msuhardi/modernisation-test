       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C780.                                                 
      *AUTHOR.        SATHAPPAN.                                                
      *DATE-WRITTEN.  22/07/02.                                                 
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  TO SELECT CURRENT RESERVE LIST USED FOR THIS  *                
      *          ROUND OF PIGEONHOLING.                        *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *      1  BP13K703 - RESERVE REQUEST DATE                *                
      *      2  BP13F705 - UNSUCCESSFUL RESERVE REQUESTS       *                
      *                                                        *                
      *     OUTPUT FILES :                                     *                
      *      1  BP13F700                                       *                
      *                                                        *                
      * CHG-NO  DATE   BY  DESCRIPTION                         *                
      * ------  ----   --  -----------                         *                
      *BP132170 220702 AS1 NEW PROGRAM                         *                
      *BP132293 100203 CKK TO CHECK F705 BASED ON NT/FT        *                
      *BP132290 160303 CKK TO CHECK END-OF-FILE FOR BP13F705   *                
      *BP132417 290903 MJ16TO CHECK CONDITION IF RECORD WRITTEN*                
      *                    TO F700 IS EQUAL TO SURVEY NO.      *                
      *BP133132 250807 IL5 TO MOVE SUCC NT TO F700             *                
      **********************************************************                
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K703 ASSIGN TO BP13K703                                   
                     ORGANIZATION IS INDEXED                                    
                     ACCESS MODE  IS DYNAMIC                                    
                     RECORD KEY   IS K703-KEY-FLD                               
                     FILE STATUS  IS WS-K703-STATUS.                            
                                                                                
           SELECT BP13F705 ASSIGN TO BP13F705.                                  
                                                                                
           SELECT BP13F700 ASSIGN TO BP13F700.                                  
                                                                                
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *                                                                         
       FD   BP13K703                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
                                                                                
       COPY BP13K703.                                                           
      *                                                                         
       FD   BP13F705                                                            
            RECORDING MODE IS F                                                 
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
                                                                                
       COPY BP13F705.                                                           
      *                                                                         
       FD   BP13F700                                                            
            RECORDING MODE IS F                                                 
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
                                                                                
       COPY BP13F700.                                                           
      *                                                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-EOF-F705             PIC X       VALUE 'N'.                       
           88  EOF-F705                        VALUE 'Y'.                       
       01  WS-EOF-K703             PIC X       VALUE 'N'.                       
           88  EOF-K703                        VALUE 'Y'.                       
       01  WS-CHG-FLAG             PIC X       VALUE 'N'.                       
                                                                                
       01  WS-TEMP.                                                             
           05  WS-K703-STATUS      PIC 9(2)    VALUE ZEROES.                    
           05  WS-READ-F705        PIC 9(6)    VALUE ZEROES.                    
           05  WS-READ-K703        PIC 9(6)    VALUE ZEROES.                    
           05  WS-WRT-F700         PIC 9(6)    VALUE ZEROES.                    
           05  WS-DEL-K703         PIC 9(6)    VALUE ZEROES.                    
           05  WS-CHK-CNT          PIC 9(5)    VALUE ZEROES.                    
           05  WS-NUM-SUR-CNT      PIC 9(5)    VALUE ZEROES.                    
           05  WS-CDE-CATEGORY     PIC X(1)    VALUE SPACES.                    
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *----------------*                                                        
       0000-MAIN-LOGIC.                                                         
      *----------------*                                                        
                                                                                
           PERFORM 1000-OPEN-ROUTINE  THRU 1000-EXIT.                           
           PERFORM 2000-READ-K703     THRU 2000-EXIT.                           
           PERFORM 4000-PROCESS-K703  THRU 4000-EXIT                            
             UNTIL (K703-NUM-SOCLIST(1:7) NOT = 'LISTDAT') OR                   
                   (EOF-K703).                                                  
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       1000-OPEN-ROUTINE.                                                       
      *-----------------*                                                       
                                                                                
           OPEN INPUT  BP13F705                                                 
                I-O    BP13K703                                                 
                OUTPUT BP13F700.                                                
                                                                                
           IF WS-K703-STATUS NOT = 00 AND 97                                    
              DISPLAY ' ERROR OPENING BP13K703, STATUS ' WS-K703-STATUS         
              MOVE WS-K703-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       2000-READ-K703.                                                          
      *--------------*                                                          
                                                                                
           MOVE SPACES                   TO BP13K703-REC.                       
           MOVE 'LISTDAT'                TO K703-KEY-FLD.                       
                                                                                
           START BP13K703 KEY >= K703-KEY-FLD.                                  
                                                                                
           EVALUATE  WS-K703-STATUS                                             
               WHEN  00                                                         
               WHEN  02                                                         
                     PERFORM 2100-READNEXT-K703 THRU 2100-EXIT                  
                     IF WS-K703-STATUS = '00' OR '02'                           
                        PERFORM 3000-READ-F705 THRU 3000-EXIT                   
                     END-IF                                                     
               WHEN  10                                                         
                     GO TO 2000-EXIT                                            
               WHEN  23                                                         
                     DISPLAY 'NO CASES INDICATED FOR SL  '                      
                             'FILE STATUS  : '  WS-K703-STATUS                  
                     DISPLAY 'K703-KEY-FLD : '  K703-KEY-FLD                    
                     PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                  
               WHEN  OTHER                                                      
                     DISPLAY 'ERROR IN START OF BP13K703 , '                    
                             'FILE STATUS  : '  WS-K703-STATUS                  
                     DISPLAY 'K703-KEY-FLD : '  K703-KEY-FLD                    
                     MOVE    WS-K703-STATUS     TO   RETURN-CODE                
                     PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2100-READNEXT-K703.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13K703 NEXT AT END MOVE 'Y'  TO  WS-EOF-K703.                 
                                                                                
           EVALUATE WS-K703-STATUS                                              
              WHEN  00                                                          
              WHEN  02                                                          
                    MOVE ZEROES TO WS-NUM-SUR-CNT                               
                    IF K703-NUM-SURVEY NOT = SPACES AND LOW-VALUES              
                      COMPUTE WS-NUM-SUR-CNT =                                  
                      FUNCTION NUMVAL-C(K703-NUM-SURVEY)                        
                    END-IF                                                      
                    ADD  1   TO  WS-READ-K703                                   
               WHEN 10                                                          
                    MOVE 'Y' TO  WS-EOF-K703                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K703, STATUS '                   
                             WS-K703-STATUS                                     
                    MOVE     WS-K703-STATUS     TO   RETURN-CODE                
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------*                                                          
       3000-READ-F705.                                                          
      *--------------*                                                          
                                                                                
           READ BP13F705   AT END MOVE  'Y' TO WS-EOF-F705                      
                           GO TO 3000-EXIT.                                     
                                                                                
           ADD 1 TO WS-READ-F705.                                               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       4000-PROCESS-K703.                                                       
      *-----------------*                                                       
                                                                                
           IF WS-EOF-F705 NOT = 'Y'                                             
      ***  DISPLAY '703 : ' K703-DTE-BALLOT-ORIGIN ' ' F705-DTE-BALLOT          
              IF K703-DTE-BALLOT-ORIGIN  =   F705-DTE-BALLOT                    
                 MOVE 'N'                    TO   WS-CHG-FLAG                   
                 MOVE ZEROES                 TO   WS-CHK-CNT                    
      ***  DISPLAY '703A: ' K703-NUM-NT ' ' F705-CDE-NT1                        
      ***  DISPLAY '703B: ' K703-NUM-FLAT ' ' F705-CDE-FLAT-TYPE                
                 PERFORM 5000-PROCESS-F700   THRU 5000-EXIT                     
                   UNTIL ((K703-NUM-NT < F705-CDE-NT1 AND                       
                          K703-NUM-FLAT = F705-CDE-FLAT-TYPE) OR                
                          (K703-NUM-NT = F705-CDE-NT1 AND                       
                          K703-NUM-FLAT < F705-CDE-FLAT-TYPE) OR                
                          (K703-NUM-NT < F705-CDE-NT1 AND                       
                          K703-NUM-FLAT < F705-CDE-FLAT-TYPE) OR                
                          (K703-NUM-NT < F705-CDE-NT1 AND                       
                          K703-NUM-FLAT > F705-CDE-FLAT-TYPE) OR                
                          (EOF-F705) OR (WS-CHG-FLAG = 'Y'))                    
              ELSE                                                              
      ***  DISPLAY '703C: ' K703-NUM-NT ' ' F705-CDE-NT1                        
      ***  DISPLAY '703D: ' K703-NUM-FLAT ' ' F705-CDE-FLAT-TYPE                
                 PERFORM 3000-READ-F705     THRU 3000-EXIT                      
                   UNTIL ((K703-DTE-BALLOT-ORIGIN = F705-DTE-BALLOT) OR         
                         (EOF-F705))                                            
                 MOVE 'N'                    TO   WS-CHG-FLAG                   
                 MOVE ZEROES                 TO   WS-CHK-CNT                    
      ***  DISPLAY '703C: ' K703-NUM-NT ' ' F705-CDE-NT1                        
      ***  DISPLAY '703D: ' K703-NUM-FLAT ' ' F705-CDE-FLAT-TYPE                
                 PERFORM 5000-PROCESS-F700   THRU 5000-EXIT                     
                   UNTIL ((K703-NUM-NT < F705-CDE-NT1 AND                       
                          K703-NUM-FLAT = F705-CDE-FLAT-TYPE) OR                
                          (K703-NUM-NT = F705-CDE-NT1 AND                       
                          K703-NUM-FLAT < F705-CDE-FLAT-TYPE) OR                
                          (K703-NUM-NT < F705-CDE-NT1 AND                       
                          K703-NUM-FLAT < F705-CDE-FLAT-TYPE) OR                
                          (K703-NUM-NT < F705-CDE-NT1 AND                       
                          K703-NUM-FLAT > F705-CDE-FLAT-TYPE) OR                
                          (EOF-F705) OR (WS-CHG-FLAG = 'Y'))                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 6000-DELETE-K703    THRU 6000-EXIT                           
           PERFORM 2100-READNEXT-K703  THRU 2100-EXIT.                          
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       5000-PROCESS-F700.                                                       
      *-----------------*                                                       
                                                                                
           IF K703-DTE-BALLOT-ORIGIN  =  F705-DTE-BALLOT                        
              IF F705-CDE-CATEGORY = '4'                                        
                 MOVE '3'  TO  WS-CDE-CATEGORY                                  
              ELSE                                                              
                 MOVE F705-CDE-CATEGORY  TO WS-CDE-CATEGORY                     
              END-IF                                                            
              IF ((F705-CDE-NT1        =  K703-NUM-NT  ) AND                    
                 (F705-CDE-FLAT-TYPE  =  K703-NUM-FLAT) AND                     
                 (WS-CDE-CATEGORY   =  K703-CDE-CATEGORY))                      
                                                                                
                  MOVE  SPACES               TO  BP13F700-REC                   
                  INITIALIZE BP13F700-REC                                       
                                                                                
                  MOVE BP13F705-REC(1:225)   TO  BP13F700-REC(1:225)            
                  MOVE K703-NUM-NT           TO  F700-CDE-SUCC-NT               
                  MOVE BP13F705-REC(229:105) TO  BP13F700-REC(229:105)          
                  MOVE K703-DTE-BALLOT-NEW   TO  F700-DTE-BALLOT                
                  MOVE BP13F705-REC(340:161) TO  BP13F700-REC(340:161)          
                                                                                
                  WRITE BP13F700-REC                                            
                  ADD    1               TO    WS-WRT-F700                      
                                               WS-CHK-CNT                       
      ***  DISPLAY 'CNT: ' WS-WRT-F700                                          
                                                                                
                  IF WS-CHK-CNT = WS-NUM-SUR-CNT                                
                     MOVE 'Y'    TO  WS-CHG-FLAG                                
                  END-IF                                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-F705    THRU 3000-EXIT.                            
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       6000-DELETE-K703.                                                        
      *--------------*                                                          
                                                                                
           DELETE BP13K703.                                                     
                                                                                
           EVALUATE  WS-K703-STATUS                                             
               WHEN  00                                                         
                     ADD  1   TO WS-DEL-K703                                    
               WHEN  OTHER                                                      
                     DISPLAY 'ERROR DELETE  BP13K703, STATUS '                  
                              WS-K703-STATUS                                    
                     MOVE WS-K703-STATUS        TO RETURN-CODE                  
                     PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       9000-CLOSE-ROUTINE.                                                      
      *------------------*                                                      
                                                                                
           DISPLAY ' *** CONTROL COUNT FOR BP13C780 ***'.                       
           DISPLAY 'TOTAL NO OF REC READ FROM F705 = ' WS-READ-F705.            
           DISPLAY 'TOTAL NO OF REC READ FROM K703 = ' WS-READ-K703             
           DISPLAY 'TOTAL NO WRIITEN INTO BP13F700 = ' WS-WRT-F700.             
           DISPLAY 'TOTAL NO DELETED FROM BP13K703 = ' WS-DEL-K703.             
                                                                                
           CLOSE BP13K703                                                       
                 BP13F705                                                       
                 BP13F700.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
