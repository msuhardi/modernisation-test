       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CK21.                                                  
      *AUTHOR.       IMC1.                                                      
      *DATE-WRITTEN. 13/08/2013.                                                
      * ========================================================== *            
      * FLAT INVENTORY SYSTEM (BM06)                               *            
      * ========================================================== *            
      *  OBJECTIVE: MATCH FTS NRIC1 WITH SUBLET SYSTEM             *            
      *           : GET CASE-REF-NUMBER IF NRIC MATCHED.           *            
      *                                                            *            
      *   INPUT  :  BP13F55A                                       *            
      *          :  BR12FV07                                       *            
      *                                                            *            
      *  OUTPUT  :  BP13OUTC BP13F55A                              *            
      * ========================================================== *            
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP135089 04/09/2013 IMC1 NEW PROGRAM.                         *         
      * BP137358 30/05/2018 FNP1 EXPAND BP13F55A FROM 800 TO 1200     *         
      * BP137439 05/09/2018 FNP1 EXPAND BP13OUT  FROM 800 TO 1200     *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F55A ASSIGN TO BP13F55A.                                  
                                                                                
           SELECT BR12FV07 ASSIGN TO BR12FV07.                                  
                                                                                
           SELECT BP13OUT  ASSIGN TO BP13OUT.                                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F55A                                                             
           BLOCK CONTAINS 0 CHARACTERS                                          
           RECORD CONTAINS 1200 CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY  BP13F55A.                                                  00015505
                                                                                
       FD  BR12FV07                                                             
           BLOCK CONTAINS 0 CHARACTERS                                          
           RECORD CONTAINS 300 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BR12FV07.                                                           
                                                                                
       FD  BP13OUT                                                              
           BLOCK CONTAINS 0 CHARACTERS                                          
           RECORD CONTAINS 1200 CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13OUT-REC           PIC X(1200).                           00015505
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-F55A-EOF                   PIC X    VALUE 'N'.                    
       01  WS-FV07-EOF                   PIC X    VALUE 'N'.                    
       01  WS-FV07-STATUS                PIC 99   VALUE ZEROS.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F55A-READ              PIC 9(9) VALUE ZEROS.                  
           05  WS-FV07-READ              PIC 9(9) VALUE ZEROS.                  
           05  WS-MATCH                  PIC 9(9) VALUE ZEROS.                  
           05  WS-FV07-NOTFND            PIC 9(9) VALUE ZEROS.                  
           05  WS-F55A-NOTFND            PIC 9(9) VALUE ZEROS.                  
           05  WS-F55AA-WRITE            PIC 9(9) VALUE ZEROS.                  
           05  WS-F55AN-WRITE            PIC 9(9) VALUE ZEROS.                  
           05  WS-BR12                   PIC X(1) VALUE SPACES.                 
           05  WS-BR12-BYPASS            PIC 9(9) VALUE ZEROS.                  
           05  WS-BR12-INVALID           PIC 9(9) VALUE ZEROS.                  
           05  WS-BR12-F                 PIC 9(9) VALUE ZEROS.                  
           05  WS-BR12-B                 PIC 9(9) VALUE ZEROS.                  
           05  WS-CASE-REF               PIC X(12) VALUE ZEROS.                 
           05  WS-BP13F55A-PREV          PIC X(60) VALUE ZEROS.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
                                                                                
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F55A    THRU 200-EXIT.                             
            PERFORM 210-READ-FV07    THRU 210-EXIT.                             
            PERFORM 300-PROCESS-F55A THRU 300-EXIT                              
                    UNTIL WS-F55A-EOF = 'Y'                                     
                    AND   WS-FV07-EOF = 'Y'.                                    
            PERFORM 900-CLOSE-FILES  THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN  INPUT BP13F55A                                                 
                       BR12FV07                                                 
                OUTPUT BP13OUT.                                                 
                                                                                
           IF WS-FV07-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, FV07-STATUS ' WS-FV07-STATUS              
              MOVE WS-FV07-STATUS       TO RETURN-CODE                          
              PERFORM 900-CLOSE-FILES THRU 900-EXIT.                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F55A.                                                           
      *-------------------------------------------------------------            
           READ BP13F55A AT END                                                 
                MOVE 'Y' TO WS-F55A-EOF                                         
                MOVE HIGH-VALUES TO F55A-NUM-NRIC1                              
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1         TO WS-F55A-READ.                                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       210-READ-FV07.                                                           
      *-------------------------------------------------------------            
           READ BR12FV07 AT END                                                 
                MOVE 'Y' TO WS-FV07-EOF                                         
                MOVE HIGH-VALUES TO FV07-NUM-IDNTY-NO                           
                GO TO 210-EXIT.                                                 
                                                                                
           ADD 1         TO WS-FV07-READ.                                       
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-F55A.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES TO WS-BR12                                               
                          WS-CASE-REF.                                          
                                                                                
           IF F55A-NUM-NRIC1 = FV07-NUM-IDNTY-NO(1:9)                           
                                                                                
              ADD 1             TO WS-MATCH                                     
              PERFORM 310-MATCH-BR12 THRU 310-EXIT                              
                UNTIL FV07-NUM-IDNTY-NO NOT = F55A-NUM-NRIC1                    
                   OR WS-FV07-EOF = 'Y'                                         
                                                                                
              IF (F55A-NUM-RDB-ROLE = SPACES OR LOW-VALUES) AND                 
                 (WS-BR12 = 'B' OR 'F')                                         
                  MOVE WS-BR12      TO F55A-NUM-SUBLET                          
                  MOVE WS-CASE-REF  TO F55A-NUM-RDB-HDB-REF                     
                  MOVE 'SL'         TO F55A-NUM-RDB-ROLE                        
              END-IF                                                            
                                                                                
              WRITE BP13OUT-REC     FROM BP13F55A-REC                           
                                                                                
              ADD 1                 TO WS-F55AA-WRITE                           
              PERFORM 200-READ-F55A THRU 200-EXIT                               
                                                                                
           ELSE                                                                 
              IF F55A-NUM-NRIC1 < FV07-NUM-IDNTY-NO(1:9)                        
                                                                                
                 ADD 1                    TO WS-F55A-NOTFND                     
                 MOVE SPACES              TO BP13OUT-REC                        
                                                                                
                 WRITE BP13OUT-REC        FROM BP13F55A-REC                     
                 ADD 1                    TO WS-F55AA-WRITE                     
                 PERFORM 200-READ-F55A    THRU 200-EXIT                         
              ELSE                                                              
                 ADD 1 TO WS-FV07-NOTFND                                        
                 PERFORM 210-READ-FV07    THRU 210-EXIT                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       310-MATCH-BR12.                                                          
                                                                                
           IF FV07-DTE-ROLE-END = '0001-01-01'                                  
              IF FV07-NUM-CASE-REF(1:1) = 'N' OR 'R'                            
                 MOVE 'B'                  TO WS-BR12                           
                 MOVE FV07-NUM-CASE-REF    TO WS-CASE-REF                       
                 ADD 1 TO WS-BR12-B                                             
              ELSE                                                              
                 IF FV07-NUM-CASE-REF(1:1) NOT = SPACE AND ZEROS                
                    MOVE 'F'               TO WS-BR12                           
                    MOVE FV07-NUM-CASE-REF TO WS-CASE-REF                       
                    ADD 1 TO WS-BR12-F                                          
                 ELSE                                                           
                    ADD 1 TO WS-BR12-BYPASS                                     
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
               ADD 1 TO WS-BR12-INVALID                                         
           END-IF.                                                              
                                                                                
           PERFORM 210-READ-FV07    THRU 210-EXIT.                              
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '------- BP13CK21 CONTROL TOTAL --------'.                   
           DISPLAY ' 1. REC READ    BP13F55A     = ' WS-F55A-READ.              
           DISPLAY ' 2. REC READ    BR12FV07     = ' WS-FV07-READ.              
           DISPLAY ' 3. REC WRITTEN BP13OUT      = ' WS-F55AA-WRITE.            
           DISPLAY '                               '.                           
           DISPLAY ' 4. FV07 NOTFD               = ' WS-FV07-NOTFND.            
           DISPLAY ' 5. F55A NOTFD               = ' WS-F55A-NOTFND.            
           DISPLAY ' 6. MATCH                    = ' WS-MATCH.                  
           DISPLAY ' 7. BR12 - BED               = ' WS-BR12-B.                 
           DISPLAY ' 8. BR12 - FLAT              = ' WS-BR12-F.                 
           DISPLAY ' 9. BR12 - BYPASS            = ' WS-BR12-BYPASS.            
           DISPLAY '10. BR12 - INVALID-DATE/EXP  = ' WS-BR12-INVALID.           
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F55A                                                       
                 BR12FV07                                                       
                 BP13OUT.                                                       
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
