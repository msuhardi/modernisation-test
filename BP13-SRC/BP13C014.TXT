       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.  BP13C014.                                                   
       AUTHOR.      ANC1.                                                       
       DATE-WRITTEN 30/05/2002.                                                 
                                                                                
      ****************************************************************          
      *            SYSTEM OF COMMITMENT (BP13)                       *          
      *--------------------------------------------------------------*          
      * OBJECTIVES  THIS PROGRAM IS TO CALL BP13C907 TO GET THE      *          
      *             DISTANCE BETWEEN TWO POSTAL CODES AND BLK-NO.    *          
      *             PROGRAM WILL BROWSE THRU ALL BLK-NO UNDER THE NT *          
      *             SO LONG AS ONE OF THE BLK-NO IS =< 2KM, GRANT    *          
      *             THE APPLN UNDER MCPS, MOVE 'T' TO RES-HA1.       *          
      *                                                              *          
      *             PRINT 3 REPORTS -                                *          
      *   P13L014A - WITHIN 2 KM                                     *          
      *   P13L014B - ALL BLK OUT OF 2 KM                             *          
      *   P13L014C - PRIVATE ADDR, BLK NOTFD IN BB29 FILE, APPLN ADDR*          
      *              NOTFD IN BB29 FILE                              *          
      *--------------------------------------------------------------*          
      * MODIFICATIONS :                                              *          
      *                                                              *          
      * CHGE REQ  DATE   AUTHOR  REMARKS                             *          
      * --------- ----- ----     ----------------------------------- *          
      * BP132162  200702 ANC -NEW PGM                                *          
      * BP132200  081102 ANC -MOVE 'T' TO RES-HA1,IF DIST =< 2KM     *          
      * BP132251  261102 ANC -PUT ALL BLKS WITHIN 2KM & OUT OF 2KM   *          
      *                       INTO SINGLE REPORT.                    *          
      * BP132376  290703 MJ16-INCLUDED REGNNO IN COPYCOB BP13F725 &  *          
      *                       REMOVED REC-TYPE & REF NO(RECOMPILE)   *          
      * BP132438  141103 CKK -TO MOVE NEW INELIG TAG                 *          
      * BP132538  270504 SK  -TO INCLUDE BP13K767                    *          
      * BP132730  280805 CKK -HARDCODE POSTAL CODE FOR CHECKING      *          
      * BP132818  150206 SSD -CHANGE REF NUM TO 8 BYTES              *          
      * BP132810  220206 CKK -CHECK 2 KM UP TO 2 DECIMAL PLACES      *          
      * BP132886  220706 CKK -USE NUMERIAL NT TO CHECK IN K016       *          
      * BP132913  240806 CKK -HARDCODE POSTAL CODE FOR CHECKING      *          
      * BP133004  240207 CKK -HARDCODE POSTAL CODE FOR CHECKING      *          
      * BP133072  240507 CKK -HARDCODE MAY 07 BTO POSTAL CODE        *          
      * BP133105  230707 IL5 -HARDCODE JUL 07 BE POSTAL CODE         *          
      * BP133177  231007 IL5 -HARDCODE OCT 07 BTO POSTAL CODE        *          
      * BP133211  271107 IL5 -HARDCODE NOV 07 BTO POSTAL CODE        *          
      * BP133226  211207 IL5 -HARDCODE DEC 07 BTO POSTAL CODE        *          
      * BP133248  160108 IL5 -HARDCODE JAN 08 BE POSTAL CODE         *          
      * BP133292  060108 IL5 -HARDCODE MAR 08 BTO POSTAL CODE        *          
      * BP133305  210508 IL5 -HARDCODE MAY 08 BTO POSTAL CODE        *          
      * BP133355  060608 IL5 -HARDCODE JUN 08 BTO POSTAL CODE        *          
      * BP133412  150808 IL5 -HARDCODE AUG 08 BTO POSTAL CODE        *          
      *                      -IF RECORD NOT FND IN F725, ELIG = BLNK *          
      * BP133438  220908 IL5 -HARDCODE SEP 08 BE POSTAL CODE         *          
      * BP133476  131108 IL5 -HARDCODE NOV 08 BTO POSTAL CODE        *          
      * BP133305  120508 MSD1-CATER FOR NEW FLAT TYPE OF BP13K016    *          
      * BP133610  030609 ESA1-TO REPLACE BP13K767 WITH BP13K816      *          
      * BP134482  250312 ZDD1-TO CATER FOR APPS & APPS ELIG-TAG      *          
      * BP134627  020812 IL5 -TO REFINE CONDITION FOR TAGGING        *          
      *                       APPS & APPS ELIG-TAGS                  *          
      * BP135254  150414 SMR -DELETE UNUSED FILE (BP13K816)          *          
      * BP136007  161115 SMR -CATER FOR ELDERLY                      *          
      * BP136239  110516 SMR -CHECK RES-HA1 BEFORE UPDATE            *          
      * BP136239  300516 SMR -ENHANCE TAGGING OF RES-HA1             *          
      * BP136609  200617 SMR -REPLACE BP13C907 WITH EJ03CS01 / ADD   *          
      *                       NEW REPORT P13L014B FOR ELIG NON-HDB   *          
      * BP137298  110418 JF7 -CHANGE DISTANCE CHECK FROM 2 TO 4 KM   *          
      * BP138040  141119 SSR -INCLUDE BALLOT QTR IN THE REPORT       *          
      * BP138727  310521 AM2 -FIX P13L014A REPORT                    *          
      *           040621 AM2 -INSPECT REPLACE NT1/NT2                *          
      * BP139673  210823 AM2 -EJ03 SUBR DISTANCE CHANGE FROM 9(3)V99 *          
      *                       TO 9(3)V999                            *          
      *                      -TO DISTANCE CUTOFF FROM 4.04 TO 4      *          
      *                      -CORRECT PRINTING OF RPT-DETAIL2        *          
      ****************************************************************          
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F730 ASSIGN  TO  BP13F730.                                
           SELECT BP13F725 ASSIGN  TO  BP13F725.                                
                                                                                
           SELECT BP13K730 ASSIGN  TO  BP13K730                                 
                     ORGANIZATION  IS  INDEXED                                  
                     ACCESS MODE   IS  RANDOM                                   
                     RECORD KEY    IS  K730-KEY-NEW                             
                     FILE STATUS   IS  WS-K730-STATUS.                          
                                                                                
           SELECT BP13K016 ASSIGN  TO  BP13K016                                 
                     ORGANIZATION  IS  INDEXED                                  
                     ACCESS MODE   IS  DYNAMIC                                  
                     RECORD KEY    IS  K016-KEY-FLD                             
                     FILE STATUS   IS  WS-K016-STATUS.                          
                                                                                
           SELECT P13L014A ASSIGN  TO  P13L014A.                                
           SELECT P13L014B ASSIGN  TO  P13L014B.                                
           SELECT P13L014C ASSIGN  TO  P13L014C.                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F730.                                                           
                                                                                
       FD   BP13F725                                                            
            RECORD CONTAINS 211 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F725.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
                                                                                
       COPY BP13K730.                                                           
                                                                                
       FD   BP13K016                                                            
            RECORD CONTAINS  50 CHARACTERS.                                     
                                                                                
       COPY BP13K016.                                                           
                                                                                
       FD   P13L014A                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
                                                                                
       01 P13L014-REC-A                      PIC  X(132).                       
                                                                                
       FD   P13L014B                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
                                                                                
       01 P13L014-REC-B                      PIC  X(132).                       
                                                                                
       FD   P13L014C                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
                                                                                
       01 P13L014-REC-C                      PIC  X(132).                       
                                                                                
                                                                                
      *----------------------------*                                            
       WORKING-STORAGE SECTION.                                                 
      *----------------------------*                                            
       01  WS-VARIABLES.                                                        
           05  WS-DIST                       PIC  9(03)V999.                    
           05  WS-NT1                        PIC  X(03).                        
           05  WS-PROP1                      PIC  X(01).                        
           05  WS-NT2                        PIC  X(03).                        
           05  WS-PROP2                      PIC  X(01).                        
           05  WS-TEMP-REFNO                 PIC  X(08).                        
           05  WS-NON-HDB                    PIC  X     VALUE 'N'.              
           05  WS-SAME-TOWN                  PIC  X     VALUE 'N'.              
           05  WS-F730-NOTFND                PIC  X     VALUE 'N'.              
           05  WS-F725-EOF                   PIC  X     VALUE 'N'.              
           05  WS-F730-EOF                   PIC  X     VALUE 'N'.              
           05  WS-K730-STATUS                PIC  9(02) VALUE ZEROES.           
           05  WS-K016-STATUS                PIC  9(02) VALUE ZEROES.           
           05  WS-PREV-LISTING               PIC  X(08) VALUE SPACES.           
           05  WS-REFNO-A                    PIC  X(08) VALUE SPACES.           
           05  WS-REFNO-B                    PIC  X(08) VALUE SPACES.           
           05  WS-REFNO-C                    PIC  X(08) VALUE SPACES.           
           05  WS-MCPS-TAG                   PIC  X     VALUE 'N'.              
           05  WS-INELIG-TAG                 PIC  X     VALUE SPACES.           
           05  WS-TEMP-DETAILS               PIC  X(96) VALUE SPACES.           
                                                                                
       01  WS-DATE.                                                             
           05  WS-CCYY                PIC 9(04).                                
           05  WS-MM                  PIC 9(02).                                
           05  WS-DD                  PIC 9(02).                                
                                                                                
       01  WS-DATE-FORMATTED.                                                   
           05  WS-DD-F                PIC X(02).                                
           05  FILLER                 PIC X VALUE '/'.                          
           05  WS-MM-F                PIC X(02).                                
           05  FILLER                 PIC X VALUE '/'.                          
           05  WS-CCYY-F              PIC X(04).                                
                                                                                
       01  WS-POSTAL-TABLE.                                                     
           05  WS-POSTAL-ARRAY   OCCURS 2000 TIMES.                             
               10  WS-PC1-REFNO              PIC  X(08).                        
               10  WS-PC1-NRIC               PIC  X(09).                        
               10  WS-PC1-CODE               PIC  X(06).                        
               10  WS-PC1-BLK                PIC  X(05).                        
               10  WS-PC2-NT                 PIC  X(03).                        
               10  WS-PC2-CODE               PIC  X(06).                        
               10  WS-PC2-BLK                PIC  X(05).                        
               10  WS-PC2-DTE-BALLOT         PIC  X(06).                        
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-SUB                        PIC 9(6) VALUE ZEROES.             
           05  WS-INDX                       PIC 9(6) VALUE ZEROES.             
           05  WS-F725-READ                  PIC 9(6) VALUE ZEROES.             
           05  WS-F730-READ                  PIC 9(6) VALUE ZEROES.             
           05  WS-K016-READ                  PIC 9(6) VALUE ZEROES.             
           05  WS-K730-READ                  PIC 9(6) VALUE ZEROES.             
           05  WS-F730-NOTMATCH              PIC 9(6) VALUE ZEROES.             
           05  WS-F725-NOTMATCH              PIC 9(6) VALUE ZEROES.             
           05  WS-K016-NTFND                 PIC 9(6) VALUE ZEROES.             
           05  WS-K730-NTFND                 PIC 9(6) VALUE ZEROES.             
           05  WS-K730-REWRITE               PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-A                      PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-PAGE-A                 PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-PAGE-B                 PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-PAGE-C                 PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-LINE-A                 PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-LINE-B                 PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-LINE-C                 PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-SNO-A                  PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-SNO-B                  PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-SNO-C                  PIC 9(6) VALUE ZEROES.             
                                                                                
       01  RPT-HEADER1.                                                         
           05  RPT-LISTING                PIC X(08) VALUE 'P13L014A'.           
           05  FILLER                     PIC X(10) VALUE '      HDB3'.         
           05  FILLER                     PIC X(27) VALUE SPACES.               
           05  FILLER                     PIC X(39) VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                     PIC X(14) VALUE SPACES.               
           05  FILLER                     PIC X(07) VALUE 'DATE : '.            
           05  RPT-DTE-CURRENT            PIC X(10) VALUE SPACES.               
           05  FILLER                     PIC X(04) VALUE SPACES.               
           05  FILLER                     PIC X(07) VALUE 'PAGE : '.            
           05  RPT-PAGE                   PIC ZZZZZ9.                           
                                                                                
       01  RPT-HEADER2.                                                         
           05  FILLER                     PIC X(38) VALUE SPACES.               
           05  FILLER                     PIC X(55) VALUE                       
               'REPORT ON DISTANCE COMPUTATION BASED ON POSTAL CODES'.          
           05  FILLER                     PIC X(39) VALUE SPACES.               
                                                                                
       01  RPT-HEADER3.                                                         
           05  FILLER                     PIC X(42) VALUE SPACES.               
           05  RPT-LISTING-TYPE           PIC X(45) VALUE SPACES.               
           05  FILLER                     PIC X(39) VALUE SPACES.               
                                                                                
       01  RPT-CAPTION-A.                                                       
           05  FILLER                     PIC X(35) VALUE                       
              ' S/NO   REFNO    NT   FT  MCPS-NRIC'.                            
           05  FILLER                     PIC X(51) VALUE                       
              '                  ADDRESS                   POSTAL1'.            
           05  FILLER                     PIC X(44) VALUE                       
              '  NT1 PC1 POSTAL2 BALDTE NT2 PC2 BLK    DIST'.                   
                                                                                
       01  RPT-CAPTION-B.                                                       
           05  FILLER                     PIC X(35) VALUE                       
              ' S/NO   REFNO    NT   FT  MCPS-NRIC'.                            
           05  FILLER                     PIC X(51) VALUE                       
              '                  ADDRESS                   POSTAL1'.            
           05  FILLER                     PIC X(9)  VALUE                       
              '  NT1 PC1'.                                                      
                                                                                
       01  RPT-CAPTION-C.                                                       
           05  FILLER                     PIC X(35) VALUE                       
              ' S/NO   REFNO    NT   FT  MCPS-NRIC'.                            
           05  FILLER                     PIC X(51) VALUE                       
              '                  ADDRESS                   POSTAL1'.            
           05  FILLER                     PIC X(17) VALUE                       
              '  NT1 PC1 REMARKS'.                                              
                                                                                
       01  RPT-DETAILS.                                                         
           05  RPT-CNT-SNO                PIC ZZZZ9 VALUE ZEROES.               
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  RPT-REFNO                  PIC X(08) VALUE SPACES.               
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  RPT-NT                     PIC X(03) VALUE SPACES.               
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  RPT-FT                     PIC X(02) VALUE SPACES.               
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  RPT-MCPS-NRIC              PIC X(09) VALUE SPACES.               
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  RPT-ADDRESS                PIC X(40) VALUE SPACES.               
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  RPT-POSTAL1                PIC X(06) VALUE SPACES.               
           05  FILLER                     PIC X(03) VALUE SPACES.               
           05  RPT-NT1                    PIC X(03) VALUE SPACES.               
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  RPT-PROP1                  PIC X(01) VALUE SPACES.               
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  RPT-GROUP1.                                                      
               10  RPT-POSTAL2            PIC X(06) VALUE SPACES.               
               10  FILLER                 PIC X(02) VALUE SPACES.               
               10  RPT-DTE-BALLOT         PIC X(06) VALUE SPACES.               
               10  FILLER                 PIC X(01) VALUE SPACES.               
               10  RPT-NT2                PIC X(03) VALUE SPACES.               
               10  FILLER                 PIC X(02) VALUE SPACES.               
               10  RPT-PROP2              PIC X(01) VALUE SPACES.               
               10  FILLER                 PIC X(02) VALUE SPACES.               
               10  RPT-BLK                PIC X(05) VALUE SPACES.               
               10  FILLER                 PIC X(01) VALUE SPACES.               
               10  RPT-DISTANCE           PIC ZZ9.999 VALUE ZEROES.             
                                                                                
       01  RPT-DETAIL2.                                                         
           05  FILLER                     PIC X(51) VALUE ALL '*'.              
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  FILLER                     PIC X(23) VALUE                       
               'MCPS ELIGIBILITY TAG = '.                                       
           05  RPT-MCPS-TAG               PIC X(01) VALUE SPACES.               
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  FILLER                     PIC X(50) VALUE ALL '*'.              
                                                                                
                                                                                
      *----------------------------*                                            
      *    LINKAGE FOR EJ03CS01    *                                            
      *----------------------------*                                            
       01  LINK-REC.                                                            
           05  LINK-OPEN-CLOSE               PIC X.                             
           05  LINK-RETURN-CODE              PIC 9(02).                         
           05  LINK-POSTAL-CODE-1            PIC X(06).                         
           05  LINK-TOWN-NAME-1              PIC X(15).                         
           05  LINK-PROPERTY-CODE-1          PIC X(01).                         
           05  LINK-POSTAL-CODE-2            PIC X(06).                         
           05  LINK-TOWN-NAME-2              PIC X(15).                         
           05  LINK-PROPERTY-CODE-2          PIC X(01).                         
           05  LINK-DISTANCE                 PIC 9(03)V999.                     
           05  LINK-RETURN-MSG1              PIC X(10).                         
           05  LINK-RETURN-MSG2              PIC X(10).                         
                                                                                
      *----------------------------*                                            
       PROCEDURE DIVISION.                                                      
      *----------------------------*                                            
      ******************************************************                    
       0000-MAIN-PROCESS.                                                       
      ******************************************************                    
           PERFORM 0100-INITIALIZATION  THRU 0100-EXIT.                         
           PERFORM 0300-READ-BP13F730   THRU 0300-EXIT.                         
           PERFORM 0200-READ-BP13F725   THRU 0200-EXIT.                         
           PERFORM 0600-PROCESS-RECORDS THRU 0600-EXIT                          
                   UNTIL F730-NUM-RES-NRIC1 = HIGH-VALUES                       
                     AND F725-NUM-NRIC      = HIGH-VALUES.                      
           PERFORM 9999-CLOSE-FILES     THRU 9999-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0100-INITIALIZATION.                                                     
      ******************************************************                    
           OPEN INPUT BP13F730                                                  
                      BP13F725                                                  
                      BP13K016                                                  
                  I-O BP13K730                                                  
               OUTPUT P13L014A                                                  
                      P13L014B                                                  
                      P13L014C.                                                 
                                                                                
           IF WS-K730-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K730: ' WS-K730-STATUS                 
              MOVE WS-K730-STATUS      TO RETURN-CODE                           
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K016-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K016: ' WS-K016-STATUS                 
              MOVE WS-K016-STATUS      TO RETURN-CODE                           
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
           END-IF.                                                              
                                                                                
           INITIALIZE LINK-REC.                                                 
           MOVE 'O'        TO LINK-OPEN-CLOSE.                                  
           CALL 'EJ03CS01' USING LINK-REC.                                      
           IF LINK-RETURN-CODE NOT = 00 AND 97                                  
              MOVE LINK-RETURN-CODE   TO RETURN-CODE                            
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
           END-IF.                                                              
           MOVE SPACE      TO LINK-OPEN-CLOSE.                                  
                                                                                
           MOVE  FUNCTION CURRENT-DATE  TO WS-DATE.                             
           MOVE  WS-CCYY                TO WS-CCYY-F.                           
           MOVE  WS-MM                  TO WS-MM-F.                             
           MOVE  WS-DD                  TO WS-DD-F.                             
           MOVE  WS-DATE-FORMATTED      TO RPT-DTE-CURRENT.                     
                                                                                
       0100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0200-READ-BP13F725.                                                      
      ******************************************************                    
           READ BP13F725 AT END                                                 
                MOVE HIGH-VALUES         TO F725-NUM-NRIC                       
                GO TO 0200-EXIT.                                                
                                                                                
           ADD  1                        TO WS-F725-READ.                       
                                                                                
       0200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0300-READ-BP13F730.                                                      
      ******************************************************                    
           READ BP13F730 AT END                                                 
                MOVE HIGH-VALUES         TO F730-NUM-RES-NRIC1                  
               GO TO 0300-EXIT.                                                 
                                                                                
           ADD  1                        TO WS-F730-READ.                       
                                                                                
       0300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0400-READ-BP13K016.                                                      
      ******************************************************                    
                                                                                
           MOVE ZEROES                   TO WS-SUB.                             
           MOVE SPACES                   TO K016-KEY-FLD.                       
           MOVE F730-CDE-NT1             TO K016-NUM-NT.                        
           MOVE F730-DTE-BALLOT          TO K016-DTE-BALLOT.                    
                                                                                
           START BP13K016 KEY IS >= K016-KEY-FLD.                               
                                                                                
           IF WS-K016-STATUS = 00 OR 10                                         
              PERFORM 0500-READNXT-BP13K016 THRU 0500-EXIT                      
                   UNTIL K016-NUM-NT      NOT = F730-CDE-NT1                    
                   OR    K016-DTE-BALLOT  NOT = F730-DTE-BALLOT                 
                   OR    K016-KEY-FLD         = HIGH-VALUES                     
           ELSE                                                                 
              IF WS-K016-STATUS = 23                                            
                 ADD 1                   TO WS-K016-NTFND                       
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K016,STATUS ' WS-K016-STATUS           
                 MOVE WS-K016-STATUS     TO RETURN-CODE                         
                 GO TO 9999-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       0400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0500-READNXT-BP13K016.                                                   
      ******************************************************                    
           READ BP13K016 NEXT                                                   
                AT END MOVE HIGH-VALUES TO K016-KEY-FLD                         
                GO TO 0500-EXIT.                                                
                                                                                
           ADD 1                        TO WS-K016-READ.                        
                                                                                
           IF  K016-NUM-NT = F730-CDE-NT1                                       
           AND K016-CDE-FLAT-TYPE = F730-CDE-FLAT-TYPE                          
           AND K016-DTE-BALLOT = F730-DTE-BALLOT                                
               PERFORM 0540-MOVE-POST-BLK-NT THRU 0540-EXIT                     
           END-IF.                                                              
                                                                                
       0500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ********************************************                              
       0540-MOVE-POST-BLK-NT.                                                   
      ********************************************                              
           ADD  1                    TO WS-SUB.                                 
           MOVE K016-NUM-POSTAL      TO WS-PC2-CODE(WS-SUB).                    
           MOVE K016-NUM-BLK         TO WS-PC2-BLK (WS-SUB).                    
           MOVE K016-NUM-NT          TO WS-PC2-NT  (WS-SUB).                    
           MOVE K016-DTE-BALLOT      TO WS-PC2-DTE-BALLOT (WS-SUB).             
                                                                                
       0540-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0600-PROCESS-RECORDS.                                                    
      ******************************************************                    
           IF F725-NUM-NRIC   = F730-NUM-RES-NRIC1                              
              MOVE 'N'                      TO WS-F730-NOTFND                   
              INITIALIZE                       WS-POSTAL-TABLE                  
              MOVE F725-CDE-POSTAL          TO WS-PC1-CODE(1)                   
              MOVE F725-NUM-BLK-HSE         TO WS-PC1-BLK(1)                    
              PERFORM 0400-READ-BP13K016    THRU 0400-EXIT                      
              IF WS-K016-STATUS = 00 OR 10                                      
                 MOVE    1                     TO WS-INDX                       
                 MOVE SPACES                   TO WS-MCPS-TAG                   
                 MOVE ZEROES                   TO WS-CNT-A                      
                 PERFORM 0700-GET-DISTANCE     THRU 0700-EXIT                   
                    VARYING WS-INDX FROM 1 BY 1                                 
                    UNTIL WS-INDX > WS-SUB                                      
                 IF WS-NON-HDB = 'Y'                                            
                    PERFORM 0750-PROCESS-NON-HDB  THRU 0750-EXIT                
                 END-IF                                                         
                 PERFORM 0800-PROCESS-BP13K730 THRU 0800-EXIT                   
                 IF WS-CNT-A > 0                                                
                    MOVE WS-MCPS-TAG     TO RPT-MCPS-TAG                        
                    WRITE P13L014-REC-A  FROM RPT-DETAIL2 AFTER 1               
                 END-IF                                                         
              END-IF                                                            
              PERFORM 0300-READ-BP13F730    THRU 0300-EXIT                      
              PERFORM 0200-READ-BP13F725    THRU 0200-EXIT                      
           ELSE                                                                 
              IF F725-NUM-NRIC  <  F730-NUM-RES-NRIC1                           
                 ADD 1 TO WS-F725-NOTMATCH                                      
                 PERFORM 0200-READ-BP13F725 THRU 0200-EXIT                      
              ELSE                                                              
                 MOVE 'Y'                   TO WS-F730-NOTFND                   
                 PERFORM 0900-PRINT-REPORT  THRU 0900-EXIT                      
                 PERFORM 0800-PROCESS-BP13K730 THRU 0800-EXIT                   
                 ADD 1 TO WS-F730-NOTMATCH                                      
                 PERFORM 0300-READ-BP13F730 THRU 0300-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       0600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0700-GET-DISTANCE.                                                       
      ******************************************************                    
      *** WS-SAME-TOWN : Y     - SAME TOWN ***                                  
      ***                N     - DIFF TOWN ***                                  
      ***                BLANK - NOT FOUND ***                                  
                                                                                
           MOVE SPACES                TO LINK-REC.                              
           INITIALIZE                    LINK-REC.                              
                                                                                
           MOVE ZEROES                TO WS-DIST.                               
           MOVE SPACES                TO WS-NT1                                 
                                         WS-PROP1                               
                                         WS-NT2                                 
                                         WS-PROP2.                              
                                                                                
           MOVE WS-PC1-CODE(1)        TO LINK-POSTAL-CODE-1.                    
           MOVE WS-PC2-CODE(WS-INDX)  TO LINK-POSTAL-CODE-2.                    
                                                                                
           CALL 'EJ03CS01' USING LINK-REC.                                      
                                                                                
           IF (LINK-RETURN-MSG1 = SPACES OR LOW-VALUES) AND                     
              (LINK-RETURN-MSG2 = SPACES OR LOW-VALUES)                         
              MOVE LINK-DISTANCE         TO WS-DIST                             
              MOVE LINK-TOWN-NAME-1(1:3) TO WS-NT1                              
              MOVE LINK-PROPERTY-CODE-1  TO WS-PROP1                            
              MOVE LINK-TOWN-NAME-2(1:3) TO WS-NT2                              
              MOVE LINK-PROPERTY-CODE-2  TO WS-PROP2                            
              IF LINK-PROPERTY-CODE-1 = 'P'                                     
                 MOVE 'Y'                TO WS-NON-HDB                          
              ELSE                                                              
                 MOVE 'N'                TO WS-NON-HDB                          
              END-IF                                                            
              IF LINK-TOWN-NAME-1(1:3) = LINK-TOWN-NAME-2(1:3)                  
                 MOVE 'Y'                TO WS-SAME-TOWN                        
              ELSE                                                              
                 MOVE 'N'                TO WS-SAME-TOWN                        
              END-IF                                                            
           ELSE                                                                 
              IF F725-NUM-ADDR-TYPE NOT = 'A'                                   
                 MOVE 'Y'                TO WS-NON-HDB                          
              ELSE                                                              
                 MOVE 'N'                TO WS-NON-HDB                          
              END-IF                                                            
              MOVE SPACES                TO WS-SAME-TOWN                        
           END-IF.                                                              
                                                                                
           PERFORM 0900-PRINT-REPORT  THRU 0900-EXIT.                           
                                                                                
       0700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0750-PROCESS-NON-HDB.                                                    
      ******************************************************                    
           MOVE SPACES                       TO RPT-DETAILS.                    
           MOVE WS-TEMP-DETAILS              TO RPT-DETAILS(1:96).              
                                                                                
           IF WS-INELIG-TAG = 'T'                                               
              PERFORM 1000-PRINT-REPORT-B    THRU 1000-EXIT                     
           ELSE                                                                 
              IF WS-INELIG-TAG = 'P'                                            
                 IF WS-SAME-TOWN = 'N' OR 'Y'                                   
                    MOVE 'DISTANCE IS GREATER THAN 4.0'                         
                                             TO RPT-GROUP1                      
                 ELSE                                                           
                    IF WS-SAME-TOWN = SPACES                                    
                       MOVE 'NOT FOUND IN EJ03K320'                             
                                             TO RPT-GROUP1                      
                    END-IF                                                      
                 END-IF                                                         
                 PERFORM 1050-PRINT-REPORT-C THRU 1050-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       0750-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0800-PROCESS-BP13K730.                                                   
      ******************************************************                    
           MOVE F730-NUM-REGN         TO K730-NUM-REGN.                         
                                                                                
           READ BP13K730.                                                       
                                                                                
           IF WS-K730-STATUS = 00                                               
              ADD   1                       TO WS-K730-READ                     
              PERFORM 0850-REWRITE-BP13K730 THRU 0850-EXIT                      
           ELSE                                                                 
              IF WS-K730-STATUS = 23                                            
                 ADD  1                  TO WS-K730-NTFND                       
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K730,STATUS ' WS-K730-STATUS           
                 MOVE WS-K730-STATUS     TO RETURN-CODE                         
                 GO TO 9999-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       0800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0850-REWRITE-BP13K730.                                                   
      ******************************************************                    
      *       MOVE 'T'                      TO K730-CDE-RESIDENT-HA1            
           IF (K730-NUM-MCPS NOT = SPACES AND LOW-VALUES) AND                   
              (K730-CDE-RESIDENT-HA1 = SPACES OR LOW-VALUES)                    
              MOVE WS-INELIG-TAG            TO K730-CDE-RESIDENT-HA1            
           END-IF.                                                              
                                                                                
           IF (K730-CDE-FLAT-TYPE = '2F') AND                                   
              (K730-NUM-ELDERLY = 'S' OR 'F') AND                               
               F730-NUM-NRIC1  = F730-NUM-RES-NRIC1                             
             IF K730-NUM-APPS  NOT = SPACES AND LOW-VALUES                      
                 MOVE WS-INELIG-TAG   TO K730-NUM-APPS-ELIG-TAG                 
             ELSE                                                               
                IF WS-INELIG-TAG = 'T'                                          
                     MOVE WS-INELIG-TAG   TO K730-NUM-APPS-ELIG-TAG             
                     MOVE 'F'  TO K730-NUM-APPS                                 
                END-IF                                                          
             END-IF                                                             
           END-IF.                                                              
                                                                                
           REWRITE BP13K730-REC.                                                
                                                                                
           IF WS-K730-STATUS = 00                                               
              ADD  1                     TO WS-K730-REWRITE                     
           ELSE                                                                 
              DISPLAY 'ERROR REWRITE BP13K730,STATUS ' WS-K730-STATUS           
              MOVE WS-K730-STATUS     TO RETURN-CODE                            
              GO TO 9999-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
       0850-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0900-PRINT-REPORT.                                                       
      ******************************************************                    
           MOVE SPACES                TO  RPT-DETAILS                           
                                          WS-TEMP-DETAILS.                      
           INITIALIZE                     RPT-DETAILS.                          
                                                                                
           IF WS-F730-NOTFND = 'Y'                                              
              MOVE F730-NUM-REF            TO RPT-REFNO                         
              MOVE F730-CDE-NT1            TO RPT-NT                            
              MOVE F730-CDE-FLAT-TYPE      TO RPT-FT                            
              MOVE F730-NUM-RES-NRIC1      TO RPT-MCPS-NRIC                     
              MOVE 'RECORD NOT FOUND IN BP13F725'                               
                                           TO RPT-GROUP1                        
           ELSE                                                                 
              MOVE F730-NUM-REF            TO RPT-REFNO                         
              MOVE F730-CDE-NT1            TO RPT-NT                            
              MOVE F730-CDE-FLAT-TYPE      TO RPT-FT                            
              MOVE F730-NUM-RES-NRIC1      TO RPT-MCPS-NRIC                     
              STRING 'BLK '                                                     
                      F725-NUM-BLK-HSE                                          
                     ' #'                                                       
                      F725-NUM-STOREY                                           
                     '-'                                                        
                      F725-NUM-UNIT ' '                                         
                      F725-NME-STREET ' '                                       
                      DELIMITED BY SIZE INTO  RPT-ADDRESS                       
              MOVE WS-PC1-CODE (1)         TO RPT-POSTAL1                       
              MOVE WS-NT1                  TO RPT-NT1                           
              INSPECT RPT-NT1 REPLACING ALL X'25' BY SPACES                     
                                        ALL X'00' BY SPACES                     
              MOVE WS-PROP1                TO RPT-PROP1                         
              MOVE WS-PC2-CODE (WS-INDX)   TO RPT-POSTAL2                       
              MOVE WS-PC2-DTE-BALLOT (WS-INDX)                                  
                                           TO RPT-DTE-BALLOT                    
              MOVE WS-NT2                  TO RPT-NT2                           
              INSPECT RPT-NT2 REPLACING ALL X'25' BY SPACES                     
                                        ALL X'00' BY SPACES                     
              MOVE WS-PROP2                TO RPT-PROP2                         
              MOVE WS-PC2-BLK  (WS-INDX)   TO RPT-BLK                           
              MOVE WS-DIST                 TO RPT-DISTANCE                      
           END-IF.                                                              
                                                                                
           MOVE RPT-DETAILS(1:96)          TO WS-TEMP-DETAILS.                  
                                                                                
           IF WS-F730-NOTFND = 'Y'                                              
              INITIALIZE  WS-DIST                                               
              MOVE SPACES                    TO WS-MCPS-TAG                     
              MOVE SPACES                    TO WS-INELIG-TAG                   
              PERFORM 1050-PRINT-REPORT-C THRU 1050-EXIT                        
           ELSE                                                                 
              IF WS-NON-HDB = 'Y'                                               
                 IF WS-SAME-TOWN = 'N' OR 'Y'                                   
                    IF WS-DIST > 4                                              
                       IF WS-MCPS-TAG NOT = 'Y'                                 
                          MOVE 'P'           TO WS-INELIG-TAG                   
                          MOVE 'P'           TO WS-MCPS-TAG                     
                       ELSE                                                     
                          MOVE 'T'           TO WS-INELIG-TAG                   
                          MOVE 'Y'           TO WS-MCPS-TAG                     
                       END-IF                                                   
                    ELSE                                                        
                       MOVE 'T'              TO WS-INELIG-TAG                   
                       MOVE 'Y'              TO WS-MCPS-TAG                     
                    END-IF                                                      
                 ELSE                                                           
                    IF WS-MCPS-TAG NOT = 'Y'                                    
                       MOVE 'P'              TO WS-INELIG-TAG                   
                       MOVE 'P'              TO WS-MCPS-TAG                     
                    ELSE                                                        
                       MOVE 'T'              TO WS-INELIG-TAG                   
                       MOVE 'Y'              TO WS-MCPS-TAG                     
                    END-IF                                                      
                 END-IF                                                         
                 PERFORM 0950-PRINT-REPORT-A THRU 0950-EXIT                     
              ELSE                                                              
                 IF WS-DIST > 4                                                 
                    IF WS-MCPS-TAG NOT = 'Y'                                    
                       MOVE 'H'              TO WS-INELIG-TAG                   
                       MOVE 'H'              TO WS-MCPS-TAG                     
                    ELSE                                                        
                       MOVE 'T'              TO WS-INELIG-TAG                   
                       MOVE 'Y'              TO WS-MCPS-TAG                     
                    END-IF                                                      
                 ELSE                                                           
                    IF WS-SAME-TOWN = SPACES                                    
                       IF WS-MCPS-TAG NOT = 'Y'                                 
                          MOVE 'H'           TO WS-INELIG-TAG                   
                          MOVE 'H'           TO WS-MCPS-TAG                     
                       ELSE                                                     
                          MOVE 'T'           TO WS-INELIG-TAG                   
                          MOVE 'Y'           TO WS-MCPS-TAG                     
                       END-IF                                                   
                    ELSE                                                        
                       MOVE 'T'              TO WS-INELIG-TAG                   
                       MOVE 'Y'              TO WS-MCPS-TAG                     
                    END-IF                                                      
                 END-IF                                                         
                 PERFORM 0950-PRINT-REPORT-A THRU 0950-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       0900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0950-PRINT-REPORT-A.                                                     
      ******************************************************                    
           MOVE 'P13L014A'                 TO RPT-LISTING.                      
           MOVE 'SHORTEST DISTANCE COMPUTED FOR MCPS ELIBILITY'                 
                                           TO RPT-LISTING-TYPE.                 
           IF WS-CNT-LINE-A = ZEROES OR                                         
              WS-CNT-LINE-A > 56                                                
              ADD   1                      TO WS-CNT-PAGE-A                     
              MOVE  WS-CNT-PAGE-A          TO RPT-PAGE                          
              WRITE P13L014-REC-A FROM RPT-HEADER1  AFTER PAGE                  
              WRITE P13L014-REC-A FROM RPT-HEADER2  AFTER 1                     
              WRITE P13L014-REC-A FROM RPT-HEADER3  AFTER 1                     
              MOVE  SPACES TO P13L014-REC-A                                     
              WRITE P13L014-REC-A                                               
              WRITE P13L014-REC-A FROM RPT-CAPTION-A                            
              MOVE  SPACES TO P13L014-REC-A                                     
              WRITE P13L014-REC-A                                               
              MOVE  5 TO WS-CNT-LINE-A                                          
           END-IF.                                                              
                                                                                
           IF F730-NUM-REF NOT = WS-REFNO-A                                     
              ADD   1                      TO WS-CNT-SNO-A                      
              MOVE  WS-CNT-SNO-A           TO RPT-CNT-SNO                       
           ELSE                                                                 
              MOVE SPACES               TO RPT-DETAILS(1:96)                    
           END-IF.                                                              
                                                                                
           WRITE P13L014-REC-A          FROM RPT-DETAILS AFTER 1.               
           ADD   1                      TO WS-CNT-LINE-A                        
                                           WS-CNT-A.                            
           MOVE 'P13L014A'              TO WS-PREV-LISTING.                     
                                                                                
           IF RPT-REFNO NOT = SPACES AND LOW-VALUES                             
              MOVE RPT-REFNO               TO WS-REFNO-A                        
           END-IF.                                                              
                                                                                
       0950-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       1000-PRINT-REPORT-B.                                                     
      ******************************************************                    
           MOVE 'P13L014B'                 TO RPT-LISTING.                      
           MOVE '  NON-HDB PROPERTIES DETERMINED AS ELIGIBLE  '                 
                                           TO RPT-LISTING-TYPE.                 
           IF WS-CNT-LINE-B = ZEROES OR                                         
              WS-CNT-LINE-B > 56                                                
              ADD   1                      TO WS-CNT-PAGE-B                     
              MOVE  WS-CNT-PAGE-B          TO RPT-PAGE                          
              WRITE P13L014-REC-B FROM RPT-HEADER1  AFTER PAGE                  
              WRITE P13L014-REC-B FROM RPT-HEADER2  AFTER 1                     
              WRITE P13L014-REC-B FROM RPT-HEADER3  AFTER 1                     
              MOVE  SPACES TO P13L014-REC-B                                     
              WRITE P13L014-REC-B                                               
              WRITE P13L014-REC-B FROM RPT-CAPTION-B                            
              MOVE  SPACES TO P13L014-REC-B                                     
              WRITE P13L014-REC-B                                               
              MOVE  5 TO WS-CNT-LINE-B                                          
           END-IF.                                                              
                                                                                
           IF F730-NUM-REF NOT = WS-REFNO-B                                     
              ADD   1                      TO WS-CNT-SNO-B                      
              MOVE  WS-CNT-SNO-B           TO RPT-CNT-SNO                       
           ELSE                                                                 
              MOVE SPACES            TO RPT-DETAILS(1:96)                       
           END-IF.                                                              
                                                                                
           WRITE P13L014-REC-B          FROM RPT-DETAILS AFTER 1.               
           ADD   1                      TO WS-CNT-LINE-B.                       
           MOVE 'P13L014B'              TO WS-PREV-LISTING.                     
                                                                                
           IF RPT-REFNO NOT = SPACES AND LOW-VALUES                             
              MOVE RPT-REFNO               TO WS-REFNO-B                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       1050-PRINT-REPORT-C.                                                     
      ******************************************************                    
           MOVE 'P13L014C'                 TO RPT-LISTING.                      
           MOVE '      DISTANCE COMPUTATION - EXCEPTIONS      '                 
                                           TO RPT-LISTING-TYPE.                 
                                                                                
           IF WS-CNT-LINE-C = ZEROES OR                                         
              WS-CNT-LINE-C > 56                                                
              ADD   1                      TO WS-CNT-PAGE-C                     
              MOVE  WS-CNT-PAGE-C          TO RPT-PAGE                          
              WRITE P13L014-REC-C FROM RPT-HEADER1  AFTER PAGE                  
              WRITE P13L014-REC-C FROM RPT-HEADER2  AFTER 1                     
              WRITE P13L014-REC-C FROM RPT-HEADER3  AFTER 1                     
              MOVE  SPACES TO P13L014-REC-C                                     
              WRITE P13L014-REC-C                                               
              WRITE P13L014-REC-C FROM RPT-CAPTION-C                            
              MOVE  SPACES TO P13L014-REC-C                                     
              WRITE P13L014-REC-C                                               
              MOVE  5 TO WS-CNT-LINE-C                                          
           END-IF.                                                              
                                                                                
           IF F730-NUM-REF NOT = WS-REFNO-C                                     
              ADD   1                      TO WS-CNT-SNO-C                      
              MOVE  WS-CNT-SNO-C           TO RPT-CNT-SNO                       
           ELSE                                                                 
              MOVE SPACES            TO RPT-DETAILS(1:96)                       
           END-IF.                                                              
                                                                                
           WRITE P13L014-REC-C     FROM RPT-DETAILS  AFTER 1.                   
           ADD   1                      TO WS-CNT-LINE-C.                       
           MOVE 'P13L014C'              TO WS-PREV-LISTING.                     
                                                                                
           IF RPT-REFNO NOT = SPACES AND LOW-VALUES                             
              MOVE RPT-REFNO               TO WS-REFNO-C                        
           END-IF.                                                              
                                                                                
       1050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************                                                 
       9999-CLOSE-FILES.                                                        
      *************************                                                 
           DISPLAY '* ------------ BP13C014 ------------- * '.                  
           DISPLAY 'NO OF REC READ    BP13F730 = ' WS-F730-READ.                
           DISPLAY 'NO OF REC READ    BP13F725 = ' WS-F725-READ.                
           DISPLAY 'NO OF REC READ    BP13K016 = ' WS-K016-READ.                
           DISPLAY 'NO OF REC READ    BP13K730 = ' WS-K730-READ.                
           DISPLAY 'NO OF REC REWRITE BP13K730 = ' WS-K730-REWRITE.             
           DISPLAY 'NO OF REC NOTFND  BP13F730 = ' WS-F730-NOTMATCH.            
           DISPLAY 'NO OF REC NOTFND  BP13F725 = ' WS-F725-NOTMATCH.            
           DISPLAY 'NO OF REC NOTFND  BP13K016 = ' WS-K016-NTFND.               
           DISPLAY 'NO OF REC NOTFND  BP13K730 = ' WS-K730-NTFND.               
                                                                                
           CLOSE BP13F725                                                       
                 BP13K730                                                       
                 BP13K016                                                       
                 P13L014A                                                       
                 P13L014B                                                       
                 P13L014C.                                                      
                                                                                
           IF WS-K730-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K730, STATUS ' WS-K730-STATUS          
              MOVE WS-K730-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K016-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K016, STATUS ' WS-K016-STATUS          
              MOVE WS-K016-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           INITIALIZE LINK-REC.                                                 
           MOVE 'C'        TO LINK-OPEN-CLOSE.                                  
           CALL 'EJ03CS01' USING LINK-REC.                                      
           IF LINK-RETURN-CODE NOT = 00 AND 97                                  
              MOVE LINK-RETURN-CODE        TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
