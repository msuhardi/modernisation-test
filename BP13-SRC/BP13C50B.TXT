       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C50B.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  10 APR 2017.                                              
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  CHECK IF FS BOOKED DO NOT HAVE MARRIAGE RECORD*                
      *          AFTER ONE MONTH FROM DATE OF SCREENING, SEND  *                
      *          RECORD TO ROM AGAIN                           *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP136646 FNP1 100417  NEW PROGRAM                      *                
      *========================================================*                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K517     ASSIGN        TO BP13K517                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K517-KEY-FLD                    
                               FILE STATUS   IS WS-K517-STATUS.                 
                                                                                
           SELECT BP13K800     ASSIGN        TO BP13K800                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K800-NUM-REGN                   
                               FILE STATUS   IS WS-K800-STATUS.                 
                                                                                
           SELECT BM06K110     ASSIGN        TO BM06K110                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K110-KEY-FLD                    
                               FILE STATUS   IS WS-K110-STATUS.                 
                                                                                
           SELECT BM06K100     ASSIGN        TO BM06K100                        
                               ORGANIZATION  IS INDEXED                         
                               ACCESS MODE   IS RANDOM                          
                               RECORD KEY    IS K100-KEY-FLD                    
                               FILE STATUS   IS WS-K100-STATUS.                 
                                                                                
           SELECT BP13F517     ASSIGN        TO BP13F517.                       
           SELECT P13F517A     ASSIGN        TO P13F517A.                       
           SELECT BP13BE01     ASSIGN        TO BP13BE01.                       
           SELECT SY02F001     ASSIGN        TO SY02F001.                       
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13F517                                                            
            RECORD CONTAINS 250  CHARACTERS                                     
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       COPY BP13F517.                                                           
                                                                                
       FD   BP13K517                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K517.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   P13F517A                                                            
            RECORD CONTAINS 250  CHARACTERS                                     
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       01   P13F517A-REC       PIC X(250).                                      
                                                                                
       FD   BP13BE01                                                            
            RECORD CONTAINS  160 CHARACTERS                                     
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       01   BP13BE01-REC.                                                       
            05  BE01-IDNO        PIC X(20).                                     
            05  BE01-IDTYP       PIC X(03).                                     
            05  FILLER           PIC X(67).                                     
            05  BE01-SOURCE      PIC X(04).                                     
            05  FILLER           PIC X(28).                                     
            05  BE01-DTE-SENT    PIC X(10).                                     
            05  FILLER           PIC X(28).                                     
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K517-STATUS       PIC 9(2)    VALUE ZEROES.                   
           05  WS-K800-STATUS       PIC 9(2)    VALUE ZEROES.                   
           05  WS-K110-STATUS       PIC 9(2)    VALUE ZEROES.                   
           05  WS-K100-STATUS       PIC 9(2)    VALUE ZEROES.                   
           05  F517-EOF             PIC X(01)   VALUE ' '.                      
           05  WS-INT-DATE          PIC 9(7)    VALUE 0.                        
           05  WS-END-DATE          PIC 9(8)    VALUE 0.                        
           05  WS-CUTOFF-CHAR       PIC X(08)   VALUE SPACES.                   
           05  WS-CUTOFF-DTE        PIC 9(08)   VALUE ZEROES.                   
           05  WS-K800-FOUND        PIC X(1)    VALUE SPACES.                   
                                                                                
           05  WS-F001-DTE-CURRENT.                                             
               10  WS-F001-CURR-CCYY   PIC 9(4).                                
               10  WS-F001-CURR-MM     PIC 9(2).                                
               10  WS-F001-CURR-DD     PIC 9(2).                                
                                                                                
       01  WS-COUNTERS.                                                         
           05 K517-READ             PIC 9(7)    VALUE ZEROES.                   
           05 K517-READ-NOTFD       PIC 9(7)    VALUE ZEROES.                   
           05 F517-READ             PIC 9(7)    VALUE ZEROES.                   
           05 F517-NO-MARRIAGE      PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K517-DELETED      PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K517-DEL-NOTFD    PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K517-UPDATED      PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K517-UPD-NOTFD    PIC 9(7)    VALUE ZEROES.                   
           05 CNT-F517-WRITTEN      PIC 9(7)    VALUE ZEROES.                   
           05 CNT-BE01-WRITE        PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K800-FOUND        PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K800-NOTFD        PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K800-VALID        PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K800-NOT-VALID    PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K110-KEY-ISSUED   PIC 9(7)    VALUE ZEROES.                   
           05 CNT                   PIC 9       VALUE ZEROES.                   
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *------------------------------------------------------------             
       0000-MAIN.                                                               
      *------------------------------------------------------------             
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 1100-READ-SY02F001   THRU 1100-EXIT.                         
           PERFORM 2100-READ-BP13F517   THRU 2100-EXIT.                         
           PERFORM 3000-PROCESS-RECORDS THRU 3000-EXIT                          
              UNTIL F517-EOF = 'Y'.                                             
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1000-OPEN-ROUTINE.                                                       
      *------------------------------------------------------------             
                                                                                
           OPEN INPUT  BP13F517                                                 
                       BP13K800                                                 
                       BM06K110                                                 
                       BM06K100                                                 
                       SY02F001                                                 
                I-O    BP13K517                                                 
                OUTPUT P13F517A                                                 
                       BP13BE01.                                                
                                                                                
           IF WS-K517-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K517 OPEN ERROR ' WS-K517-STATUS                     
              MOVE WS-K517-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K800 OPEN ERROR ' WS-K800-STATUS                     
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BM06K110 OPEN ERROR ' WS-K110-STATUS                     
              MOVE WS-K110-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BM06K100 OPEN ERROR ' WS-K100-STATUS                     
              MOVE WS-K100-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1100-READ-SY02F001.                                                      
      *------------------------------------------------------------             
                                                                                
           READ SY02F001 AT END                                                 
              DISPLAY 'SY02F001 - NO RECORD FOUND'                              
              MOVE 99 TO RETURN-CODE                                            
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
           MOVE F001-DTE-CURRENT  TO WS-F001-DTE-CURRENT.                       
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       2100-READ-BP13F517.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13F517 AT END                                                 
              MOVE 'Y' TO F517-EOF                                              
              GO TO 2100-EXIT.                                                  
                                                                                
           ADD 1 TO F517-READ.                                                  
                                                                                
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       3000-PROCESS-RECORDS.                                                    
      *------------------------------------------------------------             
                                                                                
           MOVE SPACES TO WS-K800-FOUND.                                        
           IF F517-NUM-REGN NOT = SPACES                                        
              IF (F517-NUM-NRIC-SPOUSE = SPACES OR LOW-VALUES) AND              
                 (F517-NUM-SPOUSE-SEX  = SPACES OR LOW-VALUES) AND              
                 (F517-NME-SPOUSE      = SPACES OR LOW-VALUES) AND              
                 (F517-NUM-MARRIAGE    = SPACES OR LOW-VALUES) AND              
                 (F517-DTE-MARRIAGE    = SPACES OR LOW-VALUES)                  
                 ADD 1 TO F517-NO-MARRIAGE                                      
                 PERFORM 3100-READ-BP13K800     THRU 3100-EXIT                  
                 IF WS-K800-FOUND = 'Y'                                         
                    PERFORM 3200-CHECK-FS-DATE THRU 3200-EXIT                   
                 ELSE                                                           
                    PERFORM 4000-DELETE-BP13K517  THRU 4000-EXIT                
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-BP13F517   THRU  2100-EXIT.                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       3100-READ-BP13K800.                                                      
      *------------------------------------------------------------             
                                                                                
           MOVE SPACES                   TO K800-NUM-REGN.                      
           MOVE F517-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    ADD 1 TO CNT-K800-FOUND                                     
                    MOVE 'Y' TO WS-K800-FOUND                                   
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'N' TO WS-K800-FOUND                                   
                    ADD 1 TO CNT-K800-NOTFD                                     
                    DISPLAY 'RECORD NOT FOUND IN BP13K800, KEY IS: '            
                           K800-NUM-REGN                                        
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       3200-CHECK-FS-DATE.                                                      
      *------------------------------------------------------------             
                                                                                
           IF K800-NUM-SCH-ACC NOT = SPACES  AND LOW-VALUES                     
              PERFORM 3300-READ-BM06K110 THRU 3300-EXIT                         
           END-IF.                                                              
                                                                                
           IF (K800-NUM-STATUS = 'C') OR                                        
              (K800-NUM-ELIG-SCHEME  NOT = 'FS ') OR                            
              (K110-DTE-KEY-ISSUED NOT = ZEROS AND SPACES)                      
              PERFORM 4000-DELETE-BP13K517  THRU 4000-EXIT                      
              GO TO 3200-EXIT                                                   
           ELSE                                                                 
              ADD 1 TO CNT-K800-VALID                                           
           END-IF.                                                              
                                                                                
           IF F517-DTE-SENT   NOT NUMERIC                                       
              MOVE ZEROES     TO F517-DTE-SENT                                  
           END-IF.                                                              
                                                                                
           MOVE F517-DTE-SENT      TO WS-CUTOFF-DTE.                            
           COMPUTE WS-INT-DATE = FUNCTION                                       
                              INTEGER-OF-DATE(WS-CUTOFF-DTE).                   
                                                                                
           ADD 30 TO WS-INT-DATE.                                               
                                                                                
           COMPUTE WS-END-DATE = FUNCTION                                       
                              DATE-OF-INTEGER(WS-INT-DATE).                     
                                                                                
           IF F517-NUM-TRAN = 'B'                                               
              IF K800-DTE-SALE-AGMT NOT = SPACES AND LOW-VALUES                 
                 MOVE 'S'        TO F517-NUM-TRAN                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-F001-DTE-CURRENT >= WS-END-DATE                                
              IF F517-NUM-TRAN = 'B' OR                                         
                 (F517-NUM-TRAN = 'S' AND                                       
                 (K100-NUM-PACD = 'M' AND K100-DTE-PACD > ZEROS))               
                 PERFORM 4100-REWRITE-BP13K517  THRU 4100-EXIT                  
                 PERFORM 4300-WRITE-BP13BE01    THRU 4300-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       3300-READ-BM06K110.                                                      
      *------------------------------------------------------------             
                                                                                
           MOVE SPACES                   TO K110-KEY-FLD.                       
           MOVE K800-NUM-SCH-ACC         TO K110-KEY-FLD.                       
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3400-READ-BM06K100 THRU 3400-EXIT                   
                    IF K110-DTE-KEY-ISSUED NOT = ZEROS                          
                       ADD 1 TO CNT-K110-KEY-ISSUED                             
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES TO K110-DTE-KEY-ISSUED                          
               WHEN OTHER                                                       
                    DISPLAY 'BM06K110 READ ERROR ' WS-K110-STATUS               
                    DISPLAY 'K110 KEY: ' K110-KEY-FLD                           
                    MOVE WS-K110-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3400-READ-BM06K100.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE K110-AIX1          TO   K100-KEY-FLD.                           
                                                                                
           READ BM06K100 KEY IS K100-KEY-FLD.                                   
                                                                                
           EVALUATE WS-K100-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES    TO K100-NUM-PACD                             
                                      K100-DTE-PACD                             
               WHEN OTHER                                                       
                    MOVE WS-K100-STATUS              TO RETURN-CODE             
                    DISPLAY 'ERROR READING BM06K100, STATUS '                   
                          WS-K100-STATUS                                        
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       4000-DELETE-BP13K517.                                                    
      *------------------------------------------------------------             
                                                                                
           WRITE P13F517A-REC FROM BP13F517-REC.                                
           ADD 1 TO CNT-F517-WRITTEN.                                           
           ADD 1 TO CNT-K800-NOT-VALID.                                         
           MOVE F517-NUM-NRIC          TO K517-NUM-NRIC.                        
                                                                                
           PERFORM 4200-READ-BP13K517  THRU 4200-EXIT.                          
                                                                                
           DELETE BP13K517.                                                     
                                                                                
           IF WS-K517-STATUS NOT = ZEROES                                       
              ADD 1 TO CNT-K517-DEL-NOTFD                                       
              DISPLAY 'ERROR DELETING RECORD, KEY IS: '                         
                    K517-NUM-NRIC                                               
           ELSE                                                                 
              ADD 1 TO CNT-K517-DELETED                                         
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       4100-REWRITE-BP13K517.                                                   
      *------------------------------------------------------------             
           MOVE F517-NUM-NRIC          TO K517-NUM-NRIC.                        
                                                                                
           PERFORM 4200-READ-BP13K517  THRU 4200-EXIT.                          
                                                                                
           MOVE WS-F001-DTE-CURRENT    TO K517-DTE-SENT.                        
           MOVE 'SEND'                 TO K517-NUM-REMARK.                      
           MOVE F517-NUM-TRAN          TO K517-NUM-TRAN.                        
           MOVE 'BP13C50B'             TO K517-NUM-USERID.                      
                                                                                
           REWRITE BP13K517-REC                                                 
           IF WS-K517-STATUS = 00 OR 02                                         
              ADD 1 TO CNT-K517-UPDATED                                         
           ELSE                                                                 
              ADD 1 TO CNT-K517-UPD-NOTFD                                       
              DISPLAY 'ERROR UPDATING BP13K517, KEY IS: '                       
                    K517-NUM-NRIC                                               
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       4200-READ-BP13K517.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13K517.                                                       
                                                                                
           EVALUATE WS-K517-STATUS                                              
               WHEN 00                                                          
                    ADD 1 TO K517-READ                                          
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1 TO K517-READ-NOTFD                                    
                    DISPLAY 'RECORD NOT FOUND IN BP13K517, KEY IS: '            
                    K517-NUM-NRIC                                               
               WHEN OTHER                                                       
                    DISPLAY 'BP13K517 READ ERROR ' WS-K517-STATUS               
                    DISPLAY 'K517 KEY: ' K517-KEY-FLD                           
                                                                                
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       4300-WRITE-BP13BE01.                                                     
      *------------------------------------------------------------             
           MOVE SPACES                     TO BP13BE01-REC.                     
                                                                                
           MOVE F517-NUM-NRIC              TO BE01-IDNO.                        
           MOVE 'UIN'                      TO BE01-IDTYP.                       
           MOVE 'BP13'                     TO BE01-SOURCE.                      
           STRING WS-F001-DTE-CURRENT (1:4) '-'                                 
                  WS-F001-DTE-CURRENT (5:2) '-'                                 
                  WS-F001-DTE-CURRENT (7:2)                                     
                  DELIMITED BY SIZE      INTO BE01-DTE-SENT.                    
           WRITE BP13BE01-REC.                                                  
           ADD 1 TO CNT-BE01-WRITE.                                             
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '***********BP13C50B END OF JOB*******************'.         
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'NO OF REC READ FROM F517     ' F517-READ.                   
           DISPLAY 'NO OF F517 NO MARRIAGE DATA  ' F517-NO-MARRIAGE.            
           DISPLAY 'NO OF REC READ FROM K517     ' K517-READ.                   
           DISPLAY 'NO OF REC READ K517 NOTFD    ' K517-READ-NOTFD.             
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF REC READ K800 FOUND    ' CNT-K800-FOUND.              
           DISPLAY 'NO OF REC READ K800 NOT FD   ' CNT-K800-NOTFD.              
           DISPLAY 'NO OF REC READ K800 VALID    ' CNT-K800-VALID.              
           DISPLAY 'NO OF REC READ K800 NOTVALID ' CNT-K800-NOT-VALID.          
           DISPLAY 'NO OF REC READ K110 KEY-ISSU ' CNT-K110-KEY-ISSUED.         
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF REC DELETED FROM K517  ' CNT-K517-DELETED.            
           DISPLAY 'NO OF REC WRITTEN TO   F517  ' CNT-F517-WRITTEN.            
           DISPLAY 'NO OF REC DELETED K517 NOTFD ' CNT-K517-DEL-NOTFD.          
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF REC UPDATED IN   K517  ' CNT-K517-UPDATED.            
           DISPLAY 'NO OF REC WRITTEN IN   BE01  ' CNT-BE01-WRITE.              
           DISPLAY 'NO OF REC UPD K517 NOT FOUND ' CNT-K517-UPD-NOTFD.          
                                                                                
           CLOSE BP13K800                                                       
                 BM06K110                                                       
                 BM06K100                                                       
                 BP13K517                                                       
                 BP13F517                                                       
                 BP13BE01                                                       
                 SY02F001.                                                      
                                                                                
           IF WS-K517-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K517 CLOSE ERROR ' WS-K517-STATUS                    
              MOVE WS-K517-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K800 CLOSE ERROR ' WS-K800-STATUS                    
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BM06K100 CLOSE ERROR ' WS-K100-STATUS                    
              MOVE WS-K100-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BM06K110 CLOSE ERROR ' WS-K110-STATUS                    
              MOVE WS-K110-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
