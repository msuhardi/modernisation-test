       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CF7K.                                                  
       AUTHOR.       ALJOHN MONCADA.                                            
       DATE-WRITTEN. 27/06/2018.                                                
                                                                                
      *****************************************************************         
      *                SYSTEM NAME (BP13)                             *         
      *===============================================================*         
      *  OBJECTIVES : TO UPDATE NUM-DEFER-INCOME-ASSESS IN BP13K800   *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------  ---- ------------- ---------------- *         
      * BP13F740 I     SEQUENTIAL 0500                                *         
      * BP13K820 I     DYNAMIC    0400 K820-KEY-FLD                   *         
      * BP13K800 I/O   RANDOM     2000 K800-NUM-REGN                  *         
      *                                                               *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *                                                               *         
      * CHGE REQ # DATE     AUTHOR REMARKS                            *         
      * ---------- -------- ------ ---------------------------------- *         
      * BP137356   27/06/18 AM25   NEW PROGRAM                        *         
      * BP137724   01/07/19 AM25   ADDT'L CHECK FOR DEFER-INCOME      *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN      TO BP13F740.                             
                                                                                
           SELECT BP13K820 ASSIGN      TO BP13K820                              
              ORGANIZATION IS INDEXED                                           
              ACCESS MODE  IS DYNAMIC                                           
              RECORD KEY   IS K820-KEY-FLD                                      
              FILE STATUS  IS WS-K820-STATUS.                                   
                                                                                
           SELECT BP13K800 ASSIGN      TO BP13K800                              
              ORGANIZATION IS INDEXED                                           
              ACCESS MODE  IS RANDOM                                            
              RECORD KEY   IS K800-NUM-REGN                                     
              FILE STATUS  IS WS-K800-STATUS.                                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F740                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-APPLT1-EMPLY-STATUS  PIC X(3)        VALUE SPACES.            
           05  WS-APPLT1-NS-LT12MTH    PIC X(1)        VALUE SPACE.             
           05  WS-APPLT1-EDN-LT12MTH   PIC X(1)        VALUE SPACE.             
           05  WS-PRTNER-EMPLY-STATUS  PIC X(3)        VALUE SPACES.            
           05  WS-PRTNER-NS-LT12MTH    PIC X(1)        VALUE SPACE.             
           05  WS-PRTNER-EDN-LT12MTH   PIC X(1)        VALUE SPACE.             
           05  WS-K820-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-K800-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-NUM-NRIC             PIC X(9)        VALUE SPACES.            
           05  WS-DTE-REQUEST          PIC 9(8)        VALUE ZEROES.            
           05  WS-DTE-BIRTH            PIC 9(8)        VALUE ZEROES.            
           05  WS-NUM-AGE              PIC 9(8)        VALUE ZEROES.            
           05  FILLER REDEFINES WS-NUM-AGE.                                     
               10  WS-AGE              PIC 9(4).                                
               10  FILLER              PIC 9(4).                                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-CNT-READ        PIC 9(5)        VALUE ZEROES.            
           05  WS-K820-CNT-READ        PIC 9(5)        VALUE ZEROES.            
           05  WS-K800-CNT-READ        PIC 9(5)        VALUE ZEROES.            
           05  WS-K800-CNT-REWRT       PIC 9(5)        VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-F740-EOF             PIC X           VALUE 'N'.               
           05  WS-K820-EOF             PIC X           VALUE 'N'.               
           05  WS-DEFER-OK             PIC X           VALUE 'N'.               
           05  WS-AGE-OK               PIC X           VALUE 'N'.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13F740    THRU 2000-EXIT.                        
           PERFORM 2500-MAIN-PROCESSING  THRU 2500-EXIT                         
              UNTIL WS-F740-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13K820                                                 
                I-O    BP13K800.                                                
                                                                                
           IF WS-K820-STATUS NOT = ZEROES                                       
              DISPLAY 'OPEN BP13K820 FAIL STATUS=' WS-K820-STATUS               
              MOVE WS-K820-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = ZEROES                                       
              DISPLAY 'OPEN BP13K800 FAIL STATUS=' WS-K800-STATUS               
              MOVE WS-K800-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13F740.                                                      
      *****************************************************************         
                                                                                
           READ BP13F740                                                        
              AT END                                                            
                 MOVE 'Y'              TO WS-F740-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F740-CNT-READ                      
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2500-MAIN-PROCESSING.                                                    
      *****************************************************************         
                                                                                
           PERFORM 3000-STARTBR-BP13K820 THRU 3000-EXIT.                        
                                                                                
           IF WS-DEFER-OK = 'N' AND F740-NUM-NRIC2(1:2) NOT = '##'              
           AND (F740-NUM-NRIC2 NOT = SPACES AND LOW-VALUES)                     
              MOVE F740-NUM-NRIC2      TO WS-NUM-NRIC                           
              PERFORM 3300-READ-BP13K820 THRU 3300-EXIT                         
              PERFORM 3400-PROCESS-APPLT THRU 3400-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-DEFER-OK = 'N' AND F740-NUM-NRIC3(1:2) NOT = '##'              
           AND (F740-NUM-NRIC3 NOT = SPACES AND LOW-VALUES)                     
              MOVE F740-NUM-NRIC3      TO WS-NUM-NRIC                           
              PERFORM 3300-READ-BP13K820 THRU 3300-EXIT                         
              PERFORM 3400-PROCESS-APPLT THRU 3400-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-DEFER-OK = 'N' AND F740-NUM-NRIC4(1:2) NOT = '##'              
           AND (F740-NUM-NRIC4 NOT = SPACES AND LOW-VALUES)                     
              MOVE F740-NUM-NRIC4      TO WS-NUM-NRIC                           
              PERFORM 3300-READ-BP13K820 THRU 3300-EXIT                         
              PERFORM 3400-PROCESS-APPLT THRU 3400-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F740 THRU 2000-EXIT.                           
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-STARTBR-BP13K820.                                                   
      *****************************************************************         
                                                                                
           MOVE 'N'                    TO WS-K820-EOF                           
                                          WS-AGE-OK                             
                                          WS-DEFER-OK.                          
           MOVE SPACES                 TO WS-APPLT1-EMPLY-STATUS                
                                          WS-APPLT1-NS-LT12MTH                  
                                          WS-APPLT1-EDN-LT12MTH                 
                                          WS-PRTNER-EMPLY-STATUS                
                                          WS-PRTNER-NS-LT12MTH                  
                                          WS-PRTNER-EDN-LT12MTH                 
                                          K820-KEY-FLD.                         
                                                                                
           MOVE F740-NUM-REGN          TO K820-NUM-REGN.                        
                                                                                
           START BP13K820 KEY IS >= K820-KEY-FLD.                               
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
                 PERFORM 3100-READNEXT-BP13K820 THRU 3100-EXIT                  
                    UNTIL WS-K820-EOF = 'Y'                                     
                                                                                
                 IF WS-AGE-OK = 'Y'                                             
                    PERFORM 3200-VALIDATE-COUPLE THRU 3200-EXIT                 
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'STARTBR BP13K820 FAIL STATUS='                        
                                                 WS-K820-STATUS                 
                 MOVE WS-K820-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3100-READNEXT-BP13K820.                                                  
      *****************************************************************         
                                                                                
           READ BP13K820 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
                 IF F740-NUM-REGN = K820-NUM-REGN                               
                    PERFORM 3110-CHECK-RLTNSP THRU 3110-EXIT                    
                 ELSE                                                           
                    MOVE 'Y'           TO WS-K820-EOF                           
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE 'Y'              TO WS-K820-EOF                           
              WHEN OTHER                                                        
                 DISPLAY 'READNEXT BP13K820 FAIL STATUS='                       
                                                    WS-K820-STATUS              
                 MOVE WS-K820-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3110-CHECK-RLTNSP.                                                       
      *****************************************************************         
                                                                                
           IF K820-NUM-RELATIONSHIP = '00'                                      
              ADD 1                    TO WS-K820-CNT-READ                      
                                                                                
              MOVE K820-NUM-EMPLY-STATUS                                        
                                       TO WS-APPLT1-EMPLY-STATUS                
              MOVE K820-NUM-NS-LT12MTH TO WS-APPLT1-NS-LT12MTH                  
              MOVE K820-NUM-EDN-LT12MTH                                         
                                       TO WS-APPLT1-EDN-LT12MTH                 
                                                                                
              PERFORM 3120-CHECK-AGE THRU 3120-EXIT                             
           ELSE                                                                 
              IF (K820-NUM-RELATIONSHIP = '09' OR '20')                         
                 ADD 1                 TO WS-K820-CNT-READ                      
                                                                                
                 MOVE K820-NUM-EMPLY-STATUS                                     
                                       TO WS-PRTNER-EMPLY-STATUS                
                 MOVE K820-NUM-NS-LT12MTH                                       
                                       TO WS-PRTNER-NS-LT12MTH                  
                 MOVE K820-NUM-EDN-LT12MTH                                      
                                       TO WS-PRTNER-EDN-LT12MTH                 
                                                                                
                 PERFORM 3120-CHECK-AGE THRU 3120-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3120-CHECK-AGE.                                                          
      *****************************************************************         
                                                                                
           IF WS-AGE-OK = 'N'                                                   
              MOVE F740-DTE-REQUEST    TO WS-DTE-REQUEST                        
              MOVE K820-DTE-BIRTH      TO WS-DTE-BIRTH                          
                                                                                
              COMPUTE WS-NUM-AGE = WS-DTE-REQUEST - WS-DTE-BIRTH                
                                                                                
              IF WS-AGE <= 30                                                   
                 MOVE 'Y'              TO WS-AGE-OK                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3120-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3200-VALIDATE-COUPLE.                                                    
      *****************************************************************         
                                                                                
           IF (WS-APPLT1-EMPLY-STATUS = 'A  ' OR 'B  ')                         
           OR WS-APPLT1-NS-LT12MTH = 'Y'                                        
           OR WS-APPLT1-EDN-LT12MTH = 'Y'                                       
              IF (WS-PRTNER-EMPLY-STATUS = 'A  ' OR 'B  ')                      
              OR WS-PRTNER-NS-LT12MTH = 'Y'                                     
              OR WS-PRTNER-EDN-LT12MTH = 'Y'                                    
                 MOVE 'Y'              TO WS-DEFER-OK                           
                 PERFORM 4000-READ-BP13K800 THRU 4000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3300-READ-BP13K820.                                                      
      *****************************************************************         
                                                                                
           MOVE F740-NUM-REGN          TO K820-NUM-REGN.                        
           MOVE WS-NUM-NRIC            TO K820-NUM-NRIC.                        
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K820-CNT-READ                      
              WHEN 23                                                           
                 DISPLAY 'RECORD NOT FOUND IN BP13K820: ' K820-KEY-FLD          
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K820 FAIL STATUS=' WS-K820-STATUS            
                 MOVE WS-K820-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3400-PROCESS-APPLT.                                                      
      *****************************************************************         
                                                                                
           IF  WS-K820-STATUS = ZEROES                                          
           AND K820-NUM-MARITAL-STATUS = '2'                                    
              MOVE 'N'                 TO WS-AGE-OK                             
              MOVE SPACES              TO WS-APPLT1-EMPLY-STATUS                
                                          WS-APPLT1-NS-LT12MTH                  
                                          WS-APPLT1-EDN-LT12MTH                 
                                          WS-PRTNER-EMPLY-STATUS                
                                          WS-PRTNER-NS-LT12MTH                  
                                          WS-PRTNER-EDN-LT12MTH                 
                                                                                
              MOVE K820-NUM-EMPLY-STATUS                                        
                                       TO WS-APPLT1-EMPLY-STATUS                
              MOVE K820-NUM-NS-LT12MTH TO WS-APPLT1-NS-LT12MTH                  
              MOVE K820-NUM-EDN-LT12MTH                                         
                                       TO WS-APPLT1-EDN-LT12MTH                 
                                                                                
              PERFORM 3120-CHECK-AGE     THRU 3120-EXIT                         
              PERFORM 3500-PROCESS-PRTNR THRU 3500-EXIT                         
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3500-PROCESS-PRTNR.                                                      
      *****************************************************************         
                                                                                
           IF K820-NUM-NRIC-SPOUSE = SPACES OR LOW-VALUES                       
              CONTINUE                                                          
           ELSE                                                                 
              MOVE K820-NUM-NRIC-SPOUSE                                         
                                       TO WS-NUM-NRIC                           
              PERFORM 3300-READ-BP13K820 THRU 3300-EXIT                         
                                                                                
              IF WS-K820-STATUS = ZEROES                                        
                 MOVE K820-NUM-EMPLY-STATUS                                     
                                       TO WS-PRTNER-EMPLY-STATUS                
                 MOVE K820-NUM-NS-LT12MTH                                       
                                       TO WS-PRTNER-NS-LT12MTH                  
                 MOVE K820-NUM-EDN-LT12MTH                                      
                                       TO WS-PRTNER-EDN-LT12MTH                 
                                                                                
                 PERFORM 3120-CHECK-AGE THRU 3120-EXIT                          
                 IF WS-AGE-OK = 'Y'                                             
                    PERFORM 3200-VALIDATE-COUPLE THRU 3200-EXIT                 
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-READ-BP13K800.                                                      
      *****************************************************************         
                                                                                
           MOVE F740-NUM-REGN          TO K800-NUM-REGN.                        
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K800-CNT-READ                      
                                                                                
                 PERFORM 4100-UPDATE-BP13K800 THRU 4100-EXIT                    
              WHEN 23                                                           
                 DISPLAY 'RECORD NOT FOUND IN BP13K800: ' F740-NUM-REGN         
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K800 FAIL STATUS=' WS-K800-STATUS            
                 MOVE WS-K800-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4100-UPDATE-BP13K800.                                                    
      *****************************************************************         
                                                                                
           MOVE 'Y'                    TO K800-NUM-DEFER-INCOME-ASSESS.         
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF WS-K800-STATUS = ZEROES                                           
              ADD 1                    TO WS-K800-CNT-REWRT                     
           ELSE                                                                 
              DISPLAY 'REWRITE BP13K800 FAIL STATUS=' WS-K800-STATUS            
              MOVE WS-K800-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13F740                                                       
                 BP13K820                                                       
                 BP13K800.                                                      
                                                                                
           IF WS-K820-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K820 FAIL STATUS=' WS-K820-STATUS              
              MOVE WS-K820-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K800 FAIL STATUS=' WS-K800-STATUS              
              MOVE WS-K800-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '*************************'.                                 
           DISPLAY '* PROGRAM BP13CF7K      *'.                                 
           DISPLAY '* CONTROL COUNTS        *'.                                 
           DISPLAY '*-----------------------*'.                                 
           DISPLAY '* REC READ F740 : ' WS-F740-CNT-READ ' *'.                  
           DISPLAY '* REC READ K820 : ' WS-K820-CNT-READ ' *'.                  
           DISPLAY '* REC READ K800 : ' WS-K800-CNT-READ ' *'.                  
           DISPLAY '* REC UPDT K800 : ' WS-K800-CNT-REWRT ' *'.                 
           DISPLAY '*************************'.                                 
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
