      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C319.                                                 
      *AUTHOR.        SMH.                                                      
      *DATE-WRITTEN.  15/09/89.                                                 
                                                                                
      ******************************************************************        
      *  OBJECTIVE    : 1. TO PRINT REPORT ON NUMBER OF HPI FORMS      *        
      *                    SEND TO CPF.                                *        
      ******************************************************************        
      *  CHGE    BY  DATE     REMARKS                                  *        
      * -------  --- -------- ---------------------------------------- *        
      * SOCPH8B  BAL 24/07/93 TO CHANGE REGN FROM 7 BYTES TO 8 BYTES.  *        
      * CS950420 GJ  12/04/95 TO CHANGE F300 TO F860.                  *        
      * BP130290 SGK 08/01/98 TO RECOMPILE TO REFLECT CHANGE IN F860   *        
      * BP130315 RHB 19/06/98 TO CATER FOR RETIREMENT ACCOUNT.         *        
      * BP130522 SGK 28/07/98 TO CATER FOR CPFB Y2000.                 *        
      * BP130418 RHB 26/08/98 TO CATER FOR Y2K.                        *        
      * BP130929 MCC 13/10/00 TO CHNGE BP13F435 TO AB02F860            *        
      * BP132842 MTS 18/08/06 TO ADD READING OF BP13K410               *        
      * BP136692 KSJ 15/02/17 EXPAND AB02F860 FROM 600 TO 2000         *        
      * BP138865 KR1328/10/21 REMOVE REPORTS L319A & L319B.            *        
      ******************************************************************        
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
                                                                                
       SOURCE-COMPUTER.  IBM-4341.                                              
       OBJECT-COMPUTER.  IBM-4341.                                              
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
      *------------------------*                                                
                                                                                
           SELECT AB02F860 ASSIGN TO AB02F860.                                  
                                                                                
           SELECT BP13K330 ASSIGN TO BP13K330                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS          IS DYNAMIC                                    
                  RECORD KEY      IS K330-KEY-FLD                               
                  FILE STATUS     IS K330-STATUS.                               
                                                                                
           SELECT BP13K410 ASSIGN TO BP13K410                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS          IS RANDOM                                     
                  RECORD KEY      IS K410-KEY-FLD                               
                  FILE STATUS     IS K410-STATUS.                               
                                                                                
           SELECT BP13L319 ASSIGN TO BP13L319.                                  
                                                                                
           EJECT                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD   AB02F860                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY AB02F860.                                                           
                                                                                
       FD   BP13K330                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K330.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       FD   BP13L319                                                            
            LABEL RECORDS ARE OMITTED                                           
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   OUTPUT-LINE                 PIC X(132).                             
                                                                                
                                                                                
      *------------------------                                                 
       WORKING-STORAGE SECTION.                                                 
      *------------------------                                                 
                                                                                
       77  WS-CST-Y                      PIC X(01) VALUE 'Y'.                   
                                                                                
      ******************************************************************        
      *  WORKING STORAGE AREA FOR COUNTER                                       
      ******************************************************************        
       01  WS-CTR-READ-F860              PIC 9(07) VALUE 0.                     
       01  WS-CTR-WRITE-L319             PIC 9(07) VALUE 0.                     
                                                                                
      ******************************************************************        
      *  WORKING STORAGE AREA FOR VARIABLES.                                    
      ******************************************************************        
       01  WS-VAR-PROC-TYPE              PIC X(07) VALUE SPACES.                
       01  WS-VAR-AB02F860-EOF           PIC X(01) VALUE SPACES.                
       01  WS-SEQ                        PIC 9(04) VALUE ZERO.                  
       01  WS-SEQ1                       PIC 9(04) VALUE ZERO.                  
       01  WS-SEQ3                       PIC 9(04) VALUE ZERO.                  
       01  WS-SEQ2                       PIC 9(04) VALUE ZERO.                  
       01  PAGE-CNT                      PIC 9(04) VALUE ZERO.                  
       01  K330-STATUS                   PIC 9(02) VALUE ZERO.                  
       01  K410-STATUS                   PIC 9(02) VALUE ZERO.                  
       01  WS-NAME                       PIC X(27) VALUE SPACES.                
       01  WS-NRIC                       PIC X(09) VALUE SPACES.                
                                                                                
      ******************************************************************        
      *  WORKING STORAGE AREA FOR DATE                                          
      ******************************************************************        
       01  WS-DATE.                                                             
           05 WS-CC                      PIC X(02) VALUE SPACES.                
           05 WS-YY                      PIC X(02) VALUE SPACES.                
           05 WS-MM                      PIC X(02) VALUE SPACES.                
           05 WS-DD                      PIC X(02) VALUE SPACES.                
                                                                                
       01  WS-DATE1.                                                            
           05 WS-DD1                     PIC 9(02) VALUE ZEROES.                
           05 FILLER                     PIC X(01) VALUE '/'.                   
           05 WS-MM1                     PIC 9(02) VALUE ZEROES.                
           05 FILLER                     PIC X(01) VALUE '/'.                   
           05 WS-CC1                     PIC 9(02) VALUE ZEROES.                
           05 WS-YY1                     PIC 9(02) VALUE ZEROES.                
                                                                                
       01  WS-SALE-AGRM-DTE.                                                    
           05 WS-SALE-AGRM-DTE-CC        PIC X(02) VALUE ZEROES.                
           05 WS-SALE-AGRM-DTE-YY        PIC X(02) VALUE ZEROES.                
           05 WS-SALE-AGRM-DTE-MM        PIC X(02) VALUE ZEROES.                
           05 WS-SALE-AGRM-DTE-DD        PIC X(02) VALUE ZEROES.                
                                                                                
       01  WS-EDITED-DATE.                                                      
           05 WS-EDITED-DD               PIC X(02) VALUE SPACES.                
           05 FILLER                     PIC X(01) VALUE '/'.                   
           05 WS-EDITED-MM               PIC X(02) VALUE SPACES.                
           05 FILLER                     PIC X(01) VALUE '/'.                   
           05 WS-EDITED-CC               PIC X(02) VALUE SPACES.                
           05 WS-EDITED-YY               PIC X(02) VALUE SPACES.                
                                                                                
      ******************************************************************        
      *  WORKING STORAGE AREA FOR REPORT HEADING                                
      ******************************************************************        
       01  REPORT-HEADING-1.                                                    
           05 HDG-HEADER                 PIC X(08) VALUE 'BP13L319'.            
           05 FILLER                     PIC X(06) VALUE SPACES.                
           05 FILLER                     PIC X(04) VALUE 'HDB3'.                
           05 FILLER                     PIC X(28) VALUE SPACES.                
           05 FILLER                     PIC X(39) VALUE                        
             'S Y S T E M   O F   C O M M I T M E N T'.                         
           05 FILLER                     PIC X(17) VALUE SPACES.                
           05 FILLER                     PIC X(07) VALUE 'DATE : '.             
           05 HDG-DATE.                                                         
              10 DD                      PIC X(02).                             
              10 FILLER                  PIC X(01) VALUE '/'.                   
              10 MM                      PIC X(02).                             
              10 FILLER                  PIC X(01) VALUE '/'.                   
              10 CC                      PIC X(02).                             
              10 YY                      PIC X(02).                             
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(07) VALUE 'PAGE : '.             
           05 PRT-PAGE-CNT               PIC ZZZZ9.                             
                                                                                
       01  REPORT-HEADING-2.                                                    
           05 FILLER                     PIC X(57) VALUE SPACES.                
           05 FILLER                     PIC X(14) VALUE                        
             'AGREEMENT TEAM'.                                                  
                                                                                
       01  REPORT-HEADING-3.                                                    
           05 FILLER                     PIC X(49) VALUE SPACES.                
           05 FILLER                     PIC X(30) VALUE                        
             'LIST OF HPI FORMS TO CPF BOARD'.                                  
                                                                                
       01  REPORT-HEADING-4.                                                    
           05 FILLER                     PIC X(02) VALUE SPACES.                
           05 FILLER                     PIC X(03) VALUE 'NO.'.                 
           05 FILLER                     PIC X(05) VALUE SPACES.                
           05 FILLER                     PIC X(07) VALUE 'REGN-NO'.             
           05 FILLER                     PIC X(05) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE 'SERIAL NO'.           
           05 FILLER                     PIC X(05) VALUE SPACES.                
           05 FILLER                     PIC X(06) VALUE 'CPF NO'.              
           05 FILLER                     PIC X(06) VALUE SPACES.                
           05 FILLER                     PIC X(04) VALUE 'NRIC'.                
           05 FILLER                     PIC X(07) VALUE SPACES.                
           05 FILLER                     PIC X(16) VALUE                        
             'APPL/COAPPL NAME'.                                                
           05 FILLER                     PIC X(15) VALUE SPACES.                
           05 FILLER                     PIC X(16) VALUE                        
             'AMOUNT WITHDRAWN'.                                                
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(10) VALUE                        
             'RETIREMENT'.                                                      
           05 FILLER                     PIC X(02) VALUE SPACES.                
           05 FILLER                     PIC X(13) VALUE                        
             'SALE-AGMT DTE'.                                                   
                                                                                
       01  REPORT-HEADING-6.                                                    
           05 FILLER                     PIC X(132) VALUE SPACES.               
                                                                                
       01  CONTROL-LINE1.                                                       
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(10) VALUE 'TO HDB : '.           
                                                                                
       01  CONTROL-LINE2.                                                       
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(02) VALUE 'I '.                  
           05 FILLER                     PIC X(40) VALUE ALL '_'.               
           05 FILLER                     PIC X(02) VALUE SPACES.                
           05 FILLER                     PIC X(24) VALUE                        
             'ACKNOWLEDGED RECEIPT OF '.                                        
           05 PRT-TOTAL-NO               PIC ZZZ9.                              
           05 FILLER                     PIC X(17) VALUE                        
             '  CPF FORMS FOR  '.                                               
           05 WS-CONTROL-DATE            PIC X(10).                             
           05 FILLER                     PIC X(01) VALUE '.'.                   
                                                                                
       01  CONTROL-LINE3.                                                       
           05 FILLER                     PIC X(70) VALUE SPACES.                
           05 FILLER                     PIC X(20) VALUE ALL '_'.               
                                                                                
       01  CONTROL-LINE4.                                                       
           05 FILLER                     PIC X(70) VALUE SPACES.                
           05 FILLER                     PIC X(20) VALUE                        
             '  CPF BOARD'.                                                     
                                                                                
       01  REPORT-LINE.                                                         
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 PRT-NO                     PIC ZZZ9.                              
           05 FILLER                     PIC X(05) VALUE SPACES.                
           05 PRT-CUR-REG                PIC X(08).                             
           05 FILLER                     PIC X(05) VALUE SPACES.                
           05 PRT-SER-NO                 PIC X(09).                             
           05 FILLER                     PIC X(03) VALUE SPACES.                
           05 PRT-CPF                    PIC X(09).                             
           05 FILLER                     PIC X(04) VALUE SPACE.                 
           05 PRT-NRIC                   PIC X(09).                             
           05 FILLER                     PIC X(03) VALUE SPACE.                 
           05 PRT-NAME                   PIC X(27).                             
           05 FILLER                     PIC X(05) VALUE SPACES.                
           05 PRT-AMT                    PIC Z,ZZZ,ZZ9.99.                      
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 PRT-RETIRE                 PIC ZZZ,ZZ9.99.                        
           05 PRT-RETIRE-ALPHA REDEFINES   PRT-RETIRE.                          
               10 WS-RETIRE-ALPHA          PIC X(10).                           
           05 FILLER                     PIC X(04) VALUE SPACES.                
           05 PRT-DATE                   PIC X(10) VALUE SPACES.                
                                                                                
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************                                                        
       0000-MAIN-ROUTINE.                                                       
      ******************                                                        
           PERFORM 0500-GET-SYSTEM-DATE     THRU 0500-EXIT.                     
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 7000-WRITE-HEADING       THRU 7000-EXIT.                     
           PERFORM 3000-AB02F860-READ       THRU 3000-EXIT                      
                     UNTIL WS-VAR-AB02F860-EOF = WS-CST-Y.                      
           PERFORM 8000-WRITE-ACKNOWLEDGE   THRU 8000-EXIT.                     
           PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      **********************                                                    
       0500-GET-SYSTEM-DATE.                                                    
      **********************                                                    
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           MOVE WS-CC           TO  WS-EDITED-CC.                               
           MOVE WS-YY           TO  WS-EDITED-YY.                               
           MOVE WS-MM           TO  WS-EDITED-MM.                               
           MOVE WS-DD           TO  WS-EDITED-DD.                               
                                                                                
       0500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ****************                                                          
       1000-OPEN-FILES.                                                         
      ****************                                                          
           MOVE 'OPEN' TO WS-VAR-PROC-TYPE.                                     
                                                                                
           OPEN INPUT  AB02F860                                                 
                       BP13K330                                                 
                       BP13K410                                                 
                OUTPUT BP13L319.                                                
                                                                                
           IF K330-STATUS NOT EQUAL 00 AND 97                                   
              DISPLAY 'ERROR IN OPENING K330  ' K330-STATUS                     
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
           END-IF.                                                              
                                                                                
           IF K410-STATUS NOT EQUAL 00 AND 97                                   
              DISPLAY 'ERROR IN OPENING K410  ' K410-STATUS                     
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ********************                                                      
       3000-AB02F860-READ.                                                      
      ********************                                                      
           READ  AB02F860 AT END                                                
                 MOVE  WS-CST-Y  TO  WS-VAR-AB02F860-EOF                        
                 GO  TO 3000-EXIT                                               
                 END-READ.                                                      
                                                                                
           ADD   1   TO  WS-CTR-READ-F860.                                      
           PERFORM  4000-MOVE-RECORD   THRU 4000-EXIT.                          
                                                                                
           IF F860-CDE-ACTION EQUAL '3'                                         
              CONTINUE                                                          
           ELSE                                                                 
              IF F860-CDE-ACTION EQUAL '1'                                      
                 CONTINUE                                                       
              ELSE                                                              
                 IF F860-AMT-TOT-WDL NOT = ZEROES                               
                    PERFORM  6000-WRITE-RECORD  THRU 6000-EXIT                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************                                                         
       4000-MOVE-RECORD.                                                        
      *****************                                                         
           MOVE F860-NUM-ORIG-REGN     TO  PRT-CUR-REG.                         
           MOVE F860-NUM-SNO           TO  PRT-SER-NO.                          
           MOVE F860-NUM-CPF-ACCT      TO  PRT-CPF.                             
           MOVE F860-AMT-TOT-WDL       TO  PRT-AMT.                             
                                                                                
           IF F860-AMT-MSS NOT NUMERIC                                          
              MOVE ZEROES TO F860-AMT-MSS.                                      
                                                                                
           IF F860-AMT-MSS > 0                                                  
              MOVE F860-AMT-MSS        TO  PRT-RETIRE                           
           ELSE                                                                 
              MOVE '     - '           TO  PRT-RETIRE-ALPHA.                    
                                                                                
           PERFORM 5000-READ-K330      THRU 5000-EXIT.                          
                                                                                
           IF WS-SALE-AGRM-DTE > SPACES                                         
              MOVE WS-SALE-AGRM-DTE-CC    TO  WS-CC1                            
              MOVE WS-SALE-AGRM-DTE-YY    TO  WS-YY1                            
              MOVE WS-SALE-AGRM-DTE-MM    TO  WS-MM1                            
              MOVE WS-SALE-AGRM-DTE-DD    TO  WS-DD1                            
              MOVE WS-DATE1               TO  PRT-DATE                          
           ELSE                                                                 
              MOVE SPACES                 TO  PRT-DATE                          
           END-IF.                                                              
                                                                                
           MOVE WS-NAME                   TO  PRT-NAME.                         
           MOVE WS-NRIC                   TO  PRT-NRIC.                         
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ****************                                                          
       5000-READ-K330.                                                          
      ****************                                                          
           MOVE SPACES                 TO  WS-SALE-AGRM-DTE.                    
           MOVE SPACES                 TO  WS-NAME.                             
           MOVE SPACES                 TO  WS-NRIC.                             
           MOVE LOW-VALUES             TO  K330-KEY-FLD.                        
           MOVE F860-NUM-ORIG-REGN     TO  K330-CURR-REG.                       
                                                                                
           START BP13K330 KEY > K330-KEY-FLD.                                   
                                                                                
           EVALUATE K330-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    PERFORM 5600-READ-BP13K410 THRU 5600-EXIT                   
                    GO TO 5000-EXIT                                             
               WHEN OTHER                                                       
                    DISPLAY 'ERROR IN READING K330  ' K330-KEY-FLD              
                    DISPLAY 'STOPPING PROGRAM '                                 
                    PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
           READ BP13K330 NEXT.                                                  
                                                                                
           EVALUATE K330-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K330-CURR-REG = F860-NUM-ORIG-REGN                       
                       PERFORM 5500-MOVE-K330-DATA THRU 5500-EXIT               
                    ELSE                                                        
                       PERFORM 5600-READ-BP13K410 THRU 5600-EXIT                
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    PERFORM 5600-READ-BP13K410 THRU 5600-EXIT                   
               WHEN OTHER                                                       
                    DISPLAY 'ERROR IN READING K330  ' F860-NUM-ORIG-REGN        
                    DISPLAY 'STOPPING PROGRAM '                                 
                    PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ********************                                                      
       5500-MOVE-K330-DATA.                                                     
      ********************                                                      
           MOVE K330-DTE-SALE-AGRM  TO WS-SALE-AGRM-DTE.                        
                                                                                
           IF F860-NUM-CPF-ACCT EQUAL K330-CPF1                                 
              MOVE K330-NRIC1  TO WS-NRIC                                       
              MOVE K330-NAME1  TO WS-NAME                                       
           END-IF.                                                              
                                                                                
           IF F860-NUM-CPF-ACCT EQUAL K330-CPF2                                 
              MOVE K330-NRIC2  TO WS-NRIC                                       
              MOVE K330-NAME2  TO WS-NAME                                       
           END-IF.                                                              
                                                                                
           IF F860-NUM-CPF-ACCT EQUAL K330-CPF3                                 
              MOVE K330-NRIC3  TO WS-NRIC                                       
              MOVE K330-NAME3  TO WS-NAME                                       
           END-IF.                                                              
                                                                                
           IF F860-NUM-CPF-ACCT EQUAL K330-CPF4                                 
              MOVE K330-NRIC4  TO WS-NRIC                                       
              MOVE K330-NAME4  TO WS-NAME                                       
           END-IF.                                                              
                                                                                
       5500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ********************                                                      
       5600-READ-BP13K410.                                                      
      ********************                                                      
           MOVE F860-NUM-ORIG-REGN     TO K410-NUM-REGN.                        
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE K410-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K410-NUM-REGN = F860-NUM-ORIG-REGN                       
                       PERFORM 5700-MOVE-K410-DATA THRU 5700-EXIT               
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    DISPLAY 'RECORD NOT FOUND IN K410 ' K410-KEY-FLD            
               WHEN OTHER                                                       
                    DISPLAY 'ERROR IN READING K410  ' F860-NUM-ORIG-REGN        
                    DISPLAY 'STOPPING PROGRAM '                                 
                    PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       5600-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ********************                                                      
       5700-MOVE-K410-DATA.                                                     
      ********************                                                      
           MOVE K410-DTE-KEY-ISSUE TO WS-SALE-AGRM-DTE.                         
                                                                                
           IF F860-NUM-CPF-ACCT = K410-CPF1                                     
              MOVE K410-NRIC1  TO WS-NRIC                                       
              MOVE K410-NAME1  TO WS-NAME                                       
           END-IF.                                                              
                                                                                
           IF F860-NUM-CPF-ACCT = K410-CPF2                                     
              MOVE K410-NRIC2  TO WS-NRIC                                       
              MOVE K410-NAME2  TO WS-NAME                                       
           END-IF.                                                              
                                                                                
           IF F860-NUM-CPF-ACCT = K410-CPF3                                     
              MOVE K410-NRIC3  TO WS-NRIC                                       
              MOVE K410-NAME3  TO WS-NAME                                       
           END-IF.                                                              
                                                                                
           IF F860-NUM-CPF-ACCT = K410-CPF4                                     
              MOVE K410-NRIC4  TO WS-NRIC                                       
              MOVE K410-NAME4  TO WS-NAME                                       
           END-IF.                                                              
                                                                                
       5700-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *******************                                                       
       6000-WRITE-RECORD.                                                       
      *******************                                                       
           ADD  1                      TO  WS-SEQ.                              
           ADD  1                      TO  WS-SEQ1.                             
           MOVE WS-SEQ                 TO  PRT-NO.                              
                                                                                
           IF WS-SEQ1 < 51                                                      
              WRITE  OUTPUT-LINE  FROM  REPORT-LINE                             
              ADD    1            TO    WS-CTR-WRITE-L319                       
           ELSE                                                                 
              PERFORM 7000-WRITE-HEADING THRU 7000-EXIT                         
              MOVE   1            TO    WS-SEQ1                                 
              MOVE   WS-SEQ       TO    PRT-NO                                  
              WRITE  OUTPUT-LINE  FROM  REPORT-LINE                             
              ADD    1            TO    WS-CTR-WRITE-L319                       
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *******************                                                       
       7000-WRITE-HEADING.                                                      
      *******************                                                       
           MOVE   'BP13L319'   TO  HDG-HEADER.                                  
           ADD   1             TO  PAGE-CNT.                                    
           MOVE  PAGE-CNT      TO  PRT-PAGE-CNT.                                
           MOVE WS-EDITED-DATE TO  HDG-DATE.                                    
                                                                                
           WRITE OUTPUT-LINE FROM REPORT-HEADING-1 AFTER PAGE.                  
           WRITE OUTPUT-LINE FROM REPORT-HEADING-2 AFTER 1.                     
           WRITE OUTPUT-LINE FROM REPORT-HEADING-3 AFTER 1.                     
           WRITE OUTPUT-LINE FROM REPORT-HEADING-4 AFTER 3.                     
           WRITE OUTPUT-LINE FROM REPORT-HEADING-6 AFTER 2.                     
           WRITE OUTPUT-LINE FROM REPORT-HEADING-6 AFTER 1.                     
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ***********************                                                   
       8000-WRITE-ACKNOWLEDGE.                                                  
      ***********************                                                   
           MOVE WS-SEQ    TO PRT-TOTAL-NO.                                      
           MOVE WS-DATE1  TO WS-CONTROL-DATE.                                   
           MOVE SPACES    TO REPORT-LINE.                                       
                                                                                
           WRITE OUTPUT-LINE FROM REPORT-LINE AFTER PAGE.                       
           WRITE OUTPUT-LINE FROM CONTROL-LINE1 AFTER 3.                        
           WRITE OUTPUT-LINE FROM CONTROL-LINE2  AFTER 4.                       
           WRITE OUTPUT-LINE FROM CONTROL-LINE3  AFTER 8.                       
           WRITE OUTPUT-LINE FROM CONTROL-LINE4  AFTER 1.                       
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
      *  CLOSE ALL INPUT AND OUTPUT FILES.                             *        
      *  DISPLAY # RECORDS READ & WRITE FOR BP13L319                   *        
      ******************************************************************        
                                                                                
                                                                                
      ******************                                                        
       9999-CLOSE-FILES.                                                        
      ******************                                                        
           MOVE 'CLOSE' TO WS-VAR-PROC-TYPE.                                    
                                                                                
           CLOSE    AB02F860                                                    
                    BP13K330                                                    
                    BP13K410                                                    
                    BP13L319.                                                   
                                                                                
           IF K330-STATUS NOT EQUAL 00 AND 97                                   
              DISPLAY 'ERROR IN CLOSING K330  ' K330-STATUS                     
           END-IF.                                                              
                                                                                
           IF K410-STATUS NOT EQUAL 00 AND 97                                   
              DISPLAY 'ERROR IN CLOSING K410  ' K410-STATUS                     
           END-IF.                                                              
                                                                                
           DISPLAY '     '.                                                     
           DISPLAY '  ** BP13C319 ** '.                                         
           DISPLAY '     '.                                                     
           DISPLAY 'NO. OF RECORDS READ FROM  AB02F860    '                     
                   WS-CTR-READ-F860.                                            
           DISPLAY '     '.                                                     
           DISPLAY 'NO. OF HPI FORMS SENT TO CPF BOARD    '                     
                   WS-CTR-WRITE-L319.                                           
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
