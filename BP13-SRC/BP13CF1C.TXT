       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CF1C.                                                  
      *AUTHOR.       ZDD1.                                                      
      *DATE-WRITTEN. 05/10/2015.                                                
      * ========================================================== *            
      * FLAT INVENTORY SYSTEM (BM06)                               *            
      * ========================================================== *            
      *  DELETE RECORD ON BP13KH60 AFTER SENDING EMAIL             *            
      *                                                            *            
      *  INPUT   :  BP13F23A                                       *            
      *  OUTPUT  :  BP13KH60                                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F23A ASSIGN TO BP13F23A.                                  
                                                                                
                                                                                
           SELECT BP13KH60 ASSIGN TO BP13KH60                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KH60-KEY-FLD                               
                  FILE STATUS     IS WS-KH60-STATUS.                            
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F23A                                                             
           BLOCK CONTAINS 0 CHARACTERS                                          
           RECORD CONTAINS 200  CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F23A.                                                           
                                                                                
       FD   BP13KH60                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13KH60.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-F23A-EOF                   PIC X    VALUE 'N'.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F23A-READ              PIC 9(7) VALUE ZEROS.                  
           05  WS-KH60-DELETE            PIC 9(7) VALUE ZEROS.                  
           05  WS-KH60-READ              PIC 9(7) VALUE ZEROS.                  
           05  WS-KH60-STATUS            PIC X(2) VALUE SPACES.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F23A    THRU 200-EXIT.                             
            PERFORM 300-PROCESS-F23A THRU 300-EXIT                              
                    UNTIL WS-F23A-EOF = 'Y'.                                    
            PERFORM 900-CLOSE-FILES  THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F23A                                                 
                I-O    BP13KH60.                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F23A.                                                           
      *-------------------------------------------------------------            
           READ BP13F23A AT END                                                 
                MOVE 'Y' TO WS-F23A-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1         TO WS-F23A-READ.                                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-F23A.                                                        
      *-------------------------------------------------------------            
                                                                                
           PERFORM 600-READ-BP13KH60   THRU 600-EXIT.                           
           PERFORM 200-READ-F23A       THRU 200-EXIT.                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       600-READ-BP13KH60.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES            TO KH60-KEY-FLD.                              
           MOVE F23A-KEY-FLD      TO KH60-KEY-FLD.                              
                                                                                
           READ BP13KH60.                                                       
                                                                                
           IF WS-KH60-STATUS = 00                                               
              ADD 1    TO WS-KH60-READ                                          
              PERFORM 700-DELETE-BP13KH60 THRU 700-EXIT                         
           ELSE                                                                 
              IF WS-KH60-STATUS = 23                                            
                 DISPLAY 'REC NOT FOUND IN BP13KH60 : ' KH60-KEY-FLD            
                        '  STATUS ' WS-KH60-STATUS                              
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13KH60 : ' WS-KH60-STATUS             
                 MOVE WS-KH60-STATUS     TO RETURN-CODE                         
                 PERFORM 900-CLOSE-FILES THRU 900-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       700-DELETE-BP13KH60.                                                     
      *-------------------------------------------------------------            
                                                                                
           DELETE  BP13KH60.                                                    
                                                                                
           IF WS-KH60-STATUS = 00 OR 02                                         
              ADD 1 TO WS-KH60-DELETE                                           
           ELSE                                                                 
              DISPLAY 'ERROR DELETING  BP13KH60 : ' KH60-NUM-REGN               
                      '  STATUS ' WS-KH60-STATUS                                
              MOVE WS-KH60-STATUS TO RETURN-CODE                                
              PERFORM 900-CLOSE-FILES THRU 900-EXIT                             
           END-IF.                                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '- DELKH60 CONTROL TOTAL -----------'.                       
           DISPLAY '1) NO OF REC READ    F23A  = ' WS-F23A-READ.                
           DISPLAY '4) NO OF REC READ    KH60  = ' WS-KH60-READ.                
           DISPLAY '5) NO OF REC DEL     KH60  = ' WS-KH60-DELETE.              
                                                                                
           CLOSE BP13F23A                                                       
                 BP13KH60.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
