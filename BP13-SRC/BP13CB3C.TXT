      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CB3C.                                                 
      *AUTHOR.        MARGE LAM KO.                                             
      *DATE-WRITTEN.  05 APRIL 2016.                                            
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : THIS PROGRAM CREATES HOUSING GRANT CREDITING*           
      *                 FILE                                        *           
      *                                                             *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13FD10 -- VOUCHER OIC FILE                          *           
      *   2.  BP13KD10 -- VOUCHER OIC FILE                          *           
      *   3.  BP13KD05                                              *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  AB02F930 -- HDB GRANT FILE                            *           
      *   2.  AB02F93B -- HDB GRANT FILE STATUS NOT A               *           
      *   3.  AB02F93C -- HDB GRANT FILE SENT WITH GRANTYPE AND DATE*           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136182 05/04/2016 MRN1 NEW PROGRAM                        *           
      * BP136148 05/05/2016 MRN1 READ THRESHOLD FROM TABLE          *           
      * BP136236 18/05/2016 MRN1 CHANNEL TAG = N IF THRESHOLD = 0   *           
      * BP136308 23/06/2016 MRN1 GET SCHACCT AGAIN FOR STATUS NOT A *           
      * BP136309 19/07/2016 MRN1 ADD VOUCHER NUMBER                 *           
      * BP136285 22/08/2016 MRN1 MOVE SPACES TO NUM-PREFIX IF EC CASE           
      * BP136285 23/08/2016 MRN1 FIX MOVING GRANT TYPE WHEN PROCESSING          
      *                          PREVIOUS STATUS NOT A              *           
      * BP136492 08/12/2016 MRN1 CHECK SHG STAT IF AHG STAT IS NOT A*           
      * BP136622 03/04/2017 MRN1 GET EXCESS GRANT FROM TABLE        *           
      * BP138298 08/07/2020 CT2  CATER FOR CHANNEL TAG FOR EHG      *           
      * BP138332 24/11/2020 PP11 CATER FOR LHG GRANT                *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS BP13K800-STATUS.                           
                                                                                
           SELECT BP13KD05 ASSIGN TO BP13KD05                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD05-KEY-FLD                               
                  FILE STATUS     IS BP13KD05-STATUS.                           
                                                                                
           SELECT BP13KD10 ASSIGN TO BP13KD10                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD10-KEY-FLD                               
                  FILE STATUS     IS BP13KD10-STATUS.                           
                                                                                
           SELECT BP13FD10   ASSIGN       TO BP13FD10.                          
                                                                                
           SELECT AB02F93A   ASSIGN       TO AB02F93A.                          
                                                                                
           SELECT AB02F930   ASSIGN       TO AB02F930.                          
           SELECT AB02F93B   ASSIGN       TO AB02F93B.                          
           SELECT AB02F93C   ASSIGN       TO AB02F93C.                          
           SELECT AB02F93D   ASSIGN       TO AB02F93D.                          
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13KD10                                                            
            RECORD CONTAINS  1400 CHARACTERS.                                   
       COPY BP13KD10.                                                           
                                                                                
       FD   AB02F93A                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01  AB02F93A-REC.                                                00055007
           05  F93A-REC-TYPE-D               PIC X.                             
           05  F93A-NUM-CPF-ACCT             PIC X(9).                          
           05  F93A-NUM-PREF-HDB.                                               
               10  F93A-NUM-PREFIX           PIC X.                             
               10  F93A-NUM-HDB-REF.                                            
                   15  F93B-NUM-SCH-ACCNT.                                      
                       20  F93A-NUM-SCH      PIC X(4).                          
                       20  F93A-NUM-ACCNT    PIC X(4).                          
                       20  F93A-CDE-CDG      PIC X.                             
                   15  F93A-CDE-LESSEE       PIC X(2).                          
           05  F93A-TAG-CHANNEL              PIC X.                             
           05  F93A-SIGN-GRNT                PIC X(1).                          
           05  F93A-AMT-GRNT                 PIC 9(7)V99.                       
           05  F93A-SIGN-GRNT-EXCS           PIC X(1).                          
           05  F93A-AMT-GRNT-EXCS            PIC 9(7)V99.                       
           05  F93A-AMT-THRSHOLD             PIC 9(7)V99.                       
           05  F93A-CDE-SYSID                PIC X(4).                          
           05  F93A-NUM-REGN                 PIC X(8).                          
           05  F93A-GRANT-TYPE               PIC X(3).                          
           05  F93A-DATE-SENT                PIC X(8).                          
           05  F93A-USERID                   PIC X(8).                          
           05  FILLER                        PIC X(56).                         
           05  F93A-NUM-VCHR                 PIC X(11).                         
                                                                                
       FD   BP13KD05                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13KD05.                                                           
                                                                                
       FD   BP13FD10                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  1400 CHARACTERS                                    
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FD10.                                                           
                                                                                
       FD   AB02F930                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY AB02F930.                                                   00055007
                                                                                
       FD   AB02F93B                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01  AB02F93B-REC.                                                00055007
           05  F93B-REC-TYPE-D               PIC X.                             
           05  F93B-NUM-CPF-ACCT             PIC X(9).                          
           05  F93B-NUM-PREF-HDB.                                               
               10  F93B-NUM-PREFIX           PIC X.                             
               10  F93B-NUM-HDB-REF.                                            
                   15  F93B-NUM-SCH-ACCNT.                                      
                       20  F93B-NUM-SCH      PIC X(4).                          
                       20  F93B-NUM-ACCNT    PIC X(4).                          
                       20  F93B-CDE-CDG      PIC X.                             
                   15  F93B-CDE-LESSEE       PIC X(2).                          
           05  F93B-TAG-CHANNEL              PIC X.                             
           05  F93B-SIGN-GRNT                PIC X(1).                          
           05  F93B-AMT-GRNT                 PIC 9(7)V99.                       
           05  F93B-SIGN-GRNT-EXCS           PIC X(1).                          
           05  F93B-AMT-GRNT-EXCS            PIC 9(7)V99.                       
           05  F93B-AMT-THRSHOLD             PIC 9(7)V99.                       
           05  F93B-CDE-SYSID                PIC X(4).                          
           05  F93B-NUM-REGN                 PIC X(8).                          
           05  F93B-GRANT-TYPE               PIC X(3).                          
           05  F93B-DATE-SENT                PIC X(8).                          
           05  F93B-USERID                   PIC X(8).                          
           05  FILLER                        PIC X(56).                         
           05  F93B-NUM-VCHR                 PIC X(11).                         
                                                                                
       FD   AB02F93C                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01  AB02F93C-REC.                                                00055007
           05  F93C-REC-TYPE-D               PIC X.                             
           05  F93C-NUM-CPF-ACCT             PIC X(9).                          
           05  F93C-NUM-PREF-HDB.                                               
               10  F93C-NUM-PREFIX           PIC X.                             
               10  F93C-NUM-HDB-REF.                                            
                   15  F93C-NUM-SCH-ACCNT.                                      
                       20  F93C-NUM-SCH      PIC X(4).                          
                       20  F93C-NUM-ACCNT    PIC X(4).                          
                       20  F93C-CDE-CDG      PIC X.                             
                   15  F93C-CDE-LESSEE       PIC X(2).                          
           05  F93C-TAG-CHANNEL              PIC X.                             
           05  F93C-SIGN-GRNT                PIC X(1).                          
           05  F93C-AMT-GRNT                 PIC 9(7)V99.                       
           05  F93C-SIGN-GRNT-EXCS           PIC X(1).                          
           05  F93C-AMT-GRNT-EXCS            PIC 9(7)V99.                       
           05  F93C-AMT-THRSHOLD             PIC 9(7)V99.                       
           05  F93C-CDE-SYSID                PIC X(4).                          
           05  F93C-NUM-REGN                 PIC X(8).                          
           05  F93C-GRANT-TYPE               PIC X(3).                          
           05  F93C-DATE-SENT                PIC X(8).                          
           05  F93C-USERID                   PIC X(8).                          
           05  FILLER                        PIC X(56).                         
           05  F93C-NUM-VCHR                 PIC X(11).                         
                                                                                
       FD   AB02F93D                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01  AB02F93D-REC                      PIC X(150).                00055007
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-F93A-EOF                PIC X       VALUE SPACE.              
           05  WS-FD10-EOF                PIC X       VALUE SPACE.              
           05  WS-KD05-EOF                PIC X       VALUE SPACE.              
           05  WS-GRNT-FND                PIC X       VALUE SPACE.              
           05  BP13K800-STATUS            PIC 9(2)    VALUE ZEROES.             
           05  BP13KD05-STATUS            PIC 9(2)    VALUE ZEROES.             
           05  BP13KD10-STATUS            PIC 9(2)    VALUE ZEROES.             
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CTR                     PIC 9(2)    VALUE ZEROES.             
           05  WS-AHG-RCP-CTR             PIC 9    VALUE ZEROES.                
           05  WS-SHG-RCP-CTR             PIC 9    VALUE ZEROES.                
           05  CNT-FD10-READ              PIC 9(5)    VALUE ZEROES.             
           05  CNT-F93A-READ              PIC 9(5)    VALUE ZEROES.             
           05  CNT-KD05-FND               PIC 9(5)    VALUE ZEROES.             
           05  CNT-K800-FND               PIC 9(5)    VALUE ZEROES.             
           05  WS-WRITE-CNT-DETAIL-F930   PIC 9(5)    VALUE ZEROS.              
           05  WS-CNT-REC-NOT-A           PIC 9(5)    VALUE ZEROS.              
           05  WS-UNMATCH                 PIC 9(5)    VALUE ZEROS.              
       01  WS-VARIABLES.                                                        
           05  WS-RETURN-CODE             PIC 9(02) VALUE ZEROES.               
           05  WS-SCH-ACC                 PIC X(11).                            
           05  WS-NUM-VR                  PIC X(11).                            
           05  WS-AMT-GRNT                PIC 9(7)V99.                          
           05  WS-TAG-CHANNEL             PIC X(1).                             
           05  WS-AMT-THRESHOLD           PIC 9(7)V99.                          
           05  WS-AMT-EXCESS-GRANT        PIC 9(7)V99.                          
           05  WS-NUM-REGN                PIC X(08).                            
           05  WS-NUM-GRANT               PIC X(03).                            
           05  WS-STATUS                  PIC X(01).                            
                                                                                
       01  WS-DATE                        PIC X(08)   VALUE SPACES.             
                                                                                
       01  WS-SQL-CODES.                                                        
           05  WS-SQL-OK                   PIC S9(4) COMP VALUE +000.           
           05  WS-SQL-NFND                 PIC S9(4) COMP VALUE +100.           
           05  WS-SQL-MORE-ROWS            PIC S9(4) COMP VALUE -811.           
                                                                                
       01  WS-SQLCODE                      PIC ---9.                            
      *--------------------------------------------------------------*          
      *                          SQLCA                               *          
      *--------------------------------------------------------------*          
           EXEC SQL INCLUDE SQLCA          END-EXEC.                            
           EXEC SQL INCLUDE P13VGASM       END-EXEC.                            
           EXEC SQL INCLUDE P13VGAPN       END-EXEC.                            
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2100-READ-FD10    THRU 2100-EXIT.                            
           PERFORM 3100-PROCESS-REC  THRU 3100-EXIT                             
                   UNTIL WS-FD10-EOF = 'Y'                                      
           PERFORM 2200-READ-AB02F93A THRU 2200-EXIT.                           
           PERFORM 5000-CHECK-NOT-A  THRU 5000-EXIT                             
                   UNTIL WS-F93A-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13FD10                                                 
                       BP13KD05                                                 
                       BP13KD10                                                 
                       BP13K800                                                 
                       AB02F93A                                                 
                OUTPUT AB02F930                                                 
                       AB02F93B                                                 
                       AB02F93C                                                 
                       AB02F93D.                                                
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KD05-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KD05 : ' BP13KD05-STATUS             
              MOVE BP13KD05-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KD10-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KD10 : ' BP13KD10-STATUS             
              MOVE BP13KD10-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2100-READ-FD10.                                                          
      *---------------*                                                         
                                                                                
           READ BP13FD10 AT END                                                 
                MOVE HIGH-VALUES TO BP13FD10-REC                                
                MOVE 'Y' TO WS-FD10-EOF                                         
                GO TO 2100-EXIT.                                                
                                                                                
           ADD 1 TO CNT-FD10-READ.                                              
           DISPLAY 'READ BP13FD10: ' BP13FD10-REC.                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2200-READ-AB02F93A.                                                      
      *-------------------*                                                     
                                                                                
           READ AB02F93A AT END                                                 
                MOVE HIGH-VALUES TO AB02F93A-REC                                
                MOVE 'Y' TO WS-F93A-EOF                                         
                GO TO 2200-EXIT.                                                
           ADD 1 TO CNT-F93A-READ.                                              
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3100-PROCESS-REC.                                                        
      *-----------------*                                                       
           MOVE FD10-NUM-REGN          TO WS-NUM-REGN.                          
           MOVE ZEROES TO WS-AHG-RCP-CTR, WS-SHG-RCP-CTR.                       
                                                                                
           PERFORM 3500-READ-BP13K800   THRU 3500-EXIT.                         
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 4                  
             IF FD10-NUM-CPF-PAYEE(WS-CTR) NOT = SPACES                         
                                              AND LOW-VALUES                    
                                              AND ZEROES                        
                IF FD10-NUM-GRANT(WS-CTR) = 'AHG'                               
                   ADD 1 TO WS-AHG-RCP-CTR                                      
                END-IF                                                          
                IF FD10-NUM-GRANT(WS-CTR) = 'SHG'                               
                   ADD 1 TO WS-SHG-RCP-CTR                                      
                END-IF                                                          
             END-IF                                                             
           END-PERFORM.                                                         
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 4                  
             IF FD10-NUM-CPF-PAYEE(WS-CTR) NOT = SPACES                         
                                              AND LOW-VALUES                    
                                              AND ZEROES                        
                MOVE 'N'    TO WS-GRNT-FND                                      
                MOVE FD10-NUM-REGN          TO WS-NUM-REGN                      
                MOVE FD10-NUM-GRANT(WS-CTR) TO WS-NUM-GRANT                     
                MOVE SPACES TO WS-STATUS                                        
                PERFORM 3200-RETRIEVE-GRANT-ASMNT THRU 3200-EXIT                
                IF WS-GRNT-FND = 'Y'                                            
                   IF FD10-AMT-CPF-GRANT-PAYEE(WS-CTR) IS NUMERIC               
                     MOVE FD10-AMT-CPF-GRANT-PAYEE(WS-CTR)                      
                                          TO WS-AMT-GRNT                        
                   ELSE                                                         
                     MOVE ZEROES TO WS-AMT-GRNT                                 
                   END-IF                                                       
                   IF FD10-NUM-GRANT(WS-CTR) = 'AHG' OR 'SHG' OR                
                                               'PHG' OR 'EHG' OR                
                                               'LHG' OR 'SUG'                   
                      MOVE 'Y'          TO WS-TAG-CHANNEL                       
                   ELSE                                                         
                      MOVE 'N'          TO WS-TAG-CHANNEL                       
                   END-IF                                                       
                   PERFORM 4000-WRITE-DETAILS  THRU 4000-EXIT                   
                END-IF                                                          
             END-IF                                                             
           END-PERFORM.                                                         
                                                                                
           PERFORM 2100-READ-FD10 THRU 2100-EXIT.                               
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-RETRIEVE-GRANT-ASMNT.                                               
      ******************************************************************        
           MOVE WS-NUM-REGN      TO NUM-RGSTRN OF DCLGRANT-ASMNT.               
           MOVE WS-NUM-GRANT     TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT.           
                                                                                
           MOVE 'N'              TO WS-GRNT-FND.                                
                                                                                
           EXEC SQL                                                             
              SELECT NUM_GRANT_STATUS                                           
              INTO  :DCLGRANT-ASMNT.NUM-GRANT-STATUS                            
              FROM                                                              
                GRANT_ASMNT                                                     
              WHERE NUM_RGSTRN       = :DCLGRANT-ASMNT.NUM-RGSTRN               
               AND  NUM_GRANT_TYPE   = :DCLGRANT-ASMNT.NUM-GRANT-TYPE           
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN WS-SQL-OK                                                       
                MOVE NUM-GRANT-STATUS OF DCLGRANT-ASMNT TO WS-STATUS            
                MOVE 'Y' TO WS-GRNT-FND                                         
                DISPLAY 'FOUND IN GRANT_ASMNT'                                  
                DISPLAY 'REGNO : ' NUM-RGSTRN OF DCLGRANT-ASMNT                 
                DISPLAY 'GRANT TYPE: ' NUM-GRANT-TYPE OF DCLGRANT-ASMNT         
                DISPLAY SPACES                                                  
           WHEN WS-SQL-NFND                                                     
                MOVE SQLCODE   TO WS-SQLCODE                                    
                DISPLAY 'CASE NOTFND IN GRANT_ASMNT'                            
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE                              
                DISPLAY 'REGNO : ' NUM-RGSTRN OF DCLGRANT-ASMNT                 
                DISPLAY 'GRANT TYPE: ' NUM-GRANT-TYPE OF DCLGRANT-ASMNT         
                DISPLAY SPACES                                                  
                                                                                
           WHEN OTHER                                                           
                MOVE SQLCODE   TO WS-SQLCODE                                    
                DISPLAY 'ERROR READING GRANT_ASMNT'                             
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE                              
                DISPLAY SPACES                                                  
                PERFORM 9999-SQL-ERROR      THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3500-READ-BP13K800.                                                      
      *---------------------*                                                   
                                                                                
           MOVE SPACES           TO WS-SCH-ACC.                                 
           MOVE SPACES           TO BP13K800-MASTER.                            
           MOVE WS-NUM-REGN      TO K800-NUM-REGN.                              
                                                                                
           READ BP13K800.                                                       
           IF BP13K800-STATUS = 0                                               
              IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                   
                 MOVE K800-NUM-SCH-ACC TO WS-SCH-ACC                            
              END-IF                                                            
              ADD 1 TO CNT-K800-FND                                             
           ELSE                                                                 
              IF BP13K800-STATUS = 23                                           
                 MOVE SPACES    TO WS-SCH-ACC                                   
                 DISPLAY 'RECORD NOT FOUND IN BP13K800: ' K800-NUM-REGN         
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13K800 : ' BP13K800-STATUS          
                 MOVE BP13K800-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3550-READ-BP13KD05.                                                      
      *---------------------*                                                   
                                                                                
           MOVE SPACES                     TO BP13KD05-REC.                     
           INITIALIZE                         BP13KD05-REC.                     
           MOVE FD10-NUM-REGN              TO KD05-NUM-REGN.                    
           MOVE FD10-NUM-CPF-PAYEE(WS-CTR) TO KD05-NUM-NRIC.                    
                                                                                
           READ BP13KD05.                                                       
           IF BP13KD05-STATUS = 0                                               
              IF KD05-AMT-THRESHOLD NOT NUMERIC                                 
                 MOVE ZEROES                  TO WS-AMT-THRESHOLD               
              ELSE                                                              
                 MOVE KD05-AMT-THRESHOLD      TO WS-AMT-THRESHOLD               
              END-IF                                                            
           ELSE                                                                 
              IF BP13KD05-STATUS = 23                                           
                 MOVE ZEROES       TO WS-AMT-THRESHOLD                          
                 MOVE 'N'          TO WS-TAG-CHANNEL                            
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KD05 : ' BP13KD05-STATUS          
                 MOVE BP13KD05-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3550-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3700-READ-BP13KD10.                                                      
      *---------------------*                                                   
                                                                                
           MOVE SPACES                     TO BP13KD10-REC.                     
           INITIALIZE                         BP13KD10-REC.                     
           MOVE WS-NUM-GRANT               TO KD10-NUM-TRANS.                   
           MOVE WS-NUM-REGN                TO KD10-NUM-REGN.                    
           MOVE SPACES                     TO KD10-NUM-PP.                      
                                                                                
           READ BP13KD10.                                                       
           IF BP13KD10-STATUS = 0                                               
              MOVE KD10-NUM-VR TO WS-NUM-VR                                     
           ELSE                                                                 
              IF BP13KD10-STATUS = 23                                           
                 MOVE SPACES       TO WS-NUM-VR                                 
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KD10 : ' BP13KD10-STATUS          
                 MOVE BP13KD10-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       4000-WRITE-DETAILS.                                                      
      *-----------------*                                                       
                                                                                
           MOVE SPACES           TO AB02F930-DETAIL-REC.                        
           INITIALIZE               AB02F930-DETAIL-REC.                        
                                                                                
           MOVE '1'                        TO F930-REC-TYPE-D.                  
           MOVE FD10-NUM-CPF-PAYEE(WS-CTR) TO F930-NUM-CPF-ACCT.                
                                                                                
           IF WS-NUM-REGN(1:1) = '6'                                            
              MOVE SPACES                  TO F930-NUM-PREFIX                   
           ELSE                                                                 
              MOVE '0'                     TO F930-NUM-PREFIX                   
           END-IF.                                                              
                                                                                
           MOVE WS-SCH-ACC                 TO F930-NUM-HDB-REF.                 
           MOVE WS-AMT-GRNT                TO F930-AMT-GRNT.                    
                                                                                
           IF FD10-NUM-GRANT(WS-CTR) = 'AHG' OR 'SHG' OR 'EHG' OR 'PHG'         
                                             OR 'LHG' OR 'SUG'                  
              PERFORM 4500-READ-GRANT-PERSON  THRU 4500-EXIT                    
           ELSE                                                                 
              MOVE ZEROES TO WS-AMT-THRESHOLD                                   
                             WS-AMT-EXCESS-GRANT                                
           END-IF.                                                              
                                                                                
           MOVE WS-AMT-THRESHOLD           TO F930-AMT-THRSHOLD.                
                                                                                
           IF WS-AMT-THRESHOLD = ZEROES                                         
              MOVE 'N' TO WS-TAG-CHANNEL                                        
           ELSE                                                                 
              MOVE 'Y' TO WS-TAG-CHANNEL                                        
           END-IF.                                                              
                                                                                
           MOVE WS-TAG-CHANNEL             TO F930-TAG-CHANNEL.                 
                                                                                
           MOVE '+'                        TO F930-SIGN-GRNT                    
                                              F930-SIGN-GRNT-EXCS.              
           MOVE WS-AMT-EXCESS-GRANT        TO F930-AMT-GRNT-EXCS.               
           MOVE 'BP13'                     TO F930-CDE-SYSID.                   
           MOVE FD10-NUM-REGN              TO F930-NUM-REGN.                    
      *    MOVE FD10-NUM-VR                TO F930-NUM-VCHR.                    
                                                                                
           IF WS-NUM-GRANT = 'AHG'                                              
              IF WS-STATUS NOT = 'A'                                            
                 MOVE 'SHG' TO WS-NUM-GRANT                                     
                 PERFORM 3200-RETRIEVE-GRANT-ASMNT THRU 3200-EXIT               
              END-IF                                                            
           ELSE                                                                 
              IF WS-NUM-GRANT = 'SHG'                                           
                 IF WS-STATUS NOT = 'A'                                         
                    MOVE 'AHG' TO WS-NUM-GRANT                                  
                    PERFORM 3200-RETRIEVE-GRANT-ASMNT THRU 3200-EXIT            
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-STATUS = 'A'                                                   
              MOVE AB02F930-DETAIL-REC(1:64) TO AB02F93C-REC(1:64)              
              WRITE AB02F930-DETAIL-REC                                         
              ADD 1 TO WS-WRITE-CNT-DETAIL-F930                                 
              MOVE FD10-NUM-GRANT(WS-CTR)    TO F93C-GRANT-TYPE                 
              MOVE WS-DATE                   TO F93C-DATE-SENT                  
              MOVE FD10-NUM-USERID           TO F93C-USERID                     
              MOVE WS-SCH-ACC                TO F93C-NUM-HDB-REF                
              MOVE FD10-NUM-VR               TO F93C-NUM-VCHR                   
              WRITE AB02F93C-REC                                                
           ELSE                                                                 
              ADD 1 TO WS-CNT-REC-NOT-A                                         
              MOVE AB02F930-DETAIL-REC  TO AB02F93B-REC                         
              MOVE FD10-NUM-GRANT(WS-CTR) TO F93B-GRANT-TYPE                    
              MOVE WS-DATE                TO F93B-DATE-SENT                     
              MOVE FD10-NUM-USERID        TO F93B-USERID                        
              MOVE FD10-NUM-VR            TO F93B-NUM-VCHR                      
              WRITE AB02F93B-REC                                                
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       4500-READ-GRANT-PERSON.                                                  
      *-----------------*                                                       
           INITIALIZE SQLCODE.                                                  
           MOVE ZEROES TO WS-AMT-THRESHOLD                                      
                          WS-AMT-EXCESS-GRANT.                                  
                                                                                
           MOVE FD10-NUM-REGN  TO NUM-RGSTRN OF DCLGRANT-ASMNT-PERSON.          
           MOVE FD10-NUM-GRANT(WS-CTR)                                          
                           TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT-PERSON.          
           MOVE FD10-NUM-CPF-PAYEE(WS-CTR)                                      
                           TO NUM-IDNTY-NO OF DCLGRANT-ASMNT-PERSON.            
                                                                                
           EXEC SQL                                                             
             SELECT AMT_GRANT_THRSHD,                                           
                    AMT_GRANT_EXCESS                                            
               INTO :DCLGRANT-ASMNT-PERSON.AMT-GRANT-THRSHD,                    
                    :DCLGRANT-ASMNT-PERSON.AMT-GRANT-EXCESS                     
               FROM GRANT_ASMNT_PERSON                                          
             WHERE NUM_RGSTRN = :DCLGRANT-ASMNT-PERSON.NUM-RGSTRN               
              AND NUM_GRANT_TYPE = :DCLGRANT-ASMNT-PERSON.NUM-GRANT-TYPE        
              AND NUM_IDNTY_NO   = :DCLGRANT-ASMNT-PERSON.NUM-IDNTY-NO          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN WS-SQL-OK                                                    
              WHEN WS-SQL-MORE-ROWS                                             
                 MOVE AMT-GRANT-THRSHD OF DCLGRANT-ASMNT-PERSON                 
                                          TO WS-AMT-THRESHOLD                   
                 IF WS-AMT-THRESHOLD = ZEROES                                   
                    PERFORM 3550-READ-BP13KD05  THRU 3550-EXIT                  
                 END-IF                                                         
                 IF AMT-GRANT-EXCESS OF DCLGRANT-ASMNT-PERSON > ZEROES          
                    MOVE AMT-GRANT-EXCESS OF DCLGRANT-ASMNT-PERSON              
                                          TO WS-AMT-EXCESS-GRANT                
                 END-IF                                                         
              WHEN WS-SQL-NFND                                                  
                 PERFORM 3550-READ-BP13KD05  THRU 3550-EXIT                     
              WHEN OTHER                                                        
                 MOVE SQLCODE   TO WS-SQLCODE                                   
                 DISPLAY 'ERROR READING GRANT_ASMNT_PERSON'                     
                 DISPLAY 'SQLCODE IS : ' WS-SQLCODE                             
                 DISPLAY SPACES                                                 
                 PERFORM 9999-SQL-ERROR      THRU 9999-EXIT                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       5000-CHECK-NOT-A.                                                        
      *-----------------*                                                       
           MOVE F93A-NUM-REGN   TO WS-NUM-REGN.                                 
                                                                                
           IF F93A-GRANT-TYPE = 'AHG' OR 'SHG'                                  
              MOVE 'ASG' TO WS-NUM-GRANT                                        
           ELSE                                                                 
              MOVE F93A-GRANT-TYPE TO WS-NUM-GRANT                              
           END-IF.                                                              
                                                                                
           PERFORM 3500-READ-BP13K800   THRU 3500-EXIT.                         
           PERFORM 3700-READ-BP13KD10   THRU 3700-EXIT.                         
                                                                                
           MOVE F93A-GRANT-TYPE TO WS-NUM-GRANT.                                
           MOVE SPACES TO WS-STATUS.                                            
           PERFORM 3200-RETRIEVE-GRANT-ASMNT THRU 3200-EXIT                     
                                                                                
           IF WS-GRNT-FND = 'Y'                                                 
              IF WS-NUM-GRANT = 'AHG'                                           
                 IF WS-STATUS NOT = 'A'                                         
                    MOVE 'SHG' TO WS-NUM-GRANT                                  
                    PERFORM 3200-RETRIEVE-GRANT-ASMNT THRU 3200-EXIT            
                 END-IF                                                         
              ELSE                                                              
                 IF WS-NUM-GRANT = 'SHG'                                        
                    IF WS-STATUS NOT = 'A'                                      
                       MOVE 'AHG' TO WS-NUM-GRANT                               
                       PERFORM 3200-RETRIEVE-GRANT-ASMNT THRU 3200-EXIT         
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF WS-STATUS = 'A'                                                
                 IF (F93A-NUM-VCHR = SPACES OR LOW-VALUES) OR                   
                    (F93A-NUM-VCHR = WS-NUM-VR)                                 
                    MOVE AB02F93A-REC(1:64)  TO AB02F930-DETAIL-REC             
                    MOVE WS-SCH-ACC          TO F930-NUM-HDB-REF                
      *             MOVE WS-NUM-VR           TO F930-NUM-VCHR                   
                    WRITE AB02F930-DETAIL-REC                                   
                    ADD 1 TO WS-WRITE-CNT-DETAIL-F930                           
                    MOVE AB02F93A-REC        TO AB02F93C-REC                    
                    MOVE WS-SCH-ACC          TO F93C-NUM-HDB-REF                
                    MOVE WS-NUM-VR           TO F93C-NUM-VCHR                   
                    WRITE AB02F93C-REC                                          
                 ELSE                                                           
                    MOVE AB02F93A-REC        TO AB02F93D-REC                    
                    WRITE AB02F93D-REC                                          
                    ADD 1 TO WS-UNMATCH                                         
                 END-IF                                                         
              ELSE                                                              
                 ADD 1 TO WS-CNT-REC-NOT-A                                      
                 MOVE AB02F93A-REC  TO AB02F93B-REC                             
                 MOVE WS-SCH-ACC    TO F93B-NUM-HDB-REF                         
                 MOVE WS-NUM-VR     TO F93B-NUM-VCHR                            
                 WRITE AB02F93B-REC                                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2200-READ-AB02F93A THRU 2200-EXIT.                           
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CB3C *************'.                            
           DISPLAY 'NO OF RECS READ FROM  BP13FD10  : ' CNT-FD10-READ.          
           DISPLAY 'NO OF RECS FOUND IN   BP13K800  : ' CNT-K800-FND.           
           DISPLAY 'NO OF RECS WRITTEN TO AB02F930  : '                         
                                               WS-WRITE-CNT-DETAIL-F930.        
           DISPLAY 'NO OF RECS W/ STATUS NOT = A    : '                         
                                                WS-CNT-REC-NOT-A.               
           DISPLAY 'NO OF RECS WITH VRNO NOT MATCHED: ' WS-UNMATCH.             
                                                                                
           CLOSE BP13K800                                                       
                 BP13FD10                                                       
                 BP13KD05                                                       
                 BP13KD10                                                       
                 AB02F93A                                                       
                 AB02F930                                                       
                 AB02F93B                                                       
                 AB02F93C                                                       
                 AB02F93D.                                                      
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K800-STATUS ' BP13K800-STATUS          
            MOVE BP13K800-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KD05-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KD05-STATUS ' BP13KD05-STATUS          
            MOVE BP13KD05-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------         
       9999-SQL-ERROR.                                                          
      *----------------------------------------------------------------         
           CALL 'DBATIAR'  USING SQLCA.                                         
           EXEC SQL ROLLBACK               END-EXEC.                            
           MOVE 99                         TO    WS-RETURN-CODE.                
           PERFORM 9000-CLOSE-FILES        THRU  9000-EXIT.                     
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
