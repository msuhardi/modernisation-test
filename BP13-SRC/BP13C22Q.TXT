       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C22Q.                                                 
      *AUTHOR.        LIAW MEI SIEN.                                            
      *DATE-WRITTEN.  30/03/05.                                                 
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *    OBJECTIVE  :                                        *                
      *            TO PERFORM BATCH REJECTION.                 *                
      *                                                        *                
      *    INPUT    :  BP13F205                                *                
      *    I-O      :  BM06K100                                *                
      *                BM06K110                                *                
      *                BP13K270                                *                
      *                BP13K800                                *                
      *                BP13K840                                *                
      *    OUTPUT   :  BM06F100                                *                
      *                BM06F110                                *                
      *                BM06F190                                *                
      *                BP13F310                                *                
      *                BP13F800                                *                
      *                BP13F840                                *                
      *                BP13FOUT                                *                
      *--------------------------------------------------------*                
      *  CHG-NO BY    ON     DESCRIPTION                       *                
      *  -------------------------------                       *                
      *BP132681 LMS 300305 - NEW PROGRAM.                      *                
      *BP134681 ESA1200912 - CATER FOR 3-BYTES                 *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
                                                                                
           SELECT BM06K100 ASSIGN TO BM06K100                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K100-KEY-FLD                               
                  FILE STATUS     IS BM06K100-STATUS.                           
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K110-KEY-FLD                               
                  ALTERNATE RECORD KEY IS K110-AIX1                             
                  FILE STATUS     IS BM06K110-STATUS.                           
                                                                                
           SELECT BP13K270 ASSIGN TO BP13K270                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K270-KEY-FLD                               
                  FILE STATUS     IS BP13K270-STATUS.                           
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS BP13K800-STATUS.                           
                                                                                
           SELECT BP13K840 ASSIGN TO BP13K840                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K840-KEY-FLD                               
                  FILE STATUS     IS BP13K840-STATUS.                           
                                                                                
           SELECT BM06F100 ASSIGN TO BM06F100.                                  
           SELECT BM06F110 ASSIGN TO BM06F110.                                  
           SELECT BM06F190 ASSIGN TO BM06F190.                                  
           SELECT BP13F310 ASSIGN TO BP13F310.                                  
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
           SELECT BP13F840 ASSIGN TO BP13F840.                                  
           SELECT BP13FOUT ASSIGN TO BP13FOUT.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F205                                                             
           BLOCK  CONTAINS  0  RECORDS                                          
           RECORD CONTAINS 80  CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F205.                                                           
                                                                                
       FD  BM06K100                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K100.                                                           
                                                                                
       FD  BM06F100                                                             
           RECORDING MODE  F                                                    
           LABEL RECORDS   ARE STANDARD                                         
           BLOCK CONTAINS  0   RECORDS                                          
           RECORD CONTAINS 500 CHARACTERS.                                      
       01  BM06F100-REC        PIC X(500).                                      
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BM06F110                                                             
           RECORDING MODE  F                                                    
           LABEL RECORDS   ARE STANDARD                                         
           BLOCK CONTAINS  0   RECORDS                                          
           RECORD CONTAINS 500 CHARACTERS.                                      
       01  BM06F110-REC        PIC X(500).                                      
                                                                                
       FD  BP13K270                                                             
           RECORD CONTAINS 250  CHARACTERS.                                     
       COPY BP13K270.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13F800                                                             
           RECORDING MODE  F                                                    
           LABEL RECORDS   ARE STANDARD                                         
           BLOCK CONTAINS  0   RECORDS                                          
           RECORD CONTAINS 2000 CHARACTERS.                                     
       01  BP13F800-REC        PIC X(2000).                                     
                                                                                
       FD  BP13K840                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K840.                                                           
                                                                                
       FD  BP13F840                                                             
           RECORDING MODE  F                                                    
           LABEL RECORDS   ARE STANDARD                                         
           BLOCK CONTAINS  0   RECORDS                                          
           RECORD CONTAINS 500 CHARACTERS.                                      
       01  BP13F840-REC        PIC X(500).                                      
                                                                                
       FD  BP13F310                                                             
           RECORDING MODE  F                                                    
           LABEL RECORDS   ARE STANDARD                                         
           BLOCK CONTAINS  0   RECORDS                                          
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13F310.                                                           
                                                                                
       FD  BM06F190                                                             
           RECORDING MODE  F                                                    
           LABEL RECORDS   ARE STANDARD                                         
           BLOCK CONTAINS  0   RECORDS                                          
           RECORD CONTAINS 650 CHARACTERS.                                      
       COPY BM06F190.                                                           
                                                                                
       FD  BP13FOUT                                                             
           RECORDING MODE  F                                                    
           LABEL RECORDS   ARE STANDARD                                         
           BLOCK CONTAINS  0   RECORDS                                          
           RECORD CONTAINS 150 CHARACTERS.                                      
       01  BP13FOUT-REC.                                                        
           05  FOUT-REGN        PIC X(08).                                      
           05  FOUT-ZONE        PIC X(03).                                      
           05  FOUT-NT          PIC X(03).                                      
           05  FOUT-NRIC1       PIC X(09).                                      
           05  FOUT-NRIC2       PIC X(09).                                      
           05  FOUT-NRIC3       PIC X(09).                                      
           05  FOUT-NRIC4       PIC X(09).                                      
           05  FOUT-SCH-ACC     PIC X(11).                                      
           05  FOUT-BLK         PIC X(05).                                      
           05  FOUT-STREET      PIC X(32).                                      
           05  FOUT-LEVEL       PIC X(02).                                      
           05  FOUT-MAIN        PIC X(04).                                      
           05  FOUT-SUB         PIC X(03).                                      
           05  FOUT-DTE-BALLOT  PIC X(06).                                      
           05  FOUT-FLAT-TYPE   PIC X(02).                                      
           05  FILLER           PIC X(35).                                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BM06K100-STATUS          PIC 9(2)  VALUE ZEROS.                      
       01  BM06K110-STATUS          PIC 9(2)  VALUE ZEROS.                      
       01  BP13K270-STATUS          PIC 9(2)  VALUE ZEROS.                      
       01  BP13K800-STATUS          PIC 9(2)  VALUE ZEROS.                      
       01  BP13K840-STATUS          PIC 9(2)  VALUE ZEROS.                      
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-BP13F205-READ       PIC 9(08) VALUE ZEROES.                   
           05  WS-BP13K270-READ       PIC 9(08) VALUE ZEROES.                   
           05  WS-BP13F310-WRITE      PIC 9(08) VALUE ZEROES.                   
           05  WS-BP13K800-READ       PIC 9(08) VALUE ZEROES.                   
           05  WS-BP13F800-WRITE      PIC 9(08) VALUE ZEROES.                   
           05  WS-BP13K840-READ       PIC 9(08) VALUE ZEROES.                   
           05  WS-BP13F840-WRITE      PIC 9(08) VALUE ZEROES.                   
           05  WS-BM06K100-READ       PIC 9(08) VALUE ZEROES.                   
           05  WS-BM06F100-WRITE      PIC 9(08) VALUE ZEROES.                   
           05  WS-BM06K110-READ       PIC 9(08) VALUE ZEROES.                   
           05  WS-BM06F110-WRITE      PIC 9(08) VALUE ZEROES.                   
           05  WS-BM06F190-WRITE      PIC 9(08) VALUE ZEROES.                   
           05  WS-BP13FOUT-WRITE      PIC 9(08) VALUE ZEROES.                   
           05  WS-CTR                 PIC 9(03) VALUE ZEROES.                   
           05  WS-RM-PTR              PIC 9(02) VALUE ZEROES.                   
           05  WS-BP13F205-EOF        PIC X(01) VALUE 'N'.                      
           05  WS-BP13K270-EOF        PIC X(01) VALUE 'N'.                      
           05  WS-BM06K110-EOF        PIC X(01) VALUE 'N'.                      
           05  WS-BP13K800-FOUND      PIC X(01) VALUE 'N'.                      
           05  WS-BP13K840-FOUND      PIC X(01) VALUE 'N'.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K800-NUM-FLAT-TYPE  PIC X(02) VALUE SPACES.                   
           05  WS-K800-NUM-ZONE       PIC X(03) VALUE SPACES.                   
           05  WS-K800-NUM-NT         PIC X(03) VALUE SPACES.                   
                                                                                
       01  WS-K100-KEY.                                                         
           05  WS-EST                 PIC X(03) VALUE SPACES.                   
           05  WS-NEIGH               PIC X(04) VALUE SPACES.                   
           05  WS-CONTR               PIC X(04) VALUE SPACES.                   
           05  WS-BLK                 PIC X(05) VALUE SPACES.                   
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-DATE                    PIC X(8)  VALUE SPACES.               
           05  WS-CURRENT-TIME            PIC X(9)  VALUE SPACES.               
           05  FILLER                     PIC X(4)  VALUE SPACES.               
       01  WS-CURR-TIME.                                                        
           05  WS-CURR-TIME1             PIC X(9).                              
           05  WS-TIME REDEFINES WS-CURR-TIME1.                                 
               10  WS-HH                   PIC 9(2).                            
               10  WS-MM                   PIC 9(2).                            
               10  WS-SS                   PIC 9(2).                            
               10  WS-GM                   PIC 9(3).                            
       01  WS-GM-CNT                      PIC S9(8) COMP VALUE ZEROS.           
                                                                                
       01  WS-F190-SYS-TIME.                                                    
           05  WS-F190-TEMP-TIME             PIC 9(6) VALUE ZEROS.              
           05  WS-F190-TIME-HMS    REDEFINES WS-F190-TEMP-TIME.                 
               10 WS-F190-HH                 PIC 9(2).                          
               10 WS-F190-MM                 PIC 9(2).                          
               10 WS-F190-SS                 PIC 9(2).                          
           05  WS-F190-MICRO-SS              PIC 9(1) VALUE ZEROS.              
                                                                                
      *---------------------------------------------------------------*         
      *        LINKAGE FOR BP13C913                                   *         
      *---------------------------------------------------------------*         
        01  WS-LINK-REC.                                                        
            05  WS-LINK-NUM-SCH-ACC.                                            
                10  WS-LINK-NUM-SCH   PIC X(4).                                 
                10  WS-LINK-NUM-ACC   PIC X(5).                                 
        COPY P13COMM8.                                                          
                                                                                
      *-------------------*                                                     
        PROCEDURE DIVISION.                                                     
      *-------------------*                                                     
                                                                                
      *---------------------------------------------------------------*         
        0000-CONTROL.                                                           
      *---------------------------------------------------------------*         
           PERFORM  1000-OPEN-FILES    THRU     1000-EXIT.                      
                                                                                
           PERFORM  2000-READ-F205     THRU     2000-EXIT                       
                    UNTIL WS-BP13F205-EOF = 'Y'.                                
                                                                                
           PERFORM  9000-CLOSE-FILES   THRU     9000-EXIT.                      
                                                                                
        0000-EXIT.                                                              
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
      *------------------------------------                                     
      *   OPEN FILES AND CHECK STATUS                                           
      *------------------------------------                                     
           OPEN    INPUT       BP13F205                                         
                               BM06K100                                         
                               BM06K110                                         
                               BP13K270                                         
                               BP13K800                                         
                               BP13K840                                         
                   OUTPUT      BM06F100                                         
                               BM06F110                                         
                               BP13F800                                         
                               BP13F840                                         
                               BP13F310                                         
                               BM06F190                                         
                               BP13FOUT.                                        
                                                                                
           IF BM06K100-STATUS NOT = '00' AND '97'                               
              DISPLAY 'ERROR WHILE OPENING BM06K100 ' BM06K100-STATUS           
              PERFORM  9000-CLOSE-FILES THRU 9000-EXIT                          
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = '00' AND '97'                               
              DISPLAY 'ERROR WHILE OPENING BM06K110 ' BM06K110-STATUS           
              PERFORM  9000-CLOSE-FILES THRU 9000-EXIT                          
           END-IF.                                                              
                                                                                
           IF BP13K270-STATUS NOT = '00' AND '97'                               
              DISPLAY 'ERROR WHILE OPENING BP13K270 ' BP13K270-STATUS           
              PERFORM  9000-CLOSE-FILES THRU 9000-EXIT                          
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = '00' AND '97'                               
              DISPLAY 'ERROR WHILE OPENING BP13K800 ' BP13K800-STATUS           
              PERFORM  9000-CLOSE-FILES THRU 9000-EXIT                          
           END-IF.                                                              
                                                                                
           IF BP13K840-STATUS NOT = '00' AND '97'                               
              DISPLAY 'ERROR WHILE OPENING BP13K840 ' BP13K840-STATUS           
              PERFORM  9000-CLOSE-FILES THRU 9000-EXIT                          
           END-IF.                                                              
                                                                                
      *-----------------------*                                                 
      *    GETTING SYSTEM DATE                                                  
      *-----------------------*                                                 
                                                                                
            MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                      
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-F205.                                                          
      *-------------------------------------------------------------            
           READ  BP13F205 AT END MOVE 'Y' TO WS-BP13F205-EOF                    
                 GO TO 2000-EXIT.                                               
                                                                                
           ADD   1  TO    WS-BP13F205-READ.                                     
                                                                                
           PERFORM 2100-ACCESS-BP13K270 THRU 2100-EXIT.                         
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-ACCESS-BP13K270.                                                    
      *-------------------------------------------------------------            
           MOVE SPACES              TO K270-KEY-FLD.                            
           MOVE F205-DTE-ALLOCN     TO K270-ALDATE-ZONE.                        
           START BP13K270 KEY NOT < K270-KEY-FLD.                               
                                                                                
           IF BP13K270-STATUS NOT = ZEROS                                       
              DISPLAY 'BP13K270 START ERROR , KEY = ' K270-KEY-FLD              
              DISPLAY 'BP13K270 STATUS = ' BP13K270-STATUS                      
              MOVE 'Y' TO WS-BP13K270-EOF                                       
              GO TO 2100-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 2200-READNEXT-BP13K270 THRU 2200-EXIT.                       
                                                                                
           PERFORM 2300-READ-BP13K270 THRU 2300-EXIT                            
             UNTIL WS-BP13K270-EOF = 'Y'                                        
                OR K270-ALLOCN-DATE > F205-DTE-ALLOCN.                          
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-READNEXT-BP13K270.                                                  
      *-------------------------------------------------------------            
           READ BP13K270 NEXT AT END MOVE 'Y' TO WS-BP13K270-EOF.               
           ADD 1 TO WS-BP13K270-READ.                                           
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2300-READ-BP13K270.                                                      
      *-------------------------------------------------------------            
           IF F205-DTE-ALLOCN = K270-ALLOCN-DATE                                
              IF F205-NUM-NT-ZONE = K270-NUM-SITE                               
                                                                                
                 MOVE K270-ESTATE        TO WS-EST                              
                 MOVE K270-NEIGHBOURHOOD TO WS-NEIGH                            
                 MOVE K270-CONTRACT-NO   TO WS-CONTR                            
                 MOVE K270-NUM-BLK       TO WS-BLK                              
                 PERFORM 4000-READ-BM06K100 THRU 4000-EXIT                      
                                                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2200-READNEXT-BP13K270 THRU 2200-EXIT.                       
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-READ-BM06K100.                                                      
      *-------------------------------------------------------------            
           MOVE WS-K100-KEY TO K100-KEY-FLD OF K100-REC.                        
           READ BM06K100.                                                       
                                                                                
           IF BM06K100-STATUS NOT = ZEROS                                       
              DISPLAY 'READ BM06K100 ERROR, STATUS ' BM06K100-STATUS            
              DISPLAY 'BM06K100-KEY-FLD IS ' K100-KEY-FLD OF K100-REC           
              GO TO 4000-EXIT                                                   
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-BM06K100-READ.                                           
           PERFORM 4100-READ-BM06K110 THRU 4100-EXIT.                           
                                                                                
           MOVE K100-REC TO BM06F100-REC.                                       
           WRITE BM06F100-REC.                                                  
           ADD 1 TO WS-BM06F100-WRITE.                                          
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4050-PREPARE-UPD-BM06F100.                                               
      *-------------------------------------------------------------            
           EVALUATE COMM8-CDE-TYP-ACTLUSE                                       
              WHEN '01'                                                         
                 MOVE 1 TO WS-RM-PTR                                            
              WHEN '02'                                                         
                 MOVE 2 TO WS-RM-PTR                                            
              WHEN '03'                                                         
                 MOVE 3 TO WS-RM-PTR                                            
              WHEN '04'                                                         
                 IF COMM8-CDE-MODL-DESG = '06'                                  
                    MOVE 5 TO WS-RM-PTR                                         
                 ELSE                                                           
                    MOVE 4 TO WS-RM-PTR                                         
                 END-IF                                                         
              WHEN '05'                                                         
                 MOVE 6 TO WS-RM-PTR                                            
              WHEN '06'                                                         
                 MOVE 7 TO WS-RM-PTR                                            
              WHEN '07'                                                         
                 MOVE 8 TO WS-RM-PTR                                            
              WHEN '08'                                                         
                 MOVE 9 TO WS-RM-PTR                                            
           END-EVALUATE.                                                        
                                                                                
           ADD 1 TO K100-BAL-ALLOCN(WS-RM-PTR).                                 
                                                                                
           IF K800-NUM-CAT = '1'                                                
              SUBTRACT 1 FROM K100-CAT1-ACCEPT(WS-RM-PTR)                       
           END-IF.                                                              
                                                                                
           IF K800-NUM-CAT = '2'                                                
              SUBTRACT 1 FROM K100-CAT2-ACCEPT(WS-RM-PTR)                       
           END-IF.                                                              
                                                                                
           IF K800-NUM-CAT = '3'                                                
              SUBTRACT 1 FROM K100-CAT3-ACCEPT(WS-RM-PTR)                       
           END-IF.                                                              
                                                                                
       4050-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
           MOVE K100-KEY-FLD TO K110-AIX1.                                      
           START BM06K110 KEY >= K110-AIX1.                                     
                                                                                
           IF BM06K110-STATUS NOT = 00                                          
              DISPLAY 'BM06K110 START ERROR, STATUS ' BM06K110-STATUS           
              GO TO 4100-EXIT                                                   
           END-IF.                                                              
                                                                                
           READ BM06K110 NEXT AT END                                            
                              MOVE 'Y' TO WS-BM06K110-EOF.                      
                                                                                
                                                                                
           PERFORM 4150-UPDATE-BM06K110 THRU 4150-EXIT                          
                   UNTIL WS-BM06K110-EOF = 'Y' OR                               
                   K100-KEY-FLD NOT = K110-AIX1.                                
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4150-UPDATE-BM06K110.                                                    
      *-------------------------------------------------------------            
           IF (K110-AIX1 = K100-KEY-FLD) AND                                    
              K110-DTE-ACCEPTANCE NOT = ZEROES AND SPACES AND                   
                                        LOW-VALUES                              
              ADD  1 TO WS-BM06K110-READ                                        
              PERFORM 4200-GET-PIDB-INFO        THRU 4200-EXIT                  
              PERFORM 5000-WRITE-BM06F190       THRU 5000-EXIT                  
              MOVE    K110-REGN-NO              TO   K800-NUM-REGN              
              PERFORM 4400-READ-BP13K800        THRU 4400-EXIT                  
              IF WS-BP13K800-FOUND = 'Y'                                        
                 PERFORM 4900-WRITE-BP13F310       THRU 4900-EXIT               
                 MOVE    K800-NUM-REGN             TO   K840-NUM-REGN           
                 PERFORM 4600-READ-BP13K840        THRU 4600-EXIT               
                 PERFORM 4650-PREPARE-UPD-BP13K840 THRU 4650-EXIT               
                 PERFORM 4450-PREPARE-UPD-BP13K800 THRU 4450-EXIT               
              END-IF                                                            
              PERFORM 4160-PREPARE-UPD-BM06F110 THRU 4160-EXIT                  
              PERFORM 4050-PREPARE-UPD-BM06F100 THRU 4050-EXIT                  
              PERFORM 6000-WRITE-BP13FOUT       THRU 6000-EXIT                  
                                                                                
           END-IF.                                                              
                                                                                
           READ BM06K110 NEXT                                                   
                AT END MOVE 'Y' TO WS-BM06K110-EOF.                             
                                                                                
       4150-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4160-PREPARE-UPD-BM06F110.                                               
      *-------------------------------------------------------------            
           MOVE K110-1ST-OFFER-SELLING-PRICE TO                                 
                K110-CURR-SELLING-PRICE.                                        
           MOVE ZEROES      TO K110-AMT-POD                                     
                               K110-AMT-POF.                                    
           MOVE SPACES      TO K110-CDE-POD-TAG                                 
                               K110-CDE-POF-TAG.                                
           STRING '##' K110-KEY-FLD (6:6)                                       
                  DELIMITED BY SIZE                                             
                  INTO K110-REGN-NO.                                            
           MOVE ZEROS       TO K110-DTE-ACCEPTANCE.                             
           MOVE WS-DATE     TO K110-DTE-LAST-UPD.                               
           MOVE SPACES      TO K110-ALLOC-SCH.                                  
           MOVE SPACES      TO K110-CATEGORY.                                   
           MOVE 'R'         TO K110-ALLOCN-STATUS.                              
           MOVE ZEROES      TO K110-AMT-OCS-DISCOUNT.                           
           MOVE ZEROES      TO K110-AMT-FLAT-DISCOUNT.                          
           MOVE SPACES      TO K110-CDE-OCS-TAG.                                
           MOVE K110-REC TO BM06F110-REC.                                       
           WRITE BM06F110-REC.                                                  
           ADD 1 TO WS-BM06F110-WRITE.                                          
                                                                                
       4160-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4200-GET-PIDB-INFO.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES               TO BP13COMM8-REC.                          
           INITIALIZE                   BP13COMM8-REC.                          
           MOVE K110-SCH-ACC-NO      TO WS-LINK-REC.                            
           CALL 'BP13C913'  USING WS-LINK-REC,                                  
                                  BP13COMM8-REC.                                
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0 AND 100                                  
              DISPLAY 'ERROR IN READING PIDB TABLE  '                           
                      COMM8-CDE-SYSERR                                          
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4400-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
            MOVE 'N' TO WS-BP13K800-FOUND.                                      
            READ BP13K800.                                                      
            ADD 1 TO WS-BP13K800-READ.                                          
            EVALUATE BP13K800-STATUS                                            
               WHEN '00'                                                        
                 MOVE 'Y' TO WS-BP13K800-FOUND                                  
               WHEN '10'                                                        
               WHEN '23'                                                        
                 DISPLAY 'RECORD NOT FOUND IN BP13K800 '                        
                          K800-NUM-REGN                                         
               WHEN OTHER                                                       
                 DISPLAY 'ERROR IN READING BP13K800 '                           
                 MOVE BP13K800-STATUS  TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
            END-EVALUATE.                                                       
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4450-PREPARE-UPD-BP13K800.                                               
      *-------------------------------------------------------------            
           MOVE ZERO             TO K800-AMT-BK-REQ.                            
           MOVE ZERO             TO K800-AMT-BK-FEE-TRF.                        
           MOVE SPACES           TO K800-NUM-REGN-TRF.                          
           MOVE SPACES           TO K800-NUM-CONFIRM-BK-TRF.                    
           MOVE SPACES           TO K800-NUM-CONSENT-BK-TRF.                    
           MOVE SPACES           TO K800-NUM-SCH-ACC.                           
           MOVE ZEROS            TO K800-DTE-EXPIRED-TP.                        
           MOVE ZEROES           TO K800-AMT-PREMIUM.                           
           MOVE 'U'              TO K800-NUM-STATUS.                            
           MOVE WS-DATE          TO K800-NUM-ALLOC-DATE.                        
           MOVE 'RI'             TO K800-NUM-ALLOC-TAG.                         
           MOVE WS-DATE          TO K800-DTE-UPDATE.                            
                                                                                
           IF K800-NUM-HALF-IDP = 'Z'                                           
              MOVE 'S'     TO  K800-NUM-HALF-IDP                                
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-FLAT-TYPE       TO WS-K800-NUM-FLAT-TYPE.              
           IF K800-NUM-ALLO-CAT = 'BTO' OR 'SBF'                                
              MOVE K800-NUM-NEW-TOWN     TO WS-K800-NUM-ZONE                    
           ELSE                                                                 
              MOVE K800-NUM-ZONE         TO WS-K800-NUM-ZONE                    
           END-IF.                                                              
           MOVE K800-NUM-NEW-TOWN        TO WS-K800-NUM-NT.                     
           MOVE K800-NUM-BTO-FLAT-TYPE   TO K800-NUM-FLAT-TYPE.                 
                                                                                
           MOVE BP13K800-MASTER TO BP13F800-REC.                                
           WRITE BP13F800-REC.                                                  
           ADD 1 TO WS-BP13F800-WRITE.                                          
                                                                                
       4450-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4600-READ-BP13K840.                                                      
      *-------------------------------------------------------------            
            MOVE 'N' TO WS-BP13K840-FOUND.                                      
            READ BP13K840.                                                      
            ADD 1 TO WS-BP13K840-READ.                                          
            EVALUATE BP13K840-STATUS                                            
               WHEN '00'                                                        
                 MOVE 'Y' TO WS-BP13K840-FOUND                                  
               WHEN '10'                                                        
               WHEN '23'                                                        
                 DISPLAY 'RECORD NOT FOUND IN BP13K840 '                        
                          K840-NUM-REGN                                         
               WHEN OTHER                                                       
                 DISPLAY 'ERROR IN READING BP13K840 '                           
                 MOVE BP13K840-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
            END-EVALUATE.                                                       
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4650-PREPARE-UPD-BP13K840.                                               
      *-------------------------------------------------------------            
           MOVE K800-NUM-NEW-TOWN    TO K840-NUM-NEW-TOWN.                      
           MOVE K800-NUM-FLAT-TYPE   TO K840-NUM-FLAT-TYPE.                     
           MOVE K800-NUM-ALLO-SCHEME TO K840-NUM-ALLOC-SCH.                     
           MOVE K800-NUM-IDP-SCHEME  TO K840-NUM-IDP-SCH.                       
           MOVE SPACES               TO K840-NUM-SCH-ACC.                       
           MOVE K800-DTE-REGN        TO K840-DTE-REGN.                          
           MOVE 'BP13C22Q'           TO K840-NUM-USERID.                        
                                                                                
           IF K840-DTE-REJECT1 = SPACES OR LOW-VALUES                           
              MOVE WS-DATE              TO K840-DTE-REJECT1                     
              MOVE K110-KEY-FLD         TO K840-NUM-REJECT-FLAT1                
           ELSE                                                                 
              IF K840-DTE-REJECT2 = SPACES OR LOW-VALUES                        
                 MOVE WS-DATE              TO K840-DTE-REJECT2                  
                 MOVE K110-KEY-FLD         TO K840-NUM-REJECT-FLAT2             
              ELSE                                                              
                 IF K840-DTE-REJECT3 = SPACES OR LOW-VALUES                     
                    MOVE WS-DATE              TO K840-DTE-REJECT3               
                    MOVE K110-KEY-FLD         TO K840-NUM-REJECT-FLAT3          
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE BP13K840-REC TO BP13F840-REC.                                   
           WRITE BP13F840-REC.                                                  
           ADD 1 TO WS-BP13F840-WRITE.                                          
                                                                                
       4650-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4900-WRITE-BP13F310.                                                     
      *-------------------------------------------------------------            
           MOVE K800-NUM-REGN           TO F310-REGN-NO.                        
           IF K800-NUM-NEW-TOWN = SPACES OR LOW-VALUES                          
              MOVE K100-NT-CODE         TO F310-CDE-NT                          
           ELSE                                                                 
              MOVE K800-NUM-NEW-TOWN    TO F310-CDE-NT                          
           END-IF.                                                              
                                                                                
           IF K800-NUM-ALLO-CAT = 'BTO' OR 'SBF'                                
              MOVE SPACES               TO F310-NUM-ZONE                        
           ELSE                                                                 
              MOVE K800-NUM-ZONE        TO F310-NUM-ZONE                        
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-FLAT-TYPE      TO F310-CDE-FLAT-TYPE.                  
                                                                                
           MOVE K800-NUM-ALLO-SCHEME    TO F310-NUM-ALLOC-SCH.                  
           MOVE K110-KEY-FLD            TO F310-SCH-ACC.                        
           MOVE 'R1'                    TO F310-CDE-TRANS.                      
           MOVE K110-CDE-POD-TAG        TO F310-CDE-POD-TAG.                    
           MOVE WS-DATE                 TO F310-DTE-UPDATE.                     
           MOVE WS-DATE                 TO F310-DTE-TRANS.                      
           MOVE ZEROES                  TO F310-TME-ACCEPT-REJECT.              
           MOVE COMM8-NUM-BLK           TO F310-NUM-BLK.                        
           MOVE COMM8-NME-STREET        TO F310-NME-STREET.                     
           MOVE COMM8-NUM-LEVEL         TO F310-NUM-LEVEL.                      
           MOVE COMM8-NUM-UNIT-MAIN     TO F310-NUM-MAIN(1:4).                  
           MOVE COMM8-NUM-UNIT-SUB      TO F310-NUM-MAIN(5:1).                  
           MOVE COMM8-NUM-POSTAL-CODE   TO F310-NUM-POSTAL.                     
           MOVE 'BP13C22Q'              TO F310-USERID.                         
           WRITE BP13F310-TRANS-REC.                                            
           ADD 1 TO WS-BP13F310-WRITE.                                          
                                                                                
       4900-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-WRITE-BM06F190.                                                     
      *-------------------------------------------------------------            
           MOVE  K110-REC              TO  F190-REC1.                           
           MOVE  WS-CURRENT-TIME       TO  WS-CURR-TIME1.                       
           ADD   1                     TO  WS-GM-CNT.                           
           MOVE  WS-GM-CNT             TO  WS-GM.                               
           MOVE  WS-CURRENT-DATE       TO  F190-SYS-DATE.                       
           MOVE  WS-CURR-TIME1         TO  F190-SYS-TIME.                       
           MOVE  'B20900'              TO  F190-TRANS-TYPE.                     
           MOVE  'BP13C22Q'            TO  F190-USER-ID.                        
           WRITE F190-REC1.                                                     
           ADD 1 TO WS-BM06F190-WRITE.                                          
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-WRITE-BP13FOUT.                                                     
      *-------------------------------------------------------------            
           MOVE K800-NUM-REGN          TO  FOUT-REGN.                           
           MOVE WS-K800-NUM-FLAT-TYPE  TO  FOUT-FLAT-TYPE.                      
           MOVE WS-K800-NUM-ZONE       TO  FOUT-ZONE.                           
           MOVE WS-K800-NUM-NT         TO  FOUT-NT.                             
           MOVE K800-NUM-NRIC1         TO  FOUT-NRIC1.                          
           MOVE K800-NUM-NRIC2         TO  FOUT-NRIC2.                          
           MOVE K800-NUM-NRIC3         TO  FOUT-NRIC3.                          
           MOVE K800-NUM-NRIC4         TO  FOUT-NRIC4.                          
           MOVE K110-KEY-FLD           TO  FOUT-SCH-ACC.                        
           MOVE COMM8-NUM-BLK          TO  FOUT-BLK.                            
           MOVE COMM8-NME-STREET       TO  FOUT-STREET.                         
           MOVE COMM8-NUM-LEVEL        TO  FOUT-LEVEL.                          
           MOVE COMM8-NUM-UNIT-MAIN    TO  FOUT-MAIN.                           
           MOVE COMM8-NUM-UNIT-SUB     TO  FOUT-SUB.                            
           MOVE K800-DTE-BALLOT        TO  FOUT-DTE-BALLOT.                     
           WRITE BP13FOUT-REC.                                                  
           ADD 1 TO WS-BP13FOUT-WRITE.                                          
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           CLOSE               BP13F205                                         
                               BM06K100                                         
                               BM06K110                                         
                               BP13K800                                         
                               BP13K840                                         
                               BM06F100                                         
                               BM06F110                                         
                               BP13F800                                         
                               BP13F840                                         
                               BP13F310                                         
                               BM06F190                                         
                               BP13FOUT.                                        
                                                                                
           DISPLAY    '**** BP13C22Q ****'.                                     
           DISPLAY    'BP13F205 RECORDS READ    : ' WS-BP13F205-READ.           
           DISPLAY    'BM06K100 RECORDS READ    : ' WS-BM06K100-READ.           
           DISPLAY    'BM06K110 RECORDS READ    : ' WS-BM06K110-READ.           
           DISPLAY    'BP13K270 RECORDS READ    : ' WS-BP13K270-READ.           
           DISPLAY    'BP13K800 RECORDS READ    : ' WS-BP13K800-READ.           
           DISPLAY    'BP13K840 RECORDS READ    : ' WS-BP13K840-READ.           
           DISPLAY    'BM06F100 RECORDS WRITTEN : ' WS-BM06F100-WRITE.          
           DISPLAY    'BM06F110 RECORDS WRITTEN : ' WS-BM06F110-WRITE.          
           DISPLAY    'BM06F190 RECORDS WRITTEN : ' WS-BM06F190-WRITE.          
           DISPLAY    'BP13F800 RECORDS WRITTEN : ' WS-BP13F800-WRITE.          
           DISPLAY    'BP13F840 RECORDS WRITTEN : ' WS-BP13F840-WRITE.          
           DISPLAY    'BP13FOUT RECORDS WRITTEN : ' WS-BP13FOUT-WRITE.          
                                                                                
           IF BM06K100-STATUS NOT = '00' AND '97'                               
              DISPLAY 'ERROR WHILE CLOSING BM06K100 ' BM06K100-STATUS           
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = '00' AND '97'                               
              DISPLAY 'ERROR WHILE CLOSING BM06K110 ' BM06K110-STATUS           
           END-IF.                                                              
                                                                                
           IF BP13K270-STATUS NOT = '00' AND '97'                               
              DISPLAY 'ERROR WHILE CLOSING BP13K270 ' BP13K270-STATUS           
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = '00' AND '97'                               
              DISPLAY 'ERROR WHILE CLOSING BP13K800 ' BP13K800-STATUS           
           END-IF.                                                              
                                                                                
           IF BP13K840-STATUS NOT = '00' AND '97'                               
              DISPLAY 'ERROR WHILE CLOSING BP13K840 ' BP13K840-STATUS           
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
