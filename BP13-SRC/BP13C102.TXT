      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C102.                                                 
      *AUTHOR.        GOH L M.                                                  
      *DATE-WRITTEN.  05/05/89.                                                 
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : THIS PROGRAM VALIDATE TRANSACTIONS PASSSED  *           
      *                 FROM PAYMENT SYSTEM TO PROCESS CASH REFUND, *           
      *                 PAYMENT ADJUSTMENT & FORFEITURE OF FEES,    *           
      *                 CREDIT BALANCE CHECK.                       *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13K800 -- IDP APPL MASTER FILE                      *           
      *   2.  BP13K820 -- SOC OCCUPIER FILE                         *           
      *   3.  AG07F630 -- VOUCHER TRANSACTION FILE                  *           
      *   4.  BP13K880 -- SUB-TRANSFER        FILE                  *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  BP13F122 -- EDITED FIRST TRANSACTION FILE             *           
      *   2.  BP13K140 -- KIV FILE                                  *           
      *   3.  BP13L102 -- CONTROL LISTING                           *           
      *                                                             *           
      * REF      DATE      BY   DESCRIPTIONS                        *           
      * -------  --------  ---- ------------                        *           
      * SOC-PH8B 06/07/93  KPS  UPD BP13K800 INSTEAD OF RFF AND     *           
      *                         EXPAND REGN AND NRIC NO             *           
      * C931147  13/01/94  LJL  TO CONVERT FROM F01F032, VOUCHER    *           
      *                         SYSTEM TO AG07F630 ,PAYMENT SYSTEM  *           
      * S940505  13/05/94  LJL  TO RECTIFY THE ERROR FOR SOC-DEBTOR *           
      * SOCPH8F  23/11/95  EG1  TO ADD THE OPTION FOR F630-NUM-TRAN *           
      * N960002                 S-ID = 'SUPS'.                      *           
      * BP130103 23/03/96  EG1  TO CATER FOR SUB-TRANSFER ENHANCE.  *           
      * BP130289 23/04/97  SGK  TO CATER FOR SUB-TRANSFER PRESENCE. *           
      * BP130307 26/05/97  RHB  TO CATER FOR REGN-AMT-REFUND        *           
      * BP130327 20/08/97  RHB  CATER FOR REGN DEPOSIT REFUND/      *           
      *                         FORFEITURE (F630-NUM-TRANS-ID =DPPS *           
      * BP130548 27/08/98  SGK  TO CATER FOR CPF-NO-EXPANSION.      *           
      * BP130418 21/08/98  NOR  MAKE PRGM Y2K COMPLAINT             *           
      * BP132309 01/04/03  MCC  TO CATER FOR SOLICITOR BILL (BNPS)  *           
      * BP132819 06/04/06  LSB  CATER DUPLICATE K140 K880-NOTFD     *           
      * BP133638 18/06/09  JB8  CATER BATCH-TYPE 'B'                *           
      * BP137707 19/08/19  DN8  CATER FOR CANCEL CASES              *           
      * BP137707 22/08/19  DN8  TO CHECK SOC-CASH/SOC-CPF1/2/3/4 > 0*           
      *                         FOR CANCEL CASES                    *           
      * =========================================================== *           
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
      *------------------------*                                                
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K800-NUM-REGN                                   
                FILE STATUS  IS BP13K800-STATUS.                                
                                                                                
           SELECT BP13K885 ASSIGN TO BP13K885                                   
                ACCESS MODE  IS DYNAMIC                                         
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K885-KEY-FLD                                    
                FILE STATUS  IS BP13K885-STATUS.                                
                                                                                
           SELECT BP13K880 ASSIGN TO BP13K880                                   
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K880-KEY-FLD                                    
                FILE STATUS  IS K880-STATUS.                                    
                                                                                
           SELECT AG07F630 ASSIGN TO AG07F630.                                  
                                                                                
           SELECT BP13F122 ASSIGN TO BP13F122.                                  
                                                                                
           SELECT BP13K140 ASSIGN TO BP13K140                                   
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K140-KEY-FLD                                    
                FILE STATUS  IS BP13K140-STATUS.                                
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K820-KEY-FLD                                    
                FILE STATUS  IS BP13K820-STATUS.                                
                                                                                
           SELECT BP13L102 ASSIGN TO BP13L102.                                  
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD BP13K800                                                              
            BLOCK CONTAINS   0  RECORDS                                         
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K800.                                                           
                                                                                
       FD BP13K885                                                              
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K885.                                                           
                                                                                
       FD BP13K820                                                              
            BLOCK CONTAINS   0  RECORDS                                         
            RECORD CONTAINS 400  CHARACTERS                                     
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K820.                                                           
                                                                                
       FD   AG07F630                                                    00150000
            BLOCK CONTAINS   0  RECORDS                                         
            RECORD CONTAINS 130  CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY AG07F630.                                                   00150000
                                                                                
       FD BP13F122                                                              
            BLOCK CONTAINS   0  RECORDS                                         
            RECORD CONTAINS  150  CHARACTERS                                    
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F122.                                                   00150000
                                                                                
       FD BP13K140                                                              
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K140.                                                           
                                                                                
       FD  BP13K880                                                             
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13K880.                                                           
                                                                                
       FD  BP13L102                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-REC                 PIC X(132).                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-TOTAL-AMT            PIC S9(7)V99 VALUE +0.                       
       01  BP13K800-STATUS         PIC 99      VALUE ZEROS.                     
       01  BP13K885-STATUS         PIC 99      VALUE ZEROS.                     
       01  BP13K820-STATUS         PIC 99      VALUE ZEROS.                     
       01  K880-STATUS             PIC 99      VALUE ZEROS.                     
       01  BP13K140-STATUS         PIC 9(2)    VALUE ZEROES.                    
       01  WS-K140-STATUS.                                                      
           05 WS-K140-STATUS1      PIC 9.                                       
           05 WS-K140-STATUS2      PIC 9.                                       
       01  CNT-F630-READ           PIC 9(5)    VALUE ZEROS.                     
       01  CNT-K800-READ           PIC 9(5)    VALUE ZEROS.                     
       01  CNT-K885-READ           PIC 9(5)    VALUE ZEROS.                     
       01  CNT-F122-WRITE          PIC 9(5)    VALUE ZEROS.                     
       01  CNT-K140-WRITE          PIC 9(5)    VALUE ZEROS.                     
       01  CNT-LINE-ON-PAGE        PIC 99      VALUE 66.                        
       01  CNT-PAGE                PIC 9(3)    VALUE ZERO.                      
       01  CNT-KIV-CASES           PIC 9(4)    VALUE ZERO.                      
       01  WS-CDE-ERROR            PIC X       VALUE SPACES.                    
       01  WS-CPF-APPLT            PIC X       VALUE SPACES.                    
       01  WS-CPF1                 PIC X(9)    VALUE SPACES.                    
       01  WS-CPF2                 PIC X(9)    VALUE SPACES.                    
       01  WS-CPF3                 PIC X(9)    VALUE SPACES.                    
       01  WS-CPF4                 PIC X(9)    VALUE SPACES.                    
       01  WS-DIFF                 PIC S9(7)V9(2)  VALUE +0.                    
       01  WS-NRIC1                PIC X(9)    VALUE SPACES.                    
       01  WS-NRIC2                PIC X(9)    VALUE SPACES.                    
       01  WS-NRIC3                PIC X(9)    VALUE SPACES.                    
       01  WS-NRIC4                PIC X(9)    VALUE SPACES.                    
                                                                                
       01  WS-SCHEDULE-NO.                                                      
           05  WS-SCHEDULE-NUM     PIC X(8).                                    
           05  WS-DOC-NO           PIC X(6).                                    
                                                                                
       01  WS-F630-VR-NO.                                                       
           05  WS-F630-VR-NUM1     PIC X(2).                                    
           05  WS-F630-VR-NUM2     PIC X(4).                                    
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F630-EOF         PIC X       VALUE 'N'.                       
           05  WS-K885-EOF         PIC X       VALUE 'N'.                       
       01  FOUND-FLAG.                                                          
           05  WS-REGN-FOUND       PIC X        VALUE 'Y'.                      
           05  WS-AMT-TALLY        PIC X        VALUE 'Y'.                      
           05  WS-AMT-REFUND       PIC X        VALUE 'Y'.                      
           05  WS-CPF-FOUND        PIC X        VALUE 'Y'.                      
           05  WS-K880-FOUND       PIC X        VALUE 'Y'.                      
           05  WS-AMT-CD-FLAG      PIC X        VALUE 'Y'.                      
                                                                                
       01  WS-DATE                 PIC X(8)     VALUE SPACES.                   
                                                                                
       01  L102-PR-HEAD-01.                                                     
           05  FILLER              PIC X(8)      VALUE 'BP13L102'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE 'DATE: '.                
           05  13S1-DATE           PIC X(10).                                   
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE 'PAGE :'.                
           05  13S1-PAGE           PIC ZZ9 .                                    
                                                                                
       01  L102-PR-HEAD-02.                                                     
           05  FILLER              PIC X(40)     VALUE SPACES .                 
           05  FILLER              PIC X(40)     VALUE                          
               '  ERROR LIST OF TRANSACTIONS PASSED FROM'.                      
           05  FILLER              PIC X(13)     VALUE                          
                                                 ' VOUCHER FILE'.               
           05  FILLER              PIC X(40)     VALUE SPACES.                  
                                                                                
       01  L102-PR-HEAD-03.                                                     
           05  FILLER              PIC X(25)                                    
                  VALUE '    S/NO   REGN NO       '.                            
           05  FILLER              PIC X(38)                                    
                  VALUE 'AMOUNT       TRANS-TY       RECEIPT NO'.               
           05  FILLER              PIC X(31)                                    
                  VALUE '       DATE POST     PAYMT TYPE'.                      
           05  FILLER              PIC X(16)                                    
                  VALUE '          REMARK'.                                     
           05  FILLER              PIC X(22)     VALUE SPACES.                  
                                                                                
       01  L102-PR-DETAILS.                                                     
           05  FILLER              PIC X(01)     VALUE SPACES .                 
           05  13S1-SNO            PIC ZZZZZ9 .                                 
           05  FILLER              PIC X(04)     VALUE SPACES .                 
           05  13S1-REG-NO         PIC X(08).                                   
           05  FILLER              PIC X(04)     VALUE SPACES .                 
           05  13S1-AMT            PIC $$$$$$$9.99DB.                           
           05  FILLER              PIC X(05)     VALUE SPACES .                 
           05  13S1-TRANS-TY       PIC X(02).                                   
           05  FILLER              PIC X(10)     VALUE SPACES .                 
           05  13S1-NUM-RECEIPT    PIC X(14) .                                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  13S1-DTE-POST       PIC X(08) .                                  
           05  FILLER              PIC X(09)     VALUE SPACES .                 
           05  13S1-PAYMENT-TYPE   PIC X(03).                                   
           05  FILLER              PIC X(06)     VALUE SPACES .                 
           05  13S1-REMARK         PIC X(36).                                   
                                                                                
       01  L102-CONTROL-LINE.                                                   
           05  FILLER              PIC X(20)     VALUE  SPACES.                 
           05  CNTL-ITEM           PIC X(30)     VALUE  SPACES.                 
           05  CNTL-CNT            PIC ZZZZ9 .                                  
           05  FILLER              PIC X(58)     VALUE  SPACES.                 
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
                                                                                
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-FILES       THRU 100-EXIT.                          
           PERFORM 200-READ-AG07F630    THRU 200-EXIT.                          
           PERFORM 300-VALIDATE-VOUCHER THRU 300-EXIT                           
                   UNTIL WS-F630-EOF = 'Y'.                                     
           PERFORM 999-CLOSE-FILES      THRU 999-EXIT.                          
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN   INPUT  BP13K800                                               
                         BP13K885                                               
                         BP13K820                                               
                         BP13K880                                               
                         AG07F630                                               
                  I-O    BP13K140                                               
                  OUTPUT BP13F122                                               
                         BP13L102.                                              
                                                                                
           IF BP13K800-STATUS NOT = 0   AND  97                                 
               DISPLAY 'ERROR OPENING - BP13K800 : ' BP13K800-STATUS            
               MOVE BP13K800-STATUS TO RETURN-CODE                              
               PERFORM 999-CLOSE-FILES THRU 999-EXIT.                           
                                                                                
           IF BP13K885-STATUS NOT = 0   AND  97                                 
               DISPLAY 'ERROR OPENING - BP13K885 : ' BP13K885-STATUS            
               MOVE BP13K885-STATUS TO RETURN-CODE                              
               PERFORM 999-CLOSE-FILES THRU 999-EXIT.                           
                                                                                
           IF BP13K820-STATUS NOT = 0   AND  97                                 
               DISPLAY 'ERROR OPENING - BP13K820 : ' BP13K820-STATUS            
               MOVE BP13K820-STATUS TO RETURN-CODE                              
               PERFORM 999-CLOSE-FILES THRU 999-EXIT.                           
                                                                                
           IF     K880-STATUS NOT = 0   AND  97                                 
               DISPLAY 'ERROR OPENING - BP13K880 : '     K880-STATUS            
               MOVE     K880-STATUS TO RETURN-CODE                              
               PERFORM 999-CLOSE-FILES THRU 999-EXIT.                           
                                                                                
           IF BP13K140-STATUS NOT = ZEROS   AND  97                             
               DISPLAY 'ERROR OPENING - BP13K140 : ' BP13K140-STATUS            
               MOVE BP13K140-STATUS TO RETURN-CODE                              
               PERFORM 999-CLOSE-FILES THRU 999-EXIT.                           
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
              DELIMITED BY SIZE INTO 13S1-DATE.                                 
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       200-READ-AG07F630.                                                       
      *-------------------------------------------------------------            
           READ AG07F630                                                        
                AT END                                                          
                MOVE 'Y' TO  WS-F630-EOF                                        
                GO TO 200-EXIT.                                                 
           ADD 1 TO CNT-F630-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      *              VALIDATE TRANSACTION  CASES               *                
      *                                                        *                
      * IF FOUND, WRITE VOUCHER TARNS ELSE WRITE KIV FILE      *                
      **********************************************************                
      *-------------------------------------------------------------            
       300-VALIDATE-VOUCHER.                                                    
      *-------------------------------------------------------------            
           MOVE 'Y'   TO WS-REGN-FOUND  WS-CPF-APPLT WS-AMT-REFUND              
                         WS-CPF-FOUND   WS-AMT-TALLY WS-K880-FOUND              
                         WS-AMT-CD-FLAG.                                        
           MOVE ZEROS TO WS-DIFF  WS-TOTAL-AMT.                                 
                                                                                
           PERFORM 310-VALIDATE-K800      THRU 310-EXIT.                        
                                                                                
           IF (WS-REGN-FOUND = 'N') OR                                          
              (WS-CPF-FOUND  = 'N') OR                                          
              (WS-AMT-REFUND = 'N') OR                                          
              (WS-K880-FOUND = 'N') OR                                          
              (WS-AMT-TALLY  = 'N') OR                                          
              (WS-AMT-CD-FLAG = 'N')                                            
               PERFORM 360-WRITE-BP13K140 THRU 360-EXIT                         
               PERFORM 800-ERROR-LIST     THRU 800-EXIT                         
           ELSE                                                                 
               PERFORM 350-WRITE-BP13F122 THRU 350-EXIT.                        
                                                                                
           PERFORM 200-READ-AG07F630 THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      *            ACCESSING RFF MASTER                        *                
      *  F032-VR-TYPE = '3'        (REFUND CASE) -FOR CASH ONLY*                
      *  F032-VR-TYPE = '8 '       (ADJUSTMENT) -CHECK THE AMT *                
      *  CANNOT ADJUST TO NEGATIVE AMT.                        *                
      **********************************************************                
      *-------------------------------------------------------------            
       310-VALIDATE-K800.                                                       
      *-------------------------------------------------------------            
           MOVE F630-NUM-REG TO K800-NUM-REGN.                                  
                                                                                
           READ BP13K800.                                                       
           IF BP13K800-STATUS = 0                                               
              ADD 1 TO CNT-K800-READ                                            
           ELSE                                                                 
             IF BP13K800-STATUS = 23                                            
               MOVE 'N' TO WS-REGN-FOUND                                        
             ELSE                                                               
               DISPLAY 'ERROR READING - BP13K800 : ' BP13K800-STATUS    S       
               MOVE  BP13K800-STATUS TO RETURN-CODE                             
               PERFORM 999-CLOSE-FILES THRU 999-EXIT.                           
                                                                                
           IF WS-REGN-FOUND = 'Y'                                               
              IF F630-NUM-TRANS-ID = 'SUPS'                                     
                 PERFORM 700-SEARCH-K880 THRU 700-EXIT                          
                 IF K880-STATUS NOT = 0                                         
                    MOVE 'N'     TO WS-K880-FOUND                               
                    GO TO 310-EXIT                                              
                 END-IF                                                         
                 MOVE 'N'              TO WS-CPF-FOUND                          
                 MOVE SPACES           TO WS-CPF-APPLT                          
                 IF F630-NUM-CPF = LOW-VALUES                                   
                    MOVE SPACES        TO F630-NUM-CPF                          
                 END-IF                                                         
                 PERFORM 400-SEARCH-K885  THRU 400-EXIT                         
              ELSE                                                              
                 PERFORM 312-CPF-NO-CHECK THRU 312-EXIT                         
              END-IF                                                            
                                                                                
              IF WS-CPF-FOUND = 'N'                                             
                 NEXT SENTENCE                                                  
              ELSE                                                              
                 PERFORM 315-CHECK-NEGATIVE-AMT THRU 315-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      * CHECK THAT VR TRAN REC CPF NO IS FOUND IN RFF MASTER   *                
      **********************************************************                
      *-------------------------------------------------------------            
       312-CPF-NO-CHECK.                                                        
      *-------------------------------------------------------------            
           MOVE K800-NUM-NRIC1 TO K820-NUM-NRIC                                 
                                  WS-NRIC1.                                     
                                                                                
           PERFORM 313-READ-BP13K820 THRU 313-EXIT.                             
           MOVE K820-NUM-CPF TO WS-CPF1.                                        
                                                                                
           IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-NRIC2 TO K820-NUM-NRIC                              
                                     WS-NRIC2                                   
              PERFORM 313-READ-BP13K820 THRU 313-EXIT                           
              MOVE K820-NUM-CPF TO WS-CPF2.                                     
                                                                                
           IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-NRIC3 TO K820-NUM-NRIC                              
                                     WS-NRIC3                                   
              PERFORM 313-READ-BP13K820 THRU 313-EXIT                           
              MOVE K820-NUM-CPF TO WS-CPF3.                                     
                                                                                
           IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-NRIC4 TO K820-NUM-NRIC                              
                                     WS-NRIC4                                   
              PERFORM 313-READ-BP13K820 THRU 313-EXIT                           
              MOVE K820-NUM-CPF TO WS-CPF4.                                     
                                                                                
           IF F630-NUM-CPF = SPACES OR LOW-VALUES                               
              MOVE 'Y'     TO WS-CPF-FOUND                                      
              MOVE SPACES  TO WS-CPF-APPLT                                      
              GO           TO 312-EXIT.                                         
                                                                                
           IF F630-NUM-CPF = WS-CPF1                                            
              MOVE '1' TO WS-CPF-APPLT                                          
           ELSE                                                                 
           IF F630-NUM-CPF = WS-CPF2                                            
              MOVE '2' TO WS-CPF-APPLT                                          
           ELSE                                                                 
           IF F630-NUM-CPF = WS-CPF3                                            
              MOVE '3' TO WS-CPF-APPLT                                          
           ELSE                                                                 
           IF F630-NUM-CPF = WS-CPF4                                            
              MOVE '4' TO WS-CPF-APPLT                                          
           ELSE                                                                 
              MOVE 'N' TO WS-CPF-FOUND.                                         
                                                                                
       312-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       313-READ-BP13K820.                                                       
      *-------------------------------------------------------------            
           MOVE K800-NUM-REGN  TO K820-NUM-REGN.                                
           READ BP13K820.                                                       
                                                                                
           IF BP13K820-STATUS = '00' OR '02'                                    
              NEXT SENTENCE                                                     
           ELSE                                                                 
           IF BP13K820-STATUS = '23'                                            
              DISPLAY 'BP13K820 RECORD NOT FOUND '                              
              DISPLAY 'REG NO : ' K800-NUM-REGN                                 
              MOVE SPACES TO BP13K820-REC                                       
           ELSE                                                                 
              DISPLAY 'ERROR READING - BP13K820 : ' BP13K820-STATUS             
              MOVE  BP13K820-STATUS TO RETURN-CODE                              
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
       313-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      * CHECK THAT VR TRAN CANNOT ADJUST AMT IN RFF TO LESS    *                
      * THAN ZERO AMT                                          *                
      **********************************************************                
      *-------------------------------------------------------------            
       315-CHECK-NEGATIVE-AMT.                                                  
      *-------------------------------------------------------------            
           IF K800-AMT-BK-PAID NOT NUMERIC                                      
               MOVE 0 TO K800-AMT-BK-PAID.                                      
           IF K800-AMT-CPF1-PAID NOT NUMERIC                                    
               MOVE 0 TO K800-AMT-CPF1-PAID.                                    
           IF K800-AMT-CPF2-PAID NOT NUMERIC                                    
               MOVE 0 TO K800-AMT-CPF1-PAID.                                    
           IF K800-AMT-CPF3-PAID NOT NUMERIC                                    
               MOVE 0 TO K800-AMT-CPF1-PAID.                                    
           IF K800-AMT-CPF4-PAID NOT NUMERIC                                    
               MOVE 0 TO K800-AMT-CPF1-PAID.                                    
           IF K800-AMT-SOC-CASH NOT NUMERIC                                     
               MOVE 0 TO K800-AMT-SOC-CASH.                                     
           IF K800-AMT-SOC-SERCNTR-PAID NOT NUMERIC                             
               MOVE 0 TO K800-AMT-SOC-SERCNTR-PAID.                             
           IF K800-AMT-REGN-PAID NOT NUMERIC                                    
               MOVE 0 TO K800-AMT-REGN-PAID.                                    
           IF K800-AMT-REGN-DEPOSIT-CASH NOT NUMERIC                            
               MOVE 0 TO K800-AMT-REGN-DEPOSIT-CASH.                            
           IF K800-AMT-REGN-DEPOSIT-CPF1 NOT NUMERIC                            
               MOVE 0 TO K800-AMT-REGN-DEPOSIT-CPF1.                            
           IF K800-AMT-REGN-DEPOSIT-CPF2 NOT NUMERIC                            
               MOVE 0 TO K800-AMT-REGN-DEPOSIT-CPF2.                            
           IF K800-AMT-REGN-DEPOSIT-CPF3 NOT NUMERIC                            
               MOVE 0 TO K800-AMT-REGN-DEPOSIT-CPF3.                            
           IF K800-AMT-REGN-DEPOSIT-CPF4 NOT NUMERIC                            
               MOVE 0 TO K800-AMT-REGN-DEPOSIT-CPF4.                            
           IF K800-AMT-SOLICIT-CASH NOT NUMERIC                                 
               MOVE 0 TO K800-AMT-SOLICIT-CASH.                                 
           IF K800-AMT-SOLICIT-SERCNTR NOT NUMERIC                              
               MOVE 0 TO K800-AMT-SOLICIT-SERCNTR.                              
           IF K800-AMT-SOLICIT-CPF1 NOT NUMERIC                                 
               MOVE 0 TO K800-AMT-SOLICIT-CPF1.                                 
           IF K800-AMT-SOLICIT-CPF2 NOT NUMERIC                                 
               MOVE 0 TO K800-AMT-SOLICIT-CPF2.                                 
           IF K800-AMT-SOLICIT-CPF3 NOT NUMERIC                                 
               MOVE 0 TO K800-AMT-SOLICIT-CPF3.                                 
           IF K800-AMT-SOLICIT-CPF4 NOT NUMERIC                                 
               MOVE 0 TO K800-AMT-SOLICIT-CPF4.                                 
                                                                                
      *-----------------------------------------------------------*             
      * THESE TRANSACTIONS ARE FOR CREDIT/DEBIT OF SOC AMT                      
      * BKPS/CDPS/SUPS/DPPS/BNPS - DEBIT                                        
      * SDPS                     - CREDIT                                       
      * THE F122 REC CREATED WILL BE USED IN BP13C104 FOR UPDATING              
      * OF SOC AMOUNT.                                                          
      *-----------------------------------------------------------*             
           IF F630-NUM-TRANS-ID = 'BKPS' OR 'CDPS' OR 'SDPS' OR                 
                                  'SUPS' OR 'DPPS' OR 'BNPS'                    
              EVALUATE F630-NUM-TRANS-DR-CR                                     
              WHEN  'C'                                                         
                    COMPUTE WS-TOTAL-AMT = F630-AMT-TRANS                       
              WHEN  'D'                                                         
                    COMPUTE WS-TOTAL-AMT = 0 - F630-AMT-TRANS                   
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           DISPLAY 'WS-TOTAL-AMT = ' WS-TOTAL-AMT.                              
           IF F630-NUM-TRANS-ID = 'RFPS'                                        
              EVALUATE F630-NUM-TRANS-DR-CR                                     
              WHEN  'C'                                                         
                    COMPUTE WS-TOTAL-AMT = F630-AMT-TRANS +                     
                                           F630-AMT-TRANS-GST                   
              WHEN  'D'                                                         
                    COMPUTE WS-TOTAL-AMT = 0 - F630-AMT-TRANS -                 
                                           F630-AMT-TRANS-GST                   
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           EVALUATE F630-NUM-TRANS-ID                                           
           WHEN     'RFPS'                                                      
              COMPUTE WS-DIFF = K800-AMT-REGN-PAID + WS-TOTAL-AMT               
              PERFORM 317-ERROR-NEGATIVE-AMT THRU 317-EXIT                      
                                                                                
           WHEN     'BKPS'                                                      
              COMPUTE WS-DIFF = K800-AMT-BK-PAID + WS-TOTAL-AMT                 
              PERFORM 317-ERROR-NEGATIVE-AMT THRU 317-EXIT                      
                                                                                
           WHEN     'CDPS'                                                      
           WHEN     'SDPS'                                                      
                                                                                
              IF F630-NUM-TRANS-ID = 'CDPS' AND K800-NUM-STATUS = 'C'           
                 AND (K800-NUM-SCH-ACC = SPACES OR LOW-VALUES )                 
                IF  F630-AMT-TRANS  = K800-AMT-CD                               
                    NEXT SENTENCE                                               
                ELSE                                                            
                 IF K800-AMT-CD > 0 AND (K800-AMT-SOC-CASH > 0 OR               
                    K800-AMT-CPF1-PAID > 0 OR K800-AMT-CPF2-PAID > 0 OR         
                    K800-AMT-CPF3-PAID > 0 OR K800-AMT-CPF4-PAID > 0 )          
                    NEXT SENTENCE                                               
                  ELSE                                                          
                    MOVE 'N'  TO WS-AMT-CD-FLAG                                 
                 END-IF                                                         
                END-IF                                                          
              END-IF                                                            
                                                                                
               EVALUATE WS-CPF-APPLT                                            
                  WHEN '1'                                                      
                COMPUTE WS-DIFF = K800-AMT-CPF1-PAID + WS-TOTAL-AMT             
                PERFORM 317-ERROR-NEGATIVE-AMT THRU 317-EXIT                    
                  WHEN '2'                                                      
                COMPUTE WS-DIFF = K800-AMT-CPF2-PAID + WS-TOTAL-AMT             
                PERFORM 317-ERROR-NEGATIVE-AMT THRU 317-EXIT                    
                  WHEN '3'                                                      
                COMPUTE WS-DIFF = K800-AMT-CPF3-PAID + WS-TOTAL-AMT             
                PERFORM 317-ERROR-NEGATIVE-AMT THRU 317-EXIT                    
                  WHEN '4'                                                      
                COMPUTE WS-DIFF = K800-AMT-CPF4-PAID + WS-TOTAL-AMT             
                PERFORM 317-ERROR-NEGATIVE-AMT THRU 317-EXIT                    
                  WHEN ' '                                                      
                COMPUTE WS-DIFF = K800-AMT-SOC-CASH + WS-TOTAL-AMT              
      *         COMPUTE WS-DIFF = K800-AMT-SOC-SERCNTR-PAID +                   
      *                                               WS-TOTAL-AMT              
                PERFORM 317-ERROR-NEGATIVE-AMT THRU 317-EXIT                    
               END-EVALUATE                                                     
                                                                                
           WHEN     'DPPS'                                                      
               EVALUATE WS-CPF-APPLT                                            
                  WHEN '1'                                                      
                COMPUTE WS-DIFF = K800-AMT-REGN-DEPOSIT-CPF1                    
                                  + WS-TOTAL-AMT                                
                PERFORM 317-ERROR-NEGATIVE-AMT THRU 317-EXIT                    
                  WHEN '2'                                                      
                COMPUTE WS-DIFF = K800-AMT-REGN-DEPOSIT-CPF2                    
                                  + WS-TOTAL-AMT                                
                PERFORM 317-ERROR-NEGATIVE-AMT THRU 317-EXIT                    
                  WHEN '3'                                                      
                COMPUTE WS-DIFF = K800-AMT-REGN-DEPOSIT-CPF3                    
                                  + WS-TOTAL-AMT                                
                PERFORM 317-ERROR-NEGATIVE-AMT THRU 317-EXIT                    
                  WHEN '4'                                                      
                COMPUTE WS-DIFF = K800-AMT-REGN-DEPOSIT-CPF4                    
                                  + WS-TOTAL-AMT                                
                PERFORM 317-ERROR-NEGATIVE-AMT THRU 317-EXIT                    
                  WHEN ' '                                                      
                COMPUTE WS-DIFF = K800-AMT-REGN-DEPOSIT-CASH                    
                                  + WS-TOTAL-AMT                                
                PERFORM 317-ERROR-NEGATIVE-AMT THRU 317-EXIT                    
               END-EVALUATE                                                     
                                                                                
           WHEN     'SUPS'                                                      
               IF WS-CPF-APPLT = 'E'                                            
                  COMPUTE WS-DIFF = K885-AMT-SUSPENSE + WS-TOTAL-AMT            
                  PERFORM 317-ERROR-NEGATIVE-AMT THRU 317-EXIT                  
               END-IF                                                           
                                                                                
      *----------------------------------------------------*                    
      * PAYMENT SYSTEM WILL ENSURE PAYMENT VOUCHER RAISED                       
      * EQUAL TO SUM OF ALL 'SOL' TRANSACTIONS IN BP13K130                      
      *----------------------------------------------------*                    
           WHEN     'BNPS'                                                      
              COMPUTE WS-DIFF = K800-AMT-SOLICIT-CASH    +                      
                                K800-AMT-SOLICIT-SERCNTR +                      
                                K800-AMT-SOLICIT-CPF1    +                      
                                K800-AMT-SOLICIT-CPF2    +                      
                                K800-AMT-SOLICIT-CPF3    +                      
                                K800-AMT-SOLICIT-CPF4    +                      
                                WS-TOTAL-AMT                                    
              DISPLAY 'K800-AMT-SOLICIT-CASH = ' K800-AMT-SOLICIT-CASH          
              DISPLAY 'K800-AMT-SOLICIT-CPF1 = ' K800-AMT-SOLICIT-CPF1          
              DISPLAY 'WS-DIFF = ' WS-DIFF                                      
              PERFORM 317-ERROR-NEGATIVE-AMT THRU 317-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       315-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       317-ERROR-NEGATIVE-AMT.                                                  
      *-------------------------------------------------------------            
              IF WS-DIFF < ZEROS                                                
                 EVALUATE F630-NUM-TRANS-ID                                     
                   WHEN     'RFPS'                                              
                     MOVE 'N' TO WS-AMT-REFUND                                  
                   WHEN    OTHER                                                
                     MOVE 'N' TO WS-AMT-TALLY.                                  
                                                                                
       317-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      *                                                        *                
      *        WRITTING UPDATED RECORDS TO BP13F122            *                
      *                                                        *                
      * SCHEDULE  AMT  | TRANS-TY - DESCRIPTION                *                
      * ALPHA     TYPE |                                       *                
      * ---------------------------------------                *                
      *   LA (CASH)1   |  15     - CASH REFUND                 *                
      *   LA (CPF) 2   |  13     - CPF  REFUND                 *                
      *   S  (CASH)1   |  16     - CASH ADJUSTMENT/ FORFEITURE *                
      *   S  (CPF) 2   |  14     - CPF  ADJUSTMENT/ FORFEITURE *                
      *                                                        *                
      * VOUCHER TYPE | PAYMENT TYPE                            *                
      * ---------------------------                            *                
      *   17         |   REGN-FEE                              *                
      *   18         |   BOOKING FEE                           *                
      *   19         |   COMMITMENT DEPOSIT PREVIOUS PAID      *                
      *   20         |   SOC DEBTOR                            *                
      **********************************************************                
      *-------------------------------------------------------------            
       350-WRITE-BP13F122.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES TO  BP13F122-ADJTRAN.                                    
                                                                                
           IF F630-NUM-TRANS-ID = 'BKPS'                                        
                MOVE 'BK ' TO F122-CDE-PAYMENT-TYPE.                            
           IF F630-NUM-TRANS-ID = 'CDPS'                                        
                MOVE 'CD ' TO F122-CDE-PAYMENT-TYPE.                            
           IF F630-NUM-TRANS-ID = 'SDPS'                                        
                MOVE 'SOC' TO F122-CDE-PAYMENT-TYPE.                            
           IF F630-NUM-TRANS-ID = 'SUPS'                                        
                MOVE 'SUS' TO F122-CDE-PAYMENT-TYPE.                            
           IF F630-NUM-TRANS-ID = 'RFPS'                                        
                MOVE 'REG' TO F122-CDE-PAYMENT-TYPE.                            
           IF F630-NUM-TRANS-ID = 'DPPS'                                        
                MOVE 'RDE' TO F122-CDE-PAYMENT-TYPE.                            
           IF F630-NUM-TRANS-ID = 'BNPS'                                        
                MOVE 'SOL' TO F122-CDE-PAYMENT-TYPE.                            
                                                                                
      *--------------------------------------------------------*                
      *   LA (CASH)1   |  15     - CASH REFUND                 *                
      *   LA (CPF) 2   |  13     - CPF  REFUND                 *                
      *   S  (CASH)1   |  16     - CASH ADJUSTMENT/ FORFEITURE *                
      *   S  (CPF) 2   |  14     - CPF  ADJUSTMENT/ FORFEITURE *                
      *                                                        *                
      *   NOTE : NO CPF NUMBER MEANS CASH                      *                
      *--------------------------------------------------------*                
                                                                                
           IF F630-NUM-CPF  = SPACES OR LOW-VALUES                              
                IF F630-NUM-BATCH-TYPE = 'P' OR 'B'                             
                    MOVE '15' TO F122-CDE-TRANS-TYPE.                           
                                                                                
           IF F630-NUM-CPF  = SPACES OR LOW-VALUES                              
                IF F630-NUM-BATCH-TYPE = 'J'                                    
                    MOVE '16' TO F122-CDE-TRANS-TYPE.                           
                                                                                
           IF F630-NUM-CPF NOT = SPACES AND LOW-VALUES                          
                IF F630-NUM-BATCH-TYPE = 'P' OR 'B'                             
                    MOVE '13' TO F122-CDE-TRANS-TYPE.                           
                                                                                
           IF F630-NUM-CPF NOT = SPACES AND LOW-VALUES                          
                IF F630-NUM-BATCH-TYPE = 'J'                                    
                    MOVE '14' TO F122-CDE-TRANS-TYPE.                           
                                                                                
           IF F630-NUM-TRANS-ID = 'BKPS' OR 'CDPS' OR 'SDPS' OR                 
                                  'SUPS' OR 'DPPS' OR 'BNPS'                    
              EVALUATE F630-NUM-TRANS-DR-CR                                     
              WHEN  'C'                                                         
                    COMPUTE WS-TOTAL-AMT = F630-AMT-TRANS                       
              WHEN  'D'                                                         
                    COMPUTE WS-TOTAL-AMT = 0 - F630-AMT-TRANS                   
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           IF F630-NUM-TRANS-ID = 'RFPS'                                        
              EVALUATE F630-NUM-TRANS-DR-CR                                     
              WHEN  'C'                                                         
                    COMPUTE WS-TOTAL-AMT = F630-AMT-TRANS +                     
                                           F630-AMT-TRANS-GST                   
              WHEN  'D'                                                         
                    COMPUTE WS-TOTAL-AMT = 0 - F630-AMT-TRANS -                 
                                           F630-AMT-TRANS-GST                   
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
            COMPUTE  F122-AMT-RECEIPT = WS-TOTAL-AMT.                           
            MOVE  F630-NUM-REGN      TO F122-NUM-REGN.                          
                                                                                
            IF F630-NUM-TRANS-ID = 'SUPS'                                       
               MOVE  K885-NUM-NRIC      TO F122-NUM-NRIC                        
            ELSE                                                                
               IF  WS-CPF-APPLT  = '1'                                          
                   MOVE  K800-NUM-NRIC1 TO F122-NUM-NRIC                        
               ELSE                                                             
               IF  WS-CPF-APPLT  = '2'                                          
                   MOVE  K800-NUM-NRIC2 TO F122-NUM-NRIC                        
               ELSE                                                             
               IF  WS-CPF-APPLT  = '3'                                          
                   MOVE  K800-NUM-NRIC3 TO F122-NUM-NRIC                        
               ELSE                                                             
               IF  WS-CPF-APPLT  = '4'                                          
                   MOVE  K800-NUM-NRIC4 TO F122-NUM-NRIC.                       
                                                                                
            MOVE  F630-NUM-CPF       TO F122-NUM-CPF-APPL.                      
            MOVE  F630-NUM-BATCH     TO WS-SCHEDULE-NUM.                        
            MOVE  F630-NUM-VR-SERIAL TO WS-DOC-NO.                              
            MOVE  WS-SCHEDULE-NO     TO F122-NUM-RECPT-JRNO.                    
            MOVE  F630-NUM-VR-DDD    TO F122-NUM-VR-DDD.                        
            MOVE  F630-NUM-VR-YY     TO F122-NUM-VR-YY.                         
            MOVE  F630-DTE-TRANS     TO F122-DTE-TRANS.                         
            MOVE  WS-DATE            TO F122-DTE-POST.                          
            MOVE  WS-CPF-APPLT       TO F122-CDE-APPLT-CPF.                     
            MOVE  ZERO               TO F122-COUNT-HISTORY.                     
                                                                                
            IF K800-NUM-STATUS = 'C'                                            
               MOVE 'C'              TO F122-CDE-CANCEL.                        
                                                                                
            WRITE BP13F122-ADJTRAN.                                             
            ADD 1 TO CNT-F122-WRITE.                                            
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      *        WRITTING ERROR RECORDS TO BP13K140 FOR KIV      *                
      **********************************************************                
      *-------------------------------------------------------------            
       360-WRITE-BP13K140.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES TO BP13K140-KIVTRAN.                                     
           ADD 1       TO CNT-K140-WRITE.                                       
                                                                                
           IF WS-REGN-FOUND = 'N'                                               
               MOVE 'R' TO K140-CDE-ERROR.                                      
           IF WS-CPF-FOUND = 'N'                                                
               MOVE 'C' TO K140-CDE-ERROR.                                      
           IF WS-AMT-TALLY = 'N'                                                
               MOVE 'M' TO K140-CDE-ERROR.                                      
           IF WS-AMT-REFUND = 'N'                                               
               MOVE 'L' TO K140-CDE-ERROR.                                      
                                                                                
           IF WS-K880-FOUND = 'N'                                               
               MOVE 'S' TO K140-CDE-ERROR.                                      
                                                                                
           IF WS-AMT-CD-FLAG = 'N'                                              
               MOVE 'A' TO K140-CDE-ERROR.                                      
                                                                                
           IF F630-NUM-TRANS-ID = 'BKPS'                                        
                MOVE 'BK ' TO K140-CDE-PAYMENT-TYPE                             
           ELSE                                                                 
           IF F630-NUM-TRANS-ID = 'CDPS'                                        
                MOVE 'CD ' TO K140-CDE-PAYMENT-TYPE                             
           ELSE                                                                 
           IF F630-NUM-TRANS-ID = 'SDPS'                                        
                MOVE 'SOC' TO K140-CDE-PAYMENT-TYPE                             
           ELSE                                                                 
           IF F630-NUM-TRANS-ID = 'RFPS'                                        
                MOVE 'REG' TO K140-CDE-PAYMENT-TYPE                             
           ELSE                                                                 
           IF F630-NUM-TRANS-ID = 'DPPS'                                        
                MOVE 'RDE' TO K140-CDE-PAYMENT-TYPE                             
           ELSE                                                                 
           IF F630-NUM-TRANS-ID = 'SUPS'                                        
                MOVE 'SUS' TO K140-CDE-PAYMENT-TYPE                             
           ELSE                                                                 
           IF F630-NUM-TRANS-ID = 'BNPS'                                        
                MOVE 'SOL' TO K140-CDE-PAYMENT-TYPE.                            
                                                                                
           IF F630-NUM-CPF  = SPACES OR LOW-VALUES                              
                IF F630-NUM-BATCH-TYPE = 'J'                                    
                   MOVE '16' TO K140-CDE-TRANS-TYPE                             
               ELSE                                                             
                   MOVE '15' TO K140-CDE-TRANS-TYPE                             
           ELSE                                                                 
                IF F630-NUM-BATCH-TYPE = 'P'                                    
                   MOVE '13' TO K140-CDE-TRANS-TYPE                             
               ELSE                                                             
                   MOVE '14' TO K140-CDE-TRANS-TYPE.                            
                                                                                
      *  SAME CODE REPLACED AS BEFORE.                                          
           IF F630-NUM-TRANS-ID = 'BKPS' OR 'CDPS' OR 'SDPS' OR                 
                                  'SUPS' OR 'DPPS' OR 'BNPS'                    
              EVALUATE F630-NUM-TRANS-DR-CR                                     
              WHEN  'C'                                                         
                    COMPUTE WS-TOTAL-AMT = F630-AMT-TRANS                       
              WHEN  'D'                                                         
                    COMPUTE WS-TOTAL-AMT = 0 - F630-AMT-TRANS                   
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           IF F630-NUM-TRANS-ID = 'RFPS'                                        
              EVALUATE F630-NUM-TRANS-DR-CR                                     
              WHEN  'C'                                                         
                    COMPUTE WS-TOTAL-AMT = F630-AMT-TRANS +                     
                                           F630-AMT-TRANS-GST                   
              WHEN  'D'                                                         
                    COMPUTE WS-TOTAL-AMT = 0 - F630-AMT-TRANS -                 
                                           F630-AMT-TRANS-GST                   
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           MOVE F630-NUM-REGN       TO  K140-NUM-REGN.                  00051000
           MOVE 'V'                 TO  K140-COUNT-HIST-CHAR.           00051000
           MOVE CNT-K140-WRITE      TO  K140-COUNT-HIST-NUM.            00051000
           MOVE F630-NUM-BATCH      TO  WS-SCHEDULE-NUM.                        
           MOVE F630-NUM-VR-SERIAL  TO  WS-DOC-NO.                              
           MOVE WS-SCHEDULE-NO      TO  K140-NUM-RECPT-JRNO.                    
           MOVE F630-NUM-VR-DDD     TO  K140-NUM-VR-DDD.                        
           MOVE F630-NUM-VR-YY      TO  K140-NUM-VR-YY.                         
           MOVE F630-DTE-TRANS      TO  K140-DTE-TRANS.                         
           MOVE WS-DATE             TO  K140-DTE-POST.                          
                                                                                
           COMPUTE   K140-AMT-RECEIPT =  WS-TOTAL-AMT.                  00140100
                                                                                
           IF F630-NUM-CPF = SPACES OR LOW-VALUES                               
           MOVE SPACES              TO  K140-NUM-NRIC                           
           ELSE                                                                 
           IF F630-NUM-CPF = WS-CPF1                                            
           MOVE WS-NRIC1            TO  K140-NUM-NRIC                           
           ELSE                                                                 
           IF F630-NUM-CPF = WS-CPF2                                            
           MOVE WS-NRIC2            TO  K140-NUM-NRIC                           
           ELSE                                                                 
           IF F630-NUM-CPF = WS-CPF3                                            
           MOVE WS-NRIC3            TO  K140-NUM-NRIC                           
           ELSE                                                                 
           IF F630-NUM-CPF = WS-CPF4                                            
           MOVE WS-NRIC4            TO  K140-NUM-NRIC.                          
                                                                                
           WRITE BP13K140-KIVTRAN.                                              
           MOVE BP13K140-STATUS TO WS-K140-STATUS.                              
                                                                                
           IF WS-K140-STATUS1 = ZERO                                            
              NEXT SENTENCE                                                     
           ELSE                                                                 
               DISPLAY ' WRITE ERROR AT BP13K140 '                              
               DISPLAY ' REGN NO IS : ' F630-NUM-REGN                           
               DISPLAY 'BP13K140-STATUS   IS : ' BP13K140-STATUS                
               MOVE BP13K140-STATUS       TO     RETURN-CODE                    
               PERFORM 999-CLOSE-FILES THRU 999-EXIT.                           
                                                                                
       360-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       400-SEARCH-K885.                                                         
      *-------------------------------------------------------------            
           MOVE F630-NUM-REG       TO K885-KEY-FLD.                             
           PERFORM 410-START-K885  THRU 410-EXIT.                               
           PERFORM 420-CHECK-CPF   THRU 420-EXIT                                
             UNTIL WS-K885-EOF = 'Y'.                                           
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       410-START-K885.                                                          
      *-------------------------------------------------------------            
           MOVE 'N'                  TO WS-K885-EOF.                            
           START BP13K885 KEY >= K885-KEY-FLD.                                  
                                                                                
           IF BP13K885-STATUS = 00                                              
              PERFORM 415-READ-NEXT-K885 THRU 415-EXIT                          
           ELSE                                                                 
              IF BP13K885-STATUS = 20                                           
                 MOVE 'N'                  TO WS-CPF-FOUND                      
                 MOVE 'Y'                  TO WS-K885-EOF                       
              ELSE                                                              
                 MOVE BP13K885-STATUS TO RETURN-CODE                            
                 DISPLAY 'START ERROR, BP13K885-STATUS' BP13K885-STATUS         
                 PERFORM 999-CLOSE-FILES   THRU 999-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       415-READ-NEXT-K885.                                                      
      *-------------------------------------------------------------            
           READ BP13K885 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-K885-EOF                                  
                       GO TO 415-EXIT.                                          
                                                                                
           IF BP13K885-STATUS NOT = 00                                          
              MOVE BP13K885-STATUS TO RETURN-CODE                               
              DISPLAY 'READ  ERROR, BP13K885-STATUS' BP13K885-STATUS            
              PERFORM 999-CLOSE-FILES   THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF K885-NUM-REGN NOT = F630-NUM-REG                                  
              MOVE 'Y' TO WS-K885-EOF                                           
           ELSE                                                                 
              ADD 1    TO CNT-K885-READ                                         
           END-IF.                                                              
                                                                                
       415-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       420-CHECK-CPF.                                                           
      *-------------------------------------------------------------            
           DISPLAY 'F630-NUM-CPF '  F630-NUM-CPF                                
           DISPLAY 'K885-NUM-CPF '  K885-NUM-CPF.                               
                                                                                
           IF F630-NUM-CPF = K885-NUM-CPF                                       
              MOVE 'Y'          TO WS-K885-EOF                                  
              MOVE 'Y'          TO WS-CPF-FOUND                                 
              MOVE 'E'          TO WS-CPF-APPLT                                 
           ELSE                                                                 
              PERFORM 415-READ-NEXT-K885 THRU 415-EXIT                          
           END-IF.                                                              
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       700-SEARCH-K880.                                                         
      *-------------------------------------------------------------            
           MOVE  F630-NUM-REG    TO   K880-NUM-REGN.                            
           READ BP13K880.                                                       
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      *            WRITTING OUT ERROR LIST                     *                
      **********************************************************                
      *-------------------------------------------------------------            
       800-ERROR-LIST.                                                          
      *-------------------------------------------------------------            
           IF CNT-LINE-ON-PAGE > 46                                             
               MOVE 1 TO CNT-LINE-ON-PAGE                                       
               PERFORM 900-ERROR-HEADING THRU 900-EXIT                          
           ELSE                                                                 
               ADD 1 TO CNT-LINE-ON-PAGE.                                       
                                                                                
           ADD 1 TO CNT-KIV-CASES.                                              
                                                                                
           MOVE  SPACES   TO  13S1-REMARK.                                      
           IF K140-CDE-ERROR = 'R'                                              
               MOVE 'ORIG REGN NO ERROR' TO 13S1-REMARK                         
           ELSE                                                                 
           IF K140-CDE-ERROR = 'C'                                              
               MOVE 'CPF NO ERROR' TO 13S1-REMARK                               
           ELSE                                                                 
           IF K140-CDE-ERROR = 'M'                                              
             MOVE 'VOUCHER AMT NOT TALLY' TO 13S1-REMARK                        
           ELSE                                                                 
           IF K140-CDE-ERROR = 'L'                                              
             MOVE 'REGN FEE INSUFFICENT ' TO 13S1-REMARK                        
           ELSE                                                                 
           IF K140-CDE-ERROR = 'S'                                              
             MOVE 'REC NOTFD IN SUBTRF  ' TO 13S1-REMARK                        
           ELSE                                                                 
           IF K140-CDE-ERROR = 'X'                                              
               MOVE 'REFUND REC NOT EXAPPL FILE ' TO 13S1-REMARK                
           ELSE                                                                 
           IF K140-CDE-ERROR = 'A'                                              
               MOVE 'AMT CD DOESNOT MATCH       ' TO 13S1-REMARK.               
                                                                                
           MOVE  CNT-KIV-CASES         TO 13S1-SNO.                             
           MOVE  F630-NUM-REGN         TO 13S1-REG-NO.                          
           MOVE  K140-CDE-PAYMENT-TYPE TO 13S1-PAYMENT-TYPE.                    
           MOVE  K140-CDE-TRANS-TYPE   TO 13S1-TRANS-TY.                        
           MOVE  F630-DTE-TRANS        TO 13S1-DTE-POST.                        
           MOVE  K140-NUM-RECPT-JRNO   TO 13S1-NUM-RECEIPT.                     
           MOVE  K140-AMT-RECEIPT      TO 13S1-AMT.                             
           WRITE PRINT-REC FROM L102-PR-DETAILS.                                
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       900-ERROR-HEADING.                                                       
      *-------------------------------------------------------------            
            ADD 1         TO CNT-PAGE .                                         
            MOVE CNT-PAGE TO 13S1-PAGE.                                         
            MOVE SPACES   TO PRINT-REC                                          
            WRITE PRINT-REC AFTER PAGE.                                         
            WRITE PRINT-REC FROM L102-PR-HEAD-01 AFTER 2.                       
            WRITE PRINT-REC FROM L102-PR-HEAD-02 AFTER 3.                       
            WRITE PRINT-REC FROM L102-PR-HEAD-03 AFTER 3.                       
            MOVE ALL '-'  TO PRINT-REC.                                         
            WRITE PRINT-REC .                                                   
            MOVE SPACES   TO PRINT-REC.                                         
            WRITE PRINT-REC AFTER 1.                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           MOVE SPACES TO PRINT-REC.                                            
           WRITE PRINT-REC.                                                     
           MOVE 'PAYMENT INTERFACE (BP13C102)' TO CNTL-ITEM.                    
           WRITE PRINT-REC FROM L102-CONTROL-LINE AFTER 3.                      
                                                                                
           MOVE ' F630 RECS  READ   : ' TO CNTL-ITEM.                           
           MOVE CNT-F630-READ TO CNTL-CNT.                                      
           WRITE PRINT-REC FROM L102-CONTROL-LINE AFTER 1.                      
           MOVE ' K800 RECS  READ   : ' TO CNTL-ITEM.                           
           MOVE CNT-K800-READ TO CNTL-CNT.                                      
           WRITE PRINT-REC FROM L102-CONTROL-LINE.                              
           MOVE ' F122 RECS  WRITTEN: ' TO CNTL-ITEM.                           
           MOVE CNT-F122-WRITE TO CNTL-CNT.                                     
           WRITE PRINT-REC FROM L102-CONTROL-LINE .                             
           MOVE ' K140 RECS  WRITTEN: ' TO CNTL-ITEM.                           
           MOVE CNT-K140-WRITE TO CNTL-CNT.                                     
           WRITE PRINT-REC FROM L102-CONTROL-LINE .                             
                                                                                
           DISPLAY '******  BP13C102 *************'.                            
           DISPLAY 'NO OF F630 READ      (F630) : ' CNT-F630-READ.              
           DISPLAY 'NO OF RFF RECS READ  (K800) : ' CNT-K800-READ.              
           DISPLAY 'NO OF F122 WRITE     (F122) : ' CNT-F122-WRITE.             
           DISPLAY 'NO OF KIV RECS WRITE (K140) : ' CNT-K140-WRITE.             
                                                                                
           CLOSE BP13K800                                                       
                 BP13K885                                                       
                 BP13K820                                                       
                 BP13K880                                                       
                 AG07F630                                                       
                 BP13F122                                                       
                 BP13K140                                                       
                 BP13L102.                                                      
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K800-STATUS ' BP13K800-STATUS          
            MOVE BP13K800-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13K885-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K885-STATUS ' BP13K885-STATUS          
            MOVE BP13K885-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13K820-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K820-STATUS ' BP13K820-STATUS          
            MOVE BP13K820-STATUS TO RETURN-CODE.                                
                                                                                
           IF     K880-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR,     K880-STATUS '     K880-STATUS          
            MOVE     K880-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13K140-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K140-STATUS ' BP13K140-STATUS          
            MOVE BP13K140-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
            EXIT.                                                               
