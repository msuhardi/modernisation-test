      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13CC53.                                                 
      *AUTHOR.        EDEN ROSE DINGAL.                                         
      *DATE-WRITTEN.  4 DEC 2006.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT  (BP13)                                *           
      * =========================================================== *           
      *  OBJECTIVE :  1) TO PRINT BP13PC53 2ND LETTER REMINDERS     *           
      *               2) TO PRINT BP13LC53 CONTROL REPORT           *           
      *               3) TO  DELETE REC IN BP13KD76 IF              *           
      *                  LOAN IS PAID AND WRITE REC TO P13FD76A     *           
      *                                                             *           
      * INPUT FILES  :  1.  BP13FD76                                *           
      *                 2.  BP13K800                                *           
      *                 3.  BP13K820                                *           
      *                 4.  BP13K830                                *           
      *                 5.  BP13KD00                                *           
      *                 6.  AC15K010                                *           
      *                 7.  SY02F001                                *           
      *                                                             *           
      * I-O          :  1.  BP13KD76                                *           
      *                                                             *           
      * OUTPUT       :  1.  P13FD76A >> ACCTS DELETED FRM KD76      *           
      *                 2.  BP13LC53                                *           
      *                 3.  BP13PC53                                *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  DATE     BY   DESCRIPTION                          *           
      * -------- ------   ---  -----------                          *           
      * BP132936 20061201 ED1  NEW PGM                              *           
      * BP133051 20070511 ED1  CHGED CONDITION TO PRT LETR ONLY IF  *           
      *                        KD00(AMT-ARR - AMT-INST) >= INST-1ST *           
      * BP133091 20071017 RB12 EXPAND BP13KD00 FR 1200 TO 1600 BYTES*           
      * BP133175 20080122 ED1  GET AMT  ARREAR FROM                 *           
      *                        FD76-AMT-ARR-INSTAL-1ST              *           
      * =========================================================== *           
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
         SOURCE-COMPUTER.   IBM-3090.                                           
         OBJECT-COMPUTER.   IBM-3090.                                           
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
         FILE-CONTROL.                                                          
           SELECT BP13FD76 ASSIGN TO BP13FD76.                                  
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT AC15K010 ASSIGN TO AC15K010                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K010-ACCESS-KEY                                 
                FILE STATUS  IS WS-K010-STATUS.                                 
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KD00-KEY-FLD                                    
                FILE STATUS  IS WS-KD00-STATUS.                                 
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K800-NUM-REGN                                   
                FILE STATUS  IS WS-K800-STATUS.                                 
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K820-KEY-FLD                                    
                FILE STATUS  IS WS-K820-STATUS.                                 
                                                                                
           SELECT BP13K830 ASSIGN TO BP13K830                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K830-KEY-FLD                                    
                ALTERNATE RECORD KEY IS K830-NUM-REGN                           
                FILE STATUS  IS WS-K830-STATUS.                                 
                                                                                
           SELECT BP13KD76 ASSIGN TO BP13KD76                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KD76-KEY-FLD                                    
                FILE STATUS  IS WS-KD76-STATUS.                                 
                                                                                
           SELECT BP13LC53  ASSIGN TO BP13LC53.                                 
           SELECT BP13PC53  ASSIGN TO BP13PC53.                                 
           SELECT P13FD76A  ASSIGN TO P13FD76A.                                 
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD   BP13FD76    BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 300 CHARACTERS                          
                        LABEL RECORDS ARE STANDARD                              
                        RECORDING MODE IS F.                                    
       COPY BP13FD76.                                                           
                                                                                
       FD   BP13K800    RECORD CONTAINS 2000 CHARACTERS.                        
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820    RECORD CONTAINS 400 CHARACTERS.                         
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830    RECORD CONTAINS 250 CHARACTERS.                         
       COPY BP13K830.                                                           
                                                                                
       FD   BP13KD00    RECORD CONTAINS 1600 CHARACTERS.                        
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13KD76    RECORD CONTAINS 300 CHARACTERS.                         
       COPY BP13KD76.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   AC15K010    RECORD CONTAINS 200  CHARACTERS.                        
       COPY AC15K010.                                                           
                                                                                
       FD  P13FD76A     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 300 CHARACTERS                          
                        LABEL RECORD IS STANDARD                                
                        RECORDING MODE IS F.                                    
       01  P13FD76A-REC PIC X(300).                                             
                                                                                
       FD  BP13LC53     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 132 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01  LC53-PRTREC  PIC X(132).                                             
                                                                                
       FD  BP13PC53     RECORD CONTAINS 132 CHARACTERS                          
                        RECORDING MODE IS F.                                    
       01  PC53-PRTREC    PIC X(132).                                           
                                                                                
                                                                                
      *--------------------------------------------------------------           
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------           
       01  WS-COUNTERS.                                                         
           05 WS-AMT-ARREARS         PIC S9(6) VALUE ZEROES.                    
           05 WS-CNT-FD76-READ       PIC 9(7) VALUE ZEROES.                     
           05 WS-CNT-LC53-WRITE      PIC 9(7) VALUE ZEROES.                     
           05 WS-PC53-WRT-CTR        PIC 9(7) VALUE ZEROES.                     
           05 WS-SNO                 PIC 9(5) VALUE ZEROES.                     
           05 WS-PAGE-CTR            PIC 9(4) VALUE ZEROES.                     
           05 WS-LINE-CTR            PIC 9(2) VALUE ZEROES.                     
           05 WS-FD76A-WRITE         PIC 9(7) VALUE ZEROES.                     
           05 WS-KD76-WRITE          PIC 9(7) VALUE ZEROES.                     
           05 WS-KD76-REWRITE        PIC 9(7) VALUE ZEROES.                     
           05 WS-KD76-DELETED        PIC 9(7) VALUE ZEROES.                     
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 WS-K800-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-KD00-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K820-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K830-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K010-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-KD76-STATUS         PIC 9(2) VALUE ZEROES.                     
                                                                                
       01  WS-FLAGS.                                                            
           05 WS-FLG-FD76-EOF        PIC X    VALUE 'N'.                        
           05 WS-KD00-FOUND          PIC X    VALUE 'N'.                        
           05 WS-KD76-FOUND          PIC X    VALUE 'N'.                        
                                                                                
       01  WS-DATE-HDR               PIC X(10) VALUE SPACES.                    
       01  WS-MAX-LINE               PIC 99    VALUE 53.                        
                                                                                
       01  WS-DATE.                                                             
           05  WS-CCYY               PIC X(4).                                  
           05  WS-MM                 PIC X(2).                                  
           05  WS-MM-R REDEFINES WS-MM.                                         
               10 WS-MTH             PIC 9(2).                                  
           05  WS-DD                 PIC X(2).                                  
                                                                                
       01  WS-1ST-LTR-DTE.                                                      
           05  WS-YR                 PIC X(4).                                  
           05  WS-MO                 PIC X(2).                                  
           05  WS-MO-R REDEFINES WS-MO.                                         
               10 WS-MNTH            PIC 9(2).                                  
           05  WS-DAY                PIC X(2).                                  
                                                                                
       01  WS-TOTAL              PIC      9(7)V99 VALUE ZEROES.                 
                                                                                
       01 WS-MONTH-NAME.                                                        
          05 WS-STRING PIC X(36) VALUE                                          
                'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                         
          05 WS-MONTH  REDEFINES WS-STRING OCCURS 12 TIMES PIC XXX.             
                                                                        01010000
       01  WS-BLANK-LINE  PIC X(132)             VALUE SPACES.                  
                                                                                
       01  WS-DASH-LINE   PIC X(132)             VALUE ALL '-'.                 
                                                                                
      *-------------------------------------------------------------            
      * CONTROL REPORT                                                          
      *-------------------------------------------------------------            
       01  LC53-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13LC53'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LC53-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LC53-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
                                                                                
       01  LC53-HEADER02.                                                       
           05  FILLER              PIC X(28)     VALUE SPACES.                  
           05  FILLER              PIC X(42)     VALUE                          
               'REPORT ON ACCOUNTS WITH 2ND REMINDERS FOR '.                    
           05  FILLER              PIC X(13)     VALUE                          
               'THE MONTH OF '.                                                 
           05  LC53-MMM-HDR        PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X         VALUE SPACE.                   
           05  LC53-YR-HDR         PIC X(04)     VALUE SPACES.                  
                                                                                
       01  LC53-HEADER03.                                                       
           05  FILLER              PIC X(86)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'MTH IN'.                
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'AMT IN'.                
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'LATE PAYMENT'.          
                                                                                
       01  LC53-HEADER04.                                                       
           05  FILLER              PIC X(03)     VALUE 'S/N'.                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REGN NO'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'NAME/ADDRESS'.          
           05  FILLER              PIC X(58)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'ARREARS'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'ARREARS($)'.            
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'CHARGE($)'.             
                                                                                
       01 LC53-DETAILS.                                                         
          05 LC53-DTL-SERIAL      PIC ZZZZ9      VALUE ZEROES.                  
          05 FILLER               PIC X(01)      VALUE SPACES.                  
          05 LC53-REGNO           PIC X(08)      VALUE SPACES.                  
          05 FILLER               PIC X(02)      VALUE SPACES.                  
          05 LC53-NAME1           PIC X(66)      VALUE SPACES.                  
          05 FILLER               PIC X(04)      VALUE SPACES.                  
          05 LC53-MO-ARREAR       PIC      Z9.99 VALUE ZEROES.                  
          05 FILLER               PIC X(04)      VALUE SPACES.                  
          05 LC53-AMT-ARREAR      PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
          05 FILLER               PIC X(04)      VALUE SPACES.                  
          05 LC53-LTE-PYMNT-CHRGE PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
                                                                                
       01 LC53-DETAIL2.                                                         
          05 FILLER               PIC X(16)     VALUE SPACES.                   
          05 LC53-NAME2           PIC X(66)     VALUE SPACES.                   
                                                                                
       01 LC53-DETAIL3.                                                         
          05 FILLER               PIC X(16)     VALUE SPACES.                   
          05 LC53-ADDRESS         PIC X(90)     VALUE SPACES.                   
                                                                                
      *-------------------------------------------------------------            
      * FORMAT LETTER                                                           
      *-------------------------------------------------------------            
       01  PC53-PR-LINE-1.                                                      
           05  FILLER                   PIC X(72)  VALUE SPACES.                
           05  PC53-HDBREF              PIC X(14)  VALUE SPACES.                
                                                                                
       01  PC53-PR-LINE-2.                                                      
           05  FILLER                   PIC X(72)  VALUE SPACES.                
           05  PC53-REGNO               PIC X(08)  VALUE SPACES.                
                                                                                
       01  PC53-PR-LINE-3.                                                      
           05  FILLER                   PIC X(72)  VALUE SPACES.                
           05  PC53-DATE-HDR            PIC X(10)  VALUE SPACES.                
                                                                                
       01  PC53-PR-LINE-4.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC53-NAME1B              PIC X(66)  VALUE SPACES.                
                                                                                
       01  PC53-PR-LINE-5.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC53-NAME2B              PIC X(66)  VALUE SPACES.                
                                                                                
       01  PC53-PR-LINE-6.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC53-BLK                 PIC X(16)  VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACE.                 
           05  PC53-FLAT                PIC X(11)  VALUE SPACES.                
                                                                                
       01  PC53-PR-LINE-7.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC53-ADDR1               PIC X(65)  VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACE.                 
                                                                                
       01  PC53-PR-LINE-8.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC53-ADDR2               PIC X(65)  VALUE SPACES.                
                                                                                
       01  PC53-PR-LINE-9.                                                      
           05  FILLER                   PIC X(10)  VALUE SPACES.                
           05  PC53-BAR-CODE            PIC X(09)  VALUE SPACES.                
                                                                                
       01  PC53-PR-LINE-10.                                                     
           05  FILLER                   PIC X(11)  VALUE SPACES.                
           05  PC53-FLAT-ADDR           PIC X(70)  VALUE SPACES.                
                                                                                
       01  PC53-PR-LINE-11.                                                     
           05  FILLER                   PIC X(36)  VALUE SPACES.                
           05  PC53-1ST-LETR-DTE        PIC X(11)  VALUE SPACES.                
                                                                                
       01  PC53-PR-LINE-12.                                                     
           05  FILLER                   PIC X(33)  VALUE SPACES.                
           05  PC53-DTE-AS-AT1          PIC X(10)  VALUE SPACES.                
           05  FILLER                   PIC X(07)  VALUE SPACES.                
           05  FILLER                   PIC X(03)  VALUE '$  '.                 
           05  PC53-LOAN-INST           PIC ZZZ,ZZ9.99 VALUE ZEROES.            
                                                                                
       01  PC53-PR-LINE-13.                                                     
           05  FILLER                   PIC X(38)  VALUE SPACES.                
           05  PC53-DTE-AS-AT2          PIC X(10)  VALUE SPACES.                
           05  FILLER                   PIC X(02)  VALUE SPACES.                
           05  FILLER                   PIC X(03)  VALUE '$  '.                 
           05  PC53-LTE-PYMNT-CHRGE     PIC ZZZ,ZZ9.99 VALUE ZEROES.            
                                                                                
       01  PC53-PR-LINE-14.                                                     
           05  FILLER                   PIC X(50)  VALUE SPACES.                
           05  FILLER                   PIC X      VALUE '$'.                   
           05  PC53-TOTAL               PIC Z,ZZZ,ZZ9.99 VALUE ZEROES.          
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR AV02C015                                                   
      *-------------------------------------------------------------            
        01  WS-LINK-AV02C015.                                                   
            05  AV02C015-IN-POSTCODE          PIC X(6).                         
            05  AV02C015-OUT-POSTCODE-CHKSUM  PIC X(9).                         
                                                                                
       COPY AV02COMM.                                                           
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR BP13C913                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH            PIC X(04).                             
           05 WS-LINK-NUM-ACC            PIC X(04).                             
           05 WS-LINK-NUM-CHK            PIC X(01).                             
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-START-UP          THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13FD76     THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-RECORDS   THRU 3000-EXIT                        
                   UNTIL WS-FLG-FD76-EOF = 'Y'.                                 
           PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       1000-START-UP.                                                           
      *--------------------------------------------------------------           
           OPEN INPUT  BP13FD76                                                 
                       BP13K800                                                 
                       BP13KD00                                                 
                       BP13K820                                                 
                       BP13K830                                                 
                       AC15K010                                                 
                       SY02F001                                                 
                I-O    BP13KD76                                                 
               OUTPUT  BP13LC53                                                 
                       BP13PC53                                                 
                       P13FD76A.                                                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING AC15K010 FILE ERROR ' WS-K010-STATUS             
              MOVE     WS-K010-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KD00 FILE ERROR ' WS-KD00-STATUS             
              MOVE     WS-KD00-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 FILE ERROR ' WS-K800-STATUS             
              MOVE     WS-K800-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 FILE ERROR ' WS-K820-STATUS             
              MOVE     WS-K820-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K830 FILE ERROR ' WS-K830-STATUS             
              MOVE     WS-K830-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD76-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KD76 FILE ERROR ' WS-KD76-STATUS             
              MOVE     WS-KD76-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT TO WS-DATE.                                    
                                                                                
           STRING WS-DD '/' WS-MM '/' WS-CCYY                                   
           DELIMITED BY SIZE                   INTO WS-DATE-HDR.                
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       2000-READ-BP13FD76.                                                      
      *--------------------------------------------------------------           
           READ BP13FD76                                                        
              AT END MOVE 'Y' TO WS-FLG-FD76-EOF                                
                     GO TO 2000-EXIT.                                           
                                                                                
           ADD 1 TO WS-CNT-FD76-READ.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       3000-PROCESS-RECORDS.                                                    
      *--------------------------------------------------------------           
           PERFORM 3200-READ-BP13KD00   THRU 3200-EXIT.                         
           PERFORM 3300-READ-BP13KD76   THRU 3300-EXIT.                         
                                                                                
           IF WS-KD00-FOUND = 'Y' AND WS-KD76-FOUND = 'Y'                       
                                                                                
              MOVE 0 TO WS-AMT-ARREARS                                          
                                                                                
              IF FD76-AMT-ARR-INSTAL-1ST NOT NUMERIC                            
                 MOVE 0 TO FD76-AMT-ARR-INSTAL-1ST                              
              END-IF                                                            
                                                                                
              COMPUTE WS-AMT-ARREARS = KD00-AMT-ARR-INSTAL -                    
                                       KD00-AMT-INSTAL                          
                                                                                
              IF WS-AMT-ARREARS >= FD76-AMT-ARR-INSTAL-1ST                      
                 PERFORM 3400-INIT-PRNT-FIELDS   THRU 3400-EXIT                 
                 PERFORM 4000-PRINT-2ND-LETTER   THRU 4000-EXIT                 
                 PERFORM 7000-UPDATE-BP13KD76    THRU 7000-EXIT                 
              ELSE                                                              
                 PERFORM 3500-DELETE-BP13KD76    THRU 3500-EXIT                 
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD76   THRU 2000-EXIT.                         
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3200-READ-BP13KD00.                                                      
      *--------------------------------------------------------------           
                                                                                
           MOVE 'N'             TO  WS-KD00-FOUND.                              
           MOVE SPACES          TO  BP13KD00-REC.                               
           MOVE FD76-NUM-REGN   TO  KD00-NUM-REGN.                              
                                                                                
           READ BP13KD00.                                                       
                                                                                
           IF WS-KD00-STATUS = 00                                               
                MOVE 'Y' TO WS-KD00-FOUND                                       
                                                                                
                IF KD00-AMT-ARR-INSTAL NOT NUMERIC                              
                   MOVE 0 TO KD00-AMT-ARR-INSTAL                                
                END-IF                                                          
                IF KD00-AMT-INSTAL     NOT NUMERIC                              
                   MOVE 0 TO KD00-AMT-INSTAL                                    
                END-IF                                                          
                IF KD00-NUM-MTH-INSTAL  NOT NUMERIC                             
                   MOVE 0 TO KD00-NUM-MTH-INSTAL                                
                END-IF                                                          
                                                                                
           ELSE                                                                 
             IF WS-KD00-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN KD00, STATUS ' WS-KD00-STATUS           
                        ', REGN IS  '    FD76-NUM-REGN                          
             ELSE                                                               
                DISPLAY 'ERROR READING KD00, STATUS ' WS-KD00-STATUS            
                        ', REGN IS  '    FD76-NUM-REGN                          
                MOVE WS-KD00-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3300-READ-BP13KD76.                                                      
      *--------------------------------------------------------------           
                                                                                
           MOVE 'N'             TO  WS-KD76-FOUND.                              
           MOVE SPACES          TO  BP13KD76-REC.                               
           MOVE FD76-NUM-REGN   TO  KD76-NUM-REGN.                              
                                                                                
           READ BP13KD76.                                                       
                                                                                
           IF WS-KD76-STATUS = 00                                               
              MOVE 'Y' TO WS-KD76-FOUND                                         
                                                                                
           ELSE                                                                 
             IF WS-KD76-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN KD76, STATUS '                          
                WS-KD76-STATUS ', REGN IS  '    FD76-NUM-REGN                   
             ELSE                                                               
                DISPLAY 'ERROR READING KD76, STATUS ' WS-KD76-STATUS            
                        ', REGN IS  '    FD76-NUM-REGN                          
                MOVE WS-KD76-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3400-INIT-PRNT-FIELDS.                                                   
      *--------------------------------------------------------------           
           INITIALIZE                 PC53-NAME1B                               
                                      PC53-NAME2B                               
                                      PC53-DATE-HDR                             
                                      PC53-NAME2B                               
                                      PC53-BLK                                  
                                      PC53-ADDR1                                
                                      PC53-ADDR2                                
                                      PC53-BAR-CODE                             
                                      PC53-FLAT                                 
                                      PC53-FLAT-ADDR                            
                                      PC53-DTE-AS-AT1                           
                                      PC53-LOAN-INST                            
                                      PC53-DTE-AS-AT2                           
                                      PC53-LTE-PYMNT-CHRGE                      
                                      PC53-1ST-LETR-DTE                         
                                      PC53-HDBREF                               
                                      PC53-REGNO                                
                                      PC53-TOTAL.                               
                                                                                
           MOVE ZEROES            TO  WS-TOTAL.                                 
           MOVE SPACES            TO  LC53-DETAILS                              
                                      LC53-DETAIL2                              
                                      LC53-DETAIL3.                             
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3500-DELETE-BP13KD76.                                                    
      *--------------------------------------------------------------           
      *--> IF ACCT IS PAID, WRITE RECORD TO P13FD76A                            
      *-->             AND DELETE RECORD IN BP13KD76                            
                                                                                
           DELETE BP13KD76.                                                     
                                                                                
           IF WS-KD76-STATUS = 00 OR 02                                         
              ADD 1 TO WS-KD76-DELETED                                          
              PERFORM 7100-WRITE-FD76A     THRU 7100-EXIT                       
           ELSE                                                                 
             IF WS-KD76-STATUS = 23                                             
                DISPLAY 'REC NOT DELETED IN KD76, STAT IS '                     
                     WS-KD76-STATUS ',REGN IS '    FD76-NUM-REGN                
             ELSE                                                               
                DISPLAY 'ERROR DELETING KD76, STATUS ' WS-KD76-STATUS           
                        ', REGN IS  '    FD76-NUM-REGN                          
                MOVE WS-KD76-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4000-PRINT-2ND-LETTER.                                                   
      *--------------------------------------------------------------           
           PERFORM 4100-READ-BP13K800            THRU 4100-EXIT                 
                                                                                
           IF WS-K800-STATUS = 00                                               
                                                                                
              IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES AND ZEROES          
                 MOVE SPACES                TO   BP13K820-REC                   
                 MOVE K800-NUM-NRIC1        TO   K820-NUM-NRIC                  
                 MOVE FD76-NUM-REGN         TO   K820-NUM-REGN                  
                 PERFORM 4150-READ-BP13K820 THRU 4150-EXIT                      
                 MOVE K820-NME-OCCP         TO   LC53-NAME1                     
              END-IF                                                            
                                                                                
              IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND ZEROES          
                 MOVE SPACES                TO   BP13K820-REC                   
                 MOVE K800-NUM-NRIC2        TO   K820-NUM-NRIC                  
                 MOVE FD76-NUM-REGN         TO   K820-NUM-REGN                  
                 PERFORM 4150-READ-BP13K820 THRU 4150-EXIT                      
                 MOVE K820-NME-OCCP         TO   LC53-NAME2                     
              END-IF                                                            
                                                                                
              PERFORM 4200-READ-AC15K010            THRU 4200-EXIT              
              PERFORM 4300-GET-FLAT-ADDR            THRU 4300-EXIT              
              PERFORM 4400-READ-BP13K830            THRU 4400-EXIT              
              PERFORM 5000-MOVE-REPORT-DTLS         THRU 5000-EXIT              
              PERFORM 5100-MOVE-LETTER-DTLS         THRU 5100-EXIT              
              PERFORM 6000-PRINT-2ND-REMINDERS      THRU 6000-EXIT              
                                                                                
           END-IF.                                                              
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       4100-READ-BP13K800.                                                      
      *--------------------------------------------------------------           
      *    RETRIEVE OCCUPIERS NRIC1 & NRIC2 FROM FROM BP13K820    *             
      *    PRINT LETTER ONLY IF FOUND OCCUPIER(S) IN BP13K800     *             
      *-----------------------------------------------------------*             
           MOVE FD76-NUM-REGN TO K800-NUM-REGN.                                 
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
             IF WS-K800-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K800, STATUS ' WS-K800-STATUS           
                        ', REGN IS ' FD76-NUM-REGN                              
             ELSE                                                               
                DISPLAY 'ERROR READING K800, STATUS ' WS-K800-STATUS            
                        ', REGN IS  '    FD76-NUM-REGN                          
                MOVE WS-K800-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4150-READ-BP13K820.                                                      
      *--------------------------------------------------------------           
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
             IF WS-K820-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K820, STATUS ' WS-K820-STATUS           
                        ', KEY IS '  FD76-NUM-REGN ' ' K820-NUM-NRIC            
             ELSE                                                               
                DISPLAY 'ERROR READING K820, STATUS ' WS-K820-STATUS            
                        ', KEY IS '  FD76-NUM-REGN ' ' K820-NUM-NRIC            
                MOVE WS-K820-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       4150-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4200-READ-AC15K010.                                                      
      *--------------------------------------------------------------           
      *    RETRIEVE LATE PAYMENT CHARGE FROM AC15K010             *             
      *-----------------------------------------------------------*             
           MOVE FD76-NUM-SCH-ACCNT TO K010-ACCESS-KEY.                          
           READ AC15K010.                                                       
                                                                                
           IF WS-K010-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
                                                                                
             MOVE 0 TO K010-PF-BAL-OS                                           
                                                                                
             IF WS-K010-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K010, STATUS ' WS-K010-STATUS           
                        ',SCH ACC IS '   FD76-NUM-SCH-ACCNT                     
             ELSE                                                               
                DISPLAY 'ERROR READING K010, STATUS ' WS-K010-STATUS            
                        ',SCH ACC IS '   FD76-NUM-SCH-ACCNT                     
                MOVE WS-K010-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------*           
       4300-GET-FLAT-ADDR.                                                      
      *-------------------------------------------------------------*           
           MOVE SPACES                TO BP13COMM8-REC.                         
           MOVE K800-NUM-SCH-ACC(1:9) TO WS-LINK-REC.                           
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
             WHEN 000                                                           
                STRING 'BLK '  COMM8-NUM-BLK  ' '                               
                       '#'  COMM8-NUM-LEVEL   '-'                               
                       COMM8-NUM-UNIT-MAIN    ' ' DELIMITED BY SIZE             
                       COMM8-NME-STREET       DELIMITED BY '   '                
                       INTO PC53-FLAT-ADDR                                      
             WHEN 100                                                           
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                     
                         K800-NUM-SCH-ACC ', REGN : ' FD76-NUM-REGN             
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
                        ', AT KEY ' K800-NUM-SCH-ACC                            
                        ', REGN : ' FD76-NUM-REGN                               
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4400-READ-BP13K830.                                                      
      *-----------------------------------------------------------*             
      *    RETRIEVE ADDRESS FROM BP13K830 USING FD76 REGN NO      *             
      *-----------------------------------------------------------*             
           MOVE FD76-NUM-REGN          TO K830-NUM-REGN.                        
                                                                                
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           IF WS-K830-STATUS  = 00 OR 02                                        
              IF (K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR =  '#') AND                           
                 (K830-NUM-MAIL-LVL-NUM =  SPACES OR LOW-VALUES))               
                 MOVE K830-NUM-BLK         TO PC53-BLK                          
                 MOVE K830-NUM-FLAT        TO PC53-FLAT                         
                 MOVE K830-NME-STREET-BLDG TO PC53-ADDR1                        
                 IF K830-NUM-POSTAL  = SPACES OR LOW-VALUES OR ZEROES           
                    MOVE SPACES            TO PC53-ADDR2                        
                 ELSE                                                           
                    STRING 'SINGAPORE '  K830-NUM-POSTAL                        
                       DELIMITED BY SIZE    INTO PC53-ADDR2                     
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-POSTAL TO AV02C015-IN-POSTCODE                
                    PERFORM 4500-CALL-AV02C015     THRU 4500-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC53-BAR-CODE          
                 END-IF                                                         
              ELSE                                                              
                 MOVE K830-NUM-MAIL-BLK        TO PC53-BLK                      
                 MOVE K830-NUM-MAIL-FLAT       TO PC53-FLAT                     
                 MOVE K830-NME-MAIL-STR-BLDG   TO PC53-ADDR1                    
                 IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES OR ZEROS        
                    MOVE SPACES                TO PC53-ADDR2                    
                 ELSE                                                           
                    STRING 'SINGAPORE '      K830-NUM-MAIL-POSTAL               
                           DELIMITED BY SIZE INTO PC53-ADDR2                    
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-MAIL-POSTAL TO AV02C015-IN-POSTCODE           
                    PERFORM 4500-CALL-AV02C015     THRU 4500-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC53-BAR-CODE          
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
             IF WS-K830-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K830, STATUS ' WS-K830-STATUS           
                        ', REGN IS  '    FD76-NUM-REGN                          
             ELSE                                                               
                DISPLAY 'ERROR READING K830, STATUS ' WS-K830-STATUS            
                        ', REGN IS  '    FD76-NUM-REGN                          
                MOVE WS-K830-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
                                                                                
           END-IF.                                                              
                                                                                
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------*                                                  
       4500-CALL-AV02C015.                                                      
      *----------------------*                                                  
                                                                                
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       5000-MOVE-REPORT-DTLS.                                                   
      *--------------------------------------------------------------           
           MOVE FD76-NUM-REGN        TO  LC53-REGNO.                            
           MOVE WS-MONTH(WS-MTH)     TO  LC53-MMM-HDR.                          
           MOVE WS-CCYY              TO  LC53-YR-HDR.                           
                                                                                
           IF KD00-AMT-ARR-INSTAL  NOT NUMERIC                                  
              MOVE 0 TO KD00-AMT-ARR-INSTAL                                     
           END-IF.                                                              
           IF K010-PF-BAL-OS       NOT NUMERIC                                  
              MOVE 0 TO K010-PF-BAL-OS                                          
           END-IF.                                                              
                                                                                
           MOVE KD00-NUM-MTH-INSTAL       TO  LC53-MO-ARREAR.                   
           MOVE FD76-AMT-ARR-INSTAL-1ST   TO  LC53-AMT-ARREAR.                  
           MOVE K010-PF-BAL-OS            TO  LC53-LTE-PYMNT-CHRGE.             
                                                                                
           MOVE WS-DATE-HDR          TO  LC53-DATE-HDR.                         
                                                                                
           IF PC53-FLAT-ADDR NOT = SPACES                                       
              STRING 'BLK '  COMM8-NUM-BLK   ' '                                
                  '#'     COMM8-NUM-LEVEL '-'                                   
                          COMM8-NUM-UNIT-MAIN ' '   DELIMITED BY SIZE           
                          COMM8-NUM-UNIT-SUB  ' '   DELIMITED BY SIZE           
                          COMM8-NME-STREET          DELIMITED BY '   '          
                   ' S('  COMM8-NUM-POSTAL-CODE ')' DELIMITED BY SIZE           
                   INTO   LC53-ADDRESS                                          
              END-STRING                                                        
           END-IF.                                                              
                                                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       5100-MOVE-LETTER-DTLS.                                                   
      *--------------------------------------------------------------           
           MOVE LC53-REGNO        TO  PC53-REGNO.                               
           MOVE WS-DATE-HDR       TO  PC53-DATE-HDR.                            
           MOVE LC53-NAME1        TO  PC53-NAME1B.                              
           MOVE LC53-NAME2        TO  PC53-NAME2B.                              
                                                                                
           STRING FD76-NUM-SCH-ACCNT(1:4) '-'                                   
                  FD76-NUM-SCH-ACCNT(5:4) '-'                                   
                  FD76-NUM-SCH-ACCNT(9:1) '-'                                   
                  FD76-NUM-SCH-ACCNT(10:2)                                      
                  DELIMITED BY SIZE INTO PC53-HDBREF.                           
                                                                                
           STRING F001-DTE-LAST-DAY-PREV(7:2) '/'                               
                  F001-DTE-LAST-DAY-PREV(5:2) '/'                               
                  F001-DTE-LAST-DAY-PREV(1:4)                                   
              DELIMITED BY SIZE    INTO  PC53-DTE-AS-AT1                        
           END-STRING.                                                          
                                                                                
           STRING '08/'                                                         
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
              DELIMITED BY SIZE    INTO  PC53-DTE-AS-AT2                        
           END-STRING.                                                          
                                                                                
           STRING KD76-DTE-FIRST-LETTER(7:2) '/'                                
                  KD76-DTE-FIRST-LETTER(5:2) '/'                                
                  KD76-DTE-FIRST-LETTER(1:4)                                    
              DELIMITED BY SIZE INTO PC53-1ST-LETR-DTE                          
           END-STRING.                                                          
                                                                                
           MOVE LC53-AMT-ARREAR      TO  PC53-LOAN-INST.                        
           MOVE LC53-LTE-PYMNT-CHRGE TO  PC53-LTE-PYMNT-CHRGE.                  
                                                                                
           INITIALIZE                      WS-TOTAL.                            
           ADD FD76-AMT-ARR-INSTAL-1ST TO  WS-TOTAL.                            
           ADD K010-PF-BAL-OS          TO  WS-TOTAL.                            
           MOVE WS-TOTAL               TO  PC53-TOTAL.                          
                                                                                
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       6000-PRINT-2ND-REMINDERS.                                                
      *--------------------------------------------------------------           
      *-->  PRINT REPORT                                                        
           ADD   1                      TO  WS-SNO.                             
           MOVE  WS-SNO                 TO  LC53-DTL-SERIAL.                    
                                                                                
           IF WS-LINE-CTR > WS-MAX-LINE OR                                      
              WS-SNO       = 1                                                  
              ADD            1   TO WS-PAGE-CTR                                 
              MOVE ZEROES        TO LC53-PAGE-HDR                               
              MOVE WS-PAGE-CTR   TO LC53-PAGE-HDR                               
              PERFORM 6100-PRINT-HEADING THRU 6100-EXIT                         
           END-IF.                                                              
                                                                                
           WRITE LC53-PRTREC FROM LC53-DETAILS.                                 
           WRITE LC53-PRTREC FROM LC53-DETAIL2.                                 
           WRITE LC53-PRTREC FROM LC53-DETAIL3.                                 
           WRITE LC53-PRTREC FROM WS-BLANK-LINE.                                
                                                                                
           ADD 4 TO WS-LINE-CTR.                                                
           ADD   1              TO    WS-CNT-LC53-WRITE.                        
                                                                                
      *--> PRINT LETTER                                                         
           WRITE PC53-PRTREC    FROM  PC53-PR-LINE-1 AFTER PAGE.                
           WRITE PC53-PRTREC    FROM  PC53-PR-LINE-2.                           
           WRITE PC53-PRTREC    FROM  PC53-PR-LINE-3.                           
           WRITE PC53-PRTREC    FROM  PC53-PR-LINE-4 AFTER 4.                   
           WRITE PC53-PRTREC    FROM  PC53-PR-LINE-5.                           
           WRITE PC53-PRTREC    FROM  PC53-PR-LINE-6.                           
           WRITE PC53-PRTREC    FROM  PC53-PR-LINE-7.                           
           WRITE PC53-PRTREC    FROM  PC53-PR-LINE-8.                           
           WRITE PC53-PRTREC    FROM  PC53-PR-LINE-9  AFTER 4.                  
           WRITE PC53-PRTREC    FROM  PC53-PR-LINE-10 AFTER 6.                  
           WRITE PC53-PRTREC    FROM  PC53-PR-LINE-11 AFTER 2.                  
           WRITE PC53-PRTREC    FROM  PC53-PR-LINE-12 AFTER 3.                  
           WRITE PC53-PRTREC    FROM  PC53-PR-LINE-13 AFTER 1.                  
           WRITE PC53-PRTREC    FROM  PC53-PR-LINE-14 AFTER 2.                  
                                                                                
           ADD   1              TO    WS-PC53-WRT-CTR.                          
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       6100-PRINT-HEADING.                                                      
      *--------------------------------------------------------------           
            WRITE LC53-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC53-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC53-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC53-PRTREC FROM LC53-HEADER01 AFTER PAGE.                    
            WRITE LC53-PRTREC FROM LC53-HEADER02.                               
            WRITE LC53-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC53-PRTREC FROM LC53-HEADER03.                               
            WRITE LC53-PRTREC FROM LC53-HEADER04.                               
            WRITE LC53-PRTREC FROM WS-DASH-LINE.                                
                                                                                
            INITIALIZE WS-LINE-CTR.                                             
            MOVE  9 TO WS-LINE-CTR.                                             
                                                                                
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *--------------------------------------------------------------           
       7000-UPDATE-BP13KD76.                                                    
      *--------------------------------------------------------------           
           INITIALIZE                         KD76-DTE-UPDATE                   
                                              KD76-TME-UPDATE                   
                                              KD76-DTE-SECOND-LETTER            
                                              KD76-AMT-ARR-INSTAL-2ND           
                                              KD76-NUM-MTH-INSTAL-2ND.          
           MOVE WS-DATE                    TO KD76-DTE-SECOND-LETTER.           
           MOVE KD00-AMT-ARR-INSTAL        TO KD76-AMT-ARR-INSTAL-2ND.          
           MOVE KD00-NUM-MTH-INSTAL        TO KD76-NUM-MTH-INSTAL-2ND.          
           MOVE FUNCTION CURRENT-DATE(1:8) TO KD76-DTE-UPDATE.                  
           MOVE FUNCTION CURRENT-DATE(9:8) TO KD76-TME-UPDATE.                  
           MOVE 'BP13CC53'                 TO KD76-NUM-USERID.                  
                                                                                
           PERFORM 7200-REWRITE-BP13KD76   THRU 7200-EXIT.                      
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       7100-WRITE-FD76A.                                                        
      *--------------------------------------------------------------           
      * WRITE TO P13FD76A - DELETED RECORDS                                     
      *-------------------------------------                                    
              INITIALIZE                     P13FD76A-REC.                      
              MOVE BP13KD76-REC         TO   P13FD76A-REC.                      
              WRITE                          P13FD76A-REC.                      
              ADD   1                   TO   WS-FD76A-WRITE.                    
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       7200-REWRITE-BP13KD76.                                                   
      *--------------------------------------------------------------           
           REWRITE BP13KD76-REC.                                                
                                                                                
           IF WS-KD76-STATUS = 00 OR 02                                         
              ADD 1    TO WS-KD76-REWRITE                                       
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING KD76, STATUS ' WS-KD76-STATUS            
                      ', REGN IS  '    FD76-NUM-REGN                            
              MOVE WS-KD76-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
                                                                                
       7200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       9000-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------           
           CLOSE  BP13FD76                                                      
                  BP13KD00                                                      
                  BP13K800                                                      
                  BP13K820                                                      
                  BP13K830                                                      
                  BP13KD76                                                      
                  AC15K010                                                      
                  SY02F001                                                      
                  BP13LC53                                                      
                  BP13PC53                                                      
                  P13FD76A.                                                     
                                                                                
                                                                                
           DISPLAY ' RUN   DATE :   ' WS-DATE-HDR.                              
           DISPLAY ' *------------ BP13CC53 AUDIT TRAIL ------------*'.         
           DISPLAY '    '.                                                      
           DISPLAY 'NO. OF RECORDS READ FROM     BP13FD76 = '                   
                   WS-CNT-FD76-READ.                                            
           DISPLAY 'NO. OF RECORDS WRITTEN   TO  BP13LC53 = '                   
                   WS-CNT-LC53-WRITE.                                           
           DISPLAY 'NO. OF RECORDS WRITTEN   TO  BP13PC53 = '                   
                   WS-PC53-WRT-CTR.                                             
           DISPLAY 'NO. OF RECORDS WRITTEN   TO  P13FD76A = '                   
                   WS-FD76A-WRITE.                                              
           DISPLAY 'NO. OF RECORDS REWRITTEN TO  BP13KD76 = '                   
                   WS-KD76-REWRITE.                                             
           DISPLAY 'NO. OF RECORDS DELETED FROM  BP13KD76 = '                   
                   WS-KD76-DELETED.                                             
                                                                                
                                                                                
           IF WS-K010-STATUS NOT = 00                                           
              DISPLAY 'CLOSING AC15K010 FILE ERROR ' WS-K010-STATUS             
              MOVE     WS-K010-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD00-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13KD00 FILE ERROR ' WS-KD00-STATUS             
              MOVE     WS-KD00-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13K800 FILE ERROR ' WS-K800-STATUS             
              MOVE     WS-K800-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13K820 FILE ERROR ' WS-K820-STATUS             
              MOVE     WS-K820-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13K830 FILE ERROR ' WS-K830-STATUS             
              MOVE     WS-K830-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD76-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13KD76 FILE ERROR ' WS-KD76-STATUS             
              MOVE     WS-KD76-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
