      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID. BP13C301.                                                    
       AUTHOR. ACK.                                                             
                                                                                
      * ======================================================== *              
      *                SYSTEM OF COMMITMENT  (BP13)              *              
      * ======================================================== *              
      *    OBJECTIVE :                                           *              
      *      1. THIS IS A PROGRAM TO UPDATE SOC MASTER &         *              
      *         OCCUPIER MASTER FROM AGREEMENT ORDER FILE.       *              
      *      2. UPDATE THE SOC HISTORY FILE.                     *              
      *      3. AO RECORDS ARE FROM BP13B301 PANELS.             *              
      *                                                          *              
      *    INPUT FILES :                                         *              
      *      1. SORTED AGREEMENT ORDER FILE (BP13F300)           *              
      *      2. SOC FILE (BP13K800)                              *              
      *      3. OCCUPIER FILE (BP13K820)                         *              
      *      4. SOC GRANT FILE (BP13KD05)                        *              
      *                                                          *              
      *    OUTPUT FILES :                                        *              
      *      1. KIV FILE (BP13K300)                              *              
      *      2. HISTORY UPDATE FILE (BP13K840)                   *              
      *      3. SUCCESSFUL CASES (BP13OUT)                       *              
      *                                                          *              
      * CHG-NO   BY  DATE     DESCRIPTION                        *              
      * -------- --- ------   -----------                        *              
      * I920718  BSL 930630 - CONVERT TO COBOL II.               *              
      * SOC-PH8B BSL 930630 - UPD BP13K800 INSTEAD OF RFF AND    *              
      *                       EXPAND REGN AND NRIC NO            *              
      *                       CHG P10K040 TO BP13K820            *              
      *                       CHG P10K030 TO BP13K840            *              
      *                       REFORMAT PRINTING                  *              
      *                       REARRANGE AND RENUMBER PROCEDURES  *              
      * C930993  LJL 931119 - TO UPDATE THE GST COMPONENT OF     *              
      *                       CONVEYANCING FEES TO MASTER.       *              
      * BP130020 SSS 951010 - REPOSITION REPORT-ID TO COL 1      *              
      * BP130302 LJL 970820 - TO INCLUDE REGN DEPOSIT.           *              
      * BP130418 SGK 980602 - TO CATER FOR Y2K.                  *              
      * BP130850 AS1 010914 - WRITE CDB.INFO TO BP13F915         *              
      * BP130850 AS1 011213 - OUTPUT REGN-NO TO BP13F915         *              
      * BP132191 MCC 020803 - UPDTE K800-DTE-PRINCIPAL-SALES-AGMT*              
      * BP132309 MCC 020803 - UPDTE MORT-CONVY AND CAVEAT-REGN   *              
      * BP132734 JF4 120905 - REMVE K800-DTE-PRINCIPAL-SALES-AGMT*              
      * BP132842 TRS 070606 - UPDATE KD05-AMT-AHG-USED           *              
      * BP134641 MDT 030812 - CHANGE LRECL OF BP13KD05 FROM 300  *              
      *                       TO 500 IN FD                       *              
      * BP134867 PCL 110316 - TO READ BP13K480 FOR MGL CASES     *              
      * BP135833 KSJ 270416 - TO ADD UT SIGNED IN BP13K800 UPD   *              
      * BP135833 KSJ 090616 - CATER FOR EXPANDED SER CONTRA AMT  *              
      * BP135833 KSJ 100616 - FIX ISSUE ON NUMERIC VALUE         *              
      * ======================================================== *              
                                                                                
      *--------------------*                                                    
       ENVIRONMENT DIVISION.                                                    
      *--------------------*                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13K480 ASSIGN TO BP13K480                                   
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K480-NUM-REGN                                   
                  ACCESS MODE IS RANDOM                                         
                  FILE STATUS IS WS-K480-FILE-STATUS.                           
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K800-NUM-REGN                                   
                  ACCESS MODE IS RANDOM                                         
                  FILE STATUS IS WS-K800-FILE-STATUS.                           
                                                                                
           SELECT BP13K840 ASSIGN TO BP13K840                                   
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K840-KEY-FLD                                    
                  ACCESS MODE IS RANDOM                                         
                  FILE STATUS IS WS-K840-FILE-STATUS.                           
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE IS RANDOM                                         
                  RECORD KEY IS K820-KEY-FLD                                    
                  FILE STATUS IS WS-K820-FILE-STATUS.                           
                                                                                
           SELECT BP13KD05 ASSIGN TO BP13KD05                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE IS RANDOM                                         
                  RECORD KEY IS KD05-KEY-FLD                                    
                  FILE STATUS IS WS-KD05-FILE-STATUS.                           
                                                                                
           SELECT BP13F300 ASSIGN TO BP13F300                                   
                  ACCESS MODE IS SEQUENTIAL.                                    
                                                                                
           SELECT BP13OUT  ASSIGN TO BP13OUT                                    
                  ACCESS MODE IS SEQUENTIAL.                                    
                                                                                
           SELECT BP13KIV  ASSIGN TO BP13KIV                                    
                  ACCESS MODE IS SEQUENTIAL.                                    
                                                                                
           SELECT BP13L301 ASSIGN TO BP13L301.                                  
                                                                                
           SELECT BP13F915 ASSIGN TO BP13F915.                                  
                                                                                
      *-------------*                                                           
       DATA DIVISION.                                                           
      *-------------*                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K800                RECORD CONTAINS 2000 CHARACTERS.             
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K480                RECORD CONTAINS 1000 CHARACTERS.             
       COPY BP13K480.                                                           
                                                                                
       FD  BP13K840                RECORD CONTAINS 500 CHARACTERS.              
       COPY BP13K840.                                                           
                                                                                
       FD  BP13K820                RECORD CONTAINS 400 CHARACTERS.              
       COPY BP13K820.                                                           
                                                                                
       FD  BP13KD05                RECORD CONTAINS 500 CHARACTERS.              
       COPY BP13KD05.                                                           
                                                                                
       FD  BP13F300                BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 2000 CHARACTERS              
                                   LABEL RECORDS ARE STANDARD                   
                                   RECORDING MODE IS F.                         
       COPY BP13F300.                                                           
                                                                                
       FD  BP13OUT                 BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 2000 CHARACTERS              
                                   LABEL RECORDS ARE STANDARD                   
                                   RECORDING MODE IS F.                         
       01  BP13OUT-REC             PIC X(2000).                                 
                                                                                
       FD  BP13KIV                 BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 2000 CHARACTERS              
                                   LABEL RECORDS ARE STANDARD                   
                                   RECORDING MODE IS F.                         
       01  BP13KIV-REC             PIC X(2000).                                 
                                                                                
       FD  BP13F915                BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 800 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD                   
                                   RECORDING MODE IS F.                         
       COPY BP13F915.                                                           
                                                                                
       FD  BP13L301                LABEL RECORDS ARE OMITTED                    
                                   RECORDING MODE IS F.                         
       01  BP13L301-REC            PIC X(133).                                  
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
       77  WS-K480-FILE-STATUS     PIC 99       VALUE ZERO.                     
       77  WS-K800-FILE-STATUS     PIC 99       VALUE ZERO.                     
       77  WS-K840-FILE-STATUS     PIC 99       VALUE ZERO.                     
       77  WS-K820-FILE-STATUS     PIC 99       VALUE ZERO.                     
       77  WS-KD05-FILE-STATUS     PIC 99       VALUE ZERO.                     
       77  WS-F300-INPUT           PIC 9(7)     VALUE ZERO.                     
       77  WS-DELETE-BP13K820      PIC 9(7)     VALUE ZERO.                     
       77  WS-K840-OUTPUT          PIC 9(7)     VALUE ZERO.                     
       77  WS-K820-OUTPUT          PIC 9(7)     VALUE ZERO.                     
       77  WS-K820-REWRITE         PIC 9(7)     VALUE ZERO.                     
       77  WS-KD05-REWRITE         PIC 9(7)     VALUE ZERO.                     
       77  WS-K480-REWRITE         PIC 9(7)     VALUE ZERO.                     
       77  WS-K480-WRITE           PIC 9(7)     VALUE ZERO.                     
       77  WS-K800-OUTPUT          PIC 9(7)     VALUE ZERO.                     
       77  WS-F915-WRITE           PIC 9(7)     VALUE ZERO.                     
       77  WS-OUT-OUTPUT           PIC 9(7)     VALUE ZERO.                     
       77  WS-KIV-OUTPUT           PIC 9(7)     VALUE ZERO.                     
       77  WS-F300-NOTFND          PIC 9(7)     VALUE ZERO.                     
       77  WS-DEBTOR               PIC S9(7)V99 VALUE ZERO.                     
       77  WS-AMT                  PIC S9(7)V99 VALUE ZERO.                     
       77  WS-K800-NOTFND          PIC X        VALUE SPACES.                   
       77  WS-ERROR-FLAG           PIC X        VALUE SPACES.                   
       77  WS-WRONG-APPL           PIC X        VALUE SPACES.                   
       77  WS-LINE                 PIC 9(2)     VALUE 60.                       
       77  WS-PAGE                 PIC 9(3)     VALUE ZEROES.                   
       77  WS-CTR                  PIC 9        VALUE ZEROES.                   
                                                                                
       01  WS-STORE-FIELDS.                                                     
           05 WS-CASH              PIC S9(7)V99 VALUE +0.                       
           05 WS-ACPF              PIC S9(7)V99 VALUE +0.                       
           05 WS-CCPF              PIC S9(7)V99 VALUE +0.                       
           05 WS-SOC-DEBTOR        PIC S9(7)V99 VALUE +0.                       
           05 WS-SOC-AMT           PIC S9(7)V99 VALUE +0.                       
           05 WS-SOC-REQ           PIC S9(7)V99 VALUE +0.                       
           05 WS-CD                PIC S9(7)V99 VALUE +0.                       
           05 WS-CD-CASH           PIC S9(7)V99 VALUE +0.                       
           05 WS-CD-CPF1           PIC S9(7)V99 VALUE +0.                       
           05 WS-CD-CPF2           PIC S9(7)V99 VALUE +0.                       
           05 WS-CD-CPF3           PIC S9(7)V99 VALUE +0.                       
           05 WS-CD-CPF4           PIC S9(7)V99 VALUE +0.                       
           05 WS-AMT-REFUND        PIC S9(7)V99 VALUE +0.                       
           05 WS-AMT-REGN-DEP      PIC S9(7)V99 VALUE +0.                       
           05 WS-AMT-REGN-DEP-K800 PIC S9(7)V99 VALUE +0.                       
           05 WS-TOT-USED-GR       PIC S9(7)V99 VALUE +0.                       
           05 WS-NAME              PIC X(66)    VALUE SPACES.                   
           05 WS-CPF               PIC X(9)     VALUE SPACES.                   
           05 WS-NRIC              PIC X(9)     VALUE SPACES.                   
           05 WS-SER-CONTRA-ICP    PIC S9(6)V99 VALUE +0.                       
                                                                                
       01  WS-DATE.                                                             
           05 WS-CCYY              PIC X(4)     VALUE SPACES.                   
           05 WS-MM                PIC X(2)     VALUE SPACES.                   
           05 WS-DD                PIC X(2)     VALUE SPACES.                   
                                                                                
       01  WS-EDITED-DATE.                                                      
           05 WS-EDITED-DD         PIC X(2)     VALUE SPACES.                   
           05 FILLER               PIC X        VALUE '/'.                      
           05 WS-EDITED-MM         PIC X(2)     VALUE SPACES.                   
           05 FILLER               PIC X        VALUE '/'.                      
           05 WS-EDITED-CCYY       PIC X(4)     VALUE SPACES.                   
                                                                                
       01  WS-CURR-DATE.                                                        
              10 WS-CURR-CCYY      PIC 9(4).                                    
              10 WS-CURR-MM        PIC 9(2).                                    
              10 WS-CURR-DD        PIC 9(2).                                    
                                                                                
       01  PRINT-ERR1.                                                          
           05 FILLER               PIC XX       VALUE SPACES.                   
           05 FILLER               PIC X(10)    VALUE 'BP13-L301'.              
           05 FILLER               PIC X(10)    VALUE SPACES.                   
           05 FILLER               PIC X(76)    VALUE                           
              'ERROR LIST FOR UPDATING SOC MASTER & OCCUPIER MASTER  '.         
           05 FILLER               PIC X(7)     VALUE 'PAGE : '.                
           05 PRINT-PAGE           PIC ZZZ9.                                    
           05 FILLER               PIC X(10)    VALUE '  DATE : '.              
           05 PRINT-DATE1          PIC X(10)    VALUE SPACES.                   
                                                                                
       01  PRINT-ERR2.                                                          
           05 FILLER               PIC X(2)     VALUE SPACES.                   
           05 FILLER               PIC X(7)     VALUE 'REGN NO'.                
           05 FILLER               PIC X(5)     VALUE SPACES.                   
           05 FILLER               PIC X(10)    VALUE 'REMARKS'.                
                                                                                
       01  PRINT-ERR3.                                                          
           05 FILLER               PIC X(2)     VALUE SPACES.                   
           05 PRT-REGNO            PIC X(8)     VALUE SPACES.                   
           05 FILLER               PIC X(4)     VALUE SPACES.                   
           05 PRT-REMARKS          PIC X(60)    VALUE SPACES.                   
                                                                                
       01  PRINT-HDG1.                                                          
           05 FILLER               PIC X(8)     VALUE 'BP13L301'.               
           05 FILLER               PIC X(13)    VALUE SPACES.                   
           05 FILLER               PIC X(80)    VALUE                           
              'CONTROL LIST FOR UPDATING SOC MASTER & OCCUPIER MASTER'.         
           05 FILLER               PIC X(10)    VALUE SPACES.                   
           05 FILLER               PIC X(7)     VALUE 'DATE : '.                
           05 PRINT-DATE           PIC X(10)    VALUE SPACES.                   
                                                                                
       01  PRINT-REC.                                                           
           05 FILLER               PIC X(10)    VALUE SPACES.                   
           05 PRINT-DESC           PIC X(30)    VALUE SPACES.                   
           05 PRINT-TOTAL          PIC ZZZZZZZ9.                                
                                                                                
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION.                                                      
      *------------------*                                                      
                                                                                
      ******************************************************************        
       0000-INITIALIZATION.                                                     
      ******************************************************************        
           OPEN I-O    BP13K800                                                 
                       BP13K840                                                 
                       BP13K480                                                 
                       BP13K820                                                 
                       BP13KD05                                                 
                INPUT  BP13F300                                                 
                OUTPUT BP13OUT                                                  
                       BP13KIV                                                  
                       BP13L301                                                 
                       BP13F915.                                                
                                                                                
           IF WS-K480-FILE-STATUS NOT = ZEROES AND 97                           
              DISPLAY 'BP13K480 - OPEN ERROR ' WS-K480-FILE-STATUS              
              MOVE WS-K480-FILE-STATUS TO RETURN-CODE                           
              PERFORM 9999-FINALIZATION.                                        
                                                                                
           IF WS-K800-FILE-STATUS NOT = ZEROES AND 97                           
              DISPLAY 'BP13K800 - OPEN ERROR ' WS-K800-FILE-STATUS              
              MOVE WS-K800-FILE-STATUS TO RETURN-CODE                           
              PERFORM 9999-FINALIZATION.                                        
                                                                                
           IF WS-K820-FILE-STATUS NOT = ZEROES AND 97                           
              DISPLAY 'BP13K820 - OPEN ERROR ' WS-K820-FILE-STATUS              
              MOVE WS-K820-FILE-STATUS TO RETURN-CODE                           
              PERFORM 9999-FINALIZATION.                                        
                                                                                
           IF WS-K840-FILE-STATUS NOT = ZEROES AND 97                           
              DISPLAY 'BP13K840 - OPEN ERROR ' WS-K840-FILE-STATUS              
              MOVE WS-K840-FILE-STATUS TO RETURN-CODE                           
              PERFORM 9999-FINALIZATION.                                        
                                                                                
           IF WS-KD05-FILE-STATUS NOT = ZEROES AND 97                           
              DISPLAY 'BP13KD05 - OPEN ERROR ' WS-KD05-FILE-STATUS              
              MOVE WS-KD05-FILE-STATUS TO RETURN-CODE                           
              PERFORM 9999-FINALIZATION.                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO  WS-DATE.                         
           MOVE WS-CCYY                    TO  WS-EDITED-CCYY.                  
           MOVE WS-MM                      TO  WS-EDITED-MM.                    
           MOVE WS-DD                      TO  WS-EDITED-DD.                    
           MOVE WS-EDITED-CCYY             TO  WS-CURR-CCYY.                    
           MOVE WS-EDITED-MM               TO  WS-CURR-MM.                      
           MOVE WS-EDITED-DD               TO  WS-CURR-DD.                      
           MOVE WS-EDITED-DATE             TO  PRINT-DATE.                      
           MOVE WS-EDITED-DATE             TO  PRINT-DATE1.                     
                                                                                
                                                                                
      ******************************************************************        
       1000-MAIN-PROCESS.                                                       
      ******************************************************************        
           PERFORM 2000-READ-BP13F300     THRU 2000-EXIT.                       
           PERFORM 3000-UPDATE-SOC-MASTER THRU 3000-EXIT                        
                   UNTIL F300-CUR-REG = HIGH-VALUES.                            
           PERFORM 5000-PRINT-CONTROL     THRU 5000-EXIT.                       
           PERFORM 9999-FINALIZATION.                                           
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F300.                                                      
      ******************************************************************        
           READ BP13F300 AT END                                                 
                MOVE HIGH-VALUES TO F300-CUR-REG                                
                DISPLAY 'BP13F300 - END OF FILE'                                
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F300-INPUT.                                              
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       3000-UPDATE-SOC-MASTER.                                                  
      ******************************************************************        
      *===========================================================*             
      * 3000-UPDATE-SOC-MASTER        |                           *             
      *-----------------------------------------------------------*             
      *   3100-CHECK-SOC              | UNDER SOC OR EXISTING     *             
      *   3200-UPD-LESSEE-INFO        | UPDATE NRIC AND CPF NO    *             
      *   3300-UPDATE-FILE            | WRITE AND REWRITE RECORDS *             
      *===========================================================*             
                                                                                
           MOVE F300-CUR-REG TO K800-NUM-REGN.                                  
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-FILE-STATUS = ZEROES                                      
              PERFORM 3100-CHECK-SOC           THRU 3100-EXIT                   
              PERFORM 3200-UPD-LESSEE-INFO     THRU 3200-EXIT                   
              PERFORM 3300-UPDATE-FILE         THRU 3300-EXIT                   
              IF F300-NUM-AHG = 'Y'                                             
                 PERFORM 3400-VAL-USED-GRANT   THRU 3400-EXIT                   
                    VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 4                 
              END-IF                                                            
           ELSE                                                                 
              MOVE F300-CUR-REG                TO   PRT-REGNO                   
              MOVE 'RECORD NOT IN SOC MASTER ' TO   PRT-REMARKS                 
              PERFORM 4000-PRINT-ERR           THRU 4000-EXIT                   
              WRITE BP13KIV-REC FROM BP13F300-REC                               
              ADD 1 TO WS-KIV-OUTPUT.                                           
                                                                                
       3000-READ-BP13F300.                                                      
           PERFORM 2000-READ-BP13F300 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       3100-CHECK-SOC.                                                          
      ******************************************************************        
           MOVE F300-CDE-UT-SIGNED   TO  K800-CDE-UT-SIGNED.                    
           MOVE F300-DTE-SALE-AGRM   TO  K800-DTE-SALE-AGMT.                    
           MOVE F300-DTE-AGRM-ORDER  TO  K800-DTE-RAISE-AO.                     
           MOVE F300-DTE-DELIVERY-POSSN TO  K800-DTE-DELIVERY-POSSN.            
                                                                                
           IF F300-CDE-ASPF = 'Y'                                               
              MOVE F300-CDE-ASPF     TO K800-NUM-ASPF.                          
                                                                                
           IF F300-CDE-PRIORITY-PUR NOT = SPACES AND LOW-VALUE                  
              MOVE F300-CDE-PRIORITY-PUR TO K800-NUM-PRIORITY-PURCHASER.        
                                                                                
           MOVE F300-AMT-SOC-CASH    TO  K800-AMT-SOC-CASH-REQ.                 
           MOVE F300-AMT-SOC-CPF1    TO  K800-AMT-SOC-CPF1-REQ.                 
           MOVE F300-AMT-SOC-CPF2    TO  K800-AMT-SOC-CPF2-REQ.                 
           MOVE F300-AMT-SOC-CPF3    TO  K800-AMT-SOC-CPF3-REQ.                 
           MOVE F300-AMT-SOC-CPF4    TO  K800-AMT-SOC-CPF4-REQ.                 
           MOVE F300-AMT-SOC-SERCNTR TO  K800-AMT-SOC-SERCNTR-REQ.              
                                                                                
           COMPUTE WS-SOC-DEBTOR =   K800-AMT-SOC-CASH-REQ    +                 
                                     K800-AMT-SOC-SERCNTR-REQ +                 
                                     K800-AMT-SOC-CPF1-REQ    +                 
                                     K800-AMT-SOC-CPF2-REQ    +                 
                                     K800-AMT-SOC-CPF3-REQ    +                 
                                     K800-AMT-SOC-CPF4-REQ.                     
                                                                                
           ADD WS-SOC-DEBTOR         TO  K800-AMT-SOC-DEBTOR.                   
                                                                                
           MOVE F300-AMT-LEASE-STAMP    TO  K800-AMT-LEASE-FEE.                 
           MOVE F300-AMT-CONVEY         TO  K800-AMT-CONVEY-FEE.                
           MOVE F300-AMT-CONVEY-GST     TO  K800-AMT-CONVEY-FEE-GST.            
                                                                                
           MOVE F300-AMT-MORT-CONVY     TO  K800-AMT-MORT-CONVY.                
           MOVE F300-AMT-MORT-CONVY-GST TO  K800-AMT-MORT-CONVY-GST.            
           MOVE F300-AMT-CAVEAT-REGN    TO  K800-AMT-CAVEAT-REGN.               
                                                                                
           MOVE F300-AMT-CD-REQ         TO  K800-AMT-CD.                        
           MOVE F300-AMT-BK-TRANSFER    TO  K800-AMT-BK-TR.                     
                                                                                
           IF F300-AMT-BK-TRANSFER > K800-AMT-BK-PAID                           
              MOVE F300-CUR-REG                TO PRT-REGNO                     
              MOVE 'BK FEE TR > BK FEE PAID  ' TO PRT-REMARKS                   
              PERFORM 4000-PRINT-ERR THRU 4000-EXIT                             
              WRITE BP13KIV-REC FROM BP13F300-REC                               
              ADD 1                            TO WS-KIV-OUTPUT                 
              GO TO 3000-READ-BP13F300                                          
           ELSE                                                                 
              ADD      F300-AMT-BK-TRANSFER    TO   K800-AMT-SOC-CASH           
              SUBTRACT F300-AMT-BK-TRANSFER    FROM K800-AMT-BK-PAID.           
                                                                                
           IF F300-AMT-REGN-DEPOSIT-CASH IS NOT NUMERIC                         
              MOVE ZEROES TO F300-AMT-REGN-DEPOSIT-CASH.                        
                                                                                
           IF F300-AMT-REGN-DEPOSIT-CPF1 IS NOT NUMERIC                         
              MOVE ZEROES TO F300-AMT-REGN-DEPOSIT-CPF1.                        
                                                                                
           IF F300-AMT-REGN-DEPOSIT-CPF2 IS NOT NUMERIC                         
              MOVE ZEROES TO F300-AMT-REGN-DEPOSIT-CPF2.                        
                                                                                
           IF F300-AMT-REGN-DEPOSIT-CPF3 IS NOT NUMERIC                         
              MOVE ZEROES TO F300-AMT-REGN-DEPOSIT-CPF3.                        
                                                                                
           IF F300-AMT-REGN-DEPOSIT-CPF4 IS NOT NUMERIC                         
              MOVE ZEROES TO F300-AMT-REGN-DEPOSIT-CPF4.                        
                                                                                
           COMPUTE WS-AMT-REGN-DEP = F300-AMT-REGN-DEPOSIT-CASH +               
                                     F300-AMT-REGN-DEPOSIT-CPF1 +               
                                     F300-AMT-REGN-DEPOSIT-CPF2 +               
                                     F300-AMT-REGN-DEPOSIT-CPF3 +               
                                     F300-AMT-REGN-DEPOSIT-CPF4.                
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CASH IS NOT NUMERIC                         
              MOVE ZEROES TO K800-AMT-REGN-DEPOSIT-CASH.                        
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CPF1 IS NOT NUMERIC                         
              MOVE ZEROES TO K800-AMT-REGN-DEPOSIT-CPF1.                        
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CPF2 IS NOT NUMERIC                         
              MOVE ZEROES TO K800-AMT-REGN-DEPOSIT-CPF2.                        
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CPF3 IS NOT NUMERIC                         
              MOVE ZEROES TO K800-AMT-REGN-DEPOSIT-CPF3.                        
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CPF4 IS NOT NUMERIC                         
              MOVE ZEROES TO K800-AMT-REGN-DEPOSIT-CPF4.                        
                                                                                
           COMPUTE WS-AMT-REGN-DEP-K800 = K800-AMT-REGN-DEPOSIT-CASH +          
                                          K800-AMT-REGN-DEPOSIT-CPF1 +          
                                          K800-AMT-REGN-DEPOSIT-CPF2 +          
                                          K800-AMT-REGN-DEPOSIT-CPF3 +          
                                          K800-AMT-REGN-DEPOSIT-CPF4.           
                                                                                
           IF WS-AMT-REGN-DEP  NOT = WS-AMT-REGN-DEP-K800                       
              MOVE F300-CUR-REG               TO PRT-REGNO                      
              MOVE 'REGN DEPOSIT NOT = K800 ' TO PRT-REMARKS                    
              PERFORM 4000-PRINT-ERR THRU 4000-EXIT                             
              WRITE BP13KIV-REC FROM BP13F300-REC                               
              ADD 1                           TO WS-KIV-OUTPUT                  
              GO TO 3000-READ-BP13F300.                                         
                                                                                
           IF WS-AMT-REGN-DEP  NOT = 0 AND 5000                                 
              MOVE F300-CUR-REG                TO PRT-REGNO                     
              MOVE 'REGN DEPOSIT NOT = $5000'  TO PRT-REMARKS                   
              PERFORM 4000-PRINT-ERR THRU 4000-EXIT                             
              WRITE BP13KIV-REC FROM BP13F300-REC                               
              ADD 1                            TO WS-KIV-OUTPUT                 
              GO TO 3000-READ-BP13F300                                          
           ELSE                                                                 
              IF WS-AMT-REGN-DEP  = 5000                                        
                                                                                
                ADD F300-AMT-REGN-DEPOSIT-CASH TO  K800-AMT-SOC-CASH            
                SUBTRACT F300-AMT-REGN-DEPOSIT-CASH FROM                        
                         K800-AMT-REGN-DEPOSIT-CASH                             
                                                                                
                ADD F300-AMT-REGN-DEPOSIT-CPF1 TO  K800-AMT-CPF1-PAID           
                                                   K800-AMT-CD-CPF1             
                                                   WS-CD-CPF1                   
                SUBTRACT F300-AMT-REGN-DEPOSIT-CPF1 FROM                        
                         K800-AMT-REGN-DEPOSIT-CPF1                             
                                                                                
                ADD F300-AMT-REGN-DEPOSIT-CPF2 TO  K800-AMT-CPF2-PAID           
                                                   K800-AMT-CD-CPF2             
                                                   WS-CD-CPF2                   
                SUBTRACT F300-AMT-REGN-DEPOSIT-CPF2 FROM                        
                         K800-AMT-REGN-DEPOSIT-CPF2                             
                                                                                
                ADD F300-AMT-REGN-DEPOSIT-CPF3 TO  K800-AMT-CPF3-PAID           
                                                   K800-AMT-CD-CPF3             
                                                   WS-CD-CPF3                   
                SUBTRACT F300-AMT-REGN-DEPOSIT-CPF3 FROM                        
                         K800-AMT-REGN-DEPOSIT-CPF3                             
                                                                                
                ADD F300-AMT-REGN-DEPOSIT-CPF4 TO  K800-AMT-CPF4-PAID           
                                                   K800-AMT-CD-CPF4             
                                                   WS-CD-CPF4                   
                SUBTRACT F300-AMT-REGN-DEPOSIT-CPF4 FROM                        
                         K800-AMT-REGN-DEPOSIT-CPF4.                            
                                                                                
           COMPUTE WS-CD-CASH = F300-AMT-TOPUP-CASH +                           
                                F300-AMT-REGN-DEPOSIT-CASH +                    
                                F300-AMT-BK-TRANSFER.                           
                                                                                
           MOVE WS-CD-CASH              TO  K800-AMT-CD-CASH.                   
                                                                                
           IF K800-AMT-CD-CPF1 IS NOT NUMERIC                                   
              MOVE ZEROES TO K800-AMT-CD-CPF1.                                  
                                                                                
           IF K800-AMT-CD-CPF2 IS NOT NUMERIC                                   
              MOVE ZEROES TO K800-AMT-CD-CPF2.                                  
                                                                                
           IF K800-AMT-CD-CPF3 IS NOT NUMERIC                                   
              MOVE ZEROES TO K800-AMT-CD-CPF3.                                  
                                                                                
           IF K800-AMT-CD-CPF4 IS NOT NUMERIC                                   
              MOVE ZEROES TO K800-AMT-CD-CPF4.                                  
                                                                                
           IF K800-AMT-CD-SERCNTR IS NOT NUMERIC                                
              MOVE ZEROES TO K800-AMT-CD-SERCNTR.                               
                                                                                
           IF K800-AMT-CD-SERCNTR-NEW IS NOT NUMERIC                            
              MOVE ZEROES TO K800-AMT-CD-SERCNTR-NEW.                           
                                                                                
           ADD  F300-AMT-TOPUP-CPF1     TO  K800-AMT-CD-CPF1.                   
           ADD  F300-AMT-TOPUP-CPF2     TO  K800-AMT-CD-CPF2.                   
           ADD  F300-AMT-TOPUP-CPF3     TO  K800-AMT-CD-CPF3.                   
           ADD  F300-AMT-TOPUP-CPF4     TO  K800-AMT-CD-CPF4.                   
           ADD  F300-AMT-SER-CONTRA-ICP TO  K800-AMT-CD-SERCNTR.                
                                                                                
           IF F300-AMT-SER-CONTRA-ICP-NEW IS NOT NUMERIC                        
              MOVE ZEROES TO F300-AMT-SER-CONTRA-ICP-NEW.                       
                                                                                
           IF F300-AMT-SER-CONTRA-ICP-NEW > 0                                   
              ADD F300-AMT-SER-CONTRA-ICP-NEW                                   
                                        TO  K800-AMT-CD-SERCNTR-NEW             
           ELSE                                                                 
              ADD F300-AMT-SER-CONTRA-ICP                                       
                                        TO  K800-AMT-CD-SERCNTR-NEW             
           END-IF.                                                              
                                                                                
           IF F300-AMT-SER-CONTRA-ICP-NEW > F300-AMT-SER-CONTRA-ICP             
              MOVE F300-AMT-SER-CONTRA-ICP-NEW                                  
                                        TO  WS-SER-CONTRA-ICP                   
           ELSE                                                                 
              MOVE F300-AMT-SER-CONTRA-ICP                                      
                                        TO  WS-SER-CONTRA-ICP                   
           END-IF.                                                              
                                                                                
           COMPUTE WS-CD = WS-CD-CASH          +                                
                           WS-CD-CPF1          +                                
                           WS-CD-CPF2          +                                
                           WS-CD-CPF3          +                                
                           WS-CD-CPF4          +                                
                           WS-SER-CONTRA-ICP   +                                
                           F300-AMT-TOPUP-CPF1 +                                
                           F300-AMT-TOPUP-CPF2 +                                
                           F300-AMT-TOPUP-CPF3 +                                
                           F300-AMT-TOPUP-CPF4.                                 
                                                                                
           MOVE WS-CD                TO  K800-AMT-CD.                           
                                                                                
      *------------------------------*                                          
      *  INITIALIZE WORK AREA        *                                          
      *------------------------------*                                          
           MOVE ZEROES              TO WS-CD-CASH.                              
           MOVE ZEROES              TO WS-CD-CPF1.                              
           MOVE ZEROES              TO WS-CD-CPF2.                              
           MOVE ZEROES              TO WS-CD-CPF3.                              
           MOVE ZEROES              TO WS-CD-CPF4.                              
                                                                                
       3100-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       3200-UPD-LESSEE-INFO.                                                    
      ******************************************************************        
           MOVE F300-CUR-REG  TO K820-NUM-REGN.                                 
                                                                                
           IF F300-NRIC1 NOT = K800-NUM-NRIC1                                   
              MOVE F300-NRIC1 TO K800-NUM-NRIC1.                                
                                                                                
           IF F300-NRIC2 NOT = K800-NUM-NRIC2                                   
              MOVE F300-NRIC2 TO K800-NUM-NRIC2.                                
                                                                                
           IF F300-NRIC3 NOT = K800-NUM-NRIC3                                   
              MOVE F300-NRIC3 TO K800-NUM-NRIC3.                                
                                                                                
           IF F300-NRIC4 NOT = K800-NUM-NRIC4                                   
              MOVE F300-NRIC4 TO K800-NUM-NRIC4.                                
                                                                                
           IF F300-NRIC1 NOT = SPACES AND LOW-VALUES                            
              MOVE F300-NRIC1 TO K820-NUM-NRIC                                  
              MOVE F300-NAME1 TO WS-NAME                                        
              MOVE F300-CPF1  TO WS-CPF                                         
              PERFORM 3210-UPD-APPL-NAME THRU 3210-EXIT.                        
                                                                                
           IF F300-NRIC2 NOT = SPACES AND LOW-VALUES                            
              MOVE F300-NRIC2 TO K820-NUM-NRIC                                  
              MOVE F300-NAME2 TO WS-NAME                                        
              MOVE F300-CPF2  TO WS-CPF                                         
              PERFORM 3210-UPD-APPL-NAME THRU 3210-EXIT.                        
                                                                                
           IF F300-NRIC3 NOT = SPACES AND LOW-VALUES                            
              MOVE F300-NRIC3 TO K820-NUM-NRIC                                  
              MOVE F300-NAME3 TO WS-NAME                                        
              MOVE F300-CPF3  TO WS-CPF                                         
              PERFORM 3210-UPD-APPL-NAME THRU 3210-EXIT.                        
                                                                                
           IF F300-NRIC4 NOT = SPACES AND LOW-VALUES                            
              MOVE F300-NRIC4 TO K820-NUM-NRIC                                  
              MOVE F300-NAME4 TO WS-NAME                                        
              MOVE F300-CPF4  TO WS-CPF                                         
              PERFORM 3210-UPD-APPL-NAME THRU 3210-EXIT.                        
                                                                                
           IF F300-CDE-TRANS = 'SA'                                             
              MOVE 'AG'           TO K800-NUM-ALLOC-TAG                         
           ELSE                                                                 
              NEXT SENTENCE.                                                    
                                                                                
           IF F300-IDP-SCH NOT    =  K800-NUM-IDP-SCHEME                        
              MOVE F300-IDP-SCH   TO K800-NUM-IDP-SCHEME.                       
                                                                                
           IF F300-ELIG-SCH NOT   =  K800-NUM-ELIG-SCHEME                       
              MOVE F300-ELIG-SCH  TO K800-NUM-ELIG-SCHEME.                      
                                                                                
           IF F300-ALLOC-SCH NOT  =  K800-NUM-ALLO-SCHEME                       
              MOVE F300-ALLOC-SCH TO K800-NUM-ALLO-SCHEME.                      
                                                                                
           IF F300-PR-TAG1 NOT  =  K800-NUM-APPL1-PR                            
              MOVE F300-PR-TAG1 TO K800-NUM-APPL1-PR.                           
                                                                                
           IF F300-PR-TAG2 NOT  =  K800-NUM-APPL2-PR                            
              MOVE F300-PR-TAG2 TO K800-NUM-APPL2-PR.                           
                                                                                
           IF F300-PR-TAG3 NOT  =  K800-NUM-APPL3-PR                            
              MOVE F300-PR-TAG3 TO K800-NUM-APPL3-PR.                           
                                                                                
           IF F300-PR-TAG4 NOT  =  K800-NUM-APPL4-PR                            
              MOVE F300-PR-TAG4 TO K800-NUM-APPL4-PR.                           
                                                                                
           IF F300-DTE-BOND-EXP NOT = SPACES                                    
              MOVE F300-DTE-BOND-EXP TO K800-DTE-BOND-EXP.                      
                                                                                
           IF F300-CPF4 NOT = SPACES AND LOW-VALUE                              
              MOVE 4 TO K800-NUM-JT-APPL                                        
           ELSE                                                                 
              IF F300-CPF3 NOT = SPACES AND LOW-VALUE                           
                 MOVE 3 TO K800-NUM-JT-APPL                                     
              ELSE                                                              
                 IF F300-CPF2 NOT = SPACES AND LOW-VALUE                        
                    MOVE 2 TO K800-NUM-JT-APPL                                  
                 ELSE                                                           
                    MOVE 1 TO K800-NUM-JT-APPL.                                 
                                                                                
       3200-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       3210-UPD-APPL-NAME.                                                      
      ******************************************************************        
           READ BP13K820 INVALID KEY                                            
              DISPLAY 'RECORD NOT FOUND IN BP13K820 ' K820-KEY-FLD              
              MOVE SPACES TO WS-NAME WS-CPF                                     
              GO TO 3210-EXIT.                                                  
                                                                                
           IF WS-CPF  = K820-NUM-CPF  AND                                       
              WS-NAME = K820-NME-OCCP                                           
              NEXT SENTENCE                                                     
           ELSE                                                                 
              MOVE WS-NAME TO K820-NME-OCCP                                     
              MOVE WS-CPF  TO K820-NUM-CPF                                      
              PERFORM 6000-WRT-BP13F915-PSN THRU 6000-EXIT                      
              REWRITE BP13K820-REC                                              
              IF WS-K820-FILE-STATUS NOT = ZEROES                               
                 DISPLAY 'ERROR IN UPDATING FILE BP13K820 '                     
                         WS-K820-FILE-STATUS                                    
                 GO TO 3210-EXIT                                                
              ELSE                                                              
                 ADD 1       TO WS-K820-REWRITE                                 
                 MOVE SPACES TO WS-NAME WS-CPF                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3210-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       3300-UPDATE-FILE.                                                        
      ******************************************************************        
                                                                                
           IF K800-NUM-ALLO-SCHEME = 'MGL'                                      
              PERFORM 3301-PROCESS-BP13K480 THRU 3301-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-REGN TO K840-NUM-REGN.                                 
                                                                                
           READ BP13K840 INVALID KEY                                            
                DISPLAY 'RECORD NOT FOUND IN BP13K840 ' K840-KEY-FLD            
                MOVE WS-K840-FILE-STATUS TO RETURN-CODE                         
                GO TO 3300-EXIT.                                                
                                                                                
           MOVE F300-DTE-AGRM-ORDER TO K840-DTE-SA.                             
           MOVE F300-IDP-SCH        TO K840-NUM-IDP-SCH.                        
                                                                                
           REWRITE BP13K840-REC.                                                
                                                                                
           IF WS-K840-FILE-STATUS NOT = ZEROES                                  
              DISPLAY 'ERROR IN UPDATING BP13K840 ' WS-K840-FILE-STATUS         
              MOVE WS-K840-FILE-STATUS TO RETURN-CODE                           
              GO TO 3300-EXIT.                                                  
                                                                                
           ADD 1 TO WS-K840-OUTPUT.                                             
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO K800-DTE-UPDATE.                  
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF WS-K800-FILE-STATUS NOT = ZEROS                                   
              DISPLAY 'ERROR IN UPDATING BP13K800 ' WS-K800-FILE-STATUS         
              MOVE WS-K800-FILE-STATUS TO RETURN-CODE                           
              GO TO 3300-EXIT.                                                  
                                                                                
           ADD 1 TO WS-K800-OUTPUT.                                             
                                                                                
           WRITE BP13OUT-REC FROM BP13F300-REC.                                 
           ADD 1             TO   WS-OUT-OUTPUT.                                
                                                                                
       3300-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3301-PROCESS-BP13K480.                                                   
      ******************************************************************        
                                                                                
           MOVE SPACES        TO BP13K480-REC.                                  
           INITIALIZE            BP13K480-REC.                                  
                                                                                
           MOVE F300-CUR-REG  TO K480-NUM-REGN.                                 
                                                                                
           READ BP13K480.                                                       
                                                                                
           IF WS-K480-FILE-STATUS  = 00                                         
              PERFORM 3302-REWRITE-K480 THRU 3302-EXIT                          
           ELSE                                                                 
              IF WS-K480-FILE-STATUS  = 23                                      
                 PERFORM 3303-CREATE-BP13K480 THRU 3303-EXIT                    
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K480 = ' F300-CUR-REG               
                 DISPLAY 'FILE STATUS = ' WS-K480-FILE-STATUS                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3301-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3302-REWRITE-K480.                                                       
      ******************************************************************        
                                                                                
           MOVE 'BP13C301'           TO K480-USERID.                            
           MOVE F300-AMT-LEASE-STAMP TO K480-AMT-LRA-CONTRA-SF-REQ.             
           MOVE F300-AMT-CONVEY      TO K480-AMT-INT-BILLING-CF-REQ.            
                                                                                
           REWRITE BP13K480-REC.                                                
                                                                                
           IF WS-K480-FILE-STATUS = 00                                          
              ADD 1    TO WS-K480-REWRITE                                       
           ELSE                                                                 
              DISPLAY 'ERROR REWRITE BP13K480, STATUS '                         
              DISPLAY  WS-K480-FILE-STATUS                                      
              DISPLAY '      K480-KEY-FLD    ' K480-KEY-FLD                     
              MOVE WS-K480-FILE-STATUS TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
       3302-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3303-CREATE-BP13K480.                                                    
      ******************************************************************        
                                                                                
           MOVE SPACES             TO BP13K480-REC.                             
           INITIALIZE                 BP13K480-REC.                             
                                                                                
           MOVE  F300-CUR-REG      TO  K480-NUM-REGN.                           
           MOVE  F300-SCH-ACC      TO  K480-SCH-ACC.                            
           MOVE  F300-NRIC1        TO  K480-NUM-NRIC1.                          
           MOVE  F300-NRIC2        TO  K480-NUM-NRIC2.                          
           MOVE  F300-NRIC3        TO  K480-NUM-NRIC3.                          
           MOVE  F300-NRIC4        TO  K480-NUM-NRIC4.                          
           MOVE  F300-CPF1         TO  K480-NUM-CPF1.                           
           MOVE  F300-CPF2         TO  K480-NUM-CPF2.                           
           MOVE  F300-CPF3         TO  K480-NUM-CPF3.                           
           MOVE  F300-CPF4         TO  K480-NUM-CPF4.                           
           MOVE  'BP13C301'        TO  K480-USERID.                             
           MOVE  FUNCTION CURRENT-DATE(1:8)  TO  K480-DTE-UPDATE.               
                                                                                
           MOVE F300-AMT-LEASE-STAMP TO K480-AMT-LRA-CONTRA-SF-REQ.             
           MOVE F300-AMT-CONVEY      TO K480-AMT-INT-BILLING-CF-REQ.            
                                                                                
           WRITE BP13K480-REC.                                                  
                                                                                
           IF WS-K480-FILE-STATUS = 00                                          
              ADD 1    TO WS-K480-WRITE                                         
           ELSE                                                                 
              DISPLAY 'ERROR WRITE BP13K480, STATUS '                           
              DISPLAY  WS-K480-FILE-STATUS                                      
              DISPLAY '      K480-KEY-FLD    ' K480-KEY-FLD                     
              MOVE WS-K480-FILE-STATUS TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
       3303-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3400-VAL-USED-GRANT.                                                     
      ******************************************************************        
           EVALUATE WS-CTR                                                      
              WHEN '1'                                                          
                 IF F300-AMT-AHG-CPF1-USED > 0                                  
                    MOVE F300-NRIC1          TO   WS-NRIC                       
                    PERFORM 3410-READ-BP13KD05 THRU 3410-EXIT                   
                    IF WS-KD05-FILE-STATUS = 00                                 
                       COMPUTE WS-TOT-USED-GR = KD05-AMT-AHG-USED +             
                                                F300-AMT-AHG-CPF1-USED          
                       PERFORM 3420-UPDATE-BP13KD05 THRU 3420-EXIT              
                    END-IF                                                      
                 END-IF                                                         
              WHEN '2'                                                          
                 IF F300-NRIC2 NOT = SPACES AND LOW-VALUES                      
                    IF F300-AMT-AHG-CPF2-USED > 0                               
                       MOVE F300-NRIC2          TO   WS-NRIC                    
                       PERFORM 3410-READ-BP13KD05 THRU 3410-EXIT                
                       IF WS-KD05-FILE-STATUS = 00                              
                          COMPUTE WS-TOT-USED-GR = KD05-AMT-AHG-USED +          
                                                 F300-AMT-AHG-CPF2-USED         
                          PERFORM 3420-UPDATE-BP13KD05 THRU 3420-EXIT           
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
                                                                                
              WHEN '3'                                                          
                 IF F300-NRIC3 NOT = SPACES AND LOW-VALUES                      
                    IF F300-AMT-AHG-CPF3-USED > 0                               
                       MOVE F300-NRIC3          TO   WS-NRIC                    
                       PERFORM 3410-READ-BP13KD05 THRU 3410-EXIT                
                       IF WS-KD05-FILE-STATUS = 00                              
                          COMPUTE WS-TOT-USED-GR = KD05-AMT-AHG-USED +          
                                                 F300-AMT-AHG-CPF3-USED         
                          PERFORM 3420-UPDATE-BP13KD05 THRU 3420-EXIT           
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
                                                                                
              WHEN '4'                                                          
                 IF F300-NRIC4 NOT = SPACES AND LOW-VALUES                      
                    IF F300-AMT-AHG-CPF4-USED > 0                               
                       MOVE F300-NRIC4          TO   WS-NRIC                    
                       PERFORM 3410-READ-BP13KD05 THRU 3410-EXIT                
                       IF WS-KD05-FILE-STATUS = 00                              
                          COMPUTE WS-TOT-USED-GR = KD05-AMT-AHG-USED +          
                                                 F300-AMT-AHG-CPF4-USED         
                          PERFORM 3420-UPDATE-BP13KD05 THRU 3420-EXIT           
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
                                                                                
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       3410-READ-BP13KD05.                                                      
      ******************************************************************        
           MOVE F300-CUR-REG             TO KD05-NUM-REGN.                      
           MOVE WS-NRIC                  TO KD05-NUM-NRIC.                      
                                                                                
           READ BP13KD05.                                                       
                                                                                
           IF WS-KD05-FILE-STATUS = 23                                          
              DISPLAY 'RECORD NOT FOUND IN BP13KD05 ' KD05-KEY-FLD              
              MOVE WS-KD05-FILE-STATUS TO RETURN-CODE                           
           ELSE                                                                 
              IF WS-KD05-FILE-STATUS NOT EQUAL ZEROES                           
                 DISPLAY 'ERROR READING BP13KD05, STATUS '                      
                 DISPLAY WS-KD05-FILE-STATUS                                    
                 DISPLAY '      KD05-KEY-FLD    ' KD05-KEY-FLD                  
                 MOVE WS-KD05-FILE-STATUS TO RETURN-CODE                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3410-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       3420-UPDATE-BP13KD05.                                                    
      ******************************************************************        
           MOVE WS-TOT-USED-GR             TO KD05-AMT-AHG-USED.                
           MOVE 'P13C301'                  TO KD05-NUM-USERID.                  
           MOVE FUNCTION CURRENT-DATE(1:8) TO KD05-DTE-UPDATE.                  
           MOVE FUNCTION CURRENT-DATE(9:8) TO KD05-TME-UPDATE.                  
                                                                                
           REWRITE BP13KD05-REC.                                                
                                                                                
           IF WS-KD05-FILE-STATUS = 00                                          
              ADD 1    TO WS-KD05-REWRITE                                       
           ELSE                                                                 
              DISPLAY 'ERROR REWRITE BP13KD05, STATUS '                         
              DISPLAY  WS-KD05-FILE-STATUS                                      
              DISPLAY '      KD05-KEY-FLD    ' KD05-KEY-FLD                     
              MOVE WS-KD05-FILE-STATUS TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           MOVE SPACES TO WS-NRIC.                                              
           INITIALIZE     WS-TOT-USED-GR.                                       
                                                                                
       3420-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       4000-PRINT-ERR.                                                          
      ******************************************************************        
           IF WS-LINE > 55                                                      
              ADD 1              TO   WS-PAGE                                   
              MOVE WS-PAGE       TO   PRINT-PAGE                                
              WRITE BP13L301-REC FROM PRINT-ERR1                                
              WRITE BP13L301-REC FROM PRINT-ERR2 AFTER 3                        
              MOVE 0             TO   WS-LINE                                   
              MOVE SPACES        TO   BP13L301-REC                              
              WRITE BP13L301-REC AFTER 2.                                       
                                                                                
           WRITE BP13L301-REC    FROM PRINT-ERR3 AFTER 2.                       
           ADD 1                 TO   WS-LINE.                                  
                                                                                
       4000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5000-PRINT-CONTROL.                                                      
      ******************************************************************        
           WRITE BP13L301-REC FROM  PRINT-HDG1 AFTER PAGE.                      
           MOVE  SPACES       TO    BP13L301-REC.                               
           WRITE BP13L301-REC AFTER 2.                                          
                                                                                
           MOVE 'TOTAL RECORDS PROCESSED   ' TO   PRINT-DESC.                   
           WRITE BP13L301-REC                FROM PRINT-REC AFTER 2.            
                                                                                
           MOVE WS-F300-INPUT                     TO PRINT-TOTAL.               
           MOVE '      BP13F300 READ          : ' TO PRINT-DESC.                
           WRITE BP13L301-REC FROM PRINT-REC AFTER 3.                           
                                                                                
           MOVE WS-K800-OUTPUT                     TO PRINT-TOTAL.              
           MOVE '      UPDATE TO BP13K800     : ' TO PRINT-DESC.                
           WRITE BP13L301-REC FROM PRINT-REC AFTER 3.                           
                                                                                
           MOVE WS-KIV-OUTPUT                     TO PRINT-TOTAL.               
           MOVE '      WRITE  TO BP13KIV      : ' TO PRINT-DESC.                
           WRITE BP13L301-REC FROM PRINT-REC AFTER 3.                           
                                                                                
           MOVE SPACES TO BP13L301-REC.                                         
           WRITE BP13L301-REC AFTER 2.                                          
                                                                                
           MOVE '          SUB CONTROL OF RECORDS'  TO BP13L301-REC.            
           WRITE BP13L301-REC AFTER 3.                                          
                                                                                
           MOVE WS-K840-OUTPUT                    TO PRINT-TOTAL.               
           MOVE '      REWRITE TO BP13K840    : ' TO PRINT-DESC.                
           WRITE BP13L301-REC FROM PRINT-REC AFTER 2.                           
                                                                                
           MOVE WS-K820-REWRITE                   TO PRINT-TOTAL.               
           MOVE '      REWRITE TO BP13K820    : ' TO PRINT-DESC.                
           WRITE BP13L301-REC FROM PRINT-REC AFTER 3.                           
                                                                                
           MOVE WS-F915-WRITE                     TO PRINT-TOTAL.               
           MOVE '      WRITE   TO BP13F915    : ' TO PRINT-DESC.                
           WRITE BP13L301-REC FROM PRINT-REC AFTER 3.                           
                                                                                
           MOVE WS-KD05-REWRITE                   TO PRINT-TOTAL.               
           MOVE '      REWRITE TO BP13KD05    : ' TO PRINT-DESC.                
           WRITE BP13L301-REC FROM PRINT-REC AFTER 3.                           
                                                                                
           MOVE WS-K480-REWRITE                   TO PRINT-TOTAL.               
           MOVE '      REWRITE TO BP13K480    : ' TO PRINT-DESC.                
           WRITE BP13L301-REC FROM PRINT-REC AFTER 3.                           
                                                                                
           MOVE WS-K480-WRITE                     TO PRINT-TOTAL.               
           MOVE '      WRITE TO BP13K480      : ' TO PRINT-DESC.                
           WRITE BP13L301-REC FROM PRINT-REC AFTER 3.                           
                                                                                
       5000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       6000-WRT-BP13F915-PSN.                                                   
      ******************************************************************        
           INITIALIZE  BP13F915-REC.                                            
           MOVE SPACES                TO   BP13F915-REC.                        
                                                                                
           MOVE  'UA'                 TO   F915-TXN-TYPE.                       
           MOVE  'P'                  TO   F915-CSTMR-TYPE.                     
           MOVE  'BP13'               TO   F915-NUM-UPDATE-SOURCE.              
           MOVE  'BP13C301'           TO   F915-NUM-UPDATE-USERID.              
                                                                                
           MOVE   K820-NUM-CSTMR-SOURCE                                         
                                      TO   F915-NUM-CSTMR-SOURCE.               
                                                                                
           MOVE  K820-NUM-REGN        TO   F915-NUM-REGN.                       
           MOVE  K820-NUM-NRIC        TO   F915-NUM-NRIC.                       
                                                                                
           MOVE  K820-NUM-NATIONALITY TO   F915-NUM-NTNLTY.                     
           MOVE  K820-NUM-CITIZENSHIP TO   F915-NUM-PR-STATUS.                  
           MOVE  K820-NUM-MARITAL-STATUS                                        
                                      TO   F915-NUM-MRTL-STATUS.                
           MOVE  K820-NUM-SEX         TO   F915-NUM-SEX.                        
           MOVE  K820-NUM-ETHNIC      TO   F915-NUM-ETHNIC-GROUP.               
           MOVE  K820-NME-OCCP        TO   F915-NUM-NEW-NME-PERSON.             
                                                                                
           STRING K820-DTE-BIRTH(1:4) '-'  K820-DTE-BIRTH(5:2) '-'              
                  K820-DTE-BIRTH(7:2) DELIMITED BY SIZE                         
                  INTO F915-DTE-BIRTH.                                          
                                                                                
           PERFORM 6100-WRITE-BP13F915     THRU  6100-EXIT.                     
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       6100-WRITE-BP13F915.                                                     
      ******************************************************************        
           WRITE BP13F915-REC.                                                  
           ADD  1   TO  WS-F915-WRITE.                                          
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       9999-FINALIZATION.                                                       
      ******************************************************************        
            CLOSE     BP13F300                                                  
                      BP13K480                                                  
                      BP13K800                                                  
                      BP13K840                                                  
                      BP13K820                                                  
                      BP13KD05                                                  
                      BP13OUT                                                   
                      BP13KIV                                                   
                      BP13L301                                                  
                      BP13F915.                                                 
                                                                                
           IF WS-K800-FILE-STATUS NOT = ZEROES                                  
              DISPLAY 'BP13K800 - CLOSE ERROR '                                 
              MOVE WS-K800-FILE-STATUS TO RETURN-CODE.                          
                                                                                
           IF WS-K820-FILE-STATUS NOT = ZEROES                                  
              DISPLAY 'BP13K820 - CLOSE ERROR '                                 
              MOVE WS-K820-FILE-STATUS TO RETURN-CODE.                          
                                                                                
           IF WS-K480-FILE-STATUS NOT = ZEROES                                  
              DISPLAY 'BP13K480 - CLOSE ERROR '                                 
              MOVE WS-K480-FILE-STATUS TO RETURN-CODE.                          
                                                                                
           IF WS-K840-FILE-STATUS NOT = ZEROES                                  
              DISPLAY 'BP13K840 - CLOSE ERROR '                                 
              MOVE WS-K840-FILE-STATUS TO RETURN-CODE.                          
                                                                                
           IF WS-KD05-FILE-STATUS NOT = ZEROES                                  
              DISPLAY 'BP13KD05 - CLOSE ERROR '                                 
              MOVE WS-KD05-FILE-STATUS TO RETURN-CODE.                          
                                                                                
           STOP RUN.                                                            
