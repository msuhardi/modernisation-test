       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB67.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  20/02/2018.                                               
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE  :  EXTRACT APPLICATIONS AFFECTED BY EIP           *         
      *                                                               *         
      *  INPUT      :  BP13F740 - RECORDS TO BE PROCESSED             *         
      *                BP13K010 - MAX QUOTA                           *         
      *  OUTPUT     :  P13F740A - BEYOND MAX-PA1/2/3 FROM 1ST HUNDRED *         
      *                                                               *         
      *===============================================================*         
      * CHG-NO    DATE      BY    DESCRIPTION                         *         
      * --------  --------  ----  ----------------------------------- *         
      * BP13XXXX  20022018  SMR2  NEW PROGRAM                         *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN       TO BP13F740.                            
                                                                                
           SELECT BP13K010 ASSIGN       TO BP13K010                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K010-KEY-FLD                         
                           FILE STATUS  IS WS-K010-STATUS.                      
                                                                                
           SELECT P13F740A ASSIGN       TO P13F740A.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K010                                                            
            RECORD CONTAINS 600 CHARACTERS.                                     
       COPY BP13K010.                                                           
                                                                                
       FD   P13F740A                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       01   P13F740A-REC                    PIC X(500).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K010-STATUS               PIC 9(02)  VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ                 PIC 9(06)  VALUE ZEROES.            
           05  WS-K010-FOUND                PIC 9(06)  VALUE ZEROES.            
           05  WS-K010-NOTFND               PIC 9(06)  VALUE ZEROES.            
           05  WS-F740A-WRITE               PIC 9(06)  VALUE ZEROES.            
           05  WS-F740-BYPASS               PIC 9(06)  VALUE ZEROES.            
           05  WS-CAT1                      PIC 9(06)  VALUE ZEROES.            
           05  WS-CAT2                      PIC 9(06)  VALUE ZEROES.            
           05  WS-CAT3                      PIC 9(06)  VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF                  PIC X(01)  VALUE 'N'.               
           05  WS-K010-FND                  PIC X(01)  VALUE 'N'.               
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CUR-DATE                  PIC X(08)  VALUE SPACES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F740-KEY-FLD.                                                 
               10  WS-F740-NT               PIC X(03).                          
               10  WS-F740-FT               PIC X(02).                          
           05  WS-PREV-KEY-FLD.                                                 
               10  WS-PREV-NT               PIC X(03).                          
               10  WS-PREV-FT               PIC X(02).                          
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECS        THRU 3000-EXIT                      
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BP13F740                                                 
                       BP13K010                                                 
                OUTPUT P13F740A.                                                
                                                                                
           IF WS-K010-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR OPENING BP13K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-CUR-DATE.                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F740.                                                      
      *---------------------------------------------------------------*         
           READ BP13F740 AT END                                                 
                MOVE 'Y'                    TO WS-F740-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F740-READ.                    
                                                                                
           MOVE F740-CDE-NT1                TO WS-F740-NT.                      
           MOVE F740-CDE-FLAT-TYPE          TO WS-F740-FT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-RECS.                                                       
      *---------------------------------------------------------------*         
           IF WS-F740-KEY-FLD NOT = WS-PREV-KEY-FLD                             
              PERFORM 4000-READ-BP13K010    THRU 4000-EXIT                      
              MOVE F740-CDE-NT1             TO WS-PREV-NT                       
              MOVE F740-CDE-FLAT-TYPE       TO WS-PREV-FT                       
              IF WS-K010-FND = 'Y'                                              
                 MOVE ZEROES                TO WS-CAT1 WS-CAT2 WS-CAT3          
                 PERFORM 5000-WRITE-RECS    THRU 5000-EXIT                      
              ELSE                                                              
                 ADD 1                      TO WS-F740-BYPASS                   
              END-IF                                                            
           ELSE                                                                 
              IF WS-K010-FND = 'Y'                                              
                 PERFORM 5000-WRITE-RECS    THRU 5000-EXIT                      
              ELSE                                                              
                 ADD 1                      TO WS-F740-BYPASS                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-READ-BP13K010.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES                      TO BP13K010-TENDER.                 
           INITIALIZE                          BP13K010-TENDER.                 
                                                                                
           MOVE F740-CDE-NT1                TO K010-CDE-NT-ZONE.                
           MOVE F740-CDE-FLAT-TYPE          TO K010-CDE-FLAT-TY.                
                                                                                
           MOVE 'N'                         TO WS-K010-FND.                     
                                                                                
           READ BP13K010.                                                       
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN ZEROES                                                          
                MOVE 'Y'                    TO WS-K010-FND                      
                ADD 1                       TO WS-K010-FOUND                    
                                                                                
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K010 : ' K010-KEY-FLD          
                ADD 1                       TO WS-K010-NOTFND                   
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K010, STATUS ' WS-K010-STATUS        
                MOVE WS-K010-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5000-WRITE-RECS.                                                         
      *---------------------------------------------------------------*         
           IF F740-CDE-REQUEST-STATUS = 'S'                                     
              EVALUATE F740-CDE-CATEGORY                                        
              WHEN '1' ADD 1                TO WS-CAT1                          
              WHEN '2' ADD 1                TO WS-CAT2                          
              WHEN '3' ADD 1                TO WS-CAT3                          
              END-EVALUATE                                                      
                                                                                
              IF (F740-CDE-CATEGORY = '1' AND                                   
                  WS-CAT1 > K010-NUM-MAX-PA1) OR                                
                 (F740-CDE-CATEGORY = '2' AND                                   
                  WS-CAT2 > K010-NUM-MAX-PA2) OR                                
                 (F740-CDE-CATEGORY = '3' AND                                   
                  WS-CAT3 > K010-NUM-MAX-PA3)                                   
                 WRITE P13F740A-REC         FROM BP13F740-REC                   
                 ADD 1                      TO WS-F740A-WRITE                   
              ELSE                                                              
                 ADD 1                      TO WS-F740-BYPASS                   
              END-IF                                                            
           ELSE                                                                 
              ADD 1                         TO WS-F740-BYPASS                   
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-FILES.                                                        
      *---------------------------------------------------------------*         
           DISPLAY '*-----------------------------------------*'.               
           DISPLAY '   PROGRAM : BP13CB67           ' WS-CUR-DATE.              
           DISPLAY '*-----------------------------------------*'.               
           DISPLAY '  NO OF RECS READ FROM BP13F740  : ' WS-F740-READ.          
           DISPLAY '  NO OF RECS FOUND IN BP13K010   : ' WS-K010-FOUND.         
           DISPLAY '  NO OF RECS NOTFND IN BP13K010  : ' WS-K010-NOTFND.        
           DISPLAY '  NO OF RECS AFFECTED BY EIP     : ' WS-F740A-WRITE.        
           DISPLAY '  NO OF RECS BYPASSED            : ' WS-F740-BYPASS.        
           DISPLAY '*-----------------------------------------*'.               
                                                                                
           CLOSE BP13F740                                                       
                 BP13K010                                                       
                 P13F740A.                                                      
                                                                                
           IF WS-K010-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR CLOSING BP13K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
