       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF06.                                                 
      *AUTHOR.        ZDD1                                                      
      *DATE-WRITTEN.  26APR2010.                                                
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. TO CHECK IF THE CASES HAS ALREADY HAVE RI        *                
      *         TRANSACTION                                    *                
      *    INPUT FILES  : BP13FA40                             *                
      *    I-O            BP13KA40                             *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY  DATE      DETAILS                          *                
      *=======  === ========= =================================*                
      *BP133893 ZDD 20100426  NEW PGM                          *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FA40 ASSIGN TO BP13FA40.                                  
                                                                                
           SELECT P13FA40A ASSIGN TO P13FA40A                                   
                  FILE STATUS     IS WS-FA40A-STATUS.                           
                                                                                
           SELECT BP13KA40 ASSIGN TO BP13KA40                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KA40-KEY-FLD                               
                  FILE STATUS     IS WS-KA40-STATUS.                            
                                                                                
                                                                                
           SELECT P13FA40B ASSIGN TO P13FA40B.                                  
                                                                                
           SELECT P13FA40C ASSIGN TO P13FA40C.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13FA40                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13FA40.                                                           
                                                                                
       FD   P13FA40A                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 150 CHARACTERS.                                     
       01   P13FA40A-REC.                                                       
            05  FILLER           PIC X(16).                                     
            05  WS-SCH-ACC       PIC X(11).                                     
            05  FILLER           PIC X(123).                                    
                                                                                
       FD   BP13KA40                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13KA40.                                                           
                                                                                
                                                                                
       FD   P13FA40B                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 150 CHARACTERS.                                     
       01   P13FA40B-REC PIC X(150).                                            
                                                                                
       FD   P13FA40C                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 150 CHARACTERS.                                     
       01   P13FA40C-REC PIC X(150).                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-FA40-EOF                    PIC X(1)  VALUE 'N'.              
           05  WS-FA40B-EOF                   PIC X(1)  VALUE 'N'.              
           05  WS-KA40-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-FA40A-STATUS                PIC 9(2)  VALUE 0.                
           05  WS-CNT-READ-RI                 PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-READ-AI                 PIC 9(6)  VALUE ZEROS.            
           05  WS-WRITTEN-AI                  PIC 9(6)  VALUE ZEROS.            
           05  WS-WRITTEN-DEL                 PIC 9(6)  VALUE ZEROS.            
           05  WS-DELETED                     PIC 9(6)  VALUE ZEROS.            
           05  WS-FOUND                       PIC X(1)  VALUE 'N'.              
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
                                                                                
           PERFORM 2000-READ-FA40         THRU 2000-EXIT.                       
           PERFORM 2100-READ-FA40B        THRU 2100-EXIT.                       
                                                                                
           PERFORM 3000-PROCESS-FA40      THRU 3000-EXIT                        
                  UNTIL WS-FA40B-EOF = 'Y' OR WS-FA40-EOF = 'Y'.                
                                                                                
           PERFORM 4000-END-OF-FILE       THRU 4000-EXIT                        
                  UNTIL WS-FA40-EOF  = 'Y'.                                     
                                                                                
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13FA40                                                 
                       P13FA40A                                                 
                I-O    BP13KA40                                                 
                OUTPUT P13FA40B                                                 
                       P13FA40C.                                                
                                                                                
           IF WS-KA40-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KA40 OPEN ERROR, STATUS IS ' WS-KA40-STATUS          
              MOVE WS-KA40-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-FA40.                                                          
      ****************************************************************          
                                                                                
           READ BP13FA40                                                        
              AT END MOVE 'Y' TO WS-FA40-EOF                                    
              GO TO 2000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD  +1                TO WS-CNT-READ-AI.                            
                                                                                
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2100-READ-FA40B.                                                         
      ****************************************************************          
                                                                                
           READ P13FA40A                                                        
              AT END MOVE 'Y' TO WS-FA40B-EOF                                   
              GO TO 2100-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD  +1                TO WS-CNT-READ-RI.                            
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2200-READ-KA40.                                                          
      ****************************************************************          
                                                                                
           READ BP13KA40.                                                       
                                                                                
           EVALUATE WS-KA40-STATUS                                              
            WHEN 00                                                             
               MOVE 'Y' TO WS-FOUND                                             
            WHEN 23                                                             
               MOVE 'N' TO WS-FOUND                                             
            WHEN OTHER                                                          
              DISPLAY 'ERROR READING OF BP13KA40. STATUS IS '                   
                       WS-KA40-STATUS                                           
              MOVE WS-KA40-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2300-DELETE-AI.                                                          
      ****************************************************************          
                                                                                
           DELETE BP13KA40                                                      
                                                                                
           IF WS-KA40-STATUS NOT = ZEROES                                       
              DISPLAY 'ERROR DELETING  BP13KA40. STATUS IS '                    
                       WS-KA40-STATUS                                           
              MOVE WS-KA40-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-DELETED.                                                 
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-FA40.                                                       
      ****************************************************************          
                                                                                
           IF FA40-SCH-ACC =  WS-SCH-ACC                                        
              MOVE FA40-KEY-FLD  TO  KA40-KEY-FLD                               
              PERFORM 2200-READ-KA40 THRU 2200-EXIT                             
              IF WS-FOUND = 'Y'                                                 
                 WRITE P13FA40B-REC FROM BP13KA40-TRANS-REC                     
                 ADD 1 TO WS-WRITTEN-DEL                                        
                 PERFORM 2300-DELETE-AI    THRU 2300-EXIT                       
                 PERFORM 2000-READ-FA40    THRU 2000-EXIT                       
                 PERFORM 2100-READ-FA40B   THRU 2100-EXIT                       
              END-IF                                                            
           ELSE                                                                 
                                                                                
             IF FA40-SCH-ACC > WS-SCH-ACC                                       
                PERFORM 2100-READ-FA40B   THRU 2100-EXIT                        
             ELSE                                                               
                                                                                
               IF FA40-SCH-ACC < WS-SCH-ACC                                     
                 WRITE P13FA40C-REC   FROM BP13FA40-TRANS-REC                   
                 ADD 1 TO WS-WRITTEN-AI                                         
                PERFORM 2000-READ-FA40    THRU 2000-EXIT                        
               END-IF                                                           
                                                                                
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4000-END-OF-FILE.                                                        
      ****************************************************************          
                                                                                
           IF WS-FA40B-EOF = 'Y'                                                
              WRITE P13FA40C-REC   FROM BP13FA40-TRANS-REC                      
              ADD 1 TO WS-WRITTEN-AI                                            
              PERFORM 2000-READ-FA40    THRU 2000-EXIT                          
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13CF06 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF AI RECORDS READ      = '  WS-CNT-READ-AI.             
           DISPLAY 'NO OF RI RECORDS READ      = '  WS-CNT-READ-RI.             
           DISPLAY 'NO OF CASES DELETED        = '  WS-DELETED.                 
           DISPLAY 'NO OF CASES WRITTEN DEL    = '  WS-WRITTEN-DEL.             
           DISPLAY 'NO OF CASES WRITTEN AI     = '  WS-WRITTEN-AI.              
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           CLOSE BP13FA40                                                       
                 BP13KA40                                                       
                 P13FA40A                                                       
                 P13FA40B                                                       
                 P13FA40C.                                                      
                                                                                
           IF WS-KA40-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KA40 CLOSE ERROR, STATUS IS ' WS-KA40-STATUS         
              MOVE WS-KA40-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
