       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C983.                                                  
       AUTHOR.       KVL1.                                                      
      *DATE-WRITTEN. 31/05/2017.                                                
                                                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) TO CREATE IN-TRAY ALERT TO OIC FOR LATE     *            
      *                COMPLETION INTEREST USING BE08C008          *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP136797 20170531 KVL1  NEW PROGRAM                        *            
      * BP136797 20171006 KVL1  ADDED OUTPUT OF LIST OF CASES SENT *            
      *                         TO OIC                             *            
      * BP137701 20190301 ZS13  TO CATER FOR 4 MONTHS              *            
      **************************************************************            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
            SELECT BP13F800 ASSIGN           TO BP13F800.                       
            SELECT SY02F001 ASSIGN           TO SY02F001.                       
            SELECT BP13K022 ASSIGN           TO BP13K022                        
                            ORGANIZATION     IS INDEXED                         
                            ACCESS MODE      IS RANDOM                          
                            RECORD KEY       IS K022-KEY-FLD                    
                            ALTERNATE KEY    IS K022-NUM-REGN                   
                            FILE STATUS      IS BP13K022-STATUS.                
            SELECT BP13K024 ASSIGN           TO BP13K024                        
                            ORGANIZATION     IS INDEXED                         
                            ACCESS MODE      IS RANDOM                          
                            RECORD KEY       IS K024-KEY-FLD                    
                            FILE STATUS      IS BP13K024-STATUS.                
            SELECT BP13K365 ASSIGN           TO BP13K365                        
                            ORGANIZATION     IS INDEXED                         
                            ACCESS MODE      IS RANDOM                          
                            RECORD KEY       IS K365-KEY-FLD                    
                            FILE STATUS      IS BP13K365-STATUS.                
            SELECT BM06K110 ASSIGN           TO BM06K110                        
                            ORGANIZATION     IS INDEXED                         
                            ACCESS MODE      IS RANDOM                          
                            RECORD KEY       IS K110-KEY-FLD OF                 
                                                K110-REC                        
                            FILE STATUS      IS BM06K110-STATUS.                
            SELECT BM06K100 ASSIGN           TO BM06K100                        
                            ORGANIZATION     IS INDEXED                         
                            ACCESS MODE      IS RANDOM                          
                            RECORD KEY       IS K100-KEY-FLD                    
                            FILE STATUS      IS BM06K100-STATUS.                
            SELECT BB24K110 ASSIGN           TO BB24K110                        
                            ORGANIZATION     IS INDEXED                         
                            ACCESS MODE      IS DYNAMIC                         
                            RECORD KEY       IS K110-KEY-FLD OF                 
                                                BB24K110-REC                    
                            FILE STATUS      IS BB24K110-STATUS.                
            SELECT BB24K010 ASSIGN           TO BB24K010                        
                            ORGANIZATION     IS INDEXED                         
                            ACCESS MODE      IS RANDOM                          
                            RECORD KEY       IS K010-KEY-FLD                    
                            FILE STATUS      IS BB24K010-STATUS.                
            SELECT BB14K500 ASSIGN           TO BB14K500                        
                            ORGANIZATION     IS INDEXED                         
                            ACCESS MODE      IS RANDOM                          
                            RECORD KEY       IS K500-KEY-FLD                    
                            FILE STATUS      IS BB14K500-STATUS.                
            SELECT BP13L983 ASSIGN           TO BP13L983.                       
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 2000 CHARACTERS          
                                       RECORDING MODE IS F                      
                                       LABEL RECORDS ARE STANDARD.              
       COPY BP13F800.                                                           
                                                                                
       FD  BP13K022                                                             
                                       RECORD CONTAINS 100  CHARACTERS.         
       COPY BP13K022.                                                           
                                                                                
       FD  BP13K024                                                             
                                       RECORD CONTAINS 100  CHARACTERS.         
       COPY BP13K024.                                                           
                                                                                
       FD  BP13K365                                                             
                                       RECORD CONTAINS 200  CHARACTERS.         
       COPY BP13K365.                                                           
                                                                                
       FD  BM06K110                                                             
                                       RECORD CONTAINS 500  CHARACTERS.         
       COPY BM06K110.                                                           
                                                                                
       FD  BM06K100                                                             
                                       RECORD CONTAINS 500  CHARACTERS.         
       COPY BM06K100.                                                           
                                                                                
       FD  BB24K010                                                             
                                       RECORD CONTAINS 5000 CHARACTERS.         
       COPY BB24K010.                                                           
                                                                                
       FD  BB24K110                                                             
                                       RECORD CONTAINS 50   CHARACTERS.         
       COPY BB24K110.                                                           
                                                                                
       FD  BB14K500                                                             
                                       RECORD CONTAINS 400  CHARACTERS.         
       COPY BB14K500.                                                           
                                                                                
       FD   BP13L983                   RECORD CONTAINS 80   CHARACTERS          
                                       RECORDING MODE IS F                      
                                       LABEL RECORDS ARE OMITTED.               
                                                                                
       01 BP13L983-REC.                                                         
          05 L983-NUM-REGN             PIC X(08).                               
          05 L983-NUM-SCH-ACC          PIC X(11).                               
          05 L983-DTE-TP-ORIG          PIC X(08).                               
          05 L983-RESALE-FND           PIC X(01).                               
          05 L983-DTE-SENT             PIC X(10).                               
          05 L983-STAFF-NAME           PIC X(30).                               
          05 FILLER                    PIC X(12).                               
                                                                                
       COPY SY02F001.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CONSTANTS.                                                        
           05  WS-TXN-CREATE           PIC X(02)  VALUE 'C'.                    
           05  WS-SYSTEM-ID            PIC X(04)  VALUE 'BP13'.                 
           05  WS-NUM-PRCS-CTGRY       PIC X(04)  VALUE '0076'.                 
           05  WS-NUM-BRNCH            PIC X(06)  VALUE '000001'.               
           05  WS-DEFAULT-DTE          PIC X(10)  VALUE '0001-01-01'.           
           05  WS-PROGRAM-ID           PIC X(08)  VALUE 'BP13C983'.             
           05  WS-14-DAYS              PIC 9(02)  VALUE 14.                     
           05  WS-REMAINDER            PIC 9(01)  VALUE ZERO.           01190014
           05  WS-QUOTIENT             PIC 9(02)  VALUE ZEROES.         01200014
           05  WS-DAYS                 PIC 9(03)  VALUE ZEROES.         01200014
           05  WS-MONTHS2              PIC 9(03)  VALUE ZEROES.         01200014
           05  WS-CHECK-DATE.                                           01200014
               10  WS-CCYY             PIC 9(04)  VALUE ZEROES.         01200014
               10  WS-MM               PIC 9(02)  VALUE ZEROES.         01200014
               10  WS-DD               PIC 9(02)  VALUE ZEROES.         01200014
           05  WS-DUE-DAYS             PIC 9(03)  VALUE ZEROES.         01200014
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CURRENT-DTE-TME      PIC 9(14)  VALUE ZEROES.                 
           05  WS-DTE-TP-ORIG-TXT      PIC X(08)  VALUE SPACES.                 
           05  WS-DTE-TP-ORIG REDEFINES WS-DTE-TP-ORIG-TXT                      
                                       PIC 9(08).                               
           05  WS-DTE-CURRENT          PIC X(10)  VALUE SPACES.                 
           05  WS-TME-CURRENT          PIC X(08)  VALUE SPACES.                 
           05  WS-DTE-TEMP             PIC 9(08)  VALUE ZEROES.                 
           05  WS-DTE-CURR             PIC 9(08)  VALUE ZEROES.                 
           05  WS-INT-TEMP             PIC 9(08)  VALUE ZEROES.                 
           05  WS-LESS14               PIC 9(08)  VALUE ZEROES.                 
           05  WS-DAY14                PIC 9(08)  VALUE ZEROES.                 
           05  WS-INT-CURR             PIC 9(08)  VALUE ZEROES.                 
           05  WS-INT-TP-ORIG          PIC 9(08)  VALUE ZEROES.                 
           05  WS-DIFF                 PIC 9(08)  VALUE ZEROES.                 
           05  WS-COMP-INT-TP-ORIG     PIC 9(08)  VALUE ZEROES.                 
           05  WS-COMP-DTE             PIC 9(08)  VALUE ZEROES.                 
           05  WS-COMP-DTE-TXT         PIC X(10)  VALUE SPACES.                 
           05  WS-DTE-DUE.                                                      
               10 WS-DTE-CCYY          PIC X(04).                               
               10 FILLER               PIC X(01)  VALUE '-'.                    
               10 WS-DTE-MM            PIC X(02).                               
               10 FILLER               PIC X(01)  VALUE '-'.                    
               10 WS-DTE-DD            PIC X(02).                               
                                                                                
       01  WS-STATUS.                                                           
           05  BP13K022-STATUS         PIC 9(02)  VALUE ZEROES.                 
           05  BP13K024-STATUS         PIC 9(02)  VALUE ZEROES.                 
           05  BP13K365-STATUS         PIC 9(02)  VALUE ZEROES.                 
           05  BM06K110-STATUS         PIC 9(02)  VALUE ZEROES.                 
           05  BM06K100-STATUS         PIC 9(02)  VALUE ZEROES.                 
           05  BB24K110-STATUS         PIC 9(02)  VALUE ZEROES.                 
           05  BB24K010-STATUS         PIC 9(02)  VALUE ZEROES.                 
           05  BB14K500-STATUS         PIC 9(02)  VALUE ZEROES.                 
           05  WS-OIC-EXIST-TAG        PIC X(01)  VALUE 'N'.                    
           05  WS-BP13F800-EOF         PIC X(01)  VALUE 'N'.                    
           05  WS-K365-FND             PIC X(01)  VALUE 'N'.                    
           05  WS-K110-FND             PIC X(01)  VALUE 'N'.                    
           05  WS-BB24K010-FND         PIC X(01)  VALUE 'N'.                    
           05  WS-BB24K110-FND         PIC X(01)  VALUE 'N'.                    
           05  WS-RESALE-FND           PIC X(01)  VALUE 'N'.                    
           05  WS-BB24K110-DONE        PIC X(01)  VALUE 'N'.                    
           05  WS-STOP                 PIC X(01)  VALUE 'N'.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT             PIC 9(07)  VALUE ZEROES.                 
           05  WS-WRITE-CNT            PIC 9(07)  VALUE ZEROES.                 
           05  WS-S                    PIC 9(02)  VALUE ZEROES.                 
                                                                                
       01  WS-FIELDS.                                                           
           05  WS-STAFF                PIC X(06)  VALUE SPACES.                 
           05  WS-OIC                  PIC X(05)  VALUE SPACES.                 
           05  WS-HA OCCURS 4 TIMES    PIC X(09)  VALUE SPACES.                 
                                                                                
      ******************************************************************        
      *  TEXT URL VARIABLES                                                     
      ******************************************************************        
       01  WS-INBOX-TXT-TARGET-URL.                                             
           05  FILLER              PIC X(60) VALUE                              
             '/be10/home/Newsales/sssCrm/LCI/UpdateCaseStatus?HDBHA_regN        
      -      'o='.                                                              
           05  WS-CASE-REF-URL     PIC X(08).                                   
                                                                                
       01  WS-INBOX-TXT-REMARK.                                                 
           05  FILLER              PIC X(98) VALUE                              
             '4 mths since original TP appt. If HAs deferring further, p        
      -      'ls inform on late interest & update case'.                        
           05  FILLER              PIC X(01) VALUE "'".                         
           05  FILLER              PIC X(02) VALUE 's '.                        
           05  FILLER              PIC X(01) VALUE "'".                         
           05  FILLER              PIC X(24) VALUE                              
             'Late Completion Interest'.                                        
           05  FILLER              PIC X(01) VALUE "'".                         
           05  FILLER              PIC X(36) VALUE                              
             ' status in Sales Case Admin (CRM) - '.                            
           05  WS-REGN-NUM-OUT     PIC X(08) VALUE SPACES.                      
                                                                                
       01  WS-INBOX-TXT-REMARK2.                                                
           05  FILLER              PIC X(115) VALUE                             
            '4 mths since original TP appt. HAs selling or recently sold        
      -     ' current flat. Pls inform on late interest & update case'.         
           05  FILLER              PIC X(01) VALUE "'".                         
           05  FILLER              PIC X(02) VALUE 's '.                        
           05  FILLER              PIC X(01) VALUE "'".                         
           05  FILLER              PIC X(24) VALUE                              
             'Late Completion Interest'.                                        
           05  FILLER              PIC X(01) VALUE "'".                         
           05  FILLER              PIC X(36) VALUE                              
             ' status in Sales Case Admin (CRM) - '.                            
           05  WS-REGN-NUM-OUT2    PIC X(08) VALUE SPACES.                      
                                                                                
       01  WS-MORE-INFO.                                                        
           05  FILLER              PIC X(16) VALUE                              
              'ORIG APPT DATE: '.                                               
           05  WS-MORE-INFO-ORIG   PIC X(08).                                   
           05  FILLER              PIC X(02) VALUE '; '.                        
           05  FILLER              PIC X(05) VALUE                              
              'ECD: '.                                                          
           05  WS-DTE-ECD          PIC X(08).                                   
                                                                                
       01  WS-MORE-INFO2.                                                       
           05  FILLER              PIC X(16) VALUE                              
              'ORIG APPT DATE: '.                                               
           05  WS-MORE-INFO-ORIG2  PIC X(08).                                   
           05  FILLER              PIC X(02) VALUE '; '.                        
           05  FILLER              PIC X(14) VALUE                              
              'RESALE APPLN: '.                                                 
           05  WS-DTE-RESL-APPLN   PIC X(08).                                   
                                                                                
      ******************************************************************        
      *        LINKAGE FOR SUB-PROGRAM BP13CCNT                        *        
      ******************************************************************        
       01  WS-BP13CCNT-LINK.                                                    
           05  LINK-CNTCT-TYPE         PIC X(3).                                
           05  LINK-STAFF-NICK         PIC X(6).                                
           05  LINK-STAFF-NUM          PIC X(6).                                
           05  LINK-STAFF-CNTCT        PIC X(50).                               
           05  LINK-SYSERR             PIC X(4).                                
                                                                                
                                                                                
       COPY BE08W008.                                                           
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-LOGIC.                                                         
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 1100-READ-SY02F001       THRU 1100-EXIT.                     
           PERFORM 2000-READ-BP13F800       THRU 2000-EXIT.                     
           IF WS-BP13F800-EOF = 'Y'                                             
              DISPLAY 'FILE BP13F800 IS  EMPTY'                                 
           END-IF                                                               
           PERFORM 1400-GET-DATES           THRU 1400-EXIT.                     
           PERFORM 3000-PROCESS-CASE        THRU 3000-EXIT                      
             UNTIL WS-BP13F800-EOF = 'Y'.                                       
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-FILES.                                                         
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F800                                                 
                       SY02F001                                                 
                       BP13K022                                                 
                       BP13K024                                                 
                       BP13K365                                                 
                       BB24K110                                                 
                       BB24K010                                                 
                       BB14K500                                                 
                       BM06K100                                                 
                       BM06K110                                                 
                OUTPUT BP13L983.                                                
                                                                                
           IF BP13K022-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K022 OPEN ERROR, STATUS : ' BP13K022-STATUS          
              MOVE     BP13K022-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE   THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF BP13K024-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K024 OPEN ERROR, STATUS : ' BP13K024-STATUS          
              MOVE     BP13K024-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE   THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF BP13K365-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K365 OPEN ERROR, STATUS : ' BP13K365-STATUS          
              MOVE     BP13K365-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE   THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BM06K110 OPEN ERROR, STATUS : ' BM06K110-STATUS          
              MOVE     BM06K110-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE   THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF BM06K100-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BM06K100 OPEN ERROR, STATUS : ' BM06K100-STATUS          
              MOVE     BM06K100-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE   THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF BB24K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BB24K110 OPEN ERROR, STATUS : ' BB24K110-STATUS          
              MOVE     BB24K110-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE   THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF BB24K010-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BB24K010 OPEN ERROR, STATUS : ' BB24K010-STATUS          
              MOVE     BB24K010-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE   THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF BB14K500-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BB14K500 OPEN ERROR, STATUS : ' BB14K500-STATUS          
              MOVE     BB14K500-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE   THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1100-READ-SY02F001.                                                      
      ****************************************************************          
                                                                                
           READ SY02F001 AT END                                                 
                DISPLAY 'EMPTY FILE - SY02F001'                                 
                MOVE    10                 TO  RETURN-CODE                      
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                      
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1200-GET-MONTHS.                                                         
      ****************************************************************          
                                                                                
           EVALUATE WS-MM                                                       
              WHEN 01                                                           
              WHEN 03                                                           
              WHEN 05                                                           
              WHEN 07                                                           
              WHEN 08                                                           
              WHEN 10                                                           
              WHEN 12                                                           
                 ADD  31        TO WS-DAYS                                      
              WHEN 02                                                           
                 DIVIDE WS-CCYY BY 4  GIVING    WS-QUOTIENT             03390017
                                      REMAINDER WS-REMAINDER                  03
                 IF WS-CCYY = 1800 OR 1900                                    03
                    ADD  28     TO WS-DAYS                                    03
                 ELSE                                                         03
                    IF WS-REMAINDER = 0                                       03
                       ADD  29  TO WS-DAYS                                    03
                    ELSE                                                        
                       ADD  28  TO WS-DAYS                                      
                    END-IF                                                      
                 END-IF                                                         
              WHEN OTHER                                                        
                 ADD  30        TO WS-DAYS                                      
           END-EVALUATE.                                                        
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1300-GET-DIFFERENCE.                                                     
      ****************************************************************          
                                                                                
           COMPUTE WS-INT-TP-ORIG =                                             
                   FUNCTION INTEGER-OF-DATE (WS-DTE-TP-ORIG).                   
                                                                                
           COMPUTE WS-DIFF = WS-INT-CURR - WS-INT-TP-ORIG.                      
                                                                                
       1300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1400-GET-DATES.                                                          
      ****************************************************************          
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:14) TO   WS-CURRENT-DTE-TME.            
                                                                                
           STRING F001-DTE-CURRENT(1:4) '-'                                     
                  F001-DTE-CURRENT(5:2) '-'                                     
                  F001-DTE-CURRENT(7:2)     DELIMITED BY SIZE                   
                  INTO WS-DTE-CURRENT.                                          
                                                                                
           STRING WS-CURRENT-DTE-TME(9:2)                                       
                  '.'                                                           
                  WS-CURRENT-DTE-TME(11:2)                                      
                  '.'                                                           
                  WS-CURRENT-DTE-TME(13:2)  DELIMITED BY SIZE                   
                  INTO  WS-TME-CURRENT.                                         
                                                                                
           MOVE F001-DTE-CURRENT            TO   WS-DTE-TEMP                    
                                                 WS-DTE-CURR.                   
                                                                                
                                                                                
           COMPUTE WS-INT-CURR =                                                
                   FUNCTION INTEGER-OF-DATE (WS-DTE-CURR).                      
                                                                                
           MOVE WS-DTE-TEMP                 TO   WS-CHECK-DATE.                 
           MOVE ZEROES                      TO   WS-S.                          
      ************ GETTING 2 MONTHS *************************                   
           PERFORM VARYING WS-S FROM 1 BY 1 UNTIL WS-S > 4                      
              PERFORM 1200-GET-MONTHS       THRU 1200-EXIT                      
              ADD 1                         TO   WS-MM                          
              IF WS-MM = 13                                                     
                 MOVE 1                     TO   WS-MM                          
                 ADD  1                     TO   WS-CCYY                        
              END-IF                                                            
           END-PERFORM.                                                         
           MOVE WS-DAYS                     TO   WS-MONTHS2.                    
                                                                                
           COMPUTE WS-LESS14 = WS-INT-CURR - WS-14-DAYS.                        
           COMPUTE WS-DAY14  =                                                  
                   FUNCTION DATE-OF-INTEGER (WS-LESS14).                        
                                                                                
       1400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-BP13F800.                                                      
      ****************************************************************          
                                                                                
           READ BP13F800 AT END                                                 
                         MOVE 'Y'  TO WS-BP13F800-EOF                           
                         NOT AT END                                             
                         ADD  1    TO WS-READ-CNT                               
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2100-READ-BP13K022.                                                      
      ****************************************************************          
                                                                                
           READ BP13K022 KEY  IS K022-NUM-REGN.                                 
                                                                                
           EVALUATE BP13K022-STATUS                                             
             WHEN 00                                                            
                IF K022-NUM-OIC NOT     = SPACES  AND LOW-VALUES                
                   MOVE 'Y'             TO WS-OIC-EXIST-TAG                     
                   MOVE K022-NUM-OIC    TO WS-OIC                               
                ELSE                                                            
                   MOVE 'N'             TO WS-OIC-EXIST-TAG                     
                END-IF                                                          
             WHEN 23                                                            
                MOVE    'N'             TO WS-OIC-EXIST-TAG                     
             WHEN OTHER                                                         
                DISPLAY 'BP13K022 READ ERROR, STATUS : '                        
                         BP13K022-STATUS                                        
                MOVE BP13K022-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2200-CALL-BP13CCNT.                                                      
      ****************************************************************          
                                                                                
           CALL 'BP13CCNT' USING WS-BP13CCNT-LINK.                              
                                                                                
           EVALUATE LINK-SYSERR                                                 
           WHEN ZEROS                                                           
              CONTINUE                                                          
           WHEN 100                                                             
              DISPLAY 'OIC NOT FOUND IN OFFICE_CNTCT TABLE - '                  
                  WS-OIC ', REGN : ' F800-NUM-REGN                              
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING OFFICE_CNTCT TABLE = '                     
                       LINK-SYSERR                                              
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2300-READ-BP13K365.                                                      
      ****************************************************************          
                                                                                
           READ BP13K365.                                                       
                                                                                
           EVALUATE BP13K365-STATUS                                             
             WHEN 00                                                            
                MOVE    'Y'             TO WS-K365-FND                          
             WHEN 23                                                            
                MOVE    'N'             TO WS-K365-FND                          
             WHEN OTHER                                                         
                DISPLAY 'BP13K365 READ ERROR, STATUS : '                        
                         BP13K365-STATUS                                        
                MOVE BP13K365-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2400-READ-BM06K110.                                                      
      ****************************************************************          
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE BM06K110-STATUS                                             
             WHEN 00                                                            
                MOVE    'Y'             TO WS-K110-FND                          
             WHEN 23                                                            
                MOVE    'N'             TO WS-K110-FND                          
             WHEN OTHER                                                         
                DISPLAY 'BM06K110 READ ERROR, STATUS : '                        
                         BM06K110-STATUS                                        
                MOVE BM06K110-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2500-READ-BB24K010.                                                      
      ****************************************************************          
                                                                                
           READ BB24K010.                                                       
                                                                                
           EVALUATE BB24K010-STATUS                                             
             WHEN 00                                                            
                MOVE 'Y'                   TO WS-BB24K010-FND                   
             WHEN 23                                                            
                MOVE 'N'                   TO WS-BB24K010-FND                   
             WHEN OTHER                                                         
                DISPLAY 'BB24K010 READ ERROR, STATUS : '                        
                         BB24K010-STATUS                                        
                MOVE BB24K110-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2600-STARTBR-BB24K110.                                                   
      ****************************************************************          
                                                                                
           START BB24K110 KEY >= K110-KEY-FLD OF BB24K110-REC.                  
                                                                                
           EVALUATE BB24K110-STATUS                                             
              WHEN 00                                                           
                 PERFORM 2610-READNXT-BB24K110 THRU 2610-EXIT                   
                   UNTIL WS-BB24K110-DONE = 'Y'                                 
              WHEN 23                                                           
                 MOVE 'Y'                      TO WS-BB24K110-DONE              
              WHEN OTHER                                                        
                 DISPLAY 'ERROR STR BRW BB24K110. STATUS IS '                   
                          BB24K110-STATUS                                       
                 MOVE BB24K110-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       2610-READNXT-BB24K110.                                                   
      *--------------------*                                                    
                                                                                
           READ BB24K110 NEXT RECORD.                                           
                                                                                
           EVALUATE BB24K110-STATUS                                             
            WHEN 00                                                             
                 IF K110-NUM-UIN            = WS-HA(WS-S)                       
                    IF K110-CDE-STATUS      = 'S'                               
                       MOVE 'Y'            TO WS-BB24K110-DONE                  
                       MOVE 'Y'            TO WS-BB24K110-FND                   
                    END-IF                                                      
                 ELSE                                                           
                    MOVE 'Y'               TO WS-BB24K110-DONE                  
                 END-IF                                                         
            WHEN 23                                                             
                 MOVE 'Y'                  TO WS-BB24K110-DONE                  
            WHEN OTHER                                                          
                 DISPLAY 'ERROR READNXT BB24K110. STATUS IS '                   
                            BB24K110-STATUS                                     
                 MOVE BB24K110-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2610-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2700-READ-BM06K100.                                                      
      ****************************************************************          
                                                                                
           READ BM06K100.                                                       
                                                                                
           EVALUATE BM06K100-STATUS                                             
             WHEN 00                                                            
                MOVE SPACES                TO K500-KEY-FLD                      
                MOVE K100-CDE-BLK          TO K500-KEY-FLD                      
                PERFORM 2800-READ-BB14K500                                      
                                           THRU 2800-EXIT                       
             WHEN 23                                                            
                MOVE SPACES                TO WS-DTE-ECD                        
             WHEN OTHER                                                         
                DISPLAY 'BM06K100 READ ERROR, STATUS : '                        
                         BM06K100-STATUS                                        
                MOVE BM06K100-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2800-READ-BB14K500.                                                      
      ****************************************************************          
                                                                                
           READ BB14K500.                                                       
                                                                                
           EVALUATE BB14K500-STATUS                                             
             WHEN 00                                                            
                MOVE K500-DTE-BLDG-COMPLETE TO WS-DTE-ECD                       
             WHEN 23                                                            
                MOVE SPACES                 TO WS-DTE-ECD                       
             WHEN OTHER                                                         
                DISPLAY 'BB14K500 READ ERROR, STATUS : '                        
                         BB14K500-STATUS                                        
                MOVE BB14K500-STATUS        TO RETURN-CODE                      
                PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       2800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-CASE.                                                       
      ****************************************************************          
                                                                                
           MOVE SPACES                       TO   BP13K365-REC.                 
           MOVE F800-NUM-REGN                TO   K365-KEY-FLD.                 
           PERFORM 2300-READ-BP13K365        THRU 2300-EXIT.                    
           IF WS-K365-FND = 'N'                                                 
              MOVE SPACES                    TO   K110-REC                      
              MOVE F800-NUM-SCH-ACC          TO   K110-KEY-FLD OF               
                                                  K110-REC                      
              PERFORM 2400-READ-BM06K110     THRU 2400-EXIT                     
              IF WS-K110-FND = 'Y'                                              
                 IF K110-DTE-KEY-AVAIL NOT = SPACES AND LOW-VALUES AND          
                                             ZEROES                             
                    IF K110-DTE-KEY-ISSUED = SPACES OR LOW-VALUES OR            
                                             ZEROES                             
                       MOVE SPACES           TO WS-DTE-TP-ORIG-TXT              
                       MOVE F800-DTE-TP-ORIG TO WS-DTE-TP-ORIG-TXT              
                       PERFORM 1300-GET-DIFFERENCE                              
                                             THRU 1300-EXIT                     
                       IF WS-DIFF = WS-MONTHS2                                  
                          PERFORM 3100-INSERT-INBOX-INTRY                       
                                             THRU 3100-EXIT                     
                          PERFORM 3200-WRITE-BP13L983                           
                                             THRU 3200-EXIT                     
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F800        THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3010-CREATE-URL-REMARK.                                                  
      ****************************************************************          
                                                                                
           MOVE ZEROES                         TO   WS-S                        
           PERFORM VARYING WS-S FROM 1 BY 1 UNTIL WS-S > 4                      
              MOVE SPACES                      TO   WS-HA(WS-S)                 
           END-PERFORM                                                          
                                                                                
           MOVE F800-NUM-NRIC1                 TO   WS-HA(1).                   
           MOVE F800-NUM-NRIC2                 TO   WS-HA(2).                   
           MOVE F800-NUM-NRIC3                 TO   WS-HA(3).                   
           MOVE F800-NUM-NRIC4                 TO   WS-HA(4).                   
                                                                                
           MOVE SPACE                          TO   WS-STOP                     
           MOVE ZEROES                         TO   WS-S                        
           PERFORM VARYING WS-S FROM 1 BY 1 UNTIL WS-S > 4 OR                   
              WS-STOP = 'Y'                                                     
              IF WS-HA(WS-S) = SPACES                                           
                 MOVE 'Y'                      TO   WS-STOP                     
              ELSE                                                              
                 MOVE SPACES                   TO   BB24K110-REC                
                 MOVE 'N'                      TO   WS-BB24K010-FND             
                                                    WS-BB24K110-FND             
                                                    WS-BB24K110-DONE            
                 MOVE WS-HA(WS-S)              TO   K110-NUM-UIN                
                 MOVE 'S'                      TO   K110-CDE-STATUS             
                 PERFORM 2600-STARTBR-BB24K110 THRU 2600-EXIT                   
                 IF WS-BB24K110-FND = 'Y'                                       
                    MOVE 'Y'                   TO   WS-STOP                     
                    MOVE SPACES                TO   BB24K010-REC                
                    MOVE K110-NUM-CASE         TO   K010-KEY-FLD                
                    PERFORM 2500-READ-BB24K010 THRU 2500-EXIT                   
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
           MOVE SPACE                          TO   WS-RESALE-FND.              
           IF WS-BB24K010-FND = 'Y' AND                                         
             (K010-DTE-STATUS-UPDATE(17) < WS-DAY14 OR                          
             (K010-DTE-STATUS-UPDATE(17) = SPACES OR LOW-VALUES))               
              MOVE 'Y'                         TO   WS-RESALE-FND               
              MOVE K010-DTE-RECEIVE-APPLN      TO   WS-DTE-RESL-APPLN           
           ELSE                                                                 
              MOVE SPACES                      TO   WS-DTE-RESL-APPLN           
           END-IF.                                                              
           MOVE SPACES                         TO   WS-CASE-REF-URL.            
           MOVE F800-NUM-REGN                  TO   WS-CASE-REF-URL.            
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3020-COMPUTE-DATE-DUE.                                                   
      ****************************************************************          
                                                                                
           MOVE ZEROES                         TO   WS-CHECK-DATE.              
           MOVE WS-DTE-TP-ORIG                 TO   WS-CHECK-DATE.              
                                                                                
           MOVE ZEROES                         TO   WS-S                        
                                                    WS-DAYS.                    
           IF WS-RESALE-FND       = 'Y'                                         
              PERFORM VARYING WS-S FROM 1 BY 1 UNTIL WS-S > 6                   
                 PERFORM 1200-GET-MONTHS       THRU 1200-EXIT                   
                 ADD 1                         TO   WS-MM                       
                 IF WS-MM = 13                                                  
                    MOVE 1                     TO   WS-MM                       
                    ADD  1                     TO   WS-CCYY                     
                 END-IF                                                         
              END-PERFORM                                                       
           ELSE                                                                 
              PERFORM VARYING WS-S FROM 1 BY 1 UNTIL WS-S > 2                   
                 PERFORM 1200-GET-MONTHS       THRU 1200-EXIT                   
                 ADD 1                         TO   WS-MM                       
                 IF WS-MM = 13                                                  
                    MOVE 1                     TO   WS-MM                       
                    ADD  1                     TO   WS-CCYY                     
                 END-IF                                                         
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
           COMPUTE WS-COMP-INT-TP-ORIG =                                        
                   WS-INT-TP-ORIG + WS-DAYS.                                    
           COMPUTE WS-COMP-DTE =                                                
                   FUNCTION DATE-OF-INTEGER (WS-COMP-INT-TP-ORIG).              
                                                                                
           MOVE WS-COMP-DTE             TO  WS-COMP-DTE-TXT.                    
                                                                                
           MOVE WS-COMP-DTE-TXT(1:4)    TO  WS-DTE-CCYY.                        
           MOVE WS-COMP-DTE-TXT(5:2)    TO  WS-DTE-MM.                          
           MOVE WS-COMP-DTE-TXT(7:2)    TO  WS-DTE-DD.                          
                                                                                
       3020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3030-GET-OIC.                                                            
      ****************************************************************          
                                                                                
            MOVE SPACES                    TO   K022-NUM-REGN.                  
            MOVE F800-NUM-REGN             TO   K022-NUM-REGN.                  
            PERFORM 2100-READ-BP13K022     THRU 2100-EXIT.                      
            IF WS-OIC-EXIST-TAG = 'Y'                                           
               MOVE SPACES                 TO WS-BP13CCNT-LINK                  
               MOVE 'EML'                  TO LINK-CNTCT-TYPE                   
               MOVE WS-OIC                 TO LINK-STAFF-NICK                   
               MOVE SPACES                 TO LINK-STAFF-NUM                    
               PERFORM 2200-CALL-BP13CCNT  THRU 2200-EXIT                       
               MOVE LINK-STAFF-NUM         TO WS-STAFF                          
            ELSE                                                                
               DISPLAY 'BP13K022 OIC NOT FOUND FOR REGN NUM '                   
                        F800-NUM-REGN                                           
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                        
            END-IF.                                                             
                                                                                
       3030-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3100-INSERT-INBOX-INTRY.                                                 
      ****************************************************************          
                                                                                
           MOVE SPACES                      TO  BE08W008-COMMAREA.              
           INITIALIZE                           BE08W008-COMMAREA.              
                                                                                
           MOVE SPACES                      TO  K100-KEY-FLD.                   
           MOVE K110-ESTATE                 TO  K100-ESTATE.                    
           MOVE K110-NEIGHBOURHOOD          TO  K100-NEIGHBOURHOOD.             
           MOVE K110-CONTRACT-NO            TO  K100-CONTRACT-NO.               
           MOVE K110-BLK-NO                 TO  K100-BLK-NO.                    
           PERFORM 2700-READ-BM06K100     THRU  2700-EXIT.                      
           PERFORM 3010-CREATE-URL-REMARK THRU  3010-EXIT.                      
           PERFORM 3020-COMPUTE-DATE-DUE  THRU  3020-EXIT.                      
           PERFORM 3030-GET-OIC           THRU  3030-EXIT.                      
                                                                                
           MOVE WS-TXN-CREATE               TO  W008-IN-TXN-TYPE.               
           MOVE WS-SYSTEM-ID                TO  W008-IN-SYSTEM-ID.              
           MOVE WS-NUM-PRCS-CTGRY           TO  W008-IN-NUM-PRCS-CTGRY.         
           MOVE WS-NUM-BRNCH                TO  W008-IN-NUM-BRNCH.              
           MOVE F800-NUM-REGN               TO  W008-IN-NUM-CASE-REF.           
           MOVE F800-NUM-SCH-ACC            TO  W008-IN-HDBREF.                 
           MOVE WS-STAFF                    TO  W008-IN-NUM-STAFF.              
           MOVE WS-INBOX-TXT-TARGET-URL     TO  W008-IN-TXT-TARGET-URL.         
           MOVE WS-DTE-TP-ORIG-TXT          TO  WS-MORE-INFO-ORIG               
           IF WS-RESALE-FND = 'Y'                                               
              MOVE F800-NUM-REGN            TO  WS-REGN-NUM-OUT2                
              MOVE WS-INBOX-TXT-REMARK2     TO  W008-IN-TXT-REMARK              
              MOVE WS-DTE-TP-ORIG-TXT       TO  WS-MORE-INFO-ORIG2              
              MOVE WS-MORE-INFO2            TO  W008-IN-TXT-MORE-INFO           
           ELSE                                                                 
              MOVE F800-NUM-REGN            TO  WS-REGN-NUM-OUT                 
              MOVE WS-INBOX-TXT-REMARK      TO  W008-IN-TXT-REMARK              
              MOVE WS-DTE-TP-ORIG-TXT       TO  WS-MORE-INFO-ORIG               
              MOVE WS-MORE-INFO             TO  W008-IN-TXT-MORE-INFO           
           END-IF.                                                              
           MOVE WS-DTE-CURRENT              TO  W008-IN-DTE-INBOX.              
           MOVE WS-TME-CURRENT              TO  W008-IN-TME-INBOX.              
           MOVE WS-DTE-DUE                  TO  W008-IN-DUE-DATE.               
                                                                                
           CALL 'BE08C008'    USING BE08W008-COMMAREA.                          
           IF W008-OUT-RTN-MSGCDE = '0' OR 'W'                                  
              ADD 1                         TO  WS-WRITE-CNT                    
           ELSE                                                                 
              DISPLAY 'ERROR RETURNED CALLING BE08C008 SUBR (C) : '             
                                          W008-IN-NUM-CASE-REF                  
              DISPLAY '{ ' W008-OUT-RTN-MSGCDE ' }'                             
              DISPLAY '{ ' W008-OUT-RTN-MSGNO  ' }'                             
              DISPLAY '{ ' W008-OUT-RTN-DESC   ' }'                             
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3200-WRITE-BP13L983.                                                     
      ****************************************************************          
                                                                                
           MOVE SPACES                      TO BP13L983-REC.                    
           MOVE F800-NUM-REGN               TO L983-NUM-REGN.                   
           MOVE F800-NUM-SCH-ACC            TO L983-NUM-SCH-ACC.                
           MOVE SPACES                      TO K024-KEY-FLD.                    
           MOVE WS-OIC                      TO K024-KEY-FLD.                    
           PERFORM 3300-READ-BP13K024     THRU 3300-EXIT.                       
           MOVE K024-NME-OIC                TO L983-STAFF-NAME                  
           MOVE WS-DTE-TP-ORIG-TXT          TO L983-DTE-TP-ORIG.                
           IF WS-RESALE-FND = 'Y'                                               
              MOVE 'Y'                      TO L983-RESALE-FND                  
           ELSE                                                                 
              MOVE 'N'                      TO L983-RESALE-FND                  
           END-IF.                                                              
           MOVE WS-DTE-CURRENT              TO L983-DTE-SENT.                   
           WRITE BP13L983-REC.                                                  
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3300-READ-BP13K024.                                                      
      ****************************************************************          
                                                                                
           READ BP13K024.                                                       
                                                                                
           EVALUATE BP13K022-STATUS                                             
             WHEN 00                                                            
                CONTINUE                                                        
             WHEN 23                                                            
                MOVE    SPACES          TO K024-NME-OIC                         
             WHEN OTHER                                                         
                DISPLAY 'BP13K024 READ ERROR, STATUS : '                        
                         BP13K024-STATUS                                        
                MOVE BP13K024-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9000-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           DISPLAY '---- BP13C983 CONTROL TOTAL -----'.                         
           DISPLAY ' NO OF REC READ FROM BP13F800   = '                         
                     WS-READ-CNT.                                               
           DISPLAY ' NO OF RECS SENT TO BE08        = '                         
                     WS-WRITE-CNT.                                              
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13F800                                                 
                       SY02F001                                                 
                       BP13K365                                                 
                       BM06K110                                                 
                       BB24K110                                                 
                       BB24K010                                                 
                       BB14K500                                                 
                       BM06K100                                                 
                       BP13K022                                                 
                       BP13K024                                                 
                       BP13L983.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
