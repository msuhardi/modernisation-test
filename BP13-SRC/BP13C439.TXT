      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.   BP13C439.                                                  
       AUTHOR.       TEREZA SALONGA.                                            
      *--------------------------------------------------------------*          
                                                                                
      *==============================================================*          
      *        SOC - S Y S T E M   O F   C O M M I T M E N T         *          
      *==============================================================*          
      *  OBJECTIVES : TO CREATE FILE THAT WILL BE PASSED TO LEAS     *          
      *               FOR EXTENSION OF HLE EXPIRY DATE               *          
      *                                                              *          
      *      INPUT  : 1. BP13F313 - SOC TRANFILE FOR HLA EXT         *          
      *               2. BP13K800 - SOC MASTER FILE                  *          
      *               2. BP27K800 - HLA MASTER                       *          
      *               3. BM06K100 - BM06.K100.BLOCK                  *          
      *               4. BM06K110 - BM06.K110.FLATINV                *          
      *      OUTPUT : 1. BP13F800 - RECS FOR HLA EXTENSION           *          
      *               2. P13L439A - REPORT FOR CASES WITH ERRORS     *          
      *==============================================================*          
      * CHG REF  DATE     BY    REMARKS                              *          
      * -------- -------- ----- -------------------------------------*          
      * BP133385 07072008 MTSR1 NEW PGM                              *          
      * BP134496 20032012 ZDD1  ADD BP13K800 TO UPDATEE DTE-HLE-EXT  *          
      * BP135037 20130815 ESD1  CATER TO NOT ALLOW EXTENSION FOR     *          
      *                         CASE HLA-APPLN-DTE >= 20130827.      *          
      * BP136750 20170511 PCL4  TO READ BP13K317 FOR HLE EXTENSION   *          
      * BP138047 20191125 KAC1  RECOMPILE FOR BP27 COPYBOOK UPDATE   *          
      * =============================================================*          
                                                                                
      *-------------------------------------------------------------            
      * WS-ERROR: 1 - SCHEME ACCT IS BLANK                                      
      *           2 - LOAN TAG IS NOT = "H"                                     
      *           3 - HLE IS BLANK                                              
      *           4 - REGN NOT FOUND IN BP13K800                                
      *           5 - EST/NEIGH/CONT NOT FND IN BM06K100                        
      *           6 - NO VALID RECS IN BM06K110 FOR EST/NEIGH/CONT              
      *           7 - REGNO IS BLANK IN BM06K110                                
      *-------------------------------------------------------------            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F313 ASSIGN        TO BP13F313.                           
                                                                                
           SELECT BP13K800 ASSIGN        TO BP13K800                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K800-NUM-REGN                       
                           FILE STATUS   IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K313 ASSIGN        TO BP13K313                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K313-KEY-FLD                        
                           FILE STATUS   IS WS-K313-STATUS.                     
                                                                                
           SELECT BP13K317 ASSIGN        TO BP13K317                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K317-KEY-FLD                        
                           FILE STATUS   IS WS-K317-STATUS.                     
                                                                                
           SELECT BP27K800 ASSIGN        TO BP27K800                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K800-NUM-HLA OF                     
                                            BP27K800-MASTER                     
                           FILE STATUS   IS WS-BP27K800-STAT.                   
                                                                                
           SELECT BM06K100 ASSIGN        TO BM06K100                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K100-KEY-FLD                        
                           FILE STATUS   IS WS-K100-STATUS.                     
                                                                                
           SELECT BM06K110 ASSIGN        TO BM06K110                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K110-KEY-FLD                        
                           ALTERNATE KEY IS K110-REGN-NO                        
                                         WITH DUPLICATES                        
                           ALTERNATE KEY IS K110-AIX1                           
                                         WITH DUPLICATES                        
                           FILE STATUS   IS WS-K110-STATUS.                     
                                                                                
           SELECT BP13F800 ASSIGN        TO BP13F800.                           
           SELECT P13L439A ASSIGN        TO P13L439A.                           
                                                                                
                                                                                
      *-------------------------------------------------------------            
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F313                                                            
            RECORD CONTAINS 100  CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F313.                                                           
                                                                                
       FD   BP13K313                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K313.                                                           
                                                                                
       FD   BP13K317                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K317.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP27K800                                                            
            RECORD CONTAINS 1200 CHARACTERS.                                    
       COPY BP27K800.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BM06K100.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F800.                                                           
                                                                                
       FD   P13L439A                                                            
            RECORD CONTAINS 132  CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       01 P13L439A-REC           PIC X(132).                                    
                                                                                
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-VARIABLES.                                                         
          05 WS-K317-OK                 PIC X      VALUE 'N'.                   
          05 WS-F313-EOF                PIC X      VALUE 'N'.                   
          05 WS-K100-EOF                PIC X      VALUE 'N'.                   
          05 WS-K110-EOF                PIC X      VALUE 'N'.                   
                                                                                
          05 WS-K800-STATUS             PIC 9(2)   VALUE 0.                     
          05 WS-K313-STATUS             PIC 9(2)   VALUE 0.                     
          05 WS-K317-STATUS             PIC 9(2)   VALUE 0.                     
          05 WS-BP27K800-STAT           PIC 9(2)   VALUE 0.                     
          05 WS-K100-STATUS             PIC 9(2)   VALUE 0.                     
          05 WS-K110-STATUS             PIC 9(2)   VALUE 0.                     
                                                                                
          05 WS-F313-READ-REGN          PIC 9(8)   VALUE 0.                     
          05 WS-F313-READ-EST           PIC 9(8)   VALUE 0.                     
          05 WS-F800-PRINTED1           PIC 9(8)   VALUE 0.                     
          05 WS-F800-PRINTED2           PIC 9(8)   VALUE 0.                     
          05 WS-L439-PRINTED1           PIC 9(8)   VALUE 0.                     
          05 WS-L439-PRINTED2           PIC 9(8)   VALUE 0.                     
          05 WS-K800-UPD                PIC 9(8)   VALUE 0.                     
          05 WS-K313-UPD                PIC 9(8)   VALUE 0.                     
                                                                                
          05 WS-ERROR-OTH-CTR           PIC 9(8)   VALUE 0.                     
          05 WS-ERROR-3-CTR             PIC 9(8)   VALUE 0.                     
          05 WS-ERROR-4-CTR             PIC 9(8)   VALUE 0.                     
          05 WS-ERROR-5-CTR             PIC 9(8)   VALUE 0.                     
          05 WS-ERROR-6-CTR             PIC 9(8)   VALUE 0.                     
          05 WS-ERROR-7-CTR             PIC 9(8)   VALUE 0.                     
          05 WS-ERROR-8-CTR             PIC 9(8)   VALUE 0.                     
                                                                                
          05 WS-SYSTEM-DATE             PIC X(8)   VALUE SPACES.                
          05 WS-VALID                   PIC X(1)   VALUE 'N'.                   
          05 WS-ERROR                   PIC X(1)   VALUE 'N'.                   
          05 WS-K100-FND                PIC X(1)   VALUE 'N'.                   
          05 WS-REGN                    PIC X(11)  VALUE SPACES.                
          05 WS-K110-CTR                PIC 9(5)   VALUE ZEROES.                
          05 WS-PROCESS-REGN            PIC X(1)   VALUE 'N'.                   
                                                                                
          05 WS-SNO-CTR                 PIC 9(5)   VALUE ZEROES.                
          05 WS-PAGE-CTR                PIC 9(3)   VALUE ZEROES.                
          05 WS-LINE-CTR                PIC 9(2)   VALUE ZEROES.                
                                                                                
      *-------------------------------------------------------------            
      *    REPORT LAYOUT                                                        
      *-------------------------------------------------------------            
       01  L439-HD1.                                                            
           05  L439-RPT-NAME              PIC X(10)   VALUE 'P13L439A'.         
           05  FILLER                     PIC X(22)   VALUE                     
               'HDB3                  '.                                        
           05  FILLER                     PIC X(11)   VALUE SPACES.             
           05  FILLER                     PIC X(41)   VALUE                     
               'E X T E N S I O N   O F   H L E   E X P  '.                     
           05  FILLER                     PIC X(11)   VALUE SPACES.             
           05  FILLER                     PIC X(26)   VALUE                     
               '                   DATE : '.                                    
           05  L439-DATE                  PIC X(10)   VALUE SPACES.             
                                                                                
       01  L439-HD2.                                                            
           05  FILLER                     PIC X(36)   VALUE SPACES.             
           05  FILLER                     PIC X(54)   VALUE                     
               '            REPORT FOR CASES WITH ERRORS            '.          
           05  FILLER                     PIC X(24)   VALUE SPACES.             
           05  FILLER                     PIC X(07)   VALUE                     
               'PAGE : '.                                                       
           05  L439-PAGE                  PIC ZZ9     VALUE ZEROES.             
                                                                                
       01  L439-HD3.                                                            
           05  FILLER                     PIC X(08)   VALUE                     
               'S/NO.'.                                                         
           05  FILLER                     PIC X(17)   VALUE                     
               'EST/NEIGH/CONT'.                                                
           05  FILLER                     PIC X(11)   VALUE                     
               'REGN NO'.                                                       
           05  FILLER                     PIC X(07)   VALUE                     
               'REMARKS'.                                                       
                                                                                
       01  L439-DETAIL.                                                         
           05  L439-SNO                   PIC ZZZZ9   VALUE ZEROES.             
           05  FILLER                     PIC X(03)   VALUE SPACES.             
           05  L439-EST-NEIGH-CONT        PIC X(11)   VALUE SPACES.             
           05  FILLER                     PIC X(06)   VALUE SPACES.             
           05  L439-REGN                  PIC X(08)   VALUE SPACES.             
           05  FILLER                     PIC X(03)   VALUE SPACES.             
           05  L439-REMARKS               PIC X(100)  VALUE SPACES.             
                                                                                
       01  L439-TRLR.                                                           
           05  FILLER                     PIC X(37)   VALUE SPACES.             
           05  FILLER                     PIC X(56)   VALUE                     
               '******************* END OF REPORT *******************'.         
                                                                                
       01  WS-DASH-LINE                   PIC X(132)  VALUE ALL '-'.            
       01  WS-BLANK-LINE                  PIC X(132)  VALUE ALL SPACES.         
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-PROCESS-BP13F313 THRU 2000-EXIT                         
              UNTIL WS-F313-EOF = 'Y'.                                          
                                                                                
           WRITE P13L439A-REC            FROM L439-TRLR AFTER 2.                
           PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F313                                                 
                       BP27K800                                                 
                       BM06K100                                                 
                       BM06K110                                                 
                       BP13K317                                                 
                I-O    BP13K800                                                 
                       BP13K313                                                 
                OUTPUT BP13F800                                                 
                       P13L439A.                                                
                                                                                
           IF WS-K313-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K313 - OPEN ERROR '  WS-K313-STATUS                  
              MOVE    WS-K313-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K317-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K317 - OPEN ERROR '  WS-K317-STATUS                  
              MOVE    WS-K317-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - OPEN ERROR '  WS-K800-STATUS                  
              MOVE    WS-K800-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-BP27K800-STAT NOT = 00 AND 97                                  
              DISPLAY 'BP27K800 - OPEN ERROR '  WS-BP27K800-STAT                
              MOVE    WS-BP27K800-STAT     TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K100 - OPEN ERROR '  WS-K100-STATUS                  
              MOVE    WS-K100-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - OPEN ERROR '  WS-K110-STATUS                  
              MOVE    WS-K110-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO   WS-SYSTEM-DATE.                 
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
              DELIMITED BY SIZE INTO L439-DATE.                                 
                                                                                
           PERFORM 6500-WRITE-HEADING      THRU 6500-EXIT.                      
           PERFORM 3000-READ-BP13F313      THRU 3000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-PROCESS-BP13F313.                                                   
      *-------------------------------------------------------------            
           MOVE 'Y'                            TO   WS-VALID.                   
           MOVE SPACES                         TO   WS-ERROR WS-REGN.           
           MOVE 'N'                            TO   WS-PROCESS-REGN.            
                                                                                
           IF F313-NUM-KEY1(9:3) NOT = SPACES AND LOW-VALUES                    
              ADD 1                            TO   WS-F313-READ-EST            
              PERFORM 3200-READ-BM06K100       THRU 3200-EXIT                   
              IF WS-K100-FND = 'N'                                              
                 MOVE F313-NUM-KEY1            TO   WS-REGN                     
                 MOVE '5'                      TO   WS-ERROR                    
                 ADD 1                         TO   WS-ERROR-5-CTR              
                 PERFORM 6000-WRITE-P13L439A   THRU 6000-EXIT                   
              ELSE                                                              
                 IF WS-K110-CTR = 0                                             
                    MOVE F313-NUM-KEY1         TO   WS-REGN                     
                    MOVE '6'                   TO   WS-ERROR                    
                    PERFORM 6000-WRITE-P13L439A THRU 6000-EXIT                  
                    ADD 1                      TO   WS-ERROR-6-CTR              
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'                         TO   WS-PROCESS-REGN             
              ADD 1                            TO   WS-F313-READ-REGN           
              MOVE F313-NUM-REGN               TO   WS-REGN                     
              PERFORM 3100-READ-BP13K800       THRU 3100-EXIT                   
                                                                                
              IF WS-VALID = 'Y'                                                 
                 PERFORM 3160-READ-BP13K317    THRU 3160-EXIT                   
                                                                                
                 IF WS-K317-OK = 'Y'                                            
                    PERFORM 3170-READUPD-BP13K313  THRU 3170-EXIT               
                                                                                
                    MOVE K317-DTE-HLE-EXT  TO F313-DTE-HLE-EXTENSION            
                 END-IF                                                         
                                                                                
                 PERFORM 5600-UPDATE-BP13K800  THRU 5600-EXIT                   
                 PERFORM 5500-WRITE-BP13F800   THRU 5500-EXIT                   
                 ADD 1                         TO   WS-F800-PRINTED1            
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-BP13F313          THRU 3000-EXIT.                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-READ-BP13F313.                                                      
      *-------------------------------------------------------------            
           READ BP13F313                                                        
                AT END MOVE 'Y' TO WS-F313-EOF                                  
           END-READ.                                                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------*                                           
       3100-READ-BP13K800.                                                      
      *-----------------------------*                                           
           MOVE SPACES                     TO BP13K800-MASTER.                  
           INITIALIZE                         BP13K800-MASTER.                  
                                                                                
           MOVE WS-REGN                    TO K800-NUM-REGN.                    
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
              IF K800-NUM-SCH-ACC = SPACES OR LOW-VALUES                        
                 IF WS-PROCESS-REGN = 'Y'                                       
                    MOVE '1'                      TO   WS-ERROR                 
                    PERFORM 6000-WRITE-P13L439A   THRU 6000-EXIT                
                 ELSE                                                           
                    ADD 1                         TO   WS-ERROR-OTH-CTR         
                    MOVE 'N'                      TO   WS-VALID                 
                 END-IF                                                         
              ELSE                                                              
                 IF K800-NUM-LOAN-TAG OF BP13K800-MASTER NOT = 'H'              
                    IF WS-PROCESS-REGN = 'Y'                                    
                       MOVE '2'                    TO   WS-ERROR                
                       PERFORM 6000-WRITE-P13L439A THRU 6000-EXIT               
                    ELSE                                                        
                       ADD 1                       TO   WS-ERROR-OTH-CTR        
                       MOVE 'N'                    TO   WS-VALID                
                    END-IF                                                      
                 ELSE                                                           
                    IF K800-NUM-HLA OF BP13K800-MASTER = SPACES OR              
                                      LOW-VALUES OR ALL 'X'                     
                       MOVE '3'                    TO   WS-ERROR                
                       MOVE 'N'                    TO   WS-VALID                
                       PERFORM 6000-WRITE-P13L439A THRU 6000-EXIT               
                       IF WS-PROCESS-REGN = 'N'                                 
                          ADD 1                    TO   WS-ERROR-3-CTR          
                       END-IF                                                   
                    ELSE                                                        
                       PERFORM 3150-READ-BP27K800 THRU 3150-EXIT                
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
                                                                                
           WHEN 23                                                              
              DISPLAY 'WS-REGN = ' WS-REGN                                      
              IF WS-PROCESS-REGN = 'Y'                                          
                 MOVE '4'                    TO   WS-ERROR                      
                 PERFORM 6000-WRITE-P13L439A THRU 6000-EXIT                     
              ELSE                                                              
                 ADD 1                       TO   WS-ERROR-4-CTR                
                 MOVE 'N'                    TO   WS-VALID                      
              END-IF                                                            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K800 - READ ERR FOR REC ' F800-NUM-REGN              
              MOVE WS-K800-STATUS          TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------*                                           
       3150-READ-BP27K800.                                                      
      *-----------------------------*                                           
           MOVE SPACES                     TO BP27K800-MASTER.                  
           INITIALIZE                         BP27K800-MASTER.                  
                                                                                
           MOVE  K800-NUM-HLA OF BP13K800-MASTER TO                             
                 K800-NUM-HLA OF BP27K800-MASTER.                               
                                                                                
           READ BP27K800.                                                       
                                                                                
           EVALUATE WS-BP27K800-STAT                                            
           WHEN 00                                                              
                                                                                
                IF K800-DTE-APPLN >= '20130827'                                 
                   PERFORM 3160-READ-BP13K317  THRU 3160-EXIT                   
                                                                                
                   IF WS-K317-OK = 'Y'                                          
                      CONTINUE                                                  
                   ELSE                                                         
                      MOVE '8'                    TO   WS-ERROR                 
                      MOVE 'N'                    TO   WS-VALID                 
                      PERFORM 6000-WRITE-P13L439A THRU 6000-EXIT                
                      IF WS-PROCESS-REGN = 'Y'                                  
                         ADD 1                    TO   WS-ERROR-8-CTR           
                         MOVE 'N'                 TO   WS-VALID                 
                      END-IF                                                    
                   END-IF                                                       
                END-IF                                                          
           WHEN 23                                                              
                CONTINUE                                                        
           WHEN OTHER                                                           
              DISPLAY 'BP27K800 - READ ERR FOR REC ' K800-NUM-HLA OF            
                                                     BP27K800-MASTER            
              MOVE WS-BP27K800-STAT        TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------*                                           
       3160-READ-BP13K317.                                                      
      *-----------------------------*                                           
                                                                                
           MOVE SPACES                     TO BP13K317-REC.                     
           INITIALIZE                         BP13K317-REC.                     
                                                                                
           MOVE 'N'                       TO WS-K317-OK.                        
           MOVE F313-NUM-REGN             TO K317-KEY-FLD.                      
                                                                                
           READ BP13K317.                                                       
                                                                                
           EVALUATE WS-K317-STATUS                                              
           WHEN 00                                                              
                MOVE 'Y'                     TO WS-K317-OK                      
                                                                                
           WHEN 23                                                              
                MOVE SPACES                  TO BP13K317-REC                    
                INITIALIZE                      BP13K317-REC                    
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'BP13K317 - READ ERR FOR REC ' K317-KEY-FLD             
                MOVE WS-K317-STATUS          TO   RETURN-CODE                   
                PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       3160-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------*                                           
       3170-READUPD-BP13K313.                                                   
      *-----------------------------*                                           
                                                                                
           MOVE SPACES                    TO BP13K313-REC.                      
           INITIALIZE                        BP13K313-REC.                      
                                                                                
           MOVE F313-KEY-FLD                    TO K313-KEY-FLD.                
                                                                                
           READ BP13K313.                                                       
                                                                                
           EVALUATE WS-K313-STATUS                                              
           WHEN 00                                                              
                PERFORM 3180-REWRITE-BP13K313 THRU 3180-EXIT                    
                                                                                
           WHEN 23                                                              
                MOVE SPACES                     TO BP13K313-REC                 
                INITIALIZE                         BP13K313-REC                 
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'BP13K313 - READ ERR FOR REC ' K313-KEY-FLD             
                MOVE WS-K313-STATUS          TO   RETURN-CODE                   
                PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       3170-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3180-REWRITE-BP13K313.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE K317-DTE-HLE-EXT         TO K313-DTE-HLE-EXTENSION              
                                                                                
           REWRITE BP13K313-REC.                                                
                                                                                
           EVALUATE WS-K313-STATUS                                              
           WHEN 00                                                              
                ADD 1 TO WS-K313-UPD                                            
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'BP13K313 - READ ERR FOR REC ' K313-KEY-FLD             
                MOVE WS-K313-STATUS          TO   RETURN-CODE                   
                PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       3180-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-READ-BM06K100.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                         TO K100-REC.                     
           INITIALIZE                             K100-REC.                     
           MOVE 'N'                            TO WS-K100-EOF                   
                                                  WS-K100-FND.                  
           MOVE ZEROES                         TO WS-K110-CTR.                  
                                                                                
           MOVE F313-NUM-ESTATE                TO K100-ESTATE.                  
           MOVE F313-NUM-NEIGHBOURHOOD         TO K100-NEIGHBOURHOOD.           
           MOVE F313-NUM-CONTRACT-NO           TO K100-CONTRACT-NO.             
                                                                                
           START BM06K100 KEY >= K100-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K100-STATUS                                              
           WHEN 00                                                              
                PERFORM 3210-READNEXT-BM06K100 THRU 3210-EXIT                   
                   UNTIL WS-K100-EOF = 'Y'                                      
           WHEN 10                                                              
           WHEN 23                                                              
           WHEN 46                                                              
                MOVE 'Y'                   TO   WS-K100-EOF                     
           WHEN OTHER                                                           
                DISPLAY 'BM06K100 - STARTBR ERROR ' WS-K100-STATUS              
                MOVE WS-K100-STATUS        TO   RETURN-CODE                     
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3210-READNEXT-BM06K100.                                                  
      *-------------------------------------------------------------            
           READ BM06K100 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K100-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
                IF K100-KEY-FLD(1:11) = F313-NUM-KEY1(1:11)                     
                   MOVE 'Y'                    TO   WS-K100-FND                 
                   PERFORM 3300-READ-BM06K110  THRU 3300-EXIT                   
                ELSE                                                            
                   MOVE 'Y'                    TO   WS-K100-EOF                 
                END-IF                                                          
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
           WHEN 46                                                              
                MOVE 'Y'                       TO   WS-K100-EOF                 
           WHEN OTHER                                                           
                DISPLAY 'BM06K100 - READ NEXT ERROR ' WS-K100-STATUS            
                MOVE WS-K100-STATUS            TO   RETURN-CODE                 
                PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3300-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                         TO K110-REC.                     
           INITIALIZE                             K110-REC.                     
           MOVE 'N'                            TO WS-K110-EOF.                  
                                                                                
           MOVE K100-KEY-FLD                   TO K110-AIX1.                    
                                                                                
           START BM06K110 KEY >= K110-AIX1.                                     
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
                PERFORM 3310-READNEXT-BM06K110 THRU 3310-EXIT                   
                   UNTIL WS-K110-EOF = 'Y'                                      
           WHEN 10                                                              
           WHEN 23                                                              
           WHEN 46                                                              
                MOVE 'Y'                 TO   WS-K110-EOF                       
           WHEN OTHER                                                           
                DISPLAY 'BM06K110 - STARTBR ERROR ' WS-K110-STATUS              
                MOVE WS-K110-STATUS      TO   RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3310-READNEXT-BM06K110.                                                  
      *-------------------------------------------------------------            
           READ BM06K110 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
                IF K110-AIX1 = K100-KEY-FLD                                     
                   MOVE 'Y'                        TO   WS-VALID                
                   IF K110-REGN-NO NOT = SPACES AND LOW-VALUES                  
                      MOVE SPACES                  TO   WS-REGN                 
                      MOVE K110-REGN-NO            TO   WS-REGN                 
                      PERFORM 3100-READ-BP13K800   THRU 3100-EXIT               
                   ELSE                                                         
                      MOVE 'N'                     TO   WS-VALID                
                      MOVE '7'                     TO   WS-ERROR                
                      ADD 1                        TO   WS-ERROR-7-CTR          
                   END-IF                                                       
                                                                                
                   IF WS-VALID = 'Y'                                            
                      ADD 1                        TO   WS-K110-CTR             
                      PERFORM 5500-WRITE-BP13F800  THRU 5500-EXIT               
                      ADD 1                        TO   WS-F800-PRINTED2        
                   END-IF                                                       
                ELSE                                                            
                   MOVE 'Y'                        TO   WS-K110-EOF             
                END-IF                                                          
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
           WHEN 46                                                              
                MOVE 'Y'                       TO   WS-K110-EOF                 
           WHEN OTHER                                                           
                DISPLAY 'BM06K110 - READ NEXT ERROR ' WS-K110-STATUS            
                MOVE WS-K110-STATUS            TO   RETURN-CODE                 
                PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3310-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       5500-WRITE-BP13F800.                                                     
      *-------------------------------------------------------------            
           WRITE BP13F800-MASTER     FROM BP13K800-MASTER.                      
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5600-UPDATE-BP13K800.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE F313-DTE-HLE-EXTENSION   TO K800-DTE-HLE-EXTENDED               
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
              ADD 1 TO WS-K800-UPD                                              
           WHEN OTHER                                                           
              DISPLAY 'BP13K800 - READ ERR FOR REC ' F800-NUM-REGN              
              MOVE WS-K800-STATUS          TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-WRITE-P13L439A.                                                     
      *-------------------------------------------------------------            
           MOVE 'N'                  TO   WS-VALID.                             
                                                                                
           IF WS-LINE-CTR > 55                                                  
              PERFORM 6500-WRITE-HEADING  THRU 6500-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE SPACES               TO   L439-DETAIL.                          
           INITIALIZE                     L439-DETAIL.                          
                                                                                
           EVALUATE WS-ERROR                                                    
           WHEN '1'                                                             
              MOVE 'SCHEME ACCT IS BLANK'                                       
                                     TO   L439-REMARKS                          
           WHEN '2'                                                             
              MOVE 'LOAN TAG IS NOT = "H"'                                      
                                     TO   L439-REMARKS                          
           WHEN '3'                                                             
              MOVE 'HLE IS BLANK'                                               
                                     TO   L439-REMARKS                          
           WHEN '4'                                                             
              MOVE 'REGN NOT FOUND IN BP13K800'                                 
                                     TO   L439-REMARKS                          
           WHEN '5'                                                             
              MOVE 'EST/NEIGH/CONT NOT FND IN BM06K100'                         
                                     TO   L439-REMARKS                          
           WHEN '6'                                                             
              MOVE 'NO VALID RECS IN BM06K110 FOR EST/NEIGH/CONT'               
                                     TO   L439-REMARKS                          
              INITIALIZE                  WS-REGN                               
           WHEN '7'                                                             
              MOVE 'REGNO IS BLANK IN BM06K110'                                 
                                     TO   L439-REMARKS                          
           WHEN '8'                                                             
              MOVE 'NO FURTHER EXTENSION'                                       
                                     TO   L439-REMARKS                          
           WHEN OTHER                                                           
              MOVE SPACES            TO   L439-REMARKS                          
           END-EVALUATE.                                                        
                                                                                
           ADD 1                     TO   WS-SNO-CTR.                           
           MOVE WS-SNO-CTR           TO   L439-SNO.                             
                                                                                
           IF WS-PROCESS-REGN = 'N'                                             
              MOVE F313-NUM-KEY1(1:11)  TO   L439-EST-NEIGH-CONT                
           END-IF.                                                              
                                                                                
           IF WS-ERROR = '5' OR '6'                                             
              MOVE SPACES            TO   L439-REGN                             
           ELSE                                                                 
              MOVE WS-REGN           TO   L439-REGN                             
           END-IF.                                                              
                                                                                
           WRITE P13L439A-REC        FROM L439-DETAIL.                          
           ADD 1                     TO   WS-LINE-CTR.                          
                                                                                
           IF WS-PROCESS-REGN = 'Y'                                             
              ADD 1                  TO   WS-L439-PRINTED1                      
           ELSE                                                                 
              ADD 1                  TO   WS-L439-PRINTED2                      
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6500-WRITE-HEADING.                                                      
      *-------------------------------------------------------------            
           ADD 1              TO   WS-PAGE-CTR.                                 
                                                                                
           MOVE  WS-PAGE-CTR  TO   L439-PAGE.                                   
           WRITE P13L439A-REC FROM L439-HD1 AFTER PAGE.                         
           WRITE P13L439A-REC FROM L439-HD2.                                    
           WRITE P13L439A-REC FROM WS-BLANK-LINE.                               
           WRITE P13L439A-REC FROM L439-HD3.                                    
           WRITE P13L439A-REC FROM WS-DASH-LINE.                                
                                                                                
           MOVE 5             TO   WS-LINE-CTR.                                 
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY ' - BP13C439 CONTROL TOTAL - ' WS-SYSTEM-DATE.               
           DISPLAY SPACES.                                                      
           DISPLAY '   INPUT BY REGN                '.                          
           DISPLAY '   -------------                '.                          
           DISPLAY '   BP13F313 READ              : ' WS-F313-READ-REGN.        
           DISPLAY '   BP13L439 PRINTED           : ' WS-L439-PRINTED1.         
           DISPLAY '   BP13F800 RECS OUTPUT       : ' WS-F800-PRINTED1.         
           DISPLAY SPACES.                                                      
           DISPLAY '   INPUT BY EST/NEIGH/CONT      '.                          
           DISPLAY '   -----------------------      '.                          
           DISPLAY '   BP13F313 READ              : ' WS-F313-READ-EST.         
           DISPLAY '   BP13L439 PRINTED           : ' WS-L439-PRINTED2.         
           DISPLAY '   WS-ERROR-OTH-CTR (1-2)     : ' WS-ERROR-OTH-CTR.         
           DISPLAY '   WS-ERROR-3-CTR             : ' WS-ERROR-3-CTR.           
           DISPLAY '   WS-ERROR-4-CTR             : ' WS-ERROR-4-CTR.           
           DISPLAY '   WS-ERROR-5-CTR             : ' WS-ERROR-5-CTR.           
           DISPLAY '   WS-ERROR-6-CTR             : ' WS-ERROR-6-CTR.           
           DISPLAY '   WS-ERROR-7-CTR             : ' WS-ERROR-7-CTR.           
           DISPLAY '   WS-ERROR-8-CTR             : ' WS-ERROR-8-CTR.           
           DISPLAY SPACES.                                                      
           DISPLAY '   BP13F800 RECS OUTPUT       : ' WS-F800-PRINTED2.         
           DISPLAY '   BP13K800 RECS UPDATED      : ' WS-K800-UPD.              
           DISPLAY '   BP13K313 RECS UPDATED      : ' WS-K313-UPD.              
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE BP13F313                                                       
                 BP13K800                                                       
                 BP13K313                                                       
                 BP13K317                                                       
                 BP27K800                                                       
                 BM06K100                                                       
                 BM06K110                                                       
                 BP13F800                                                       
                 P13L439A.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - CLOSE ERROR '  WS-K800-STATUS                 
              MOVE    WS-K800-STATUS       TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K313-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K313 - CLOSE ERROR '  WS-K313-STATUS                 
              MOVE    WS-K313-STATUS       TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K317-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K317 - CLOSE ERROR '  WS-K317-STATUS                 
              MOVE    WS-K317-STATUS       TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-BP27K800-STAT NOT = 00 AND 97                                  
              DISPLAY 'BP27K800 - CLOSE ERROR '  WS-BP27K800-STAT               
              MOVE    WS-BP27K800-STAT     TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K100 - CLOSE ERROR '  WS-K100-STATUS                 
              MOVE    WS-K100-STATUS       TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - CLOSE ERROR '  WS-K110-STATUS                 
              MOVE    WS-K110-STATUS       TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
