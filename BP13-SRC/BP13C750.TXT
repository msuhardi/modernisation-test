       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C750.                                                 
       AUTHOR.        RAYATHATHA ASHOK.                                         
                                                                                
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID : BP13                                    *                
      *    OBJECTIVE  : TO FACILITATE PIGEONHOLING OF CGO WITH *                
      *                 NO CONTROL ON ETHNIC BY ALLOC-SCHEME,  *                
      *                 ADD MAXIMUM ALLOWABLE PA1, PA2, PA3    *                
      *                 TO MAXIMUM ALLOWABLE CCC1, CCC2, CCC3, *                
      *                 SO THAT GRO CAN TAKE UP THE ETHNIC     *                
      *                 QUOTA AVAILABLE FOR PA SCHEME.         *                
      *                 TO ADD THE THE BALANCE UNITS AND CAT   *                
      *                 QUOTA FROM MTF/TCP/JB  TO CCC.         *                
      *  CHG-NO  BY  ON    DESCRIPTION                         *                
      *  -----------------------------                         *                
      * I920718  LJL 120193 TO CHANGE FROM COBOL74 TO COBOL85.  *               
      * I930335  LSB 930317 EXPAND BP13K010 FOR RDC             *               
      * SOCPH9B  LSB 941012 UNSUCC JB/TCP TRANSFER TO GRO       *               
      * C950543  GJ  950523 UNSUCC JB NOT TO BE XFERRED TO GRO  *               
      * BP132555 CKK 040707 UNSUCC TCP NOT TO BE XFERRED TO GRO *               
      * BP132870 CKK 061110 UNSUCC MTF NOT TO BE XFERRED TO GRO *               
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.     IBM-3090.                                           
       OBJECT-COMPUTER.     IBM-3090.                                           
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K010 ASSIGN TO BP13K010                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS SEQUENTIAL                                 
                  RECORD KEY      IS K010-KEY-FLD                               
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13K010.                                                           
       COPY BP13K010.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
        01  WS-VARIABLES.                                                       
            05 WS-CNT-K010-READ            PIC 9(08) VALUE 0.                   
            05 WS-CNT-K010-REWRITE         PIC 9(08) VALUE 0.                   
            05 WS-K010-EOF                 PIC X(01) VALUE 'N'.                 
               88 K010-EOF                     VALUE 'Y'.                       
            05 WS-K010-STATUS              PIC 9(02) VALUE 0.                   
            05 WS-NUM-BAL-MTF              PIC 9(05) VALUE 0.                   
            05 WS-NUM-BAL-TCP              PIC 9(05) VALUE 0.                   
            05 WS-NUM-BAL-JB               PIC 9(05) VALUE 0.                   
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************                                                        
       0000-CONTROL-RTN.                                                        
      ******************                                                        
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 1500-START-BP13K010-KEY THRU 1500-EXIT.              01790000
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
             UNTIL K010-EOF.                                                    
                                                                                
           PERFORM 9999-CLOSE-FILES        THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************                                                         
       1000-OPEN-FILES.                                                         
      *****************                                                         
                                                                                
           OPEN I-O    BP13K010.                                                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
              PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                        01780000
      *************************                                                 
       1500-START-BP13K010-KEY.                                         01790000
      *************************                                                 
                                                                        01780000
           MOVE SPACES TO K010-KEY-FLD                                  01800000
                                                                        01780000
           START BP13K010 KEY > K010-KEY-FLD                            01810000
                                                                                
           IF WS-K010-STATUS  = 00                                              
              PERFORM 2000-READ-BP13K010    THRU 2000-EXIT                      
           ELSE                                                                 
              DISPLAY 'START READ BP13K010, STATUS ' WS-K010-STATUS             
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
              PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
       1500-EXIT.                                                               
            EXIT.                                                               
                                                                        01860000
      ********************                                                      
       2000-READ-BP13K010.                                                      
      ********************                                                      
                                                                                
           READ BP13K010 NEXT AT END                                            
                MOVE 'Y' TO WS-K010-EOF                                         
                GO TO 2000-EXIT                                                 
                END-READ.                                                       
                                                                                
           IF WS-K010-STATUS NOT = 00                                           
              DISPLAY 'ERROR READING BP13K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
              PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           ADD 1                         TO WS-CNT-K010-READ.                   
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *******************                                                       
       3000-MAIN-ROUTINE.                                                       
      *******************                                                       
                                                                                
           PERFORM 4000-ADD-ROUTINE   THRU 4000-EXIT.                           
                                                                                
           REWRITE BP13K010-TENDER.                                             
                                                                                
           IF WS-K010-STATUS = 00                                               
              ADD 1                   TO WS-CNT-K010-REWRITE                    
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13K010, STATUS '                       
                                       WS-K010-STATUS                           
              DISPLAY '                          KEY IS '                       
                                       K010-KEY-FLD                             
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
              PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13K010    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************                                                        
       4000-ADD-ROUTINE.                                                        
      ******************                                                        
                                                                                
           IF K010-NUM-MAX-PA1 NOT NUMERIC                                      
              MOVE ZEROS TO K010-NUM-MAX-PA1                                    
           END-IF.                                                              
                                                                                
           IF K010-NUM-MAX-CCC1 NOT NUMERIC                                     
              MOVE ZEROS TO K010-NUM-MAX-CCC1                                   
           END-IF.                                                              
                                                                                
           ADD K010-NUM-MAX-PA1 TO K010-NUM-MAX-CCC1.                           
                                                                                
           IF K010-NUM-MAX-PA2 NOT NUMERIC                                      
              MOVE ZEROS TO K010-NUM-MAX-PA2                                    
           END-IF.                                                              
                                                                                
           IF K010-NUM-MAX-CCC2 NOT NUMERIC                                     
              MOVE ZEROS TO K010-NUM-MAX-CCC2                                   
           END-IF.                                                              
                                                                                
           ADD K010-NUM-MAX-PA2 TO K010-NUM-MAX-CCC2.                           
                                                                                
           IF K010-NUM-MAX-PA3 NOT NUMERIC                                      
              MOVE ZEROS TO K010-NUM-MAX-PA3                                    
           END-IF.                                                              
                                                                                
           IF K010-NUM-MAX-CCC3 NOT NUMERIC                                     
              MOVE ZEROS TO K010-NUM-MAX-CCC3                                   
           END-IF.                                                              
                                                                                
           ADD K010-NUM-MAX-PA3 TO K010-NUM-MAX-CCC3.                           
                                                                                
      ******************************************************************        
      ***  THE FOLLOWING LINES HAVE BEEN COMMENTED OUT SO THAT THE MTF *        
      ***  QUOTA IS INTACT WHICH IS TO BE USED IN DBSS SHORTLISTING    *        
      ******************************************************************        
                                                                                
      ***  COMPUTE WS-NUM-BAL-MTF = K010-NUM-TOT-MTF - K010-REG-MTF1 -          
      ***                           K010-REG-MTF2    - K010-REG-MTF3.           
      ***                                                                       
      ***  IF WS-NUM-BAL-MTF > 0                                                
      ***     ADD WS-NUM-BAL-MTF        TO K010-NUM-TOT-CCC                     
      ***     SUBTRACT WS-NUM-BAL-MTF FROM K010-NUM-TOT-MTF                     
      ***     COMPUTE K010-NUM-MAX-CCC1 =  K010-NUM-MAX-CCC1 +                  
      ***            (K010-NUM-MAX-MTF1 -  K010-REG-MTF1)                       
      ***     COMPUTE K010-NUM-MAX-CCC2 =  K010-NUM-MAX-CCC2 +                  
      ***            (K010-NUM-MAX-MTF2 -  K010-REG-MTF2)                       
      ***     COMPUTE K010-NUM-MAX-CCC3 =  K010-NUM-MAX-CCC3 +                  
      ***            (K010-NUM-MAX-MTF3 -  K010-REG-MTF3)                       
      ***  END-IF.                                                              
                                                                                
      ******************************************************************        
      ***  THE FOLLOWING LINES HAVE BEEN COMMENTED OUT SO THAT THE TCP *        
      ***  QUOTA IS INTACT WHICH IS TO BE USED IN BP13APRO PROGRAM     *        
      ******************************************************************        
                                                                                
      ***  COMPUTE WS-NUM-BAL-TCP = K010-NUM-TOT-TCP - K010-REG-TCP1 -          
      ***                           K010-REG-TCP2    - K010-REG-TCP3.           
      ***                                                                       
      ***  IF WS-NUM-BAL-TCP > 0                                                
      ***     ADD WS-NUM-BAL-TCP        TO K010-NUM-TOT-CCC                     
      ***     SUBTRACT WS-NUM-BAL-TCP FROM K010-NUM-TOT-TCP                     
      ***     COMPUTE K010-NUM-MAX-CCC1 =  K010-NUM-MAX-CCC1 +                  
      ***            (K010-NUM-MAX-TCP1 -  K010-REG-TCP1)                       
      ***     COMPUTE K010-NUM-MAX-CCC2 =  K010-NUM-MAX-CCC2 +                  
      ***            (K010-NUM-MAX-TCP2 -  K010-REG-TCP2)                       
      ***     COMPUTE K010-NUM-MAX-CCC3 =  K010-NUM-MAX-CCC3 +                  
      ***            (K010-NUM-MAX-TCP3 -  K010-REG-TCP3)                       
      ***  END-IF.                                                              
                                                                                
      ******************************************************************        
      ***  THE FOLLOWING LINES HAVE BEEN COMMENTED OUT SO THAT THE JB  *        
      ***  QUOTA IS INTACT WHICH IS TO BE USED IN BP13U719 PROGRAM     *        
      ******************************************************************        
                                                                                
      ***  COMPUTE WS-NUM-BAL-JB = K010-NUM-TOT-JB - K010-REG-JB1 -             
      ***                          K010-REG-JB2    - K010-REG-JB3.              
      ***                                                                       
      ***  IF WS-NUM-BAL-JB > 0                                                 
      ***     ADD WS-NUM-BAL-JB          TO K010-NUM-TOT-CCC                    
      ***     SUBTRACT WS-NUM-BAL-JB   FROM K010-NUM-TOT-JB                     
      ***     COMPUTE K010-NUM-MAX-CCC1 =  K010-NUM-MAX-CCC1 +                  
      ***            (K010-NUM-MAX-JB1  -  K010-REG-JB1)                        
      ***     COMPUTE K010-NUM-MAX-CCC2 =  K010-NUM-MAX-CCC2 +                  
      ***            (K010-NUM-MAX-JB2  -  K010-REG-JB2)                        
      ***     COMPUTE K010-NUM-MAX-CCC3 =  K010-NUM-MAX-CCC3 +                  
      ***            (K010-NUM-MAX-JB3  -  K010-REG-JB3)                        
      ***  END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       9999-CLOSE-FILES.                                                        
      ******************                                                        
                                                                                
           DISPLAY '>>>>>>>>>>>>>>>> BP13C750 <<<<<<<<<<<<<<<<<<'.              
           DISPLAY '- ADD MAX-CAT QUOTA FROM PA/MTF/TCP TO GRO - '.             
           DISPLAY '  '.                                                        
           DISPLAY 'K010 RECORDS READ    : '  WS-CNT-K010-READ.                 
           DISPLAY 'K010 RECORDS REWRITE : '  WS-CNT-K010-REWRITE.              
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13K010.                                                      
                                                                                
           IF WS-K010-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS       TO RETURN-CODE                          
           END-IF.                                                              
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
