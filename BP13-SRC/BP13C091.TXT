       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C091.                                                    
       AUTHOR.     SURESH.                                                      
       INSTALLATION. HDB.                                                       
       DATE-WRITTEN. 05-11-1999.                                                
       DATE-COMPILED.                                                           
      *-----------------------------------------------------------*             
      *        SYSTEM OF COMMITMENT - BP13                        *             
      *-----------------------------------------------------------*             
      * OBJECTIVES :                                              *             
      *                                                           *             
      *  READ BROADCAST FILE FROM CDB, RECORDS PASSED FROM MHA,   *             
      *  MATCH BP13F820 WITH BE01F516 USING NRIC NO.              *             
      *  IF PR/CC-RENOUNCE CASE FOUND, OUTPUT BP13F820 WITH       *             
      *  PR/RENOUNCE DATE.                                        *             
      *                                                           *             
      * INPUT  FILE   : BE01F516 (INTERFACE FILE FROM CDB)        *             
      * OUTPUT FILE   : BP13F820 (OCCUP FILE)                     *             
      *               : P13F820A (OCCUP FILE WITH DTE-RENOUNCE    *             
      *                                                           *             
      * REQ NO.   DATE    BY  DESCRIPTION                         *             
      * -------   ----    --- -----------                         *             
      * BP130851 31072000 SRN NEW PROGRAM                         *             
      * BP131021 18062001 STP - CHG BE01 LAYOUT                   *             
      * BP132887 11062007 BA2 - CHG BE01F516 FILE LAYOUT          *             
      *                                                           *             
      *-----------------------------------------------------------*             
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F820   ASSIGN TO BP13F820.                                
                                                                                
           SELECT BE01F516   ASSIGN TO BE01F516.                                
                                                                                
           SELECT P13F820A   ASSIGN TO P13F820A.                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD BE01F516.                                                             
       COPY BE01F516.                                                           
                                                                                
       FD BP13F820.                                                             
       COPY BP13F820.                                                           
                                                                                
       FD P13F820A.                                                             
       01  P13F820A-REC.                                                        
           05 FILLER                             PIC X(234).                    
           05 F820A-DTE-CHG-PR                   PIC X(8).                      
           05 F820A-DTE-DEATH                    PIC X(8).                      
           05 F820A-PR-TAG                       PIC X(2).                      
           05 FILLER                             PIC X(148).                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CONTROL-COUNT.                                                    
           05  WS-F820-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-F516-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-F516-NO-DATE         PIC 9(8)    VALUE ZEROS.                 
           05  WS-F516-DTE-INVALID     PIC 9(8)    VALUE ZEROS.                 
           05  WS-F820-WRITE           PIC 9(8)    VALUE ZEROS.                 
                                                                                
       01  WS-F516-DTE-CHG-PR          PIC X(10)   VALUE SPACES.                
       01  WS-PR-TAG                   PIC X(2)    VALUE SPACES.                
       01  WS-SYS-DATE                 PIC 9(8)    VALUE ZEROES.                
       01  WS-PRT-DATE                 PIC X(10)   VALUE SPACES.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------*                 
       0000-MAIN.                                                               
      *-------------------------------------------------------*                 
            PERFORM 0500-OPEN-FILES       THRU 0500-EXIT.                       
            PERFORM 1000-READ-BE01F516    THRU 1000-EXIT.                       
            PERFORM 2000-READ-BP13F820    THRU 2000-EXIT.                       
            PERFORM 3000-PROCESS          THRU 3000-EXIT                        
                     UNTIL BE01F516-IDNO = HIGH-VALUES OR                       
                           F820-NUM-NRIC = HIGH-VALUES.                         
            PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                       
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       0500-OPEN-FILES.                                                         
      *-------------------------------------------------------*                 
            OPEN INPUT  BE01F516                                                
                        BP13F820                                                
                 OUTPUT P13F820A.                                               
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8)   TO WS-SYS-DATE.                   
                                                                                
       0500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       1000-READ-BE01F516.                                                      
      *-------------------------------------------------------*                 
            READ BE01F516 AT END                                                
                 MOVE HIGH-VALUES TO BE01F516-IDNO                              
                 GO TO 1000-EXIT.                                               
                                                                                
            ADD 1                            TO WS-F516-READ.                   
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       2000-READ-BP13F820.                                                      
      *-------------------------------------------------------*                 
            READ BP13F820 AT END                                                
                 MOVE HIGH-VALUES TO F820-NUM-NRIC                              
                 GO TO 2000-EXIT.                                               
                                                                                
            ADD 1                            TO WS-F820-READ.                   
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       3000-PROCESS.                                                            
      *-------------------------------------------------------*                 
                                                                                
            IF BE01F516-IDNO = F820-NUM-NRIC                                    
               PERFORM 4200-VALIDATE-BE01F516    THRU 4200-EXIT UNTIL           
                       BE01F516-IDNO NOT = F820-NUM-NRIC OR                     
                       F820-NUM-NRIC = HIGH-VALUES                              
               PERFORM 1000-READ-BE01F516        THRU 1000-EXIT                 
            ELSE                                                                
               IF BE01F516-IDNO > F820-NUM-NRIC                                 
                  PERFORM 2000-READ-BP13F820     THRU 2000-EXIT                 
               ELSE                                                             
                  IF BE01F516-IDNO < F820-NUM-NRIC                              
                     PERFORM 1000-READ-BE01F516  THRU 1000-EXIT                 
                  END-IF                                                        
               END-IF                                                           
            END-IF.                                                             
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       4200-VALIDATE-BE01F516.                                                  
      *-------------------------------------------------------*                 
            STRING BE01F516-DTE-CHG-PR(1:4),                                    
                   BE01F516-DTE-CHG-PR(6:2),                                    
                   BE01F516-DTE-CHG-PR(9:2)                                     
                   DELIMITED BY SIZE INTO                                       
                   WS-F516-DTE-CHG-PR.                                          
                                                                                
            IF BE01F516-BRDCST-CEASED-PR                                        
               MOVE  'PC'   TO WS-PR-TAG                                        
               PERFORM 4250-WRITE-P13F820A  THRU 4250-EXIT                      
            ELSE                                                                
             IF BE01F516-BRDCST-GRNTD-PR                                        
                MOVE 'PG'   TO WS-PR-TAG                                        
                PERFORM 4250-WRITE-P13F820A THRU 4250-EXIT                      
            ELSE                                                                
            IF BE01F516-BRDCST-CEASED-CTZN                                      
               MOVE  'CC'   TO WS-PR-TAG                                        
               PERFORM 4250-WRITE-P13F820A  THRU 4250-EXIT                      
            ELSE                                                                
             IF BE01F516-BRDCST-GRNTD-CTZN                                      
                MOVE 'CG'   TO WS-PR-TAG                                        
                PERFORM 4250-WRITE-P13F820A THRU 4250-EXIT                      
            END-IF.                                                             
            PERFORM 2000-READ-BP13F820      THRU 2000-EXIT.                     
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       4250-WRITE-P13F820A.                                                     
      *-------------------------------------------------------*                 
            MOVE BP13F820-REC            TO P13F820A-REC.                       
            MOVE SPACES                  TO F820A-DTE-CHG-PR.                   
            MOVE SPACES                  TO F820A-DTE-DEATH.                    
            MOVE WS-F516-DTE-CHG-PR      TO F820A-DTE-CHG-PR.                   
            MOVE WS-PR-TAG               TO F820A-PR-TAG.                       
            WRITE P13F820A-REC.                                                 
            ADD 1                        TO WS-F820-WRITE.                      
                                                                                
       4250-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------*                 
            STRING WS-SYS-DATE (7:2), '/',                                      
                   WS-SYS-DATE (5:2), '/',                                      
                   WS-SYS-DATE (1:4)                                            
                   DELIMITED BY SIZE                                            
                   INTO                                                         
                   WS-PRT-DATE.                                                 
                                                                                
            DISPLAY '   '.                                                      
            DISPLAY 'SUMMARY REPORT   BP13C091   RUNDATE ' WS-PRT-DATE.         
            DISPLAY '______________________________________________'.           
            DISPLAY '   '.                                                      
            DISPLAY 'NO OF RECORDS READ FROM BE01F516   : '                     
                                            WS-F516-READ                        
            DISPLAY 'NO OF RECORDS READ FROM BP13F820   : '                     
                                            WS-F820-READ                        
            DISPLAY 'NO OF RECORDS INVALID DATE         : '                     
                                            WS-F516-DTE-INVALID                 
            DISPLAY 'NO OF RECORDS WITHOUT DATE         : '                     
                                            WS-F516-NO-DATE                     
            DISPLAY 'NO OF RECORDS WRITTEN TO P13F820A  : '                     
                                            WS-F820-WRITE                       
                                                                                
            CLOSE BE01F516                                                      
                  BP13F820                                                      
                  P13F820A.                                                     
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
