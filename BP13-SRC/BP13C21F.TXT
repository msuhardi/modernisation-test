      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C21F.                                                 
      *AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
      *DATE-WRITTEN.  24 JULY 2007.                                             
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : ADD RECORDS TO BP13K249                     *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13K762                                              *           
      *   2.  BP13K060                                              *           
      *   3.  BP13K062                                              *           
      *                                                             *           
      *   INPUT/OUTPUT:                                             *           
      *   1.  BP13K249                                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP133094 24/07/2007 RB12 NEW PROGRAM                        *           
      * BP133177 24/10/2007 RB12 CHANGE NUM-FT 1 TO 1A AND 2A       *           
      * BP133231 15/01/2008 RB12 REMOVE BP13K769 LOGIC              *           
      * BP133367 25/04/2008 RB12 INCREASE K249 LENGTH               *           
      * BP133485 17/12/2008 RB12 POPULATE ALT KEY1 VALUES           *           
      * BP133487 29/12/2008 LMS1 TO BYPASS 1B                       *           
      * BP133933 10/08/2010 RB12 RETRIEVE PROJECT NAME FROM BP13K062*           
      * BP133978 13/08/2010 RB12 MOVE NUM BTO ZONE FROM K240 TO K249*           
      * BP134207 18/04/2011 ESA1 REWRITE BP13K249 IF FILE-ORIG NOT  *           
      *                          IN BP13K240                        *           
      * BP135135 14/11/2013 ESA1 ADD READING OF BP13K813 FOR BONUS  *           
      * BP135190 04/12/2013 SMR2 EXPAND BP13K813 TO 1000            *           
      * BP135415 13/08/2014 ESA1 EXTEND BP13K249 TO 450             *           
      * BP135783 25/05/2015 ESA1 TO CATER FOR K249-CDE-NT           *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K762 ASSIGN       TO BP13K762                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K762-KEY-FLD                         
                           FILE STATUS  IS WS-K762-STATUS                       
                           ACCESS MODE  IS DYNAMIC.                             
                                                                                
           SELECT BP13K249 ASSIGN       TO BP13K249                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K249-KEY-FLD                         
                           FILE STATUS  IS WS-K249-STATUS                       
                           ACCESS MODE  IS DYNAMIC.                             
                                                                                
           SELECT BP13K060 ASSIGN       TO BP13K060                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K060-KEY-FLD                         
                           FILE STATUS  IS WS-K060-STATUS                       
                           ACCESS MODE  IS RANDOM.                              
                                                                                
           SELECT BP13K062 ASSIGN       TO BP13K062                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K062-KEY-FLD                         
                           FILE STATUS  IS WS-K062-STATUS                       
                           ACCESS MODE  IS RANDOM.                              
                                                                                
           SELECT BP13K813 ASSIGN       TO BP13K813                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K813-KEY-FLD                         
                           FILE STATUS  IS WS-K813-STATUS                       
                           ACCESS MODE  IS RANDOM.                              
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K762                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K762.                                                           
                                                                                
       FD   BP13K249                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K249.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25 CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K062                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K062.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-K249-STATUS         PIC 9(02) VALUE ZEROES.                       
       01  WS-K060-STATUS         PIC 9(02) VALUE ZEROES.                       
       01  WS-K062-STATUS         PIC 9(02) VALUE ZEROES.                       
       01  WS-K813-STATUS         PIC 9(02) VALUE ZEROES.                       
       01  WS-K762-STATUS         PIC 9(02) VALUE ZEROES.                       
       01  WS-K762-EOF            PIC X     VALUE SPACES.                       
       01  WS-K062-FND            PIC X     VALUE SPACES.                       
       01  WS-K762-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-K249-WRITE-CNT      PIC 9(08) VALUE ZEROES.                       
       01  WS-K249-WRITE-ERR      PIC 9(08) VALUE ZEROES.                       
       01  WS-K249-REWRITE-CNT    PIC 9(08) VALUE ZEROES.                       
       01  WS-K249-REWRITE-ERR    PIC 9(08) VALUE ZEROES.                       
       01  WS-NUM-FT              PIC X(02) VALUE SPACES.                       
       01  WS-FLAT-TYPE           PIC X(09) VALUE SPACES.                       
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-PRE-PROCESS      THRU 1000-EXIT.                        
           PERFORM 2000-READ-K762        THRU 2000-EXIT.                        
           PERFORM 3000-PROCESS-K762-REC THRU 3000-EXIT                         
                   UNTIL WS-K762-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-PRE-PROCESS.                                                        
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13K762                                                 
                       BP13K060                                                 
                       BP13K062                                                 
                       BP13K813                                                 
                I-O    BP13K249.                                                
                                                                                
           IF WS-K762-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13K762 - OPEN ERROR: ' WS-K762-STATUS                  
              MOVE WS-K762-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K249-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13K249 - OPEN ERROR: ' WS-K249-STATUS                  
              MOVE WS-K249-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13K060 - OPEN ERROR: ' WS-K060-STATUS                  
              MOVE WS-K060-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K062-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13K062 - OPEN ERROR: ' WS-K062-STATUS                  
              MOVE WS-K062-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13K813 - OPEN ERROR: ' WS-K813-STATUS                  
              MOVE WS-K813-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-K762.                                                          
      *---------------*                                                         
                                                                                
           READ BP13K762 NEXT RECORD AT END                                     
                MOVE HIGH-VALUES TO BP13K762-REC                                
                MOVE 'Y' TO WS-K762-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-K762-READ-CNT.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3000-PROCESS-K762-REC.                                                   
      *---------------------*                                                   
                                                                                
           IF (K762-NUM-FT = '1B') OR                                           
              (K762-DTE-BALLOT = '200912')                                      
              PERFORM 2000-READ-K762     THRU 2000-EXIT                         
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 3100-CREATE-FROM-K762 THRU 3100-EXIT.                        
                                                                                
           PERFORM 2000-READ-K762        THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3100-CREATE-FROM-K762.                                                   
      *---------------------*                                                   
                                                                                
           MOVE SPACES                     TO BP13K249-REC.                     
           INITIALIZE                         BP13K249-REC.                     
                                                                                
           MOVE 02                         TO K060-SERIAL-NO.                   
           MOVE K762-NUM-CHOICE            TO K060-CODE.                        
           PERFORM 7000-DECODE-NEW-TOWN  THRU 7000-EXIT.                        
                                                                                
           MOVE K060-DESC                  TO K249-NUM-NEW-TOWN                 
                                              K249-NUM-NEW-TOWN1.               
                                                                                
           MOVE K762-NUM-FT                TO WS-NUM-FT.                        
                                                                                
           PERFORM 8000-DECODE-FLAT-TYPE THRU 8000-EXIT.                        
                                                                                
           MOVE WS-FLAT-TYPE               TO K249-NUM-FLAT-TYPE                
                                              K249-NUM-FLAT-TYPE1.              
                                                                                
           MOVE 'BTO'                      TO K249-NUM-SELECTION                
                                              K249-NUM-SELECTION1.              
                                                                                
           MOVE K762-DTE-BALLOT            TO K249-DTE-BALLOT                   
                                              K249-DTE-BALLOT1.                 
                                                                                
           PERFORM 3160-READ-K813          THRU 3160-EXIT.                      
                                                                                
           MOVE K813-NUM-ADDITIONAL-FLAT   TO K249-NUM-BONUS.                   
           MOVE K813-CDE-NT                TO K249-CDE-NT.                      
                                                                                
           READ BP13K249 KEY IS K249-KEY-FLD.                                   
                                                                                
           EVALUATE WS-K249-STATUS                                              
             WHEN 00                                                            
                IF K249-FILE-ORIG NOT = 'BP13K240'                              
                   PERFORM 3150-MOVE-FIELDS THRU 3150-EXIT                      
                   REWRITE BP13K249-REC                                         
                   IF WS-K249-STATUS = 00                                       
                      ADD 1 TO WS-K249-REWRITE-CNT                              
                   ELSE                                                         
                      DISPLAY 'ERROR REWRITING BP13K249, KEY: '                 
                              K249-KEY-FLD                                      
                      ADD 1 TO WS-K249-REWRITE-ERR                              
                   END-IF                                                       
                END-IF                                                          
             WHEN 23                                                            
                PERFORM 3150-MOVE-FIELDS THRU 3150-EXIT                         
                WRITE BP13K249-REC                                              
                IF WS-K249-STATUS = 00                                          
                   ADD 1 TO WS-K249-WRITE-CNT                                   
                ELSE                                                            
                   DISPLAY 'ERROR WRITING BP13K249, KEY: '                      
                           K249-KEY-FLD                                         
                   DISPLAY 'STATUS IS ' WS-K249-STATUS                          
                   ADD 1 TO WS-K249-WRITE-ERR                                   
                END-IF                                                          
             WHEN OTHER                                                         
                DISPLAY 'BP13K249 - READ ERROR: ' WS-K249-STATUS                
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3150-MOVE-FIELDS.                                                        
      *-----------------*                                                       
                                                                                
           MOVE K762-NUM-SUPPLY            TO K249-NUM-TOTAL                    
                                              K249-NUM-AVAIL-TOTAL.             
                                                                                
           MOVE 'BP13K762'                 TO K249-FILE-ORIG                    
                                                                                
           MOVE K762-NUM-CHOICE            TO K249-NUM-BTO-ZONE.                
                                                                                
           PERFORM 7500-DECODE-PROJECT   THRU 7500-EXIT.                        
                                                                                
           IF WS-K062-FND = 'Y'                                                 
              MOVE K062-DESC-L1(1:40)      TO K249-NME-PROJECT                  
           ELSE                                                                 
              MOVE SPACES                  TO K249-NME-PROJECT                  
           END-IF.                                                              
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3160-READ-K813.                                                          
      *-----------------*                                                       
           MOVE SPACES               TO K813-KEY-FLD.                           
           MOVE K762-NUM-CHOICE      TO K813-NUM-ZONE.                          
           MOVE K762-NUM-FT          TO K813-NUM-FLAT-TYPE.                     
           MOVE K762-DTE-BALLOT      TO K813-DTE-BALLOT.                        
                                                                                
           READ BP13K813.                                                       
           EVALUATE WS-K813-STATUS                                              
           WHEN 00                                                              
              CONTINUE                                                          
           WHEN 23                                                              
              MOVE SPACES      TO K813-NUM-ADDITIONAL-FLAT                      
                                  K813-CDE-NT                                   
              DISPLAY 'RECORD NOT FOUND BP13K813 : ' K813-KEY-FLD               
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K813 : ' WS-K813-STATUS                
              MOVE WS-K813-STATUS            TO  RETURN-CODE                    
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       3160-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       7000-DECODE-NEW-TOWN.                                                    
      *---------------------*                                                   
                                                                                
           READ BP13K060.                                                       
                                                                                
           IF WS-K060-STATUS EQUAL 00                                           
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'BP13K060 - READ ERROR: ' WS-K060-STATUS                  
              DISPLAY 'K060-KEY-FLD = '    K060-KEY-FLD                         
              MOVE WS-K060-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       7500-DECODE-PROJECT.                                                     
      *--------------------*                                                    
                                                                                
           MOVE SPACES            TO BP13K062-REC.                              
           INITIALIZE                BP13K062-REC.                              
                                                                                
           MOVE 45                TO K062-SERIAL-NO.                            
           MOVE K762-NUM-CHOICE   TO K062-CODE.                                 
                                                                                
           MOVE 'N'               TO WS-K062-FND.                               
                                                                                
           READ BP13K062.                                                       
                                                                                
           IF WS-K062-STATUS EQUAL 00                                           
              MOVE 'Y' TO WS-K062-FND                                           
           ELSE                                                                 
              DISPLAY 'BP13K062 - READ ERROR: ' WS-K062-STATUS                  
              DISPLAY 'K062-KEY-FLD = '    K062-KEY-FLD                         
              MOVE WS-K062-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       7500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       8000-DECODE-FLAT-TYPE.                                                   
      *----------------------*                                                  
                                                                                
           MOVE SPACES TO WS-FLAT-TYPE.                                         
                                                                                
           EVALUATE WS-NUM-FT                                                   
              WHEN '1A'                                                         
              WHEN '2A'                                                         
                 MOVE 'SA       ' TO WS-FLAT-TYPE                               
              WHEN '2 '                                                         
                 MOVE '2-ROOM   ' TO WS-FLAT-TYPE                               
              WHEN '3 '                                                         
                 MOVE '3-ROOM   ' TO WS-FLAT-TYPE                               
              WHEN '4 '                                                         
                 MOVE '4-ROOM   ' TO WS-FLAT-TYPE                               
              WHEN '5 '                                                         
                 MOVE '5-ROOM   ' TO WS-FLAT-TYPE                               
              WHEN '6 '                                                         
                 MOVE 'EXECUTIVE' TO WS-FLAT-TYPE                               
              WHEN OTHER                                                        
                 DISPLAY 'FLAT TYPE NOT FOUND: ' WS-NUM-FT                      
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C21F  *************'.                           
           DISPLAY 'RECS READ    FROM BP13K762 : ' WS-K762-READ-CNT.            
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN IN BP13K249   : ' WS-K249-WRITE-CNT.           
           DISPLAY 'WRITE ERRORS IN BP13K249   : ' WS-K249-WRITE-ERR.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS REWRITTEN IN BP13K249 : ' WS-K249-REWRITE-CNT.         
           DISPLAY 'REWRITE ERRORS IN BP13K249 : ' WS-K249-REWRITE-ERR.         
                                                                                
           CLOSE BP13K762                                                       
                 BP13K060                                                       
                 BP13K062                                                       
                 BP13K813                                                       
                 BP13K249.                                                      
                                                                                
           IF WS-K762-STATUS NOT EQUAL 00                                       
              DISPLAY 'BP13K762 - CLOSE ERROR: ' WS-K762-STATUS                 
              MOVE WS-K762-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT EQUAL 00                                       
              DISPLAY 'BP13K060 - CLOSE ERROR: ' WS-K060-STATUS                 
              MOVE WS-K060-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K062-STATUS NOT EQUAL 00                                       
              DISPLAY 'BP13K062 - CLOSE ERROR: ' WS-K062-STATUS                 
              MOVE WS-K062-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K249-STATUS NOT EQUAL 00                                       
              DISPLAY 'BP13K249 - CLOSE ERROR: ' WS-K249-STATUS                 
              MOVE WS-K249-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT EQUAL 00                                       
              DISPLAY 'BP13K813 - CLOSE ERROR: ' WS-K813-STATUS                 
              MOVE WS-K813-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
