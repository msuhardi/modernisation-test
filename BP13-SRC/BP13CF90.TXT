       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF90.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  13/06/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    : UPDATE PPS-SUCC AND AST-SUCC                *            
      *                                                            *            
      * INPUT FILE   :  BP13F740  -  SEQUENTIAL DATASET            *            
      * I-O FILE     :  BP13K740  -  BP13.K740.MATUREBA            *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP134914  SMR2  13062013  NEW PROGRAM                      *            
      * BP134914  SMR2  24062013  MOVE 'Y' TO PPS-SUCC/AST-SUCC    *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740  ASSIGN       TO BP13F740.                           
                                                                                
           SELECT BP13K740  ASSIGN       TO BP13K740                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K740-KEY-FLD                        
                            FILE STATUS  IS WS-K740-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F740                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K740                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K740.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K740-STATUS           PIC 9(02)  VALUE ZEROES.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ             PIC 9(06)  VALUE ZEROES.                
           05  WS-K740-MATCH            PIC 9(06)  VALUE ZEROES.                
           05  WS-K740-UNMATCH          PIC 9(06)  VALUE ZEROES.                
           05  WS-K740-REWRITE          PIC 9(06)  VALUE ZEROES.                
           05  WS-K740-UNSUCC           PIC 9(06)  VALUE ZEROES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF              PIC X(01)  VALUE 'N'.                   
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE                  PIC X(08)  VALUE SPACES.                
           05  WS-DATE-EDIT             PIC X(10)  VALUE SPACES.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT                      
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT BP13F740                                                  
                I-O   BP13K740.                                                 
                                                                                
           IF WS-K740-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K740 ERROR ' WS-K740-STATUS                  
              MOVE WS-K740-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-DATE.                         
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO WS-DATE-EDIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F740.                                                      
      *-------------------------------------------------------------            
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F740-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F740-READ.                    
           PERFORM 3000-READ-BP13K740       THRU 3000-EXIT.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BP13K740.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                      TO BP13K740-REC.                    
           INITIALIZE                          BP13K740-REC.                    
                                                                                
           MOVE F740-KEY-FLD                TO K740-KEY-FLD.                    
                                                                                
           READ BP13K740.                                                       
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-K740-MATCH                    
              IF K740-CDE-SUCC-NT-ZONE NOT = SPACES AND LOW-VALUES              
                 PERFORM 4000-REWRITE-BP13K740 THRU 4000-EXIT                   
              ELSE                                                              
                 ADD 1                      TO WS-K740-UNSUCC                   
              END-IF                                                            
                                                                                
           WHEN 23                                                              
              ADD 1                         TO WS-K740-UNMATCH                  
              DISPLAY 'RANDOM NO. NOT FOUND IN K740 : ' F740-KEY-FLD            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K740   : ' WS-K740-STATUS              
              MOVE WS-K740-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-REWRITE-BP13K740.                                                   
      *-------------------------------------------------------------            
           IF K740-NUM-PPS-ELIG-TAG = 'Y' OR 'E'                                
              MOVE 'Y'                     TO K740-NUM-PPS-SUCC                 
           END-IF.                                                              
           IF K740-NUM-AST = 'Y'                                                
              MOVE 'Y'                     TO K740-NUM-AST-SUCC                 
           END-IF.                                                              
                                                                                
           REWRITE BP13K740-REC.                                                
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                        TO WS-K740-REWRITE                   
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR WRITING TO BP13K740. STATUS IS - '                 
                      WS-K740-STATUS                                            
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY '    BP13CF90 CONTROL TOTAL   : ' WS-DATE-EDIT.              
           DISPLAY '  '.                                                        
           DISPLAY ' 1. NO OF BP13F740 READ      : ' WS-F740-READ.              
           DISPLAY ' 2. NO OF MATCHED RECORDS    : ' WS-K740-MATCH.             
           DISPLAY ' 3. NO OF UNMATCHED RECORDS  : ' WS-K740-UNMATCH.           
           DISPLAY ' 4. NO OF BP13K740 REWRITTEN : ' WS-K740-REWRITE.           
           DISPLAY ' 5. NO OF BP13K740 UNSUCC    : ' WS-K740-UNSUCC.            
           DISPLAY '  '.                                                        
           DISPLAY ' CHECK '.                                                   
           DISPLAY '       1.  =   2.  +  3. '.                                 
           DISPLAY '       2.  =   4.  +  5. '.                                 
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F740                                                       
                 BP13K740.                                                      
                                                                                
           IF WS-K740-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K740 ERROR ' WS-K740-STATUS                  
              MOVE WS-K740-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
