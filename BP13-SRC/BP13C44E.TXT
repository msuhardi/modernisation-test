       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C44E.                                                 
       AUTHOR.        ELGER DE GUZMAN                                           
      *DATE-WRITTEN.  15/11/2013.                                               
      * ===============================================================*        
      * SYSTEM NAME (SYSTEM ID)                                        *        
      * ===============================================================*        
      * OBJECTIVE   : CREATE MAIL FILE FOR LOAN TAG CHANGE             *        
      * BP135147  ESD1  201113  NEW PROGRAM                            *        
      * BP135537  RJB1  030315  CHANGE THE EMAIL RECIPIENT FROM        *        
      *                         NICKNAME INTO COMPLETE NAME            *        
      * BP136082  PCL4  080116  TO UPDATE EMAIL RECIPIENT              *        
      * BP136477  RJB1  181016  TO REPLACE KIM HUAT TO YEE MIEN        *        
      * BP136560  KVL1  281216  TO REMOVE SANGEETHA IN THE RECIPIENTS  *        
      * BP136632  KSJ3  230317  TO EXPAND BP13K600 (1000 TO 1800)      *        
      * BP137230  PCL4  050318  TO UPDATE EMAIL RECIPIENTS             *        
      * BP137668 20190823 ZDD   To remove Watt Kee TEO (HDB) and       *        
      *                         replace  with Ivy M BALANAG(HDB)       *        
      * BP138202 20200310 KV5   REPLACE MAIL FROM MOHAMED ALI BEE      *        
      *                         KATHIJAH TO HENG SC SUZANNE(HDB)       *        
      * BP139030 20220107 ZAR7  REPLACE CHUA YEE MIEN TO LIM KOK CHUN           
      * BP139175 20220907 KAC1  REMOVED Ivy_M_BALANAG@hdb.gov.sg       *        
      *                         ADDED HE_Jialing@hdb.gov.sg,           *        
      *                          Kathijah_Bee_ALI_MOHAMED@hdb.gov.sg & *        
      *                          Nor'ain_MOHAMAD_JOHAR@hdb.gov.sg.     *        
      * BP139276 20240405 KAC1  REPLACE LIM KOK CHUN BY                *        
      *                                 KATHIJAH_BEE_ALI_MOHAMED       *        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K600  ASSIGN        TO BP13K600                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K600-KEY-FLD                       
                            FILE STATUS   IS K600-STATUS.                       
                                                                                
           SELECT BP13F600  ASSIGN        TO BP13F600.                          
                                                                                
           SELECT MAILF600  ASSIGN        TO MAILF600.                          
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13K600                                                            
            RECORD CONTAINS 1800 CHARACTERS.                                    
       COPY BP13K600.                                                           
                                                                                
       FD  BP13F600                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 1800 CHARACTERS.                                     
       COPY BP13F600.                                                           
                                                                                
       FD  MAILF600                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  MAILF600-REC      PIC X(132).                                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  K600-STATUS                      PIC 99    VALUE 00.             
                                                                                
       01  WS-SAVE-SCH                  PIC X(11)  VALUE SPACES.                
       01  WS-SQL-CODE                  PIC 9(4)   VALUE ZEROS.                 
       01  WS-F600-EOF                  PIC X      VALUE 'N'.                   
       01  WS-DBSS-SW                   PIC X      VALUE 'N'.                   
       01  WS-F600RPT-COUNT             PIC 9(7)   VALUE ZERO.                  
       01  WS-UPDT-K600-CNT             PIC 9(7)   VALUE ZERO.                  
       01  WS-CTR                       PIC 9(2)   VALUE ZERO.                  
       01  WS-FORMAT-NO                 PIC ZZZ,ZZZ,ZZ9.99 VALUE ZERO.          
       01  WS-FIRST-REPORT              PIC X      VALUE 'Y'.                   
       01  WS-ADDRESS-FULL.                                                     
           05  WS-NUM-BLK-FLAT                  PIC X(05).                      
           05  WS-NME-STREET-FLAT               PIC X(32).                      
           05  WS-NUM-LEVEL-FLAT                PIC X(02).                      
           05  WS-NUM-MAIN-FLAT                 PIC X(04).                      
           05  WS-NUM-SUB-FLAT                  PIC X(03).                      
           05  WS-NUM-POSTAL-FLAT               PIC X(06).                      
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY                  PIC 9(04).                          
           05  WS-CUR-MM                    PIC 9(02).                          
           05  WS-CUR-DD                    PIC 9(02).                          
                                                                                
       01  WS-REPORT-DTL.                                                       
           05  WS-CONSTANT-TXT.                                                 
               10 WS-TEXT-1.                                                    
                  15 FILLER                 PIC X(80)                           
                  VALUE 'There has been an adjustment of CPF withdrawal         
      -           'details for the following:    '.                             
                                                                                
                                                                                
                                                                                
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(60)                               
               VALUE 'RCPT TO:<SERENA_JM_TEH@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT4             PIC X(60)                               
               VALUE 'RCPT TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT5             PIC X(60)                               
               VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.           
           05  WS-MAIL-RPT6             PIC X(60)                               
               VALUE 'RCPT TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                    
      *        VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.           
           05  WS-MAIL-RPT8             PIC X(60)                               
               VALUE 'RCPT TO:<CHONG_YENG_MUI@HDB.GOV.SG>'.                     
           05  WS-MAIL-RPT9A            PIC X(60)                               
               VALUE 'RCPT TO:<HE_Jialing@hdb.gov.sg>'.                         
           05  WS-MAIL-RPT9B            PIC X(60)                               
               VALUE 'RCPT TO:<Kathijah_Bee_ALI_MOHAMED@hdb.gov.sg>'.           
           05  WS-MAIL-RPT9C            PIC X(60)                               
               VALUE "RCPT TO:<Nor'ain_MOHAMAD_JOHAR@hdb.gov.sg>".              
           05  WS-MAIL-RPT10            PIC X(26)                               
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT11            PIC X(29)                               
               VALUE 'FROM:SOC System - Email alert'.                           
           05  WS-MAIL-RPT12            PIC X(60)                               
               VALUE 'TO:<SERENA_JM_TEH@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT13            PIC X(60)                               
               VALUE 'TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT14            PIC X(60)                               
               VALUE 'CC:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                
           05  WS-MAIL-RPT15            PIC X(60)                               
               VALUE 'CC:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                         
      *        VALUE 'CC:<KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.                
           05  WS-MAIL-RPT17            PIC X(60)                               
               VALUE 'CC:<CHONG_YENG_MUI@HDB.GOV.SG>'.                          
           05  WS-MAIL-RPT18A           PIC X(60)                               
               VALUE 'CC:<HE_Jialing@hdb.gov.sg>'.                              
           05  WS-MAIL-RPT18B           PIC X(60)                               
               VALUE 'CC:<Kathijah_Bee_ALI_MOHAMED@hdb.gov.sg>'.                
           05  WS-MAIL-RPT18C           PIC X(60)                               
               VALUE "CC:<Nor'ain_MOHAMAD_JOHAR@hdb.gov.sg>".                   
           05  WS-MAIL-RPT19            PIC X(34)                               
               VALUE  'SUBJECT : CPF WITHDRAWAL AMENDMENT'.                     
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-PROCESS-INDATA  THRU 2000-EXIT.                         
           PERFORM 3000-GET-ALL-TRANS   THRU 3000-EXIT                          
           UNTIL WS-F600-EOF = 'Y'.                                             
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  BP13F600                                                 
                OUTPUT MAILF600                                                 
                I-O    BP13K600                                                 
                                                                                
           INITIALIZE  BP13F600-REC                                             
                       MAILF600-REC                                             
                       BP13K600-REC.                                            
                                                                                
           IF K600-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR, K600-STATUS = ' K600-STATUS               
              MOVE     K600-STATUS TO RETURN-CODE                               
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-PROCESS-INDATA.                                                     
      *=============================================================*           
                                                                                
            READ BP13F600                                                       
                 AT END MOVE 'Y'  TO WS-F600-EOF                                
            END-READ.                                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-GET-ALL-TRANS.                                                      
      *=============================================================*           
                                                                                
            ADD   1   TO   WS-F600RPT-COUNT.                                    
                                                                                
            PERFORM 4500-CREATE-REPORT   THRU  4500-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       4500-CREATE-REPORT.                                                      
      *================================================================*        
                                                                                
            WRITE MAILF600-REC FROM WS-MAIL-RPT1                                
            WRITE MAILF600-REC FROM WS-MAIL-RPT2                                
            WRITE MAILF600-REC FROM WS-MAIL-RPT3                                
            WRITE MAILF600-REC FROM WS-MAIL-RPT4                                
            WRITE MAILF600-REC FROM WS-MAIL-RPT5                                
            WRITE MAILF600-REC FROM WS-MAIL-RPT6                                
            WRITE MAILF600-REC FROM WS-MAIL-RPT8                                
            WRITE MAILF600-REC FROM WS-MAIL-RPT9A                               
            WRITE MAILF600-REC FROM WS-MAIL-RPT9B                               
            WRITE MAILF600-REC FROM WS-MAIL-RPT9C                               
                                                                                
            WRITE MAILF600-REC FROM WS-MAIL-RPT10                               
            WRITE MAILF600-REC FROM WS-MAIL-RPT11                               
            WRITE MAILF600-REC FROM WS-MAIL-RPT12                               
            WRITE MAILF600-REC FROM WS-MAIL-RPT13                               
            WRITE MAILF600-REC FROM WS-MAIL-RPT14                               
            WRITE MAILF600-REC FROM WS-MAIL-RPT15                               
            WRITE MAILF600-REC FROM WS-MAIL-RPT17                               
            WRITE MAILF600-REC FROM WS-MAIL-RPT18A                              
            WRITE MAILF600-REC FROM WS-MAIL-RPT18B                              
            WRITE MAILF600-REC FROM WS-MAIL-RPT18C                              
            WRITE MAILF600-REC FROM WS-MAIL-RPT19                               
                                                                                
            STRING WS-CUR-DD '/' WS-CUR-MM '/' WS-CUR-CCYY                      
                   DELIMITED BY SIZE INTO WS-DATE-RPT                           
            WRITE MAILF600-REC FROM WS-DATE-RPT-DTL                             
            MOVE SPACES TO  MAILF600-REC                                        
            WRITE   MAILF600-REC                                                
            WRITE   MAILF600-REC   FROM  WS-TEXT-1                              
            MOVE SPACES TO  MAILF600-REC                                        
            WRITE   MAILF600-REC                                                
                                                                                
            PERFORM UNTIL WS-F600-EOF = 'Y'                                     
               MOVE SPACES TO  MAILF600-REC                                     
               IF F600-AMT-TOT-WDL NOT NUMERIC                                  
                  MOVE ZEROES TO F600-AMT-TOT-WDL                               
               END-IF                                                           
               STRING 'REGISTRATION NO:       ' F600-NUM-ORIG-REGN              
                       DELIMITED BY SIZE INTO MAILF600-REC                      
               WRITE   MAILF600-REC                                             
                                                                                
               MOVE SPACES TO  MAILF600-REC                                     
               STRING 'SS NO          :      ' F600-NUM-SNO                     
                       DELIMITED BY SIZE INTO MAILF600-REC                      
               WRITE   MAILF600-REC                                             
                                                                                
               MOVE SPACES TO  MAILF600-REC                                     
               STRING 'CPF ACCOUNT NO :      ' F600-NUM-CPF                     
                       DELIMITED BY SIZE INTO MAILF600-REC                      
               WRITE   MAILF600-REC                                             
                                                                                
               MOVE SPACES TO  MAILF600-REC                                     
               MOVE    F600-AMT-TOT-WDL  TO WS-FORMAT-NO                        
               STRING 'UPDATED AMOUNT : ' WS-FORMAT-NO                          
                       DELIMITED BY SIZE INTO MAILF600-REC                      
               WRITE   MAILF600-REC                                             
                                                                                
               MOVE SPACES TO  MAILF600-REC                                     
               WRITE   MAILF600-REC                                             
               MOVE SPACES TO  MAILF600-REC                                     
               WRITE   MAILF600-REC                                             
                                                                                
               PERFORM 5000-READ-BP13K600  THRU 5000-EXIT                       
               PERFORM 2000-PROCESS-INDATA THRU 2000-EXIT                       
            END-PERFORM.                                                        
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       5000-READ-BP13K600.                                                      
      ******************************************************************        
           MOVE SPACES TO BP13K600-REC.                                         
           INITIALIZE  BP13K600-REC.                                            
           MOVE F600-KEY-FLD TO K600-KEY-FLD                                    
                                                                                
           READ BP13K600.                                                       
                                                                                
           IF K600-STATUS = 00                                                  
              PERFORM 5100-UPDATE-BP13K600 THRU 5100-EXIT                       
           ELSE                                                                 
           IF K600-STATUS = 23                                                  
              DISPLAY 'RECORD NOT FOUND IN K600, STAT = ' K600-STATUS           
              DISPLAY '           K600-KEY-FIELD      = ' F600-KEY-FLD          
              MOVE  12            TO RETURN-CODE                                
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K600, STATUS = ' K600-STATUS           
              DISPLAY '           K600-KEY-FIELD      = ' F600-KEY-FLD          
              MOVE     K600-STATUS      TO     RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ****************************************************************          
       5100-UPDATE-BP13K600.                                                    
      *-------------------------------------------------------------            
           MOVE   'P'  TO   K600-FORCE-TAG.                                     
                                                                                
           REWRITE BP13K600-REC.                                                
                                                                                
           IF K600-STATUS =  00                                                 
              ADD     1   TO WS-UPDT-K600-CNT                                   
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING BP13K600, STAT = ' K600-STATUS            
              DISPLAY '          K600-KEY-FIELD      = ' F600-KEY-FLD           
              MOVE     K600-STATUS      TO     RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
           DISPLAY 'TOTAL EMAIL CREATED: '                                      
                   WS-F600RPT-COUNT.                                            
                                                                                
           CLOSE BP13K600                                                       
                 BP13F600                                                       
                 MAILF600.                                                      
                                                                                
           IF K600-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13K600, STATUS = ' K600-STATUS           
              MOVE     K600-STATUS    TO    RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
