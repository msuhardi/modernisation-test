       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C619.                                                 
       AUTHOR.        NOR.                                                      
      *DATE-WRITTEN.  16/10/1997.                                               
                                                                                
      *****************************************************************         
      * SYSTEM OF COMMITMENT (SOC)                                    *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVES : 1. PROGRAM TO MATCH NRIC OF BP13F558 WITH NRIC  *         
      *                  OF TRANSFER HISTORY FOR 10-YR TIME BAR CHECK *         
      *                                                               *         
      *               2. THE POLICY ONLY CHECK CASES TRANSFER AFTER   *         
      *                  OR ON 1997/09/01.                            *         
      *                                                               *         
      *               3. IF THE SCH-ACC-NO IN TRANSFER IS SAME AS     *         
      *                  SMS-SCH-ACC-NO, THEN DO NOT OUTPUT.          *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                ACCESS      REC                                *         
      * DD NAME  I/O/E  MODE       LEN  KEY          REMARKS          *         
      * -------  ----- -------     ---  ---          --------         *         
      * BP13F558 I      SEQUENTIAL                                    *         
      * BB13K510 I      DYNAMIC                                       *         
      * BB13K500 I      RANDOM                                        *         
      * P13F558O O      SEQUENTIAL                                    *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      * CHGE REQ  DATE      BY     REMARKS                            *         
      * --------- ----      ---    -------                            *         
      * BP130445  03101997  NOR - NEW PGM                             *         
      * BP130450  12061998  LSB - CHECK OWNERSHIP' ONLY TAKE IN 'L'   *         
      *                           BYPASS ADMINISTRATOR/TRUSTEE        *         
      * BP130542  12081998  LSB - CHK DTE-APPLN INSTEAD OF DTE-TRANFR *         
      *                           DIVORCE, 10/7-30/9/97- UPG, NO 10-YR*         
      *                           DIVORCE, AF 1/9/97 - UPG, CHK 10-YR *         
      * BP130810  02032000  SRA - TO CHGE DTE-TRANSFER TO DTE-APPROVE.*         
      * BP130897  03082000  AKT - EXPAND F558 RECL FROM 400 TO 600    *         
      * BP131015  27102001  ANC1- REMOVE CHECKING OF DIVORCE CODE TO  *         
      *                           GET TRANSFER RECORD.                *         
      * BP132210  27092002  ANC1- ADD CHECKING OF BUYER CASE. DROP    *         
      *                           CASES WITH SAME SCH ACCT NUMBER FOR *         
      *                           BUYER AND SELLER RECORD.            *         
      * BP135921  24072015  FNP1- UPDATE F558 WITH OTHER TRF FLAT     *         
      * BP139518  24022023  EAA2  TO INCLUDE 6 SALES TYPE CODE IN     *         
      *                           HOUSEHOLD DETERMINATION             *         
      *                           (8L/8M/8N/8R/8S/8T)                 *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BB13K500 ASSIGN TO BB13K500                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE IS DYNAMIC                               
                           RECORD KEY IS K500-KEY-FLD                           
                           FILE STATUS IS BB13K500-STATUS.                      
                                                                                
           SELECT BB13K510 ASSIGN TO BB13K510                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE IS DYNAMIC                               
                           RECORD KEY IS K510-KEY-FLD                           
                           FILE STATUS IS BB13K510-STATUS.                      
                                                                                
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
                                                                                
           SELECT P13F558O ASSIGN TO P13F558O.                                  
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BB13K500     RECORD CONTAINS 1000 CHARACTERS.                        
       COPY BB13K500.                                                           
                                                                                
       FD  BB13K510     RECORD CONTAINS 100 CHARACTERS.                         
       COPY BB13K510.                                                           
                                                                                
       FD  BP13F558     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 600 CHARACTERS                          
                        LABEL RECORD IS STANDARD                                
                        RECORDING MODE IS F.                                    
       COPY BP13F558.                                                           
                                                                                
       FD  P13F558O     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 600 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01  P13F558O-REC.                                                        
           05  FILLER           PIC X(600).                                     
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-FLAGS.                                                       
           05  BB13K500-STATUS     PIC 9(2)      VALUE ZEROS.                   
           05  BB13K510-STATUS     PIC 9(2)      VALUE ZEROS.                   
           05  WS-F558-EOF         PIC X         VALUE 'N'.                     
           05  WS-K510-EOF         PIC X         VALUE 'N'.                     
           05  WS-BUYER-FND        PIC X.                                       
               88  BUYER-FND                     VALUE 'Y'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F558-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-K510-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-F558O-CNT-WRT    PIC 9(8)      VALUE ZEROS.                   
           05  WS-TRF-FD           PIC 9(8)      VALUE ZEROS.                   
           05  WS-TRF-VALID        PIC 9(8)      VALUE ZEROS.                   
           05  WS-TRF-LESSEE       PIC X(1)      VALUE SPACES.                  
                                                                                
       01  WS-TRANSFER-INFO.                                                    
           05  WS-F558O-NUM-TRANSFEROR     PIC X(2).                            
           05  WS-F558O-NUM-RSHIP-TR       PIC X(2).                            
           05  WS-F558O-NUM-MSTAT-TR       PIC X(2).                            
           05  WS-F558O-NUM-HDB-REF-TR     PIC X(11).                           
           05  WS-F558O-NUM-SALES-TYPE-TR  PIC X(2).                            
           05  WS-F558O-NUM-ALLO-SCH-TR    PIC X(3).                            
           05  WS-F558O-NUM-ELIG-SCH-TR    PIC X(3).                            
           05  WS-F558O-DTE-EFF-SALE-TR    PIC X(8).                            
           05  WS-F558O-DTE-TRANSFER-TR    PIC X(8).                            
           05  WS-F558O-DTE-APPLN-TR       PIC X(8).                            
           05  WS-F558O-NUM-DIVORCE-TR     PIC X(1).                            
           05  WS-F558O-MULTIPLE-FLAT-TRF  PIC X(11).                           
           05  WS-F558O-MULTIPLE-STY-TRF   PIC X(02).                           
           05  WS-F558O-MULTIPLE-DIVORCE-TRF PIC X(01).                         
                                                                                
       01  WS-DTE-CC               PIC X(2).                                    
       01  WS-DTE-CURRENT.                                                      
           05  WS-DTE-DD           PIC X(2).                                    
           05  WS-DTE-MM           PIC X(2).                                    
           05  WS-DTE-YY           PIC X(2).                                    
                                                                                
       01  WS-TME-SYSTEM           PIC X(8).                                    
       01  WS-K500-DTE-APPROVE     PIC X(8)      VALUE SPACES.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 1010-READ-BP13F558-REC   THRU 1010-EXIT.                     
           PERFORM 2000-MAIN-ROUTINE        THRU 2000-EXIT                      
             UNTIL WS-F558-EOF = 'Y' OR                                         
                   F558-NUM-NRIC = HIGH-VALUE.                                  
                                                                                
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BB13K500                                                 
                       BB13K510                                                 
                       BP13F558                                                 
                OUTPUT P13F558O.                                                
                                                                                
                                                                                
           IF BB13K500-STATUS NOT = ZEROES AND 97                               
              MOVE BB13K500-STATUS TO RETURN-CODE                               
              DISPLAY 'OPEN BB13K500 FAIL STATUS=' BB13K500-STATUS              
              GO TO 9000-CLOSE-FILES.                                           
                                                                                
           IF BB13K510-STATUS NOT = ZEROES AND 97                               
              MOVE BB13K510-STATUS TO RETURN-CODE                               
              DISPLAY 'OPEN BB13K510 FAIL STATUS=' BB13K510-STATUS              
              GO TO 9000-CLOSE-FILES.                                           
                                                                                
           ACCEPT WS-DTE-CURRENT FROM DATE.                                     
           ACCEPT WS-TME-SYSTEM  FROM TIME.                                     
                                                                                
           IF WS-DTE-YY < '90'                                                  
              MOVE '20'      TO WS-DTE-CC                                       
           ELSE                                                                 
              MOVE '19'      TO WS-DTE-CC.                                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1010-READ-BP13F558-REC.                                                  
      *****************************************************************         
                                                                                
           READ BP13F558 AT END MOVE 'Y'        TO WS-F558-EOF                  
                                MOVE HIGH-VALUE TO F558-NUM-NRIC                
                GO TO 1010-EXIT.                                                
           ADD 1 TO WS-F558-CNT-READ.                                           
       1010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1020-READ-BB13K510-REC.                                                  
      *****************************************************************         
                                                                                
           READ BB13K510 NEXT AT END MOVE HIGH-VALUE TO K510-NUM-UIN.           
                                                                                
       1020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1030-READ-TRANSFEREE.                                                    
      *****************************************************************         
           MOVE SPACES                  TO WS-BUYER-FND  K500-KEY-FLD.          
           MOVE K510-NUM-HDB-REF        TO K500-KEY-FLD.                        
                                                                                
           START BB13K500 KEY >= K500-KEY-FLD.                                  
                                                                                
           IF BB13K500-STATUS = ZEROES                                          
              PERFORM 1040-READ-BB13K500-REC  THRU 1040-EXIT                    
              PERFORM 1050-PROCESS-BUYER      THRU 1050-EXIT                    
                UNTIL BUYER-FND OR                                              
                      K500-KEY-FLD(1:9) NOT =                                   
                      K510-NUM-HDB-REF(1:9) OR                                  
                      K500-KEY-FLD = HIGH-VALUES                                
           ELSE                                                                 
              IF BB13K500-STATUS = 23                                           
                 DISPLAY 'RECORD NOT FOUND IN BB13K500 ' K500-KEY-FLD           
              END-IF                                                            
           END-IF.                                                              
       1030-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1040-READ-BB13K500-REC.                                                  
      *****************************************************************         
                                                                                
           READ BB13K500 NEXT AT END MOVE HIGH-VALUE TO K500-KEY-FLD.           
                                                                                
       1040-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1050-PROCESS-BUYER.                                                      
      *****************************************************************         
           IF K500-CDE-REC = 'B'                                                
              IF (K510-NUM-HDB-REF(1:9) = K500-NUM-HDB-REF(1:9)) AND            
                 (K510-NUM-HDB-REF(10:1) < K500-NUM-HDB-REF(10:1))              
                 EVALUATE F558-NUM-NRIC                                         
                    WHEN K500-NRIC-TE1                                          
                       MOVE 'Y' TO WS-BUYER-FND                                 
                    WHEN K500-NRIC-TE2                                          
                       MOVE 'Y' TO WS-BUYER-FND                                 
                    WHEN K500-NRIC-TE3                                          
                       MOVE 'Y' TO WS-BUYER-FND                                 
                    WHEN K500-NRIC-TE4                                          
                       MOVE 'Y' TO WS-BUYER-FND                                 
                 END-EVALUATE                                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 1040-READ-BB13K500-REC        THRU 1040-EXIT.                
       1050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-MAIN-ROUTINE.                                                       
      *****************************************************************         
                                                                                
      *----------------------------------------------------*                    
      * COMMENTS :  DO THE MATCHING.                       *                    
      *----------------------------------------------------*                    
                                                                                
           MOVE SPACES                  TO WS-K500-DTE-APPROVE                  
                                           WS-TRANSFER-INFO.                    
                                                                                
           IF F558-NUM-NRIC = SPACES OR LOW-VALUES                              
              PERFORM 2020-WRITE-F558-REC           THRU 2020-EXIT              
              PERFORM 1010-READ-BP13F558-REC        THRU 1010-EXIT              
              GO TO 2000-EXIT.                                                  
                                                                                
           MOVE SPACES                  TO K510-KEY-FLD.                        
           MOVE F558-NUM-NRIC           TO K510-NUM-UIN.                        
           START BB13K510 KEY >= K510-KEY-FLD.                                  
           IF BB13K510-STATUS = ZEROES                                          
              PERFORM 1020-READ-BB13K510-REC  THRU 1020-EXIT                    
              PERFORM 2005-CHECK-STATUS-REC   THRU 2005-EXIT                    
                UNTIL F558-NUM-NRIC NOT = K510-NUM-UIN OR                       
                      K510-NUM-UIN = HIGH-VALUE                                 
           ELSE                                                                 
           IF BB13K510-STATUS = 23                                              
              DISPLAY 'K510-NUM-UIN : ' K510-NUM-UIN ' NOT FOUND IN '           
                      'BB13K510!'                                               
           END-IF.                                                              
                                                                                
           PERFORM 2020-WRITE-F558-REC        THRU 2020-EXIT.                   
                                                                                
           PERFORM 1010-READ-BP13F558-REC     THRU 1010-EXIT.                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2005-CHECK-STATUS-REC.                                                   
      *****************************************************************         
                                                                                
           IF K510-CDE-REC = 'S'                                                
                                                                                
              IF F558-NUM-HDB-REF-SMS (1:9) = K510-NUM-HDB-REF (1:9)            
                 CONTINUE                                                       
              ELSE                                                              
                 PERFORM 1030-READ-TRANSFEREE THRU 1030-EXIT                    
                 IF BUYER-FND                                                   
                    CONTINUE                                                    
                 ELSE                                                           
                    PERFORM 2010-EVALUATE-DATA   THRU 2010-EXIT.                
                                                                                
           PERFORM 1020-READ-BB13K510-REC        THRU 1020-EXIT.                
                                                                                
       2005-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2010-EVALUATE-DATA.                                                      
      *****************************************************************         
                                                                                
              MOVE SPACES                    TO WS-TRF-LESSEE.                  
              MOVE SPACES                    TO K500-KEY-FLD.                   
              MOVE K510-NUM-HDB-REF          TO K500-NUM-HDB-REF.               
              MOVE K510-CDE-REC              TO K500-CDE-REC.                   
                                                                                
              READ BB13K500.                                                    
                                                                                
              IF BB13K500-STATUS = ZEROES                                       
                 IF K500-CDE-REC = 'S' AND                                      
                    K510-NUM-HDB-REF = K500-NUM-HDB-REF                         
                                                                                
                    IF (K500-CDE-SALES-TYPE = '7 ' OR '7C' OR '7J' OR           
                                              '8A' OR '8B' OR '8E' OR           
                                              '8L' OR '8M' OR '8N' OR           
                                              '8R' OR '8S' OR '8T') OR          
                       (K500-CDE-ALLOC-TR = 'OM ' OR 'PPO' OR 'AOM' OR          
                                            'PPS' OR 'ROM')                     
                       CONTINUE                                                 
                    ELSE                                                        
                       PERFORM 2015-FILLIN-TRANS-INFO  THRU 2015-EXIT           
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
              IF BB13K500-STATUS = 23                                           
                 DISPLAY 'K510-NUM-HDB-REF ' K510-NUM-HDB-REF                   
                         ' NOT FOUND IN BB13K500'                               
              END-IF.                                                           
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2015-FILLIN-TRANS-INFO.                                                  
      *****************************************************************         
                                                                                
           IF K500-DTE-APPROVE > WS-K500-DTE-APPROVE                            
                                                                                
              MOVE K500-DTE-APPROVE        TO WS-K500-DTE-APPROVE               
              MOVE SPACES                  TO WS-TRANSFER-INFO                  
              ADD 1 TO WS-TRF-FD                                                
              EVALUATE K510-NUM-UIN                                             
              WHEN K500-NRIC-TR1                                                
                                                                                
                IF K500-CDE-OWNSHIP1 = 'E' OR 'A' OR 'P'                        
                   CONTINUE                                                     
                ELSE                                                            
                   MOVE 'Y'                TO WS-TRF-LESSEE                     
                   MOVE 'L1'               TO WS-F558O-NUM-TRANSFEROR           
                   MOVE K500-CDE-RSHIP-TR1 TO WS-F558O-NUM-RSHIP-TR             
                   MOVE K500-CDE-MSTAT-TR1 TO WS-F558O-NUM-MSTAT-TR             
                END-IF                                                          
                                                                                
              WHEN K500-NRIC-TR2                                                
                IF K500-CDE-OWNSHIP2 = 'E' OR 'A' OR 'P'                        
                   CONTINUE                                                     
                ELSE                                                            
                   MOVE 'Y'                TO WS-TRF-LESSEE                     
                   MOVE 'L2'               TO WS-F558O-NUM-TRANSFEROR           
                   MOVE K500-CDE-RSHIP-TR2 TO WS-F558O-NUM-RSHIP-TR             
                   MOVE K500-CDE-MSTAT-TR2 TO WS-F558O-NUM-MSTAT-TR             
                END-IF                                                          
                                                                                
              WHEN K500-NRIC-TR3                                                
                IF K500-CDE-OWNSHIP3 = 'E' OR 'A' OR 'P'                        
                   CONTINUE                                                     
                ELSE                                                            
                   MOVE 'Y'                TO WS-TRF-LESSEE                     
                   MOVE 'L3'               TO WS-F558O-NUM-TRANSFEROR           
                   MOVE K500-CDE-RSHIP-TR3 TO WS-F558O-NUM-RSHIP-TR             
                   MOVE K500-CDE-MSTAT-TR3 TO WS-F558O-NUM-MSTAT-TR             
                END-IF                                                          
                                                                                
              WHEN K500-NRIC-TR4                                                
                IF K500-CDE-OWNSHIP4 = 'E' OR 'A' OR 'P'                        
                   CONTINUE                                                     
                ELSE                                                            
                   MOVE 'Y'                TO WS-TRF-LESSEE                     
                   MOVE 'L4'               TO WS-F558O-NUM-TRANSFEROR           
                   MOVE K500-CDE-RSHIP-TR4 TO WS-F558O-NUM-RSHIP-TR             
                   MOVE K500-CDE-MSTAT-TR4 TO WS-F558O-NUM-MSTAT-TR             
                END-IF                                                          
              END-EVALUATE                                                      
                                                                                
              IF WS-TRF-LESSEE = 'Y'                                            
                MOVE K500-NUM-HDB-REF      TO WS-F558O-NUM-HDB-REF-TR           
                MOVE K500-CDE-SALES-TYPE   TO WS-F558O-NUM-SALES-TYPE-TR        
                MOVE K500-CDE-ALLOC-TR     TO WS-F558O-NUM-ALLO-SCH-TR          
                MOVE K500-CDE-ELIG-TR      TO WS-F558O-NUM-ELIG-SCH-TR          
                MOVE K500-DTE-EFF-SALES-TR TO WS-F558O-DTE-EFF-SALE-TR          
                MOVE K500-DTE-APPROVE      TO WS-F558O-DTE-TRANSFER-TR          
                MOVE K500-DTE-APPLN        TO WS-F558O-DTE-APPLN-TR             
                MOVE K500-CDE-DIVORCE      TO WS-F558O-NUM-DIVORCE-TR           
                IF K500-NUM-HDB-REF(1:9) NOT =                                  
                               WS-F558O-NUM-HDB-REF-TR(1:9)                     
                   MOVE K500-NUM-HDB-REF      TO                                
                                          WS-F558O-MULTIPLE-FLAT-TRF            
                   MOVE K500-CDE-SALES-TYPE   TO                                
                                          WS-F558O-MULTIPLE-STY-TRF             
                   MOVE K500-CDE-DIVORCE      TO                                
                                          WS-F558O-MULTIPLE-DIVORCE-TRF         
                END-IF                                                          
              END-IF.                                                           
                                                                                
       2015-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2020-WRITE-F558-REC.                                                     
      *****************************************************************         
                                                                                
           MOVE SPACES                     TO P13F558O-REC.                     
                                                                                
           IF WS-F558O-DTE-APPLN-TR > '19970900'                                
                 PERFORM 3000-OUTPUT-RECORD THRU 3000-EXIT                      
           END-IF.                                                              
                                                                                
           WRITE P13F558O-REC FROM BP13F558-REC.                                
           ADD 1                      TO WS-F558O-CNT-WRT.                      
       2020-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-OUTPUT-RECORD.                                                      
                                                                                
            ADD 1 TO WS-TRF-VALID.                                              
            MOVE WS-F558O-NUM-TRANSFEROR   TO F558-NUM-TRANSFEROR.              
            MOVE WS-F558O-NUM-RSHIP-TR     TO F558-NUM-RSHIP-TR.                
            MOVE WS-F558O-NUM-MSTAT-TR     TO F558-NUM-MSTAT-TR.                
            MOVE WS-F558O-NUM-HDB-REF-TR   TO F558-NUM-HDB-REF-TR.              
            MOVE WS-F558O-NUM-SALES-TYPE-TR TO F558-NUM-SALES-TYPE-TR.          
            MOVE WS-F558O-NUM-ALLO-SCH-TR  TO F558-NUM-ALLO-SCH-TR.             
            MOVE WS-F558O-NUM-ELIG-SCH-TR  TO F558-NUM-ELIG-SCH-TR.             
            MOVE WS-F558O-DTE-EFF-SALE-TR  TO F558-DTE-EFF-SALE-TR.             
            MOVE WS-F558O-DTE-TRANSFER-TR  TO F558-DTE-TRANSFER-TR.             
            MOVE WS-F558O-DTE-APPLN-TR     TO F558-DTE-APPLN-TR.                
            MOVE WS-F558O-NUM-DIVORCE-TR   TO F558-NUM-DIVORCE-TR.              
            MOVE WS-F558O-MULTIPLE-FLAT-TRF TO F558-MULTIPLE-FLAT-TRF.          
            MOVE WS-F558O-MULTIPLE-STY-TRF  TO F558-MULTIPLE-STY-TRF.           
            MOVE WS-F558O-MULTIPLE-DIVORCE-TRF TO                               
                                            F558-MULTIPLE-DIVORCE-TRF.          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           DISPLAY 'PROGRAM = BP13C619 MATCH TRANSFER HISTORY'.                 
           DISPLAY 'TOTAL F558 REC READ     = ' WS-F558-CNT-READ.               
           DISPLAY 'TOTAL F558 REC WRITTEN  = ' WS-F558O-CNT-WRT.               
           DISPLAY 'REC FD IN TRANSFER SYS  = ' WS-TRF-FD.                      
           DISPLAY 'TRF REC AFTER 19970901  = ' WS-TRF-VALID.                   
                                                                                
           CLOSE      BB13K510                                                  
                      BB13K500                                                  
                      BP13F558                                                  
                      P13F558O.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
