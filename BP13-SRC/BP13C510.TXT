       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C510.                                                 
      *AUTHOR.        BALA AMUTHAN.                                             
      *DATE-WRITTEN.  22/03/2007.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :                                              *            
      *      PRINT REPORT ON DATE OF APPOINTMENT FOR BO            *            
      *                                                            *            
      *  INPUT FILES: 1. BP13L507                                  *            
      *  OUTPUT     : 1. BP27L510                                  *            
      *                                                            *            
      * BP133129 BA3  17082007 - NEW PROGRAM                       *            
      * BP133364 RB12 26062008 - CHANGE 'MONTHLY' TO 'QUARTERLY'   *            
      * BP133430 LMS1 10092008 - NEW SELECTION TYPE WIH            *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13L507 ASSIGN        TO BP13L507.                           
                                                                                
           SELECT BP13L510 ASSIGN        TO BP13L510.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13L507                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
                                                                                
       01  BP13L507-REC.                                                        
           05 L507-NUM-SELTYPE           PIC X(03).                     00161105
           05 L507-DTE-BALLOT            PIC X(06).                     00161105
           05 L507-NME-OCCP              PIC X(66).                             
           05 L507-NUM-REGN              PIC X(08).                             
           05 L507-NUM-NRIC              PIC X(09).                             
           05 L507-NUM-NT-FT-QUEUE       PIC X(05).                             
           05 L507-DTE-BK-APPT           PIC X(08).                             
           05 L507-TME-BK-APPT           PIC X(04).                             
           05 L507-TME-BK-APPT-NUM  REDEFINES L507-TME-BK-APPT.                 
              10 L507-TME-BK-APPT-HH     PIC 9(02).                             
              10 L507-TME-BK-APPT-MM     PIC 9(02).                             
           05 L507-NUM-FLAT-TYPE         PIC X(02).                             
           05 FILLER                     PIC X(39).                             
                                                                                
                                                                                
       FD  BP13L510                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  L510-PRTREC                   PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-SWITCHES.                                                         
           05  WS-L507-EOF               PIC X         VALUE 'N'.               
                                                                                
       01  WS-CNT-VAR.                                                          
           05  WS-L507-READ              PIC 9(5) COMP VALUE ZEROES.            
           05  WS-L510-PRINT             PIC 9(5) COMP VALUE ZEROES.            
           05  WS-PAGE-CNT               PIC 9(5) COMP VALUE ZEROES.            
           05  WS-LINE-CNT               PIC 9(2)      VALUE 56.                
                                                                                
       01  WS-DATE                       PIC X(8)      VALUE SPACES.            
                                                                                
       01  WS-PREV.                                                             
           05  WS-PREV-BALL-DTE          PIC X(6)      VALUE SPACES.            
           05  WS-PREV-FT-NT             PIC X(3)      VALUE SPACES.            
                                                                                
       77  WS-TME-BK-APPT-HH             PIC 99        VALUE ZEROES.            
                                                                                
      ***************************************************************           
      *    WS-TABLE-MONTH                                           *           
      ***************************************************************           
       01  WS-MTH.                                                              
           02  FILLER        PIC X(5)   VALUE '01JAN'.                          
           02  FILLER        PIC X(5)   VALUE '02FEB'.                          
           02  FILLER        PIC X(5)   VALUE '03MAR'.                          
           02  FILLER        PIC X(5)   VALUE '04APR'.                          
           02  FILLER        PIC X(5)   VALUE '05MAY'.                          
           02  FILLER        PIC X(5)   VALUE '06JUN'.                          
           02  FILLER        PIC X(5)   VALUE '07JUL'.                          
           02  FILLER        PIC X(5)   VALUE '08AUG'.                          
           02  FILLER        PIC X(5)   VALUE '09SEP'.                          
           02  FILLER        PIC X(5)   VALUE '10OCT'.                          
           02  FILLER        PIC X(5)   VALUE '11NOV'.                          
           02  FILLER        PIC X(5)   VALUE '12DEC'.                          
                                                                                
       01  WS-MTH-TAB REDEFINES WS-MTH.                                         
           05  WS-MTH-DETAIL OCCURS 12  TIMES INDEXED BY WS-IND.                
               10  WS-MTH-CODE      PIC X(2).                                   
               10  WS-MTH-DEF       PIC X(3).                                   
                                                                                
      ***************************************************************           
      *    WS-REPORT VARIBLE                                        *           
      ***************************************************************           
       01  L510-HEADER1.                                                        
           05  FILLER              PIC X(08)  VALUE 'BP13L510'.                 
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'HDBCAT3'.                  
           05  FILLER              PIC X(17)  VALUE SPACES.                     
           05  L510-RPT-NAME       PIC X(59)  VALUE SPACES.                     
           05  FILLER              PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'DATE : '.                  
           05  L510-DATE           PIC X(10)  VALUE SPACES.                     
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'PAGE : '.                  
           05  L510-PAGE           PIC Z(3)9  VALUE SPACES.                     
           05  FILLER              PIC X(12)  VALUE SPACES.                     
                                                                                
       01  L510-HEADER2.                                                        
           05  FILLER              PIC X(49)  VALUE SPACES.                     
           05  FILLER              PIC X(22)  VALUE                             
               'BALLOTING RESULTS FOR '.                                        
           05  L510-PRT-DATE       PIC X(08).                                   
           05  FILLER              PIC X(53)  VALUE SPACES.                     
                                                                                
       01  L510-HEADER4.                                                        
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  FILLER              PIC X(04)  VALUE 'NAME'.                     
           05  FILLER              PIC X(57)  VALUE SPACES.                     
           05  FILLER              PIC X(10)  VALUE 'NRIC-NO   '.               
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  FILLER              PIC X(08)  VALUE 'REGN-NO '.                 
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  FILLER              PIC X(08)  VALUE 'QUEUE-NO'.                 
                                                                                
       01  L510-DETAIL.                                                         
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  L510-NME-APPL       PIC X(60)    VALUE SPACES.                   
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  L510-NRIC-APPL      PIC X(09)    VALUE SPACES.                   
           05  FILLER              PIC X(03)    VALUE SPACES.                   
           05  L510-REGN           PIC X(08)    VALUE SPACES.                   
           05  FILLER              PIC X(04)    VALUE SPACES.                   
           05  L510-QUEUE-NO       PIC X(05)    VALUE SPACES.                   
           05  FILLER              PIC X(03)    VALUE SPACES.                   
                                                                                
       01  L510-DASH-LINE.                                                      
           05  FILLER              PIC X(2)   VALUE SPACES.                     
           05  FILLER              PIC X(124) VALUE ALL '-'.                    
           05  FILLER              PIC X(6)   VALUE SPACES.                     
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 0100-OPEN-ROUTINE            THRU 0100-EXIT.                 
           PERFORM 0200-READ-BP13L507           THRU 0200-EXIT.                 
           MOVE   L507-NUM-SELTYPE  TO  WS-PREV-FT-NT.                  00161105
           MOVE   L507-DTE-BALLOT   TO  WS-PREV-BALL-DTE.               00161105
           PERFORM 0300-MAIN-PROCESS            THRU 0300-EXIT                  
                   UNTIL WS-L507-EOF  = 'Y'.                                    
           PERFORM 9999-CLOSE-ROUTINE           THRU 9999-EXIT.                 
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       0100-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT  BP13L507                                                 
                OUTPUT BP13L510.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                          
                                                                                
           STRING WS-DATE(7:2) '/'                                              
                  WS-DATE(5:2) '/'                                              
                  WS-DATE(1:4)                                                  
              DELIMITED BY SIZE             INTO L510-DATE.                     
                                                                                
       0100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       0200-READ-BP13L507.                                                      
      *-------------------------------------------------------------            
           READ BP13L507                                                        
                AT END MOVE 'Y'         TO WS-L507-EOF                          
                GO TO 0200-EXIT.                                                
           ADD  +1                      TO WS-L507-READ.                        
                                                                                
       0200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       0300-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
           IF  L507-NUM-SELTYPE  NOT =  WS-PREV-FT-NT     OR            00161105
               L507-DTE-BALLOT   NOT =  WS-PREV-BALL-DTE  OR            00161105
               WS-LINE-CNT > 55                                                 
               PERFORM 0600-WRITE-HEAD       THRU 0600-EXIT                     
           END-IF.                                                              
           MOVE L507-NME-OCCP             TO  L510-NME-APPL.                    
           MOVE L507-NUM-REGN             TO  L510-REGN.                        
           IF  L507-NUM-NRIC NOT = SPACES AND LOW-VALUES                        
               STRING L507-NUM-NRIC(1:1) 'XXX' L507-NUM-NRIC(5:5)               
                      DELIMITED BY SIZE INTO L510-NRIC-APPL                     
           END-IF.                                                              
           MOVE L507-NUM-NT-FT-QUEUE      TO  L510-QUEUE-NO.                    
                                                                                
           WRITE L510-PRTREC FROM L510-DETAIL.                                  
                                                                                
           ADD  +1                       TO WS-L510-PRINT.                      
           ADD  +1                       TO WS-LINE-CNT.                        
           MOVE L507-NUM-SELTYPE         TO WS-PREV-FT-NT.                      
           MOVE L507-DTE-BALLOT          TO WS-PREV-BALL-DTE.                   
           PERFORM 0200-READ-BP13L507    THRU 0200-EXIT.                        
                                                                                
       0300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       0600-WRITE-HEAD.                                                         
      *-------------------------------------------------------------            
           MOVE SPACES          TO L510-RPT-NAME.                               
           EVALUATE L507-NUM-SELTYPE                                            
            WHEN 'WIA'                                                          
              MOVE 'BI-MONTHLY SALE OF 4-ROOM AND BIGGER FLATS (SECTOR A        
      -       ')' TO L510-RPT-NAME                                              
            WHEN 'WIB'                                                          
              STRING 'BI-MONTHLY SALE OF 4-ROOM AND BIGGER FLATS'               
                     '(SECTOR B)' DELIMITED BY SIZE INTO L510-RPT-NAME          
            WHEN 'WIC'                                                          
              STRING 'BI-MONTHLY SALE OF 4-ROOM AND BIGGER FLATS'               
                     '(SECTOR C)' DELIMITED BY SIZE INTO L510-RPT-NAME          
            WHEN '3R '                                                          
              MOVE '     QUARTERLY SALE OF 3-ROOM AND SMALLER FLATS' TO         
                                  L510-RPT-NAME                                 
            WHEN 'WIH'                                                          
              STRING 'HALF-YEARLY SALE OF 3-ROOM PREMIUM, 4-ROOM'               
                     ' AND BIGGER FLATS'                                        
              DELIMITED BY SIZE INTO L510-RPT-NAME                              
           END-EVALUATE.                                                        
                                                                                
           IF L507-DTE-BALLOT(5:2) NOT = LOW-VALUES AND SPACES                  
              SET WS-IND                TO 1                                    
              SEARCH WS-MTH-DETAIL                                              
                  AT END                                                        
                     DISPLAY 'MONTH NOT FOUND - ' L507-DTE-BALLOT(5:2)          
                  WHEN WS-MTH-CODE (WS-IND) = L507-DTE-BALLOT(5:2)              
                     STRING WS-MTH-DEF (WS-IND) ' '                             
                            L507-DTE-BALLOT(1:4) DELIMITED BY SIZE INTO         
                            L510-PRT-DATE                                       
           END-IF.                                                              
                                                                                
           ADD +1                           TO   WS-PAGE-CNT.                   
           MOVE WS-PAGE-CNT                 TO   L510-PAGE.                     
                                                                                
           MOVE SPACES                TO L510-PRTREC.                           
           WRITE L510-PRTREC FROM L510-HEADER1 AFTER PAGE.                      
           WRITE L510-PRTREC FROM L510-HEADER2.                                 
           MOVE SPACES                TO L510-PRTREC.                           
           WRITE L510-PRTREC.                                                   
           WRITE L510-PRTREC FROM L510-HEADER4.                                 
           WRITE L510-PRTREC FROM L510-DASH-LINE.                               
                                                                                
           MOVE    6                  TO  WS-LINE-CNT.                          
                                                                                
       0600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '- BP13C510 CONTROL TOTALS -----'.                           
           DISPLAY '  NO OF L507 READ  (BP13L507): ' WS-L507-READ.              
           DISPLAY '  NO OF RECORDS PRINT        : ' WS-L510-PRINT.             
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13L507                                                       
                 BP13L510.                                                      
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
