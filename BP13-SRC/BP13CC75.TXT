      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    BP13CC75.                                                 
       AUTHOR.        ARIEL HERNANDEZ.                                          
       DATE-WRITTEN.  08/05/2007.                                               
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  TO DELETE ARREARS PAYMENT FROM BP13KD92       *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13F125                                  *        
      *                  2.  SY02F001                                  *        
      *   I-O FILES      1.  BP13KD92                                  *        
      *                  2.  BP13KD94                                  *        
      *   OUTPUT         1.  BP13LC75                                  *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *                                                                *        
      *   CHGE-NO   BY    DATE      DESCRIPTION                        *        
      *   --------  ---  ---------- -----------                        *        
      *   BP133033  BA3  08/05/2007 NEW PROGRAM                        *        
      *   BP133171  ED1  02/11/2007 REMOVED UPD OF KD94-DTE&TME UPDATE *        
      ******************************************************************        
                                                                                
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F125  ASSIGN        TO BP13F125.                          
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
                                                                                
           SELECT BP13KD92  ASSIGN        TO BP13KD92                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD92-KEY-FLD                       
                            FILE STATUS   IS WS-KD92-STATUS.                    
                                                                                
           SELECT BP13KD94  ASSIGN        TO BP13KD94                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD94-KEY-FLD                       
                            FILE STATUS   IS WS-KD94-STATUS.                    
                                                                                
           SELECT BP13LC75  ASSIGN        TO BP13LC75.                          
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F125                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       COPY BP13F125.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13KD92                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
                                                                                
       COPY BP13KD92.                                                           
                                                                                
       FD  BP13KD94                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
                                                                                
       COPY BP13KD94.                                                           
                                                                                
       FD  BP13LC75                                                             
           BLOCK  CONTAINS    0 RECORDS                                         
           RECORD CONTAINS  132 CHARACTERS                                      
           LABEL  RECORD   IS   STANDARD                                        
           RECORDING MODE  IS   F.                                              
                                                                                
       01  BP13LC75-REC      PIC X(132).                                        
                                                                                
      *----------------------------------------------------------------*        
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
                                                                                
       COPY WSTABLES.                                                           
                                                                                
       01  WS-PAGE                      PIC 9(5)    VALUE ZEROES.               
       01  WS-SNO                       PIC 9(5)    VALUE ZEROES.               
       01  WS-LINE                      PIC 9(2)    VALUE 60.                   
       01  WS-KD92-FD                   PIC X(01)   VALUE SPACES.               
       01  WS-EOF-F125                  PIC X(01)   VALUE SPACES.               
       01  CNT-F125-READ                PIC 9(05)   VALUE ZEROES.               
       01  CNT-FD92-WRITE               PIC 9(05)   VALUE ZEROES.               
       01  WS-KD92-STATUS               PIC 9(2)    VALUE ZEROES.               
       01  WS-KD94-STATUS               PIC 9(2)    VALUE ZEROES.               
       01  CNT-KD92-READ                PIC 9(8)    VALUE ZEROES.               
       01  CNT-KD92-DELETE              PIC 9(8)    VALUE ZEROES.               
       01  CNT-KD92-NOTFOUND            PIC 9(8)    VALUE ZEROES.               
       01  CNT-KD94-WRITE               PIC 9(8)    VALUE ZEROES.               
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01  LC75-PR-HEAD-01.                                                     
           05  LC75-RPTNAME        PIC X(8)      VALUE 'BP13LC75'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  LC75-DATE           PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  LC75-PRT-PAGE       PIC ZZZZ9     VALUE ZEROES.                  
                                                                                
       01  LC75-PR-HEAD-02.                                                     
           05  FILLER              PIC X(41)     VALUE SPACES .                 
           05  FILLER              PIC X(46)     VALUE                          
               ' REPORT ON DELETED INSTALMENT ARREARS PAYMENT '.                
                                                                                
       01  LC75-PR-HEAD-03.                                                     
           05  FILLER              PIC X(34)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE                          
               ' SNO '.                                                         
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE                          
               'REGN NO'.                                                       
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE                          
               'SCHEME-ACCT'.                                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE                          
               '  AMOUNT  '.                                                    
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE                          
               'TRANS-DATE'.                                                    
                                                                                
       01  LC75-PR-HEAD-04.                                                     
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE ALL '-'.                 
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE ALL '-'.                 
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE ALL '-'.                 
                                                                                
       01  LC75-PR-DTL.                                                         
           05  FILLER              PIC X(34)     VALUE SPACES.                  
           05  LC75-PRT-SNO        PIC ZZZZ9     VALUE ZEROES.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LC75-REGN-NO        PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LC75-SCH            PIC X(11)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LC75-AMOUNT         PIC ZZZ,ZZ9.99      VALUE ZEROES.            
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LC75-TRANS-DATE     PIC X(10)     VALUE SPACES.                  
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN-ROUTINE.                                                       
      *----------------------------------------------------------------*        
                                                                                
           PERFORM  1000-OPEN-FILES        THRU    1000-EXIT.                   
           PERFORM  2000-READ-F125         THRU    2000-EXIT.                   
           PERFORM  3000-PROCESS           THRU    3000-EXIT                    
                    UNTIL WS-EOF-F125 = 'Y'.                                    
           PERFORM  9000-CLOSE-ROUTINE     THRU    9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  BP13F125                                                 
                       SY02F001                                                 
                I-O    BP13KD92                                                 
                       BP13KD94                                                 
                OUTPUT BP13LC75.                                                
                                                                                
           IF WS-KD92-STATUS NOT = 00 AND 97                                    
               DISPLAY 'OPENING ERROR, BP13KD92-STATUS ' WS-KD92-STATUS         
               MOVE     WS-KD92-STATUS     TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
           IF WS-KD94-STATUS NOT = 00 AND 97                                    
               DISPLAY 'OPENING ERROR, BP13KD94-STATUS ' WS-KD94-STATUS         
               MOVE     WS-KD94-STATUS     TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
           STRING F001-DTE-CURRENT(7:2) '/'                                     
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
                  DELIMITED BY SIZE INTO LC75-DATE.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-F125.                                                          
      *----------------------------------------------------------------*        
                                                                                
           READ BP13F125 AT END MOVE 'Y' TO WS-EOF-F125                         
                                MOVE HIGH-VALUES TO BP13F125-CASHTRAN           
                                GO TO 2000-EXIT                                 
           END-READ.                                                            
                                                                                
           ADD 1                         TO CNT-F125-READ.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
           PERFORM 4000-READ-KD92               THRU 4000-EXIT                  
           IF WS-KD92-FD = 'Y'                                                  
              PERFORM 5000-WRITE-KD94     THRU 5000-EXIT                        
              PERFORM 6000-DELETE-KD92    THRU 6000-EXIT                        
              PERFORM 7000-PRINT-REPORT   THRU 7000-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F125               THRU 2000-EXIT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-READ-KD92.                                                          
      *----------------------------------------------------------------*        
                                                                                
           MOVE F125-NUM-REGN      TO KD92-NUM-REGN.                            
           MOVE 'AHOC'             TO KD92-NUM-TRANS.                           
                                                                                
           READ BP13KD92                                                        
           EVALUATE WS-KD92-STATUS                                              
              WHEN 00                                                           
                 MOVE 'Y' TO WS-KD92-FD                                         
                 ADD 1 TO CNT-KD92-READ                                         
                                                                                
              WHEN 23                                                           
                 MOVE 'N' TO WS-KD92-FD                                         
                 ADD 1 TO CNT-KD92-NOTFOUND                                     
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READING BP13KD92 - STATUS '                  
                             WS-KD92-STATUS                                     
                 MOVE        WS-KD92-STATUS    TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5000-WRITE-KD94.                                                         
      *----------------------------------------------------------------*        
           MOVE SPACES              TO BP13KD94-REC.                            
           INITIALIZE                  BP13KD94-REC.                            
           MOVE BP13KD92-REC        TO BP13KD94-REC.                            
           MOVE KD92-NUM-REGN       TO KD94-NUM-REGN-LOG.                       
           MOVE KD92-NUM-TRANS      TO KD94-NUM-TRANS-LOG.                      
           MOVE 'BP13CC75'          TO KD94-NUM-USERID.                         
                                                                                
           WRITE BP13KD94-REC.                                                  
                                                                                
           EVALUATE WS-KD94-STATUS                                              
           WHEN 00                                                              
                ADD 1               TO CNT-KD94-WRITE                           
           WHEN OTHER                                                           
               DISPLAY 'BP13KD94 - ERROR IN WRITE, STATUS: '                    
                        WS-KD94-STATUS ' KEY: ' KD94-KEY-FLD                    
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       6000-DELETE-KD92.                                                        
      *----------------------------------------------------------------*        
           DELETE BP13KD92.                                                     
                                                                                
           EVALUATE WS-KD92-STATUS                                              
           WHEN 00                                                              
                ADD   1  TO   CNT-KD92-DELETE                                   
           WHEN OTHER                                                           
               DISPLAY 'BP13KD92 - ERROR IN DELETE, STATUS: '                   
                        WS-KD92-STATUS ' KEY ' KD92-KEY-FLD                     
           END-EVALUATE.                                                        
                                                                                
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       7000-PRINT-REPORT.                                                       
      *----------------------------------------------------------------*        
                                                                                
           IF WS-LINE > 50                                                      
              MOVE  SPACES                 TO BP13LC75-REC                      
              WRITE BP13LC75-REC        AFTER PAGE                              
              ADD   1                      TO WS-PAGE                           
              MOVE  WS-PAGE                TO LC75-PRT-PAGE                     
              WRITE BP13LC75-REC FROM LC75-PR-HEAD-01  AFTER 3                  
              WRITE BP13LC75-REC FROM LC75-PR-HEAD-02  AFTER 2                  
              WRITE BP13LC75-REC FROM LC75-PR-HEAD-03  AFTER 2                  
              WRITE BP13LC75-REC FROM LC75-PR-HEAD-04                           
              MOVE  SPACES                 TO BP13LC75-REC                      
              WRITE BP13LC75-REC                                                
              MOVE  10                     TO WS-LINE                           
           END-IF.                                                              
                                                                                
           ADD 1                             TO WS-SNO.                         
                                                                                
           MOVE SPACES                       TO LC75-PR-DTL.                    
                                                                                
           MOVE WS-SNO                       TO LC75-PRT-SNO.                   
           MOVE F125-NUM-REGN                TO LC75-REGN-NO.                   
           MOVE F125-NUM-SCH-ACCT            TO LC75-SCH.                       
           MOVE F125-AMT-RECEIPT             TO LC75-AMOUNT.                    
           STRING F125-DTE-TRANS(7:2) '/'                                       
                  F125-DTE-TRANS(5:2) '/'                                       
                  F125-DTE-TRANS(1:4)                                           
                  DELIMITED BY SIZE INTO        LC75-TRANS-DATE.                
                                                                                
           WRITE BP13LC75-REC              FROM LC75-PR-DTL AFTER 2.            
           ADD 2                             TO WS-LINE.                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-ROUTINE.                                                      
      *----------------------------------------------------------------*        
                                                                                
           CLOSE BP13F125                                                       
                 SY02F001                                                       
                 BP13KD92                                                       
                 BP13KD94                                                       
                 BP13LC75.                                                      
                                                                                
           IF WS-KD92-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR, BP13KD92-STATUS ' WS-KD92-STATUS.         
              MOVE WS-KD92-STATUS TO RETURN-CODE.                               
           IF WS-KD94-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR, BP13KD94-STATUS ' WS-KD94-STATUS.         
              MOVE WS-KD94-STATUS TO RETURN-CODE.                               
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '***** BP13CC75 CONTROL TOTALS ***** '.                      
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS READ FROM  (BP13F125) = '                     
                    CNT-F125-READ.                                              
           DISPLAY 'NO OF RECORDS FOUND FROM (BP13KD92) = '                     
                    CNT-KD92-READ.                                              
           DISPLAY 'NO OF RECORDS NOT FOUND  (BP13KD92) = '                     
                    CNT-KD92-NOTFOUND.                                          
           DISPLAY 'NO OF RECORDS DELETED    (BP13KD92) = '                     
                    CNT-KD92-DELETE.                                            
           DISPLAY 'NO OF RECORDS WRITTEN    (BP13KD94) = '                     
                    CNT-KD94-WRITE.                                             
           DISPLAY 'NO OF RECORDS WRITTEN    (BP13LC75) = '                     
                    WS-SNO.                                                     
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
