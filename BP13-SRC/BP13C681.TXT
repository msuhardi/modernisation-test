       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C681.                                                 
      *AUTHOR.        SURESH.                                                   
      *DATE-WRITTEN.  08/05/2000.                                               
      * =========================================================== *           
      *                   SYSTEM OF COMMITMENT (SOC)                *           
      * =========================================================== *           
      *  OBJECTIVES : 1. TO UPDATE EARLIEST MOVEIN DATE FROM RDBII  *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  DATE     BY   DESCRIPTION                          *           
      * -------- -------- ---- -----------                          *           
      * BP130833 20001006 YCH2 EARLIEST DTE-IN FOR LSE/CO-LSE SAME HH*          
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716 ASSIGN TO BP13F716.                                  
                                                                                
           SELECT P13F716A ASSIGN TO P13F716A.                                  
                                                                                
           SELECT P13F716B ASSIGN TO P13F716B.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F716     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 350 CHARACTERS                          
                        LABEL RECORD IS STANDARD                                
                        RECORDING MODE IS F.                                    
       COPY BP13F716.                                                           
                                                                                
       FD  P13F716A     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 350 CHARACTERS                          
                        LABEL RECORD IS STANDARD                                
                        RECORDING MODE IS F.                                    
                                                                                
       01 P13F716A-REC.                                                         
          05 F716A-NUM-REGN        PIC X(8).                                    
          05 F716A-NUM-NRIC        PIC X(9).                                    
          05 FILLER                PIC X(33).                                   
          05 F716A-NUM-SCH-ACCNT   PIC X(09).                                   
          05 FILLER                PIC X(29).                                   
          05 F716A-CDE-OCCUPIER    PIC X(1).                                    
          05 FILLER                PIC X(25).                                   
          05 F716A-DTE-MOVING-IN   PIC X(8).                                    
          05 F716A-DTE-MOVING-OUT  PIC X(8).                                    
          05 FILLER                PIC X(220).                                  
                                                                                
       FD  P13F716B     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 350 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
                                                                                
       01  P13F716B-REC            PIC X(350).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  WS-F716-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-F716A-CNT-READ   PIC 9(8)      VALUE ZEROS.                   
           05  WS-F716B-CNT-WRITE  PIC 9(8)      VALUE ZEROS.                   
                                                                                
       01  WS-SYS-DATE             PIC X(8).                                    
                                                                                
       01  WS-F716-KEY-FLD.                                                     
           05 WS-F716-NUM-REGN     PIC X(8)      VALUE SPACES.                  
           05 WS-F716-NUM-NRIC     PIC X(9)      VALUE SPACES.                  
           05 WS-F716-NUM-SCHACC   PIC X(9)      VALUE SPACES.                  
                                                                                
       01  WS-F716A-KEY-FLD.                                                    
           05 WS-F716A-NUM-REGN     PIC X(8)     VALUE SPACES.                  
           05 WS-F716A-NUM-NRIC     PIC X(9)     VALUE SPACES.                  
           05 WS-F716A-NUM-SCHACC   PIC X(9)     VALUE SPACES.                  
                                                                                
       01 WS-CDE-OCCUPIER           PIC X(1)     VALUE SPACES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
                                                                                
           PERFORM 2000-READ-BP13F716   THRU 2000-EXIT.                         
                                                                                
           PERFORM 3000-READ-P13F716A   THRU 3000-EXIT.                         
                                                                                
           PERFORM 5000-MAIN-ROUTINE    THRU 5000-EXIT                          
                   UNTIL WS-F716-KEY-FLD = HIGH-VALUE.                          
                                                                                
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
      ** BP13F716 -> RDB CURRENT RECORDS WITH PARENT-CHILD TAG                  
      ** P13F716A -> RDB MOBILITY RECORDS.                                      
      ** P13F716B -> UPDATE CURRENT RECORDS WITH EARLIEST MOVEIN DATE           
                                                                                
           OPEN INPUT  BP13F716                                                 
                       P13F716A                                                 
                OUTPUT P13F716B.                                                
                                                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYS-DATE.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13F716.                                                      
      *****************************************************************         
           READ BP13F716 AT END                                                 
                        MOVE HIGH-VALUE TO WS-F716-KEY-FLD                      
                        GO TO 2000-EXIT.                                        
                                                                                
           ADD 1 TO WS-F716-CNT-READ.                                           
                                                                                
           MOVE F716-NUM-REGN      TO WS-F716-NUM-REGN                          
           MOVE F716-NUM-NRIC      TO WS-F716-NUM-NRIC.                         
           MOVE F716-NUM-SCH-ACCNT TO WS-F716-NUM-SCHACC.                       
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-READ-P13F716A.                                                      
      *****************************************************************         
                                                                                
           READ P13F716A AT END                                                 
                        MOVE HIGH-VALUE TO WS-F716A-KEY-FLD                     
                        GO TO 3000-EXIT.                                        
                                                                                
           MOVE F716A-NUM-REGN      TO WS-F716A-NUM-REGN                        
           MOVE F716A-NUM-NRIC      TO WS-F716A-NUM-NRIC.                       
           MOVE F716A-NUM-SCH-ACCNT TO WS-F716A-NUM-SCHACC.                     
                                                                                
           ADD 1 TO WS-F716A-CNT-READ.                                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5000-MAIN-ROUTINE.                                                       
      *****************************************************************         
                                                                                
           IF WS-F716-KEY-FLD = WS-F716A-KEY-FLD                                
                                                                                
              MOVE F716-CDE-OCCUPIER        TO WS-CDE-OCCUPIER                  
                                                                                
              PERFORM 5200-UPDATE-MOVEINDTE THRU 5200-EXIT                      
                UNTIL WS-F716A-KEY-FLD NOT = WS-F716-KEY-FLD                    
                                                                                
              WRITE P13F716B-REC            FROM BP13F716-REC                   
              ADD 1                         TO WS-F716B-CNT-WRITE               
                                                                                
              PERFORM 2000-READ-BP13F716    THRU 2000-EXIT                      
           ELSE                                                                 
              IF WS-F716-KEY-FLD > WS-F716A-KEY-FLD                             
                 PERFORM 3000-READ-P13F716A THRU 3000-EXIT                      
              ELSE                                                              
                 IF WS-F716-KEY-FLD < WS-F716A-KEY-FLD                          
                    WRITE P13F716B-REC      FROM BP13F716-REC                   
                    ADD 1                   TO WS-F716B-CNT-WRITE               
                    PERFORM 2000-READ-BP13F716                                  
                                            THRU 2000-EXIT                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5200-UPDATE-MOVEINDTE.                                                   
      *****************************************************************         
                                                                                
           IF WS-CDE-OCCUPIER = '0'                                             
              IF F716A-CDE-OCCUPIER =  '0'                                      
                 PERFORM 5210-CHECK-DATES      THRU 5210-EXIT                   
              END-IF                                                            
           ELSE                                                                 
              IF WS-CDE-OCCUPIER = '1'                                          
                 IF F716A-CDE-OCCUPIER =  '0' OR '1'                            
                    PERFORM 5210-CHECK-DATES      THRU 5210-EXIT                
                 END-IF                                                         
              ELSE                                                              
                 PERFORM 5210-CHECK-DATES      THRU 5210-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-P13F716A          THRU 3000-EXIT.                  
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5210-CHECK-DATES.                                                        
            IF F716A-DTE-MOVING-IN < F716-DTE-MOVING-IN                         
               MOVE F716A-DTE-MOVING-IN         TO F716-DTE-MOVING-IN           
               MOVE F716A-CDE-OCCUPIER          TO WS-CDE-OCCUPIER              
            END-IF.                                                             
       5210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           DISPLAY 'SUMMARY: PGM = BP13C681 RUN-DATE: ' WS-SYS-DATE.            
           DISPLAY ' '.                                                         
           DISPLAY 'TOTAL BP13F716 READ      = ' WS-F716-CNT-READ.              
           DISPLAY 'TOTAL P13F716A READ      = ' WS-F716A-CNT-READ.             
           DISPLAY 'TOTAL P13F716B WRITTEN   = ' WS-F716B-CNT-WRITE.            
                                                                                
           CLOSE      BP13F716                                                  
                      P13F716A                                                  
                      P13F716B.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
