       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CB55.                                                  
      *AUTHOR.       YCH.                                                       
      *DATE-WRITTEN. 5/5/88.                                                    
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  THIS IS A PROGRAM TO MATCH THE F716 HDBREF    *                
      *          WITH SMS TO DETERMINE DP OF OM FLAT           *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE   DESCRIPTION                        *                
      * ======  ===  ====   ============                       *                
      *BP133275 YCH 190208  NEW PGM                            *                
      *BP139518 EAA2 230228 EAA2 TO INCLUDE 6 SALES TYPE CODE  *                
      *                     IN HOUSEHOLD DETERMINATION         *                
      *                     (8L/8M/8N/8R/8S/8T)                *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F716   ASSIGN TO BP13F716.                                
                                                                                
           SELECT AB03K030   ASSIGN TO AB03K030                                 
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K030-NUM-HDB-REF                              
                  FILE STATUS  IS AB03K030-STATUS.                              
                                                                                
           SELECT P13F716O   ASSIGN TO P13F716O.                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F716                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F716.                                                           
                                                                                
       FD   P13F716O                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01  P13F716O-REC          PIC X(350).                                    
                                                                                
       FD   AB03K030                                                            
            RECORD CONTAINS 800 CHARACTERS.                                     
                                                                                
       COPY AB03K030.                                                           
                                                                                
      ***************************                                               
       WORKING-STORAGE SECTION.                                                 
      ***************************                                               
       01  AB03K030-STATUS         PIC 9(2)    VALUE ZEROES.                    
       01  WS-F716-READ            PIC 9(8)    VALUE 0.                         
       01  WS-OUTPUT-CNT           PIC 9(8)    VALUE 0.                         
                                                                                
       01  EOF-BP13F716            PIC X       VALUE 'N'.                       
                                                                                
       01  WS-CUR-DATE             PIC X(8)   VALUE SPACES.                     
                                                                                
      ***************************                                               
       PROCEDURE DIVISION.                                                      
      ***************************                                               
       000-MAIN.                                                                
           OPEN INPUT  BP13F716                                                 
                       AB03K030                                                 
                OUTPUT P13F716O.                                                
                                                                                
           IF AB03K030-STATUS NOT = ZEROS AND 97                                
               DISPLAY 'FILE AB03K030 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' AB03K030-STATUS                        
               MOVE AB03K030-STATUS TO RETURN-CODE                              
               PERFORM 900-CLOSE-ROUTINE.                                       
                                                                                
           PERFORM 100-READ-F716-REC THRU 100-EXIT.                             
           PERFORM 200-PROCESS-REC   THRU 200-EXIT                              
                   UNTIL EOF-BP13F716 = 'Y'                                     
                     OR  F716-NUM-HDB-REF = HIGH-VALUE.                         
           PERFORM 900-CLOSE-ROUTINE.                                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-READ-F716-REC.                                                       
           READ BP13F716 AT END MOVE 'Y' TO EOF-BP13F716                        
                MOVE HIGH-VALUE TO F716-NUM-HDB-REF                             
                GO TO 100-EXIT.                                                 
           ADD 1 TO WS-F716-READ.                                               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *                                                                         
       200-PROCESS-REC.                                                         
                                                                                
           IF F716-NUM-HDB-REF NOT = SPACES AND LOW-VALUES AND ZEROS            
             MOVE F716-NUM-HDB-REF TO K030-NUM-HDB-REF                          
             READ AB03K030                                                      
             EVALUATE AB03K030-STATUS                                           
               WHEN 00                                                          
                IF (K030-CDE-SALES-TYP = '7 ' OR '7J' OR '8A' OR '8B' OR        
                                         '7C' OR '8E' OR '8L' OR '8M' OR        
                                         '8N' OR '8R' OR '8S' OR                
                                         '8T' ) OR                              
                   (K030-CDE-ALLOC-SCH = 'OM ' OR 'PPO' OR 'AOM' OR             
                                         'ROM' OR 'PPR' OR 'SPR')               
                    MOVE 'OM'             TO  F716-NUM-FILE-STATUS-SMS          
                ELSE                                                            
                    MOVE 'DP'             TO  F716-NUM-FILE-STATUS-SMS          
                END-IF                                                          
               WHEN 23                                                          
               WHEN 10                                                          
                    DISPLAY 'RECORD NOT FND ' K030-NUM-HDB-REF                  
               WHEN OTHER                                                       
                    MOVE AB03K030-STATUS TO RETURN-CODE                         
                    PERFORM 900-CLOSE-ROUTINE                                   
             END-EVALUATE                                                       
           END-IF.                                                              
                                                                                
           WRITE P13F716O-REC FROM BP13F716-REC.                                
           ADD 1 TO WS-OUTPUT-CNT.                                              
           PERFORM 100-READ-F716-REC THRU 100-EXIT.                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *    ACCEPT WS-CUR-DATE FROM DATE.                                        
           MOVE FUNCTION CURRENT-DATE             TO  WS-CUR-DATE.              
                                                                                
           DISPLAY 'BP13CB55                    DATE : ' WS-CUR-DATE.           
           DISPLAY 'MATCH NRIC IN REQUEST FILE WITH SMS MASTER'.                
           DISPLAY '*----------------------------------------*'.                
           DISPLAY '1) NO OF RECORDS READ FROM F716 ' WS-F716-READ.             
           DISPLAY ' '.                                                         
           DISPLAY '2) NO OF REORDS WRITE OUT       ' WS-OUTPUT-CNT.            
                                                                                
           CLOSE P13F716O                                                       
                 BP13F716                                                       
                 AB03K030.                                                      
                                                                                
           IF  AB03K030-STATUS NOT = ZEROS AND 97                               
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'AB03K030-STATUS ' AB03K030-STATUS.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
