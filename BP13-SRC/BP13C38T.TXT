      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C38T.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  OCTOBER 23, 2013.                                         
      *===============================================================*         
      * OBJECTIVE  :  TO PRINT EXCEPTION REPORT FOR 2RM FLEXI (AO/AP) *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F410                                     *         
      *               2. BP13K820                                     *         
      *               3. P04K010                                      *         
      *               4. BP13K024                                     *         
      *                                                               *         
      * OUTPUT FILE:  1. BP13L39X                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP136159 16/03/2016 PCL4 NEW PROGRAM                          *         
      * BP136477 18/10/2016 RJB1 TO REPLACE KIM HUAT TO YEE MIEN      *         
      * BP136600 14/03/2017 PCL4 TO UPDATE EMAIL RECIPIENT            *         
      * BP139030 07/01/2022 ZAR7 REPLACE CHUA YEE MIEN TO LIM KOK CHUN          
      * BP139276 05/04/2024 KAC1 REPLACE LIM KOK CHUN BY              *         
      *                                  KATHIJAH_BEE_ALI_MOHAMED     *         
      * BP139160 17/04/2024 KAC1 Update email recipient:              *         
      *                            TO: Chee Khiang CHIAM & Juariah ALI*         
      *                            CC: Soo Chiew low,Catherine HL TAN&*         
      *                                Catherine HL TAN               *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F410  ASSIGN        TO BP13F410.                         
           SELECT  BP13K820  ASSIGN        TO BP13K820                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K820-KEY-FLD                      
                             FILE STATUS   IS WS-K820-STATUS.                   
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
           SELECT  BP13K893  ASSIGN        TO BP13K893                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K893-KEY-FLD                      
                             FILE STATUS   IS WS-K893-STATUS.                   
           SELECT  BP13K895  ASSIGN        TO BP13K895                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K895-KEY-FLD                      
                             FILE STATUS   IS WS-K895-STATUS.                   
           SELECT  P04K010   ASSIGN        TO P04K010                           
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K010-PRIME-KEY                    
                             FILE STATUS   IS WS-K010-STATUS.                   
           SELECT  BP13L38T ASSIGN        TO BP13L38T.                          
           SELECT  MAILC38T ASSIGN        TO MAILC38T.                          
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F410                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K410.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K895                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K895.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   P04K010                                                             
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY P04K010.                                                            
                                                                                
       FD   BP13L38T                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   BP13L38T-REC               PIC X(132).                              
                                                                                
       FD   MAILC38T                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  100 CHARACTERS.                                    
       01   MAILC38T-REC              PIC X(100).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F410             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K820             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K800             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K893             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K895             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K010             PIC X(1)  VALUE 'N'.                     
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K893-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K820-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K895-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K010-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-HA-INFO.                                                      
               10  HA-INFO OCCURS 4 TIMES.                                      
                   20  HA-NRIC               PIC  X(09).                        
                   20  FTS-DEB               PIC  X(02).                        
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(1)  VALUE ZEROES.                  
           05  WS-HA                   PIC 9(1)  VALUE ZEROES.                  
           05  WS-FTS-DEBAR            PIC 9(1)  VALUE ZEROES.                  
           05  WS-F410-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-K820-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-P04K             PIC 9(7)  VALUE ZEROES.                  
           05  WS-P04K-FND             PIC 9(7)  VALUE ZEROES.                  
           05  WS-P04K-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-L38T-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-L38X-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(1)  VALUE 2.                       
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-NCS-CODE              PIC X(01) VALUE SPACES.                  
           05 WS-HA-WRITE              PIC X(1)  VALUE SPACES.                  
           05 WS-SPOUSE-NRIC           PIC X(09) VALUE SPACES.                  
           05 WS-SPOUSE-CODE           PIC X(02) VALUE SPACES.                  
           05 WS-SPOUSE-DEB            PIC X(01) VALUE SPACES.                  
           05 WS-SPOUSE-WRITE          PIC X(01) VALUE SPACES.                  
           05 WS-PRIOR-DTE             PIC 9(08) VALUE ZEROES.                  
           05 WS-NUM-DTE-APPT          PIC 9(07) VALUE ZEROES.                  
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-F410-NUM-REGN         PIC X(8)  VALUE SPACES.                  
           05 WS-NUM-NRIC              PIC X(9)  VALUE SPACES.                  
           05 WS-NUM-FTS               PIC X(2)  VALUE SPACES.                  
           05 WS-NUM-RELN              PIC X(20) VALUE SPACES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
           05 WS-FIRST-WRITE           PIC X(1)  VALUE 'Y'.                     
           05 WS-NUM-RELATIONSHIP      PIC X(02) VALUE SPACES.                  
           05 WS-DCODE                 PIC X(3)  VALUE SPACES.                  
           05 WS-CHECK-FC-DEBAR        PIC X(1)  VALUE SPACES.                  
           05 WS-FC-DEBAR              PIC X(1)  VALUE SPACES.                  
                                                                                
       01 WS-PRIO-DTE.                                                          
          05  WS-PRIO-CCYY                 PIC 9(04).                           
          05  WS-PRIO-MM                   PIC 9(02).                           
          05  WS-PRIO-DD                   PIC 9(02).                           
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
       01  WS-DEBAR-CODE               PIC X(02).                               
           88 VALID-AO-CODE          VALUE 'AO'.                                
           88 VALID-AP-CODE          VALUE 'AP'.                                
                                                                                
       01  L38T-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L38T'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L38T-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L38T-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  L38T-HEADER02.                                                       
           05  FILLER              PIC X(03)    VALUE SPACES.                   
           05  L38T-HDR02          PIC X(100)   VALUE SPACES.                   
                                                                                
       01  L38T-HEADER03.                                                       
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE '  S/N'.                  
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE 'REGN NO '.               
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(11)    VALUE 'ELIG SCHEME'.            
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(13)    VALUE 'KEY ISSUE-DTE'.          
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(09)    VALUE 'NRIC'.                   
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(13)    VALUE 'FIN OF SPOUSE'.          
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(13)    VALUE 'DEBAR CODE'.             
           05  FILLER              PIC X(02)    VALUE SPACES.                   
                                                                                
       01  L38T-DETAIL01.                                                       
           05  FILLER1             PIC X(01)        VALUE SPACES.               
           05  L38T-SN             PIC X(05)        VALUE SPACES.               
           05  FILLER2             PIC X(01)        VALUE SPACES.               
           05  L38T-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER3             PIC X(06)        VALUE SPACES.               
           05  L38T-ELIG           PIC X(03)        VALUE SPACES.               
           05  FILLER4             PIC X(06)        VALUE SPACES.               
           05  L38T-DTE-ISSUE      PIC X(10)        VALUE SPACES.               
           05  FILLER5             PIC X(03)        VALUE SPACES.               
           05  L38T-NRIC           PIC X(09)        VALUE SPACES.               
           05  FILLER6             PIC X(03)        VALUE SPACES.               
           05  L38T-SPOUSE         PIC X(13)        VALUE SPACES.               
           05  FILLER7             PIC X(02)        VALUE SPACES.               
           05  L38T-DEBAR          PIC X(13)        VALUE SPACES.               
           05  FILLER8             PIC X(01)        VALUE SPACES.               
                                                                                
       01  WS-REPORT-DTL.                                                       
           05  WS-EMPTY-RPT.                                                    
               10 FILLER                  PIC X(30) VALUE SPACES.               
               10 FILLER                  PIC X(21)                             
                                          VALUE '<   EMPTY  REPORT   >'.        
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(50)                               
               VALUE 'RCPT TO:<CHIAM_Chee_Khiang@hdb.gov.sg>'.                  
           05  WS-MAIL-RPT4             PIC X(50)                               
               VALUE 'RCPT TO:<JUARIAH_ALI@HDB.GOV.SG>'.                        
           05  WS-MAIL-RPT5             PIC X(50)                               
               VALUE 'RCPT TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT5A            PIC X(50)                               
               VALUE 'RCPT TO:<Catherine_HL_TAN@hdb.gov.sg>'.                   
           05  WS-MAIL-RPT6             PIC X(50)                               
               VALUE 'RCPT TO:<JAMILAH_BAHROM@HDB.GOV.SG>'.                     
           05  WS-MAIL-RPT7             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT8             PIC X(29)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT9             PIC X(50)                               
               VALUE 'TO:<CHIAM_Chee_Khiang@hdb.gov.sg>'.                       
           05  WS-MAIL-RPT10            PIC X(50)                               
               VALUE 'TO:<JUARIAH_ALI@HDB.GOV.SG>'.                             
           05  WS-MAIL-RPT11            PIC X(50)                               
               VALUE 'CC:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT11A           PIC X(50)                               
               VALUE 'CC:<Catherine_HL_TAN@hdb.gov.sg>'.                        
           05  WS-MAIL-RPT12            PIC X(50)                               
               VALUE 'CC:<JAMILAH_BAHROM@HDB.GOV.SG>'.                          
           05  WS-MAIL-RPT13            PIC X(100)                              
               VALUE 'SUBJECT: EXCEPTION REPORT FOR FTS'.                       
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F410      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F410 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BP13K820                                                 
                       BP13K895                                                 
                       BP13K800                                                 
                       BP13K893                                                 
                       P04K010                                                  
              OUTPUT   BP13L38T                                                 
                       MAILC38T.                                                
                                                                                
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR OPENING : ' WS-K820-STATUS              
              MOVE WS-K820-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K895-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K895 - ERROR OPENING : ' WS-K895-STATUS              
              MOVE WS-K895-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'P04K010 - ERROR OPENING : ' WS-K010-STATUS               
              MOVE WS-K010-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 7100-GET-DATE           THRU 7100-EXIT.                      
           PERFORM 7000-PRINT-HEADER       THRU 7000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F410.                                                      
      ******************************************************************        
                                                                                
           READ BP13F410           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F410                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F410-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       2100-READ-BP13K800.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K800-MASTER.                    
           INITIALIZE                       BP13K800-MASTER.                    
                                                                                
           MOVE K410-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    PERFORM 2200-START-BP13K893  THRU 2200-EXIT                 
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2200-START-BP13K893.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO BP13K893-MASTER             
                                                    BP13K800-MASTER.            
           INITIALIZE                               BP13K893-MASTER             
                                                    BP13K800-MASTER.            
                                                                                
           MOVE SPACES                           TO K893-KEY-FLD.               
           MOVE K410-NUM-REGN                    TO K893-NUM-REGN-HIST.         
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K893-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 2300-READ-BP13K893  THRU 2300-EXIT                  
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K893: ' WS-K893-STATUS          
                            ' REGN = ' K410-NUM-REGN                            
                    PERFORM  9999-CLOSE-ROUTINE THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2300-READ-BP13K893.                                                      
      ******************************************************************        
                                                                                
            READ  BP13K893 NEXT AT END                                          
                  MOVE SPACES               TO K893-KEY-FLD.                    
                                                                                
            EVALUATE WS-K893-STATUS                                             
                WHEN 00                                                         
                WHEN 02                                                         
                     IF K893-NUM-REGN-HIST = K410-NUM-REGN                      
                        MOVE BP13K893-MASTER(1:2000) TO BP13K800-MASTER         
                     END-IF                                                     
                WHEN 23                                                         
                     CONTINUE                                                   
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K893 : ' WS-K893-STATUS         
                             ' REGN = ' K893-NUM-REGN-HIST                      
                     PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                
            END-EVALUATE.                                                       
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 2100-READ-BP13K800                 THRU 2100-EXIT.           
                                                                                
                                                                                
           IF (K800-NUM-FLAT-TYPE = '2F') AND                                   
              (K800-NUM-LEASE-TENURE > 0  AND                                   
                       K800-NUM-LEASE-TENURE <= 45)                             
                                                                                
              IF (K410-DTE-KEY-ISSUE  >= WS-PRIO-DTE)    AND                    
                 (K410-DTE-KEY-ISSUE  <= WS-SYSTEM-DATE) AND                    
                 (K800-NUM-PORT-ELIG-TAG NOT = 'Y')      AND                    
                 (K800-NUM-ALLO-CAT      NOT = 'SER')                           
                 MOVE 0                         TO WS-HA                        
                                                   WS-FTS-DEBAR                 
                                                                                
                 MOVE SPACES                    TO WS-SPOUSE-NRIC               
                                                   WS-NCS-CODE                  
                                                   WS-SPOUSE-CODE               
                                                   FTS-DEB(1)                   
                                                   FTS-DEB(2)                   
                                                   FTS-DEB(3)                   
                                                   FTS-DEB(4)                   
                                                                                
                 MOVE K410-NRIC1                 TO HA-NRIC(1)                  
                 MOVE K410-NRIC2                 TO HA-NRIC(2)                  
                 MOVE K410-NRIC3                 TO HA-NRIC(3)                  
                 MOVE K410-NRIC4                 TO HA-NRIC(4)                  
                                                                                
                 PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 4                
                     IF HA-NRIC(WS-I) NOT = SPACES AND LOW-VALUES               
                        PERFORM 3100-READ-BP13K820 THRU 3100-EXIT               
                     END-IF                                                     
                 END-PERFORM                                                    
                                                                                
                 IF K410-CDE-ELIG-SCH = 'FTS'                                   
                    EVALUATE K410-CDE-ELIG-SCH                                  
                        WHEN 'SSC'                                              
                        WHEN 'SSE'                                              
                             MOVE 1                 TO WS-HA                    
                                                                                
                        WHEN 'JSS'                                              
                        WHEN 'JSE'                                              
                        WHEN 'NCS'                                              
                        WHEN 'NSE'                                              
                             MOVE 2                 TO WS-HA                    
                    END-EVALUATE                                                
                 ELSE                                                           
                    MOVE 1                          TO WS-HA                    
                 END-IF                                                         
                                                                                
                 PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 5                
                    IF WS-I <= 4                                                
                       IF HA-NRIC(WS-I) NOT = SPACES AND LOW-VALUES             
                          MOVE HA-NRIC(WS-I)            TO WS-NUM-NRIC          
                          PERFORM 4000-START-P04K010    THRU 4000-EXIT          
                       END-IF                                                   
                    ELSE                                                        
                       IF WS-SPOUSE-NRIC NOT = SPACES AND LOW-VALUES            
                          MOVE 'Y'                      TO WS-NCS-CODE          
                          MOVE WS-SPOUSE-NRIC           TO WS-NUM-NRIC          
                          PERFORM 4000-START-P04K010  THRU 4000-EXIT            
                       END-IF                                                   
                    END-IF                                                      
                 END-PERFORM                                                    
                                                                                
                 PERFORM 3050-PROCESS-FTS-DEBAR       THRU 3050-EXIT            
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F410              THRU 2000-EXIT.              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3050-PROCESS-FTS-DEBAR.                                                  
      ******************************************************************        
                                                                                
           IF WS-FTS-DEBAR >= WS-HA                                             
              CONTINUE                                                          
           ELSE                                                                 
              MOVE 'Y'                               TO WS-HA-WRITE             
                                                                                
              PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 5                   
                   IF WS-I <= 4                                                 
                      IF HA-NRIC(WS-I) NOT = SPACES AND LOW-VALUES              
                         MOVE HA-NRIC(WS-I)           TO WS-NUM-NRIC            
                         MOVE FTS-DEB(WS-I)           TO WS-NUM-FTS             
                         PERFORM 6000-PRINT-REPORT  THRU 6000-EXIT              
                      END-IF                                                    
                   ELSE                                                         
                      IF WS-SPOUSE-NRIC NOT = SPACES AND LOW-VALUES             
                         MOVE WS-SPOUSE-NRIC          TO WS-NUM-NRIC            
                         MOVE WS-SPOUSE-CODE          TO WS-NUM-FTS             
                         PERFORM 6000-PRINT-REPORT  THRU 6000-EXIT              
                      END-IF                                                    
                   END-IF                                                       
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
       3050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3100-READ-BP13K820.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K820-REC.                       
           INITIALIZE                       BP13K820-REC.                       
                                                                                
           MOVE K410-NUM-REGN            TO K820-NUM-REGN.                      
           MOVE HA-NRIC(WS-I)            TO K820-NUM-NRIC.                      
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
                   IF K820-NUM-NRIC-SPOUSE NOT = SPACES AND LOW-VALUES          
                      EVALUATE K820-NUM-NRIC-SPOUSE                             
                          WHEN K410-NRIC1                                       
                          WHEN K410-NRIC2                                       
                          WHEN K410-NRIC3                                       
                          WHEN K410-NRIC4                                       
                               CONTINUE                                         
                          WHEN OTHER                                            
                               MOVE K820-NUM-NRIC-SPOUSE                        
                                 TO WS-SPOUSE-NRIC                              
                      END-EVALUATE                                              
                   END-IF                                                       
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    PERFORM 3200-START-BP13K895   THRU 3200-EXIT                
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 READ ERROR ' WS-K820-STATUS               
                    DISPLAY 'K820 KEY: ' K820-KEY-FLD                           
                    MOVE WS-K820-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3200-START-BP13K895.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO BP13K895-REC                
                                                    BP13K820-REC.               
           INITIALIZE                               BP13K895-REC                
                                                    BP13K820-REC.               
                                                                                
           MOVE SPACES                           TO K895-KEY-FLD.               
           MOVE K410-NUM-REGN                    TO K895-NUM-REGN-HIST.         
           MOVE HA-NRIC(WS-I)                    TO K895-NUM-NRIC-HIST.         
                                                                                
           START BP13K895 KEY >= K895-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K895-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3300-READ-BP13K895  THRU 3300-EXIT                  
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K895: ' WS-K895-STATUS          
                            ' REGN = ' K410-NUM-REGN                            
                    PERFORM  9999-CLOSE-ROUTINE THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3300-READ-BP13K895.                                                      
      ******************************************************************        
                                                                                
            READ  BP13K895 NEXT AT END                                          
                  MOVE SPACES               TO K895-KEY-FLD.                    
                                                                                
            EVALUATE WS-K895-STATUS                                             
                WHEN 00                                                         
                WHEN 02                                                         
                     IF K895-NUM-REGN-HIST = K410-NUM-REGN AND                  
                        K895-NUM-NRIC-HIST = HA-NRIC(WS-I)                      
                        MOVE BP13K895-REC(1:400) TO BP13K820-REC                
                                                                                
                        IF K820-NUM-NRIC-SPOUSE NOT = SPACES AND                
                                                      LOW-VALUES                
                           EVALUATE K820-NUM-NRIC-SPOUSE                        
                               WHEN K410-NRIC1                                  
                               WHEN K410-NRIC2                                  
                               WHEN K410-NRIC3                                  
                               WHEN K410-NRIC4                                  
                                    CONTINUE                                    
                               WHEN OTHER                                       
                                    MOVE K820-NUM-NRIC-SPOUSE                   
                                      TO WS-SPOUSE-NRIC                         
                           END-EVALUATE                                         
                        END-IF                                                  
                     END-IF                                                     
                WHEN 23                                                         
                     CONTINUE                                                   
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K895 : ' WS-K895-STATUS         
                             ' REGN = ' K895-NUM-REGN-HIST                      
                     PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                
            END-EVALUATE.                                                       
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-START-P04K010.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                              TO P04K010-REC.             
           INITIALIZE                                  P04K010-REC.             
                                                                                
           MOVE 'N'                                 TO WS-EOF-K010.             
                                                                                
           MOVE SPACES                              TO K010-PRIME-KEY.          
           MOVE WS-NUM-NRIC                         TO K010-NUM-UINFIN.         
                                                                                
           START P04K010  KEY = K010-NUM-UINFIN.                                
                                                                                
           EVALUATE WS-K010-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 4100-READNEXT-P04K010  THRU 4100-EXIT               
                    PERFORM 4200-PROCESS-P04K010   THRU 4200-EXIT               
                            UNTIL WS-EOF-K010 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'START ERROR P04K010 : ' WS-K010-STATUS             
                            ' NRIC : ' K010-NUM-UINFIN                          
                    PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT               
            END-EVALUATE.                                                       
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       4100-READNEXT-P04K010.                                                   
      ******************************************************************        
                                                                                
           READ P04K010 NEXT RECORD                                             
                         AT END MOVE 'Y' TO WS-EOF-K010.                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4200-PROCESS-P04K010.                                                    
      ******************************************************************        
                                                                                
           IF K010-REASON-CODE = 'AO' OR 'AP'                                   
              MOVE K010-REASON-CODE                TO FTS-DEB(WS-I)             
           END-IF.                                                              
                                                                                
           IF (K010-NUM-UINFIN = WS-NUM-NRIC)                                   
               IF ((K410-CDE-ELIG-SCH = 'SSC' OR 'JSS' OR                       
                                        'SSE' OR 'JSE') AND                     
                   (K010-REASON-CODE  = 'AO')) OR                               
                  ((K410-CDE-ELIG-SCH = 'NCS' OR 'NSE') AND                     
                   (K010-REASON-CODE  = 'AP'))                                  
                                                                                
                                                                                
                    IF WS-NCS-CODE = 'Y'                                        
                       IF K410-CDE-ELIG-SCH = 'NCS' OR 'NSE'                    
                          ADD 1                     TO WS-FTS-DEBAR             
                       END-IF                                                   
                    ELSE                                                        
                       ADD 1                        TO WS-FTS-DEBAR             
                    END-IF                                                      
                                                                                
                    MOVE 'Y'                        TO WS-EOF-K010              
               ELSE                                                             
                   IF K010-REASON-CODE = 'AO' OR 'AP'                           
                      ADD 1                         TO WS-FTS-DEBAR             
                      MOVE 'Y'                      TO WS-EOF-K010              
                   END-IF                                                       
               END-IF                                                           
                                                                                
               PERFORM 4100-READNEXT-P04K010      THRU 4100-EXIT                
           ELSE                                                                 
               MOVE 'Y'                             TO WS-EOF-K010              
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       5000-HEADER-EMAIL.                                                       
      ******************************************************************        
                                                                                
            WRITE MAILC38T-REC FROM WS-MAIL-RPT1.                               
            WRITE MAILC38T-REC FROM WS-MAIL-RPT2.                               
            WRITE MAILC38T-REC FROM WS-MAIL-RPT3.                               
            WRITE MAILC38T-REC FROM WS-MAIL-RPT4.                               
            WRITE MAILC38T-REC FROM WS-MAIL-RPT5.                               
            WRITE MAILC38T-REC FROM WS-MAIL-RPT5A.                              
            WRITE MAILC38T-REC FROM WS-MAIL-RPT6.                               
            WRITE MAILC38T-REC FROM WS-MAIL-RPT7.                               
            WRITE MAILC38T-REC FROM WS-MAIL-RPT8.                               
            WRITE MAILC38T-REC FROM WS-MAIL-RPT9.                               
            WRITE MAILC38T-REC FROM WS-MAIL-RPT10.                              
            WRITE MAILC38T-REC FROM WS-MAIL-RPT11.                              
            WRITE MAILC38T-REC FROM WS-MAIL-RPT11A.                             
            WRITE MAILC38T-REC FROM WS-MAIL-RPT12.                              
                                                                                
            STRING 'SUBJECT: EXCEPTION REPORT-2RM FLEXI '                       
                    '(SHORT LEASE) ('                                           
                    WS-PRIO-DD '/' WS-PRIO-MM '/' WS-PRIO-CCYY ' TO '           
                    WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY  ') '            
                    'WITH NO AO/AP DEB TAG'                                     
              DELIMITED BY SIZE INTO WS-MAIL-RPT13.                             
                                                                                
            WRITE MAILC38T-REC FROM WS-MAIL-RPT13.                              
                                                                                
                                                                                
            STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY                   
                    DELIMITED BY SIZE INTO WS-DATE-RPT.                         
                                                                                
            WRITE MAILC38T-REC FROM WS-DATE-RPT-DTL                             
                                                                                
            MOVE SPACES TO MAILC38T-REC.                                        
            WRITE MAILC38T-REC.                                                 
                                                                                
            WRITE MAILC38T-REC FROM L38T-HEADER01.                              
            WRITE MAILC38T-REC FROM L38T-HEADER02.                              
                                                                                
            MOVE SPACES TO MAILC38T-REC.                                        
            WRITE MAILC38T-REC.                                                 
                                                                                
            WRITE MAILC38T-REC FROM L38T-HEADER03.                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       6000-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
           MOVE SPACES                       TO L38T-DETAIL01.                  
           INITIALIZE                           L38T-DETAIL01.                  
                                                                                
                                                                                
                                                                                
           MOVE ';'                       TO FILLER2 FILLER3 FILLER4            
                                             FILLER5 FILLER6 FILLER7            
                                             FILLER8.                           
                                                                                
           IF WS-HA-WRITE = 'Y'                                                 
              MOVE 'N'                    TO WS-HA-WRITE                        
                                                                                
              ADD 1                       TO WS-SNO                             
              MOVE WS-SNO                 TO WS-SNO-ZZZZ9                       
              MOVE WS-SNO-ZZZZ9           TO L38T-SN                            
              MOVE K410-NUM-REGN          TO L38T-REGN                          
              MOVE K410-CDE-ELIG-SCH      TO L38T-ELIG                          
                                                                                
              IF K410-DTE-KEY-ISSUE NOT = SPACES AND LOW-VALUES                 
                                                 AND ZEROES                     
                 STRING K410-DTE-KEY-ISSUE(7:2) '/'                             
                        K410-DTE-KEY-ISSUE(5:2) '/'                             
                        K410-DTE-KEY-ISSUE(1:4)                                 
                   DELIMITED BY SIZE INTO L38T-DTE-ISSUE                        
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES                 TO L38T-REGN                          
                                             L38T-SN                            
                                             L38T-ELIG                          
                                             L38T-DTE-ISSUE                     
           END-IF.                                                              
                                                                                
           MOVE WS-SPOUSE-NRIC            TO L38T-SPOUSE.                       
           MOVE WS-NUM-NRIC               TO L38T-NRIC.                         
           MOVE WS-NUM-FTS                TO L38T-DEBAR.                        
                                                                                
           WRITE BP13L38T-REC           FROM L38T-DETAIL01.                     
           WRITE MAILC38T-REC           FROM L38T-DETAIL01.                     
           ADD 1                          TO WS-CNT-L38T-WRITE.                 
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)      TO WS-SYSTEM-DATE               
                                                   WS-CURR-DATE.                
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
              DELIMITED BY SIZE INTO L38T-DATE-HDR.                             
                                                                                
            STRING 'EXCEPTION REPORT FOR 2RM FLEXI (SHORT LEASE) ('             
                    WS-PRIO-DD '/' WS-PRIO-MM '/' WS-PRIO-CCYY ' TO '           
                    WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY  ')'             
                   ' W/ NO AO/AP DEB TAG'                                       
                     DELIMITED BY SIZE INTO L38T-HDR02.                         
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO L38T-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO L38T-PAGE-HDR.                
                                                                                
           WRITE BP13L38T-REC    FROM L38T-HEADER01 AFTER PAGE.                 
           WRITE BP13L38T-REC    FROM L38T-HEADER02.                            
           WRITE BP13L38T-REC    FROM L38T-HEADER03 AFTER 2.                    
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE 5  TO WS-LINE-CTR.                                              
                                                                                
           PERFORM 5000-HEADER-EMAIL           THRU 5000-EXIT.                  
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7100-GET-DATE.                                                           
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO  WS-PRIOR-DTE.                
                                                                                
           COMPUTE WS-NUM-DTE-APPT =                                            
                   FUNCTION INTEGER-OF-DATE(WS-PRIOR-DTE).                      
                                                                                
           SUBTRACT 6 FROM  WS-NUM-DTE-APPT.                                    
                                                                                
           COMPUTE  WS-PRIOR-DTE =                                              
                    FUNCTION DATE-OF-INTEGER(WS-NUM-DTE-APPT).                  
                                                                                
           MOVE WS-PRIOR-DTE        TO WS-PRIO-DTE.                             
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C38T'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F410 RECORDS READ............. : '            
                    WS-F410-READ.                                               
           DISPLAY '(2) NO OF BP13L38T RECORDS WRITTEN.......... : '            
                    WS-CNT-L38T-WRITE.                                          
                                                                                
           CLOSE    BP13F410                                                    
                    BP13K820                                                    
                    BP13K895                                                    
                    BP13K800                                                    
                    BP13K893                                                    
                    P04K010                                                     
                    BP13L38T                                                    
                    MAILC38T.                                                   
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR CLOSING : ' WS-K820-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K895-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K895 - ERROR CLOSING : ' WS-K895-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K895-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K893 - ERROR CLOSING : ' WS-K895-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'P04K010  - ERROR CLOSING : ' WS-K010-STATUS              
              MOVE WS-K010-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
