/******************************************************************/            
/* SYSTEM OF COMMITMENT (SOC)                                     */            
/*================================================================*/            
/*                                                                */            
/* OBJECTIVES :                                                   */            
/*  OUTPUT ALL AHG CASES                                          */            
/*                                                                */            
/*----------------------------------------------------------------*/            
/* CHG REF  DATE     BY    DESCRIPTION                            */            
/* -------- -------- ----  -----------                            */            
/* BP132892 20060925 SSS2  NEW PROGRAM                            */            
/******************************************************************/            
                                                                                
  PROC FORMAT;                                                                  
                                                                                
   VALUE $AHG_FMT "A"="APPROVE"                                                 
                  "C"="CLOSED"                                                  
                  "F"="FORM PRINT"                                              
                  "R"="REJECT"                                                  
                  "S"="SUBMISSION";                                             
                                                                                
   VALUE $CAT_FMT "1"="MALAY  "                                                 
                  "2"="CHINESE"                                                 
                  "3"="INDIAN "                                                 
                  "4"="OTHERS ";                                                
                                                                                
   VALUE $HH_FMT  "H"="FT"                                                      
                  "T"="FT"                                                      
                  "G"="2T"                                                      
                  "3"="2T";                                                     
                                                                                
  OPTIONS NODATE CAPS;                                                          
                                                                                
  DATA  BP13F800;                                                               
     INFILE BP13F800 END=EOF;                                                   
     INPUT @4    NUM_REGN        $CHAR8.                                        
           @30   FT_TYPE         $CHAR2.                                        
           @51   CDE_NT          $CHAR3.                                        
           @57   ALL_SCH         $CHAR3.                                        
           @60   ELG_SCH         $CHAR3.                                        
           @81   ETHNIC          $CHAR1.                                        
           @525  HH_TYPE         $CHAR1.                                        
           @924  NUM_SEL         $CHAR3.                                        
           @785  AL_QTR          $CHAR6.                                        
           @1229 AMT_AHG         $CHAR3.                                        
           @1248 AHG_STAT        $CHAR1.                                        
           @1240 AHG_PRCY        $CHAR4.                                        
           @1244 AHG_PRMM        $CHAR2.                                        
           @1246 AHG_PRDD        $CHAR2.                                        
           @1232 AHG_RECY        $CHAR4.                                        
           @1236 AHG_REMM        $CHAR2.                                        
           @1238 AHG_REDD        $CHAR2.                                        
           @1980 HH_SZ           $CHAR2.                                        
           @1982 AGE_HA1         $CHAR4.                                        
           @1994 HH_INC          $CHAR5.                                        
           ;                                                                    
                                                                                
      LENGTH K110KEY $8.;                                                       
      K110KEY=NUM_REGN;                                                         
                                                                                
      INFILE BM06K111 VSAM KEY=K110KEY;                                         
      INPUT @;                                                                  
      RC=_IORC_; FBC=_FDBK_;                                                    
      IF RC=0 THEN                                                              
         INPUT @15  NEIGH     $CHAR4.                                           
               @19  CONTR     $CHAR4.                                           
               @299 ACCPT_CY  $CHAR4.                                           
               @303 ACCPT_MM  $CHAR2.                                           
               @305 ACCPT_DD  $CHAR2.;                                          
      ELSE                                                                      
         DO;                                                                    
            _ERROR_=0;                                                          
            FILE LOG;                                                           
            PUT @1 'NUM_REGN NOTFND IN K110 ' K110KEY;                          
         END;                                                                   
                                                                                
   PROC SORT DATA=BP13F800; BY NUM_REGN;                                        
                                                                                
   DATA FLAT1;                                                                  
      SET BP13F800;BY NUM_REGN;                                                 
      DACCEPT=ACCPT_DD||'/'||ACCPT_MM||'/'||ACCPT_CY;                           
      DPROCESS=AHG_PRDD||'/'||AHG_PRMM||'/'||AHG_PRCY;                          
      DREQUEST=AHG_REDD||'/'||AHG_REMM||'/'||AHG_RECY;                          
                                                                                
   PROC SORT DATA=FLAT1; BY NUM_REGN;                                           
   DATA _NULL_;                                                                 
   SET FLAT1  END=EOF;                                                          
   FILE BP13FOUT NOTITLES;                                                      
      CNT+1;                                                                    
      IF CNT=1 THEN                                                             
         LINK HEADER1;                                                          
                                                                                
      PUT @1   NUM_REGN                                                         
          @10  CDE_NT                                                           
          @14  NEIGH                                                            
          @19  CONTR                                                            
          @24  HH_TYPE   $HH_FMT.                                               
          @27  FT_TYPE                                                          
          @30  NUM_SEL                                                          
          @34  AL_QTR                                                           
          @41  HH_INC                                                           
          @47  AGE_HA1                                                          
          @52  ALL_SCH                                                          
          @56  ELG_SCH                                                          
          @60  ETHNIC    $CAT_FMT.                                              
          @68  HH_SZ                                                            
          @71  AMT_AHG                                                          
          @75  AHG_STAT  $AHG_FMT.                                              
          @86  DPROCESS                                                         
          @97  DREQUEST                                                         
          @108 DACCEPT                                                          
          ;                                                                     
      RETURN;                                                                   
                                                                                
   HEADER1:                                                                     
      PUT @1   'REGN'                                                           
          @10  'NT'                                                             
          @14  'NEI'                                                            
          @19  'CON'                                                            
          @24  'HH'                                                             
          @27  'FT'                                                             
          @30  'SEL'                                                            
          @34  'AL QTR'                                                         
          @41  'INCM'                                                           
          @47  'AGE'                                                            
          @52  'ALC'                                                            
          @56  'ELG'                                                            
          @60  'ETHNIC'                                                         
          @68  'SZ'                                                             
          @71  'AHG'                                                            
          @75  'AHG STAT'                                                       
          @86  'AHG PRO'                                                        
          @97  'AHG REQ'                                                        
          @108 'APPL ACCPT'                                                     
          ;                                                                     
      RETURN;                                                                   
                                                                                
