      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C26A.                                                 
       AUTHOR.        ELGER DE GUZMAN                                           
       DATE-WRITTEN.  05/01/15.                                                 
                                                                                
      * ====================================================== *                
      *    ONE-TIME PROGRAM.                                   *                
      *--------------------------------------------------------*                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : FOR APPOINTMENT CPF REQUEST BALANCE   *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                         *                
      * -------  ---  ------   -----------------------------   *                
      * BP135619 ESD1 050115   NEW PROGRAM                     *                
      * BP136460 KSJ3 020317   EXPAND BP13K68A (1500-1600)     *                
      * ====================================================== *                
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
      *---------------------*                                                   
       INPUT-OUTPUT SECTION.                                                    
      *---------------------*                                                   
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310 ASSIGN TO BP13F310.                                  
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS K800-STATUS.                               
                                                                                
           SELECT BP13K68A ASSIGN TO BP13K68A                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K68A-NUM-REGN                              
                  FILE STATUS     IS K68A-STATUS.                               
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS K820-STATUS.                               
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD    KEY    IS K110-KEY-FLD                    
                            ALTERNATE RECORD KEY IS K110-AIX1                   
                            FILE STATUS      IS WS-K110-STATUS.                 
                                                                                
           SELECT AB02F870 ASSIGN TO AB02F870.                                  
                                                                                
                                                                                
      *-------------*                                                           
       DATA DIVISION.                                                           
      *-------------*                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F310                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F310.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K68A                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13K68A.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  AB02F870                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY AB02F870.                                                           
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
       01  WS-FILE-STATUS.                                                      
           05 K68A-STATUS                 PIC 99    VALUE ZEROS.                
           05 K800-STATUS                 PIC 99    VALUE ZEROS.                
           05 K820-STATUS                 PIC 99    VALUE ZEROS.                
           05 WS-K110-STATUS              PIC 9(2)   VALUE 00.                  
           05 WS-K100-STATUS              PIC 9(2)   VALUE 00.                  
                                                                                
       01  WS-OTHER-VAR.                                                        
           05  WS-EOF-BM06K110            PIC X      VALUE 'N'.                 
           05  WS-NUM-REGN                PIC X(8)   VALUE SPACES.              
           05  K68A-FOUND                 PIC X(1)   VALUE SPACES.              
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F310-READ                PIC 9(5)  VALUE 0.                    
           05 WS-F870-WRITE               PIC 9(5)  VALUE 0.                    
                                                                                
       01  WS-EOF-F310                    PIC 9     VALUE 0.                    
           88 EOF-F310                              VALUE 1.                    
                                                                                
       01  WS-K110-KEY-TEMP.                                                    
           05 K110-ESTATE-TEMP          PIC X(3)   VALUE SPACES.                
           05 K110-NEIGHBOURHOOD-TEMP   PIC X(4)   VALUE SPACES.                
           05 K110-CONTRACT-NO-TEMP     PIC X(4)   VALUE SPACES.                
           05 K110-BLK-NO-TEMP          PIC X(5)   VALUE SPACES.                
                                                                                
       01  WS-CONTROL-DATE.                                                     
           05 WS-CTRL-CC                  PIC XX.                               
           05 WS-CTRL-YY                  PIC XX.                               
           05 WS-CTRL-MM                  PIC XX.                               
           05 WS-CTRL-DD                  PIC XX.                               
                                                                                
       01  WS-DATE.                                                             
           05 WS-CCYY.                                                          
              10 WS-CC                    PIC 99.                               
              10 WS-YY                    PIC 99.                               
           05 WS-CCYY1 REDEFINES WS-CCYY  PIC 9(4).                             
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *------------------*                                                      
       000-MAIN-ROUTINE.                                                        
      *------------------*                                                      
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 150-READ-SY02F001  THRU 150-EXIT.                            
           PERFORM 220-READ-BP13F310  THRU 220-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL EOF-F310.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       100-INITIALIZATION.                                                      
      *------------------*                                                      
                                                                                
           OPEN INPUT  BP13F310                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BM06K110                                                 
                       SY02F001                                                 
                I-O    BP13K68A                                                 
                OUTPUT AB02F870.                                                
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING BP13K800 ERROR ' K800-STATUS                     
              MOVE     K800-STATUS         TO RETURN-CODE                       
              PERFORM 999-CLOSE-FILES       THRU 999-EXIT                       
           END-IF.                                                              
                                                                                
           IF K68A-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING BP13K68A ERROR ' K68A-STATUS                     
              MOVE     K68A-STATUS         TO RETURN-CODE                       
              PERFORM 999-CLOSE-FILES       THRU 999-EXIT                       
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING BP13K820 ERROR ' K820-STATUS                     
              MOVE     K820-STATUS         TO RETURN-CODE                       
              PERFORM 999-CLOSE-FILES       THRU 999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO RETURN-CODE                       
              PERFORM 999-CLOSE-FILES       THRU 999-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE ZEROS  TO WS-COUNTERS.                                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       150-READ-SY02F001.                                                       
      *-------------------------------------------------------------            
           READ SY02F001                                                        
                AT END                                                          
                   GO TO 150-EXIT.                                              
                                                                                
           MOVE F001-DTE-CURRENT TO WS-DATE.                                    
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       220-READ-BP13F310.                                                       
      *------------------*                                                      
           READ BP13F310 AT END                                                 
                MOVE 1   TO WS-EOF-F310                                         
                GO TO 220-EXIT.                                                 
                                                                                
           ADD  1  TO WS-F310-READ.                                             
                                                                                
       220-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       300-PROCESS-RTN.                                                         
      *------------------*                                                      
           PERFORM 350-EVALUATE-TRANS-CODE  THRU  350-EXIT                      
           PERFORM 220-READ-BP13F310 THRU 220-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       350-EVALUATE-TRANS-CODE.                                                 
      *-----------------------*                                                 
           MOVE F310-NUM-BLK          TO   K110-BLK-NO-TEMP                     
           MOVE F310-NME-STREET(1:3)  TO   K110-ESTATE-TEMP                     
           MOVE F310-NME-STREET(4:4)  TO   K110-NEIGHBOURHOOD-TEMP              
           MOVE F310-NME-STREET(8:4)  TO   K110-CONTRACT-NO-TEMP                
                                                                                
           MOVE WS-K110-KEY-TEMP      TO   K110-AIX1                            
                                                                                
           IF  F310-CDE-TRANS = 'C7'                                            
               MOVE 'N'                   TO   WS-EOF-BM06K110                  
               START BM06K110 KEY  = K110-AIX1                                  
               IF WS-K110-STATUS NOT = 00                                       
                  DISPLAY 'BM06K110 READ FILE ERROR : ' WS-K110-STATUS          
                  MOVE WS-K110-STATUS           TO RETURN-CODE                  
                  PERFORM 999-CLOSE-FILES       THRU 999-EXIT                   
               END-IF                                                           
               PERFORM 3100-BM06K110-REGNO THRU  3100-EXIT                      
               UNTIL WS-K110-KEY-TEMP NOT = K110-AIX1                           
               OR WS-EOF-BM06K110 = 'Y'                                         
           ELSE                                                                 
               IF  F310-CDE-TRANS = 'C8'                                        
                   MOVE F310-REGN-NO           TO    WS-NUM-REGN                
                   PERFORM 400-WRITE-OUTPUT    THRU  400-EXIT                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       400-WRITE-OUTPUT.                                                        
      *------------------*                                                      
           MOVE SPACES                  TO AB02F870-REC.                        
           MOVE WS-NUM-REGN           TO K800-NUM-REGN.                         
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS = 00                                                  
              CONTINUE                                                          
           ELSE                                                                 
              IF K800-STATUS = 23                                               
                 DISPLAY 'RECORD NOT FOUND IN BP13K800, ' K800-NUM-REGN         
                 GO TO 400-EXIT                                                 
              ELSE                                                              
                 MOVE K800-STATUS       TO RETURN-CODE                          
                 DISPLAY 'ERROR READING BP13K800, ' K800-NUM-REGN               
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
           MOVE 'HPI'                   TO F870-NUM-SYSTEM.                     
           MOVE K800-NUM-REGN           TO F870-NUM-REGN                        
           MOVE K800-NUM-SCH-ACC        TO F870-NUM-HDB-REF.                    
                                                                                
           MOVE 'B1'                    TO F870-NUM-TRANS.                      
           MOVE ZEROS                   TO F870-AMT-LOAN-GRANT.                 
           MOVE ZEROS                   TO F870-DTE-LOAN-COMM.                  
                                                                                
           MOVE K800-NUM-REGN           TO K820-NUM-REGN.                       
           MOVE K800-NUM-NRIC1          TO K820-NUM-NRIC.                       
           PERFORM 410-READ-BP13K820    THRU 410-EXIT.                          
           MOVE K820-NUM-NRIC           TO F870-NUM-UIN-LSE1.                   
           MOVE K820-NUM-CPF            TO F870-NUM-CPF1.                       
                                                                                
           IF K800-NUM-NRIC2          = LOW-VALUES OR SPACES                    
              CONTINUE                                                          
           ELSE                                                                 
              MOVE K800-NUM-REGN        TO K820-NUM-REGN                        
              MOVE K800-NUM-NRIC2       TO K820-NUM-NRIC                        
              PERFORM 410-READ-BP13K820 THRU 410-EXIT                           
              MOVE K820-NUM-NRIC        TO F870-NUM-UIN-LSE2                    
              MOVE K820-NUM-CPF         TO F870-NUM-CPF2.                       
                                                                                
           IF K800-NUM-NRIC3          = LOW-VALUES OR SPACES                    
              CONTINUE                                                          
           ELSE                                                                 
              MOVE K800-NUM-REGN        TO K820-NUM-REGN                        
              MOVE K800-NUM-NRIC3       TO K820-NUM-NRIC                        
              PERFORM 410-READ-BP13K820 THRU 410-EXIT                           
              MOVE K820-NUM-NRIC        TO F870-NUM-UIN-LSE3                    
              MOVE K820-NUM-CPF         TO F870-NUM-CPF3.                       
                                                                                
           IF K800-NUM-NRIC4          = LOW-VALUES OR SPACES                    
              CONTINUE                                                          
           ELSE                                                                 
              MOVE K800-NUM-REGN        TO K820-NUM-REGN                        
              MOVE K800-NUM-NRIC4       TO K820-NUM-NRIC                        
              PERFORM 410-READ-BP13K820 THRU 410-EXIT                           
              MOVE K820-NUM-NRIC        TO F870-NUM-UIN-LSE4                    
              MOVE K820-NUM-CPF         TO F870-NUM-CPF4.                       
                                                                                
           MOVE 'SS'                    TO F870-NUM-OFFICE                      
                                           F870-NUM-ORIGINATOR.                 
           MOVE 'P13C26A'               TO F870-NUM-USERID.                     
                                                                                
           IF K800-NUM-LOAN-TAG = 'P' OR 'H'                                    
              MOVE 'P' TO F870-NUM-LOAN-SCHEME                                  
           ELSE                                                                 
              IF K800-NUM-LOAN-TAG = 'B' OR 'N'                                 
                 MOVE 'M' TO F870-NUM-LOAN-SCHEME                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           WRITE AB02F870-REC.                                                  
                                                                                
           ADD 1     TO  WS-F870-WRITE.                                         
                                                                                
           MOVE WS-NUM-REGN    TO       K68A-NUM-REGN                           
           PERFORM 420-READ-BP13K68A THRU 420-EXIT                              
                                                                                
           MOVE SPACES TO BP13K68A-REC                                          
           INITIALIZE BP13K68A-REC                                              
                                                                                
           IF K68A-FOUND = 'N'                                                  
                                                                                
              MOVE WS-NUM-REGN    TO     K68A-NUM-REGN                          
              WRITE BP13K68A-REC                                                
                                                                                
              IF K68A-STATUS = 00                                               
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE  K68A-STATUS TO RETURN-CODE                               
                 DISPLAY 'ERROR WRITING BP13K68A FILE: ' K68A-STATUS            
                 DISPLAY 'K68A-NUM-REGN = ' K68A-NUM-REGN                       
                 GO TO 999-CLOSE-FILES                                          
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       410-READ-BP13K820.                                                       
      *------------------*                                                      
           READ BP13K820.                                                       
                                                                                
           IF K820-STATUS = 00                                                  
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF K820-STATUS = 23                                               
                 DISPLAY 'RECORD NOT FOUND IN BP13K820, ' K820-KEY-FLD          
                 MOVE SPACES            TO K820-NUM-NRIC                        
                                           K820-NUM-CPF                         
              ELSE                                                              
                 MOVE K820-STATUS       TO RETURN-CODE                          
                 DISPLAY 'ERROR READING BP13K820, ' K820-KEY-FLD                
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       420-READ-BP13K68A.                                                       
      *------------------*                                                      
           READ BP13K68A.                                                       
                                                                                
           IF K68A-STATUS = 00                                                  
              MOVE   'Y'    TO   K68A-FOUND                                     
           ELSE                                                                 
              IF K68A-STATUS = 23                                               
                 MOVE   'N'    TO   K68A-FOUND                                  
              ELSE                                                              
                 MOVE K68A-STATUS       TO RETURN-CODE                          
                 DISPLAY 'ERROR READING BP13K68A, ' K68A-NUM-REGN               
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *==============================================================*          
       3100-BM06K110-REGNO.                                                     
      *==============================================================*          
                                                                                
           READ BM06K110 NEXT                                                   
           AT END                                                               
                MOVE 'Y' TO WS-EOF-BM06K110                                     
                GO TO 3100-EXIT.                                                
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
                IF WS-K110-KEY-TEMP  = K110-AIX1                                
                   IF (K110-REGN-NO(1:2) =  '##') OR                            
                      (K110-REGN-NO = SPACES OR LOW-VALUES)                     
                      CONTINUE                                                  
                   ELSE                                                         
                      MOVE K110-REGN-NO   TO   WS-NUM-REGN                      
                      PERFORM 400-WRITE-OUTPUT    THRU  400-EXIT                
                   END-IF                                                       
                ELSE                                                            
                   MOVE 'Y' TO WS-EOF-BM06K110                                  
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
               DISPLAY 'BM06K110 RECORD NOT FOUND: '                            
               MOVE 'Y'  TO WS-EOF-BM06K110                                     
           WHEN OTHER                                                           
               DISPLAY 'BM06K110 READ FILE ERROR : ' WS-K110-STATUS             
               MOVE WS-K110-STATUS       TO RETURN-CODE                         
               GO TO 999-CLOSE-FILES                                            
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       999-CLOSE-FILES.                                                         
      *--------------*                                                          
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C26A ================*'.              
           DISPLAY '    NO. OF F310 RECORDS READ     = ' WS-F310-READ.          
           DISPLAY '    NO. OF F870 RECORDS WRITTEN  = ' WS-F870-WRITE.         
           DISPLAY '*==========================================*'.              
                                                                                
           CLOSE BP13F310                                                       
                 BP13K800                                                       
                 BP13K68A                                                       
                 BP13K820                                                       
                 BM06K110                                                       
                 AB02F870.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF K68A-STATUS    NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K68A ERROR ' K68A-STATUS                     
              MOVE     K68A-STATUS         TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF K800-STATUS    NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' K800-STATUS                     
              MOVE     K800-STATUS         TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF K820-STATUS    NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K820 ERROR ' K820-STATUS                     
              MOVE     K820-STATUS         TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
