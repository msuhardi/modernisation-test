      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C253.                                                 
       AUTHOR.        JAI.                                                      
       DATE-WRITTEN.  13/01/95.                                                 
                                                                                
      * ======================================================== *              
      *                SYSTEM OF COMMITMENT (BP13)               *              
      * ======================================================== *              
      *    OBJECTIVE  :                                          *              
      *        THIS PROGRAM READS BM06F110 SEQUENTIALLY AND      *              
      *        GETS ESTATE NAME, NEI, CONT, BLK, STREET-NAME,    *              
      *        NT-CODE AND ESTATE-CODE TO UPDATE BP13K280.       *              
      *    INPUT FILES : BM06F110. (SORTED SEQ FILE USED)        *              
      *    OUTPUT FILE : BP13K280.                               *              
      *----------------------------------------------------------*              
      * CHG-NO   BY   DATE    DETAILS                            *              
      * -------- ---  ------  -------                            *              
      * C950184  LMS  951214  REVISE UPDATING OF ALLOCATION TAGS *              
      * BP130180 SSS  960907  INCLUDE IA COMMENTS.               *              
      * BP130418 SIA  980303  CHGE BM06F211 TO BM06F110 AND      *              
      *                       CHGE BM06K210 TO BM06K100          *              
      * BP130883 RAS  040800  TO CHANGE BP13C904 TO BP13C913     *              
      * BP132631 GAN1 050428  ADD NEW FIELD K280-CDE-PROTECTED   *              
      * ======================================================== *              
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F110 ASSIGN TO BM06F110.                                  
                                                                                
           SELECT BP13K280 ASSIGN TO BP13K280                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K280-KEY-FLD                                  
                  FILE STATUS  IS WS-K280-STATUS.                               
                                                                                
           SELECT BM06K100 ASSIGN TO BM06K100                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K100-KEY-FLD                                  
                  FILE STATUS  IS WS-K100-STATUS.                               
                                                                                
           SELECT BP13K060 ASSIGN TO BP13K060                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K060-KEY-FLD                                  
                  FILE STATUS  IS WS-K060-STATUS.                               
                                                                                
           SELECT BM06K510 ASSIGN TO BM06K510                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K510-ESTCDE                                   
                  FILE STATUS  IS WS-K510-STATUS.                               
                                                                                
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
                                                                                
       FD   BM06F110                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BM06F110.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BM06K510                                                            
            RECORD CONTAINS 60 CHARACTERS.                                      
       COPY BM06K510.                                                           
                                                                                
       FD   BP13K280                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K280.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS  25 CHARACTERS.                                     
       COPY BP13K060.                                                           
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-K060-STATUS                 PIC 9(02) VALUE ZEROS.                
       01  WS-K280-STATUS                 PIC 9(02) VALUE ZEROS.                
       01  WS-K510-STATUS                 PIC 9(02) VALUE ZEROS.                
       01  WS-K100-STATUS                 PIC 9(02) VALUE ZEROS.                
       01  WS-F110-READ                   PIC 9(08) VALUE ZEROS.                
       01  WS-K280-REWRITE                PIC 9(08) VALUE ZEROS.                
       01  WS-K280-WRITE                  PIC 9(08) VALUE ZEROS.                
       01  EOF-F110                       PIC X(01) VALUE 'N'.                  
       01  WS-SQL-CODE                    PIC 9(04) VALUE ZEROS.                
       01  WS-1ST-SALE-DATE               PIC X(01) VALUE SPACES.               
                                                                                
       01  WS-DATE                        PIC X(8)  VALUE SPACES.               
                                                                                
      *============================================================*            
      *              LINKAGE FOR SUBPROGRAM 'BP13C913'             *            
      *============================================================*            
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-SCH            PIC X(04).                            
            05 WS-LINK-NUM-ACC            PIC X(04).                            
            05 WS-LINK-NUM-CHK            PIC X(01).                            
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *----------------*                                                        
       000-MAIN-ROUTINE.                                                        
      *----------------*                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F110      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-F110   THRU 300-EXIT                             
             UNTIL EOF-F110 = 'Y'.                                              
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       100-INITIALIZATION.                                                      
      *-------------------*                                                     
           OPEN INPUT  BM06F110                                                 
                       BM06K510                                                 
                       BM06K100                                                 
                       BP13K060                                                 
                I-O    BP13K280.                                                
                                                                                
           IF WS-K280-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K280  OPEN ERROR ' WS-K280-STATUS               
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K060  OPEN ERROR ' WS-K060-STATUS               
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           IF WS-K510-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BM06K510  OPEN ERROR ' WS-K510-STATUS               
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BM06K100  OPEN ERROR ' WS-K100-STATUS               
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
           MOVE 'N'      TO EOF-F110.                                           
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------*                                                           
       200-READ-F110.                                                           
      *-------------*                                                           
           READ BM06F110 AT END                                                 
                MOVE 'Y'        TO EOF-F110                                     
                GO  TO 200-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD   1               TO  WS-F110-READ.                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *----------------*                                                        
       300-PROCESS-F110.                                                        
      *----------------*                                                        
           MOVE 'N' TO WS-1ST-SALE-DATE.                                        
                                                                                
           IF F110-SCH-ACC-NO  >  SPACES                                        
                                                                                
              PERFORM 350-CHECK-BM06K100     THRU 350-EXIT                      
              IF WS-1ST-SALE-DATE = 'Y'                                         
                 PERFORM 400-CALL-BP13C913   THRU 400-EXIT                      
                 PERFORM 500-WRITE-BP13K280  THRU 500-EXIT                      
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F110             THRU 200-EXIT.                     
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       350-CHECK-BM06K100.                                                      
      *-------------------*                                                     
           MOVE SPACES             TO K100-KEY-FLD.                             
           MOVE F110-ESTATE        TO K100-ESTATE.                              
           MOVE F110-NEIGHBOURHOOD TO K100-NEIGHBOURHOOD.                       
           MOVE F110-CONTRACT-NO   TO K100-CONTRACT-NO.                         
           MOVE F110-BLK-NO        TO K100-BLK-NO.                              
                                                                                
           READ BM06K100.                                                       
                                                                                
           IF WS-K100-STATUS = 00                                               
                                                                                
              IF K100-DTE-1ST-SALE NOT NUMERIC                                  
                 MOVE ZEROES TO K100-DTE-1ST-SALE                               
              END-IF                                                            
                                                                                
              IF K100-DTE-1ST-SALE = ZEROES OR SPACES OR LOW-VALUES             
                 MOVE 'N' TO WS-1ST-SALE-DATE                                   
              ELSE                                                              
                 MOVE 'Y' TO WS-1ST-SALE-DATE                                   
              END-IF                                                            
                                                                                
           ELSE                                                                 
              DISPLAY 'RECORD NOT FOUND IN BM06K100, KEY : '                    
                       K100-KEY-FLD                                             
           END-IF.                                                              
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       400-CALL-BP13C913.                                                       
      *-----------------*                                                       
           MOVE SPACES           TO BP13COMM8-REC.                              
           INITIALIZE               BP13COMM8-REC.                              
                                                                                
           MOVE F110-SCHEME      TO WS-LINK-NUM-SCH.                            
           MOVE F110-ACC         TO WS-LINK-NUM-ACC.                            
           MOVE F110-CHK         TO WS-LINK-NUM-CHK.                            
                                                                                
           CALL 'BP13C913' USING                                                
                 WS-LINK-REC, BP13COMM8-REC                                     
           END-CALL.                                                            
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE COMM8-CDE-SYSERR  TO WS-SQL-CODE                             
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PIDB TABLE' WS-SQL-CODE          
              ELSE                                                              
                 DISPLAY 'ERROR READING PIDB TABLE' WS-SQL-CODE                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       500-WRITE-BP13K280.                                                      
      *------------------*                                                      
           MOVE SPACES              TO BP13K280-REC.                            
           MOVE F110-SCH-ACC-NO     TO K280-NUM-SCH-ACC.                        
                                                                                
           READ BP13K280.                                                       
                                                                                
           EVALUATE WS-K280-STATUS                                              
           WHEN 00                                                              
                PERFORM 600-MOVE-DATA-K280  THRU 600-EXIT                       
                                                                                
                REWRITE BP13K280-REC                                            
                                                                                
                EVALUATE WS-K280-STATUS                                         
                WHEN 00                                                         
                     ADD  1                  TO WS-K280-REWRITE                 
                WHEN OTHER                                                      
                     DISPLAY 'ERROR IN RE-WRITING K280 ' WS-K280-STATUS         
                     PERFORM 999-CLOSE-FILES THRU 999-EXIT                      
                END-EVALUATE                                                    
                                                                                
           WHEN 23                                                              
                PERFORM 600-MOVE-DATA-K280  THRU 600-EXIT                       
                                                                                
                WRITE BP13K280-REC                                              
                                                                                
                EVALUATE WS-K280-STATUS                                         
                WHEN 00                                                         
                     ADD  1                  TO WS-K280-WRITE                   
                WHEN OTHER                                                      
                     DISPLAY 'ERROR IN WRITING K280 ' WS-K280-STATUS            
                     PERFORM 999-CLOSE-FILES THRU 999-EXIT                      
                END-EVALUATE                                                    
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING K280 FILE  ' WS-K280-STATUS              
                MOVE WS-K280-STATUS  TO RETURN-CODE                             
                DISPLAY 'K280-NUM-SCH-ACC = ' K280-NUM-SCH-ACC                  
                PERFORM 999-CLOSE-FILES THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       600-MOVE-DATA-K280.                                                      
      *------------------*                                                      
           MOVE SPACES                TO BP13K280-REC.                          
           MOVE F110-SCH-ACC-NO       TO K280-NUM-SCH-ACC.                      
           MOVE F110-DTE-ALLOCN       TO K280-DTE-ALLOC.                        
           MOVE F110-ALLOCN-TYPE      TO K280-NUM-ALLOC-TYPE.                   
           MOVE F110-ALLOCN-TAG       TO K280-NUM-ALLOC-TAG.                    
           MOVE F110-FLAT-STATUS      TO K280-NUM-FLAT-STATUS.                  
           MOVE F110-REPOSSESSED-CODE TO K280-NUM-REPOSSESSED-CODE.             
           MOVE F110-CDE-PROTECTED    TO K280-CDE-PROTECTED.                    
           MOVE WS-DATE               TO K280-DTE-UPDATE.                       
           MOVE 'P13C253'             TO K280-NUM-USERID.                       
           MOVE COMM8-NUM-BLK         TO K280-NUM-BLK.                          
           MOVE COMM8-NUM-LEVEL       TO K280-NUM-HSELVL.                       
           MOVE COMM8-NUM-UNIT-MAIN   TO K280-NUM-HSEMAIN.                      
           MOVE COMM8-NUM-UNIT-SUB    TO K280-NUM-HSESUB.                       
           MOVE COMM8-NME-STREET      TO K280-NME-STREET.                       
           MOVE SPACES                TO K280-NUM-FLAT-TYPE.                    
                                                                                
           EVALUATE COMM8-CDE-TYP-ACTLUSE                                       
           WHEN '01'                                                            
                MOVE '1 '           TO K280-NUM-FLAT-TYPE                       
           WHEN '02'                                                            
                MOVE '2 '           TO K280-NUM-FLAT-TYPE                       
           WHEN '03'                                                            
                MOVE '3 '           TO K280-NUM-FLAT-TYPE                       
           WHEN '04'                                                            
                MOVE '4 '           TO K280-NUM-FLAT-TYPE                       
           WHEN '05'                                                            
                MOVE '5 '           TO K280-NUM-FLAT-TYPE                       
           WHEN '06'                                                            
                MOVE 'E '           TO K280-NUM-FLAT-TYPE                       
           WHEN '07'                                                            
                MOVE 'H '           TO K280-NUM-FLAT-TYPE                       
           END-EVALUATE.                                                        
                                                                                
           MOVE F110-ESTATE         TO K510-ESTCDE.                             
                                                                                
           READ BM06K510.                                                       
                                                                                
           EVALUATE WS-K510-STATUS                                              
           WHEN  00                                                             
                 MOVE K510-ALPHA-NTCDE   TO K280-NUM-NEWTOWN                    
                                            K060-CODE                           
                 MOVE 02                 TO K060-SERIAL-NO                      
                                                                                
                 READ BP13K060                                                  
                                                                                
                 EVALUATE WS-K060-STATUS                                        
                 WHEN  00                                                       
                       MOVE K060-DESC    TO K280-NME-NEWTOWN                    
                 WHEN  23                                                       
                       DISPLAY 'RECORD NOT IN K060 ' K060-KEY-FLD               
                 WHEN  OTHER                                                    
                       DISPLAY 'ERROR IN READING K060 ' WS-K060-STATUS          
                       PERFORM 999-CLOSE-FILES  THRU 999-EXIT                   
                 END-EVALUATE                                                   
                                                                                
           WHEN  23                                                             
                 DISPLAY 'RECORD NOT IN K510 ' K510-ESTCDE                      
           WHEN  OTHER                                                          
                 DISPLAY 'ERROR IN READING K510 ' WS-K510-STATUS                
                 PERFORM 999-CLOSE-FILES  THRU 999-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------*                                                         
       999-CLOSE-FILES.                                                         
      *---------------*                                                         
           CLOSE BM06F110                                                       
                 BM06K510                                                       
                 BP13K060                                                       
                 BP13K280.                                                      
                                                                                
           IF WS-K280-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K280  CLOSE ERROR ' WS-K280-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K060  CLOSE ERROR ' WS-K060-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K510-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BM06K510  CLOSE ERROR ' WS-K510-STATUS              
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========END OF BP13C253============*'.                  
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF F110 RECORDS READ      = ' WS-F110-READ.             
           DISPLAY 'NO. OF K280 RECORDS WRITTEN   = ' WS-K280-WRITE.            
           DISPLAY 'NO. OF K280 RECORDS REWRITTEN = ' WS-K280-REWRITE           
           DISPLAY SPACES.                                                      
           DISPLAY '*======================================*'.                  
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
