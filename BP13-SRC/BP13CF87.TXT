       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF87.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  03/06/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    :  SEPARATE COMPLETE AND INCOMPLETE CASES     *            
      *                                                            *            
      * INPUT FILES  :  BE01F500 - MHA INTERFACE FILE              *            
      *                 BE01F515 - OBTAIN FROM MHA VIA IDNET       *            
      * OUTPUT FILES :  P13LF87C - REGN OF COMPLETE CASES          *            
      *                 P13LF87I - REGN OF INCOMPLETE CASES        *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP134922  SMR2  03062013  NEW PROGRAM                      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BE01F500 ASSIGN TO BE01F500.                                  
           SELECT BE01F515 ASSIGN TO BE01F515.                                  
           SELECT P13LF87C ASSIGN TO P13LF87C.                                  
           SELECT P13LF87I ASSIGN TO P13LF87I.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BE01F500                                                            
            RECORD CONTAINS  50 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       COPY BE01F500.                                                           
                                                                                
       FD   BE01F515                                                            
            RECORD CONTAINS 580 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       COPY BE01F515.                                                           
                                                                                
       FD   P13LF87C                                                            
            RECORD CONTAINS  20 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       01   P13LF87C-REC           PIC X(20).                                   
                                                                                
       FD   P13LF87I                                                            
            RECORD CONTAINS  20 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       01   P13LF87I-REC           PIC X(20).                                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05  WS-F500-READ        PIC 9(6)    VALUE ZEROES.                    
           05  WS-F515-READ        PIC 9(6)    VALUE ZEROES.                    
           05  WS-LF87C-WRITE      PIC 9(6)    VALUE ZEROES.                    
           05  WS-LF87I-WRITE      PIC 9(6)    VALUE ZEROES.                    
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F500-COMP        PIC X(1)    VALUE 'Y'.                       
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-PREV-REGN        PIC X(8)    VALUE SPACES.                    
           05  WS-F500-KEY-FLD.                                                 
               10  WS-F500-REGN    PIC X(8)    VALUE SPACES.                    
               10  WS-F500-NRIC    PIC X(9)    VALUE SPACES.                    
           05  WS-F515-KEY-FLD.                                                 
               10  WS-F515-REGN    PIC X(8)    VALUE SPACES.                    
               10  WS-F515-NRIC    PIC X(9)    VALUE SPACES.                    
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-MAIN-ROUTINE        THRU 2000-EXIT.                     
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT  BE01F500                                                 
                       BE01F515                                                 
                OUTPUT P13LF87C                                                 
                       P13LF87I.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 3000-READ-BE01F500       THRU 3000-EXIT.                     
           PERFORM 4000-READ-BE01F515       THRU 4000-EXIT.                     
           PERFORM 5000-PROCESS-FILES       THRU 5000-EXIT                      
             UNTIL WS-F500-KEY-FLD = HIGH-VALUES.                               
                                                                                
           IF WS-PREV-REGN NOT = WS-F500-REGN AND SPACES AND LOW-VALUES         
              IF WS-F500-COMP = 'Y'                                             
                 WRITE P13LF87C-REC         FROM WS-PREV-REGN                   
                 ADD 1                      TO WS-LF87C-WRITE                   
              ELSE                                                              
                 WRITE P13LF87I-REC         FROM WS-PREV-REGN                   
                 ADD 1                      TO WS-LF87I-WRITE                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BE01F500.                                                      
      *-------------------------------------------------------------            
           READ BE01F500 AT END                                                 
                MOVE HIGH-VALUES            TO WS-F500-KEY-FLD                  
                GO TO 3000-EXIT.                                                
                                                                                
           IF BE01F500-REF-NO(1:8) NOT = WS-F500-REGN                           
              ADD 1                         TO WS-F500-READ                     
           END-IF.                                                              
                                                                                
           MOVE BE01F500-REF-NO(1:8)        TO WS-F500-REGN.                    
           MOVE BE01F500-IDNO               TO WS-F500-NRIC.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-READ-BE01F515.                                                      
      *-------------------------------------------------------------            
           READ BE01F515 AT END                                                 
                MOVE HIGH-VALUES            TO WS-F515-KEY-FLD                  
                GO TO 4000-EXIT.                                                
                                                                                
           IF BE01F515-REF-NO(1:8) NOT = WS-F515-REGN                           
              ADD 1                         TO WS-F515-READ                     
           END-IF.                                                              
                                                                                
           MOVE BE01F515-REF-NO(1:8)        TO WS-F515-REGN.                    
           MOVE BE01F515-IDNO               TO WS-F515-NRIC.                    
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                      TO P13LF87C-REC                     
                                               P13LF87I-REC.                    
                                                                                
           IF WS-PREV-REGN NOT = WS-F500-REGN AND SPACES AND LOW-VALUES         
              IF WS-F500-COMP = 'Y'                                             
                 WRITE P13LF87C-REC         FROM WS-PREV-REGN                   
                 ADD 1                      TO WS-LF87C-WRITE                   
              ELSE                                                              
                 WRITE P13LF87I-REC         FROM WS-PREV-REGN                   
                 ADD 1                      TO WS-LF87I-WRITE                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-PREV-REGN NOT = WS-F500-REGN                                   
              MOVE WS-F500-REGN             TO WS-PREV-REGN                     
              MOVE 'Y'                      TO WS-F500-COMP                     
           END-IF.                                                              
                                                                                
           IF WS-F500-KEY-FLD = WS-F515-KEY-FLD                                 
              PERFORM 3000-READ-BE01F500    THRU 3000-EXIT                      
           ELSE                                                                 
              IF WS-F500-KEY-FLD < WS-F515-KEY-FLD                              
                 PERFORM 3000-READ-BE01F500 THRU 3000-EXIT                      
                 MOVE 'N'                   TO WS-F500-COMP                     
              ELSE                                                              
                 PERFORM 4000-READ-BE01F515 THRU 4000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY '*============== BP13CF87 ==============*'.                  
           DISPLAY '  NO. OF F500 REGN READ      =  ' WS-F500-READ.             
           DISPLAY '  NO. OF F515 REGN READ      =  ' WS-F515-READ.             
           DISPLAY '  NO. OF LF87C REGN WRITTEN  =  ' WS-LF87C-WRITE.           
           DISPLAY '  NO. OF LF87I REGN WRITTEN  =  ' WS-LF87I-WRITE.           
           DISPLAY '*======================================*'.                  
                                                                                
           CLOSE BE01F500                                                       
                 BE01F515                                                       
                 P13LF87C                                                       
                 P13LF87I.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
