       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH20.                                                 
      *AUTHOR.        PAULO CAMIA LEGASPI.                                      
      *DATE-WRITTEN.  20/04/2009.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: TO MATCH BP13F800 AND BP13FH17 AND CHECK IF    *            
      *             BP13F800 QUEUE NO. IS IN THE RANGE OF BP13FH17 *            
      *             QUEUE NO.                                                   
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133601  PCL3  20042009  NEW PROGRAM                      *            
      * BP134303  PCL3  18082011  TO CHECK QUEUE-NO IF ALPHANUMERIC*            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *-------------------------------------------------------------            
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13FH17  ASSIGN        TO BP13FH17.                          
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
           SELECT P13F800A  ASSIGN        TO P13F800A.                          
           SELECT BP13LH20  ASSIGN        TO BP13LH20.                          
           SELECT BP13K595  ASSIGN        TO BP13K595                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K595-KEY-FLD                       
                            FILE STATUS   IS WS-K595-STATUS.                    
                                                                                
      *-------------------------------------------------------------            
       DATA DIVISION.                                                           
      *-------------------------------------------------------------            
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13FH17                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FH17.                                                           
                                                                                
       FD   BP13K595                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K595.                                                           
                                                                                
       FD   P13F800A                                                            
            RECORDING MODE IS F.                                                
       01 P13F800A-REC                 PIC X(2000).                             
                                                                                
       FD   BP13LH20                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   BP13LH20-REC               PIC X(132).                              
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-CURRENT-DATE      PIC X(8)  VALUE SPACES.                       
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-FH17-READ         PIC 9(08) VALUE ZEROES.                       
          05 WS-F800-READ         PIC 9(08) VALUE ZEROES.                       
          05 WS-K595-NOTFD        PIC 9(08) VALUE ZEROES.                       
          05 WS-K595-FND          PIC 9(08) VALUE ZEROES.                       
          05 WS-MATCHED           PIC 9(08) VALUE ZEROES.                       
          05 WS-FH17-NOTFD        PIC 9(08) VALUE ZEROES.                       
          05 WS-F800-NOTFD        PIC 9(08) VALUE ZEROES.                       
          05 WS-F800-WRITE        PIC 9(08) VALUE ZEROES.                       
          05 WS-F800-PRINT        PIC 9(08) VALUE ZEROES.                       
          05 WS-SNO               PIC 9(08) VALUE ZEROES.                       
          05 WS-LINE-CTR          PIC 9(02) VALUE ZEROES.                       
          05 WS-PAGE-CTR          PIC 9(04) VALUE ZEROES.                       
          05 WS-MAX-LINE          PIC 9(02) VALUE 55.                           
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 WS-K595-STATUS       PIC 9(02) VALUE ZEROES.                       
                                                                                
       01 WS-SWITCHES.                                                          
          05 WS-FH17-EOF                 PIC X(1) VALUE 'N'.                    
          05 WS-F800-EOF                 PIC X(1) VALUE 'N'.                    
          05 WS-K595-EOF                 PIC X(1) VALUE 'N'.                    
                                                                                
       01 WS-FH17-KEY.                                                          
          10 WS-FH17-MAIN-KEY.                                                  
             15 WS-FH17-NUM-SELECTION         PIC X(3).                         
             15 WS-FH17-DTE-BALLOT            PIC X(6).                         
             15 WS-FH17-NUM-NT                PIC X(3).                         
             15 WS-FH17-NUM-FT                PIC X(2).                         
             15 WS-FH17-NUM-ETHNIC            PIC X(1).                         
                                                                                
       01 WS-FH17-QUEUE.                                                        
          10 WS-QUEUE-START                   PIC X(5).                         
          10 WS-QUEUE-END                     PIC X(5).                         
                                                                                
       01 WS-F800-KEY.                                                          
          10 WS-F800-MAIN-KEY.                                                  
             15 WS-F800-ALLO-CAT              PIC X(3).                         
             15 WS-F800-DTE-BALLOT            PIC X(6).                         
             15 WS-F800-NUM-NEW-TOWN          PIC X(3).                         
             15 WS-F800-NUM-FLAT-TYPE         PIC X(2).                         
             15 WS-F800-NUM-CAT               PIC X(1).                         
          10 WS-F800-QUEUE                    PIC X(5).                         
                                                                                
       01  CH20-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13LH20'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  CH20-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  CH20-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  CH20-HEADER02.                                                       
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(48)     VALUE                          
              'CANCELLATION CASE OUTSIDE THE RANGE OF QUEUE-NO '.               
           05  FILLER              PIC X(11)     VALUE                          
              'FOR THE DAY'.                                                    
                                                                                
       01  CH20-HEADER03.                                                       
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE 'SOC MASTER'.            
           05  FILLER              PIC X(05)     VALUE 'MODE '.                 
           05  FILLER              PIC X(04)     VALUE 'DATE'.                  
           05  FILLER              PIC X(14)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'DATE-BK'.               
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'CAN'.                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'DATE'.                  
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'QUEUE'.                 
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'ALLO'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'START'.                 
           05  FILLER              PIC X(02)     VALUE SPACE.                   
           05  FILLER              PIC X(05)     VALUE 'END'.                   
           05  FILLER              PIC X(02)     VALUE SPACE.                   
                                                                                
       01  CH20-HEADER04.                                                       
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE '  S/N'.                 
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'REGN NO '.              
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'SALE'.                  
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'BALLOT'.                
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'NT '.                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE 'FT'.                    
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'CAT'.                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'APPT   '.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'RSN'.                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'CANCELED'.              
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'NO.  '.                 
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'TAG'.                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'QUEUE'.                 
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'QUEUE'.                 
           05  FILLER              PIC X(02)     VALUE SPACES.                  
                                                                                
       01  CH20-DETAIL01.                                                       
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  CH20-SN             PIC ZZZZ9        VALUE ZEROES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  CH20-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER              PIC X(03)        VALUE SPACES.               
           05  CH20-ALLOC-CAT      PIC X(04)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  CH20-DTE-BALLOT     PIC X(06)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  CH20-NT             PIC X(03)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  CH20-FT             PIC X(02)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  CH20-CAT            PIC X(03)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  CH20-BK-APPT        PIC X(10)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  CH20-REASON         PIC X(03)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  CH20-DATE-CANCEL    PIC X(10)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  CH20-QUEUE-NO       PIC X(05)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  CH20-ALLOC-F800     PIC X(04)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  CH20-REFER-START-Q  PIC X(05)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  CH20-REFER-END-Q    PIC X(05)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
           PERFORM 3000-READ-BP13FH17  THRU 3000-EXIT.                          
           PERFORM 4000-PROCESS-DATA   THRU 4000-EXIT                           
              UNTIL WS-F800-EOF = 'Y'                                           
               AND  WS-FH17-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13FH17                                                 
                       BP13F800                                                 
                       BP13K595                                                 
               OUTPUT  P13F800A                                                 
                       BP13LH20.                                                
                                                                                
           IF WS-K595-STATUS  NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13K595 : ' WS-K595-STATUS              
              MOVE     WS-K595-STATUS       TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE        TO WS-CURRENT-DATE.                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800 AT END                                                 
             MOVE 'Y'                TO WS-F800-EOF                             
             MOVE HIGH-VALUES        TO WS-F800-KEY                             
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD 1                     TO WS-F800-READ.                           
                                                                                
           MOVE F800-NUM-ALLO-CAT    TO WS-F800-ALLO-CAT.                       
           MOVE F800-DTE-BALLOT      TO WS-F800-DTE-BALLOT.                     
           MOVE F800-NUM-NEW-TOWN    TO WS-F800-NUM-NEW-TOWN.                   
                                                                                
           IF F800-NUM-FLAT-TYPE   = '1A'                                       
               MOVE '1 '                 TO WS-F800-NUM-FLAT-TYPE               
           ELSE                                                                 
               MOVE F800-NUM-FLAT-TYPE   TO WS-F800-NUM-FLAT-TYPE               
           END-IF.                                                              
                                                                                
           MOVE F800-NUM-CAT         TO WS-F800-NUM-CAT.                        
                                                                                
           IF F800-NUM-NT-FT-QUEUE(2:1) NOT NUMERIC                             
              MOVE ZEROS             TO F800-NUM-NT-FT-QUEUE(2:1)               
           END-IF.                                                              
                                                                                
           MOVE ZEROS                TO F800-NUM-NT-FT-QUEUE(1:1).              
           MOVE F800-NUM-NT-FT-QUEUE TO WS-F800-QUEUE.                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-READ-BP13FH17.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FH17 AT END                                                 
             MOVE 'Y'                TO WS-FH17-EOF                             
             MOVE HIGH-VALUES        TO WS-FH17-KEY                             
             GO TO 3000-EXIT.                                                   
                                                                                
           ADD 1                     TO WS-FH17-READ.                           
                                                                                
           MOVE FH17-NUM-SELECTION   TO WS-FH17-NUM-SELECTION.                  
           MOVE FH17-DTE-BALLOT      TO WS-FH17-DTE-BALLOT.                     
           MOVE FH17-NUM-NEW-TOWN    TO WS-FH17-NUM-NT.                         
                                                                                
           IF FH17-NUM-FT = '1A'                                                
              MOVE '1 '              TO WS-FH17-NUM-FT                          
           ELSE                                                                 
              MOVE FH17-NUM-FT       TO WS-FH17-NUM-FT                          
           END-IF.                                                              
                                                                                
           MOVE FH17-NUM-ETHNIC      TO WS-FH17-NUM-ETHNIC.                     
                                                                                
           MOVE FH17-NUM-QUEUE-START TO WS-QUEUE-START                          
           MOVE FH17-NUM-QUEUE-END   TO WS-QUEUE-END.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF WS-F800-MAIN-KEY = WS-FH17-MAIN-KEY                               
              ADD 1                                   TO WS-MATCHED             
              PERFORM 5000-CHECK-QUEUE-NO           THRU 5000-EXIT              
              PERFORM 2000-READ-BP13F800            THRU 2000-EXIT              
           ELSE                                                                 
           IF WS-F800-MAIN-KEY <  WS-FH17-MAIN-KEY                              
              ADD 1                              TO WS-FH17-NOTFD               
              PERFORM 2000-READ-BP13F800       THRU 2000-EXIT                   
           ELSE                                                                 
              ADD 1                              TO WS-F800-NOTFD               
              PERFORM 3000-READ-BP13FH17       THRU 3000-EXIT                   
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-CHECK-QUEUE-NO.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF WS-F800-QUEUE >= WS-QUEUE-START AND                               
              WS-F800-QUEUE <= WS-QUEUE-END                                     
              PERFORM 6000-WRITE-P13F800A             THRU 6000-EXIT            
           ELSE                                                                 
              PERFORM 6100-CHECK-BOOK-STATUS          THRU 6100-EXIT            
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-WRITE-P13F800A.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                              TO P13F800A-REC.            
           INITIALIZE                                  P13F800A-REC.            
                                                                                
           WRITE P13F800A-REC                     FROM BP13F800-MASTER.         
           ADD 1                                    TO WS-F800-WRITE.           
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6100-CHECK-BOOK-STATUS.                                                  
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                         TO  K595-KEY-FLD.                
           MOVE F800-NUM-REGN                  TO  K595-NUM-REGN-REF.           
                                                                                
           MOVE 'N'                            TO  WS-K595-EOF.                 
                                                                                
           START BP13K595 KEY >= K595-KEY-FLD.                                  
           EVALUATE WS-K595-STATUS                                              
           WHEN 00                                                              
                PERFORM 6200-READNEXT-BP13K595 THRU 6200-EXIT                   
                   UNTIL K595-NUM-REGN-REF NOT = F800-NUM-REGN                  
                      OR WS-K595-EOF = 'Y'                                      
           WHEN 10                                                              
           WHEN 23                                                              
                ADD  1       TO WS-K595-NOTFD                                   
                DISPLAY 'BP13K595 RECORD NOT FOUND  = ' K595-KEY-FLD            
           WHEN OTHER                                                           
                DISPLAY 'ERROR START BP13K595, STATUS  '                        
                        WS-K595-STATUS                                          
                        ',  REGN ' K595-NUM-REGN-REF                            
                MOVE WS-K595-STATUS TO RETURN-CODE                              
                PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6200-READNEXT-BP13K595.                                                  
      *-------------------------------------------------------------            
                                                                                
           READ BP13K595 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-K595-EOF.                                 
                                                                                
           EVALUATE WS-K595-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
                IF K595-NUM-REGN-REF = F800-NUM-REGN                            
                   IF (K595-CDE-REQUEST-STATUS NOT = SPACES AND                 
                                                     LOW-VALUES) AND            
                      (K595-NUM-NT-FT-QUEUE NOT = SPACES AND                    
                                                     LOW-VALUES) AND            
                      (K595-NUM-BOOK-STATUS = SPACES OR                         
                                                     LOW-VALUES)                
                      PERFORM 7000-PRINT-REPORT          THRU 7000-EXIT         
                      ADD  1       TO WS-K595-FND                               
                      MOVE 'Y'     TO WS-K595-EOF                               
                   ELSE                                                         
                      ADD  1       TO WS-K595-NOTFD                             
                   END-IF                                                       
                ELSE                                                            
                   MOVE 'Y'        TO WS-K595-EOF                               
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y'         TO WS-K595-EOF                                 
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K595, STATUS '                       
                        WS-K595-STATUS ',  REGN ' K595-NUM-REGN-REF             
                MOVE WS-K595-STATUS             TO RETURN-CODE                  
                PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       7000-PRINT-REPORT.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 7100-MOVE-FIELDS                THRU 7100-EXIT.              
                                                                                
           ADD 1                                     TO WS-F800-PRINT.          
           ADD 1                                     TO WS-SNO.                 
           MOVE WS-SNO                               TO CH20-SN.                
           PERFORM  7200-CHECK-WRITE-HEADER        THRU 7200-EXIT.              
                                                                                
           WRITE BP13LH20-REC                      FROM CH20-DETAIL01.          
           ADD 1                                     TO WS-LINE-CTR.            
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       7100-MOVE-FIELDS.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                              TO CH20-DETAIL01.           
                                                                                
           STRING WS-CURRENT-DATE(7:2) '/'                                      
                  WS-CURRENT-DATE(5:2) '/'                                      
                  WS-CURRENT-DATE(1:4)                                          
              DELIMITED BY SIZE INTO CH20-DATE-HDR.                             
                                                                                
           MOVE F800-NUM-REGN                       TO CH20-REGN.               
           MOVE F800-NUM-ALLO-CAT                   TO CH20-ALLOC-CAT.          
           MOVE F800-DTE-BALLOT                     TO CH20-DTE-BALLOT.         
                                                                                
           MOVE F800-NUM-NEW-TOWN                   TO CH20-NT.                 
           MOVE F800-NUM-FLAT-TYPE                  TO CH20-FT.                 
           MOVE F800-NUM-CAT                        TO CH20-CAT.                
                                                                                
           MOVE F800-NUM-NT-FT-QUEUE                TO CH20-QUEUE-NO.           
           MOVE F800-NUM-ALLOC-TAG                  TO CH20-ALLOC-F800.         
                                                                                
           IF F800-DTE-BK-APPT NOT = SPACES AND LOW-VALUES AND ZEROES           
              STRING F800-DTE-BK-APPT(7:2) '/'                                  
                     F800-DTE-BK-APPT(5:2) '/'                                  
                     F800-DTE-BK-APPT(1:4)                                      
                 DELIMITED BY SIZE INTO CH20-BK-APPT                            
           END-IF.                                                              
                                                                                
           IF F800-DTE-CANCEL NOT = SPACES AND LOW-VALUES AND ZEROES            
              STRING F800-DTE-CANCEL(7:2) '/'                                   
                     F800-DTE-CANCEL(5:2) '/'                                   
                     F800-DTE-CANCEL(1:4)                                       
                 DELIMITED BY SIZE INTO CH20-DATE-CANCEL                        
           END-IF.                                                              
                                                                                
           MOVE F800-NUM-CANCEL-REASON           TO CH20-REASON.                
           MOVE WS-QUEUE-START                   TO CH20-REFER-START-Q.         
           MOVE WS-QUEUE-END                     TO CH20-REFER-END-Q.           
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       7200-CHECK-WRITE-HEADER.                                                 
      *-------------------------------------------------------------            
                                                                                
           IF (WS-LINE-CTR  >  WS-MAX-LINE) OR (WS-SNO = 1)                     
              PERFORM 7300-PRINT-HEADER      THRU 7300-EXIT                     
           END-IF.                                                              
                                                                                
       7200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       7300-PRINT-HEADER.                                                       
      *-------------------------------------------------------------            
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO CH20-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO CH20-PAGE-HDR.                
                                                                                
           WRITE BP13LH20-REC    FROM CH20-HEADER01 AFTER PAGE.                 
           WRITE BP13LH20-REC    FROM CH20-HEADER02.                            
           WRITE BP13LH20-REC    FROM CH20-HEADER03 AFTER 2.                    
           WRITE BP13LH20-REC    FROM CH20-HEADER04.                            
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE 5  TO WS-LINE-CTR.                                              
                                                                                
       7300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13CH20 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ FROM BP13F800: ' WS-F800-READ.              
           DISPLAY 'NO OF RECS READ FROM BP13FH17: ' WS-FH17-READ.              
           DISPLAY 'NO OF RECS NOT FOUND BP13F800: ' WS-F800-NOTFD.             
           DISPLAY 'NO OF RECS NOT FOUND BP13FH17: ' WS-FH17-NOTFD.             
           DISPLAY 'NO OF RECS FOUND     BP13K595: ' WS-K595-FND.               
           DISPLAY 'NO OF RECS NOT FOUND BP13K595: ' WS-K595-NOTFD.             
           DISPLAY 'NO OF RECS WRITTEN   BP13F800: ' WS-F800-WRITE.             
           DISPLAY 'NO OF RECS PRINTED   BP13F800: ' WS-F800-PRINT.             
                                                                                
           CLOSE BP13F800                                                       
                 BP13FH17                                                       
                 BP13K595                                                       
                 P13F800A                                                       
                 BP13LH20.                                                      
                                                                                
           IF WS-K595-STATUS  NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K595 : ' WS-K595-STATUS              
              MOVE WS-K595-STATUS                TO RETURN-CODE                 
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
