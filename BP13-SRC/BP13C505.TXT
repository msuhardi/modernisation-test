       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C505.                                                 
      *AUTHOR.        JAI.                                                      
      *DATE-WRITTEN.  26/10/94.                                                 
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *                                                        *                
      *      1.  TO LINK THE JOINT BALLOT REQUEST BY SAME      *                
      *          SEQUENCE NO ( IE, THE SMALLEST REGISTRATION   *                
      *          NO AMONG THE JOINT BALLOT CASES.)             *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *      1. BP13K800                                       *                
      *      2. BP13F800                                       *                
      *                                                        *                
      *     OUTPUT FILES :                                     *                
      *      1. BP13F800                                       *                
      *                                                        *                
      *CHG-NO   BY   ON      DESCRIPTION                       *                
      *---------------------------------                       *                
      *BP130418 SIA 140798   CATER FOR YR2000                  *                
      **********************************************************                
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K800-NUM-REGN                                   
                FILE STATUS IS BP13K800-STATUS.                                 
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
      *    SELECT BP13F810 ASSIGN TO BP13F810.                                  
           SELECT BP13L505 ASSIGN TO BP13L505.                                  
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *                                                                         
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
      *                                                                         
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F800.                                                           
      *                                                                         
      *FD   BP13F810                                                            
      *     BLOCK CONTAINS 0 RECORDS                                            
      *     RECORD CONTAINS 800 CHARACTERS                                      
      *     RECORDING MODE IS F                                                 
      *     LABEL RECORD IS STANDARD.                                           
      *                                                                         
      *01   BP13F810-REC.                                                       
      *     05 FILLER                  PIC X(265).                              
      *     05 F810-NUM-CUR-REGN       PIC X(08).                               
      *     05 FILLER                  PIC X(527).                              
      *                                                                         
       FD   BP13L505                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   PRINT-REC                  PIC X(132).                              
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-ACCESS-STATUS.                                               
           05  BP13K800-STATUS         PIC 9(02) VALUE ZEROS.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC S9(4) COMP.                          
           05  WS-CNT                  PIC S9(4) COMP.                          
           05  WS-TOT-JB-PARENT        PIC S9(4) COMP.                          
           05  WS-L505-LINE-CNT        PIC S9(4) COMP.                          
           05  WS-JB-CNT               PIC 9(02) VALUE ZEROS.                   
           05  WS-SEQ-REGN             PIC X(08).                               
           05  WS-CDE-ZONE             PIC X(02).                               
           05  WS-F800-NUM-REGN        PIC X(08).                               
           05  WS-F800-JB-CNT          PIC 9(01).                               
           05  WS-CNT-PAGE             PIC 9(05) VALUE ZEROS.                   
           05  WS-CNT-F800-READ        PIC 9(08) VALUE ZEROS.                   
           05  WS-CNT-SER-NO           PIC 9(08) VALUE ZEROS.                   
           05  WS-EDIT-SER-NO          PIC ZZZZZZZ9.                            
      *    05  WS-CNT-F810-WRITE       PIC 9(08) VALUE ZEROS.                   
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-YY                   PIC 9(04).                               
           05  WS-MM                   PIC 9(02).                               
           05  WS-DD                   PIC 9(02).                               
           05 WS-TIME-HH               PIC 9(02) VALUE ZERO.                    
           05 WS-TIME-MM               PIC 9(02) VALUE ZERO.                    
           05 WS-TIME-SS               PIC 9(02) VALUE ZERO.                    
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-DD                   PIC 9(02).                               
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-MM                   PIC 9(02).                               
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-YY                   PIC 9(04).                               
                                                                                
       01  WS-SWITCHES.                                                         
           05 WS-F800-STATUS           PIC X(01) VALUE 'N'.                     
              88 WS-F800-EOF               VALUE 'Y'.                           
           05 WS-LINK-STATUS           PIC X(01) VALUE ' '.                     
              88 WS-LINK-COMPLETE          VALUE 'C'.                           
              88 WS-LINK-ERROR             VALUE 'E'.                           
              88 WS-INVALID-CNT            VALUE 'I'.                           
              88 WS-ZONE-ERROR             VALUE 'Z'.                           
      *                                                                         
       01  WS-REG-NUM-TABLE.                                                    
           05 WS-REG-CNT               PIC S9(4) COMP.                          
           05 WS-REG-JB-NUM OCCURS 1 TO 99 TIMES                                
                            DEPENDING ON  WS-REG-CNT.                           
              10 WS-REGN-NUM          PIC X(08).                                
              10 WS-JB-REG-NUM        PIC X(08).                                
              10 WS-JB-PARENT         PIC X(01).                                
              10 WS-REMARKS           PIC X(27).                                
              10 WS-K800-REC          PIC X(800).                               
                                                                                
       01  L505-PRT-HEADING-1.                                                  
           05  FILLER              PIC X(8)      VALUE 'BP13L505'.              
           05  FILLER              PIC X(4)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L505-DATE           PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE ' ('.                    
           05  L505-TME-HH         PIC X(2).                                    
           05  FILLER              PIC X         VALUE ':'.                     
           05  L505-TME-MM         PIC X(2).                                    
           05  FILLER              PIC X(2)      VALUE ') '.                    
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L505-PAGENO         PIC ZZZZ9 .                                  
           05  FILLER              PIC X(53)     VALUE SPACES.                  
                                                                                
       01  L505-PRT-HEADING-2.                                                  
           05  FILLER                     PIC X(47)  VALUE SPACES.              
           05  FILLER                     PIC X(32)  VALUE                      
               'SALES UNIT (APPLICATION TEAM)'.                                 
           05  FILLER                     PIC X(53)  VALUE SPACES.              
                                                                                
       01  L505-PRT-HEADING-3.                                                  
           05  FILLER              PIC X(43)     VALUE SPACES.                  
           05  FILLER              PIC X(52)     VALUE                          
               '       REPORT ON THE BROKEN LINKAGES                '.          
           05  FILLER              PIC X(37)     VALUE SPACES.                  
                                                                                
       01  L505-PRT-HEADING-4.                                                  
           05 FILLER               PIC X(25)  VALUE SPACES.                     
           05 FILLER               PIC X(10)  VALUE '   S / NO '.               
           05 FILLER               PIC X(12)  VALUE '  REG NUM   '.             
           05 FILLER               PIC X(11)  VALUE 'JB-REG-NUM '.              
           05 FILLER               PIC X(11)  VALUE ' JB-PARENT '.              
           05 FILLER               PIC X(28)  VALUE                             
             '   R  E  M  A  R  K  S      '.                                    
           05 FILLER               PIC X(35)  VALUE SPACES.                     
                                                                                
       01  L505-PRT-HEADING-5.                                                  
           05 FILLER               PIC X(25)  VALUE SPACES.                     
           05 FILLER               PIC X(10)  VALUE '|---------'.               
           05 FILLER               PIC X(12)  VALUE '|----------|'.             
           05 FILLER               PIC X(11)  VALUE '----------|'.              
           05 FILLER               PIC X(11)  VALUE '----------|'.              
           05 FILLER               PIC X(28)  VALUE                             
             '---------------------------|'.                                    
           05 FILLER               PIC X(35)  VALUE SPACES.                     
                                                                                
       01  L505-PRT-HEADING-6.                                                  
           05 FILLER               PIC X(25)  VALUE SPACES.                     
           05 FILLER               PIC X(10)  VALUE ' ---------'.               
           05 FILLER               PIC X(12)  VALUE ' ---------- '.             
           05 FILLER               PIC X(11)  VALUE '---------- '.              
           05 FILLER               PIC X(11)  VALUE '---------- '.              
           05 FILLER               PIC X(28)  VALUE                             
             '--------------------------- '.                                    
           05 FILLER               PIC X(35)  VALUE SPACES.                     
                                                                                
       01  L505-PRT-DETAILS.                                                    
           05 FILLER               PIC X(25)  VALUE SPACES.                     
           05 FILLER               PIC X(01)  VALUE ' '.                        
           05 L505-PRT-SER-NO      PIC X(8)   VALUE SPACES.                     
           05 FILLER               PIC X(01)  VALUE SPACES.                     
           05 FILLER               PIC X(02)  VALUE SPACES.                     
           05 L505-PRT-REG-NUM     PIC X(08)  VALUE SPACES.                     
           05 FILLER               PIC X(02)  VALUE SPACES.                     
           05 FILLER               PIC X(02)  VALUE SPACES.                     
           05 L505-PRT-JB-REG-NUM  PIC X(08)  VALUE SPACES.                     
           05 FILLER               PIC X(02)  VALUE SPACES.                     
           05 FILLER               PIC X(04)  VALUE SPACES.                     
           05 L505-PRT-JB-PARENT   PIC X(01)  VALUE SPACES.                     
           05 FILLER               PIC X(05)  VALUE SPACES.                     
           05 FILLER               PIC X(01)  VALUE ' '.                        
           05 L505-PRT-REMARKS     PIC X(27)  VALUE SPACES.                     
           05 FILLER               PIC X(01)  VALUE ' '.                        
           05 FILLER               PIC X(35)  VALUE SPACES.                     
                                                                                
       PROCEDURE DIVISION.                                                      
      *                                                                         
      ******************                                                        
       000-MAIN-ROUTINE.                                                        
      ******************                                                        
                                                                                
           PERFORM 100-OPEN-FILES                                               
              THRU 100-EXIT.                                                    
                                                                                
           PERFORM 200-PROCESS-BP13F800                                         
              THRU 200-EXIT                                                     
             UNTIL (WS-F800-EOF)                                                
                                                                                
           PERFORM 900-CLOSE-ROUTINE                                            
              THRU 900-EXIT.                                                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************                                                          
       100-OPEN-FILES.                                                          
      ****************                                                          
                                                                                
           OPEN INPUT  BP13K800                                                 
                       BP13F800                                                 
                OUTPUT BP13L505.                                                
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
              DISPLAY '*** OPENING ERROR, BP13K800-STATUS : '                   
                        BP13K800-STATUS                                         
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 900-CLOSE-ROUTINE                                         
                 THRU 900-EXIT                                                  
           END-IF.                                                              
                                                                                
            MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                          
            MOVE CORR WS-CUR-DATE TO WS-EDITED-DATE .                           
            MOVE WS-EDITED-DATE   TO L505-DATE.                                 
            MOVE WS-TIME-HH       TO L505-TME-HH.                               
            MOVE WS-TIME-MM       TO L505-TME-MM.                               
                                                                                
            MOVE  56              TO WS-L505-LINE-CNT.                          
            MOVE  ZERO            TO  WS-CNT-SER-NO.                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************                                                    
       200-PROCESS-BP13F800.                                                    
      **********************                                                    
                                                                                
      ***************************************************************           
      *   READ SEQUENTIALLY FROM  BP13F800 EXTRACT FILE,            *           
      *        IF JB CODE = 0,                                      *           
      *           UPDATE SEQUENCE FILE BP13F810 DIRECTLY FROM       *           
      *           BP13F800 RECORD, WITH SEQUENCE REGISTRN  NO       *           
      *           EQUALS TO REGISTRN  NO.                           *           
      *        IF JB CODE = 1,2,3 OR 5                              *           
      *           BROWSE THRU THE JOINT BALLOT LINK TO SELECT       *           
      *           THE SMALLEST REGN . NO     AS THE SEQUENCE        *           
      *           REGN NO.      FOR THAT JOINT BALLOT REQUEST       *           
      *           AND UPDATE TO SEQUENCE FILE BP13F810.             *           
      ***************************************************************           
                                                                                
           READ BP13F800 AT END                                                 
                MOVE 'Y' TO  WS-F800-STATUS                                     
                GO TO 200-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-F800-READ.                                           
                                                                                
           MOVE F800-NUM-REGN TO WS-SEQ-REGN                                    
                                WS-F800-NUM-REGN.                               
                                                                                
           PERFORM 400-PROCESS-JB-APPLTN                                        
              THRU 400-EXIT.                                                    
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************                                                   
       400-PROCESS-JB-APPLTN.                                                   
      ***********************                                                   
                                                                                
           MOVE F800-NUM-REGN    TO  K800-NUM-REGN.                             
           MOVE F800-NUM-ZONE    TO  WS-CDE-ZONE .                              
           MOVE     0            TO  WS-JB-CNT.                                 
           MOVE     0            TO  WS-REG-CNT.                                
           MOVE     0            TO  WS-TOT-JB-PARENT.                          
           MOVE  SPACES          TO  WS-LINK-STATUS.                            
                                                                                
      *  BROWSING THRU THE JB LINK FOR THE SMALLEST REGN                        
      *  NUMBER.                                                                
                                                                                
           IF F800-QTY-JT-BALLOT NUMERIC                                        
              MOVE F800-QTY-JT-BALLOT TO WS-F800-JB-CNT                         
           ELSE                                                                 
              MOVE ZEROES             TO WS-F800-JB-CNT                         
           END-IF.                                                              
                                                                                
           PERFORM 450-READ-BP13K800                                            
              THRU 450-EXIT                                                     
             UNTIL (WS-JB-CNT > WS-F800-JB-CNT) OR                              
                   (WS-LINK-ERROR)  OR (WS-LINK-COMPLETE) OR                    
                   (WS-INVALID-CNT) OR (WS-ZONE-ERROR).                         
                                                                                
      *  CHECK FOR ERROR IN JB LINK                                             
                                                                                
           IF WS-JB-CNT > 10                                                    
              DISPLAY '*** MORE THAN 5 APPLICANT IN AN JB FOR REG NO :'         
                       F800-NUM-REGN                                            
      *       MOVE '99'            TO RETURN-CODE                               
           END-IF                                                               
                                                                                
           IF WS-LINK-ERROR                                                     
              DISPLAY '*** JOINT BALLOT APPLICANT LINK ERROR '                  
                      ' AT REG NUM = ' F800-NUM-REGN                            
      *       MOVE '99'            TO RETURN-CODE                               
           END-IF                                                               
                                                                                
           IF WS-INVALID-CNT                                                    
              DISPLAY '*** INVALID-CNT                       '                  
      *       MOVE '99'            TO RETURN-CODE                               
           END-IF                                                               
                                                                                
           IF WS-ZONE-ERROR                                                     
              DISPLAY 'THE ZONE OF INPUT FILE DIFF FROM K800'                   
                      ' AT REG NUM = ' F800-NUM-REGN                            
              DISPLAY 'INPUT ZONE : ' WS-CDE-ZONE 'K800-ZONE '                  
                       K800-NUM-ZONE                                            
      *       MOVE '99'            TO RETURN-CODE                               
           END-IF                                                               
                                                                                
           IF (WS-JB-CNT > 10)  OR (WS-LINK-ERROR) OR                           
              (WS-INVALID-CNT)  OR (WS-ZONE-ERROR)                              
               PERFORM 500-WRITE-REPORT  THRU 500-EXIT                          
           ELSE                                                                 
              CONTINUE                                                          
                                                                                
      *       DISPLAY 'REG CNT ' WS-REG-CNT                                     
      *       PERFORM VARYING WS-CNT FROM 1 BY 1 UNTIL                          
      *                       WS-CNT > WS-REG-CNT                               
      *          MOVE SPACES               TO  BP13F810-REC                     
      *          MOVE WS-K800-REC (WS-CNT) TO  BP13F810-REC                     
      *          DISPLAY 'SEQ-REGN ' WS-SEQ-REGN                                
      *          MOVE WS-SEQ-REGN          TO  F810-NUM-CUR-REGN                
      *          ADD        1              TO  WS-CNT-F810-WRITE                
      *          WRITE BP13F810-REC                                             
      *       END-PERFORM                                                       
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *******************                                                       
       450-READ-BP13K800.                                                       
      *******************                                                       
                                                                                
      ***************************************************************           
      *  PROGRAM CANNOT UPDATE THE SEQUENCE NUMBER IF THE REG NUMBER*           
      * IS NOT FOUND IN THE SEQUENTIAL FILE.                        *           
      ***************************************************************           
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE BP13K800-STATUS                                             
           WHEN  23                                                             
                 DISPLAY '*** NOT FOUND IN BP13K800 FOR REGN NUM = '            
                         K800-NUM-REGN                                          
                 MOVE 'E' TO  WS-LINK-STATUS                                    
      *          MOVE BP13K800-STATUS TO RETURN-CODE                            
                 ADD  1                  TO WS-REG-CNT                          
                 MOVE K800-NUM-REGN      TO WS-REGN-NUM   (WS-REG-CNT)          
                 MOVE SPACES             TO WS-JB-REG-NUM (WS-REG-CNT)          
                 MOVE SPACES             TO WS-JB-PARENT  (WS-REG-CNT)          
                 MOVE ' NOT FOUND IN BP13K800 FILE'                             
                                         TO WS-REMARKS    (WS-REG-CNT)          
                 GO TO 450-EXIT                                                 
           WHEN  00                                                             
                 IF K800-NUM-STATUS = 'C'                                       
                    DISPLAY '*** CANCELLATION CASE, REGN NUM = '                
                            K800-NUM-REGN                                       
                    MOVE 'E' TO  WS-LINK-STATUS                                 
                    ADD  1                  TO WS-REG-CNT                       
                    MOVE K800-NUM-REGN    TO WS-REGN-NUM   (WS-REG-CNT)         
                    MOVE SPACES           TO WS-JB-REG-NUM (WS-REG-CNT)         
                    MOVE SPACES           TO WS-JB-PARENT  (WS-REG-CNT)         
                    MOVE ' CANCELLATION CASES'                                  
                                          TO WS-REMARKS    (WS-REG-CNT)         
                    GO TO 450-EXIT                                              
                 END-IF                                                         
                 ADD  1                  TO WS-REG-CNT                          
                 MOVE K800-NUM-REGN      TO WS-REGN-NUM   (WS-REG-CNT)          
                 MOVE K800-NUM-JT-BALLOT-REGN                                   
                                         TO WS-JB-REG-NUM (WS-REG-CNT)          
                 MOVE K800-NUM-JT-BALLOT-PARENT                                 
                                         TO WS-JB-PARENT  (WS-REG-CNT)          
                 MOVE SPACES             TO WS-REMARKS    (WS-REG-CNT)          
                 MOVE BP13K800-MASTER    TO WS-K800-REC   (WS-REG-CNT)          
           WHEN  OTHER                                                          
                 DISPLAY '*** ERROR READING BP13K800 AT REG NUM = '             
                          K800-NUM-REGN                                         
                 DISPLAY '***       RETURN CODE = '                             
                          BP13K800-STATUS                                       
                 MOVE BP13K800-STATUS TO RETURN-CODE                            
                 PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                        
           END-EVALUATE.                                                        
                                                                                
      *  INCREMENT  RECORD  COUNTER.                                            
                                                                                
           ADD  1  TO  WS-JB-CNT.                                               
                                                                                
      *  SELECT THE SMALLER REGISTRN  NO. AS THE SEQ REG. NO.                   
                                                                                
           IF K800-NUM-REGN <  WS-SEQ-REGN                                      
              MOVE K800-NUM-REGN TO  WS-SEQ-REGN                                
           END-IF                                                               
                                                                                
      *  IF BROKEN LINK EXISTS .....                                            
                                                                                
           IF K800-NUM-JT-BALLOT-REGN = SPACES OR LOW-VALUES                    
                                               OR '00000000'                    
              MOVE 'E' TO  WS-LINK-STATUS                                       
              MOVE ' NUM JB-REG SPACES IN K800 '                                
                       TO WS-REMARKS    (WS-REG-CNT)                            
              GO TO 450-EXIT                                                    
           END-IF.                                                              
                                                                                
      *  IF JB COUNT DOES NOT TALLY WITH THE ACTUAL LINK .....                  
                                                                                
           IF ( (K800-NUM-JT-BALLOT-REGN = WS-F800-NUM-REGN) AND                
                (WS-JB-CNT NOT = WS-F800-JB-CNT) )                              
              DISPLAY '*** F800-JB-CNT > ACTUAL NO. EXIST : REG-NUM = '         
                      F800-NUM-REGN                                             
              MOVE 'I' TO WS-LINK-STATUS                                        
              MOVE ' F800-JB-CNT > ACTUAL NO.  '                                
                       TO WS-REMARKS    (WS-REG-CNT)                            
              GO TO 450-EXIT                                                    
           END-IF.                                                              
                                                                                
           IF ( (K800-NUM-JT-BALLOT-REGN NOT = WS-F800-NUM-REGN) AND            
                (WS-JB-CNT = WS-F800-JB-CNT) )                                  
              DISPLAY '*** F800-JB-CNT < ACTUAL NO. EXIST : REG-NUM = '         
                      F800-NUM-REGN                                             
              MOVE ' F800-JB-CNT < ACTUAL NO.  '                                
                       TO WS-REMARKS    (WS-REG-CNT)                            
              MOVE 'I' TO WS-LINK-STATUS                                        
              GO TO 450-EXIT                                                    
           END-IF.                                                              
                                                                                
           IF ( (K800-NUM-JT-BALLOT-REGN NOT = WS-F800-NUM-REGN) AND            
                (WS-F800-JB-CNT = 0) )                                          
              DISPLAY '*** F800-JB-CNT = 0 : REG-NUM = '                        
                      F800-NUM-REGN                                             
              MOVE ' F800-JB-CNT = 0.  '                                        
                       TO WS-REMARKS    (WS-REG-CNT)                            
              MOVE 'I' TO WS-LINK-STATUS                                        
              GO TO 450-EXIT                                                    
           END-IF.                                                              
                                                                                
      *  IF THE NO. OF JBPARENT = 'Y' IS > 1 THEN IT IS AN ERROR                
                                                                                
           IF K800-NUM-JT-BALLOT-PARENT = 'Y'                                   
              ADD  1  TO WS-TOT-JB-PARENT                                       
           END-IF                                                               
                                                                                
      *  IF THE ZONES OF INPUT DOES NOT MATCH WITH K800 THEN ERROR              
                                                                                
           IF K800-NUM-ZONE NOT EQUAL  WS-CDE-ZONE                              
              MOVE  'Z'  TO WS-LINK-STATUS                                      
              MOVE ' NOT IN SAME ZONE AS INPUT '                                
                       TO WS-REMARKS    (WS-REG-CNT)                            
              GO TO 450-EXIT                                                    
           END-IF                                                               
                                                                                
      *  IF THE LINK IS COMPLETED, EXIT.                                        
                                                                                
           IF K800-NUM-JT-BALLOT-REGN = WS-F800-NUM-REGN                        
              MOVE 'C' TO WS-LINK-STATUS                                        
              IF WS-TOT-JB-PARENT > 1                                           
                 MOVE  'E'  TO WS-LINK-STATUS                                   
                 MOVE ' TOTAL JB-PARENT ''Y'' > 1 '                             
                          TO WS-REMARKS    (WS-REG-CNT)                         
                 GO TO 450-EXIT                                                 
              ELSE                                                              
                 IF WS-TOT-JB-PARENT < 1                                        
                    MOVE  'E'  TO WS-LINK-STATUS                                
                    MOVE ' TOTAL JB-PARENT ''Y'' < 1 '                          
                             TO WS-REMARKS    (WS-REG-CNT)                      
                    GO TO 450-EXIT                                              
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *  OTHERWISE, READ NEXT MEMBER.                                           
                                                                                
           MOVE K800-NUM-JT-BALLOT-REGN TO  K800-NUM-REGN.                      
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *******************                                                       
       500-WRITE-REPORT.                                                        
      *******************                                                       
                                                                                
           IF WS-L505-LINE-CNT > 55                                             
              PERFORM 505-WRITE-HEADING  THRU 505-EXIT                          
           END-IF.                                                              
                                                                                
           ADD      1            TO  WS-CNT-SER-NO.                             
                                                                                
           PERFORM VARYING  WS-I FROM 1 BY 1 UNTIL WS-I > WS-REG-CNT            
                                                                                
               MOVE  WS-REGN-NUM   (WS-I) TO L505-PRT-REG-NUM                   
               MOVE  WS-JB-REG-NUM (WS-I) TO L505-PRT-JB-REG-NUM                
               MOVE  WS-JB-PARENT  (WS-I) TO L505-PRT-JB-PARENT                 
               MOVE  WS-REMARKS    (WS-I) TO L505-PRT-REMARKS                   
                                                                                
               IF WS-I EQUAL 1                                                  
                  MOVE  WS-CNT-SER-NO     TO WS-EDIT-SER-NO                     
                  MOVE  WS-EDIT-SER-NO    TO L505-PRT-SER-NO                    
               ELSE                                                             
                  MOVE  SPACES            TO L505-PRT-SER-NO                    
               END-IF                                                           
                                                                                
               WRITE PRINT-REC     FROM L505-PRT-DETAILS AFTER 1                
                                                                                
               ADD   1                    TO WS-L505-LINE-CNT                   
                                                                                
           END-PERFORM.                                                         
                                                                                
      *    WRITE PRINT-REC FROM L505-PRT-HEADING-6 AFTER 1.                     
                                                                                
           MOVE SPACES  TO PRINT-REC.                                           
           WRITE PRINT-REC.                                                     
           ADD   1                    TO WS-L505-LINE-CNT .                     
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *******************                                                       
       505-WRITE-HEADING.                                                       
      *******************                                                       
                                                                                
            ADD        1       TO WS-CNT-PAGE.                                  
            MOVE  WS-CNT-PAGE  TO L505-PAGENO.                                  
                                                                                
            WRITE PRINT-REC FROM L505-PRT-HEADING-1 AFTER PAGE.                 
            WRITE PRINT-REC FROM L505-PRT-HEADING-2 AFTER 2.                    
            WRITE PRINT-REC FROM L505-PRT-HEADING-3 AFTER 2.                    
      **    WRITE PRINT-REC FROM L505-PRT-HEADING-6 AFTER 4.                    
            WRITE PRINT-REC FROM L505-PRT-HEADING-4 AFTER 4.                    
            WRITE PRINT-REC FROM L505-PRT-HEADING-6 AFTER 1.                    
                                                                                
            MOVE SPACES  TO PRINT-REC                                           
            WRITE PRINT-REC .                                                   
                                                                                
            MOVE  10           TO WS-L505-LINE-CNT.                             
                                                                                
       505-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *******************                                                       
       900-CLOSE-ROUTINE.                                                       
      *******************                                                       
                                                                                
           CLOSE BP13K800                                                       
                 BP13F800                                                       
      *          BP13F810                                                       
                 BP13L505.                                                      
                                                                                
           IF BP13K800-STATUS NOT = 0                                           
              DISPLAY '***  CLOSING ERROR, BP13K800-STATUS '                    
              BP13K800-STATUS                                                   
           END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '      ******* BP13C505 ******* '.                           
           DISPLAY ' '.                                                         
           DISPLAY                                                              
           'TOTAL NO. OF REC READ FROM EXTRACT FILE   (BP13F800) : '            
                   WS-CNT-F800-READ.                                            
           DISPLAY                                                              
           'TOTAL NO. OF REC WRITTEN TO REPORT  (L505)           : '            
                   WS-CNT-SER-NO.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
