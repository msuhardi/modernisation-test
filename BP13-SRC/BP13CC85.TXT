       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC85.                                                 
      *AUTHOR.        BALA AMUTHAN.                                             
      *DATE-WRITTEN.  18/06/2007.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE :                                                    *        
      *             ADJUST AMT ARREAR INSTAL & ARR MTH DUE TO          *        
      *             CORRECTION OF ARR INSTAL                           *        
      *            *ADD ADJUSTMT AMT TOT ARREAR INSTAL                 *        
      *                                                                *        
      * INPUT  FILES : BP13FD92                                        *        
      *                SY02F001                                        *        
      * I-O    FILE  : BP13KD00                                        *        
      *              : BP13KD60                                        *        
      *              : BP13KD61                                        *        
      *              : BP13KD92                                        *        
      *              : BP13KD94                                        *        
      * OUTPUT FILE  : BP13LC85.                                       *        
      *                                                                *        
      * CHG-NO    BY    ON      - DESCRIPTION                          *        
      * ---------------------------------                              *        
      * BP133095 BA2 18/06/2007   NEW PROGRAM                          *        
      *          ED1 29/06/2007   ADDED KD99 WRITE                     *        
      *          BA3 22/08/2007   ROUNDED COMPUTATION FOR NUM-MTH-INSTAL        
      * BP133171 ED1 02/11/2007   REMOVED UPD OF KD94-DTE&TME UPDATE   *        
      * BP133091 RB12 16/11/2007  EXPAND KD00 LENGTH FR 1200 TO 1600   *        
      * BP133091 LMS1 15/12/2007  EXPAND KD99 LENGTH FR 1250 TO 1650   *        
      * BP133209 ED1  25/03/2008  ADD TXHIST TRANS BP13KD63 = 'ARRADJ' *        
      *                           & CHANGE COMPUTATION TO ADD THE      *        
      *                           ADJUSTMNT AMT TO ARR-INSTAL          *        
      * BP133209 ED1  31/03/2008  CHANGED REPORT TITLE                 *        
      * BP133389 JB8  19/03/2009  TO CATER FOR KD63 NOT FND            *        
      * BP134493 IMC1 29/03/2012  FIXED DIVIDE BY 0 IN MONTH INSTAL    *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT   BP13FD92      ASSIGN TO BP13FD92.                           
           SELECT   BP13LC85      ASSIGN TO BP13LC85.                           
           SELECT   SY02F001      ASSIGN TO SY02F001.                           
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KD00-KEY-FLD                                    
                FILE STATUS  IS WS-KD00-STATUS.                                 
                                                                                
           SELECT BP13KD92 ASSIGN TO BP13KD92                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KD92-KEY-FLD                                    
                FILE STATUS  IS WS-KD92-STATUS.                                 
                                                                                
           SELECT BP13KD94 ASSIGN TO BP13KD94                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KD94-KEY-FLD                                    
                FILE STATUS  IS WS-KD94-STATUS.                                 
                                                                                
           SELECT BP13KD99 ASSIGN TO BP13KD99                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KD99-KEY-FLD                                    
                FILE STATUS  IS WS-KD99-STATUS.                                 
                                                                                
           SELECT BP13KD60 ASSIGN TO BP13KD60                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KD60-KEY-FLD                                    
                FILE STATUS  IS KD60-STATUS.                                    
                                                                                
           SELECT BP13KD61 ASSIGN TO BP13KD61                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KD61-KEY-FLD                                    
                FILE STATUS  IS KD61-STATUS.                                    
                                                                                
           SELECT BP13KD63 ASSIGN       TO BP13KD63                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS KD63-KEY-FLD                         
                           FILE STATUS  IS WS-KD63-STATUS.                      
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13FD92    RECORD CONTAINS 1000 CHARACTERS                         
                        LABEL RECORDS ARE STANDARD                              
                        RECORDING MODE IS F.                                    
       COPY BP13FD92.                                                           
                                                                                
       FD   BP13KD00    RECORD CONTAINS 1600 CHARACTERS.                        
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13KD92    RECORD CONTAINS 1000 CHARACTERS.                        
       COPY BP13KD92.                                                           
                                                                                
       FD   BP13KD94    RECORD CONTAINS 1000 CHARACTERS.                        
       COPY BP13KD94.                                                           
                                                                                
       FD   BP13KD99    RECORD CONTAINS 1650 CHARACTERS.                        
       COPY BP13KD99.                                                           
                                                                                
       FD   BP13KD60    RECORD CONTAINS 250 CHARACTERS.                         
       COPY BP13KD60.                                                           
                                                                                
       FD   BP13KD61    RECORD CONTAINS 250 CHARACTERS.                         
       COPY BP13KD61.                                                           
                                                                                
       FD   BP13KD63    RECORD CONTAINS 150  CHARACTERS.                        
       COPY BP13KD63.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13LC85    BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 132 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01  LC85-PRTREC  PIC X(132).                                             
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-KD00-STATUS                  PIC 9(02) VALUE 0.                   
       01  WS-KD92-STATUS                  PIC 9(02) VALUE 0.                   
       01  WS-KD94-STATUS                  PIC 9(02) VALUE 0.                   
       01  WS-KD99-STATUS                  PIC 9(02) VALUE 0.                   
       01  WS-KD63-STATUS                  PIC 9(02) VALUE 0.                   
       01  KD60-STATUS                     PIC 9(02) VALUE 0.                   
       01  KD61-STATUS                     PIC 9(02) VALUE 0.                   
                                                                                
       01  WS-SWITCH.                                                           
           05 WS-KD00-FND                  PIC X(01) VALUE 'N'.                 
              88 KD00-FND                  VALUE 'Y'.                           
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-KD00-READ                 PIC 9(07) VALUE ZEROS.               
           05 WS-KD00-UPDATE               PIC 9(07) VALUE ZEROS.               
           05 WS-FD92-READ                 PIC 9(07) VALUE ZEROS.               
           05 WS-KD92-READ                 PIC 9(07) VALUE ZEROS.               
           05 WS-KD94-WRITE                PIC 9(07) VALUE ZEROS.               
           05 WS-KD99-WRITE                PIC 9(07) VALUE ZEROS.               
           05 WS-KD63-WRITE                PIC 9(07) VALUE ZEROS.               
           05 WS-KD60-READ                 PIC 9(07) VALUE ZEROS.               
           05 WS-KD60-DELETE               PIC 9(07) VALUE ZEROS.               
           05 WS-KD60-REWRITE              PIC 9(07) VALUE ZEROS.               
           05 WS-KD60-WRITE                PIC 9(07) VALUE ZEROS.               
           05 WS-KD61-WRITE                PIC 9(07) VALUE ZEROS.               
           05 WS-KD92-DEL                  PIC 9(07) VALUE ZEROS.               
           05 WS-SNO                       PIC 9(05) VALUE ZEROS.               
           05 WS-PAGE-CTR                  PIC 9(04) VALUE ZEROS.               
           05 WS-LINE-CTR                  PIC 9(02) VALUE 65.                  
           05 WS-WRITE-KD61                PIC X(01) VALUE SPACE.               
           05 WS-KD60-FND                  PIC X(01) VALUE SPACE.               
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CUR-DD                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-MM                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-CCYY                 PIC 9(04) VALUE ZEROES.              
                                                                                
       01  WS-KD63-EOF                     PIC X(01) VALUE 'N'.                 
                                                                                
       01  WS-TRANS-DTE.                                                        
           05  WS-TRANS-DTE-CYM.                                                
               10  WS-TRANS-DATE-CCYY  PIC X(4).                                
               10  WS-TRANS-DATE-MM    PIC X(2).                                
           05  WS-TRANS-DATE-DD        PIC X(2) VALUE '01'.                     
                                                                                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE                 PIC X(08) VALUE SPACES.              
           05  WS-SYS-TIME                 PIC X(08) VALUE SPACES.              
           05  FILLER                      PIC X(05) VALUE SPACES.              
                                                                                
       01  WS-SER-VAR.                                                          
           05  WS-SER-CHR.                                                      
               10 WS-SER-CHR-1          PIC X(01).                              
               10 WS-SER-CHR-2          PIC X(01).                              
               10 WS-SER-CHR-3          PIC X(01).                              
           05  WS-NUM-SER REDEFINES WS-SER-CHR                                  
                                       PIC 9(03).                               
                                                                                
       01 WS-OTHER-VAR.                                                         
           05  WS-AMT-ARR-INSTAL-AF        PIC S9(06)V99 VALUE ZEROES.          
           05  WS-AMT-ARR-INSTAL-BF        PIC S9(06)V99 VALUE ZEROES.          
           05  WS-NUM-MTH-INSTAL-AF        PIC 9(02)V99  VALUE ZEROES.          
           05  WS-NUM-MTH-INSTAL-BF        PIC 9(02)V99  VALUE ZEROES.          
           05  WS-NUM-SEQ                  PIC 9(03)     VALUE ZEROES.          
                                                                                
       01 WS-MONTH-NAME.                                                        
          05 WS-STRING PIC X(36) VALUE                                          
                'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                         
          05 WS-MONTH  REDEFINES WS-STRING OCCURS 12 TIMES PIC XXX.             
                                                                        01010000
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01  LC85-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13LC85'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LC85-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LC85-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
                                                                                
       01  LC85-HEADER02.                                                       
           05  FILLER              PIC X(34)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'REPORT ON CORRECTION OF ARREARS / ADVANCE'.                     
           05  FILLER              PIC X(20)     VALUE                          
               ' INSTALMENT IN SOC'.                                            
                                                                                
       01  LC85-HEADER03.                                                       
           05  FILLER              PIC X(05)     VALUE 'S/N  '.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'REGN NO '.              
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(17)     VALUE                          
                                          'BEFORE ARR-INSTAL'.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(16)     VALUE                          
                                          'AFTER ARR-INSTAL'.                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(17)     VALUE                          
                                          'BEFORE MTH-INSTAL'.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(16)     VALUE                          
                                          'AFTER MTH-INSTAL'.                   
           05  FILLER              PIC X(38)     VALUE SPACES.                  
                                                                                
       01 LC85-DETAILS.                                                         
           05  LC85-SNO            PIC ZZZZ9     VALUE ZEROES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LC85-REGNO          PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  LC85-BF-ARR-INST    PIC Z(5)9.99  VALUE ZEROES.                  
           05  LC85-BF-ARR-SIGN    PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE SPACES.                  
           05  LC85-AF-ARR-INST    PIC Z(5)9.99  VALUE ZEROES.                  
           05  LC85-AF-ARR-SIGN    PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  LC85-BF-MTH-INST    PIC Z9.99     VALUE ZEROES.                  
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  LC85-AF-MTH-INST    PIC Z9.99     VALUE ZEROES.                  
           05  FILLER              PIC X(43)     VALUE SPACES.                  
                                                                                
       01 LC85-ROLL.                                                            
           05  FILLER              PIC X(94)     VALUE ALL '-'.                 
           05  FILLER              PIC X(38)     VALUE SPACES.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13FD92       THRU 2000-EXIT.                     
           PERFORM 3000-MAIN-PROCESS        THRU 3000-EXIT                      
             UNTIL FD92-NUM-REGN    = HIGH-VALUES.                              
           PERFORM 9000-CLOSING-ROUTINE     THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT BP13FD92                                                  
                      SY02F001                                                  
                I-O   BP13KD60                                                  
                      BP13KD61                                                  
                      BP13KD00                                                  
                      BP13KD92                                                  
                      BP13KD94                                                  
                      BP13KD99                                                  
                      BP13KD63                                                  
               OUTPUT BP13LC85.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
           MOVE WS-SYS-DATE(1:4)      TO WS-CUR-CCYY.                           
           MOVE WS-SYS-DATE(5:2)      TO WS-CUR-MM.                             
           MOVE WS-SYS-DATE(7:2)      TO WS-CUR-DD.                             
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KD00 FILE ERROR ' WS-KD00-STATUS             
              MOVE     WS-KD00-STATUS   TO  RETURN-CODE                         
              PERFORM 9000-CLOSING-ROUTINE     THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF KD60-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING BP13KD60 FILE ERROR ' KD60-STATUS                
              MOVE     KD60-STATUS   TO  RETURN-CODE                            
              PERFORM 9000-CLOSING-ROUTINE     THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF KD61-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING BP13KD61 FILE ERROR ' KD61-STATUS                
              MOVE     KD61-STATUS   TO  RETURN-CODE                            
              PERFORM 9000-CLOSING-ROUTINE     THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-KD92-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KD92 FILE ERROR ' WS-KD92-STATUS             
              MOVE     WS-KD92-STATUS   TO  RETURN-CODE                         
              PERFORM 9000-CLOSING-ROUTINE     THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-KD94-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KD94 FILE ERROR ' WS-KD94-STATUS             
              MOVE     WS-KD94-STATUS   TO  RETURN-CODE                         
              PERFORM 9000-CLOSING-ROUTINE     THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-KD99-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KD99 FILE ERROR ' WS-KD99-STATUS             
              MOVE     WS-KD99-STATUS   TO  RETURN-CODE                         
              PERFORM 9000-CLOSING-ROUTINE     THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-KD63-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KD63 FILE ERROR ' WS-KD63-STATUS             
              MOVE     WS-KD63-STATUS   TO  RETURN-CODE                         
              PERFORM 9000-CLOSING-ROUTINE     THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13FD92.                                                      
      *-------------------------------------------------------------            
           READ BP13FD92                                                        
             AT END MOVE HIGH-VALUES TO  FD92-KEY-FLD                           
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD  1                    TO  WS-FD92-READ.                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' FD92-AMT-ARR-ADJUSTMT : ' FD92-AMT-ARR-ADJUSTMT            
           PERFORM 3100-READ-BP13KD00         THRU 3100-EXIT.                   
           IF KD00-FND                                                          
              PERFORM 3150-WRITE-BP13KD99     THRU 3150-EXIT                    
              PERFORM 3200-DETER-MTH-INSTAL   THRU 3200-EXIT                    
              PERFORM 3300-REWRITE-BP13KD00   THRU 3300-EXIT                    
              PERFORM 3400-READ-BP13KD92      THRU 3400-EXIT                    
              PERFORM 3500-WRITE-BP13KD94     THRU 3500-EXIT                    
              PERFORM 3600-DELETE-BP13KD92    THRU 3600-EXIT                    
              PERFORM 3700-WRITE-BP13KD63     THRU 3700-EXIT                    
              PERFORM 5000-WRITE-REPORT       THRU 5000-EXIT                    
           END-IF.                                                              
           PERFORM 2000-READ-BP13FD92         THRU 2000-EXIT.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       3100-READ-BP13KD00.                                                      
      *--------------------------------------------------------------           
           MOVE SPACES        TO BP13KD00-REC.                                  
           MOVE FD92-NUM-REGN TO KD00-NUM-REGN.                                 
           READ BP13KD00.                                                       
                                                                                
           IF WS-KD00-STATUS = 00                                               
              MOVE 'Y'      TO       WS-KD00-FND                                
              ADD  +1       TO       WS-KD00-READ                               
           ELSE                                                                 
             IF WS-KD00-STATUS = 23                                             
                MOVE 'N'    TO       WS-KD00-FND                                
                DISPLAY 'REC NOT FND IN KD00, STATUS ' WS-KD00-STATUS           
                        ', REGN IS ' KD00-NUM-REGN                              
             ELSE                                                               
                DISPLAY 'ERROR READING KD00, STATUS ' WS-KD00-STATUS            
                        ', REGN IS ' KD00-NUM-REGN                              
                PERFORM 9000-CLOSING-ROUTINE     THRU 9000-EXIT                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3150-WRITE-BP13KD99.                                                     
      *-------------------------------------------------------------            
           MOVE BP13KD00-REC        TO BP13KD99-REC.                            
           MOVE KD00-NUM-REGN       TO KD99-NUM-REGN-LOG.                       
           MOVE WS-SYS-DATE         TO KD99-DTE-UPDATE-LOG.                     
           MOVE WS-SYS-TIME         TO KD99-TME-UPDATE-LOG.                     
                                                                                
           WRITE BP13KD99-REC.                                                  
           EVALUATE WS-KD99-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-KD99-WRITE                                         
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN WRITING  BP13KD99 - STATUS '                 
                             WS-KD99-STATUS                                     
           END-EVALUATE.                                                        
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-DETER-MTH-INSTAL.                                                   
      *-------------------------------------------------------------            
           IF KD00-AMT-ARR-INSTAL NOT NUMERIC                                   
              MOVE 0 TO KD00-AMT-ARR-INSTAL.                                    
                                                                                
           IF KD00-AMT-INSTAL NOT NUMERIC                                       
              MOVE 0 TO KD00-AMT-INSTAL.                                        
                                                                                
           IF FD92-AMT-ARR-ADJUSTMT NOT NUMERIC                                 
              MOVE 0 TO FD92-AMT-ARR-ADJUSTMT.                                  
                                                                                
           INITIALIZE WS-OTHER-VAR.                                             
                                                                                
           COMPUTE WS-AMT-ARR-INSTAL-AF = KD00-AMT-ARR-INSTAL +                 
                                       FD92-AMT-ARR-ADJUSTMT.                   
                                                                                
           IF WS-AMT-ARR-INSTAL-AF >= 0 AND KD00-AMT-INSTAL > 0                 
              COMPUTE WS-NUM-MTH-INSTAL-AF ROUNDED =                            
                      WS-AMT-ARR-INSTAL-AF / KD00-AMT-INSTAL.                   
                                                                                
           MOVE KD00-AMT-ARR-INSTAL  TO WS-AMT-ARR-INSTAL-BF.                   
           MOVE KD00-NUM-MTH-INSTAL  TO WS-NUM-MTH-INSTAL-BF.                   
           MOVE WS-AMT-ARR-INSTAL-AF TO KD00-AMT-ARR-INSTAL.                    
           MOVE WS-NUM-MTH-INSTAL-AF TO KD00-NUM-MTH-INSTAL.                    
           MOVE 'BP13CC85'           TO KD00-NUM-USERID.                        
           MOVE WS-SYS-DATE          TO KD00-DTE-UPDATE.                        
           MOVE WS-SYS-TIME          TO KD00-TME-UPDATE.                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3300-REWRITE-BP13KD00.                                                   
      *-------------------------------------------------------------            
                                                                                
           REWRITE BP13KD00-REC.                                                
                                                                                
           EVALUATE WS-KD00-STATUS                                              
           WHEN 00                                                              
               ADD +1  TO WS-KD00-UPDATE                                        
           WHEN OTHER                                                           
               DISPLAY 'BP13KD00 - ERROR IN WRITE, STATUS: '                    
                        WS-KD00-STATUS ' KEY ' KD00-KEY-FLD                     
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3400-READ-BP13KD92.                                                      
      *--------------------------------------------------------------           
           MOVE SPACES        TO BP13KD92-REC.                                  
           MOVE FD92-KEY-FLD  TO KD92-KEY-FLD.                                  
           READ BP13KD92.                                                       
                                                                                
           IF WS-KD92-STATUS = 00                                               
              ADD +1          TO WS-KD92-READ                                   
           ELSE                                                                 
             IF WS-KD92-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN KD92, STATUS ' WS-KD92-STATUS           
                        ', REGN IS ' KD92-NUM-REGN                              
             ELSE                                                               
                DISPLAY 'ERROR READING KD92, STATUS ' WS-KD92-STATUS            
                        ', REGN IS ' KD92-NUM-REGN                              
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3500-WRITE-BP13KD94.                                                     
      *-------------------------------------------------------------            
           INITIALIZE                  BP13KD94-REC.                            
           MOVE SPACES              TO BP13KD94-REC.                            
           MOVE BP13KD92-REC        TO BP13KD94-REC.                            
           MOVE KD92-NUM-REGN       TO KD94-NUM-REGN-LOG.                       
           MOVE KD92-NUM-TRANS      TO KD94-NUM-TRANS-LOG.                      
           MOVE 'BP13CC85'          TO KD94-NUM-USERID.                         
                                                                                
           WRITE BP13KD94-REC.                                                  
                                                                                
           EVALUATE WS-KD94-STATUS                                              
           WHEN 00                                                              
               ADD +1   TO  WS-KD94-WRITE                                       
           WHEN OTHER                                                           
               DISPLAY 'BP13KD94 - ERROR IN WRITE, STATUS: '                    
                        WS-KD94-STATUS ' KEY ' KD94-KEY-FLD                     
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3600-DELETE-BP13KD92.                                                    
      *-------------------------------------------------------------            
           DELETE BP13KD92.                                                     
                                                                                
           EVALUATE WS-KD92-STATUS                                              
           WHEN 00                                                              
                ADD 1 TO WS-KD92-DEL                                            
           WHEN OTHER                                                           
               DISPLAY 'BP13KD92 - ERROR IN DELETE, STATUS: '                   
                        WS-KD92-STATUS ' KEY ' KD92-KEY-FLD                     
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3700-WRITE-BP13KD63.                                                     
      *-------------------------------------------------------------            
           MOVE 'N'    TO WS-KD63-EOF.                                          
           MOVE 'N'    TO WS-KD60-FND.                                          
           MOVE ZEROS  TO WS-NUM-SEQ.                                           
                                                                                
           PERFORM 3710-STARTBR-KD63     THRU 3710-EXIT.                        
           PERFORM 3711-READNXT-KD63     THRU 3711-EXIT                         
                   UNTIL WS-KD63-EOF = 'Y'.                                     
                                                                                
           IF WS-NUM-SEQ = ZEROS                                                
              PERFORM 3720-READ-BP13KD60          THRU 3720-EXIT                
              IF WS-KD60-FND = 'Y'                                              
                 IF KD60-DTE-TRANS(1:6) = WS-SYS-DATE(1:6)                      
                    CONTINUE                                                    
                 ELSE                                                           
                    PERFORM 3730-MOVE-VALUES-KD61 THRU 3730-EXIT                
                    MOVE 'N' TO WS-WRITE-KD61                                   
                    PERFORM 3740-WRITE-KD61       THRU 3740-EXIT                
                      UNTIL WS-WRITE-KD61 = 'Y'                                 
                    PERFORM 3750-MOVE-VALUE-KD60  THRU 3750-EXIT                
                    PERFORM 3760-REWRITE-BP13KD60 THRU 3760-EXIT                
                 END-IF                                                         
              ELSE                                                              
                 PERFORM 3750-MOVE-VALUE-KD60     THRU 3750-EXIT                
                 PERFORM 3770-WRITE-BP13KD60      THRU 3770-EXIT                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD  1                     TO WS-NUM-SEQ.                            
           MOVE SPACES                TO BP13KD63-REC.                          
           INITIALIZE                    BP13KD63-REC.                          
           MOVE FD92-NUM-REGN         TO KD63-NUM-REGN.                         
           MOVE FD92-DTE-UPDATE       TO KD63-DTE-PROCESS.                      
           MOVE WS-NUM-SEQ            TO KD63-NUM-SEQ.                          
           MOVE 'ARRADJ'              TO KD63-TXT-DESC.                         
           MOVE FD92-NUM-USERID       TO KD63-NUM-REF.                          
           MOVE FD92-NUM-SCH-ACC      TO KD63-NUM-SCH-ACCNT.                    
           MOVE FD92-AMT-ARR-ADJUSTMT TO KD63-AMT-LOAN.                         
           IF  FD92-AMT-ARR-ADJUSTMT < 0                                        
               MOVE 'C'                TO KD63-CDE-CR-DR                        
           ELSE                                                                 
               MOVE 'D'                TO KD63-CDE-CR-DR                        
           END-IF.                                                              
                                                                                
           WRITE BP13KD63-REC.                                                  
                                                                                
           EVALUATE WS-KD63-STATUS                                              
           WHEN 00                                                              
               ADD +1   TO  WS-KD63-WRITE                                       
           WHEN OTHER                                                           
               DISPLAY 'BP13KD63 - ERROR IN WRITE, STATUS: '                    
                        WS-KD63-STATUS ' KEY ' KD63-KEY-FLD                     
           END-EVALUATE.                                                        
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       3710-STARTBR-KD63.                                                       
      *--------------------------------------------------------*                
           MOVE SPACES               TO KD63-KEY-FLD.                           
           INITIALIZE                   KD63-KEY-FLD.                           
           MOVE FD92-NUM-REGN        TO KD63-NUM-REGN.                          
           MOVE FD92-DTE-UPDATE(1:6) TO KD63-DTE-PROCESS.                       
                                                                                
           START BP13KD63 KEY >= KD63-KEY-FLD.                                  
                                                                                
           EVALUATE WS-KD63-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE 'Y' TO WS-KD63-EOF                                        
              WHEN OTHER                                                        
                 MOVE 'Y' TO WS-KD63-EOF                                        
                 DISPLAY 'ERRROR READING BP13KD63 STATUS - '                    
                                            WS-KD63-STATUS                      
           END-EVALUATE.                                                        
                                                                                
       3710-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3711-READNXT-KD63.                                                       
      *--------------------------------------------------------*                
           READ BP13KD63 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-KD63-EOF                                  
           END-READ.                                                            
                                                                                
           IF KD63-NUM-REGN         = FD92-NUM-REGN AND                         
              KD63-DTE-PROCESS(1:6) = FD92-DTE-UPDATE(1:6)                      
              MOVE KD63-NUM-SEQ TO  WS-NUM-SEQ                                  
           ELSE                                                                 
              MOVE 'Y' TO  WS-KD63-EOF                                          
           END-IF.                                                              
                                                                                
       3711-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3720-READ-BP13KD60.                                                      
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES                      TO BP13KD60-REC.                    
           INITIALIZE                          BP13KD60-REC.                    
                                                                                
           MOVE FD92-NUM-REGN               TO KD60-NUM-REGN.                   
                                                                                
           READ BP13KD60.                                                       
                                                                                
           IF KD60-STATUS = 00 OR 02                                            
              MOVE 'Y'                      TO WS-KD60-FND                      
              ADD 1 TO WS-KD60-READ                                             
           ELSE                                                                 
              IF KD60-STATUS = 23                                               
                 DISPLAY 'BP13KD60 - RECORD NOT FND : ' KD60-NUM-REGN           
              ELSE                                                              
                 DISPLAY ' BP13KD60-READING ERROR, STATUS: '                    
                          KD60-STATUS                                           
                 MOVE KD60-STATUS        TO   RETURN-CODE                       
                 PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3720-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3730-MOVE-VALUES-KD61.                                                   
      ******************************************************************        
           MOVE SPACES                 TO BP13KD61-REC.                         
           INITIALIZE                     BP13KD61-REC.                         
           MOVE BP13KD60-REC           TO BP13KD61-REC.                         
                                                                                
           MOVE WS-SYS-TIME            TO KD61-TME-UPDATE.                      
           MOVE WS-SYS-DATE            TO KD61-DTE-UPDATE.                      
           MOVE 'BP13CC85'             TO KD61-NUM-USERID.                      
                                                                                
       3730-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       3740-WRITE-KD61.                                                         
      ******************************************************************        
           WRITE BP13KD61-REC.                                                  
           IF KD61-STATUS = 00 OR 02                                            
              ADD 1    TO WS-KD61-WRITE                                         
              MOVE 'Y' TO WS-WRITE-KD61                                         
           ELSE                                                                 
              IF KD61-STATUS = 22                                               
                 PERFORM 3745-SERIAL-PROCESS  THRU 3745-EXIT                    
              ELSE                                                              
                 DISPLAY ' BP13KD61-WRITING ERROR, STATUS: '                    
                         KD61-STATUS                                            
                 MOVE KD61-STATUS           TO   RETURN-CODE                    
                 PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3740-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       3745-SERIAL-PROCESS.                                                     
      ******************************************************************        
           MOVE KD61-NUM-SEQ           TO   WS-SER-CHR.                         
           IF WS-NUM-SER IS NOT NUMERIC                                         
              MOVE ZEROES              TO   WS-NUM-SER                          
           END-IF.                                                              
                                                                                
           ADD +1                      TO   WS-NUM-SER.                         
           MOVE WS-NUM-SER             TO   KD61-NUM-SEQ.                       
                                                                                
       3745-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       3750-MOVE-VALUE-KD60.                                                    
      ******************************************************************        
                                                                                
           IF KD00-AMT-LOAN-BAL IS NOT NUMERIC                                  
              MOVE ZEROES            TO KD00-AMT-LOAN-BAL                       
           END-IF.                                                              
           IF KD00-AMT-ARR-INSTAL IS NOT NUMERIC                                
              MOVE ZEROES            TO KD00-AMT-ARR-INSTAL                     
           END-IF.                                                              
           IF KD00-AMT-INSTAL IS NOT NUMERIC                                    
              MOVE ZEROES            TO KD00-AMT-INSTAL                         
           END-IF.                                                              
           IF KD00-DTE-INSTAL-EFF IS NOT NUMERIC                                
              MOVE ZEROES            TO KD00-DTE-INSTAL-EFF                     
           END-IF.                                                              
           IF KD00-AMT-ARR-INTR IS NOT NUMERIC                                  
              MOVE ZEROES            TO KD00-AMT-ARR-INTR                       
           END-IF.                                                              
                                                                                
           MOVE WS-SYS-DATE(1:6)       TO WS-TRANS-DTE-CYM.                     
           MOVE WS-TRANS-DTE           TO KD60-DTE-TRANS.                       
           MOVE '001'                  TO KD60-NUM-SEQ.                         
           MOVE KD00-NUM-SCH-ACCNT     TO KD60-NUM-SCH-ACCNT.                   
           MOVE KD00-AMT-LOAN-BAL      TO KD60-AMT-OPEN-LOAN-BAL.               
           MOVE WS-AMT-ARR-INSTAL-BF   TO KD60-AMT-OPEN-ARR-INST.               
           MOVE KD00-CDE-LOAN-SCH      TO KD60-CDE-LOAN-SCH.                    
           MOVE KD00-AMT-INSTAL        TO KD60-AMT-INST.                        
           MOVE KD00-DTE-INSTAL-EFF    TO KD60-DTE-INST.                        
           MOVE KD00-AMT-ARR-INTR      TO KD60-AMT-OPEN-INTEREST.               
                                                                                
           MOVE WS-SYS-TIME      TO KD60-TME-UPDATE.                            
           MOVE WS-SYS-DATE      TO KD60-DTE-UPDATE.                            
           MOVE 'BP13CC85'       TO KD60-NUM-USERID.                            
                                                                                
       3750-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3760-REWRITE-BP13KD60.                                                   
      ******************************************************************        
           REWRITE BP13KD60-REC.                                                
                                                                                
           IF KD60-STATUS = 00 OR 02                                            
              ADD 1 TO WS-KD60-REWRITE                                          
           ELSE                                                                 
              DISPLAY ' BP13KD60-REWRITING ERROR, STATUS: '                     
                       KD60-STATUS                                              
              MOVE KD60-STATUS           TO   RETURN-CODE                       
              PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       3760-EXIT.  EXIT.                                                        
                                                                                
      *----------------------------------------------------------------*        
       3770-WRITE-BP13KD60.                                                     
      *----------------------------------------------------------------*        
                                                                                
           WRITE BP13KD60-REC.                                                  
                                                                                
           IF KD60-STATUS = 00 OR 02                                            
              ADD 1 TO WS-KD60-WRITE                                            
           ELSE                                                                 
              IF KD60-STATUS = 22                                               
                 DISPLAY ' BP13KD60 - RECORD EXISTS. SKIP TO NEXT STEP'         
              ELSE                                                              
                 DISPLAY ' BP13KD60-WRINTING ERROR, STATUS: '                   
                          KD60-STATUS                                           
                 MOVE KD60-STATUS        TO   RETURN-CODE                       
                 PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3770-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-WRITE-REPORT.                                                       
      *-------------------------------------------------------------            
           IF WS-LINE-CTR > 58                                                  
              PERFORM 5100-PRINT-HEADING THRU 5100-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE SPACES  TO LC85-DETAILS.                                        
           INITIALIZE      LC85-DETAILS.                                        
                                                                                
           MOVE SPACES                 TO LC85-BF-ARR-SIGN                      
                                          LC85-AF-ARR-SIGN.                     
                                                                                
           ADD   1                     TO WS-SNO.                               
           MOVE  WS-SNO                TO LC85-SNO.                             
           MOVE FD92-NUM-REGN          TO LC85-REGNO.                           
                                                                                
           IF WS-AMT-ARR-INSTAL-BF < 0                                          
           MOVE '-'                    TO LC85-BF-ARR-SIGN.                     
                                                                                
           MOVE WS-AMT-ARR-INSTAL-BF   TO LC85-BF-ARR-INST.                     
           MOVE WS-NUM-MTH-INSTAL-BF   TO LC85-BF-MTH-INST.                     
                                                                                
           IF WS-AMT-ARR-INSTAL-AF < 0                                          
           MOVE '-'                    TO LC85-AF-ARR-SIGN.                     
                                                                                
           MOVE WS-AMT-ARR-INSTAL-AF   TO LC85-AF-ARR-INST.                     
           MOVE WS-NUM-MTH-INSTAL-AF   TO LC85-AF-MTH-INST.                     
                                                                                
           MOVE  WS-CURRENT-DATE  TO  LC85-DATE-HDR.                            
                                                                                
                                                                                
           WRITE LC85-PRTREC FROM LC85-DETAILS.                                 
                                                                                
           ADD   1   TO   WS-LINE-CTR.                                          
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       5100-PRINT-HEADING.                                                      
      *-------------------------------------------------------------            
           ADD          1   TO WS-PAGE-CTR.                                     
           MOVE WS-PAGE-CTR TO LC85-PAGE-HDR.                                   
                                                                                
           MOVE  SPACES  TO LC85-PRTREC.                                        
           WRITE LC85-PRTREC AFTER PAGE.                                        
           WRITE LC85-PRTREC.                                                   
           WRITE LC85-PRTREC.                                                   
                                                                                
           WRITE LC85-PRTREC FROM LC85-HEADER01.                                
           WRITE LC85-PRTREC FROM LC85-HEADER02.                                
                                                                                
           MOVE  SPACES  TO LC85-PRTREC.                                        
           WRITE LC85-PRTREC.                                                   
                                                                                
           WRITE LC85-PRTREC FROM LC85-HEADER03.                                
                                                                                
           MOVE  SPACES  TO LC85-PRTREC.                                        
           WRITE LC85-PRTREC.                                                   
                                                                                
           MOVE  8 TO WS-LINE-CTR.                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
                                                                                
           CLOSE BP13FD92                                                       
                 BP13KD00                                                       
                 BP13KD60                                                       
                 BP13KD61                                                       
                 BP13KD92                                                       
                 BP13KD94                                                       
                 BP13KD99                                                       
                 BP13KD63                                                       
                 BP13LC85.                                                      
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13KD00, STATUS ' WS-KD00-STATUS          
              MOVE WS-KD00-STATUS        TO RETURN-CODE.                        
                                                                                
           IF KD60-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13KD60, STATUS ' KD60-STATUS             
              MOVE KD60-STATUS        TO RETURN-CODE.                           
                                                                                
           IF KD61-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13KD61, STATUS ' KD61-STATUS             
              MOVE KD61-STATUS        TO RETURN-CODE.                           
                                                                                
           IF WS-KD92-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13KD92, STATUS ' WS-KD92-STATUS          
              MOVE WS-KD92-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-KD94-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13KD94, STATUS ' WS-KD94-STATUS          
              MOVE WS-KD94-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-KD99-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13KD99, STATUS ' WS-KD99-STATUS          
              MOVE WS-KD99-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-KD63-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13KD63, STATUS ' WS-KD63-STATUS          
              MOVE WS-KD63-STATUS        TO RETURN-CODE.                        
                                                                                
           DISPLAY ' RUN   DATE :  ' WS-CURRENT-DATE.                           
           DISPLAY ' *------------ BP13CC85 AUDIT TRAIL ------------*'.         
           DISPLAY '    '.                                                      
           DISPLAY 'NO. OF BP13FD92 READ     : ' WS-FD92-READ.                  
           DISPLAY 'NO. OF BP13KD00 READ     : ' WS-KD00-READ.                  
           DISPLAY 'NO. OF BP13KD00 UPDATE   : ' WS-KD00-UPDATE.                
           DISPLAY 'NO. OF BP13KD99 WRITE    : ' WS-KD99-WRITE.                 
           DISPLAY 'NO. OF BP13KD92 READ     : ' WS-KD92-READ.                  
           DISPLAY 'NO. OF BP13KD92 DELETED  : ' WS-KD92-DEL.                   
           DISPLAY 'NO. OF BP13KD94 WRITE    : ' WS-KD94-WRITE.                 
           DISPLAY 'NO. OF BP13KD63 WRITE    : ' WS-KD63-WRITE.                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
