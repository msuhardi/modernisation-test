       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF18.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  22/09/10.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  TO CHECK FOR CASES THAT EXIST ON BP13K76D   *            
      *                IF FOUND BYPASS RECORD ELSE OUTPUT RECORD   *            
      *  INPUT FILE :  1. BP13F740                                 *            
      *                2. BP13K76D                                 *            
      *  OUTPUT FILE:  1. P13F740A                                 *            
      * ---------------------------------------------------------- *            
      * BP134024 ZDD 20100922   NEW PROGRAM                        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K76D ASSIGN TO BP13K76D                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K76D-KEY-FLD                               
                  FILE STATUS     IS WS-K76D-STATUS.                            
                                                                                
           SELECT P13F740A ASSIGN TO P13F740A.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F740                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F740.                                                           
                                                                                
       FD  P13F740A                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  P13F740A-REC  PIC X(500).                                            
                                                                                
       FD   BP13K76D                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K76D.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  NO-F740-READ        PIC 9(5)    VALUE ZEROS.                     
           05  NO-F740-EXIST       PIC 9(5)    VALUE ZEROS.                     
           05  NO-F740-NOT-EXIST   PIC 9(5)    VALUE ZEROS.                     
           05  WS-EOF              PIC X(1)    VALUE 'N'.                       
           05  WS-K76D-STATUS      PIC 9(2)    VALUE ZEROS.                     
           05  WS-FOUND            PIC X(1)    VALUE 'N'.                       
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPENING-ROUTINE  THRU 1000-EXIT.                        
           PERFORM 2000-READ-F740        THRU 2000-EXIT.                        
           PERFORM 4000-PROCESS-FILES    THRU 4000-EXIT                         
                   UNTIL WS-EOF = 'Y'.                                          
           PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F740                                                 
                       BP13K76D                                                 
                OUTPUT P13F740A.                                                
                                                                                
           IF WS-K76D-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K76D OPEN ERROR, STATUS IS ' WS-K76D-STATUS          
              MOVE WS-K76D-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-F740.                                                          
      *-------------------------------------------------------------            
            READ BP13F740 AT END                                                
                 MOVE 'Y' TO WS-EOF                                             
                 GO TO 2000-EXIT.                                               
            ADD 1 TO NO-F740-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           PERFORM 4200-PROCESS-RECORD  THRU 4200-EXIT                          
           PERFORM 2000-READ-F740 THRU 2000-EXIT.                               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4200-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
                                                                                
            MOVE SPACES       TO P13F740A-REC                                   
                                 BP13K76D-REC.                                  
                                                                                
            MOVE 'N'                      TO   WS-FOUND.                        
            MOVE F740-NUM-ALLO-CAT        TO   K76D-NUM-SALES-MODE.             
            MOVE F740-DTE-BALLOT          TO   K76D-NUM-DTE-BALLOT.             
            MOVE F740-NUM-REGN            TO   K76D-NUM-REGN.                   
                                                                                
            PERFORM 4300-READ-BP13K76D    THRU 4300-EXIT.                       
                                                                                
            IF WS-FOUND = 'N'                                                   
               WRITE P13F740A-REC FROM BP13F740-REC                             
               ADD 1 TO NO-F740-NOT-EXIST                                       
            ELSE                                                                
               ADD 1 TO NO-F740-EXIST                                           
            END-IF.                                                             
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4300-READ-BP13K76D.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K76D.                                                       
                                                                                
           EVALUATE WS-K76D-STATUS                                              
            WHEN 00                                                             
               MOVE 'Y' TO WS-FOUND                                             
            WHEN 23                                                             
               MOVE 'N' TO WS-FOUND                                             
            WHEN OTHER                                                          
              DISPLAY 'ERROR READING OF BP13K76D. STATUS IS '                   
                       WS-K76D-STATUS                                           
              MOVE WS-K76D-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           CLOSE P13F740A                                                       
                 BP13F740                                                       
                 BP13K76D.                                                      
                                                                                
           DISPLAY 'NO. OF F740 REC READ         > ' NO-F740-READ.              
           DISPLAY 'NO. OF F740 REC FOUND K76D   > ' NO-F740-EXIST.             
           DISPLAY 'NO. OF F740 REC NOT FND K76D > ' NO-F740-NOT-EXIST.         
                                                                                
           IF WS-K76D-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K76D CLOSE ERROR, STATUS IS ' WS-K76D-STATUS         
              MOVE WS-K76D-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
