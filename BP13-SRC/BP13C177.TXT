      *---------------------------------------------------------------*         
       IDENTIFICATION DIVISION.                                                 
      *---------------------------------------------------------------*         
       PROGRAM-ID. BP13C177.                                                    
      *AUTHOR. SK.                                                              
      *DATE-WRITTEN.   27/01/2005.                                              
      *----------------------------------------------------------------*        
      *                                                                *        
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                            *        
      *                                                                *        
      *  SYSTEM ID   : BP13                                            *        
      *                                                                *        
      *  OBJECTIVE  1: PROGRAM TO CHECK IF APPLICANTS HAS PPO          *        
      *                                                                *        
      * CHGE ID  OIC  DATE   DESCRIPTION                               *        
      * -------  ---  ----   -----------                               *        
      * BP132663 CKK  220205 NEW PGM                                   *        
      * BP136469 SMR2 031016 DROP CASES WHERE EX-PPO HAS BEEN DISPOSED *        
      *                      MORE THAN 30 MONTHS AGO                   *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F825   ASSIGN TO BP13F825.                                
           SELECT BP13K730 ASSIGN TO BP13K730                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K730-NUM-REGN                              
                  FILE STATUS     IS WS-K730-STATUS.                            
                                                                                
           SELECT BR10K010 ASSIGN TO BR10K010                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K010-KEY-FLD                               
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
           SELECT BR10K080 ASSIGN TO BR10K080                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K080-KEY-FLD                               
                  FILE STATUS     IS WS-K080-STATUS.                            
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K730.                                                           
                                                                                
       FD   BR10K010                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BR10K010.                                                           
                                                                                
       FD   BR10K080                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BR10K080.                                                           
                                                                                
       FD   BP13F825                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F825.                                                           
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
       01  WORK-AREAS.                                                          
           05 WS-K730-STATUS            PIC 9(2)    VALUE 0.                    
           05 WS-K010-STATUS            PIC 9(2)    VALUE 0.                    
           05 WS-K080-STATUS            PIC 9(2)    VALUE 0.                    
           05 WS-READ-F825              PIC 9(8)    VALUE 0.                    
           05 WS-EOF                    PIC X(1)    VALUE 'N'.                  
           05 WS-EOF1                   PIC X(1)    VALUE 'N'.                  
           05 WS-FND                    PIC X(1)    VALUE 'N'.                  
           05 WS-K730-UPD               PIC 9(8)    VALUE 0.                    
           05 WS-K010-FND               PIC 9(8)    VALUE 0.                    
           05 WS-K080-FND               PIC 9(8)    VALUE 0.                    
           05 WS-K730-NTFND             PIC 9(8)    VALUE 0.                    
           05 WS-DTE-DISP               PIC X(8)    VALUE SPACES.               
           05 WS-DTE-DISP-NUM REDEFINES WS-DTE-DISP                             
                                        PIC 9(8).                               
           05 WS-DTE-REQ                PIC X(8)    VALUE SPACES.               
           05 WS-DTE-REQ-R    REDEFINES WS-DTE-REQ.                             
              10 WS-DTE-REQ-NUM.                                                
                 15 WS-DTE-REQ-CCYY     PIC 9(4).                               
                 15 WS-DTE-REQ-MM       PIC 9(2).                               
                 15 WS-DTE-REQ-DD       PIC 9(2).                               
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY            PIC 9(4).                                 
           05  WS-SYS-MM              PIC 9(2).                                 
           05  WS-SYS-DD              PIC 9(2).                                 
                                                                                
       01  WS-EDITED-DATE             PIC X(10).                                
                                                                                
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
      *--------------------------------------------------------*                
       0000-MAIN.                                                               
      *--------------------------------------------------------*                
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 1500-READ-F825     THRU 1500-EXIT.                           
           PERFORM 2000-PROCESS       THRU 2000-EXIT                            
                   UNTIL WS-EOF = 'Y'.                                          
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILES.                                                         
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  BP13F825                                                 
                       BR10K010                                                 
                       BR10K080                                                 
                I-O    BP13K730.                                                
                                                                                
           EVALUATE WS-K730-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'OPEN BP13K730 FAIL. STATUS - '                        
                                            WS-K730-STATUS                      
                 MOVE  WS-K730-STATUS       TO RETURN-CODE                      
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K010-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'OPEN BR10K010 FAIL. STATUS - '                        
                                            WS-K010-STATUS                      
                 MOVE  WS-K010-STATUS       TO RETURN-CODE                      
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K080-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'OPEN BR10K080 FAIL. STATUS - '                        
                                            WS-K080-STATUS                      
                 MOVE  WS-K080-STATUS       TO RETURN-CODE                      
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1500-READ-F825.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F825                                                        
                AT END                                                          
                   MOVE 'Y'  TO WS-EOF                                          
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F825                                        
           END-READ.                                                            
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-PROCESS.                                                            
      *--------------------------------------------------------*                
                                                                                
           MOVE 'N'  TO WS-FND WS-EOF1.                                         
           PERFORM 2100-START-K010    THRU 2100-EXIT.                           
           IF WS-FND = 'Y'                                                      
              PERFORM 3400-READ-K730  THRU 3400-EXIT                            
              PERFORM 3500-UPDATE-K730 THRU 3500-EXIT                           
           ELSE                                                                 
              MOVE 'N'  TO WS-EOF1                                              
              PERFORM 2300-START-K080 THRU 2300-EXIT                            
              IF WS-FND = 'Y'                                                   
                 PERFORM 3400-READ-K730  THRU 3400-EXIT                         
                 PERFORM 3410-COMPUTE-REQ THRU 3410-EXIT                        
                 IF (WS-DTE-DISP = ZEROES OR SPACES OR LOW-VALUES) OR           
                    (WS-DTE-REQ = ZEROES OR SPACES OR LOW-VALUES) OR            
                    (WS-DTE-DISP-NUM >= WS-DTE-REQ-NUM)                         
                    PERFORM 3500-UPDATE-K730 THRU 3500-EXIT                     
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 1500-READ-F825     THRU 1500-EXIT.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2100-START-K010.                                                         
      *--------------------------------------------------------*                
                                                                                
           MOVE LOW-VALUES       TO K010-KEY-FLD.                               
                                                                                
           MOVE F825-NUM-NRIC    TO K010-NUM-UIN.                               
           START BR10K010 KEY >= K010-NUM-UIN.                                  
                                                                                
           EVALUATE WS-K010-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE 'Y' TO WS-EOF1                                            
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BR10K010 STATUS - '                    
                                            WS-K010-STATUS                      
                 MOVE WS-K010-STATUS        TO RETURN-CODE                      
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2200-READNXT-K010   THRU 2200-EXIT                           
              UNTIL K010-NUM-UIN NOT = F825-NUM-NRIC                            
                 OR WS-EOF1 = 'Y'.                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2200-READNXT-K010.                                                       
      *--------------------------------------------------------*                
                                                                                
           READ BR10K010 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-EOF1                                      
           END-READ.                                                            
                                                                                
           IF (K010-NUM-UIN = F825-NUM-NRIC)                                    
              MOVE 'Y' TO WS-FND WS-EOF1                                        
              ADD 1  TO WS-K010-FND                                             
           ELSE                                                                 
              MOVE 'Y' TO  WS-EOF1                                              
           END-IF.                                                              
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2300-START-K080.                                                         
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES           TO WS-DTE-DISP.                                
           MOVE LOW-VALUES       TO K080-KEY-FLD.                               
                                                                                
           MOVE F825-NUM-NRIC    TO K080-NUM-UIN.                               
           START BR10K080 KEY >= K080-NUM-UIN.                                  
                                                                                
           EVALUATE WS-K080-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE 'Y' TO WS-EOF1                                            
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BR10K080 STATUS - '                    
                                            WS-K080-STATUS                      
                 MOVE WS-K080-STATUS        TO RETURN-CODE                      
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2400-READNXT-K080   THRU 2400-EXIT                           
              UNTIL K080-NUM-UIN NOT = F825-NUM-NRIC                            
                 OR WS-EOF1 = 'Y'.                                              
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2400-READNXT-K080.                                                       
      *--------------------------------------------------------*                
                                                                                
           READ BR10K080 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-EOF1                                      
           END-READ.                                                            
                                                                                
           IF (K080-NUM-UIN = F825-NUM-NRIC)                                    
              MOVE 'Y' TO WS-FND WS-EOF1                                        
              ADD 1  TO WS-K080-FND                                             
              MOVE K080-DTE-DISPOSAL TO WS-DTE-DISP                             
           ELSE                                                                 
              MOVE 'Y' TO  WS-EOF1                                              
           END-IF.                                                              
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3400-READ-K730.                                                          
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES         TO WS-DTE-REQ.                                   
           MOVE SPACES         TO K730-NUM-REGN                                 
           MOVE F825-NUM-REGN  TO K730-NUM-REGN.                                
           READ BP13K730.                                                       
                                                                                
           IF WS-K730-STATUS = 00                                               
              MOVE K730-DTE-REQUEST TO WS-DTE-REQ                               
           ELSE                                                                 
           IF WS-K730-STATUS = 23                                               
              ADD  1      TO WS-K730-NTFND                                      
           ELSE                                                                 
              DISPLAY 'ERROR READING IN BP13K730, STATUS IS '                   
                       WS-K730-STATUS                                           
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3410-COMPUTE-REQ.                                                        
      *--------------------------------------------------------*                
                                                                                
           IF WS-DTE-REQ = ZEROES OR SPACES OR LOW-VALUES                       
              GO TO 3410-EXIT                                                   
           END-IF.                                                              
                                                                                
      * --------------------------------                                        
      * MINUS 30 MONTHS (2 YRS & 6 MOS)                                         
      * --------------------------------                                        
           COMPUTE WS-DTE-REQ-CCYY = WS-DTE-REQ-CCYY - 2.                       
                                                                                
           IF WS-DTE-REQ-MM > 6                                                 
              COMPUTE WS-DTE-REQ-MM = WS-DTE-REQ-MM - 6                         
           ELSE                                                                 
              IF WS-DTE-REQ-MM = 6                                              
                 COMPUTE WS-DTE-REQ-CCYY = WS-DTE-REQ-CCYY - 1                  
                 MOVE 12 TO WS-DTE-REQ-MM                                       
              ELSE                                                              
                 IF WS-DTE-REQ-MM < 6                                           
                    COMPUTE WS-DTE-REQ-CCYY = WS-DTE-REQ-CCYY - 1               
                    COMPUTE WS-DTE-REQ-MM = WS-DTE-REQ-MM + 12 - 6              
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3410-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3500-UPDATE-K730.                                                        
      *--------------------------------------------------------*                
                                                                                
           MOVE 'Y'  TO K730-NUM-PPO.                                           
                                                                                
           REWRITE BP13K730-REC.                                                
                                                                                
           IF WS-K730-STATUS = 00                                               
              ADD  1   TO WS-K730-UPD                                           
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING IN BP13K730, STATUS IS '                 
                       WS-K730-STATUS                                           
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           DISPLAY '*********** BP13C177 **************'.                       
           DISPLAY SPACES.                                                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS READ (BP13F825) :' WS-READ-F825.             
           DISPLAY 'NO. OF RECORDS NOT FND IN K730 :' WS-K730-NTFND.            
           DISPLAY 'NO. OF RECORDS UPDATE K730     :' WS-K730-UPD.              
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS FND IN BR01K010 :' WS-K010-FND.              
           DISPLAY 'NO. OF RECORDS FND IN BR01K080 :' WS-K080-FND.              
           DISPLAY '***********************************'.                       
                                                                                
           CLOSE       BP13K730                                                 
                       BR10K010                                                 
                       BR10K080                                                 
                       BP13F825.                                                
                                                                                
           EVALUATE WS-K730-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'CLOSE BP13K730 FAIL. STATUS - '                       
                                            WS-K730-STATUS                      
                 MOVE  WS-K730-STATUS       TO RETURN-CODE                      
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K010-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'CLOSE BR10K010 FAIL. STATUS - '                       
                                            WS-K010-STATUS                      
                 MOVE  WS-K010-STATUS       TO RETURN-CODE                      
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K080-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'CLOSE BR10K080 FAIL. STATUS - '                       
                                            WS-K080-STATUS                      
                 MOVE  WS-K080-STATUS       TO RETURN-CODE                      
           END-EVALUATE.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
