       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C36R.                                                 
      *AUTHOR.        ALWYN BENNY.                                              
      *DATE-WRITTEN.  08 SEP 2011.                                              
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *  OBJECTIVES : SEND EMAIL ALERT OF  EXCEPTIONAL CASES FROM   *           
      *               COLLECTION SYSTEM (SA/2R/3R)                  *           
      *                                                             *           
      *  INPUT FILES  - BP13F52A                                    *           
      *  OUTPUT FILES - BP13MAIL                                    *           
      * ----------------------------------------------------------- *           
      * CHG REF  BY    DATE       DESCRIPTION                       *           
      * -------- ----  ---------- -----------                       *           
      * BP134355 AB9   08/09/2011 NEW PROGRAM                       *           
      * BP134355 AB9   09/03/2012 cater for adp changes             *           
      * BP135201 PCL4  13/01/2014 TO CHANGE EMAIL RECIPIENT         *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F52A  ASSIGN        TO BP13F52A.                          
                                                                                
           SELECT BP13MAIL  ASSIGN        TO BP13MAIL.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F52A                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS  130 CHARACTERS.                                     
       COPY BP13F52A.                                                           
                                                                                
       FD  BP13MAIL                                                             
           RECORDING  MODE  IS F                                                
           RECORD CONTAINS  130 CHARACTERS.                                     
       01  MAIL-PRTREC                PIC X(130).                               
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  BP13F52A-EOF-TAG        PIC X(01).                               
               88  BP13F52A-EOF        VALUE 'Y'.                               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F52A-READ-CNT        PIC 9(06)     VALUE ZEROES.              
           05  WS-F52A-CNT             PIC 9(06)     VALUE ZEROES.              
           05  WS-MAIL-SEND-CNT        PIC 9(06)     VALUE ZEROES.              
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-DATE                 PIC X(08)     VALUE SPACES.              
           05  WS-EDIT-DATE            PIC X(10)     VALUE SPACES.              
           05  WS-NUM-OIC              PIC X(05)     VALUE SPACES.              
           05  WS-SNO                  PIC 9(03)     VALUE ZEROES.              
           05  WS-EDIT-CNT             PIC Z(05)9.                              
                                                                                
      *------------------------------------------------------------*            
      * EMAIL LAYOUT                                               *            
      *------------------------------------------------------------*            
       01  MAIL-HEADER-1.                                                       
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(118) VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-2.                                                       
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  SEND-ID         PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(106) VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-3.                                                       
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  TO-MAIL-ID-A    PIC X(04)  VALUE 'OML1'.                         
           05  FILLER          PIC X(12)  VALUE '@hdb.gov.sg>'.                 
                                                                                
       01  MAIL-HEADER-3A.                                                      
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  RCPT-ID         PIC X(05)  VALUE 'CCH16'.                        
           05  FILLER          PIC X(12)  VALUE '@hdb.gov.sg>'.                 
                                                                                
       01  MAIL-HEADER-3B.                                                      
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  RCPT-ID         PIC X(05)  VALUE 'CCK16'.                        
           05  FILLER          PIC X(12)  VALUE '@hdb.gov.sg>'.                 
                                                                                
       01  MAIL-HEADER-3C.                                                      
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  RCPT-ID         PIC X(05)  VALUE 'CLC11'.                        
           05  FILLER          PIC X(12)  VALUE '@hdb.gov.sg>'.                 
                                                                                
                                                                                
       01  MAIL-HEADER-3D.                                                      
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  RCPT-ID         PIC X(04)  VALUE 'KMK4'.                         
           05  FILLER          PIC X(12)  VALUE '@hdb.gov.sg>'.                 
                                                                                
       01  MAIL-HEADER-4.                                                       
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(126) VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-5.                                                       
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  FROM-MAIL-ID    PIC X(24)  VALUE                                 
               'SOC SYSTEM - EMAIL ALERT'.                                      
           05  FILLER          PIC X(101) VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-6.                                                       
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  TO-MAIL-ID-A    PIC X(04)  VALUE 'OML1'.                         
           05  FILLER          PIC X(12)  VALUE '@hdb.gov.sg>'.                 
           05  FILLER          PIC X(110) VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-7.                                                       
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  TO-MAIL-ID-B    PIC X(05)  VALUE 'CCH16'.                        
           05  FILLER          PIC X(12)  VALUE '@hdb.gov.sg>'.                 
           05  FILLER          PIC X(109) VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-8.                                                       
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  TO-MAIL-ID-B    PIC X(05)  VALUE 'CCK16'.                        
           05  FILLER          PIC X(12)  VALUE '@hdb.gov.sg>'.                 
           05  FILLER          PIC X(109) VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-9.                                                       
           05  FILLER          PIC X(04)  VALUE 'CC:<'.                         
           05  CC-MAIL-ID      PIC X(05)  VALUE 'CLC11'.                        
           05  FILLER          PIC X(12)  VALUE '@hdb.gov.sg>'.                 
           05  FILLER          PIC X(109) VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-10.                                                      
           05  FILLER          PIC X(04)  VALUE 'CC:<'.                         
           05  CC-MAIL-ID      PIC X(04)  VALUE 'KMK4'.                         
           05  FILLER          PIC X(12)  VALUE '@hdb.gov.sg>'.                 
           05  FILLER          PIC X(110) VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-10A     PIC X(41)                                        
           VALUE 'Mime-Version: 1.0                        '.                   
       01  MAIL-HEADER-10B          PIC X(130)                                  
           VALUE 'Content-type: multipart/mixed; boundary="Mime.Part.Bou        
      -     'ndary"'.                                                           
       01  MAIL-HEADER-10C          PIC X(130)                                  
           VALUE 'This is a multi-part message in MIME format.'.                
       01  MAIL-HEADER-10D          PIC X(130)                                  
           VALUE '--Mime.Part.Boundary                        '.                
       01  MAIL-HEADER-10E          PIC X(130)                                  
           VALUE 'Content-Type: text/plain; charset="iso-8859-1"'.              
       01  MAIL-HEADER-10F          PIC X(130)                                  
           VALUE 'Content-Transfer-Encoding: 7bit               '.              
       01  MAIL-HEADER-10G          PIC X(130)                                  
           VALUE 'Content-disposition: attachment;              '.              
       01  MAIL-HEADER-10H          PIC X(130)                                  
           VALUE '           FILENAME="UNSUCCESS.TXT"            '.             
                                                                                
       01  MAIL-HEADER-11.                                                      
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  SUBJECT-1       PIC X(39)  VALUE                                 
               'Mis-match Advance cash collection cases'.                       
           05  FILLER          PIC X(81)  VALUE SPACES.                         
                                                                                
                                                                                
       01  MAIL-BODY-TXT-1.                                                     
           05 Filler          PIC X(50)  VALUE                                  
              'S/No  Sch acc    Dte Txn   Txn id  Receipt no    '.              
           05 FILLER          PIC X(38)  VALUE                                  
              ' Amount     Cr/Dr Receipt code Regn no'.                         
           05 Filler          PIC X(42)  VALUE SPACES.                          
                                                                                
       01  MAIL-BODY-TXT-2.                                                     
           05 Filler          PIC X(47)  VALUE                                  
              '---- ---------   -------   ------  -----------'.                 
           05 FILLER          PIC X(41)  VALUE                                  
              '   ---------  ----- ------------ -------'.                       
           05 Filler          PIC X(42)  VALUE SPACES.                          
                                                                                
       01  MAIL-BODY-DTL-1.                                                     
           05 BODY-SNO        PIC Z(04)  VALUE SPACES.                          
           05 FILLER          PIC X(02)  VALUE SPACES.                          
           05 SCH-ACC         PIC X(11)  VALUE SPACES.                          
           05 FILLER          PIC X(01)  VALUE SPACES.                          
           05 DATE-TXN        PIC X(08)  VALUE SPACES.                          
           05 FILLER          PIC X(01)  VALUE SPACES.                          
           05 TXN-ID          PIC X(04)  VALUE SPACES.                          
           05 FILLER          PIC X(03)  VALUE SPACES.                          
           05 RECPT-NO        PIC X(14)  VALUE SPACES.                          
           05 FILLER          PIC X(01)  VALUE SPACES.                          
           05 AMOUNT          PIC ZZZZZZ99.99 VALUE SPACES.                     
           05 FILLER          PIC X(03)  VALUE SPACES.                          
           05 CR-DR           PIC X(1)   VALUE SPACES.                          
           05 FILLER          PIC X(05)  VALUE SPACES.                          
           05 RECPT-CDE       PIC X(5)   VALUE SPACES.                          
           05 FILLER          PIC X(06)  VALUE SPACES.                          
           05 REGN-NO         PIC X(8)   VALUE SPACES.                          
           05 FILLER          PIC X(42)  VALUE SPACES.                          
                                                                                
       01  MAIL-BODY-BLANK-LINE.                                                
           05 FILLER          PIC X(130) VALUE SPACES.                          
                                                                                
       01  MAIL-FOOTER-END.                                                     
           05 FILLER          PIC X      VALUE '.'.                             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13F52A     THRU 2000-EXIT.                       
           PERFORM 3000-START-PROCESS     THRU 3000-EXIT                        
                                          UNTIL BP13F52A-EOF.                   
      *    PERFORM 6000-END-PROCESS       THRU 6000-EXIT.                       
           WRITE MAIL-PRTREC    FROM MAIL-HEADER-10D.                           
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F52A                                                 
                OUTPUT BP13MAIL.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DATE.                        
           STRING WS-DATE(7:2) '/'                                              
                  WS-DATE(5:2) '/'                                              
                  WS-DATE(1:4)                                                  
                  DELIMITED BY SIZE        INTO WS-EDIT-DATE.                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F52A.                                                      
      *-------------------------------------------------------------            
           READ BP13F52A AT END                                                 
                MOVE    'Y'       TO   BP13F52A-EOF-TAG                         
                NOT AT END                                                      
                ADD      1        TO   WS-F52A-READ-CNT                         
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-START-PROCESS.                                                      
      *-------------------------------------------------------------            
            IF WS-F52A-READ-CNT = 1                                             
              MOVE ZEROES                    TO   WS-SNO                        
              PERFORM 4100-SETUP-HEADER      THRU 4100-EXIT                     
            END-IF.                                                             
              PERFORM 4300-SETUP-DETAIL      THRU 4300-EXIT                     
              ADD  1                         TO   WS-MAIL-SEND-CNT              
                                                                                
           PERFORM 2000-READ-BP13F52A        THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-SETUP-HEADER.                                                       
      *-------------------------------------------------------------            
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-1.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-2.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-3.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-3A.                         
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-3B.                         
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-3C.                         
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-3D.                         
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-4.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-5.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-6.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-7.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-8.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-9.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-10.                         
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-11.                         
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-10A.                        
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-10B.                        
           WRITE MAIL-PRTREC       FROM MAIL-BODY-BLANK-LINE.                   
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-10C.                        
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-10D.                        
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-10E.                        
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-10F.                        
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-10G.                        
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-10H.                        
           WRITE MAIL-PRTREC       FROM MAIL-BODY-TXT-1.                        
           WRITE MAIL-PRTREC       FROM MAIL-BODY-TXT-2.                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4300-SETUP-DETAIL.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES               TO   MAIL-BODY-DTL-1.                      
           ADD 1                     TO   WS-SNO.                               
           MOVE WS-SNO               TO   BODY-SNO.                             
           MOVE F52A-NUM-HDBREF      TO   SCH-ACC.                              
           MOVE F52A-DTE-TXN         TO   DATE-TXN.                             
           MOVE F52A-NUM-TXN-ID      TO   TXN-ID.                               
           MOVE F52A-NUM-RCPT        TO   RECPT-NO.                             
           MOVE F52A-AMT-TXN         TO   AMOUNT.                               
           MOVE F52A-NUM-CRDR        TO   CR-DR.                                
           MOVE F52A-NUM-RCPTCD      TO   RECPT-CDE.                            
           MOVE F52A-NUM-REF         TO   REGN-NO.                              
           WRITE MAIL-PRTREC         FROM MAIL-BODY-DTL-1.                      
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4500-SETUP-FOOTER.                                                       
      *-------------------------------------------------------------            
           WRITE MAIL-PRTREC         FROM MAIL-BODY-BLANK-LINE.                 
           WRITE MAIL-PRTREC         FROM MAIL-FOOTER-END.                      
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-END-PROCESS.                                                        
      *-------------------------------------------------------------            
           IF WS-MAIL-SEND-CNT > 0                                              
              PERFORM 4500-SETUP-FOOTER    THRU 4500-EXIT                       
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY ' BP13C36R CONTROL LIST    DATE : ' WS-EDIT-DATE.            
           DISPLAY '*------------------------------------------*'.              
           DISPLAY ' '.                                                         
           MOVE WS-F52A-READ-CNT     TO WS-EDIT-CNT.                            
           DISPLAY 'CASES READ FROM BP13F52A    : ' WS-EDIT-CNT.                
           MOVE WS-MAIL-SEND-CNT     TO WS-EDIT-CNT.                            
           DISPLAY 'CASES WRITTEN TO EMAIL FILE : ' WS-EDIT-CNT.                
           DISPLAY ' '.                                                         
           DISPLAY '*------------------------------------------*'.              
           DISPLAY ' '.                                                         
                                                                                
           CLOSE  BP13F52A                                                      
                  BP13MAIL.                                                     
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
