       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CA01.                                                 
       AUTHOR.        LMS.                                                      
      *DATE-WRITTEN.  09/02/2006.                                               
                                                                                
      ******************************************************************        
      *              SYSTEM OF COMMITMENT (BP13)                       *        
      *================================================================*        
      * OBJECTIVES : TO PRINT REPORT ON FLATLIST FOR FLAT APPORTIONMENT*        
      *================================================================*        
      * FILE DESCRIPTION :-                                            *        
      *                                                                *        
      * DD NAME   I/O/E ACCESSMODE    RECLEN KEY        REMARKS        *        
      * -------   ----- ----------    ----------        -------        *        
      * BP13FA70  I     SEQUENTIAL    80                               *        
      * BP13LA01  O                   132               LISTING        *        
      *================================================================*        
      * MODIFICATIONS :                                                *        
      *                                                                *        
      * CHGE REQ #  DATE       AUTHOR REMARKS                          *        
      * ----------  ----       ------ -------                          *        
      * BP132789    09/02/2006 LMS    NEW PROGRAM.                     *        
      * BP133932    03/09/2010 PCL3   TO CATER FOR HANDLING FT OF EC   *        
      *                               OR DBSS CASES                    *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FA70   ASSIGN       TO BP13FA70.                          
           SELECT BP13LA01   ASSIGN       TO BP13LA01.                          
                                                                                
      ****************************************************************          
       DATA DIVISION.                                                           
      ****************************************************************          
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13FA70     BLOCK CONTAINS 0 RECORDS                               
                         RECORD CONTAINS 80 CHARACTERS                          
                         LABEL RECORDS ARE STANDARD                             
                         RECORDING MODE IS F.                                   
       COPY BP13FA70.                                                           
                                                                                
       FD   BP13LA01     RECORD CONTAINS 132 CHARACTERS                         
                         LABEL RECORDS ARE STANDARD                             
                         RECORDING MODE IS F.                                   
                                                                                
       01  BP13LA01-REC           PIC X(132).                                   
                                                                                
      ****************************************************************          
       WORKING-STORAGE SECTION.                                                 
      ****************************************************************          
                                                                                
       01  LA01-HEADER-1.                                                       
           05 FILLER               PIC X(08) VALUE 'BP13LA01'.                  
           05 FILLER               PIC X(06) VALUE SPACES.                      
           05 FILLER               PIC X(04) VALUE 'HDB3'.                      
           05 FILLER               PIC X(30) VALUE SPACES.                      
           05 FILLER               PIC X(20)                                    
              VALUE 'SYSTEM OF COMMITMENT'.                                     
           05 FILLER               PIC X(31) VALUE SPACES.                      
           05 FILLER               PIC X(07) VALUE 'DATE : '.                   
           05 LA01-DATE            PIC X(10) VALUE SPACES.                      
           05 FILLER               PIC X(03) VALUE SPACES.                      
           05 FILLER               PIC X(07) VALUE 'PAGE : '.                   
           05 LA01-PAGENO          PIC ZZZ9.                                    
                                                                                
       01  LA01-HEADER-2.                                                       
           05 FILLER               PIC X(36) VALUE SPACES.                      
           05 FILLER               PIC X(44)                                    
              VALUE "      FLAT LIST BEFORE BALLOTING          ".               
           05 FILLER               PIC X(19) VALUE SPACES.                      
           05 FILLER               PIC X(14) VALUE 'USER: SOC TEAM'.            
           05 FILLER               PIC X(19) VALUE SPACES.                      
                                                                                
       01  LA01-HEADER-3.                                                       
           05 FILLER               PIC X(04) VALUE 'S/NO'.                      
           05 FILLER               PIC X(01) VALUE SPACE.                       
           05 FILLER               PIC X(09) VALUE 'PROJ CODE'.                 
           05 FILLER               PIC X(01) VALUE SPACE.                       
           05 FILLER               PIC X(06) VALUE 'BLK NO'.                    
           05 FILLER               PIC X(05) VALUE SPACE.                       
           05 FILLER               PIC X(07) VALUE 'UNIT NO'.                   
           05 FILLER               PIC X(03) VALUE SPACE.                       
           05 LA01-FT-HDR3         PIC X(09) VALUE 'UNIT TYPE'.                 
           05 FILLER               PIC X(87) VALUE SPACE.                       
                                                                                
       01  LA01-HEADER-LINE.                                                    
           05 FILLER               PIC X(04) VALUE '----'.                      
           05 FILLER               PIC X(01) VALUE SPACE.                       
           05 FILLER               PIC X(09) VALUE '---------'.                 
           05 FILLER               PIC X(01) VALUE SPACE.                       
           05 FILLER               PIC X(06) VALUE '------'.                    
           05 FILLER               PIC X(05) VALUE SPACE.                       
           05 FILLER               PIC X(07) VALUE '-------'.                   
           05 FILLER               PIC X(03) VALUE SPACE.                       
           05 LA01-FT-HDR          PIC X(09) VALUE '---------'.                 
           05 FILLER               PIC X(87) VALUE SPACE.                       
                                                                                
       01  LA01-DATA.                                                           
           05 LA01-SRNO            PIC ZZZ9.                                    
           05 FILLER               PIC X(01) VALUE SPACE.                       
           05 LA01-PRJCT           PIC X(03) VALUE SPACES.                      
           05 FILLER               PIC X(07) VALUE SPACES.                      
           05 LA01-BLOCK           PIC X(10) VALUE SPACES.                      
           05 FILLER               PIC X(01) VALUE SPACES.                      
           05 LA01-UNIT            PIC X(10) VALUE SPACES.                      
           05 FILLER               PIC X(02) VALUE SPACES.                      
           05 LA01-UNIT-TYPE       PIC X(06) VALUE SPACES.                      
           05 FILLER               PIC X(88) VALUE SPACES.                      
                                                                                
       01  WS-CUR-DATE.                                                         
           05 WS-CCYY              PIC 9(04).                                   
           05 WS-MM                PIC 9(02).                                   
           05 WS-DD                PIC 9(02).                                   
                                                                                
       01  WS-EDT-DATE.                                                         
           05 WS-EDT-DD            PIC 9(02).                                   
           05 FILLER               PIC X(01) VALUE '/'.                         
           05 WS-EDT-MM            PIC 9(02).                                   
           05 FILLER               PIC X(01) VALUE '/'.                         
           05 WS-EDT-CCYY          PIC 9(04).                                   
                                                                                
       01  WS-FILE-COUNTERS.                                                    
           05 BP13FA70-CNT-READ    PIC 9(08) VALUE ZEROES.                      
           05 BP13LA01-CNT-WRITE   PIC 9(08) VALUE ZEROES.                      
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05 BP13FA70-EOF         PIC X(01) VALUE SPACES.                      
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-REC-CNT           PIC 9(08) VALUE ZEROES.                      
           05 WS-PAGE-CNT          PIC 9(08) VALUE ZEROES.                      
           05 WS-PRJCT-CNT         PIC 9(04) VALUE ZEROES.                      
           05 WS-LINE-CNT          PIC 9(02) VALUE ZEROES.                      
                                                                                
       01  WS-OTHERS.                                                           
           05 WS-PROJECT           PIC X(3)  VALUE SPACES.                      
           05 WS-TMP               PIC Z(07)9.                                  
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
                                                                                
      *==============================================================*          
       0000-CONTROL.                                                            
      *==============================================================*          
                                                                                
           PERFORM 1000-OPEN-FILES    THRU  1000-EXIT.                          
           PERFORM 2000-MAIN-ROUTINE  THRU  2000-EXIT                           
                                      UNTIL BP13FA70-EOF = 'Y'.                 
           PERFORM 9000-CLOSE-FILES   THRU  9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==============================================================*          
       1000-OPEN-FILES.                                                         
      *==============================================================*          
                                                                                
           OPEN INPUT  BP13FA70                                                 
                OUTPUT BP13LA01.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
                                                                                
           MOVE WS-CCYY TO WS-EDT-CCYY.                                         
           MOVE WS-MM   TO WS-EDT-MM.                                           
           MOVE WS-DD   TO WS-EDT-DD.                                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==============================================================*          
       2000-MAIN-ROUTINE.                                                       
      *==============================================================*          
                                                                                
           READ BP13FA70 AT END                                                 
                MOVE 'Y' TO BP13FA70-EOF                                        
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO BP13FA70-CNT-READ.                                          
           PERFORM 3000-PROCESS-REC    THRU 3000-EXIT.                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==============================================================*          
       3000-PROCESS-REC.                                                        
      *==============================================================*          
                                                                                
           IF WS-PROJECT NOT = FA70-NUM-PRJCT OR                                
              WS-LINE-CNT > 55                                                  
              MOVE 0 TO WS-LINE-CNT                                             
              ADD  1 TO WS-PAGE-CNT                                             
              MOVE WS-EDT-DATE           TO LA01-DATE                           
              MOVE WS-PAGE-CNT           TO LA01-PAGENO                         
                                                                                
              IF FA70-NUM-PRJCT(1:1) = 'E'                                      
                 MOVE SPACES             TO LA01-FT-HDR                         
                                            LA01-FT-HDR3                        
              ELSE                                                              
                 MOVE '---------'        TO LA01-FT-HDR                         
                 MOVE 'UNIT TYPE'        TO LA01-FT-HDR3                        
              END-IF                                                            
                                                                                
              WRITE BP13LA01-REC         FROM LA01-HEADER-1 AFTER PAGE          
              WRITE BP13LA01-REC         FROM LA01-HEADER-2                     
              MOVE SPACES                TO BP13LA01-REC                        
              WRITE BP13LA01-REC                                                
              WRITE BP13LA01-REC         FROM LA01-HEADER-3                     
              WRITE BP13LA01-REC         FROM LA01-HEADER-LINE                  
              ADD 5 TO WS-LINE-CNT                                              
           END-IF.                                                              
                                                                                
           IF WS-PROJECT NOT = FA70-NUM-PRJCT                                   
              MOVE 0 TO WS-PRJCT-CNT                                            
              MOVE FA70-NUM-PRJCT TO WS-PROJECT                                 
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-PRJCT-CNT.                                               
           MOVE SPACES                   TO LA01-DATA.                          
           MOVE WS-PRJCT-CNT             TO LA01-SRNO.                          
           MOVE FA70-NUM-PRJCT           TO LA01-PRJCT.                         
           MOVE FA70-NUM-BLOCK           TO LA01-BLOCK.                         
           STRING FA70-NUM-LEVEL '-' FA70-NUM-MAIN-UNIT                         
                  FA70-NUM-SUB-UNIT                                             
           DELIMITED BY SIZE                                                    
             INTO LA01-UNIT.                                                    
           MOVE FA70-NUM-DESIGN-TYPE     TO LA01-UNIT-TYPE.                     
           WRITE BP13LA01-REC            FROM LA01-DATA.                        
           ADD 1 TO WS-LINE-CNT                                                 
           ADD 1 TO BP13LA01-CNT-WRITE.                                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==============================================================*          
       9000-CLOSE-FILES.                                                        
      *==============================================================*          
                                                                                
           DISPLAY '================================================='          
           DISPLAY ' PROGRAM-ID : BP13CA01         DATE : ' WS-EDT-DATE.        
           DISPLAY '================================================='          
           DISPLAY ' '.                                                         
           MOVE     BP13FA70-CNT-READ     TO   WS-TMP.                          
           DISPLAY 'NO OF RECORDS READ    FROM FA70 : ' WS-TMP.                 
           MOVE     BP13LA01-CNT-WRITE    TO   WS-TMP.                          
           DISPLAY 'NO OF RECORDS WRITTEN INTO LA01 : ' WS-TMP.                 
           DISPLAY ' '.                                                         
           DISPLAY '================================================='.         
                                                                                
           CLOSE BP13FA70                                                       
                 BP13LA01.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
