      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C26D.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
       DATE-WRITTEN.  FEBRUARY 8 2018.                                          
      *===============================================================*         
      * OBJECTIVE  :  FILTER OUT DEBARRED CASE IN BP13.K010.DEBAR     *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :  P04F010                                         *         
      *            :  P04K010                                         *         
      * OUTPUT FILE:  P04F010A                                        *         
      *===============================================================*         
      * CHG REQ  DATE       BY   DESCRIPTION                          *         
      *--------- ---------- ---- -------------------------------------*         
      * BP137196 08/02/2018 ESA1 NEW PROGRAM                          *         
      * BP138645 15/04/2021 KR13 GET K820 CSTMR_SRC FOR F010.         *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  P04F010   ASSIGN        TO P04F010.                          
                                                                                
           SELECT  P04K010   ASSIGN        TO P04K010                           
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K010-PRIME-KEY                    
                             FILE STATUS   IS WS-K010-STATUS.                   
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                    ALTERNATE RECORD KEY IS K820-NUM-NRIC                       
                                            WITH DUPLICATES                     
                            FILE STATUS  IS WS-K820-STATUS.                     
                                                                                
           SELECT  P04F010A  ASSIGN        TO P04F010A.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   P04F010                                                             
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY P04F010.                                                            
                                                                                
       FD   P04K010                                                             
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY P04K010.                                                            
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   P04F010A                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   P04F010A-REC      PIC X(300).                                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F010             PIC X(01) VALUE 'N'.                     
           05  WS-EOF-K010             PIC X(01) VALUE 'N'.                     
           05  WS-K010-FND             PIC X(01) VALUE 'N'.                     
           05  WS-K010-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K820-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F010-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-P04K             PIC 9(7)  VALUE ZEROES.                  
           05  WS-P04K-FND             PIC 9(7)  VALUE ZEROES.                  
           05  WS-P04K-NFND            PIC 9(7)  VALUE ZEROES.                  
                                                                                
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-P04F010       THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F010 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  P04F010                                                  
                       P04K010                                                  
                       BP13K820                                                 
                OUTPUT P04F010A.                                                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'P04K010 - ERROR OPENING : ' WS-K010-STATUS               
              MOVE WS-K010-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'OPEN ERROR OCCURED FOR BP13K820'                         
              DISPLAY 'FILE STATUS :' WS-K820-STATUS                            
              MOVE WS-K820-STATUS         TO RETURN-CODE                        
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-READ-P04F010.                                                       
      ******************************************************************        
                                                                                
           READ P04F010            AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F010                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F010-READ.                             
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 4000-START-P04K010    THRU 4000-EXIT.                        
                                                                                
           IF WS-K010-FND = 'Y'                                                 
              ADD 1               TO WS-P04K-FND                                
              CONTINUE                                                          
           ELSE                                                                 
              ADD 1               TO WS-P04K-NFND                               
              PERFORM 3500-READ-BP13K820 THRU 3500-EXIT                         
              MOVE P04F010-REC    TO P04F010A-REC                               
              WRITE P04F010A-REC                                                
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-P04F010     THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3500-READ-BP13K820.                                                      
      ******************************************************************        
           INITIALIZE  BP13K820-REC                                             
                                                                                
           MOVE SPACES                     TO K820-KEY-FLD                      
           MOVE F010-NUM-UINFIN(1:9)       TO K820-NUM-NRIC                     
                                                                                
           START BP13K820 KEY >= K820-NUM-NRIC                                  
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    READ BP13K820 NEXT                                          
                    IF WS-K820-STATUS = 00 OR 02                                
                       IF K820-NUM-NRIC = F010-NUM-UINFIN(1:9)                  
                          MOVE K820-NUM-CSTMR-SOURCE                            
                                            TO F010-NUM-CSTMR-SOURCE            
                       END-IF                                                   
                    END-IF                                                      
               WHEN 23                                                          
                    DISPLAY 'KEY NOT FOUND IN BP13K820!' K820-KEY-FLD           
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K820!'                          
                             ' / KEY : ' K820-KEY-FLD                           
                             ' / FILE STATUS :' WS-K820-STATUS                  
                    PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT               
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       4000-START-P04K010.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                              TO P04K010-REC.             
           INITIALIZE                                  P04K010-REC.             
                                                                                
           MOVE 'N'                                 TO WS-EOF-K010              
                                                       WS-K010-FND.             
           MOVE ZEROES                              TO WS-CNT-P04K.             
           MOVE SPACES                              TO K010-PRIME-KEY.          
           MOVE F010-NUM-UINFIN                     TO K010-NUM-UINFIN.         
                                                                                
           START P04K010  KEY = K010-NUM-UINFIN.                                
                                                                                
           EVALUATE WS-K010-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 4100-READNEXT-P04K010  THRU 4100-EXIT               
                    PERFORM 4200-PROCESS-P04K010   THRU 4200-EXIT               
                            UNTIL WS-EOF-K010 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'START ERROR P04K010 : ' WS-K010-STATUS             
                            ' NRIC : ' K010-NUM-UINFIN                          
                    PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT               
            END-EVALUATE.                                                       
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       4100-READNEXT-P04K010.                                                   
      ******************************************************************        
                                                                                
           READ P04K010 NEXT RECORD                                             
                         AT END MOVE 'Y' TO WS-EOF-K010.                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4200-PROCESS-P04K010.                                                    
      ******************************************************************        
                                                                                
           IF K010-NUM-UINFIN = F010-NUM-UINFIN                                 
              IF K010-REASON-CODE   = F010-REASON-CODE   AND                    
                 K010-EX-SCH-ACCTNO = F010-EX-SCH-ACCTNO AND                    
                 K010-LSE-TEN-CODE  = F010-LSE-TEN-CODE  AND                    
                 K010-EFF-DATE      = F010-EFF-DATE                             
                 MOVE 'Y'       TO  WS-K010-FND                                 
                                    WS-EOF-K010                                 
                 DISPLAY 'DUP=' K010-PRIME-KEY                                  
              ELSE                                                              
                PERFORM 4100-READNEXT-P04K010 THRU 4100-EXIT                    
              END-IF                                                            
           ELSE                                                                 
               MOVE 'Y'                       TO WS-EOF-K010                    
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C26D'.                                     
           DISPLAY '----------------------------------------------'.            
           DISPLAY '(1) NO OF P04F010 RECORDS READ               : '            
                    WS-F010-READ.                                               
           DISPLAY '(2) NO OF P04K010  RECORDS FOUND............ : '            
                    WS-P04K-FND.                                                
           DISPLAY '(3) NO OF P04K010  RECORDS NOT FOUND........ : '            
                    WS-P04K-NFND.                                               
                                                                                
           CLOSE    P04F010                                                     
                    P04K010                                                     
                    BP13K820                                                    
                    P04F010A.                                                   
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'P04K010  - ERROR CLOSING : ' WS-K010-STATUS              
              MOVE WS-K010-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING FILE : BP13K820!'                          
              DISPLAY 'FILE-STATUS :' WS-K820-STATUS                            
           END-IF                                                               
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
