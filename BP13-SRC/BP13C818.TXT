       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C818.                                                    
      *AUTHOR. LSB.                                                             
      *DATE-WRITTEN : 31.08.94                                                  
      *----------------------------------------------------------------*        
      *                                                                *        
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                            *        
      *                                                                *        
      *  SYSTEM ID   : BP13                                            *        
      *                                                                *        
      *  OBJECTIVE   : PROGRAM BASED ON F717  REFORMATTED FILE MATCH   *        
      *                WITH EXTRACTED RDB RECORD TO OUTPUT NOT MATCH   *        
      *                CASES.                                          *        
      *                                                                *        
      *----------------------------------------------------------------*        
      * MODIFICATIONS                                                  *        
      * -------------                                                  *        
      * CHG-REQ  OIC   DATE     DESCRIPTION                            *        
      * --------------------------------------                         *        
      * BP130261 YCH2 040497  BREAKDOWN OF RFS APPLNS                  *        
      * BP130630 NOR  030199  CHGE F715 F716 EXPAND TO 350             *        
      *               070699  CHGE F717 F716 EXPAND TO 350             *        
      *----------------------------------------------------------------*        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT P13F716A ASSIGN TO P13F716A.                                  
           SELECT BP13F716 ASSIGN TO BP13F716.                                  
           SELECT P13F716O ASSIGN TO P13F716O.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F716                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 350 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F716.                                                           
                                                                                
       FD  P13F716A                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 350 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       01   P13F716A-REC.                                                       
            05  F716A-NUM-REGN                PIC X(08).                        
            05  F716A-NUM-NRIC                PIC X(09).                        
            05  FILLER                        PIC X(333).                       
                                                                                
       FD  P13F716O                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 350 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       01  P13F716O-REC            PIC X(350).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F716-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F716A-READ           PIC 9(8)    VALUE ZERO.                      
       01  WS-OUTPUT               PIC 9(8)    VALUE ZERO.                      
       01  WS-F716-WRITE           PIC 9(8)    VALUE ZERO.                      
       01  WS-F716-BYPASS          PIC 9(8)    VALUE ZERO.                      
       01  WS-F716A-WRITE          PIC 9(8)    VALUE ZERO.                      
       01  WS-NUM-NRIC             PIC X(9)    VALUE SPACES.                    
                                                                                
       77  WS-EOF-F716             PIC X(3).                                    
           88  EOF-OF-F716                     VALUE 'YES'.                     
       77  WS-EOF-F716A            PIC X(3).                                    
           88  EOF-OF-F716A                    VALUE 'YES'.                     
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       000-MAIN-LOGIC.                                                          
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F716 THRU 200-EXIT.                                 
           PERFORM 300-READ-F716A THRU 300-EXIT.                                
           PERFORM 400-PROCESS   THRU 400-EXIT                                  
                   UNTIL EOF-OF-F716 AND EOF-OF-F716A.                          
           PERFORM 900-FINALIZATION THRU 900-EXIT.                              
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
           OPEN INPUT  P13F716A                                                 
                       BP13F716                                                 
                OUTPUT P13F716O.                                                
           MOVE 'NO' TO WS-EOF-F716.                                            
           MOVE 'NO' TO WS-EOF-F716A.                                           
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F716.                                                           
           READ BP13F716 AT END MOVE 'YES' TO WS-EOF-F716                       
                                MOVE HIGH-VALUE TO F716-NUM-NRIC                
               GO TO 200-EXIT.                                                  
            ADD 1 TO WS-F716-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-F716A.                                                          
           READ P13F716A AT END MOVE 'YES' TO WS-EOF-F716A                      
                                MOVE HIGH-VALUE TO F716A-NUM-NRIC               
               GO TO 300-EXIT.                                                  
            ADD 1 TO WS-F716A-READ.                                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *  MATCH  BY REF-NO                                             *         
      *****************************************************************         
                                                                                
       400-PROCESS.                                                             
                                                                                
           IF F716-NUM-NRIC = F716A-NUM-NRIC                                    
              WRITE P13F716O-REC FROM BP13F716-REC                              
              ADD 1 TO WS-OUTPUT                                                
              PERFORM 410-READ-F716 THRU 410-EXIT                               
                UNTIL F716-NUM-NRIC NOT = F716A-NUM-NRIC                        
              PERFORM 300-READ-F716A THRU 300-EXIT                              
           ELSE                                                                 
              IF F716-NUM-NRIC   < F716A-NUM-NRIC                               
                 DISPLAY 'F716 NOT IN F716A ' F716-NUM-NRIC                     
                 MOVE F716-NUM-NRIC TO WS-NUM-NRIC                              
                 PERFORM 410-READ-F716 THRU 410-EXIT                            
                   UNTIL F716-NUM-NRIC NOT = WS-NUM-NRIC                        
              ELSE                                                              
                 MOVE SPACES              TO P13F716O-REC                       
                 WRITE P13F716O-REC FROM P13F716A-REC                           
                 ADD 1 TO WS-F716A-WRITE                                        
                 PERFORM 300-READ-F716A THRU 300-EXIT.                          
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       410-READ-F716.                                                           
           PERFORM 200-READ-F716 THRU 200-EXIT.                                 
           IF F716-NUM-NRIC = F716A-NUM-NRIC                                    
              ADD 1 TO WS-F716-BYPASS                                           
              GO TO 410-READ-F716.                                              
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-FINALIZATION.                                                        
             DISPLAY '*--------BP13C818  -----------*'.                         
             DISPLAY '* MATCH TO EXTRACT NOT FOUND IN RDB'.                     
             DISPLAY ' '.                                                       
             DISPLAY ' P13F716A-READ      ' WS-F716A-READ.                      
             DISPLAY ' BP13F716-READ      ' WS-F716-READ.                       
             DISPLAY ' MATCH              ' WS-OUTPUT.                          
             DISPLAY ' F716-DUPLICATED    ' WS-F716-BYPASS.                     
             DISPLAY ' F716 WRITE         ' WS-F716-WRITE.                      
             DISPLAY ' F716A WRITE         ' WS-F716A-WRITE.                    
                                                                                
           CLOSE      P13F716A                                                  
                      P13F716O                                                  
                      BP13F716.                                                 
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
