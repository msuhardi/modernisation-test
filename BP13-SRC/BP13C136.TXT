      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13C136.                                                 
      *AUTHOR.        ERWIN GAW.                                                
      *DATE-WRITTEN.  29/03/96.                                                 
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME  :    SYSTEM OF COMMITMENT   (SOC)               *        
      *                                                                *        
      *   SYSTEM ID    :    BP13                                       *        
      *                                                                *        
      *   OBJECTIVE    :    TO PRINT LISTING OF SUB-TRANSFER CASES >   *        
      *                     2 MONTHS OLD AS AT RUN-DATE FROM DATE OF   *        
      *                     AGREEMENT ORDER.                           *        
      *                                                                *        
      *   INPUT FILES  :    PRTFILE                                    *        
      *                     BP13K022                                   *        
      *                                                                *        
      *   REPORT       :    1. BP13L136                                *        
      *                                                                *        
      *   MODIFICATION HISTORY:                                        *        
      *   =====================                                        *        
      *   CHGE-NO  BY    DATE    DESCRIPTION                           *        
      *   -------  ---  ------   ------------------------------------  *        
      *  BP130103  EG1  29031996 TO CATER FOR CHANGES ARISING FROM     *        
      *                          SUB-TRANSFER MODULE.                  *        
      *  BP130227  RHB  06011997 TO PRINT FOR SUSPENSE > 0 ONLY.       *        
      *  BP130418  CLT  25081998 TO CATER FOR Y2K.                     *        
      *  BP130852  AKT  19052000 PRINT TOTAL NO OF CASES & GRAND TOTAL.*        
      *  BP130883  RAS  03082000 TO CHANGE BP13C904 TO BP13C913        *        
      *  BP132481  MJ16 16022004 TO INCLUDE OIC IN THE REPORT          *        
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT    DIVISION.                                                 
      ******************************************************************        
      *================================================================*        
       CONFIGURATION  SECTION.                                                  
      *================================================================*        
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
      *================================================================*        
       INPUT-OUTPUT SECTION.                                                    
      *================================================================*        
      *----------------------------------------------------------------*        
       FILE-CONTROL.                                                            
      *----------------------------------------------------------------*        
                                                                                
           SELECT PRTFILE   ASSIGN       TO PRTFILE.                            
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS          IS RANDOM                                     
                  RECORD KEY      IS K022-KEY-FLD                               
                  ALTERNATE KEY   IS K022-NUM-REGN                              
                  FILE STATUS     IS K022-STATUS.                               
                                                                                
           SELECT BP13L136  ASSIGN       TO BP13L136.                           
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *================================================================*        
       FILE SECTION.                                                            
      *================================================================*        
                                                                                
       FD   PRTFILE                                                             
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 420 CHARACTERS.                                     
       01   IN-REC.                                                             
            05 IN-K885-REC               PIC X(400).                            
            05 IN-FSA-DATE               PIC X(08).                             
            05 IN-AGE-MONTHS             PIC 9(02).                             
            05 IN-AGE-DAYS               PIC 9(02).                             
            05 FILLER                    PIC X(08).                             
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13L136                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   L136-REC            PIC X(132).                                     
                                                                                
                                                                                
      *================================================================*        
       WORKING-STORAGE SECTION.                                                 
      *================================================================*        
      *----------------------------------------------------------------*        
      *     ERROR VARIABLES                                            *        
      *----------------------------------------------------------------*        
       01  WS-ERR-MSG                     PIC X(80).                            
                                                                                
       COPY BP13F885.                                                           
                                                                                
      *----------------------------------------------------------------*        
      *---LINKAGE-VARIABLES-PASS-TO-SUB-PROGRAM--BP13C913--------------*        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH      PIC X(4)  VALUE SPACES.                 
               10  WS-LINK-NUM-ACC      PIC X(5)  VALUE SPACES.                 
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *----------------------------------------------------------------*        
      *     FLAG VARIABLES                                             *        
      *----------------------------------------------------------------*        
       01  WS-FLAGS.                                                            
           05  WS-PRTFILE-EOF             PIC X(1)   VALUE 'N'.                 
                                                                                
      *----------------------------------------------------------------*        
      *    COUNTER VARIABLES                                           *        
      *----------------------------------------------------------------*        
       01  WS-COUNTERS.                                                         
           05  WS-CNT-PRTFILE-READ        PIC 9(8)   VALUE ZEROES.              
           05  WS-CNT-L136-WRITE          PIC 9(8)   VALUE ZEROES.              
                                                                                
      *----------------------------------------------------------------*        
      *    PRINTING VARIABLES                                          *        
      *----------------------------------------------------------------*        
       01  WS-PRINT-VARIABLES.                                                  
           05  WS-SEQ-CNT                 PIC 9(5)   VALUE ZEROES.              
           05  WS-PAGE-CNT                PIC 9(5)   VALUE 0.                   
           05  WS-LINE-CNT                PIC 9(2)   VALUE 0.                   
                                                                                
      *----------------------------------------------------------------*        
      *  MISCELLEANEOUS VARIABLES                                      *        
      *----------------------------------------------------------------*        
       01  WS-STRING-POINTER              PIC 9(2).                             
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-CC                 PIC X(2).                             
           05  WS-CURR-YY                 PIC X(2).                             
           05  WS-CURR-MM                 PIC X(2).                             
           05  WS-CURR-DD                 PIC X(2).                             
       01  K022-STATUS                    PIC X(2).                             
                                                                                
       01  WS-GRAND-TOTAL                 PIC S9(09)V99 VALUE ZEROES.           
                                                                                
      *----------------------------------------------------------------*        
      *    REPORT LAYOUT OF SUMMARY AGEING REPORT.                     *        
      *----------------------------------------------------------------*        
       01  L136-PRT-HDR-1.                                                      
           05  FILLER                     PIC X      VALUE SPACE.               
           05  FILLER                     PIC X(8)   VALUE 'BP13L136'.          
           05  FILLER                     PIC X(13)  VALUE SPACES.              
           05  FILLER                     PIC X(4)   VALUE 'HDB3'.              
           05  FILLER                     PIC X(14)  VALUE SPACES.              
           05  FILLER                     PIC X(39)                             
               VALUE 'S Y S T E M   O F   C O M M I T M E N T'.                 
           05  FILLER                     PIC X(10)  VALUE SPACES.              
           05  FILLER                     PIC X(7)   VALUE 'DATE : '.           
           05  L136-PRT-DATE              PIC X(10)  VALUE SPACES.              
           05  FILLER                     PIC X(10)  VALUE SPACES.              
           05  FILLER                     PIC X(9)   VALUE 'PAGE   : '.         
           05  L136-PRT-PAGE              PIC ZZ9.                              
                                                                                
       01  L136-PRT-HDR-2.                                                      
           05  FILLER                     PIC X(25)  VALUE SPACES.              
           05  FILLER                     PIC X(50)                             
           VALUE 'LISTING OF SUB-TRANFER CASES > 2 MONTHS OLD AS AT '.          
           05  L136-RUN-DATE              PIC X(10).                            
                                                                                
       01  L136-PRT-HDR-3.                                                      
           05  FILLER                     PIC X(43)  VALUE SPACES.              
           05  FILLER                     PIC X(31)                             
           VALUE 'FROM DATE OF SUB-TRANSFER ORDER'.                             
                                                                                
       01  L136-PRT-HDR-4.                                                      
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE 'S/N'.               
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(08)  VALUE 'REGN-NO '.          
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(17)                             
               VALUE 'SUB-TRANSFER DATE'.                                       
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(63)                             
               VALUE 'UNIT ADDRESS'.                                            
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  FILLER                     PIC X(05) VALUE 'OIC  '.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(19)                             
               VALUE '  AMT OUSTANDING   '.                                     
                                                                                
       01  L136-PRT-HDR-5.                                                      
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(08)  VALUE '/NRIC   '.          
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(17)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(63)                             
               VALUE '/NAME '.                                                  
           05  FILLER                     PIC X(10)  VALUE SPACES.              
           05  FILLER                     PIC X(19)  VALUE SPACES.              
                                                                                
       01  L136-PRT-HDR-6.                                                      
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(04)  VALUE '===='.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(08)  VALUE '========'.          
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(17)                             
               VALUE '================='.                                       
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(63)  VALUE ALL '='.             
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(05)  VALUE '====='.             
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(19)                             
               VALUE '==================='.                                     
                                                                                
       01  L136-PRT-FOOTER.                                                     
           05  FILLER                     PIC X(05)  VALUE SPACES.              
           05  FILLER                     PIC X(24)   VALUE                     
               'TOTAL NUMBER OF CASES : '.                                      
           05  L136-PRT-TOT               PIC ZZZZ9.                            
           05  FILLER                     PIC X(65)  VALUE SPACES.              
           05  FILLER                     PIC X(14)                             
               VALUE 'GRAND TOTAL : '.                                          
           05  FILLER                     PIC X(04)  VALUE '$'.                 
           05  L136-PRT-GTOT              PIC ZZZ,ZZ9.99.                       
           05  FILLER                     PIC X(01)  VALUE SPACE.               
           05  L136-DR-CR-DESC-FOOT       PIC X(02).                            
           05  FILLER                     PIC X(06)  VALUE SPACES.              
                                                                                
       01  L136-PRT-DTL-1.                                                      
           05  FILLER                     PIC X(01)  VALUE SPACES.              
           05  L136-SERIAL-NO             PIC ZZZZ9.                            
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  L136-REGN-NO               PIC X(08).                            
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(04)  VALUE SPACES.              
           05  L136-SUB-TRF-DATE          PIC X(10).                            
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  L136-UNIT-ADDR             PIC X(63)  VALUE SPACES.              
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  L136-OIC                   PIC X(05)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(04)  VALUE '$'.                 
           05  L136-SUSP-AMT              PIC ZZZ,ZZ9.99.                       
           05  FILLER                     PIC X(01)  VALUE SPACE.               
           05  L136-DR-CR-DESC            PIC X(02).                            
                                                                                
       01  L136-PRT-DTL-2.                                                      
           05  FILLER                     PIC X(06)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  L136-NRIC-NO               PIC X(09).                            
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(17)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  L136-EXAPPLT-NAME          PIC X(66).                            
                                                                                
       01  L136-PRT-DTL-3.                                                      
           05  FILLER                     PIC X(06)                             
               VALUE 'NOTE: '.                                                  
           05  FILLER                     PIC X(30)                             
               VALUE 'AGEING IS THE DIFFERENCE BETWE'.                          
           05  FILLER                     PIC X(40)                             
               VALUE 'EN RUN DATE AND SUB-TRANSFER ORDER DATE '.                
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *================================================================*        
       0000-MAIN-ROUTINE.                                                       
      *================================================================*        
           PERFORM 1000-INITIALIZATION      THRU 1000-EXIT.                     
           PERFORM 2000-PROCESS-ROUTINE     THRU 2000-EXIT                      
                   UNTIL WS-PRTFILE-EOF = 'Y'.                                  
           PERFORM 9000-PRINT-FOOTER        THRU 9000-EXIT.                     
           PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       1000-INITIALIZATION.                                                     
      *================================================================*        
           OPEN INPUT  PRTFILE                                                  
                       BP13K022                                                 
                OUTPUT BP13L136.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
                                                                                
           STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CC WS-CURR-YY           
                  DELIMITED BY SIZE INTO L136-PRT-DATE.                         
                                                                                
           READ  PRTFILE  AT END                                                
                 MOVE 'Y'          TO  WS-PRTFILE-EOF.                          
                                                                                
           IF WS-PRTFILE-EOF = 'Y'                                              
              STRING 'NO PRINT FILE RECORDS FOUND'                              
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
           ELSE                                                                 
              ADD 1                TO WS-CNT-PRTFILE-READ                       
              MOVE IN-K885-REC     TO BP13F885-REC                              
           END-IF.                                                              
                                                                                
           MOVE 60                 TO WS-LINE-CNT.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       2000-PROCESS-ROUTINE.                                                    
      *================================================================*        
           IF (IN-AGE-MONTHS > 2) OR                                            
              (IN-AGE-MONTHS = 2 AND IN-AGE-DAYS > 0)                           
              PERFORM 3000-FORMAT-L136 THRU 3000-EXIT                           
           END-IF.                                                              
                                                                                
           READ  PRTFILE  AT END                                                
                 MOVE 'Y'        TO WS-PRTFILE-EOF                              
                 GO  TO 2000-EXIT                                               
                 END-READ.                                                      
                                                                                
           ADD   1                   TO WS-CNT-PRTFILE-READ.                    
           MOVE IN-K885-REC          TO BP13F885-REC.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       3000-FORMAT-L136.                                                        
      *================================================================*        
           MOVE SPACES                 TO BP13COMM8-REC.                        
           INITIALIZE                     BP13COMM8-REC.                        
                                                                                
           MOVE F885-NUM-SCH-ACCT      TO WS-LINK-REC.                          
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC BP13COMM8-REC.                     
                                                                                
           IF F885-AMT-SUSPENSE = ZEROES                                        
              GO TO 3000-EXIT.                                                  
                                                                                
           ADD 1                       TO WS-SEQ-CNT.                           
           MOVE WS-SEQ-CNT             TO L136-SERIAL-NO.                       
           MOVE F885-NUM-REGN          TO L136-REGN-NO.                         
                                                                                
           STRING IN-FSA-DATE(7:2) '/' IN-FSA-DATE(5:2) '/'                     
                  IN-FSA-DATE(1:4) DELIMITED BY SIZE                            
                                     INTO L136-SUB-TRF-DATE.                    
                                                                                
           MOVE 1                      TO WS-STRING-POINTER.                    
           STRING 'BLK. ' COMM8-NUM-BLK '  #' COMM8-NUM-LEVEL                   
                  '-' COMM8-NUM-UNIT-MAIN ' ' DELIMITED BY SIZE                 
                                     INTO L136-UNIT-ADDR                        
                  WITH POINTER WS-STRING-POINTER.                               
                                                                                
           IF COMM8-NUM-UNIT-SUB > SPACES                                       
              STRING '-' COMM8-NUM-UNIT-SUB ' ' DELIMITED BY SIZE               
                                     INTO L136-UNIT-ADDR                        
                      WITH POINTER   WS-STRING-POINTER                          
           END-IF.                                                              
                                                                                
           STRING COMM8-NME-STREET ' S(' COMM8-NUM-POSTAL-CODE ')'              
                  DELIMITED BY SIZE  INTO L136-UNIT-ADDR                        
                  WITH POINTER WS-STRING-POINTER.                               
                                                                                
           IF F885-AMT-SUSPENSE IS NUMERIC                                      
              ADD F885-AMT-SUSPENSE TO WS-GRAND-TOTAL                           
           ELSE                                                                 
              DISPLAY 'INVALID SUSPENSE-AMOUNT'                                 
           END-IF.                                                              
           DISPLAY 'GRAND-TOT' WS-GRAND-TOTAL                                   
                                                                                
           MOVE F885-AMT-SUSPENSE    TO L136-SUSP-AMT.                          
           IF F885-AMT-SUSPENSE >= 0                                            
              MOVE 'CR'              TO L136-DR-CR-DESC                         
              MOVE 'CR'              TO L136-DR-CR-DESC-FOOT                    
           ELSE                                                                 
              MOVE 'DR'              TO L136-DR-CR-DESC                         
              MOVE 'DR'              TO L136-DR-CR-DESC-FOOT                    
           END-IF.                                                              
                                                                                
           MOVE F885-NUM-NRIC        TO L136-NRIC-NO.                           
           MOVE F885-NME-NAME        TO L136-EXAPPLT-NAME.                      
                                                                                
           PERFORM 3100-READ-BP13K022           THRU 3100-EXIT.                 
                                                                                
           IF K022-NUM-OIC NOT = SPACES AND LOW-VALUES                          
              MOVE K022-NUM-OIC TO L136-OIC                                     
           ELSE                                                                 
              MOVE SPACES       TO L136-OIC                                     
           END-IF.                                                              
                                                                                
           PERFORM 6000-PRINT-L136   THRU 6000-EXIT.                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       3100-READ-BP13K022.                                                      
      *================================================================*        
           MOVE LOW-VALUES           TO K022-KEY-FLD.                           
           MOVE F885-NUM-REGN        TO K022-NUM-REGN.                          
           MOVE SPACES               TO K022-NUM-OIC.                           
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE K022-STATUS                                                 
             WHEN '00'                                                          
             WHEN '02'                                                          
             WHEN '23'                                                          
             WHEN '10'                                                          
                  CONTINUE                                                      
                                                                                
             WHEN OTHER                                                         
               STRING 'BP13K022 READ ERROR CODE(' K022-STATUS ')'               
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
                                                                                
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       6000-PRINT-L136.                                                         
      *================================================================*        
           IF WS-LINE-CNT > 45                                                  
              PERFORM 6100-PRINT-L136-HDG THRU 6100-EXIT                        
           END-IF.                                                              
                                                                                
           WRITE L136-REC    FROM L136-PRT-DTL-1   AFTER 2.                     
           WRITE L136-REC    FROM L136-PRT-DTL-2   AFTER 1.                     
           ADD 3             TO   WS-LINE-CNT.                                  
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       6100-PRINT-L136-HDG.                                                     
      *================================================================*        
           IF WS-SEQ-CNT  > 1                                                   
              WRITE L136-REC FROM L136-PRT-DTL-3   AFTER 5                      
           END-IF.                                                              
                                                                                
           ADD   1                   TO WS-PAGE-CNT.                            
           MOVE  WS-PAGE-CNT         TO L136-PRT-PAGE.                          
           MOVE  L136-PRT-DATE       TO L136-RUN-DATE.                          
           MOVE  8                   TO WS-LINE-CNT.                            
                                                                                
           WRITE L136-REC    FROM L136-PRT-HDR-1   AFTER PAGE.                  
           WRITE L136-REC    FROM L136-PRT-HDR-2   AFTER 2.                     
           WRITE L136-REC    FROM L136-PRT-HDR-3   AFTER 1.                     
           WRITE L136-REC    FROM L136-PRT-HDR-4   AFTER 2.                     
           WRITE L136-REC    FROM L136-PRT-HDR-5   AFTER 1.                     
           WRITE L136-REC    FROM L136-PRT-HDR-6   AFTER 1.                     
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9000-PRINT-FOOTER.                                                       
      *================================================================*        
           MOVE WS-CNT-PRTFILE-READ   TO L136-PRT-TOT.                          
           MOVE WS-GRAND-TOTAL        TO L136-PRT-GTOT.                         
                                                                                
           WRITE L136-REC    FROM L136-PRT-FOOTER  AFTER 2.                     
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9900-HANDLE-ABEND.                                                       
      *================================================================*        
            CLOSE   PRTFILE                                                     
                    BP13L136.                                                   
                                                                                
            DISPLAY '***************************************'                   
            DISPLAY '*    BP13C136  CONTROL TOTAL          *'                   
            DISPLAY '***************************************'                   
            DISPLAY '* NO. OF INPUT READ : ' WS-CNT-PRTFILE-READ.               
            DISPLAY '***************************************'                   
            DISPLAY '* ABEND-DESC: ' WS-ERR-MSG.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9900-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9999-CLOSE-FILES.                                                        
      *================================================================*        
            CLOSE   PRTFILE                                                     
                    BP13K022                                                    
                    BP13L136.                                                   
                                                                                
            DISPLAY '***************************************'                   
            DISPLAY '*    BP13C136  CONTROL TOTAL          *'                   
            DISPLAY '***************************************'                   
            DISPLAY '* NO. OF INPUT READ : ' WS-CNT-PRTFILE-READ.               
            DISPLAY '***************************************'                   
            DISPLAY '* REMARKS: ' WS-ERR-MSG.                                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
