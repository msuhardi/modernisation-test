       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C734.                                                 
      *AUTHOR.        ANNALYN C BANTA.                                          
      *DATE-WRITTEN.  12/07/94.                                                 
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *                                                        *                
      *    OBJECTIVE   : IF MULTIPLE REC FOUND FOR SAME NRIC   *                
      *                     SELECT THE BIGGEST FLAT            *                
      *                     SELECT THE UPGRADER OVER NHH       *                
      *                     SELECT THE NHH OVER OPEN MARKET    *                
      *                                                        *                
      *    INPUT FILE  : BP13F558                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE     DETAILS                          *                
      *--------------------------------------------------------*                
      * N940014 LSB 140994 - REDEFINE F558-NUM-HDB-REF         *                
      *BP130258 LSB 040397 - EVEN SAME FT, ALSO CHECK HHTY     *                
      *                      OUTPUT DP FLAT, IF 2 DP FLAT, CHK *                
      *                      FOR BIGGER FT                     *                
      *BP130445 SSS 050298 - EXPAND F558 RECL FROM 150 TO 400  *                
      *                      IF MULTIPLE DP FLAT, TAKE LATEST  *                
      *                      SOLD-DATE                         *                
      *BP130810 SRA 060300 - MAKE HH-TYPE='H', WHEN FLAT-TYPE =*                
      *                      '02' AND SALES-TYPE = '6'.        *                
      *BP130897 AKT 040800 - EXPAND F558 RECL FROM 400 TO 600  *                
      *BP130974 AKT 170101 - CONSIDER NRIC AS WELL AS REGNO    *                
      *BP130925 ANC 010701 - ADD CHECKING OF SALES TYPE 2 & 8, *                
      *                      IF SALES TYPE = 2 TREAT AS 'H',   *                
      *                      IF SALES TYPE = 8 & ALLOC-SCH =   *                
      *                      STD OR RP AND 1ST 4BYTES OF SCHEME*                
      *                      ACCT = '8575' TREAT AS 'G'.       *                
      *BP138075 SMP 041119 - ENHANCE HH DETERMINATION, TREAT   *                
      *                      AS 'G' WHEN DTE-FLAT-SOLD-SMS >=  *                
      *                      '19951001'                        *                
      *BP139518 EAA2 280223 TO INCLUDE 6 SALES TYPE CODE IN    *                
      *                      HOUSEHOLD DETERMINATION           *                
      *                      (8L/8M/8N/8R/8S/8T)               *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT P13F558A ASSIGN TO P13F558A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F558                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F558.                                                           
                                                                                
       FD  P13F558A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  P13F558A-REC                   PIC X(600).                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F558-READ                PIC 9(8)    VALUE ZEROS.              
           05 WS-F558-WRITE               PIC 9(8)    VALUE ZEROS.              
           05 PREV-FT                     PIC X(2)    VALUE SPACES.             
           05 WS-CNT                      PIC 9(2)    VALUE 0.                  
                                                                                
       01  WS-EOF-F558                    PIC 9.                                
           88 EOF-F558                    VALUE 1.                              
                                                                                
       01  WS-PREV-F558-REC.                                                    
           05 WS-PREV-REGN                PIC X(8).                             
           05 WS-PREV-MATURE-KEY REDEFINES WS-PREV-REGN.                        
              10 WS-PREV-REFNO            PIC X(6).                             
              10 FILLER                   PIC X(2).                             
           05 WS-PREV-NRIC                PIC X(9).                             
           05 FILLER                      PIC X(35).                            
           05 WS-PREV-DTE-FLAT-SOLD-SMS   PIC X(8).                             
           05 WS-PREV-SALE-TY-SMS         PIC X(2).                             
           05 FILLER                      PIC X(30).                            
           05 WS-PREV-HOUSEHOLD           PIC X(1).                             
           05 FILLER                      PIC X(1).                             
           05 WS-PREV-TWO-ROOMER          PIC X(1).                             
           05 FILLER                      PIC X(4).                             
           05 WS-PREV-CDE-FILE            PIC X(1).                             
           05 WS-PREV-NUM-HDB-REF         PIC X(11).                            
           05 FILLER                      PIC X(3).                             
           05 WS-PREV-FLAT-TYPE           PIC XX.                               
           05 FILLER                      PIC X(30).                            
           05 WS-PREV-ALLOC-SCH           PIC X(3).                             
           05 FILLER                      PIC X.                                
           05 FILLER                      PIC X(450).                           
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F558      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL EOF-F558.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
                                                                                
           OPEN INPUT  BP13F558                                                 
                OUTPUT P13F558A.                                                
                                                                                
           MOVE SPACES                  TO WS-PREV-F558-REC.                    
           MOVE ZEROS                   TO WS-COUNTERS                          
                                           WS-EOF-F558.                         
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F558.                                                           
                                                                                
           READ BP13F558 AT END                                                 
                MOVE HIGH-VALUE TO F558-NUM-NRIC                                
                MOVE 1          TO WS-EOF-F558                                  
                GO TO 200-EXIT.                                                 
                                                                                
           ADD  1 TO WS-F558-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *                                                                         
      * IF RECORD NOT FOUND IN SMS  - F558-NUM-HDB-REF = BLANK                  
      *                             - F558-CDE-HOUSEHOLD = 'H'                  
      *                             - F558-CDE-FLAT-TYPE = BLANK                
      *                                                                         
      * IF RECORD NOT IN SMS        - F558-NUM-HDB-REF = SMS-SCH-ACC            
      *                             - F558-CDE-HOUSEHOLD = 'G'                  
      *                             - F558-CDE-FLAT-TYPE = SMS-FT               
      *                                                                         
      * --- CHECK FLAT TYPE IF                                                  
      *      CURR- CDE-HSEHOLD = 'G' AND PREV-CDE-HSEHOLD = 'G'                 
      *      CURR- CDE-HSEHOLD = 'H' AND PREV-CDE-HSEHOLD = 'H'                 
      *                                                                         
      * --- PROGRAM WILL DO UPDATE TO 'H' IF SMS-FT = 1 OR 2                    
      *                     UPDATE TO '3' IF SMS-FT = 3                         
                                                                                
       300-PROCESS-RTN.                                                         
                                                                                
           MOVE BP13F558-REC  TO WS-PREV-F558-REC.                              
                                                                                
           MOVE 0 TO WS-CNT.                                                    
           MOVE F558-CDE-FLAT-TYPE-SMS TO PREV-FT.                              
           PERFORM 310-CHECK-NRIC THRU 310-EXIT                                 
             UNTIL BP13F558-REC(1:17) NOT = WS-PREV-F558-REC(1:17)              
               OR  EOF-F558.                                                    
                                                                                
           PERFORM 500-WRITE-OUTPUT THRU 500-EXIT.                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       310-CHECK-NRIC.                                                          
                                                                                
           ADD 1  TO WS-CNT.                                                    
           IF WS-CNT > 1                                                        
                                                                                
           IF (F558-CDE-HOUSEHOLD = 'G' AND WS-PREV-HOUSEHOLD = 'G') OR         
              (F558-CDE-HOUSEHOLD = 'H' AND WS-PREV-HOUSEHOLD = 'H')            
              IF F558-DTE-FLAT-SOLD-SMS > WS-PREV-DTE-FLAT-SOLD-SMS             
                  MOVE BP13F558-REC TO WS-PREV-F558-REC                         
              ELSE                                                              
                  NEXT SENTENCE                                                 
           ELSE                                                                 
              IF (F558-CDE-HOUSEHOLD = 'G' AND WS-PREV-HOUSEHOLD = 'H')         
                  MOVE BP13F558-REC TO WS-PREV-F558-REC                         
              ELSE                                                              
                  NEXT SENTENCE                                                 
           ELSE                                                                 
                  NEXT SENTENCE.                                                
                                                                                
                                                                                
       310-READ-F558.                                                           
           PERFORM 200-READ-F558      THRU 200-EXIT.                            
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-WRITE-OUTPUT.                                                        
           PERFORM 510-CHECK-FLAT-TYPE THRU 510-EXIT.                           
           WRITE P13F558A-REC FROM WS-PREV-F558-REC.                            
           ADD  1  TO WS-F558-WRITE.                                            
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       510-CHECK-FLAT-TYPE.                                                     
           IF WS-PREV-FLAT-TYPE = '01' OR '02'                                  
              IF (WS-PREV-SALE-TY-SMS = '7 ' OR '7J' OR '8A' OR                 
                                        '8B' OR '8E' OR '7C' OR                 
                                        '8L' OR '8M' OR '8N' OR                 
                                        '8R' OR '8S' OR '8T' ) OR               
                 (WS-PREV-ALLOC-SCH = 'OM ' OR 'PPO' OR 'AOM' OR                
                                      'ROM' OR 'PPR' OR 'SPR')                  
                 MOVE 'H' TO WS-PREV-HOUSEHOLD                                  
              ELSE                                                              
                 IF (WS-PREV-ALLOC-SCH = 'STD' OR 'RP ') OR                     
                    (WS-PREV-NUM-HDB-REF >= '85750245901' AND                   
                                         <= '85750375015') OR                   
                    (WS-PREV-DTE-FLAT-SOLD-SMS >= '19951001')                   
                    MOVE 'G' TO WS-PREV-HOUSEHOLD                               
                    MOVE 'Y' TO WS-PREV-TWO-ROOMER                              
                 ELSE                                                           
                     MOVE 'H' TO WS-PREV-HOUSEHOLD                              
                 END-IF                                                         
              END-IF                                                            
              DISPLAY '* FLAT-TY =' WS-PREV-FLAT-TYPE                           
                      ' REF/REG = ' WS-PREV-REGN                                
                      ' NRIC = ' WS-PREV-NRIC                                   
                      ' SALE-TY = ' WS-PREV-SALE-TY-SMS                         
                      ' HHTY= ' WS-PREV-HOUSEHOLD                               
           ELSE                                                                 
              IF WS-PREV-FLAT-TYPE = '03' AND  WS-PREV-CDE-FILE = 'S'           
                 MOVE '3' TO WS-PREV-HOUSEHOLD                                  
              ELSE                                                              
                 NEXT SENTENCE.                                                 
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       999-CLOSE-FILES.                                                         
           CLOSE BP13F558                                                       
                 P13F558A.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C734 ================*'.              
           DISPLAY '    NO. OF F558 RECORDS READ     = ' WS-F558-READ.          
           DISPLAY '    NO. OF F558 RECORDS WRITTEN  = ' WS-F558-WRITE.         
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
