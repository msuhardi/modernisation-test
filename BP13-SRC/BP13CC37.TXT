       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC37.                                                 
       AUTHOR.        BALA AMUTHAN.                                             
       DATE-WRITTEN.  17/10/2006.                                               
      *****************************************************************         
      * OBJECTIVE  - MATCH AG07F630M WITH AG07G640                              
      *            - IF MATCH                                                   
      *                 CREATE VOUCHER TRANSACTION FILE WITH IBG SERIAL.        
      *              IF NOT MATCH, ALSO OUTPUT AG07F630.                        
      *                                                                         
      * SUBROUTINE    - NIL                                                     
      *                                                                         
      * IDENTICAL PGM - NIL                                                     
      *                                                                         
      *----------------------------------------------------------------         
      * REF NO   DATE     BY   AMENDMENTS/ENHANCEMENTS                          
      * -------- ----     ---  -----------------------                          
      * BP132888 17102006 BA2  NEW PROGRAM.                                     
      * BP134154 26042011 JB8  TO CATER FOR CHEQUE & IBG                        
      *****************************************************************         
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  AG07F630  ASSIGN TO AG07F630.                                
                                                                                
           SELECT  AG07F640  ASSIGN TO AG07F640.                                
                                                                                
           SELECT  AG07F63A  ASSIGN TO AG07F63A.                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  AG07F630                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 130 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
                                                                                
       COPY AG07F630.                                                           
                                                                                
       FD  AG07F640                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
                                                                                
       COPY AG07F640.                                                           
                                                                                
       FD  AG07F63A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 130 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           DATA RECORD IS WS-F630A-REC                                          
           RECORDING MODE IS F.                                                 
       01  WS-F630A-REC    PIC  X(130).                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F630                      PIC 9(7)  VALUE 0.              
           05  WS-CNT-F640                      PIC 9(7)  VALUE 0.              
           05  WS-CNT-630A                      PIC 9(7)  VALUE 0.              
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-END-F630                      PIC X(1)  VALUE 'N'.            
               88  CS-END-F630                            VALUE 'Y'.            
           05  WS-END-F640                      PIC X(1)  VALUE 'N'.            
               88  CS-END-F640                            VALUE 'Y'.            
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-TODAY-DATE.                                                   
               10  WS-SYS-YY              PIC 9(4).                             
               10  WS-SYS-MM              PIC 9(2).                             
               10  WS-SYS-DD              PIC 9(2).                             
           05  WS-TIME-NUM                PIC 9(9).                             
           05  WS-TIME REDEFINES WS-TIME-NUM.                                   
               10  WS-HH                  PIC 9(2).                             
               10  WS-MM                  PIC 9(2).                             
               10  WS-SS                  PIC 9(2).                             
               10  WS-GG                  PIC 9(2).                             
               10  WS-SG                  PIC 9(1).                             
                                                                                
       01  WS-F640-KEY.                                                         
           05  WS-F640-NUM-BATCH          PIC X(08) VALUE SPACES.               
           05  WS-F640-NUM-VR             PIC X(11) VALUE SPACES.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ********************************************************                  
       0000-MAIN-LOGIC.                                                         
      ********************************************************                  
                                                                                
           PERFORM 1000-START-UP                THRU 1000-EXIT.                 
           PERFORM 2000-READ-AG07F630           THRU 2000-EXIT.                 
           PERFORM 3000-READ-AG07F640           THRU 3000-EXIT.                 
           PERFORM 4000-MAIN-PROCESS            THRU 4000-EXIT                  
                   UNTIL CS-END-F630 AND CS-END-F640.                           
           PERFORM 9999-WRAP-UP.                                                
                                                                                
                                                                                
      ********************************************************                  
       1000-START-UP.                                                           
      ********************************************************                  
                                                                                
           OPEN INPUT  AG07F630                                                 
                       AG07F640                                                 
                OUTPUT AG07F63A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE.                        
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ********************************************************                  
       2000-READ-AG07F630.                                                      
      ********************************************************                  
                                                                                
           READ AG07F630 AT END MOVE 'Y'         TO  WS-END-F630                
                                MOVE HIGH-VALUES TO  F630-NUM-TRANS-REF         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-CNT-F630.                                                
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ********************************************************                  
       3000-READ-AG07F640.                                                      
      ********************************************************                  
                                                                                
           READ AG07F640 AT END MOVE 'Y'         TO  WS-END-F640                
                                MOVE HIGH-VALUES TO  WS-F640-KEY                
                GO TO 3000-EXIT.                                                
                                                                                
           MOVE F640-NUM-BATCH    TO WS-F640-NUM-BATCH.                         
           MOVE F640-NUM-VR       TO WS-F640-NUM-VR.                            
           ADD 1 TO WS-CNT-F640.                                                
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ********************************************************                  
       4000-MAIN-PROCESS.                                                       
      ********************************************************                  
                                                                                
           IF F630-NUM-TRANS-REF = WS-F640-KEY                                  
              PERFORM 4500-MOVE-WRITE        THRU 4500-EXIT                     
           ELSE                                                                 
             IF F630-NUM-TRANS-REF < WS-F640-KEY                                
                MOVE AG07F630-REC            TO   WS-F630A-REC                  
                PERFORM 5000-WRITE-AG07F63A  THRU 5000-EXIT                     
                PERFORM 2000-READ-AG07F630   THRU 2000-EXIT                     
             ELSE                                                               
                PERFORM 3000-READ-AG07F640   THRU 3000-EXIT                     
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ********************************************************                  
       4500-MOVE-WRITE.                                                         
      ********************************************************                  
           MOVE SPACES                     TO   WS-F630A-REC.                   
           INITIALIZE WS-F630A-REC.                                             
                                                                                
           MOVE AG07F630-REC               TO   WS-F630A-REC.                   
           IF F640-NUM-CHEQUE     = SPACES OR ZEROS                             
               MOVE F640-TXT-IBG-SERIAL TO   WS-F630A-REC(119:12)               
               MOVE F640-DTE-IBG-VALUE   TO   WS-F630A-REC(111:8)               
           ELSE                                                                 
               MOVE F640-NUM-CHEQUE     TO   WS-F630A-REC(119:12)               
           END-IF.                                                              
           PERFORM 5000-WRITE-AG07F63A     THRU 5000-EXIT.                      
           PERFORM 2000-READ-AG07F630      THRU 2000-EXIT.                      
                                                                                
       4500-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ********************************************************                  
       5000-WRITE-AG07F63A.                                                     
      ********************************************************                  
           WRITE WS-F630A-REC.                                                  
           ADD 1 TO WS-CNT-630A.                                                
                                                                                
       5000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ********************************************************                  
       9999-WRAP-UP.                                                            
      ********************************************************                  
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '******* CONTROL   TOTALS *******'.                          
           DISPLAY 'PROGRAM-ID : BP13CC37'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY '                         '.                                 
           DISPLAY ' NUMBER OF AG07F630 RECORDS READ           : '              
                   WS-CNT-F630.                                                 
           DISPLAY ' NUMBER OF AG07F640 RECORDS READ           : '              
                   WS-CNT-F640.                                                 
           DISPLAY ' NUMBER OF AG07F63A RECORDS WRITE          : '              
                   WS-CNT-630A.                                                 
                                                                                
           CLOSE AG07F630                                                       
                 AG07F640                                                       
                 AG07F63A.                                                      
                                                                                
           STOP RUN.                                                            
