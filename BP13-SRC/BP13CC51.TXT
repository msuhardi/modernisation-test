       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC51.                                                 
      *AUTHOR.        EDEN ROSE DINGAL.                                         
      *DATE-WRITTEN.  07/05/2007.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      * OBJECTIVE    :  PRINT LETTERS ON LUMP SUM PAYMENTS          *           
      *                 FOR DBSS FLAT.                              *           
      *                                                             *           
      * INPUT FILES  :  1.  BP13FD92                                *           
      *                 2.  BP13KD00 - DBSS MASTER FILE             *           
      *                 3.  BP13K800 - SOC APPLICATION MASTER       *           
      *                 4.  BP13K820 - SOC OCCUPIER FILE            *           
      *                 5.  BP13K830 - SOC CORR ADDRESS FILE        *           
      *                 6.  SY02F001 - DATE FILE                    *           
      *                                                             *           
      * OUTPUT       :  1.  P13PC51A - LUMP SUM PYMT LETTER         *           
      *                 2.  P13PC51B - < $500 LUMP SUM PAYMENT      *           
      *                 2.  P13PC51C - < $5000 LUMP SUM PAYMENT     *           
      *                 3.  BP13LC51 - REPORT                       *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  OIC  DATE     DESCRIPTION                          *           
      * -------- ---  -------- ------------------------------------ *           
      * BP133033 ED1  07/05/07 NEW PROGRAM                          *           
      * BP132936 ED1  10/05/07 RETRIEVED HEADER DTE FROM SY02F001 & *           
      *                        DTE-AS-AT FROM FD92-AMT-LUMPSUM-PAID *           
      * BP133091 RB12 17/10/07 EXPAND BP13KD00 FR 1200 TO 1600 BYTES*           
      * BP134493 IMC1 20/03/12 CHANGE MIN LUMPSUM FROM $500 TO $5000*           
      * BP134768 IMC1 15/03/13 ADD 'CR' TAG IF LOAN BAL IS NEGATIVE *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13FD92  ASSIGN       TO BP13FD92.                           
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS WS-K820-STATUS.                     
                                                                                
           SELECT BP13K830  ASSIGN       TO BP13K830                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K830-KEY-FLD                        
                            ALTERNATE RECORD KEY IS  K830-NUM-REGN              
                            FILE STATUS  IS WS-K830-STATUS.                     
                                                                                
           SELECT BP13KD00  ASSIGN       TO BP13KD00                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS KD00-KEY-FLD                        
                            FILE STATUS  IS WS-KD00-STATUS.                     
                                                                                
           SELECT P13PC51A  ASSIGN       TO P13PC51A.                           
           SELECT P13PC51B  ASSIGN       TO P13PC51B.                           
           SELECT P13PC51C  ASSIGN       TO P13PC51C.                           
           SELECT BP13LC51  ASSIGN       TO BP13LC51.                           
           SELECT SY02F001  ASSIGN       TO SY02F001.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13FD92                                                            
            RECORDING MODE  IS    F                                             
            LABEL RECORDS   ARE   STANDARD                                      
            BLOCK  CONTAINS    0  RECORDS                                       
            RECORD CONTAINS  1000 CHARACTERS.                                   
       COPY BP13FD92.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   P13PC51A                                                            
            RECORD CONTAINS 90 CHARACTERS                                       
            RECORDING MODE  IS  F.                                              
       01   PC51-PRTREC-A               PIC X(90).                              
                                                                                
       FD   P13PC51B                                                            
            RECORD CONTAINS 90 CHARACTERS                                       
            RECORDING MODE  IS  F.                                              
       01   PC51-PRTREC-B               PIC X(90).                              
                                                                                
       FD   P13PC51C                                                            
            RECORD CONTAINS 90 CHARACTERS                                       
            RECORDING MODE  IS  F.                                              
       01   PC51-PRTREC-C               PIC X(90).                              
                                                                                
       FD   BP13LC51                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   LC51-PRTREC                 PIC X(132).                             
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-INDICATORS.                                                       
           05  WS-K800-STATUS           PIC 99     VALUE 00.                    
           05  WS-K820-STATUS           PIC 99     VALUE 00.                    
           05  WS-K830-STATUS           PIC 99     VALUE 00.                    
           05  WS-KD00-STATUS           PIC 99     VALUE 00.                    
           05  WS-EOF-FD38              PIC X      VALUE 'N'.                   
                                                                                
       01  WS-COUNTERS-POINTERS.                                                
           05  WS-FD92-READ             PIC 9(6)   VALUE ZEROS.                 
           05  WS-K800-READ             PIC 9(6)   VALUE ZEROS.                 
           05  WS-CC51A-PRINTED         PIC 9(6)   VALUE ZEROS.                 
           05  WS-CC51B-PRINTED         PIC 9(6)   VALUE ZEROS.                 
           05  WS-CC51C-PRINTED         PIC 9(6)   VALUE ZEROS.                 
           05  WS-LC51-WRT-CTR          PIC 9(7)   VALUE ZEROES.                
           05  WS-SNO                   PIC 9(5)   VALUE ZEROES.                
           05  WS-PAGE-CTR              PIC 9(4)   VALUE ZEROES.                
           05  WS-LINE-CTR              PIC 9(2)   VALUE ZEROES.                
                                                                                
       01  WS-PAYMENT-CASE              PIC X(13).                              
                                                                                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE               PIC X(08) VALUE SPACES.                
           05  WS-SYS-TIME               PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(05) VALUE SPACES.                
                                                                                
       01  WS-SYSTEM-DATE               PIC X(8).                               
       01  WS-TEMP-DATE                 PIC X(8).                               
       01  WS-TEMP-DATE-R REDEFINES WS-TEMP-DATE.                               
           05 WS-CCYY                   PIC 9(4).                               
           05 WS-MM                     PIC 9(2).                               
           05 WS-DD                     PIC 9(2).                               
       01 WS-MONTH-NAME.                                                        
          05 WS-STRING PIC X(36) VALUE                                          
                'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                         
          05 WS-MONTH  REDEFINES WS-STRING OCCURS 12 TIMES PIC XXX.             
                                                                                
       01  WS-NAME                      PIC X(66)  VALUE SPACES.                
       01  WS-MAXLINE                   PIC 99     VALUE 60.                    
                                                                                
      *-------------------------------------------------------------            
      * FORMAT LETTER WITH LUMPSUM PAYMENT                                      
      *-------------------------------------------------------------            
       01  PC51-LINE01A.                                                        
           05  FILLER               PIC X(72) VALUE SPACES.                     
           05  PC51-HDB-REF         PIC X(14) VALUE SPACES.                     
                                                                                
       01  PC51-LINE01.                                                         
           05  FILLER               PIC X(72) VALUE SPACES.                     
           05  PC51-OUR-REF         PIC X(8)  VALUE SPACES.                     
                                                                                
       01  PC51-LINE02.                                                         
           05  FILLER               PIC X(72) VALUE SPACES.                     
           05  PC51-DATE            PIC X(10) VALUE SPACES.                     
                                                                                
       01  PC51-LINE03.                                                         
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC51-NAME1           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PC51-LINE03B.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC51-NAME2           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PC51-LINE04.                                                         
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC51-BLK             PIC X(16) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE SPACES.                     
           05  PC51-UNIT            PIC X(11) VALUE SPACES.                     
                                                                                
       01  PC51-LINE04B.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC51-ADDR-L2         PIC X(65) VALUE SPACES.                     
                                                                                
       01  PC51-LINE04C.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC51-ADDR-L3         PIC X(20) VALUE SPACES.                     
                                                                                
       01  PC51-LINE05.                                                         
           05  FILLER               PIC X(10) VALUE SPACES.                     
           05  PC51-BAR-CODE        PIC X(09) VALUE SPACES.                     
                                                                                
       01  PC51-LINE06.                                                         
           05  FILLER               PIC X(11) VALUE SPACES.                     
           05  PC51-FLAT-ADDRESS    PIC X(60) VALUE SPACES.                     
                                                                                
      *-------- LUMP SUM PAYMENT --------------------------*                    
                                                                                
       01  PC51-LINE07.                                                         
           05  FILLER               PIC X(50) VALUE SPACES.                     
           05  PC51-AMT-PAID        PIC $$$$,$$9.99 VALUE ZEROS.                
           05  FILLER               PIC X(08) VALUE SPACES.                     
           05  PC51-DTE-PAID        PIC X(11) VALUE SPACES.                     
                                                                                
       01  PC51-LINE08.                                                         
           05  FILLER               PIC X(18) VALUE SPACES.                     
           05  PC51-INT-REBATE      PIC $$$,$$9.99 VALUE ZEROS.                 
           05  FILLER               PIC X(46) VALUE SPACES.                     
           05  PC51-DTE-AS-AT       PIC X(11) VALUE SPACES.                     
                                                                                
       01  PC51-LINE09.                                                         
           05  FILLER               PIC X(38) VALUE SPACES.                     
           05  PC51-AMT-BAL         PIC $$$$,$$9.99 VALUE ZEROS.                
           05  PC51-CR              PIC X(4)  VALUE SPACES.                     
                                                                                
      *-------- LUMP SUM PAYMENT OF LESS THAN $500 / $5000 --------*            
                                                                                
       01  PC51-LINE07B.                                                        
           05  FILLER               PIC X(52) VALUE SPACES.                     
           05  PC51-LUMPSUM         PIC $$$$,$$9.99 VALUE ZEROS.                
           05  FILLER               PIC X(06) VALUE SPACES.                     
           05  PC51-AMT-PAID-B      PIC $$$$9.99 VALUE ZEROS.                   
                                                                                
       01  PC51-LINE08B.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC51-DTE-AS-AT-B     PIC X(11) VALUE SPACES.                     
                                                                                
      *-------------------------------------------------------------            
      * CONTROL REPORT                                                          
      *-------------------------------------------------------------            
       01  LC51-HEADER01.                                                       
           05  FILLER              PIC X(08)     VALUE 'BP13LC51'.              
           05  FILLER              PIC X(09)     VALUE '     HDB3'.             
           05  FILLER              PIC X(31)     VALUE SPACES.                  
           05  FILLER              PIC X(27)     VALUE                          
               'HOUSING & DEVELOPMENT BOARD'.                                   
           05  FILLER              PIC X(23)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LC51-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LC51-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  LC51-HEADER02.                                                       
           05  FILLER              PIC X(41)     VALUE SPACES.                  
           05  FILLER              PIC X(49)     VALUE                          
               'REPORT ON LUMP SUM PAYMENT FOR DBSS FLAT'.                      
                                                                                
       01  LC51-HEADER03.                                                       
           05  FILLER              PIC X(03)     VALUE 'S/N'.                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REGN NO'.               
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'NAME/ADDRESS'.          
           05  FILLER              PIC X(56)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'DTE PAID'.              
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'LUMP SUM'.              
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'AMT PAID'.              
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'LOAN BAL'.              
                                                                                
       01 LC51-DETAIL1.                                                         
          05 LC51-DTL-SERIAL      PIC ZZZZ9      VALUE ZEROES.                  
          05 FILLER               PIC X(01)      VALUE SPACES.                  
          05 LC51-REGNO           PIC X(08)      VALUE SPACES.                  
          05 FILLER               PIC X(01)      VALUE SPACES.                  
          05 LC51-NAME1           PIC X(66)      VALUE SPACES.                  
          05 FILLER               PIC X(01)      VALUE SPACES.                  
          05 LC51-DTE-PAID        PIC X(10)      VALUE SPACES.                  
          05 FILLER               PIC X(01)      VALUE SPACES.                  
          05 LC51-LUMPSUM-AMT     PIC Z,ZZZ,ZZ9.99 VALUE ZEROES.                
          05 FILLER               PIC X(01)      VALUE SPACES.                  
          05 LC51-AMT-RECPT       PIC Z,ZZZ,ZZ9.99 VALUE ZEROES.                
          05 FILLER               PIC X(01)      VALUE SPACES.                  
          05 LC51-AMT-BAL         PIC Z,ZZZ,ZZ9.99 VALUE ZEROES.                
          05 LC51-AMT-BAL-SIGN    PIC X(01)      VALUE SPACES.                  
                                                                                
       01 LC51-DETAIL2.                                                         
          05 FILLER               PIC X(15)     VALUE SPACES.                   
          05 LC51-NAME2           PIC X(66)     VALUE SPACES.                   
                                                                                
       01 LC51-DETAIL3.                                                         
          05 FILLER               PIC X(15)     VALUE SPACES.                   
          05 LC51-FLAT-ADDR       PIC X(70)     VALUE SPACES.                   
                                                                                
       01  WS-BLANK-LINE          PIC X(132)     VALUE SPACES.                  
       01  WS-DASH-LINE           PIC X(132)     VALUE ALL '-'.                 
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR AV02C015                                                   
      *-------------------------------------------------------------            
        01  WS-LINK-AV02C015.                                                   
            05  AV02C015-IN-POSTCODE          PIC X(6).                         
            05  AV02C015-OUT-POSTCODE-CHKSUM  PIC X(9).                         
                                                                                
       COPY AV02COMM.                                                           
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR BP13C913                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH            PIC X(04).                             
           05 WS-LINK-NUM-ACC            PIC X(04).                             
           05 WS-LINK-NUM-CHK            PIC X(01).                             
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-FILES-ROUTINE   THRU  1000-EXIT.                  
            PERFORM 2000-READ-BP13FD92        THRU  2000-EXIT.                  
            PERFORM 3000-MAIN-PROCESS         THRU  3000-EXIT                   
                    UNTIL FD92-NUM-REGN = HIGH-VALUES.                          
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES-ROUTINE.                                                 
      *-------------------------------------------------------------            
            OPEN INPUT  BP13FD92                                                
                        BP13K800                                                
                        BP13K820                                                
                        BP13K830                                                
                        BP13KD00                                                
                        SY02F001                                                
                 OUTPUT P13PC51A                                                
                        P13PC51B                                                
                        P13PC51C                                                
                        BP13LC51.                                               
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K800-STATUS ' WS-K800-STATUS           
              MOVE     WS-K800-STATUS   TO  RETURN-CODE                         
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K820-STATUS ' WS-K820-STATUS           
              MOVE     WS-K820-STATUS TO RETURN-CODE                            
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K830-STATUS ' WS-K830-STATUS           
              MOVE     WS-K830-STATUS   TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-KD00-STATUS ' WS-KD00-STATUS           
              MOVE     WS-KD00-STATUS   TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           READ SY02F001.                                                       
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-SYSTEM-DATE-TIME.                
                                                                                
           STRING  F001-DTE-CURRENT(7:2) '/'                                    
                   F001-DTE-CURRENT(5:2) '/'                                    
                   F001-DTE-CURRENT(1:4)                                        
                   DELIMITED BY SIZE INTO PC51-DATE                             
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13FD92.                                                      
      *-------------------------------------------------------------            
           READ BP13FD92                                                        
                AT END MOVE HIGH-VALUES TO FD92-NUM-REGN                        
                GO TO  2000-EXIT.                                               
                                                                                
           ADD  1  TO   WS-FD92-READ.                                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES   TO WS-PAYMENT-CASE.                                    
                                                                                
           IF FD92-AMT-LUMPSUM-PAID NOT NUMERIC                                 
              MOVE 0 TO FD92-AMT-LUMPSUM-PAID                                   
           END-IF.                                                              
                                                                                
           PERFORM 7100-GET-LOAN-BAL     THRU 7100-EXIT                         
                                                                                
           PERFORM 3050-INITIALIZE-FIELDS   THRU 3050-EXIT.                     
                                                                                
           IF FD92-AMT-LUMPSUM-PAID = FD92-AMT-LUMPSUM                          
              IF KD00-DTE-LOAN-COMM < '20120401'                                
                 IF FD92-AMT-LUMPSUM-PAID < 500.00                              
                    MOVE 'LUMPSUM-LT500'  TO WS-PAYMENT-CASE                    
                 ELSE                                                           
                    MOVE 'LUMPSUM-PAID '  TO WS-PAYMENT-CASE                    
                 END-IF                                                         
              ELSE                                                              
                 IF FD92-AMT-LUMPSUM-PAID < 5000.00                             
                    MOVE 'LUMPSUM-L5000'  TO WS-PAYMENT-CASE                    
                 ELSE                                                           
                    MOVE 'LUMPSUM-PAID '  TO WS-PAYMENT-CASE                    
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3100-PROCESS-LETTER      THRU 3100-EXIT                      
           PERFORM 2000-READ-BP13FD92       THRU 2000-EXIT.                     
                                                                                
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3050-INITIALIZE-FIELDS.                                                  
      *-------------------------------------------------------------            
           MOVE SPACES TO  PC51-HDB-REF                                         
                           PC51-OUR-REF                                         
                           PC51-NAME1                                           
                           PC51-NAME2                                           
                           PC51-BLK                                             
                           PC51-UNIT                                            
                           PC51-ADDR-L2                                         
                           PC51-ADDR-L3                                         
                           PC51-BAR-CODE                                        
                           PC51-FLAT-ADDRESS                                    
                           PC51-DTE-PAID                                        
                           PC51-DTE-AS-AT                                       
                           PC51-DTE-AS-AT-B.                                    
           MOVE ZEROS  TO  PC51-AMT-PAID                                        
                           PC51-INT-REBATE                                      
                           PC51-AMT-BAL                                         
                           PC51-LUMPSUM                                         
                           PC51-AMT-PAID-B.                                     
                                                                                
           IF FD92-AMT-LUMPSUM-REBATE NOT NUMERIC                               
              MOVE 0 TO FD92-AMT-LUMPSUM-REBATE                                 
           END-IF.                                                              
           IF FD92-AMT-LOAN-BAL-COMP  NOT NUMERIC                               
              MOVE 0 TO FD92-AMT-LOAN-BAL-COMP                                  
           END-IF.                                                              
                                                                                
                                                                                
       3050-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-PROCESS-LETTER.                                                     
      *-------------------------------------------------------------            
           PERFORM 4000-READ-BP13K800          THRU 4000-EXIT.                  
                                                                                
           IF WS-K800-STATUS = 00                                               
                                                                                
              PERFORM 5000-GET-NAMES              THRU 5000-EXIT                
              PERFORM 6000-GET-MAIL-ADDR          THRU 6000-EXIT                
              PERFORM 7000-GET-FLAT-ADDR          THRU 7000-EXIT                
              PERFORM 7100-GET-LOAN-BAL           THRU 7100-EXIT                
                                                                                
              EVALUATE TRUE                                                     
              WHEN WS-PAYMENT-CASE = 'LUMPSUM-PAID '                            
                 PERFORM 8000-PRINT-LUMPSUM-PAID  THRU 8000-EXIT                
              WHEN WS-PAYMENT-CASE = 'LUMPSUM-LT500'                            
                 PERFORM 8100-PRINT-LESSTHAN-500  THRU 8100-EXIT                
              WHEN WS-PAYMENT-CASE = 'LUMPSUM-L5000'                            
                 PERFORM 8500-PRINT-LESSTHAN-5000 THRU 8500-EXIT                
              END-EVALUATE                                                      
                                                                                
              PERFORM 8200-MOVE-REPORT-DTLS       THRU 8200-EXIT                
              PERFORM 8300-PRINT-REPORT           THRU 8300-EXIT                
           END-IF.                                                              
                                                                                
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       4000-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
           MOVE FD92-NUM-REGN TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
                ADD  1  TO   WS-K800-READ                                       
           WHEN 23                                                              
                DISPLAY 'BP13K800 REC NOTFND, KEY ' K800-NUM-REGN               
           WHEN OTHER                                                           
                DISPLAY 'BP13K800 READ ERR, STATUS ' WS-K800-STATUS             
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       5000-GET-NAMES.                                                          
      *-------------------------------------------------------------            
           MOVE    K800-NUM-NRIC1          TO   K820-NUM-NRIC.                  
           PERFORM 5500-READ-BP13K820      THRU 5500-EXIT.                      
           MOVE    WS-NAME                 TO   PC51-NAME1.                     
                                                                                
           IF K800-NUM-NRIC2 NOT =  SPACES AND  LOW-VALUES AND ZEROS            
              MOVE     K800-NUM-NRIC2       TO  K820-NUM-NRIC                   
              PERFORM  5500-READ-BP13K820   THRU 5500-EXIT                      
              MOVE     WS-NAME              TO  PC51-NAME2                      
           ELSE                                                                 
              MOVE     SPACES               TO  PC51-NAME2                      
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       5500-READ-BP13K820.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES        TO WS-NAME.                                       
           MOVE K800-NUM-REGN TO K820-NUM-REGN.                                 
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              MOVE  K820-NME-OCCP           TO    WS-NAME                       
                                                                                
           ELSE                                                                 
              MOVE SPACES TO K820-NME-OCCP                                      
                                                                                
           IF WS-K820-STATUS = 23                                               
              DISPLAY 'RECORD NOTFND IN BP13K820 = ' WS-K820-STATUS             
                      ', KEY-FLD = ' K820-KEY-FLD                               
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K820, STAT ' WS-K820-STATUS            
                      ', KEY-FLD = ' K820-KEY-FLD                               
           END-IF.                                                              
                                                                                
       5500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       6000-GET-MAIL-ADDR.                                                      
      *-------------------------------------------------------------            
           MOVE FD92-NUM-REGN     TO    K830-NUM-REGN.                          
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           IF WS-K830-STATUS = 00                                               
              IF (K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR =  '#') AND                           
                 (K830-NUM-MAIL-LVL-NUM =  SPACES OR LOW-VALUES))               
                 MOVE K830-NUM-BLK         TO PC51-BLK                          
                 MOVE K830-NUM-FLAT        TO PC51-UNIT                         
                 MOVE K830-NME-STREET-BLDG TO PC51-ADDR-L2                      
                 IF K830-NUM-POSTAL  = SPACES OR LOW-VALUES OR ZEROES           
                    MOVE SPACES            TO PC51-ADDR-L3                      
                 ELSE                                                           
                    STRING 'SINGAPORE '  K830-NUM-POSTAL                        
                       DELIMITED BY SIZE    INTO PC51-ADDR-L3                   
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-POSTAL TO AV02C015-IN-POSTCODE                
                    PERFORM 6500-CALL-AV02C015     THRU 6500-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC51-BAR-CODE          
                 END-IF                                                         
              ELSE                                                              
                 MOVE K830-NUM-MAIL-BLK        TO PC51-BLK                      
                 MOVE K830-NUM-MAIL-FLAT       TO PC51-UNIT                     
                 MOVE K830-NME-MAIL-STR-BLDG   TO PC51-ADDR-L2                  
                 IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES OR ZEROS        
                    MOVE SPACES                TO PC51-ADDR-L3                  
                 ELSE                                                           
                    STRING 'SINGAPORE '      K830-NUM-MAIL-POSTAL               
                           DELIMITED BY SIZE INTO PC51-ADDR-L3                  
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-MAIL-POSTAL TO AV02C015-IN-POSTCODE           
                    PERFORM 6500-CALL-AV02C015     THRU 6500-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC51-BAR-CODE          
                 END-IF                                                         
              END-IF                                                            
                                                                                
           ELSE                                                                 
                                                                                
              MOVE SPACES TO PC51-BLK                                           
                             PC51-UNIT                                          
                             PC51-ADDR-L2                                       
                             PC51-ADDR-L3                                       
                             PC51-BAR-CODE                                      
                                                                                
           IF WS-K830-STATUS = 23                                               
              DISPLAY 'BP13K830 RECORD NOT FOUND = ' WS-K830-STATUS             
                      ', KEY ' K830-KEY-FLD                                     
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K830, STATUS ' WS-K830-STATUS          
                      ', KEY ' K830-KEY-FLD                                     
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6500-CALL-AV02C015.                                                      
      *-------------------------------------------------------------            
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       7000-GET-FLAT-ADDR.                                                      
      *-------------------------------------------------------------*           
           MOVE SPACES                TO BP13COMM8-REC.                         
           MOVE K800-NUM-SCH-ACC(1:9) TO WS-LINK-REC.                           
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           IF COMM8-CDE-SYSERR = 000                                            
                STRING 'BLK '  COMM8-NUM-BLK  '  '                              
                       '#'  COMM8-NUM-LEVEL   '-'                               
                       COMM8-NUM-UNIT-MAIN    '  ' DELIMITED BY SIZE            
                       COMM8-NME-STREET       DELIMITED BY '   '                
                       INTO PC51-FLAT-ADDRESS                                   
           ELSE                                                                 
              MOVE SPACES TO PC51-FLAT-ADDRESS                                  
                                                                                
           IF COMM8-CDE-SYSERR = 100                                            
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                     
                         K800-NUM-SCH-ACC ', REGN : ' FD92-NUM-REGN             
           ELSE                                                                 
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
                        ', AT KEY ' K800-NUM-SCH-ACC                            
                        ', REGN : ' FD92-NUM-REGN                               
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *--------------------------------------------------------------           
       7100-GET-LOAN-BAL.                                                       
      *--------------------------------------------------------------           
           MOVE SPACES          TO  BP13KD00-REC.                               
           MOVE FD92-NUM-REGN   TO  KD00-NUM-REGN.                              
                                                                                
           READ BP13KD00.                                                       
                                                                                
           IF WS-KD00-STATUS = 00                                               
                IF KD00-AMT-LOAN-BAL NOT NUMERIC                                
                   MOVE 0 TO KD00-AMT-LOAN-BAL                                  
                END-IF                                                          
                                                                                
           ELSE                                                                 
                                                                                
             MOVE 0 TO KD00-AMT-LOAN-BAL                                        
                                                                                
             IF WS-KD00-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN KD00, STATUS ' WS-KD00-STATUS           
                        ', REGN IS  '    FD92-NUM-REGN                          
             ELSE                                                               
                DISPLAY 'ERROR READING KD00, STATUS ' WS-KD00-STATUS            
                        ', REGN IS  '    FD92-NUM-REGN                          
                MOVE WS-KD00-STATUS        TO RETURN-CODE                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       8000-PRINT-LUMPSUM-PAID.                                                 
      *-------------------------------------------------------------            
           STRING FD92-NUM-SCH-ACC(1:4) '-'                                     
                  FD92-NUM-SCH-ACC(5:4) '-'                                     
                  FD92-NUM-SCH-ACC(9:1) '-'                                     
                  FD92-NUM-SCH-ACC(10:2)                                        
           DELIMITED BY SIZE INTO PC51-HDB-REF.                                 
                                                                                
           MOVE  FD92-NUM-REGN           TO PC51-OUR-REF.                       
           MOVE  FD92-AMT-LUMPSUM-PAID   TO PC51-AMT-PAID.                      
           MOVE  KD00-AMT-LOAN-BAL       TO PC51-AMT-BAL.                       
           IF KD00-AMT-LOAN-BAL < ZEROES                                        
              MOVE ' CR.'                TO PC51-CR                             
           ELSE                                                                 
              MOVE '.'                   TO PC51-CR                             
           END-IF                                                               
           MOVE  FD92-AMT-LUMPSUM-REBATE TO PC51-INT-REBATE.                    
                                                                                
           STRING FD92-DTE-LUMPSUM-PAID(7:2) '/'                                
                  FD92-DTE-LUMPSUM-PAID(5:2) '/'                                
                  FD92-DTE-LUMPSUM-PAID(1:4)                                    
                  DELIMITED BY SIZE INTO PC51-DTE-PAID                          
           END-STRING.                                                          
                                                                                
           MOVE PC51-DTE-PAID TO PC51-DTE-AS-AT.                                
                                                                                
           MOVE SPACES                TO PC51-PRTREC-A.                         
           WRITE PC51-PRTREC-A      FROM PC51-LINE01A AFTER PAGE.               
           WRITE PC51-PRTREC-A      FROM PC51-LINE01.                           
           WRITE PC51-PRTREC-A      FROM PC51-LINE02.                           
           WRITE PC51-PRTREC-A      FROM PC51-LINE03 AFTER 4.                   
           WRITE PC51-PRTREC-A      FROM PC51-LINE03B.                          
           WRITE PC51-PRTREC-A      FROM PC51-LINE04.                           
           WRITE PC51-PRTREC-A      FROM PC51-LINE04B.                          
           WRITE PC51-PRTREC-A      FROM PC51-LINE04C.                          
           WRITE PC51-PRTREC-A      FROM PC51-LINE05 AFTER 4.                   
           WRITE PC51-PRTREC-A      FROM PC51-LINE06 AFTER 6.                   
                                                                                
           WRITE PC51-PRTREC-A      FROM PC51-LINE07 AFTER 3.                   
           WRITE PC51-PRTREC-A      FROM PC51-LINE08.                           
           WRITE PC51-PRTREC-A      FROM PC51-LINE09.                           
                                                                                
           ADD 1 TO WS-CC51A-PRINTED.                                           
                                                                                
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8100-PRINT-LESSTHAN-500.                                                 
      *-------------------------------------------------------------            
           STRING FD92-NUM-SCH-ACC(1:4) '-'                                     
                  FD92-NUM-SCH-ACC(5:4) '-'                                     
                  FD92-NUM-SCH-ACC(9:1) '-'                                     
                  FD92-NUM-SCH-ACC(10:2)                                        
           DELIMITED BY SIZE INTO PC51-HDB-REF.                                 
                                                                                
           MOVE  FD92-NUM-REGN           TO PC51-OUR-REF.                       
           MOVE  FD92-AMT-LUMPSUM        TO PC51-LUMPSUM.                       
           MOVE  FD92-AMT-LUMPSUM-PAID   TO PC51-AMT-PAID-B.                    
                                                                                
           STRING FD92-DTE-LUMPSUM-PAID(7:2) '/'                                
                  FD92-DTE-LUMPSUM-PAID(5:2) '/'                                
                  FD92-DTE-LUMPSUM-PAID(1:4)                                    
                  DELIMITED BY SIZE INTO PC51-DTE-AS-AT-B                       
           END-STRING.                                                          
                                                                                
           MOVE SPACES                TO PC51-PRTREC-B.                         
           WRITE PC51-PRTREC-B      FROM PC51-LINE01A AFTER PAGE.               
           WRITE PC51-PRTREC-B      FROM PC51-LINE01.                           
           WRITE PC51-PRTREC-B      FROM PC51-LINE02.                           
           WRITE PC51-PRTREC-B      FROM PC51-LINE03 AFTER 4.                   
           WRITE PC51-PRTREC-B      FROM PC51-LINE03B.                          
           WRITE PC51-PRTREC-B      FROM PC51-LINE04.                           
           WRITE PC51-PRTREC-B      FROM PC51-LINE04B.                          
           WRITE PC51-PRTREC-B      FROM PC51-LINE04C.                          
           WRITE PC51-PRTREC-B      FROM PC51-LINE05 AFTER 4.                   
           WRITE PC51-PRTREC-B      FROM PC51-LINE06 AFTER 6.                   
                                                                                
           WRITE PC51-PRTREC-B      FROM PC51-LINE07B AFTER 2.                  
           WRITE PC51-PRTREC-B      FROM PC51-LINE08B.                          
                                                                                
           ADD 1 TO WS-CC51B-PRINTED.                                           
                                                                                
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8200-MOVE-REPORT-DTLS.                                                   
      *-------------------------------------------------------------            
           MOVE SPACES               TO  LC51-DETAIL1                           
                                         LC51-DETAIL2                           
                                         LC51-DETAIL3.                          
                                                                                
           STRING  WS-SYS-DATE(7:2) '/'                                         
                   WS-SYS-DATE(5:2) '/'                                         
                   WS-SYS-DATE(1:4)                                             
                   DELIMITED BY SIZE INTO LC51-DATE-HDR                         
           END-STRING.                                                          
                                                                                
           MOVE  FD92-NUM-REGN           TO LC51-REGNO.                         
           MOVE  PC51-NAME1              TO LC51-NAME1.                         
           MOVE  PC51-NAME2              TO LC51-NAME2.                         
           MOVE  KD00-AMT-LOAN-BAL       TO LC51-AMT-BAL.                       
           MOVE  FD92-AMT-LUMPSUM        TO LC51-LUMPSUM-AMT.                   
           MOVE  FD92-AMT-LUMPSUM-PAID   TO LC51-AMT-RECPT.                     
           IF KD00-AMT-LOAN-BAL < 0                                             
              MOVE '-'                   TO LC51-AMT-BAL-SIGN                   
           END-IF                                                               
                                                                                
           IF PC51-FLAT-ADDRESS NOT = SPACES                                    
              STRING 'BLK '  COMM8-NUM-BLK  ' '                                 
                     '#'  COMM8-NUM-LEVEL   '-'                                 
                     COMM8-NUM-UNIT-MAIN    ' ' DELIMITED BY SIZE               
                     COMM8-NME-STREET            DELIMITED BY '   '             
               ' S('  COMM8-NUM-POSTAL-CODE ')'   DELIMITED BY SIZE             
                     INTO LC51-FLAT-ADDR                                        
           END-IF.                                                              
                                                                                
           IF FD92-DTE-LUMPSUM-PAID NOT = SPACES AND LOW-VALUES AND ZEROES      
              STRING  FD92-DTE-LUMPSUM-PAID(7:2) '/'                            
                      FD92-DTE-LUMPSUM-PAID(5:2) '/'                            
                      FD92-DTE-LUMPSUM-PAID(1:4)                                
              DELIMITED BY SIZE INTO LC51-DTE-PAID                              
           END-IF.                                                              
                                                                                
                                                                                
       8200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8300-PRINT-REPORT.                                                       
      *--------------------------------------------------------------           
           ADD   1                      TO  WS-SNO.                             
           MOVE  WS-SNO                 TO  LC51-DTL-SERIAL.                    
                                                                                
           IF WS-LINE-CTR > WS-MAXLINE OR                                       
              WS-SNO       = 1                                                  
              ADD            1   TO WS-PAGE-CTR                                 
              MOVE ZEROES        TO LC51-PAGE-HDR                               
              MOVE WS-PAGE-CTR   TO LC51-PAGE-HDR                               
              PERFORM 8400-PRINT-HEADING THRU 8400-EXIT                         
           END-IF.                                                              
                                                                                
           WRITE LC51-PRTREC FROM LC51-DETAIL1.                                 
           WRITE LC51-PRTREC FROM LC51-DETAIL2.                                 
           WRITE LC51-PRTREC FROM LC51-DETAIL3.                                 
           WRITE LC51-PRTREC FROM WS-BLANK-LINE.                                
                                                                                
           ADD   4              TO    WS-LINE-CTR.                              
           ADD   1              TO    WS-LC51-WRT-CTR.                          
                                                                                
       8300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       8400-PRINT-HEADING.                                                      
      *--------------------------------------------------------------           
                                                                                
            WRITE LC51-PRTREC FROM LC51-HEADER01 AFTER PAGE.                    
            WRITE LC51-PRTREC FROM LC51-HEADER02.                               
            WRITE LC51-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC51-PRTREC FROM LC51-HEADER03.                               
            WRITE LC51-PRTREC FROM WS-DASH-LINE.                                
                                                                                
            INITIALIZE WS-LINE-CTR.                                             
            MOVE  5 TO WS-LINE-CTR.                                             
                                                                                
       8400-EXIT.                                                               
           EXIT.                                                                
                                                                                
       8500-PRINT-LESSTHAN-5000.                                                
      *-------------------------------------------------------------            
           STRING FD92-NUM-SCH-ACC(1:4) '-'                                     
                  FD92-NUM-SCH-ACC(5:4) '-'                                     
                  FD92-NUM-SCH-ACC(9:1) '-'                                     
                  FD92-NUM-SCH-ACC(10:2)                                        
           DELIMITED BY SIZE INTO PC51-HDB-REF.                                 
                                                                                
           MOVE  FD92-NUM-REGN           TO PC51-OUR-REF.                       
           MOVE  FD92-AMT-LUMPSUM        TO PC51-LUMPSUM.                       
           MOVE  FD92-AMT-LUMPSUM-PAID   TO PC51-AMT-PAID-B.                    
                                                                                
           STRING FD92-DTE-LUMPSUM-PAID(7:2) '/'                                
                  FD92-DTE-LUMPSUM-PAID(5:2) '/'                                
                  FD92-DTE-LUMPSUM-PAID(1:4)                                    
                  DELIMITED BY SIZE INTO PC51-DTE-AS-AT-B                       
           END-STRING.                                                          
                                                                                
           MOVE SPACES                TO PC51-PRTREC-C.                         
           WRITE PC51-PRTREC-C      FROM PC51-LINE01A AFTER PAGE.               
           WRITE PC51-PRTREC-C      FROM PC51-LINE01.                           
           WRITE PC51-PRTREC-C      FROM PC51-LINE02.                           
           WRITE PC51-PRTREC-C      FROM PC51-LINE03 AFTER 4.                   
           WRITE PC51-PRTREC-C      FROM PC51-LINE03B.                          
           WRITE PC51-PRTREC-C      FROM PC51-LINE04.                           
           WRITE PC51-PRTREC-C      FROM PC51-LINE04B.                          
           WRITE PC51-PRTREC-C      FROM PC51-LINE04C.                          
           WRITE PC51-PRTREC-C      FROM PC51-LINE05 AFTER 4.                   
           WRITE PC51-PRTREC-C      FROM PC51-LINE06 AFTER 6.                   
                                                                                
           WRITE PC51-PRTREC-C      FROM PC51-LINE07B AFTER 2.                  
           WRITE PC51-PRTREC-C      FROM PC51-LINE08B.                          
                                                                                
           ADD 1 TO WS-CC51C-PRINTED.                                           
                                                                                
       8500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY ' -- BP13CC51 CONTROL TOTAL -------'.                        
           DISPLAY ' '.                                                         
           DISPLAY ' READ FROM BP13FD92       : ' WS-FD92-READ.                 
           DISPLAY ' READ FROM K800           : ' WS-K800-READ.                 
           DISPLAY ' LUMPSUM PYMT LETR PRINTED: ' WS-CC51A-PRINTED.             
           DISPLAY ' PYMT < $500  LETR PRINTED: ' WS-CC51B-PRINTED.             
           DISPLAY ' PYMT < $5000 LETR PRINTED: ' WS-CC51C-PRINTED.             
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13FD92                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13KD00                                                       
                 P13PC51A                                                       
                 P13PC51B                                                       
                 BP13LC51                                                       
                 SY02F001.                                                      
                                                                                
           IF WS-K800-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K800, STAT ' WS-K800-STATUS            
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K820, STAT ' WS-K820-STATUS            
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K830, STAT ' WS-K830-STATUS            
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
