      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    BP13CCAE.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  24/03/2011.                                               
      ******************************************************************        
      *                                                                *        
      * SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                     *        
      *                                                                *        
      * SYSTEM ID   :  BP13                                            *        
      *                                                                *        
      * OBJECTIVE   :  TO CREATE BP13K482/BP13K486                     *        
      *                                                                *        
      * INPUT FILES :  1.  BP13F310                                    *        
      *                2.  BP27K860                                    *        
      * I-O FILES      3.  BP13K482                                    *        
      *                4.  BP13K486                                    *        
      *----------------------------------------------------------------*        
      * CHGE-NO   BY    DATE      DESCRIPTION                          *        
      * --------  ---- ---------- -----------                          *        
      * BP133971  JB8  24/03/2011 NEW PROGRAM                          *        
      * BP138047  KAC1 25/11/2019 RECOMPILE FOR BP27 COPYBOOK UPDATE   *        
      ******************************************************************        
                                                                                
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310  ASSIGN        TO BP13F310.                          
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                                          OF BP13K800-MASTER                    
                            FILE STATUS   IS BP13K800-STATUS.                   
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            FILE STATUS   IS BP13K820-STATUS.                   
                                                                                
           SELECT BP27K800  ASSIGN        TO BP27K800                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-HLA OF                    
                                             BP27K800-MASTER                    
                            FILE STATUS   IS BP27K800-STATUS.                   
                                                                                
           SELECT BP27K860  ASSIGN        TO BP27K860                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K860-KEY-FLD                       
                            FILE STATUS   IS BP27K860-STATUS.                   
                                                                                
           SELECT BP13K482  ASSIGN        TO BP13K482                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K482-KEY-FLD                       
                            FILE STATUS   IS BP13K482-STATUS.                   
                                                                                
           SELECT BP13K486  ASSIGN        TO BP13K486                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K486-KEY-FLD                       
                            FILE STATUS   IS BP13K486-STATUS.                   
                                                                                
           SELECT BP13LCAE  ASSIGN        TO BP13LCAE.                          
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13F310                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F310.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP27K800                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
       COPY BP27K800.                                                           
                                                                                
       FD  BP27K860                                                             
           RECORD CONTAINS 10250 CHARACTERS.                                    
       COPY BP27K860.                                                           
                                                                                
       FD  BP13K482                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K482.                                                           
                                                                                
       FD  BP13K486                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K486.                                                           
                                                                                
       FD  BP13LCAE                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  BP13LCAE-REC    PIC X(132).                                          
                                                                                
      *----------------------------------------------------------------*        
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
       01  WS-FILE-VAR.                                                         
           05  WS-F310-EOF                   PIC X(01) VALUE SPACES.            
               88  F310-EOF                            VALUE 'Y'.               
           05  WS-BP27K800-FND               PIC X(01) VALUE SPACES.            
               88  BP27K800-FND                        VALUE 'Y'.               
           05  WS-BP27K860-FND               PIC X(01) VALUE SPACES.            
               88  BP27K860-FND                        VALUE 'Y'.               
           05  WS-BP13K482-FND               PIC X(01) VALUE SPACES.            
               88  BP13K482-FND                        VALUE 'Y'.               
           05  WS-BP13K486-FND               PIC X(01) VALUE SPACES.            
               88  BP13K486-FND                        VALUE 'Y'.               
           05  BP13K800-STATUS               PIC 9(02) VALUE ZEROES.            
           05  BP13K820-STATUS               PIC 9(02) VALUE ZEROES.            
           05  BP27K800-STATUS               PIC 9(02) VALUE ZEROES.            
           05  BP27K860-STATUS               PIC 9(02) VALUE ZEROES.            
           05  BP13K482-STATUS               PIC 9(02) VALUE ZEROES.            
           05  BP13K486-STATUS               PIC 9(02) VALUE ZEROES.            
           05  NO-F310-READ                  PIC 9(08) VALUE ZEROES.            
           05  NO-K482-WRITE                 PIC 9(08) VALUE ZEROES.            
           05  NO-K482-REWRITE               PIC 9(08) VALUE ZEROES.            
           05  NO-K486-WRITE                 PIC 9(08) VALUE ZEROES.            
           05  NO-K486-REWRITE               PIC 9(08) VALUE ZEROES.            
                                                                                
       01  WS-VARS.                                                             
           05  WS-SYS-DATE                   PIC X(08) VALUE SPACES.            
           05  WS-SYS-TIME                   PIC X(08) VALUE SPACES.            
           05  WS-PAGE                       PIC 9(05) VALUE ZEROES.            
           05  WS-SNO                        PIC 9(05) VALUE ZEROES.            
           05  WS-NO-REC                     PIC 9(02) VALUE 58.                
           05  WS-I                          PIC 9(02) VALUE ZEROES.            
           05  WS-J                          PIC 9(02) VALUE ZEROES.            
           05  WS-LEAS-EXIST                 PIC X(01) VALUE SPACES.            
               88  LEAS-EXIST                          VALUE 'Y'.               
           05  WS-CUR-NRICS.                                                    
               10  WS-CUR-NRIC OCCURS 4 TIMES PIC X(09) VALUE SPACES.           
           05  WS-HA-SP.                                                        
               10  WS-HA OCCURS 4 TIMES      PIC X(09) VALUE SPACES.            
               10  WS-SP OCCURS 4 TIMES      PIC X(09) VALUE SPACES.            
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR CONTROL LISTING                         *            
      *------------------------------------------------------------*            
       01  LCAE-HDG1.                                                           
           05  FILLER                   PIC X(14) VALUE 'BP13LCAE'.             
           05  FILLER                   PIC X(8)  VALUE 'HDB3'.                 
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  LCAE-DATE                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(8)  VALUE ' PAGE: '.              
           05  LCAE-PAGE                PIC ZZZZ9.                              
                                                                                
       01  LCAE-HDG2.                                                           
           05  FILLER                   PIC X(57) VALUE SPACES.                 
           05  FILLER                   PIC X(18) VALUE                         
                                            'UNSUCCESSFUL CASES'.               
       01  LCAE-HDG3.                                                           
           05  FILLER                   PIC X(132) VALUE                        
               ' S/NO   REGN      TRANS  SCH-ACC         HLA        STAT        
      -        'TUS  REASON'.                                                   
                                                                                
       01  LCAE-DTL.                                                            
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LCAE-SNO                 PIC ZZZZ9 VALUE ZEROES.                 
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  LCAE-REGN                PIC X(08) VALUE SPACES.                 
           05  FILLER                   PIC X(03) VALUE SPACES.                 
           05  LCAE-TRANS               PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(04) VALUE SPACES.                 
           05  LCAE-SCH-ACC             PIC X(14) VALUE SPACES.                 
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  LCAE-HLA                 PIC X(09) VALUE SPACES.                 
           05  FILLER                   PIC X(03) VALUE SPACES.                 
           05  LCAE-STATUS              PIC X(03) VALUE SPACES.                 
           05  FILLER                   PIC X(04) VALUE SPACES.                 
           05  LCAE-REASON              PIC X(67) VALUE SPACES.                 
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
       0000-MAIN-ROUTINE.                                                       
      *----------------------------------------------------------------*        
                                                                                
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-F310         THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS           THRU 3000-EXIT                        
                   UNTIL F310-EOF.                                              
           PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  BP13F310                                                 
                       SY02F001                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP27K800                                                 
                       BP27K860                                                 
                I-O    BP13K482                                                 
                       BP13K486                                                 
                OUTPUT BP13LCAE.                                                
                                                                                
           IF BP27K800-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR, BP27K800-STATUS ' BP27K800-STATUS        
               MOVE     BP27K800-STATUS    TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
           IF BP27K860-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR, BP27K860-STATUS ' BP27K860-STATUS        
               MOVE     BP27K860-STATUS    TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
           IF BP13K800-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR, BP13K800-STATUS ' BP13K800-STATUS        
               MOVE     BP13K800-STATUS    TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
           IF BP13K820-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR, BP13K820-STATUS ' BP13K820-STATUS        
               MOVE     BP13K820-STATUS    TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
           IF BP13K482-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR, BP13K482-STATUS ' BP13K482-STATUS        
               MOVE     BP13K482-STATUS    TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
           IF BP13K486-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR, BP13K486-STATUS ' BP13K486-STATUS        
               MOVE     BP13K486-STATUS    TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT           TO WS-SYS-DATE.                      
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-SYS-TIME.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-F310.                                                          
      *----------------------------------------------------------------*        
                                                                                
           READ BP13F310                                                        
             AT END                                                             
                MOVE 'Y'   TO WS-F310-EOF                                       
                GO         TO 2000-EXIT                                         
           END-READ.                                                            
                                                                                
           ADD 1           TO NO-F310-READ.                                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
           MOVE F310-REGN-NO                   TO K800-NUM-REGN                 
           PERFORM 8001-READ-BP13K800        THRU 8001-EXIT                     
                                                                                
           MOVE K800-NUM-HLA OF BP13K800-MASTER                                 
             TO K800-NUM-HLA OF BP27K800-MASTER.                                
           PERFORM 8002-READ-BP27K800        THRU 8002-EXIT                     
                                                                                
           IF BP27K800-FND                                                      
              IF K800-NUM-CASE-STATUS = 'AC ' OR 'EXP'                          
                 CONTINUE                                                       
              ELSE                                                              
                 PERFORM 4100-OUTPUT-NA-HLE  THRU 4100-EXIT                     
              END-IF                                                            
           ELSE                                                                 
              PERFORM 4200-OUTPUT-NF-HLE     THRU 4200-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-HLA OF BP27K800-MASTER TO K860-NUM-HLA                 
           PERFORM 8003-READ-BP27K860         THRU 8003-EXIT                    
                                                                                
           IF NOT BP27K860-FND                                                  
              PERFORM 4300-OUTPUT-NF2-HLE    THRU 4300-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE 'N'                            TO WS-LEAS-EXIST                 
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 5                      
              IF K860-NUM-CURR-HDBREF(WS-I) NOT = SPACES AND LOW-VALUES         
                 MOVE 'Y'                      TO WS-LEAS-EXIST                 
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           IF LEAS-EXIST                                                        
              PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 5                   
                 IF K860-NUM-CURR-HDBREF(WS-I) NOT = SPACES                     
                                                 AND LOW-VALUES                 
                    PERFORM 3100-CREATE-K482       THRU 3100-EXIT               
                    PERFORM 3200-CREATE-K486       THRU 3200-EXIT               
                 END-IF                                                         
              END-PERFORM                                                       
           ELSE                                                                 
              PERFORM 4400-OUTPUT-NO-REF     THRU 4400-EXIT                     
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F310            THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3100-CREATE-K482.                                                        
      *----------------------------------------------------------------*        
                                                                                
           MOVE K800-NUM-SCH-ACC           TO K482-NUM-HDBREF-NEW               
           MOVE K860-NUM-CURR-HDBREF(WS-I) TO K482-NUM-HDBREF-DISPOSAL          
           PERFORM 8004-READ-BP13K482    THRU 8004-EXIT.                        
                                                                                
           MOVE SPACES                     TO BP13K482-REC                      
           INITIALIZE                         BP13K482-REC                      
                                                                                
           MOVE K800-NUM-SCH-ACC           TO K482-NUM-HDBREF-NEW               
           MOVE K860-NUM-CURR-HDBREF(WS-I) TO K482-NUM-HDBREF-DISPOSAL          
           MOVE K800-NUM-REGN OF BP13K800-MASTER                                
             TO K482-NUM-REGN                                                   
           MOVE K800-NUM-NRIC1 OF BP13K800-MASTER                               
             TO K482-NUM-NRIC1-NEW                                              
           MOVE K800-NUM-NRIC2 OF BP13K800-MASTER                               
             TO K482-NUM-NRIC2-NEW                                              
           MOVE K800-NUM-NRIC3 OF BP13K800-MASTER                               
             TO K482-NUM-NRIC3-NEW                                              
           MOVE K800-NUM-NRIC4 OF BP13K800-MASTER                               
             TO K482-NUM-NRIC4-NEW                                              
           MOVE K860-NUM-CURR-NRIC1(WS-I)  TO K482-NUM-NRIC1-DISPOSAL           
           MOVE K860-NUM-CURR-NRIC2(WS-I)  TO K482-NUM-NRIC2-DISPOSAL           
           MOVE K860-NUM-CURR-NRIC3(WS-I)  TO K482-NUM-NRIC3-DISPOSAL           
           MOVE K860-NUM-CURR-NRIC4(WS-I)  TO K482-NUM-NRIC4-DISPOSAL           
                                                                                
           MOVE SPACES                     TO WS-HA-SP.                         
                                                                                
           MOVE K800-NUM-REGN              TO K820-NUM-REGN                     
           MOVE K800-NUM-NRIC1 OF BP13K800-MASTER                               
             TO K820-NUM-NRIC  WS-HA(1)                                         
           PERFORM 8005-READ-BP13K820    THRU 8005-EXIT                         
           MOVE K820-NUM-CPF               TO K482-NUM-CPF1-NEW                 
           MOVE K820-NUM-NRIC-SPOUSE       TO K482-NUM-NRIC1-SPOUSE-NEW         
                                              WS-SP(1)                          
                                                                                
           IF K800-NUM-NRIC2 OF BP13K800-MASTER NOT = SPACES AND                
                                                      LOW-VALUES                
              MOVE K800-NUM-REGN           TO K820-NUM-REGN                     
              MOVE K800-NUM-NRIC2 OF BP13K800-MASTER                            
                TO K820-NUM-NRIC  WS-HA(2)                                      
              PERFORM 8005-READ-BP13K820 THRU 8005-EXIT                         
              MOVE K820-NUM-CPF            TO K482-NUM-CPF2-NEW                 
              MOVE K820-NUM-NRIC-SPOUSE    TO K482-NUM-NRIC2-SPOUSE-NEW         
                                              WS-SP(2)                          
           END-IF                                                               
                                                                                
           IF K800-NUM-NRIC3 OF BP13K800-MASTER NOT = SPACES AND                
                                                      LOW-VALUES                
              MOVE K800-NUM-REGN           TO K820-NUM-REGN                     
              MOVE K800-NUM-NRIC3 OF BP13K800-MASTER                            
                TO K820-NUM-NRIC  WS-HA(3)                                      
              PERFORM 8005-READ-BP13K820 THRU 8005-EXIT                         
              MOVE K820-NUM-CPF            TO K482-NUM-CPF3-NEW                 
              MOVE K820-NUM-NRIC-SPOUSE    TO K482-NUM-NRIC3-SPOUSE-NEW         
                                              WS-SP(3)                          
           END-IF                                                               
                                                                                
           IF K800-NUM-NRIC4 OF BP13K800-MASTER NOT = SPACES AND                
                                                      LOW-VALUES                
              MOVE K800-NUM-REGN           TO K820-NUM-REGN                     
              MOVE K800-NUM-NRIC4 OF BP13K800-MASTER                            
                TO K820-NUM-NRIC  WS-HA(4)                                      
              PERFORM 8005-READ-BP13K820 THRU 8005-EXIT                         
              MOVE K820-NUM-CPF            TO K482-NUM-CPF4-NEW                 
              MOVE K820-NUM-NRIC-SPOUSE    TO K482-NUM-NRIC4-SPOUSE-NEW         
                                              WS-SP(4)                          
           END-IF                                                               
                                                                                
           MOVE 'BP13'                     TO K482-NUM-SYSTEM                   
           MOVE 'BP13CCAE'                 TO K482-USERID                       
           MOVE WS-SYS-DATE                TO K482-DTE-UPDATE                   
           MOVE WS-SYS-TIME                TO K482-TME-UPDATE.                  
                                                                                
           IF BP13K482-FND                                                      
              PERFORM 8007-REWRITE-BP13K482 THRU 8007-EXIT                      
           ELSE                                                                 
              PERFORM 8006-WRITE-BP13K482   THRU 8006-EXIT                      
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3200-CREATE-K486.                                                        
      *----------------------------------------------------------------*        
                                                                                
           MOVE K860-NUM-CURR-NRIC1(WS-I)     TO WS-CUR-NRIC(1)                 
           MOVE K860-NUM-CURR-NRIC2(WS-I)     TO WS-CUR-NRIC(2)                 
           MOVE K860-NUM-CURR-NRIC3(WS-I)     TO WS-CUR-NRIC(3)                 
           MOVE K860-NUM-CURR-NRIC4(WS-I)     TO WS-CUR-NRIC(4)                 
                                                                                
           PERFORM VARYING WS-J FROM 1 BY 1 UNTIL WS-J > 4                      
              IF WS-CUR-NRIC(WS-J) NOT = SPACES AND LOW-VALUES                  
                 IF WS-CUR-NRIC(WS-J) = WS-HA(1) OR WS-HA(2)                    
                                     OR WS-HA(3) OR WS-HA(4)                    
                                     OR WS-SP(1) OR WS-SP(2)                    
                                     OR WS-SP(3) OR WS-SP(4)                    
                    MOVE WS-CUR-NRIC(WS-J)     TO K486-NUM-NRIC                 
                    MOVE K800-NUM-SCH-ACC      TO K486-NUM-HDBREF-NEW           
                    MOVE K860-NUM-CURR-HDBREF(WS-I)                             
                                               TO K486-NUM-HDBREF-EXIST         
                    PERFORM 8008-READ-BP13K486 THRU 8008-EXIT                   
                    MOVE SPACES                TO BP13K486-REC                  
                    INITIALIZE                     BP13K486-REC                 
                    MOVE WS-CUR-NRIC(WS-J)     TO K486-NUM-NRIC                 
                    MOVE K800-NUM-SCH-ACC      TO K486-NUM-HDBREF-NEW           
                    MOVE K860-NUM-CURR-HDBREF(WS-I)                             
                                               TO K486-NUM-HDBREF-EXIST         
                    MOVE 'BP13CCAE'           TO K486-NUM-USERID                
                    MOVE 'BP13'               TO K486-NUM-SYSTEM-UPDATE         
                    IF WS-CUR-NRIC(WS-J) = WS-HA(1) OR WS-HA(2) OR              
                                           WS-HA(3) OR WS-HA(4)                 
                       MOVE 'L'               TO K486-CDE-OWNER-STATUS          
                    ELSE                                                        
                       MOVE 'S'               TO K486-CDE-OWNER-STATUS          
                    END-IF                                                      
                    MOVE WS-SYS-DATE          TO K486-DTE-UPDATE                
                    MOVE WS-SYS-TIME          TO K486-TME-UPDATE                
                    IF BP13K486-FND                                             
                       PERFORM 8009-REWRITE-BP13K486  THRU 8009-EXIT            
                    ELSE                                                        
                       PERFORM 8010-WRITE-BP13K486    THRU 8010-EXIT            
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-OUTPUT-HEADER.                                                      
      *----------------------------------------------------------------*        
                                                                                
           ADD 1                              TO WS-PAGE                        
           MOVE WS-PAGE                       TO LCAE-PAGE                      
           STRING WS-SYS-DATE(7:2) '/' WS-SYS-DATE(5:2) '/'                     
                  WS-SYS-DATE(1:4) DELIMITED BY SIZE INTO LCAE-DATE.            
                                                                                
           WRITE BP13LCAE-REC FROM LCAE-HDG1 AFTER PAGE.                        
           WRITE BP13LCAE-REC FROM LCAE-HDG2.                                   
           WRITE BP13LCAE-REC FROM LCAE-HDG3 AFTER 2.                           
                                                                                
           MOVE 4                             TO WS-NO-REC.                     
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4100-OUTPUT-NA-HLE.                                                      
      *----------------------------------------------------------------*        
                                                                                
           ADD 1                             TO WS-NO-REC                       
           IF WS-NO-REC > 58                                                    
              PERFORM 4000-OUTPUT-HEADER   THRU 4000-EXIT                       
              ADD 1                          TO WS-NO-REC                       
           END-IF.                                                              
                                                                                
           MOVE SPACES                       TO LCAE-DTL.                       
           INITIALIZE                           LCAE-DTL.                       
                                                                                
           ADD 1                             TO WS-SNO                          
           MOVE WS-SNO                       TO LCAE-SNO                        
           MOVE F310-REGN-NO                 TO LCAE-REGN                       
           MOVE F310-CDE-TRANS               TO LCAE-TRANS                      
           MOVE F310-SCH-ACC                 TO LCAE-SCH-ACC                    
           MOVE K800-NUM-HLA OF BP13K800-MASTER  TO LCAE-HLA                    
           MOVE K800-NUM-CASE-STATUS         TO LCAE-STATUS                     
           MOVE 'CASE NOT AVAILABLE'         TO LCAE-REASON.                    
                                                                                
           WRITE BP13LCAE-REC FROM LCAE-DTL.                                    
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4200-OUTPUT-NF-HLE.                                                      
      *----------------------------------------------------------------*        
                                                                                
           ADD 1                             TO WS-NO-REC                       
           IF WS-NO-REC > 58                                                    
              PERFORM 4000-OUTPUT-HEADER   THRU 4000-EXIT                       
              ADD 1                          TO WS-NO-REC                       
           END-IF.                                                              
                                                                                
           MOVE SPACES                       TO LCAE-DTL.                       
           INITIALIZE                           LCAE-DTL.                       
                                                                                
           ADD 1                             TO WS-SNO                          
           MOVE WS-SNO                       TO LCAE-SNO                        
           MOVE F310-REGN-NO                 TO LCAE-REGN                       
           MOVE F310-CDE-TRANS               TO LCAE-TRANS                      
           MOVE F310-SCH-ACC                 TO LCAE-SCH-ACC                    
           MOVE K800-NUM-HLA OF BP13K800-MASTER                                 
             TO LCAE-HLA                                                        
           MOVE SPACES                       TO LCAE-STATUS                     
           MOVE 'CASE NOT FND IN BP27K800'   TO LCAE-REASON.                    
                                                                                
           WRITE BP13LCAE-REC FROM LCAE-DTL.                                    
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4300-OUTPUT-NF2-HLE.                                                     
      *----------------------------------------------------------------*        
                                                                                
           ADD 1                             TO WS-NO-REC                       
           IF WS-NO-REC > 58                                                    
              PERFORM 4000-OUTPUT-HEADER   THRU 4000-EXIT                       
              ADD 1                          TO WS-NO-REC                       
           END-IF.                                                              
                                                                                
           MOVE SPACES                       TO LCAE-DTL.                       
           INITIALIZE                           LCAE-DTL.                       
                                                                                
           ADD 1                             TO WS-SNO                          
           MOVE WS-SNO                       TO LCAE-SNO                        
           MOVE F310-REGN-NO                 TO LCAE-REGN                       
           MOVE F310-CDE-TRANS               TO LCAE-TRANS                      
           MOVE F310-SCH-ACC                 TO LCAE-SCH-ACC                    
           MOVE K800-NUM-HLA OF BP13K800-MASTER                                 
             TO LCAE-HLA                                                        
           MOVE K800-NUM-CASE-STATUS         TO LCAE-STATUS                     
           MOVE 'CASE NOT FND IN BP27K860'   TO LCAE-REASON.                    
                                                                                
           WRITE BP13LCAE-REC FROM LCAE-DTL.                                    
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4400-OUTPUT-NO-REF.                                                      
      *----------------------------------------------------------------*        
                                                                                
           ADD 1                             TO WS-NO-REC                       
           IF WS-NO-REC > 58                                                    
              PERFORM 4000-OUTPUT-HEADER   THRU 4000-EXIT                       
              ADD 1                          TO WS-NO-REC                       
           END-IF.                                                              
                                                                                
           MOVE SPACES                       TO LCAE-DTL.                       
           INITIALIZE                           LCAE-DTL.                       
                                                                                
           ADD 1                             TO WS-SNO                          
           MOVE WS-SNO                       TO LCAE-SNO                        
           MOVE F310-REGN-NO                 TO LCAE-REGN                       
           MOVE F310-CDE-TRANS               TO LCAE-TRANS                      
           MOVE F310-SCH-ACC                 TO LCAE-SCH-ACC                    
           MOVE K800-NUM-HLA OF BP13K800-MASTER                                 
             TO LCAE-HLA                                                        
           MOVE K800-NUM-CASE-STATUS         TO LCAE-STATUS                     
           MOVE 'NO SCH ACC FND IN BP28K860' TO LCAE-REASON.                    
                                                                                
           WRITE BP13LCAE-REC FROM LCAE-DTL.                                    
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       8001-READ-BP13K800.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF BP13K800-STATUS = 00 OR 02                                        
              CONTINUE                                                          
           ELSE                                                                 
              IF BP13K800-STATUS = 23                                           
                 DISPLAY 'BP13K800 - RECORD NOT FOUND ('                        
                         K800-NUM-REGN ')'                                      
              ELSE                                                              
                 DISPLAY 'BP13K800 - READ ERROR (' BP13K800-STATUS ')'          
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       8002-READ-BP27K800.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ BP27K800.                                                       
                                                                                
           IF BP27K800-STATUS = 00 OR 02                                        
              MOVE 'Y'                       TO WS-BP27K800-FND                 
           ELSE                                                                 
              IF BP27K800-STATUS = 23                                           
                 MOVE 'N'                    TO WS-BP27K800-FND                 
              ELSE                                                              
                 DISPLAY 'BP27K800 - READ ERROR (' BP27K800-STATUS ')'          
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8002-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       8003-READ-BP27K860.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ BP27K860.                                                       
                                                                                
           IF BP27K860-STATUS = 00 OR 02                                        
              MOVE 'Y'                        TO WS-BP27K860-FND                
           ELSE                                                                 
              IF BP27K860-STATUS = 23                                           
                 MOVE 'N'                     TO WS-BP27K860-FND                
              ELSE                                                              
                 DISPLAY 'BP27K860 - READ ERROR (' BP27K860-STATUS ')'          
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8003-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       8004-READ-BP13K482.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ BP13K482.                                                       
                                                                                
           IF BP13K482-STATUS = 00 OR 02                                        
              MOVE 'Y'                        TO WS-BP13K482-FND                
           ELSE                                                                 
              IF BP13K482-STATUS = 23                                           
                 MOVE 'N'                     TO WS-BP13K482-FND                
              ELSE                                                              
                 DISPLAY 'BP13K482 - READ ERROR (' BP13K482-STATUS ')'          
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8004-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       8005-READ-BP13K820.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF BP13K820-STATUS = 00 OR 02                                        
              CONTINUE                                                          
           ELSE                                                                 
              IF BP13K820-STATUS = 23                                           
                 MOVE SPACES                  TO BP13K820-REC                   
              ELSE                                                              
                 DISPLAY 'BP13K820 - READ ERROR (' BP13K820-STATUS ')'          
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8005-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       8006-WRITE-BP13K482.                                                     
      *----------------------------------------------------------------*        
                                                                                
           WRITE BP13K482-REC.                                                  
                                                                                
           IF BP13K482-STATUS = 00 OR 02                                        
              ADD 1                       TO NO-K482-WRITE                      
           ELSE                                                                 
              DISPLAY 'BP13K482 - WRITE ERROR (' BP13K482-STATUS ')'            
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       8006-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       8007-REWRITE-BP13K482.                                                   
      *----------------------------------------------------------------*        
                                                                                
           REWRITE BP13K482-REC.                                                
                                                                                
           IF BP13K482-STATUS = 00 OR 02                                        
              ADD 1                       TO NO-K482-REWRITE                    
           ELSE                                                                 
              DISPLAY 'BP13K482 - REWRITE ERROR (' BP13K482-STATUS ')'          
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       8007-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       8008-READ-BP13K486.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ BP13K486.                                                       
                                                                                
           IF BP13K486-STATUS = 00 OR 02                                        
              MOVE 'Y'                        TO WS-BP13K486-FND                
           ELSE                                                                 
              IF BP13K486-STATUS = 23                                           
                 MOVE 'N'                     TO WS-BP13K486-FND                
              ELSE                                                              
                 DISPLAY 'BP13K486 - READ ERROR (' BP13K486-STATUS ')'          
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8008-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       8009-REWRITE-BP13K486.                                                   
      *----------------------------------------------------------------*        
                                                                                
           REWRITE BP13K486-REC.                                                
                                                                                
           IF BP13K486-STATUS = 00 OR 02                                        
              ADD 1                      TO NO-K486-REWRITE                     
           ELSE                                                                 
              DISPLAY 'BP13K486 - REWRITE ERROR (' BP13K486-STATUS ')'          
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       8009-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       8010-WRITE-BP13K486.                                                     
      *----------------------------------------------------------------*        
                                                                                
           WRITE BP13K486-REC.                                                  
                                                                                
           IF BP13K486-STATUS = 00 OR 02                                        
              ADD 1                      TO NO-K486-WRITE                       
           ELSE                                                                 
              DISPLAY 'BP13K486 - WRITE ERROR (' BP13K486-STATUS ')'            
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       8010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-ROUTINE.                                                      
      *----------------------------------------------------------------*        
           CLOSE BP13F310                                                       
                 SY02F001                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP27K800                                                       
                 BP27K860                                                       
                 BP13K482                                                       
                 BP13K486                                                       
                 BP13LCAE.                                                      
                                                                                
           IF BP13K800-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K800-STATUS ' BP13K800-STATUS         
              MOVE BP13K800-STATUS  TO RETURN-CODE.                             
           IF BP13K820-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K820-STATUS ' BP13K820-STATUS         
              MOVE BP13K820-STATUS  TO RETURN-CODE.                             
           IF BP27K800-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP27K800-STATUS ' BP27K800-STATUS         
              MOVE BP27K800-STATUS  TO RETURN-CODE.                             
           IF BP27K860-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP27K860-STATUS ' BP27K860-STATUS         
              MOVE BP27K860-STATUS  TO RETURN-CODE.                             
           IF BP13K482-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K482-STATUS ' BP13K482-STATUS         
              MOVE BP13K482-STATUS  TO RETURN-CODE.                             
           IF BP13K486-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K486-STATUS ' BP13K486-STATUS         
              MOVE BP13K486-STATUS  TO RETURN-CODE.                             
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '***** BP13CCAE CONTROL LIST ********'                       
           DISPLAY ' NO-F310-READ       : ' NO-F310-READ.                       
           DISPLAY ' NO-K482-WRITE      : ' NO-K482-WRITE.                      
           DISPLAY ' NO-K482-REWRITE    : ' NO-K482-REWRITE.                    
           DISPLAY ' NO-K486-WRITE      : ' NO-K486-WRITE.                      
           DISPLAY ' NO-K486-REWRITE    : ' NO-K486-REWRITE.                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
