       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C42I.                                                 
      *AUTHOR.        KSJ3                                                      
      *DATE-WRITTEN.  21/01/2016.                                               
      * ========================================================== *            
      *                SYSTEM OF COMMITMENT (BP13)                 *            
      * ========================================================== *            
      *  OBJECTIVE : PROCESS AG07 VOUCHER RETURN FILE (AG07F640)   *            
      *              FOR RESALE LEVY LEVY - PREMIUM LEVY CONV.     *            
      *                                                            *            
      *    1. SUCCESSFUL RECS - UPDATE BP13KC10 AND BP13K470       *            
      *    2. REJECTED RECS - BACK-UP OLD BP13KC10 RECORD AND      *            
      *       DELETE/CREATE NEW RECORD WITH NEWLY GENERATED VR NO. *            
      *       PRINT NEW VR AND REJECTION DETAILS IN REPORT.        *            
      *                                                            *            
      *  INPUT   :  AG07F640 - AG07 INTERFACE FILE                 *            
      *             AG07K970 - AG07 REJECT CODES                   *            
      *                                                            *            
      *  I-O     :  BP13KC10 - SOC LEVY VOUCHER FILE               *            
      *             BP13K470 - SOC PREMIUM FILE                    *            
      *                                                            *            
      *  OUTPUT  :  BP13FC10 - SOC LEVY VOUCHER FILE (SEQ AS BKUP) *            
      *                                                            *            
      *  LISTING :  P13L42IA - REJECTED VOUCHERS LIST              *            
      *          :  P13L42IB - ERROR LIST                          *            
      *                                                            *            
      * ========================================================== *            
      * CHG-NO    BY    DATE    DESCRIPTION                        *            
      * BP135600  KSJ3  210116  NEW PROGRAM                        *            
      * BP135858  KSJ3  170216  FIX ISSUE ON BP13K470 UPDATE       *            
      *                         ADD WRITE TO BP13K130 FOR SUCCESS  *            
      *                         RECORDS RETURNED                   *            
      * BP135861  KSJ3  240216  FIX ISSUE ON FIN HIST RECORDS      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AG07F640 ASSIGN        TO AG07F640.                           
                                                                                
           SELECT BP13KC10 ASSIGN        TO BP13KC10                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS KC10-KEY-FLD                        
                           ALTERNATE KEY IS KC10-ALT-KEY1                       
                           FILE STATUS   IS WS-KC10-STATUS.                     
                                                                                
           SELECT BP13K470 ASSIGN        TO BP13K470                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K470-KEY-FLD                        
                           FILE STATUS   IS WS-K470-STATUS.                     
                                                                                
           SELECT BP13K350 ASSIGN        TO BP13K350                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K350-KEY-FLD                        
                           FILE STATUS   IS WS-K350-STATUS.                     
                                                                                
           SELECT BP13K130 ASSIGN        TO BP13K130                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K130-KEY-FLD                        
                           FILE STATUS   IS WS-K130-STATUS.                     
                                                                                
           SELECT AG07K970 ASSIGN        TO AG07K970                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K970-KEY-FLD                        
                           FILE STATUS   IS WS-K970-STATUS.                     
                                                                                
           SELECT BP13FC10 ASSIGN        TO BP13FC10.                           
           SELECT P13L42IA ASSIGN        TO P13L42IA.                           
           SELECT P13L42IB ASSIGN        TO P13L42IB.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  AG07F640                                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
           COPY AG07F640.                                                       
                                                                                
       FD  BP13KC10                                                             
           RECORD CONTAINS 800 CHARACTERS.                                      
           COPY BP13KC10.                                                       
                                                                                
       FD  BP13K470                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
           COPY BP13K470.                                                       
                                                                                
       FD  AG07K970                                                             
           RECORD CONTAINS 40  CHARACTERS.                                      
           COPY AG07K970.                                                       
                                                                                
       FD  BP13K350                                                             
           RECORD CONTAINS 600 CHARACTERS.                                      
           COPY BP13K350.                                                       
                                                                                
       FD  BP13K130                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
           COPY BP13K130.                                                       
                                                                                
       FD  BP13FC10                                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
           COPY BP13FC10.                                                       
                                                                                
       FD  P13L42IA                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  P13L42IA-REC                  PIC X(132).                            
                                                                                
       FD  P13L42IB                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  P13L42IB-REC                  PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-KC10-STATUS            PIC X(2)    VALUE SPACES.              
           05  WS-K470-STATUS            PIC X(2)    VALUE SPACES.              
           05  WS-K970-STATUS            PIC X(2)    VALUE SPACES.              
           05  WS-K350-STATUS            PIC X(2)    VALUE SPACES.              
           05  WS-K130-STATUS            PIC X(2)    VALUE SPACES.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F640-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F640-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F640-ACC           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F640-REJ           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KC10-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KC10-WRT           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KC10-DEL           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-FC10-WRT           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K470-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-PAGE-A             PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-PAGE-B             PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-LINE-A             PIC 9(2)   VALUE 60.                   
           05  WS-CNT-LINE-B             PIC 9(2)   VALUE 60.                   
           05  WS-CNT-SNO-A              PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-SNO-B              PIC 9(4)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F640-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-KC10-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K470-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K970-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K350-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-SKIP-REC               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-FMT8-CCYY      PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-FMT8-MM        PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-FMT8-DD        PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-FMT10-DD       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-MM       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-CCYY.                                           
                   15  WS-DTE-FMT10-CC   PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-FMT10-YY   PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-TMP                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-INT                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-NUM-DDD-FILE-REF.                                             
               10  WS-NUM-REGN           PIC X(8)   VALUE SPACES.               
               10  FILLER                PIC X(7)   VALUE SPACES.               
           05  WS-NUM-VR.                                                       
               10  WS-NUM-DDD            PIC X(3).                              
               10  WS-NUM-FIN-YEAR       PIC X(2).                              
               10  WS-NUM-VR-SERIAL      PIC X(6).                              
           05  WS-BP13KC10-REC-BKUP      PIC X(800) VALUE SPACES.               
           05  WS-IDX                    PIC 9(1)   VALUE ZEROES.               
                                                                                
      *-------------------------------------------------------------            
      *    P13L42IA REPORT (REJECT LIST)                                        
      *-------------------------------------------------------------            
       01  L42IA-HEADER1.                                                       
           05  FILLER                    PIC X(9)   VALUE 'BP13L42IA'.          
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE 'HDBCAT 3'.           
           05  FILLER                    PIC X(28)  VALUE SPACES.               
           05  FILLER                    PIC X(39)  VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(17)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE 'DATE: '.             
           05  L42IA-DTE-CURRENT         PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE '  PAGE: '.           
           05  L42IA-CNT-PAGE            PIC ZZZ9.                              
                                                                                
       01  L42IA-HEADER2.                                                       
           05  FILLER                    PIC X(37)  VALUE SPACES.               
           05  FILLER                    PIC X(48)  VALUE                       
               'VOUCHER REASSIGNMENT DUE TO ETLSVS REJECTION ON '.              
           05  L42IA-DTE-CURRENT2        PIC X(10)  VALUE SPACES.               
                                                                                
       01  L42IA-HEADER3.                                                       
           05  FILLER                    PIC X(132) VALUE                       
               'S/NO    REGN        NEW VOUCHER NO    OLD VOUCHER NO            
      -        'SUBMIT BY    SUBMIT DATE    REJECT REASON'.                     
                                                                                
       01  L42IA-HEADER4.                                                       
           05  FILLER                    PIC X(132) VALUE                       
               '----    --------    --------------    --------------            
      -        '---------    -----------    ----------------------------        
      -        '-------------'.                                                 
                                                                                
       01  L42IA-DETAIL.                                                        
           05  L42IA-SNO                 PIC ZZZ9.                              
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  L42IA-NUM-REGN            PIC X(8)   VALUE SPACES.               
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  L42IA-NUM-NEW-VR          PIC X(13)  VALUE SPACES.               
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L42IA-NUM-OLD-VR          PIC X(13)  VALUE SPACES.               
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L42IA-NUM-SUB-BY          PIC X(8)   VALUE SPACES.               
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  L42IA-DTE-SUBMITTED       PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L42IA-NUM-DESC            PIC X(41)  VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      *    P13L42IB REPORT (EXCEPTION LIST)                                     
      *-------------------------------------------------------------            
       01  L42IB-HEADER1.                                                       
           05  FILLER                    PIC X(9)   VALUE 'BP13L42IB'.          
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE 'HDBCAT 3'.           
           05  FILLER                    PIC X(28)  VALUE SPACES.               
           05  FILLER                    PIC X(39)  VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(17)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE 'DATE: '.             
           05  L42IB-DTE-CURRENT         PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE '  PAGE: '.           
           05  L42IB-CNT-PAGE            PIC ZZZ9.                              
                                                                                
       01  L42IB-HEADER2.                                                       
           05  FILLER                    PIC X(44)  VALUE SPACES.               
           05  FILLER                    PIC X(44)  VALUE                       
               'EXCEPTION REPORT ON RESALE LEVY VOUCHERS'.                      
                                                                                
       01  L42IB-HEADER3.                                                       
                05  FILLER                    PIC X(132) VALUE                  
               '    S/NO    REGN NO     VOUCHER NO       ERROR DESCRIPTI        
      -        'ON'.                                                            
                                                                                
       01  L42IB-HEADER4.                                                       
           05  FILLER                    PIC X(132) VALUE                       
               '    ----    --------    -------------    ---------------        
      -        '-----------------------------------'.                           
                                                                                
       01  L42IB-DETAIL.                                                        
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  L42IB-SNO                 PIC ZZZ9.                              
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  L42IB-NUM-REGN            PIC X(8)   VALUE SPACES.               
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  L42IB-NUM-VR              PIC X(13)  VALUE SPACES.               
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  L42IB-NUM-DESC            PIC X(50)  VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      *    WS VARIABLES PASSED FROM SUB-PROGRAM "AG07C907"                      
      *-------------------------------------------------------------            
       COPY AG7C907C.                                                           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-AG07F640-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F640-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  AG07F640                                                 
                       AG07K970                                                 
                       BP13K350                                                 
                I-O    BP13KC10                                                 
                       BP13K470                                                 
                       BP13K130                                                 
                OUTPUT BP13FC10                                                 
                       P13L42IA                                                 
                       P13L42IB.                                                
                                                                                
           IF WS-K350-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K350 ERROR ' WS-K350-STATUS                  
              MOVE     WS-K350-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KC10-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC10 ERROR ' WS-KC10-STATUS                  
              MOVE     WS-KC10-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K470-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K470 ERROR ' WS-K470-STATUS                  
              MOVE     WS-K470-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K130-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K130 ERROR ' WS-K130-STATUS                  
              MOVE     WS-K130-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K970-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K970 ERROR ' WS-K970-STATUS                  
              MOVE     WS-K970-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    OPEN VSAM FOR VOUCHER NUMBER GENERATION                              
      *-------------------------------------------------------------            
           MOVE 'AG07K960'                 TO   C907-NUM-FILENME.               
           CALL 'K960OPEN' USING AG07C907-AREA.                                 
                                                                                
           IF C907-NUM-FILE-STATUS NOT = '00' AND '97'                          
              DISPLAY 'OPENING AG07K960 ERROR ' C907-NUM-FILE-STATUS            
              MOVE     C907-NUM-FILE-STATUS     TO   RETURN-CODE                
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           PERFORM 8100-CONV-DATE-TO-10    THRU 8100-EXIT.                      
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-AG07F640-READ.                                                      
      *=============================================================            
                                                                                
           READ AG07F640 AT END                                                 
                MOVE 'Y'                   TO   WS-F640-EOF                     
                GO                         TO   2000-EXIT.                      
                                                                                
           ADD 1                           TO   WS-CNT-F640-READ.               
           MOVE F640-NUM-DDD-FILE-REF      TO   WS-NUM-DDD-FILE-REF.            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   L42IB-NUM-DESC.                 
                                                                                
           MOVE WS-NUM-REGN                TO   KC10-NUM-REGN.                  
           PERFORM 3100-BP13KC10-READ      THRU 3100-EXIT.                      
                                                                                
           IF WS-KC10-FND = 'Y'                                                 
              IF KC10-NUM-VR NOT = F640-NUM-VR                                  
                 ADD 1                     TO   WS-CNT-F640-SKIP                
              ELSE                                                              
                 IF KC10-NUM-VR-STATUS NOT = 'P'                                
                    ADD 1                  TO   WS-CNT-F640-SKIP                
                    MOVE 'INVALID VOUCHER STATUS'                               
                                           TO   L42IB-NUM-DESC                  
                    PERFORM 8900-PRINT-ERROR                                    
                                           THRU 8900-EXIT                       
                 ELSE                                                           
                    EVALUATE F640-NUM-REJECT                                    
                    WHEN ZEROES                                                 
                    WHEN SPACES                                                 
                    WHEN LOW-VALUES                                             
                       ADD 1               TO   WS-CNT-F640-ACC                 
                       PERFORM 4000-UPD-LVYVCHR-ACC                             
                                           THRU 4000-EXIT                       
                       PERFORM 4100-UPD-PREMIUM THRU 4100-EXIT                  
                               VARYING WS-IDX FROM 1 BY 2                       
                                 UNTIL (WS-IDX > 8)                             
                                    OR (KC10-NUM-NRIC(WS-IDX) =                 
                                        SPACES OR LOW-VALUES OR ZEROES)         
                       PERFORM 4200-GET-STD-FLAT-TYPE                           
                                                THRU 4200-EXIT                  
                       PERFORM 4300-WRT-CDHIST  THRU 4300-EXIT                  
                               VARYING WS-IDX FROM 1 BY 1                       
                                 UNTIL (WS-IDX > 8)                             
                                    OR (KC10-NUM-NRIC(WS-IDX) =                 
                                        SPACES OR LOW-VALUES OR ZEROES)         
                                                                                
                    WHEN OTHER                                                  
                       ADD 1               TO   WS-CNT-F640-REJ                 
                       PERFORM 5000-GET-REJ-REASON                              
                                           THRU 5000-EXIT                       
                       PERFORM 5100-GET-NEW-VR                                  
                                           THRU 5100-EXIT                       
                       PERFORM 5200-PRINT-REJ                                   
                                           THRU 5200-EXIT                       
                       PERFORM 5300-XFER-LVYVCHR                                
                                           THRU 5300-EXIT                       
                                                                                
                    END-EVALUATE                                                
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              ADD 1                        TO   WS-CNT-F640-SKIP                
           END-IF.                                                              
                                                                                
           PERFORM 2000-AG07F640-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-BP13KC10-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13KC10.                                                       
                                                                                
           EVALUATE WS-KC10-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-KC10-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-KC10-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13KC10 ERROR '    WS-KC10-STATUS                  
              MOVE     WS-KC10-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4000-UPD-LVYVCHR-ACC.                                                    
      *=============================================================            
                                                                                
           MOVE 'S'                        TO   KC10-NUM-VR-STATUS.             
           MOVE WS-DTE-CURR-8              TO   KC10-DTE-VR-FIN.                
           MOVE F640-NUM-BATCH             TO   KC10-NUM-BATCH.                 
           MOVE 'BP13C42I'                 TO   KC10-NUM-USERID.                
           MOVE WS-DTE-CURR-8              TO   KC10-DTE-UPDATE.                
                                                                                
           PERFORM 4010-REWRITE-BP13KC10   THRU 4010-EXIT.                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4010-REWRITE-BP13KC10.                                                   
      *=============================================================            
                                                                                
           REWRITE BP13KC10-REC.                                                
                                                                                
           EVALUATE WS-KC10-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-KC10-UPD                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'REWRITE BP13KC10 ERROR ' WS-KC10-STATUS                  
              MOVE     WS-KC10-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4100-UPD-PREMIUM.                                                        
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13K470-REC.                   
           MOVE WS-NUM-REGN                TO   K470-NUM-REGN.                  
           MOVE KC10-NUM-NRIC(WS-IDX)      TO   K470-NUM-NRIC.                  
           PERFORM 4110-BP13K470-READ      THRU 4110-EXIT.                      
                                                                                
           IF WS-K470-FND = 'Y'                                                 
              MOVE 'S'                     TO   K470-NUM-VR-STATUS              
              MOVE 'BP13C42I'              TO   K470-NUM-USERID                 
              MOVE WS-DTE-CURR-8           TO   K470-DTE-UPDATE                 
                                                                                
              PERFORM 4120-REWRITE-BP13K470                                     
                                           THRU 4120-EXIT                       
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4110-BP13K470-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K470.                                                       
                                                                                
           EVALUATE WS-K470-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K470-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K470-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K470 ERROR '    WS-K470-STATUS                  
              MOVE     WS-K470-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4120-REWRITE-BP13K470.                                                   
      *=============================================================            
                                                                                
           REWRITE BP13K470-REC.                                                
                                                                                
           EVALUATE WS-K470-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K470-UPD                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'REWRITE BP13K470 ERROR ' WS-K470-STATUS                  
              MOVE     WS-K470-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4120-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4200-GET-STD-FLAT-TYPE.                                                  
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   K350-KEY-FLD.                   
           MOVE WS-NUM-REGN                TO   K350-NUM-REGN.                  
                                                                                
           PERFORM 4210-BP13K350-READ      THRU 4210-EXIT.                      
                                                                                
           IF WS-K350-FND = 'Y'                                                 
              MOVE SPACES                  TO   K350-NUM-HA1-RSLE-FT            
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4210-BP13K350-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K350.                                                       
                                                                                
           EVALUATE WS-K350-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K350-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K350-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K350 ERROR '    WS-K350-STATUS                  
              MOVE     WS-K350-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4210-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4300-WRT-CDHIST.                                                         
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13K130-CDHIST.                
                                                                                
           IF KC10-NUM-CR-DR(WS-IDX) = 'C'                                      
              MOVE '23'                    TO   K130-TRANS-TYPE                 
              MOVE 'LVL'                   TO   K130-PAYMENT-TYPE               
              IF KC10-AMT-TX(WS-IDX) NOT NUMERIC                                
                 MOVE 0                    TO   K130-AMT-CASH                   
              ELSE                                                              
                 COMPUTE K130-AMT-CASH = KC10-AMT-TX(WS-IDX) * -1               
              END-IF                                                            
           ELSE                                                                 
              MOVE '21'                    TO   K130-TRANS-TYPE                 
              MOVE 'PRM'                   TO   K130-PAYMENT-TYPE               
              IF KC10-AMT-TX(WS-IDX) NOT NUMERIC                                
                 MOVE 0                    TO   K130-AMT-CASH                   
              ELSE                                                              
                 MOVE KC10-AMT-TX(WS-IDX)  TO   K130-AMT-CASH                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE WS-NUM-REGN                TO   K130-NUM-ORIG-REGN.             
           MOVE WS-DTE-CURR-8              TO   K130-DTE-POST.                  
           COMPUTE K130-COUNT-HISTORY = WS-IDX - 1.                             
           MOVE KC10-NUM-NRIC(WS-IDX)      TO   K130-NUM-NRIC.                  
           MOVE KC10-NUM-CR-DR(WS-IDX)     TO   K130-AMT-TYP.                   
           MOVE WS-DTE-CURR-8              TO   K130-DTE-TRANS.                 
           MOVE KC10-NUM-HDB-REF(WS-IDX)   TO   K130-NUM-SCH-ACCT.              
           MOVE KC10-NUM-FLAT-TYPE(WS-IDX) TO   K130-NUM-FLAT-TYPE-SA.          
           MOVE K350-NUM-HA1-RSLE-FT       TO   K130-NUM-FLAT-TYPE-RSL.         
           MOVE F640-NUM-VR                TO   K130-NUM-RECPT-JRNO.            
                                                                                
           WRITE BP13K130-CDHIST.                                               
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5000-GET-REJ-REASON.                                                     
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   L42IA-NUM-DESC.                 
                                                                                
           MOVE F640-NUM-REJECT            TO   K970-NUM-CODE.                  
           PERFORM 5010-AG07K970-READ      THRU 5010-EXIT.                      
                                                                                
           IF WS-K970-FND = 'Y'                                                 
              STRING K970-NUM-CODE '-' K970-TXT-EMSG                            
                     DELIMITED BY SIZE     INTO L42IA-NUM-DESC                  
           ELSE                                                                 
              MOVE F640-NUM-REJECT         TO   L42IA-NUM-DESC                  
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5010-AG07K970-READ.                                                      
      *=============================================================            
                                                                                
           READ AG07K970.                                                       
                                                                                
           EVALUATE WS-K970-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K970-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K970-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ AG07K970 ERROR '    WS-K970-STATUS                  
              MOVE     WS-K970-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5100-GET-NEW-VR.                                                         
      *=============================================================            
                                                                                
           MOVE 'SS '                      TO   C907-NUM-DDD.                   
           MOVE 'AG07K960'                 TO   C907-NUM-FILENME.               
                                                                                
           CALL 'AG07C907' USING AG07C907-AREA.                                 
                                                                                
           IF C907-NUM-FILE-STATUS =  '00'                                      
              STRING C907-NUM-DDD                                               
                     C907-NUM-FIN-YR                                            
                     C907-NUM-VR-SERIAL                                         
                     DELIMITED BY SIZE INTO WS-NUM-VR                           
              END-STRING                                                        
           ELSE                                                                 
              MOVE C907-NUM-FILE-STATUS    TO   RETURN-CODE                     
              DISPLAY 'ERROR CALLING AG07C907, STATUS = '                       
                       C907-NUM-FILE-STATUS                                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5200-PRINT-REJ.                                                          
      *=============================================================            
                                                                                
           IF WS-CNT-LINE-A > 50                                                
              PERFORM 5210-PRINT-REJ-HDR   THRU 5210-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-SNO-A                    
                                                WS-CNT-LINE-A.                  
           MOVE WS-CNT-SNO-A               TO   L42IA-SNO.                      
           MOVE WS-NUM-REGN                TO   L42IA-NUM-REGN.                 
           MOVE WS-NUM-VR                  TO   L42IA-NUM-NEW-VR.               
           MOVE F640-NUM-VR                TO   L42IA-NUM-OLD-VR.               
           MOVE KC10-NUM-VR-SEND-BY        TO   L42IA-NUM-SUB-BY.               
           MOVE KC10-DTE-VR-SEND           TO   L42IA-DTE-SUBMITTED.            
                                                                                
           WRITE P13L42IA-REC              FROM L42IA-DETAIL.                   
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5210-PRINT-REJ-HDR.                                                      
      *=============================================================            
                                                                                
           ADD 1                           TO   WS-CNT-PAGE-A.                  
           MOVE WS-CNT-PAGE-A              TO   L42IA-CNT-PAGE.                 
           MOVE WS-DTE-CURR-10             TO   L42IA-DTE-CURRENT               
                                                L42IA-DTE-CURRENT2.             
                                                                                
           WRITE P13L42IA-REC FROM L42IA-HEADER1 AFTER PAGE.                    
           WRITE P13L42IA-REC FROM L42IA-HEADER2.                               
           WRITE P13L42IA-REC FROM L42IA-HEADER3 AFTER 2.                       
           WRITE P13L42IA-REC FROM L42IA-HEADER4.                               
                                                                                
           MOVE SPACES                     TO   P13L42IA-REC.                   
           WRITE P13L42IA-REC.                                                  
                                                                                
           MOVE 6                          TO   WS-CNT-LINE-A.                  
                                                                                
       5210-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5300-XFER-LVYVCHR.                                                       
      *=============================================================            
                                                                                
           MOVE BP13KC10-REC               TO   WS-BP13KC10-REC-BKUP.           
                                                                                
      *-------------------------------------------------------------            
      *    BACK-UP BP13KC10 REC TO BP13FC10 AND DELETE FROM BP13KC10            
      *-------------------------------------------------------------            
           PERFORM 5310-BP13FC10-WRITE     THRU 5310-EXIT.                      
           PERFORM 5320-BP13KC10-DELETE    THRU 5320-EXIT.                      
                                                                                
      *-------------------------------------------------------------            
      *    CREATE NEW BP13KC10 REC FROM WS BKUP WITH NEW VR NO                  
      *-------------------------------------------------------------            
           MOVE WS-BP13KC10-REC-BKUP       TO   BP13KC10-REC.                   
           MOVE WS-NUM-VR                  TO   KC10-NUM-VR.                    
           MOVE SPACES                     TO   KC10-NUM-VR-STATUS              
                                                KC10-DTE-VR-SEND                
                                                KC10-NUM-VR-SEND-BY             
                                                KC10-DTE-VR-FIN.                
           MOVE 'BP13C42I'                 TO   KC10-NUM-USERID                 
           MOVE WS-DTE-CURR-8              TO   KC10-DTE-VR-PREPARE             
                                                KC10-DTE-UPDATE.                
                                                                                
           PERFORM 5330-BP13KC10-WRITE     THRU 5330-EXIT.                      
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5310-BP13FC10-WRITE.                                                     
      *=============================================================            
                                                                                
           MOVE BP13KC10-REC               TO   BP13FC10-REC.                   
                                                                                
           WRITE BP13FC10-REC.                                                  
           ADD 1                           TO   WS-CNT-FC10-WRT.                
                                                                                
       5310-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5320-BP13KC10-DELETE.                                                    
      *=============================================================            
                                                                                
           DELETE BP13KC10.                                                     
                                                                                
           EVALUATE WS-KC10-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-KC10-DEL                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'DELETE BP13KC10 ERROR '  WS-KC10-STATUS                  
              MOVE     WS-KC10-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5320-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5330-BP13KC10-WRITE.                                                     
      *=============================================================            
                                                                                
           WRITE BP13KC10-REC.                                                  
                                                                                
           EVALUATE WS-KC10-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-KC10-WRT                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITE BP13KC10 ERROR '   WS-KC10-STATUS                  
              MOVE     WS-KC10-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5330-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8000-CONV-DATE-TO-8.                                                     
      *=============================================================            
      *    INPUT  - WS-DTE-FMT10                                                
      *    OUTPUT - WS-DTE-FMT8                                                 
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT10-CCYY  TO  WS-DTE-FMT8-CCYY.                        
           MOVE WS-DTE-FMT10-MM    TO  WS-DTE-FMT8-MM.                          
           MOVE WS-DTE-FMT10-DD    TO  WS-DTE-FMT8-DD.                          
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8100-CONV-DATE-TO-10.                                                    
      *=============================================================            
      *    INPUT  - WS-DTE-FMT8                                                 
      *    OUTPUT - WS-DTE-FMT10                                                
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT8-CCYY   TO  WS-DTE-FMT10-CCYY.                       
           MOVE WS-DTE-FMT8-MM     TO  WS-DTE-FMT10-MM.                         
           MOVE WS-DTE-FMT8-DD     TO  WS-DTE-FMT10-DD.                         
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8900-PRINT-ERROR.                                                        
      *=============================================================            
                                                                                
           IF WS-CNT-LINE-B > 50                                                
              PERFORM 8910-PRINT-ERROR-HDR THRU 8910-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-SNO-B                    
                                                WS-CNT-LINE-B.                  
           MOVE WS-CNT-SNO-B               TO   L42IB-SNO.                      
           MOVE WS-NUM-REGN                TO   L42IB-NUM-REGN.                 
           MOVE F640-NUM-VR                TO   L42IB-NUM-VR.                   
                                                                                
           WRITE P13L42IB-REC             FROM L42IB-DETAIL.                    
                                                                                
       8900-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8910-PRINT-ERROR-HDR.                                                    
      *=============================================================            
                                                                                
           ADD 1                           TO   WS-CNT-PAGE-B.                  
           MOVE WS-CNT-PAGE-B              TO   L42IB-CNT-PAGE.                 
           MOVE WS-DTE-CURR-10             TO   L42IB-DTE-CURRENT.              
                                                                                
           WRITE P13L42IB-REC FROM L42IB-HEADER1 AFTER PAGE.                    
           WRITE P13L42IB-REC FROM L42IB-HEADER2.                               
           WRITE P13L42IB-REC FROM L42IB-HEADER3 AFTER 2.                       
           WRITE P13L42IB-REC FROM L42IB-HEADER4.                               
                                                                                
           MOVE SPACES                     TO   P13L42IB-REC.                   
           WRITE P13L42IB-REC.                                                  
                                                                                
           MOVE 6                          TO   WS-CNT-LINE-B.                  
                                                                                
       8910-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*------- BP13C42I CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF AG07F640 READ      : ' WS-CNT-F640-READ.          
           DISPLAY ' 2. NO OF AG07F640 ACCEPTED  : ' WS-CNT-F640-ACC.           
           DISPLAY ' 3. NO OF AG07F640 REJECTED  : ' WS-CNT-F640-REJ.           
           DISPLAY ' 4. NO OF AG07F640 SKIPPED   : ' WS-CNT-F640-SKIP.          
           DISPLAY ' 5. NO OF BP13K470 UPDATED   : ' WS-CNT-K470-UPD.           
           DISPLAY ' 6. NO OF BP13KC10 UPDATED   : ' WS-CNT-KC10-UPD.           
           DISPLAY ' 7. NO OF BP13KC10 WRITTEN   : ' WS-CNT-KC10-WRT.           
           DISPLAY ' 8. NO OF BP13KC10 DELETED   : ' WS-CNT-KC10-DEL.           
           DISPLAY ' 9. NO OF BP13FC10 WRITTEN   : ' WS-CNT-FC10-WRT.           
           DISPLAY ' '.                                                         
                                                                                
           CLOSE AG07F640                                                       
                 AG07K970                                                       
                 BP13K350                                                       
                 BP13KC10                                                       
                 BP13K470                                                       
                 BP13K130                                                       
                 BP13FC10                                                       
                 P13L42IA                                                       
                 P13L42IB.                                                      
                                                                                
           IF WS-K350-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K350 ERROR ' WS-K350-STATUS                  
              MOVE     WS-K350-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-KC10-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC10 ERROR ' WS-KC10-STATUS                  
              MOVE     WS-KC10-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K470-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K470 ERROR ' WS-K470-STATUS                  
              MOVE     WS-K470-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K130-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K130 ERROR ' WS-K130-STATUS                  
              MOVE     WS-K130-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K970-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K970 ERROR ' WS-K970-STATUS                  
              MOVE     WS-K970-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    CLOSE VSAM FOR VOUCHER NUMBER GENERATION                             
      *-------------------------------------------------------------            
           MOVE 'AG07K960'                 TO   C907-NUM-FILENME.               
           CALL 'K960CLSE' USING AG07C907-AREA.                                 
                                                                                
           IF C907-NUM-FILE-STATUS NOT = '00' AND '97'                          
              DISPLAY 'CLOSING AG07K960 ERROR ' C907-NUM-FILE-STATUS            
              MOVE     C907-NUM-FILE-STATUS     TO   RETURN-CODE                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
