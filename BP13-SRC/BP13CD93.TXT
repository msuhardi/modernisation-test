       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD93.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  16/03/2016.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    : GENERATE REPORT ON SHORTLISTING OF          *            
      *                APPLICATIONS                                *            
      *                                                            *            
      * INPUT  FILES : BP13F740 - CASES PER REGN                   *            
      *                BP13K813 - BP13.K813.BTOBE                  *            
      * OUTPUT FILE  : BP13LD93 - GENERATED REPORT                 *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP136134  SMR2  16032016  NEW PROGRAM                      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN       TO BP13F740.                            
                                                                                
           SELECT BP13K813 ASSIGN       TO BP13K813                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K813-KEY-FLD                         
                           FILE STATUS  IS WS-K813-STATUS.                      
                                                                                
           SELECT BP13LD93 ASSIGN       TO BP13LD93.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13LD93                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  132 CHARACTERS.                                    
       01   BP13LD93-REC                    PIC X(132).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K813-STATUS               PIC 9(02)  VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ                 PIC 9(05)  VALUE ZEROES.            
           05  WS-LD93-WRITE                PIC 9(05)  VALUE ZEROES.            
           05  WS-CNT-100                   PIC 9(05)  VALUE ZEROES.            
           05  WS-CNT-200                   PIC 9(05)  VALUE ZEROES.            
           05  WS-CNT-300                   PIC 9(05)  VALUE ZEROES.            
           05  WS-CNT-UNSUCC                PIC 9(05)  VALUE ZEROES.            
           05  WS-CTR                       PIC 9(01)  VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF                  PIC X(01)  VALUE 'N'.               
           05  WS-LD93-HDR                  PIC X(01)  VALUE 'N'.               
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-SYSTEM-DATE               PIC 9(08)  VALUE ZEROES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F740-KEY-FLD.                                                 
               10  WS-F740-CDE-NT           PIC X(03).                          
               10  WS-F740-CDE-FT           PIC X(02).                          
           05  WS-PREV-KEY-FLD.                                                 
               10  WS-PREV-CDE-NT           PIC X(03).                          
               10  WS-PREV-CDE-FT           PIC X(02).                          
           05  WS-PREV-DTE-BAL              PIC X(06).                          
           05  WS-QUEUE                     PIC 9(05)  VALUE ZEROES.            
                                                                                
       01 WS-REPORT-VARS.                                                       
          05 LD93-TITLE.                                                        
             10 FILLER            PIC X(30)  VALUE                              
               'SHORTLISTING OF APPLICATIONS ('.                                
             10 LD93-MONTH        PIC X(03)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LD93-YEAR         PIC X(04)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LD93-SMODE        PIC X(03)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE ')'.                         
          05 LD93-DESC.                                                         
             10 LD93-NT           PIC X(03)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LD93-FT           PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(02)  VALUE ' ('.                        
             10 LD93-ZONE         PIC X(03)  VALUE SPACES.                      
             10 FILLER            PIC X(04)  VALUE ') - '.                      
             10 LD93-NME-PROJ     PIC X(100) VALUE SPACES.                      
          05 LD93-HEADER.                                                       
             10 FILLER            PIC X(53)  VALUE                              
                'COHORT;FIRST QUEUE;LAST QUEUE;NO. OF SUCCESSFUL APPNS'.        
             10 FILLER            PIC X(50)  VALUE                              
                ';NO. OF UNSUCCESSFUL APPNS;FLAT SUPPLY;NET NO. OF '.           
             10 FILLER            PIC X(25)  VALUE                              
                'APPNS INCLUDED IN BALLOT;'.                                    
          05 LD93-RECORD.                                                       
             10 LD93-DETAIL       PIC X(32)  VALUE SPACES.                      
             10 FIL01             PIC X(01)  VALUE SPACES.                      
             10 LD93-SUPPLY       PIC X(05)  VALUE SPACES.                      
             10 FIL02             PIC X(01)  VALUE SPACES.                      
             10 LD93-ALL-APP      PIC X(05)  VALUE SPACES.                      
             10 FIL03             PIC X(01)  VALUE SPACES.                      
          05 LD93-DET-REC.                                                      
             10 LD93-COHORT       PIC X(08)  VALUE SPACES.                      
             10 FIL04             PIC X(01)  VALUE SPACES.                      
             10 LD93-FIRST-Q      PIC X(05)  VALUE ZEROES.                      
             10 FIL05             PIC X(01)  VALUE SPACES.                      
             10 LD93-LAST-Q       PIC X(05)  VALUE ZEROES.                      
             10 FIL06             PIC X(01)  VALUE SPACES.                      
             10 LD93-SUCC-APP     PIC X(05)  VALUE ZEROES.                      
             10 FIL07             PIC X(01)  VALUE SPACES.                      
             10 LD93-UNSUCC-APP   PIC X(05)  VALUE ZEROES.                      
          05 LD93-BLANK.                                                        
             10 FILLER            PIC X(132) VALUE SPACES.                      
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAIN-CONTROL.                                                       
      ******************************************************************        
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECS        THRU 3000-EXIT                      
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 4000-READ-BP13K813       THRU 4000-EXIT.                     
           PERFORM 5000-PROCESS-REPORT      THRU 5000-EXIT.                     
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT  BP13F740                                                 
                       BP13K813                                                 
                OUTPUT BP13LD93.                                                
                                                                                
           IF WS-K813-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR OPENING BP13K813, STATUS ' WS-K813-STATUS          
              MOVE WS-K813-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-SYSTEM-DATE.                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F740.                                                      
      ******************************************************************        
           READ BP13F740 AT END                                                 
                MOVE 'Y'                    TO WS-F740-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F740-READ.                    
                                                                                
           IF WS-F740-READ = 1                                                  
              MOVE F740-CDE-NT1             TO WS-PREV-CDE-NT                   
              MOVE F740-CDE-FLAT-TYPE       TO WS-PREV-CDE-FT                   
              MOVE F740-DTE-BALLOT          TO WS-PREV-DTE-BAL                  
           END-IF.                                                              
                                                                                
           MOVE F740-CDE-NT1                TO WS-F740-CDE-NT.                  
           MOVE F740-CDE-FLAT-TYPE          TO WS-F740-CDE-FT.                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECS.                                                       
      ******************************************************************        
           IF WS-F740-KEY-FLD = WS-PREV-KEY-FLD                                 
              EVALUATE F740-CDE-REQUEST-STATUS                                  
              WHEN 'S'                                                          
                 ADD 1                      TO WS-CNT-100                       
              WHEN 'R'                                                          
                 ADD 1                      TO WS-CNT-200                       
              WHEN 'W'                                                          
                 ADD 1                      TO WS-CNT-300                       
              WHEN 'K'                                                          
              WHEN 'U'                                                          
                 ADD 1                      TO WS-CNT-UNSUCC                    
              END-EVALUATE                                                      
           ELSE                                                                 
              DISPLAY 'NT     : ' WS-PREV-CDE-NT                                
              DISPLAY '100    : ' WS-CNT-100                                    
              DISPLAY '200    : ' WS-CNT-200                                    
              DISPLAY '300    : ' WS-CNT-300                                    
              DISPLAY 'UNSUCC : ' WS-CNT-UNSUCC                                 
              PERFORM 4000-READ-BP13K813    THRU 4000-EXIT                      
              PERFORM 5000-PROCESS-REPORT   THRU 5000-EXIT                      
              MOVE ZEROES                   TO WS-CNT-100 WS-CNT-200            
                                               WS-CNT-300 WS-CNT-UNSUCC         
              EVALUATE F740-CDE-REQUEST-STATUS                                  
              WHEN 'S'                                                          
                 ADD 1                      TO WS-CNT-100                       
              WHEN 'R'                                                          
                 ADD 1                      TO WS-CNT-200                       
              WHEN 'W'                                                          
                 ADD 1                      TO WS-CNT-300                       
              WHEN 'K'                                                          
              WHEN 'U'                                                          
                 ADD 1                      TO WS-CNT-UNSUCC                    
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           MOVE F740-CDE-NT1                TO WS-PREV-CDE-NT.                  
           MOVE F740-CDE-FLAT-TYPE          TO WS-PREV-CDE-FT.                  
           MOVE F740-DTE-BALLOT             TO WS-PREV-DTE-BAL.                 
                                                                                
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-READ-BP13K813.                                                      
      ******************************************************************        
           MOVE SPACES                      TO K813-KEY-FLD.                    
           INITIALIZE                          K813-KEY-FLD.                    
                                                                                
           MOVE WS-PREV-CDE-NT              TO K813-NUM-ZONE.                   
           MOVE WS-PREV-CDE-FT              TO K813-NUM-FLAT-TYPE.              
           MOVE WS-PREV-DTE-BAL             TO K813-DTE-BALLOT.                 
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
           WHEN 00                                                              
              CONTINUE                                                          
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K813 : ' K813-KEY-FLD            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K813, STATUS ' WS-K813-STATUS          
              MOVE WS-K813-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-PROCESS-REPORT.                                                     
      ******************************************************************        
           MOVE SPACES                      TO LD93-NME-PROJ.                   
                                                                                
           IF WS-LD93-HDR = 'N'                                                 
              MOVE 'Y'                      TO WS-LD93-HDR                      
              EVALUATE K813-DTE-BALLOT(5:2)                                     
              WHEN '01'                                                         
                 MOVE 'JAN'                 TO LD93-MONTH                       
              WHEN '02'                                                         
                 MOVE 'FEB'                 TO LD93-MONTH                       
              WHEN '03'                                                         
                 MOVE 'MAR'                 TO LD93-MONTH                       
              WHEN '04'                                                         
                 MOVE 'APR'                 TO LD93-MONTH                       
              WHEN '05'                                                         
                 MOVE 'MAY'                 TO LD93-MONTH                       
              WHEN '06'                                                         
                 MOVE 'JUN'                 TO LD93-MONTH                       
              WHEN '07'                                                         
                 MOVE 'JUL'                 TO LD93-MONTH                       
              WHEN '08'                                                         
                 MOVE 'AUG'                 TO LD93-MONTH                       
              WHEN '09'                                                         
                 MOVE 'SEP'                 TO LD93-MONTH                       
              WHEN '10'                                                         
                 MOVE 'OCT'                 TO LD93-MONTH                       
              WHEN '11'                                                         
                 MOVE 'NOV'                 TO LD93-MONTH                       
              WHEN '12'                                                         
                 MOVE 'DEC'                 TO LD93-MONTH                       
              END-EVALUATE                                                      
              MOVE K813-DTE-BALLOT(1:4)     TO LD93-YEAR                        
              MOVE K813-NUM-SALES-MODE      TO LD93-SMODE                       
              WRITE BP13LD93-REC            FROM LD93-TITLE                     
           END-IF.                                                              
                                                                                
           WRITE BP13LD93-REC               FROM LD93-BLANK.                    
           WRITE BP13LD93-REC               FROM LD93-BLANK.                    
           WRITE BP13LD93-REC               FROM LD93-BLANK.                    
           MOVE K813-CDE-NT                 TO LD93-NT.                         
           MOVE K813-NUM-FLAT-TYPE          TO LD93-FT.                         
           MOVE K813-NUM-ZONE               TO LD93-ZONE.                       
           MOVE K813-NME-PROJECT-LTR        TO LD93-NME-PROJ                    
           WRITE BP13LD93-REC               FROM LD93-DESC.                     
                                                                                
           WRITE BP13LD93-REC               FROM LD93-HEADER.                   
                                                                                
           PERFORM 6000-WRITE-BP13LD93      THRU 6000-EXIT                      
           VARYING WS-CTR FROM 1 BY 1                                           
             UNTIL WS-CTR > 4.                                                  
                                                                                
           ADD 1                            TO WS-LD93-WRITE.                   
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6000-WRITE-BP13LD93.                                                     
      ******************************************************************        
           MOVE SPACES                      TO LD93-RECORD.                     
                                                                                
           MOVE ';'                         TO FIL01  FIL02  FIL03              
                                               FIL04  FIL05  FIL06              
                                               FIL07.                           
                                                                                
           EVALUATE WS-CTR                                                      
           WHEN 1                                                               
              MOVE '1ST 100%'               TO LD93-COHORT                      
              MOVE '00001'                  TO LD93-FIRST-Q                     
              MOVE WS-CNT-100               TO LD93-LAST-Q                      
                                               LD93-SUCC-APP                    
              MOVE SPACES                   TO LD93-UNSUCC-APP                  
              MOVE K813-NUM-UNIT-OFFER      TO LD93-SUPPLY                      
              COMPUTE WS-QUEUE = WS-CNT-100 + WS-CNT-200 +                      
                                 WS-CNT-300 + WS-CNT-UNSUCC                     
              MOVE WS-QUEUE                 TO LD93-ALL-APP                     
              MOVE LD93-DET-REC             TO LD93-DETAIL                      
              WRITE BP13LD93-REC            FROM LD93-RECORD                    
                                                                                
           WHEN 2                                                               
              IF WS-CNT-200 > ZEROES                                            
                 MOVE '2ND 100%'            TO LD93-COHORT                      
                 COMPUTE WS-QUEUE = WS-CNT-100 + 1                              
                 MOVE WS-QUEUE              TO LD93-FIRST-Q                     
                 COMPUTE WS-QUEUE = WS-CNT-100 + WS-CNT-200                     
                 MOVE WS-QUEUE              TO LD93-LAST-Q                      
                 MOVE WS-CNT-200            TO LD93-SUCC-APP                    
                 MOVE SPACES                TO LD93-UNSUCC-APP                  
                                               LD93-SUPPLY                      
                                               LD93-ALL-APP                     
                 MOVE LD93-DET-REC          TO LD93-DETAIL                      
                 WRITE BP13LD93-REC         FROM LD93-RECORD                    
              END-IF                                                            
                                                                                
           WHEN 3                                                               
              IF WS-CNT-300 > ZEROES                                            
                 MOVE '3RD 100%'            TO LD93-COHORT                      
                 COMPUTE WS-QUEUE = WS-CNT-100 + WS-CNT-200 + 1                 
                 MOVE WS-QUEUE              TO LD93-FIRST-Q                     
                 COMPUTE WS-QUEUE = WS-CNT-100 + WS-CNT-200 +                   
                                    WS-CNT-300                                  
                 MOVE WS-QUEUE              TO LD93-LAST-Q                      
                 MOVE WS-CNT-300            TO LD93-SUCC-APP                    
                 MOVE SPACES                TO LD93-UNSUCC-APP                  
                                               LD93-SUPPLY                      
                                               LD93-ALL-APP                     
                 MOVE LD93-DET-REC          TO LD93-DETAIL                      
                 WRITE BP13LD93-REC         FROM LD93-RECORD                    
              END-IF                                                            
                                                                                
           WHEN 4                                                               
              IF WS-CNT-UNSUCC > ZEROES                                         
                 MOVE 'UNSUCC  '            TO LD93-COHORT                      
                 MOVE SPACES                TO LD93-FIRST-Q                     
                                               LD93-LAST-Q                      
                                               LD93-SUCC-APP                    
                 MOVE WS-CNT-UNSUCC         TO LD93-UNSUCC-APP                  
                 MOVE SPACES                TO LD93-SUPPLY                      
                                               LD93-ALL-APP                     
                 MOVE LD93-DET-REC          TO LD93-DETAIL                      
                 WRITE BP13LD93-REC         FROM LD93-RECORD                    
              END-IF                                                            
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-FILES.                                                        
      ******************************************************************        
           DISPLAY 'RUN DATE : ' WS-SYSTEM-DATE.                                
           DISPLAY '*================ BP13CD93 ================*'.              
           DISPLAY ' NO OF BP13F740 RECS READ    : ' WS-F740-READ.              
           DISPLAY ' NO OF BP13LD93 RECS WRITTEN : ' WS-LD93-WRITE.             
           DISPLAY '*==========================================*'.              
                                                                                
           CLOSE BP13F740                                                       
                 BP13K813                                                       
                 BP13LD93.                                                      
                                                                                
           IF WS-K813-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR CLOSING BP13K813, STATUS ' WS-K813-STATUS          
              MOVE WS-K813-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
