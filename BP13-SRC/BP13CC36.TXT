       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC36.                                                 
       AUTHOR.        BALA AMUTHAN.                                             
       DATE-WRITTEN.  16/10/2006.                                               
      *****************************************************************         
      * OBJECTIVE  - TO CREATE MONTHLY PENALTY INTERFACE FILE.                  
      *                                                                         
      * SUBROUTINE    - NIL                                                     
      *                                                                         
      * IDENTICAL PGM - NIL                                                     
      *                                                                         
      *----------------------------------------------------------------         
      * REF NO   DATE     BY   AMENDMENTS/ENHANCEMENTS                          
      * -------- ----     ---  -----------------------                          
      * BP132928 16102006 BA2  NEW PROGRAM.                                     
      * BP133091 17102007 RB12 EXPAND BP13KD00 FROM 1200 TO 1600 BYTES          
      *****************************************************************         
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  AC15F530  ASSIGN TO AC15F530.                                
                                                                                
           SELECT  BP13KD00  ASSIGN TO BP13KD00                                 
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS KD00-KEY-FLD                       
                             FILE STATUS  IS KD00-STATUS                        
                             ACCESS MODE  IS SEQUENTIAL.                        
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY AC15F530.                                                           
                                                                                
       FD  BP13KD00                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 1600 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13KD00.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  KD00-STATUS                      PIC 99    VALUE 0.              
                                                                                
       01  WS-SWITCH.                                                           
           05  WS-END-KD00                      PIC X(1)  VALUE 'N'.            
               88 CS-END-KD00                   VALUE  'Y'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-KD00                      PIC 9(7)  VALUE 0.              
           05  WS-CNT-F530                      PIC 9(7)  VALUE 0.              
           05  WS-CNT-BYPASS                    PIC 9(7)  VALUE 0.              
           05  WS-CNT-CURR-STATUS               PIC 9(7)  VALUE 0.              
           05  WS-CNT-NON-CURR                  PIC 9(7)  VALUE 0.              
                                                                                
       01  WS-DTE-CYMD.                                                         
           05 WS-DTE-TODAY-CC                   PIC 9(2)  VALUE 0.              
           05 WS-DTE-TODAY.                                                     
              10 WS-DTE-TODAY-YY                PIC 9(2)  VALUE 0.              
              10 WS-DTE-TODAY-MM                PIC 9(2)  VALUE 0.              
              10 WS-DTE-TODAY-DD                PIC 9(2)  VALUE 0.              
                                                                                
       01  WS-DTE-PREV2-CYMD REDEFINES WS-DTE-CYMD.                             
           05 WS-DTE-PREV2-CCYY                 PIC 9(4).                       
           05 WS-DTE-PREV2-CY REDEFINES WS-DTE-PREV2-CCYY.                      
              10 WS-DTE-PREV2-CC                PIC 9(2).                       
              10 WS-DTE-PREV2-YY                PIC 9(2).                       
           05 WS-DTE-PREV2-MM                   PIC 9(2).                       
           05 WS-DTE-PREV2-DD                   PIC 9(2).                       
                                                                                
       01  WS-DTE-PREV-MY.                                                      
           05 WS-DTE-PREV-MM                    PIC 9(2)  VALUE 0.              
           05 WS-DTE-PREV-YY                    PIC 9(2)  VALUE 0.              
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ********************************************************                  
       0000-MAIN-LOGIC.                                                         
      ********************************************************                  
                                                                                
           PERFORM 1000-START-UP                THRU 1000-EXIT.                 
           PERFORM 2000-READ-BP13KD00           THRU 2000-EXIT.                 
           PERFORM 3000-MAIN-PROCESS            THRU 3000-EXIT                  
                   UNTIL CS-END-KD00.                                           
           PERFORM 9999-WRAP-UP.                                                
                                                                                
                                                                                
      ********************************************************                  
       1000-START-UP.                                                           
      ********************************************************                  
                                                                                
           OPEN INPUT  BP13KD00                                                 
                OUTPUT AC15F530.                                                
                                                                                
           IF KD00-STATUS NOT = 00 AND 97                                       
              DISPLAY ' ERROR OPENING BP13KD00 ' KD00-STATUS                    
              MOVE KD00-STATUS                  TO RETURN-CODE                  
              GO TO 9999-WRAP-UP.                                               
                                                                                
            ACCEPT WS-DTE-CYMD  FROM DATE YYYYMMDD.                             
                                                                                
            IF WS-DTE-TODAY-MM = 01                                             
               MOVE 12                          TO WS-DTE-PREV2-MM              
               SUBTRACT 1 FROM WS-DTE-PREV2-CCYY                                
            ELSE                                                                
               SUBTRACT 1 FROM WS-DTE-PREV2-MM.                                 
            MOVE WS-DTE-PREV2-MM                TO WS-DTE-PREV-MM.              
            MOVE WS-DTE-PREV2-YY                TO WS-DTE-PREV-YY.              
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ********************************************************                  
       2000-READ-BP13KD00.                                                      
      ********************************************************                  
                                                                                
           READ BP13KD00 AT END MOVE 'Y'   TO  WS-END-KD00                      
                GO TO 2000-EXIT.                                                
           ADD 1 TO WS-CNT-KD00.                                                
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
                                                                                
      ********************************************************                  
       3000-MAIN-PROCESS.                                                       
      ********************************************************                  
                                                                                
           IF KD00-NUM-MTH-INSTAL IS NOT NUMERIC                                
              MOVE ZEROS TO KD00-NUM-MTH-INSTAL                                 
           END-IF.                                                              
                                                                                
           IF KD00-CDE-ACCT-STAT = 'C'                                          
                ADD 1 TO WS-CNT-CURR-STATUS                                     
                PERFORM 4000-WRITE-AC15F530 THRU 4000-EXIT                      
           ELSE                                                                 
                ADD +1    TO WS-CNT-BYPASS                                      
                ADD 1 TO WS-CNT-NON-CURR                                        
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13KD00      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
      ********************************************************                  
       4000-WRITE-AC15F530.                                                     
      ********************************************************                  
                                                                                
           IF KD00-AMT-INSTAL IS NOT NUMERIC                                    
              MOVE ZEROES  TO  KD00-AMT-INSTAL                                  
           END-IF.                                                              
           IF KD00-AMT-ARR-INSTAL IS NOT NUMERIC                                
              MOVE ZEROES  TO KD00-AMT-ARR-INSTAL                               
           END-IF.                                                              
                                                                                
           MOVE SPACES                          TO F530S-SMSARR.                
           INITIALIZE F530S-SMSARR.                                             
           MOVE KD00-NUM-SCH-ACCNT              TO F530S-ACCESS-KEY.            
           MOVE KD00-AMT-ARR-INSTAL             TO F530S-ARR-INST.              
           MOVE KD00-AMT-INSTAL                 TO F530S-INSTALMENT.            
           IF KD00-NUM-GIRO-INFO NOT = SPACES AND ZEROS                         
              MOVE 'G'           TO F530S-ACCOUNT-TYPE                          
           END-IF.                                                              
           MOVE 'M'                             TO F530S-PAYMENT-TYPE.          
           MOVE WS-DTE-PREV-MM                  TO F530S-CYCLE-MM.              
           MOVE WS-DTE-PREV2-CC                 TO F530S-CYCLE-CC.              
           MOVE WS-DTE-PREV-YY                  TO F530S-CYCLE-YY.              
           WRITE F530S-SMSARR.                                                  
           ADD 1 TO WS-CNT-F530.                                                
                                                                                
       4000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ********************************************************                  
       9999-WRAP-UP.                                                            
      ********************************************************                  
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '******* CONTROL   TOTALS *******'.                          
           DISPLAY 'PROGRAM-ID : BP13CC36'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY '                         '.                                 
           DISPLAY ' NUMBER OF BP13KD00 RECORDS READ           : '              
                   WS-CNT-KD00.                                                 
           DISPLAY '   -  MONTHLY INSTAL <= 02 STATUS          : '              
                   WS-CNT-BYPASS.                                               
           DISPLAY '   -  NON CURRENT ACCT STATUS              : '              
                   WS-CNT-NON-CURR.                                             
           DISPLAY '   -  CURRENT ACCT STATUS                  : '              
                   WS-CNT-CURR-STATUS.                                          
           DISPLAY ' NUMBER OF AC15F530 RECS WRITTEN           : '              
                   WS-CNT-F530.                                                 
                                                                                
           CLOSE BP13KD00                                                       
                 AC15F530.                                                      
                                                                                
           IF KD00-STATUS NOT = 00                                              
              DISPLAY ' ERROR CLOSING BP13KD00 ' KD00-STATUS                    
              MOVE KD00-STATUS                  TO RETURN-CODE.                 
                                                                                
           STOP RUN.                                                            
