       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CK19.                                                 
      *AUTHOR.        CHYEO.                                                    
      *DATE-WRITTEN.  13/03/2013.                                               
      * =========================================================== *           
      *  SERS SYSTEM (BP17)                                         *           
      * =========================================================== *           
      *  OBJECTIVE :                                                *           
      *    1.  HOUSEKEEP SERS TRANSFERRED CASES FROM BP13K730       *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG-NO    DATE     BY   DESCRIPTION                         *           
      * --------  -------- ---  -----------                         *           
      * BP134838  13032013 YCH2 DELETE SERS RECORDS FROM BP13K730   *           
      * =========================================================== *           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
                                                                                
           SELECT BP13K730 ASSIGN TO BP13K730                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K730-NUM-REGN                              
                  FILE STATUS     IS WS-BP13K730-STATUS.                        
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F.                             
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K730                                                            
                               RECORD CONTAINS 500 CHARACTERS.                  
       COPY BP13K730.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *----- COUNTERS ----------------------------------                        
       01  WS-BP13K730-STATUS          PIC 99    VALUE ZEROS.                   
       01  WS-EOF-F730                 PIC X     VALUE 'N'.                     
       01  WS-BP13F730-READ            PIC 9(8)  VALUE ZEROS.                   
       01  WS-BP13K730-DELETE          PIC 9(8)  VALUE ZEROS.                   
                                                                                
       PROCEDURE DIVISION.                                                      
      *===========================================================              
       MAIN.                                                                    
           PERFORM 0000-OPEN-ROUTINE     THRU 0000-EXIT.                        
           PERFORM 0500-READ-BP13F730    THRU 0500-EXIT.                        
           PERFORM 2000-PROCESS-DETAIL   THRU 2000-EXIT                         
                   UNTIL WS-EOF-F730 = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT.                        
                                                                                
      ******************************************************************        
       0000-OPEN-ROUTINE.                                                       
      ******************************************************************        
           OPEN  INPUT  BP13F730                                                
                 I-O    BP13K730.                                               
                                                                                
                                                                                
           IF WS-BP13K730-STATUS NOT = 00 AND 97                                
              DISPLAY 'OPENING ERROR K730-STATUS IS ' WS-BP13K730-STATUS        
              MOVE WS-BP13K730-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       0500-READ-BP13F730.                                                      
      ******************************************************************        
           READ BP13F730 AT END                                                 
                MOVE 'Y' TO WS-EOF-F730                                         
                GO TO 0500-EXIT.                                                
                                                                                
           ADD 1 TO WS-BP13F730-READ.                                           
                                                                                
       0500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2000-PROCESS-DETAIL.                                                     
      ******************************************************************        
                                                                                
            MOVE F730-NUM-REGN    TO K730-NUM-REGN.                             
            READ BP13K730.                                                      
            EVALUATE WS-BP13K730-STATUS                                         
            WHEN 00                                                             
                 PERFORM 6000-DELETE-BP13K730 THRU 6000-EXIT                    
            WHEN 23                                                             
                 DISPLAY 'REC NOT FND BP13K730   : ' K730-NUM-REGN              
            WHEN OTHER                                                          
                 DISPLAY 'READ BP13K730 ERROR    : ' K730-NUM-REGN              
                 MOVE WS-BP13K730-STATUS TO RETURN-CODE                         
                 PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                   
            END-EVALUATE.                                                       
            PERFORM 0500-READ-BP13F730  THRU 0500-EXIT.                         
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       6000-DELETE-BP13K730.                                                    
      ******************************************************************        
                                                                                
            DELETE BP13K730.                                                    
            EVALUATE WS-BP13K730-STATUS                                         
            WHEN 00                                                             
                 ADD  1     TO WS-BP13K730-DELETE                               
            WHEN OTHER                                                          
                 DISPLAY 'DELETE ERROR        : ' K730-NUM-REGN                 
                 MOVE WS-BP13K730-STATUS TO RETURN-CODE                         
                 PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                   
            END-EVALUATE.                                                       
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       9000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
           DISPLAY ' '.                                                         
           DISPLAY '------ BP13CK19 CONTROL LIST ------'.                       
           DISPLAY '1) TOTAL BP13F730 READ        : ' WS-BP13F730-READ.         
           DISPLAY '2) TOTAL BP13K730 DELETED     : ' WS-BP13K730-DELETE        
           DISPLAY '    '.                                                      
                                                                                
           CLOSE  BP13F730                                                      
                  BP13K730.                                                     
                                                                                
           IF WS-BP13K730-STATUS NOT = 00 AND 97                                
              DISPLAY 'CLOSING ERROR, K730-STATUS ' WS-BP13K730-STATUS          
              MOVE WS-BP13K730-STATUS TO RETURN-CODE.                           
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
