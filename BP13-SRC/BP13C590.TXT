       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C590.                                                 
       AUTHOR.        LOU J L.                                                  
      *DATE-WRITTEN.  17/10/89.                                                 
      *DATE-COMPILED.                                                           
      * ========================================================= *             
      *                SYSTEM OF COMMITMENT  (BP13)               *             
      * ==========================================================*             
      *                                                           *             
      *    OBJECTIVE :                                            *             
      *      1.  THIS IS A PROGRAM TO PRINT A REPORT ON THE       *             
      *          APPLICATION RECEIVED AFTER DATA ENTRY FOR        *             
      *          SUBSEQUENT OPENING OF NEW TOWN.                  *             
      *                                                           *             
      *    INPUT FILES :                                          *             
      *      1.  BP13F500                                         *             
      *      1.  BP13K825                                         *             
      *      1.  BP13K830                                         *             
      *                                                           *             
      *    LISTING :                                              *             
      *      1.  CONTOL LISTING  (BP13L590)                       *             
      *                                                           *             
      * CHG-NO   BY  ON      DESCRIPTION                          *             
      * -------  --- ------  -----------                          *             
      * C920206  YPH 910221  TO CAPTURE NRIC2 & ADDRESS           *             
      * N930012  RML 930517  TO CAPTURE NAME & ADDRESS FROM       *             
      *                      BP13K825 AND BP13K830, RESPECTIVELY  *             
      * BP130020 SSS 951002  INCREASE WS3-NO-OF-PAGE TO 4 BYTE    *             
      * BP130347 IT1 970726  PRINT BLK (WITH SUB-BLK)             *             
      * BP130328 MPA 971017  PRINT LFP ON THE REPORT              *             
      * BP130625 NOR 990219  EXPAND F500 RECL FROM 300 TO 500     *             
      * BP132500 YCH 01042004 DETERMINE 3B OR 3N FLAT             *             
      * BP135944 SMR 150915  REMOVE REFERENCE TO CDE-NT6/7/8      *             
      * ========================================================= *             
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
           SELECT BP13K825 ASSIGN TO BP13K825                                   
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY IS K825-KEY-FLD                           
                           FILE STATUS IS K825-STATUS                           
                           ACCESS MODE IS RANDOM.                               
                                                                                
           SELECT BP13K830 ASSIGN TO BP13K830                                   
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY IS K830-KEY-FLD                           
                           FILE STATUS IS K830-STATUS                           
                           ACCESS MODE IS RANDOM.                               
                                                                                
           SELECT BP13L590 ASSIGN TO BP13L590.                                  
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *                                                                         
       FD   BP13F500                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F500.                                                           
                                                                                
       FD   BP13K825                 RECORD CONTAINS 200 CHARACTERS.            
       COPY BP13K825.                                                           
                                                                                
       FD   BP13K830                 RECORD CONTAINS 250 CHARACTERS.            
       COPY BP13K830.                                                           
                                                                                
       FD  BP13L590                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  PRINT-REC                   PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS1-FILE-ACCESS-STATUS.                                              
           05  BP13F500-STATUS         PIC 9(2)    VALUE ZEROS.                 
           05  K825-STATUS             PIC 9(2)    VALUE ZEROS.                 
           05  K830-STATUS             PIC 9(2)    VALUE ZEROS.                 
                                                                                
       01  WS2-COUNTERS.                                                        
           05  WS-CNT-F500-READ        PIC 9(5)    VALUE ZEROS.                 
           05  WS-CNT-L590-WRITE       PIC 9(5)    VALUE ZEROS.                 
           05  WS-CNT-LINE-ON-PAGE     PIC 9(3)    VALUE 66.                    
           05  WS-PREV-NT              PIC X(3)    VALUE SPACES.                
                                                                                
       01  WS3-ACCUM-VARIABLES.                                                 
           05  WS3-NO-OF-PAGE          PIC 9(4)      VALUE ZEROS.               
                                                                                
       01  WS4-SWITCHES.                                                        
               10 WS4-F500-EOF     PIC X  VALUE 'N'.                            
                                                                                
       01  WS5-DATES-VARIABLES.                                                 
           05  WS5-DATE.                                                        
               10 WS5-DD           PIC 99.                                      
               10 FILLER           PIC X      VALUE '/'.                        
               10 WS5-MM           PIC 99.                                      
               10 FILLER           PIC X      VALUE '/'.                        
               10 WS5-YEAR         PIC 9999.                                    
                                                                                
                                                                                
           05  WS-CUR-YYMMDD.                                                   
               10 WS-CUR-YY        PIC 9999.                                    
               10 WS-CUR-MM        PIC 99.                                      
               10 WS-CUR-DD        PIC 99.                                      
                                                                                
           05  WS5-CURRENT-DATE.                                                
               10 WS5-CUR-MM       PIC 99.                                      
               10 FILLER           PIC X      VALUE '/'.                        
               10 WS5-CUR-DD       PIC 99.                                      
               10 FILLER           PIC X      VALUE '/'.                        
               10 WS5-CUR-YY       PIC 9999.                                    
                                                                                
           05  WS-DATE.                                                         
               10 WS-YEAR          PIC 9999.                                    
               10 WS-MTH           PIC 99.                                      
               10 WS-DAY           PIC 99.                                      
                                                                                
           05  WS-DATE-PR.                                                      
               10 WS-DD            PIC 99.                                      
               10 FILLER           PIC X      VALUE '/'.                        
               10 WS-MM            PIC 99.                                      
               10 FILLER           PIC X      VALUE '/'.                        
               10 WS-YY            PIC 9999.                                    
                                                                                
       01  WS6-DEFAULT-VALUES.                                                  
           05  WS6-MAX-LINES       PIC 9(02)   VALUE  30.                       
                                                                                
       01  L590-FILLER             PIC X(132) VALUE SPACES.                     
                                                                                
       01  WS-ADDRESS.                                                          
           05  FILLER              PIC X      VALUE SPACE.                      
           05  WS-NUM-BLK          PIC X(10)  VALUE SPACES.                     
           05  FILLER              PIC X(06)  VALUE SPACES.                     
           05  WS-NUM-FLAT         PIC X(11)  VALUE SPACES.                     
           05  WS-STREET           PIC X(20)  VALUE SPACES.                     
           05  FILLER              PIC X(45)  VALUE SPACES.                     
           05  WS-POSTAL           PIC X(06)  VALUE SPACES.                     
                                                                                
      **********************************************************                
      *        WS FOR LIST OF PAYMENT FOR STAMPING             *                
      **********************************************************                
                                                                                
       01  L590-PR-HEAD-01.                                                     
           05  FILLER              PIC X(14)   VALUE 'BP13L590      '.          
           05  FILLER              PIC X(8)    VALUE 'HDB3    '.                
           05  FILLER              PIC X(18)   VALUE SPACES.                    
           05  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(20)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'DATE: '.                  
           05  PRINT-DATE-1        PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(3)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'PAGE: '.                  
           05  PRINT-PAGE          PIC ZZZ9.                                    
                                                                                
       01  L590-PR-HEAD-02.                                                     
           05  FILLER              PIC X(34)   VALUE SPACES.                    
           05  FILLER              PIC X(52)   VALUE                            
               'REPORT ON THE APPLICATION RECEIVED AFTER DATA ENTRY'.           
           05  FILLER              PIC X(13)   VALUE SPACES.                    
           05  FILLER              PIC X(32)   VALUE                            
               'SALES UNIT (APPLICATION TEAM)'.                                 
                                                                                
       01  L590-PR-HEAD-03.                                                     
           05  FILLER              PIC X(43)   VALUE SPACES.                    
           05  FILLER              PIC X(34)   VALUE                            
              'FOR BALLOTING TO ZONE OR NEW TOWN'.                              
           05  FILLER              PIC X(58)   VALUE SPACES.                    
                                                                                
                                                                                
       01  L590-PR-HEAD-04.                                                     
           05  FILLER              PIC X(03)   VALUE 'S/N'.                     
           05  FILLER              PIC X(03)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE                            
               'REGN NO'.                                                       
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(11)   VALUE                            
              'NRIC1/NRIC2'.                                                    
           05  FILLER              PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(15)   VALUE                            
               'APPLICANT NAMES'.                                               
           05  FILLER              PIC X(15)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'ALL'.                     
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(02)   VALUE 'FT'.                      
           05  FILLER              PIC X(06)   VALUE SPACES.                    
           05  FILLER              PIC X(02)   VALUE 'JB'.                      
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(02)   VALUE 'JB'.                      
           05  FILLER              PIC X(07)   VALUE SPACES.                    
           05  FILLER              PIC X(02)   VALUE 'JB'.                      
           05  FILLER              PIC X(12)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'NT1'.                     
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'NT2'.                     
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'NT3'.                     
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'NT4'.                     
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(02)   VALUE 'ZE'.                      
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'BO '.                     
           05  FILLER              PIC X(01)   VALUE SPACES.                    
                                                                                
       01  L590-PR-HEAD-05.                                                     
           05  FILLER              PIC X(15)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'ADDRESS'.                 
           05  FILLER              PIC X(39)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'CAT'.                     
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'SCH'.                     
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'TYP'.                     
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'LFP'.                     
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(02)   VALUE 'CD'.                      
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(04)   VALUE 'REGN'.                    
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  FILLER              PIC X(02)   VALUE 'CN'.                      
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(06)   VALUE 'DTEUPD'.                  
           05  FILLER              PIC X(06)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'NT5'.                     
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'NT6'.                     
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'NT7'.                     
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'NT8'.                     
           05  FILLER              PIC X(04)   VALUE SPACES.                    
           05  FILLER              PIC X(04)   VALUE 'ADDR'.                    
                                                                                
       01  L590-PR-DETAIL-1.                                                    
           05  L590-NO             PIC ZZZZ9.                                   
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L590-REGN           PIC X(8).                                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L590-NRIC1          PIC X(09).                                   
           05  FILLER              PIC X(01)   VALUE '/'.                       
           05  L590-NRIC2          PIC X(09).                                   
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L590-NAME           PIC X(66).                                   
           05  FILLER              PIC X(03)   VALUE SPACES.                    
           05  L590-NT1            PIC X(03).                                   
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L590-NT2            PIC X(03).                                   
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L590-NT3            PIC X(03).                                   
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L590-NT4            PIC X(03)   VALUE SPACES.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L590-ZONE           PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L590-BO             PIC X(03)   VALUE SPACES.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
                                                                                
       01  L590-PR-DETAIL-2.                                                    
           05  FILLER              PIC X(13)   VALUE SPACES.                    
           05  L590-BLK            PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(1)    VALUE SPACES.                    
           05  L590-NUM-FLAT       PIC X(11)   VALUE SPACES.                    
           05  L590-STREET         PIC X(20)   VALUE SPACES.                    
           05  FILLER              PIC X       VALUE SPACES.                    
           05  L590-POST           PIC X(06)   VALUE SPACES.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L590-CAT            PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L590-ALLOC          PIC X(03)   VALUE SPACES.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L590-FLAT           PIC X(03)   VALUE SPACES.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L590-LFP            PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE SPACES.                    
           05  L590-JB-CDE         PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  L590-JB-REGN        PIC X(08)   VALUE SPACES.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L590-JB-CNT         PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L590-DATE-UPD       PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  L590-NT5            PIC X(04)   VALUE SPACES.                    
           05  L590-NT6            PIC X(04)   VALUE SPACES.                    
           05  L590-NT7            PIC X(04)   VALUE SPACES.                    
           05  L590-NT8            PIC X(04)   VALUE SPACES.                    
           05  L590-ADDR-TYPE      PIC X(01)   VALUE SPACES.                    
                                                                                
      **********************************************************                
       PROCEDURE DIVISION.                                                      
      **********************************************************                
                                                                                
       000-MAIN-ROUTINE.                                                        
                                                                                
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
           PERFORM 200-READ-BP13F500 THRU 200-EXIT.                             
           PERFORM 250-READ-K825-K830 THRU 250-EXIT.                            
           PERFORM 300-PROCESS-F500  THRU 300-EXIT                              
                   UNTIL WS4-F500-EOF = 'Y'.                                    
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
                                                                                
           OPEN INPUT  BP13F500                                                 
                       BP13K825                                                 
                       BP13K830                                                 
                OUTPUT BP13L590.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-BP13F500.                                                       
                                                                                
           READ BP13F500 NEXT                                                   
              AT END                                                            
              MOVE 'Y' TO  WS4-F500-EOF                                         
              GO TO 200-EXIT.                                                   
                                                                                
           ADD 1 TO WS-CNT-F500-READ.                                           
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       250-READ-K825-K830.                                                      
                                                                                
           MOVE SPACES TO  WS-ADDRESS   BP13K830-REC                            
                                        BP13K825-REC.                           
           MOVE F500-NUM-NRIC1    TO K825-NUM-NRIC.                             
           MOVE F500-NUM-REGN     TO K825-NUM-REGN                              
           MOVE F500-NUM-REGN     TO K830-NUM-REGN-BK.                          
                                                                                
           READ BP13K825 KEY IS K825-KEY-FLD.                                   
           IF K825-STATUS = 00                                                  
              MOVE K825-NME-OCCP TO L590-NAME                                   
           ELSE IF K825-STATUS = 23                                             
              MOVE SPACES            TO L590-NAME                               
              DISPLAY 'BP13K825 NOT FOUND ' K825-KEY-FLD                        
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K825'                                  
              DISPLAY 'FILE STATUS = ' K825-STATUS                              
              GO TO 900-CLOSE-ROUTINE.                                          
                                                                                
           READ BP13K830 KEY IS K830-KEY-FLD.                                   
           IF K830-STATUS = 00                                                  
              IF K830-NUM-MAIL-ADDR = SPACES                                    
                 MOVE K830-NUM-RESIDENT-ADDR TO WS-ADDRESS                      
              ELSE                                                              
                 MOVE K830-NUM-MAIL-ADDR     TO WS-ADDRESS                      
              END-IF                                                            
           ELSE IF K830-STATUS = 23                                             
              MOVE SPACES                 TO WS-ADDRESS                         
              DISPLAY 'BP13K830 NOT FOUND ' K830-KEY-FLD                        
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K830'                                  
              DISPLAY 'FILE STATUS = ' K830-STATUS                              
              GO TO 900-CLOSE-ROUTINE.                                          
                                                                                
                                                                                
           MOVE WS-NUM-BLK           TO L590-BLK.                               
           MOVE WS-NUM-FLAT          TO L590-NUM-FLAT.                          
           MOVE WS-STREET            TO L590-STREET.                            
           MOVE WS-POSTAL            TO L590-POST.                              
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-F500.                                                        
      **********************************************************                
      *                                                        *                
      *  CHECK F500-CDE-PRINT-STATUS = 'P' THEN SKIP TO NEXT   *                
      *  RECORD IN THE PREREGN FILE.                           *                
      *                                                        *                
      **********************************************************                
                                                                                
              MOVE F500-NUM-REGN          TO L590-REGN.                         
              MOVE F500-NUM-NRIC1         TO L590-NRIC1.                        
              MOVE F500-CDE-CATEGORY      TO L590-CAT.                          
              MOVE F500-CDE-ALLOC-SCH     TO L590-ALLOC.                        
                                                                                
              IF  F500-CDE-FLAT-TYPE = '3 ' AND                                 
                  F500-NUM-BUYBACK = 'Y'                                        
                  MOVE '3B'            TO L590-FLAT                             
              ELSE                                                              
                  MOVE F500-CDE-FLAT-TYPE  TO L590-FLAT                         
              END-IF.                                                           
                                                                                
              MOVE F500-CDE-JB            TO L590-JB-CDE.                       
              MOVE F500-NUM-JB-REGN       TO L590-JB-REGN.                      
              MOVE F500-NUM-JB-CNT        TO L590-JB-CNT.                       
              MOVE F500-NUM-LOW-FLOOR     TO L590-LFP.                          
              MOVE F500-DTE-UPDATE        TO WS-DATE.                           
              MOVE WS-DAY                 TO WS-DD.                             
              MOVE WS-MTH                 TO WS-MM.                             
              MOVE WS-YEAR                TO WS-YY.                             
              MOVE WS-DATE-PR             TO L590-DATE-UPD.                     
              IF F500-NUM-NRIC2 (2:1) NOT = '#'                                 
                  MOVE F500-NUM-NRIC2     TO L590-NRIC2                         
              ELSE                                                              
                  MOVE SPACES             TO L590-NRIC2.                        
                                                                                
              MOVE F500-CDE-BO            TO L590-BO.                           
                                                                                
              MOVE F500-CDE-ZONE          TO L590-ZONE.                         
                                                                                
              MOVE F500-CDE-NT1           TO L590-NT1.                          
              MOVE F500-CDE-NT2           TO L590-NT2.                          
              MOVE F500-CDE-NT3           TO L590-NT3.                          
              MOVE F500-CDE-NT4           TO L590-NT4.                          
              MOVE F500-CDE-NT5           TO L590-NT5.                          
              MOVE SPACES                 TO L590-NT6.                          
              MOVE SPACES                 TO L590-NT7.                          
              MOVE SPACES                 TO L590-NT8.                          
                                                                                
              PERFORM 400-PRINT           THRU 400-EXIT.                        
                                                                                
              PERFORM 200-READ-BP13F500   THRU 200-EXIT.                        
                                                                                
              PERFORM 250-READ-K825-K830  THRU 250-EXIT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-PRINT.                                                               
      ******************************************************************        
      *                                                                *        
      *       PRINT LIST OF APPLICANTS AFTER THE OPENING OF NEW TOWN   *        
      *                                                                *        
      ******************************************************************        
           PERFORM 500-CHECK-LINECT THRU 500-EXIT.                              
                                                                                
           ADD 1       TO WS-CNT-LINE-ON-PAGE                                   
                          WS-CNT-L590-WRITE.                                    
           MOVE WS-CNT-L590-WRITE TO L590-NO.                                   
           WRITE PRINT-REC FROM L590-PR-DETAIL-1 AFTER 2.                       
           MOVE SPACES             TO PRINT-REC.                                
           WRITE PRINT-REC FROM L590-PR-DETAIL-2.                               
           ADD 1                   TO WS-CNT-LINE-ON-PAGE.                      
           MOVE SPACES TO PRINT-REC.                                            
                                                                                
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-CHECK-LINECT.                                                        
      ******************************************************************        
      *                                                                *        
      *         CHECK LINE COUNT TO DETERMINE PAGE THROW               *        
      *                                                                *        
      ******************************************************************        
           IF (WS-CNT-LINE-ON-PAGE > WS6-MAX-LINES)                             
                PERFORM 600-PRINT-HEADING                                       
                MOVE 1 TO WS-CNT-LINE-ON-PAGE.                                  
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-PRINT-HEADING.                                                       
      ****************************************************************          
      *                                                              *          
      *                PRINT HEADING                                 *          
      *                                                              *          
      ****************************************************************          
           MOVE SPACES                TO PRINT-REC.                             
           ADD 1 TO WS3-NO-OF-PAGE.                                             
           MOVE WS3-NO-OF-PAGE        TO PRINT-PAGE.                            
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-CUR-YYMMDD.                  
                                                                                
           STRING WS-CUR-DD '/'                                                 
                  WS-CUR-MM '/'                                                 
                  WS-CUR-YY DELIMITED BY SIZE                                   
           INTO PRINT-DATE-1.                                                   
                                                                                
           WRITE PRINT-REC FROM  L590-PR-HEAD-01  AFTER PAGE.                   
           WRITE PRINT-REC FROM  L590-FILLER      AFTER 2.                      
           WRITE PRINT-REC FROM  L590-PR-HEAD-02  AFTER 1.                      
           WRITE PRINT-REC FROM  L590-PR-HEAD-03  AFTER 1.                      
           WRITE PRINT-REC FROM  L590-FILLER      AFTER 1.                      
           WRITE PRINT-REC FROM  L590-PR-HEAD-04  AFTER 1.                      
           WRITE PRINT-REC FROM  L590-PR-HEAD-05  AFTER 1.                      
           MOVE SPACES TO PRINT-REC.                                            
           WRITE PRINT-REC AFTER 2.                                             
           MOVE F500-CDE-NT1 TO  WS-PREV-NT.                                    
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      ****************************************************************          
      *                                                              *          
      *            CLOSE FILES AND END PROCESSING                    *          
      *                                                              *          
      ****************************************************************          
           CLOSE                                                                
                 BP13F500                                                       
                 BP13K825                                                       
                 BP13K830                                                       
                 BP13L590.                                                      
                                                                                
           DISPLAY ' ** BP13C590 ** '.                                          
           DISPLAY 'NO OF REC READ FROM PREREGN FILE    (BP13F500) : '          
                   WS-CNT-F500-READ.                                            
           DISPLAY 'NO OF REC PRINTED FROM PREREGN FILE (BP13F500) : '          
                   WS-CNT-L590-WRITE.                                           
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
