      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
                                                                                
       PROGRAM-ID.    BP13C235.                                                 
       AUTHOR.        JOEL E PARRENO.                                           
      *DATE-WRITTEN.  01/28/94.                                                 
      *DATE-COMPILED.                                                           
      * ============================================================== *        
      *              SYSTEM OF COMMITMENT (SOC) - (BP13)               *        
      * ============================================================== *        
      *                                                                *        
      *    OBJECTIVE   :                                               *        
      *      1.   THIS IS A PROGRAM TO SUM THE ETHNIC PROPORTION IN    *        
      *           FIS BY BLOCK, THEN VERIFY THE DATA IN THE BLOCK      *        
      *           SUMMARY. IF DID NOT TALLY, UPDATE BLOCK SUMMARY WITH *        
      *           COMPUTATION FROM FIS. PRINT REPORT ON THE UPDATING.  *        
      *                                                                *        
      *    INPUT  FILE :                                               *        
      *      1.   BM06F120   (FLAT INVENTORY FILE)                     *        
      *                                                                *        
      *    I-O    FILE :                                               *        
      *      1.   BM06K100   (BLOCK SUMMARY FILE)                      *        
      *                                                                *        
      *    OUTPUT LIST :                                               *        
      *      1.   BP13L235   (UPDATE LISTING)                          *        
      * -------------------------------------------------------------- *        
      * CHG-NO   BY    DATE    DESCRIPTION                             *        
      * -------- ---  ------   -----------                             *        
      * SOC 8G   SSS  140995   CHG F211 TO F212                        *        
      *BP130418  SIA  280298   CHG F212 TO F120-Y2K COMPLIANT          *        
      *                        CHG K210 TO K100-Y2K COMPLIANT          *        
      * ============================================================== *        
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F120  ASSIGN       TO BM06F120.                           
                                                                                
           SELECT BM06K100  ASSIGN       TO BM06K100                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K100-KEY-FLD                        
                            FILE STATUS  IS WS-K100-STATUS.                     
                                                                                
           SELECT BP13L235  ASSIGN       TO BP13L235.                           
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BM06F120                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 700 CHARACTERS                                      
            LABEL RECORD ARE STANDARD                                           
            RECORDING MODE  IS  F.                                              
       COPY BM06F120.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BP13L235                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD ARE STANDARD                                           
            RECORDING MODE  IS  F.                                              
       01   L235-REC                   PIC X(132).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-K100-STATUS          PIC 99     VALUE 00.                     
           05  WS-EOF-F120             PIC X      VALUE 'N'.                    
               88  EOF-OF-F120                    VALUE 'Y'.                    
           05  WS-TOT-NOT-TALLY        PIC X      VALUE SPACE.                  
           05  WS-UPD-CAT1             PIC X      VALUE SPACE.                  
           05  WS-UPD-CAT2             PIC X      VALUE SPACE.                  
           05  WS-UPD-CAT3             PIC X      VALUE SPACE.                  
           05  WS-UPD-CAT4             PIC X      VALUE SPACE.                  
           05  WS-UPD-BAL              PIC X      VALUE SPACE.                  
           05  WS-PRINT-IND            PIC 9      VALUE 0.                      
               88  PRINT-UPDATE-CAT1              VALUE 1.                      
               88  PRINT-UPDATE-CAT2              VALUE 2.                      
               88  PRINT-UPDATE-CAT3              VALUE 3.                      
               88  PRINT-UPDATE-CAT4              VALUE 4.                      
               88  PRINT-UPDATE-BAL               VALUE 5.                      
                                                                                
       01  WS-K100-INFO.                                                        
           05  WS-K100-BAL             PIC 9(3)   VALUE 0.                      
           05  WS-K100-SALE            PIC 9(3)   VALUE 0.                      
           05  WS-K100-CAT1-ACCEPT     PIC 9(3)   VALUE 0.                      
           05  WS-K100-CAT2-ACCEPT     PIC 9(3)   VALUE 0.                      
           05  WS-K100-CAT3-ACCEPT     PIC 9(3)   VALUE 0.                      
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-DETL-CTR             PIC 9(4)   VALUE 0.                      
           05  WS-LINE-CTR             PIC 99     VALUE 55.                     
           05  WS-PAGE-CTR             PIC 9(4)   VALUE 0.                      
           05  WS-READ-F120-CNT        PIC 9(6)   VALUE 0.                      
           05  WS-READ-K100-CNT        PIC 9(6)   VALUE 0.                      
           05  WS-UPDT-K100-CNT        PIC 9(6)   VALUE 0.                      
                                                                                
       01  WS-OTHER-VARIABLES.                                                  
           05  WS-K100-KEY-FLD.                                                 
               10  WS-K100-ESTATE      PIC X(3)   VALUE SPACES.                 
               10  WS-K100-NEIGH       PIC X(4)   VALUE SPACES.                 
               10  WS-K100-CONTRACT    PIC X(4)   VALUE SPACES.                 
               10  WS-K100-BLOCK       PIC X(5)   VALUE SPACES.                 
                                                                                
           05  WS-DATE                 PIC X(8)   VALUE SPACES.                 
           05  WS-FLAT-TYPE            PIC 9      VALUE 0.                      
           05  PTR                     PIC 99     VALUE 00.                     
           05  WS-EDITED-READ-F120     PIC ZZZZZ9.                              
           05  WS-EDITED-READ-K100     PIC ZZZZZ9.                              
           05  WS-EDITED-UPDT-K100     PIC ZZZZZ9.                              
                                                                                
       01  WS-ARRAYS.                                                           
           05  WS-TOTAL-UNIT           PIC 9(3)   OCCURS 9 TIMES.               
           05  WS-TOTAL-BAL            PIC 9(3)   OCCURS 9 TIMES.               
           05  WS-CAT1-ACCEPT          PIC 9(3)   OCCURS 9 TIMES.               
           05  WS-CAT2-ACCEPT          PIC 9(3)   OCCURS 9 TIMES.               
           05  WS-CAT3-ACCEPT          PIC 9(3)   OCCURS 9 TIMES.               
           05  WS-CAT4-ACCEPT          PIC 9(3)   OCCURS 9 TIMES.               
           05  WS-K100-CAT4-ACCEPT     PIC 9(3)   OCCURS 9 TIMES.               
                                                                                
       01  WS-PRINT-VARIABLES.                                                  
           05  L235-HDG-LINE.                                                   
               10  FILLER              PIC X(8)   VALUE 'BP13L235'.             
               10  FILLER              PIC X(25)  VALUE SPACES.                 
               10  FILLER              PIC X(43)  VALUE                         
                   'S Y S T E M     O F     C O M M I T M E N T'.               
               10  FILLER              PIC X(20)  VALUE SPACES.                 
               10  FILLER              PIC X(8)   VALUE 'DATE :'.               
               10  L235-DATE           PIC X(10)  VALUE SPACES.                 
               10  FILLER              PIC X(6)   VALUE SPACES.                 
               10  FILLER              PIC X(7)   VALUE 'PAGE : '.              
               10  L235-PAGE-CTR       PIC ZZZ9.                                
                                                                                
           05  L235-SHDG1-LINE.                                                 
               10  FILLER              PIC X(31)  VALUE SPACES.                 
               10  FILLER              PIC X(47)  VALUE                         
                   'UPDATE ETHNIC PROPORTION TO BLOCK SUMMARY FILE'.            
               10  FILLER              PIC X(18)  VALUE SPACES.                 
               10  FILLER              PIC X(30)  VALUE                         
                   'USER :  ALLOCATION TEAM'.                                   
                                                                                
           05  L235-SHDG2-LINE.                                                 
               10  FILLER              PIC X(36)  VALUE SPACES.                 
               10  FILLER              PIC X(40)  VALUE                         
                   'BY MATCHING WITH THE DETAIL FIS RECORD'.                    
                                                                                
           05  L235-DETL-TITLE.                                                 
               10  FILLER              PIC X(4)   VALUE 'S/NO'.                 
               10  FILLER              PIC X(3)   VALUE  SPACES.                
               10  FILLER              PIC X(6)   VALUE 'ESTATE'.               
               10  FILLER              PIC X(3)   VALUE  SPACES.                
               10  FILLER              PIC X(5)   VALUE 'NEIGH'.                
               10  FILLER              PIC X(3)   VALUE  SPACES.                
               10  FILLER              PIC X(8)   VALUE 'CONTRACT'.             
               10  FILLER              PIC X(3)   VALUE  SPACES.                
               10  FILLER              PIC X(5)   VALUE 'BLOCK'.                
               10  FILLER              PIC X(15)  VALUE  SPACES.                
               10  FILLER              PIC X(3)   VALUE '1  '.                  
               10  FILLER              PIC X(4)   VALUE  SPACES.                
               10  FILLER              PIC X(3)   VALUE '2  '.                  
               10  FILLER              PIC X(4)   VALUE  SPACES.                
               10  FILLER              PIC X(3)   VALUE '3  '.                  
               10  FILLER              PIC X(4)   VALUE  SPACES.                
               10  FILLER              PIC X(3)   VALUE '4  '.                  
               10  FILLER              PIC X(4)   VALUE  SPACES.                
               10  FILLER              PIC X(3)   VALUE '4S '.                  
               10  FILLER              PIC X(4)   VALUE  SPACES.                
               10  FILLER              PIC X(3)   VALUE '5  '.                  
               10  FILLER              PIC X(4)   VALUE  SPACES.                
               10  FILLER              PIC X(3)   VALUE 'E  '.                  
               10  FILLER              PIC X(4)   VALUE  SPACES.                
               10  FILLER              PIC X(3)   VALUE 'H  '.                  
               10  FILLER              PIC X(4)   VALUE  SPACES.                
               10  FILLER              PIC X(3)   VALUE 'MG '.                  
                                                                                
           05  L235-UPD-DETL-BEFORE.                                            
               10  L235-SERIAL-NO             PIC ZZZZ.                         
               10  FILLER                     PIC X(3)  VALUE SPACES.           
               10  L235-ESTATE                PIC X(6)  VALUE SPACES.           
               10  FILLER                     PIC X(3)  VALUE SPACES.           
               10  L235-NEIGHBOURHOOD         PIC X(5)  VALUE SPACES.           
               10  FILLER                     PIC X(3)  VALUE SPACES.           
               10  L235-CONTRACT              PIC X(8)  VALUE SPACES.           
               10  FILLER                     PIC X(3)  VALUE SPACES.           
               10  L235-BLOCK                 PIC X(5)  VALUE SPACES.           
               10  FILLER                     PIC X(5)  VALUE SPACES.           
               10  L235-CAT-DESC              PIC X(5)  VALUE SPACES.           
               10  FILLER                     PIC X(5)  VALUE SPACES.           
               10  L235-RM-TYP-ARRAY-BEFORE   OCCURS 9  TIMES.                  
                   15  L235-ROOM-TYP-BEF      PIC X(3).                         
                   15  FILLER                 PIC X(4).                         
                                                                                
           05  L235-UPD-DETL-AFTER.                                             
               10  FILLER                     PIC X(55) VALUE SPACES.           
               10  L235-RM-TYP-ARRAY-AFTER    OCCURS 9  TIMES.                  
                   15  L235-ROOM-TYP-AFT      PIC X(3).                         
                   15  FILLER                 PIC X(4).                         
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *    MAIN LOGIC PROCESS SECTION                                  *        
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
                                                                                
           PERFORM  100-OPEN-ROUTINE          THRU   100-EXIT.                  
           PERFORM  200-INITIALIZE-ROUTINE    THRU   200-EXIT.                  
           PERFORM  300-READ-INPUT-BM06F120   THRU   300-EXIT.                  
           MOVE     F120-AIX1                 TO     WS-K100-KEY-FLD.           
           PERFORM  400-PROCESS-RECORD        THRU   400-EXIT                   
                        UNTIL   EOF-OF-F120.                                    
           PERFORM  500-READ-UPDATE-BM06K100  THRU   500-EXIT.                  
           PERFORM  800-DISPLAY-STATISTICS    THRU   800-EXIT.                  
           PERFORM  900-CLOSE-ROUTINE         THRU   900-EXIT.                  
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    OPENING FILES SECTION                                       *        
      ******************************************************************        
       100-OPEN-ROUTINE.                                                        
                                                                                
           OPEN INPUT   BM06F120                                                
                I-O     BM06K100                                                
                OUTPUT  BP13L235.                                               
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BM06K100, STATUS = ' WS-K100-STATUS        
              MOVE     WS-K100-STATUS    TO    RETURN-CODE                      
              PERFORM  900-CLOSE-ROUTINE THRU  900-EXIT                         
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    INITIALIZE WS VARIABLES                                     *        
      ******************************************************************        
       200-INITIALIZE-ROUTINE.                                                  
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
           STRING WS-DATE (7:2) '/'                                             
                  WS-DATE (5:2) '/'                                             
                  WS-DATE (1:4)                                                 
           DELIMITED BY SIZE                                                    
                INTO L235-DATE.                                                 
                                                                                
           MOVE    ZEROES          TO   WS-PRINT-IND                            
                                        WS-PAGE-CTR                             
                                        WS-READ-F120-CNT                        
                                        WS-READ-K100-CNT                        
                                        WS-UPDT-K100-CNT                        
                                        WS-FLAT-TYPE.                           
                                                                                
           PERFORM 250-INITIALIZE-ARRAY THRU 250-EXIT                           
                       VARYING PTR FROM 1 BY 1 UNTIL PTR > 9.                   
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *   INITIALIZE TO ZEROES ALL VALUE OF WS ARRAYS                  *        
      ******************************************************************        
       250-INITIALIZE-ARRAY.                                                    
                                                                                
           MOVE ZEROES  TO  WS-TOTAL-UNIT  (PTR)                                
                            WS-TOTAL-BAL   (PTR)                                
                            WS-CAT1-ACCEPT (PTR)                                
                            WS-CAT2-ACCEPT (PTR)                                
                            WS-CAT3-ACCEPT (PTR)                                
                            WS-CAT4-ACCEPT (PTR).                               
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *   READ FLAT INVENTOY FILE (BM06F120) AT END MOVE 'Y' EOF-IND   *        
      ******************************************************************        
       300-READ-INPUT-BM06F120.                                                 
                                                                                
           READ BM06F120                                                        
                AT END                                                          
                   MOVE  'Y' TO  WS-EOF-F120                                    
                   GO TO 300-EXIT.                                              
                                                                                
           ADD  1  TO WS-READ-F120-CNT.                                         
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    READ BM06F120 UNTIL ESTATE/NEIGHBOURHOOD/CONTRACT/BLOCK     *        
      *                        CHANGES THEN UPDATE THE BM06K100        *        
      ******************************************************************        
       400-PROCESS-RECORD.                                                      
                                                                                
           IF F120-AIX1 = WS-K100-KEY-FLD                                       
              PERFORM     420-CHECK-F120-DETAILS    THRU 420-EXIT               
           ELSE                                                                 
              PERFORM     500-READ-UPDATE-BM06K100  THRU 500-EXIT               
              PERFORM 250-INITIALIZE-ARRAY THRU 250-EXIT                        
                          VARYING PTR FROM 1 BY 1 UNTIL PTR > 9                 
              MOVE        F120-AIX1                 TO   WS-K100-KEY-FLD        
              PERFORM     420-CHECK-F120-DETAILS    THRU 420-EXIT               
           END-IF.                                                              
                                                                                
           PERFORM        300-READ-INPUT-BM06F120   THRU 300-EXIT.              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    CHECK CDE-TYPE-ACTUSE TO GET THE VALUE OF WS-FLAT-TYPE      *        
      ******************************************************************        
       420-CHECK-F120-DETAILS.                                                  
                                                                                
           IF F120-CDE-TYP-ACTUSE = '01'                                        
              MOVE   1  TO WS-FLAT-TYPE                                         
           ELSE                                                                 
           IF F120-CDE-TYP-ACTUSE = '02'                                        
              MOVE   2  TO WS-FLAT-TYPE                                         
           ELSE                                                                 
           IF F120-CDE-TYP-ACTUSE = '03'                                        
              MOVE   3  TO WS-FLAT-TYPE                                         
           ELSE                                                                 
           IF F120-CDE-TYP-ACTUSE = '04'                                        
              IF   F120-CDE-MODL-DESG NOT = '06'                                
                   MOVE  4   TO WS-FLAT-TYPE                                    
              ELSE                                                              
                   MOVE  5   TO WS-FLAT-TYPE                                    
           ELSE                                                                 
           IF F120-CDE-TYP-ACTUSE = '05'                                        
              MOVE   6  TO WS-FLAT-TYPE                                         
           ELSE                                                                 
           IF F120-CDE-TYP-ACTUSE = '06'                                        
              MOVE   7  TO WS-FLAT-TYPE                                         
           ELSE                                                                 
           IF F120-CDE-TYP-ACTUSE = '07'                                        
              MOVE   8  TO WS-FLAT-TYPE                                         
           ELSE                                                                 
           IF F120-CDE-TYP-ACTUSE = '08'                                        
              MOVE   9  TO WS-FLAT-TYPE                                         
           ELSE                                                                 
              GO TO 420-EXIT.                                                   
                                                                                
           ADD   1    TO   WS-TOTAL-UNIT (WS-FLAT-TYPE)                         
                                                                                
           IF F120-DTE-ACCEPTANCE = ZEROES OR SPACES OR LOW-VALUES              
              ADD  1    TO           WS-TOTAL-BAL (WS-FLAT-TYPE)                
           ELSE                                                                 
              IF   F120-CATEGORY   = 1                                          
                   ADD  1     TO     WS-CAT1-ACCEPT (WS-FLAT-TYPE)              
              ELSE                                                              
              IF   F120-CATEGORY   = 2                                          
                   ADD  1     TO     WS-CAT2-ACCEPT (WS-FLAT-TYPE)              
              ELSE                                                              
              IF   F120-CATEGORY   = 3                                          
                   ADD  1     TO     WS-CAT3-ACCEPT (WS-FLAT-TYPE)              
              ELSE                                                              
              IF   F120-CATEGORY   = 4                                          
                   ADD  1     TO     WS-CAT4-ACCEPT (WS-FLAT-TYPE).             
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    READ VSAM FILE BM06K100 (BLOCK FILE) FOR UPDATE             *        
      ******************************************************************        
       500-READ-UPDATE-BM06K100.                                                
                                                                                
           MOVE WS-K100-KEY-FLD TO K100-KEY-FLD.                                
                                                                                
           READ BM06K100.                                                       
                                                                                
           IF WS-K100-STATUS =  00                                              
              ADD    1        TO  WS-READ-K100-CNT                              
              MOVE   SPACES   TO  WS-TOT-NOT-TALLY                              
              MOVE   SPACES   TO  WS-UPD-CAT1                                   
              MOVE   SPACES   TO  WS-UPD-CAT2                                   
              MOVE   SPACES   TO  WS-UPD-CAT3                                   
              MOVE   SPACES   TO  WS-UPD-CAT4                                   
              MOVE   SPACES   TO  WS-UPD-BAL                                    
              PERFORM 510-COMPUTE-K100-CAT4 THRU 510-EXIT                       
                           VARYING PTR FROM 1 BY 1                              
                           UNTIL   PTR >  9                                     
                                                                                
              PERFORM 520-CHECK-TOTALS THRU 520-EXIT                            
                           VARYING PTR FROM 1 BY 1                              
                           UNTIL   PTR >  9                                     
                                                                                
              IF  (WS-UPD-BAL  = 'Y' OR                                         
                   WS-UPD-CAT1 = 'Y' OR                                         
                   WS-UPD-CAT2 = 'Y' OR                                         
                   WS-UPD-CAT3 = 'Y' OR                                         
                   WS-UPD-CAT4 = 'Y' )                                          
                  PERFORM 600-CHECK-CAT-TO-PRINT    THRU  600-EXIT              
                  PERFORM 700-REWRITE-BM06K100  THRU  700-EXIT                  
              END-IF                                                            
                                                                                
           ELSE                                                                 
           IF WS-K100-STATUS = 23                                               
              DISPLAY 'RECORD NOT FOUND BM06K100, STAT ' WS-K100-STATUS         
                      ',  K100-KEY-FLD  = ' K100-KEY-FLD                        
           ELSE                                                                 
              DISPLAY 'ERROR READING BM06K100, STAT = '  WS-K100-STATUS         
              MOVE     WS-K100-STATUS    TO    RETURN-CODE                      
              PERFORM  900-CLOSE-ROUTINE THRU  900-EXIT.                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       510-COMPUTE-K100-CAT4.                                                   
           COMPUTE WS-K100-CAT4-ACCEPT(PTR) =                                   
                   K100-TOT-SALE(PTR) -                                         
                   K100-BAL-ALLOCN(PTR) -                                       
                   K100-CAT1-ACCEPT(PTR) -                                      
                   K100-CAT2-ACCEPT(PTR) -                                      
                   K100-CAT3-ACCEPT(PTR).                                       
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    CHECK WS-TOTALS WITH BM06K100-TOTAL FIELDS                  *        
      ******************************************************************        
       520-CHECK-TOTALS.                                                        
           IF WS-TOTAL-UNIT (PTR) NOT = K100-TOT-SALE (PTR)                     
              MOVE     'Y'        TO    WS-TOT-NOT-TALLY                        
           END-IF                                                               
                                                                                
           IF WS-TOT-NOT-TALLY = SPACES                                         
              IF WS-TOTAL-BAL   (PTR) NOT =  K100-BAL-ALLOCN (PTR)              
                 MOVE     'Y'         TO     WS-UPD-BAL                         
              END-IF                                                            
                                                                                
              IF WS-CAT1-ACCEPT (PTR) NOT =  K100-CAT1-ACCEPT (PTR)             
                 MOVE     'Y'         TO     WS-UPD-CAT1                        
              END-IF                                                            
                                                                                
              IF WS-CAT2-ACCEPT (PTR) NOT =  K100-CAT2-ACCEPT (PTR)             
                 MOVE     'Y'         TO     WS-UPD-CAT2                        
              END-IF                                                            
                                                                                
              IF WS-CAT3-ACCEPT (PTR) NOT =  K100-CAT3-ACCEPT (PTR)             
                 MOVE     'Y'         TO     WS-UPD-CAT3                        
              END-IF                                                            
                                                                                
              IF WS-CAT4-ACCEPT (PTR) NOT =  WS-K100-CAT4-ACCEPT(PTR)           
                 MOVE     'Y'         TO     WS-UPD-CAT4                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       520-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    CHECK WHICH CATEGORY HAS AN IMPENDING UPDATE                *        
      ******************************************************************        
       600-CHECK-CAT-TO-PRINT.                                                  
           IF WS-UPD-CAT1 =   'Y'                                               
              MOVE    1               TO     WS-PRINT-IND                       
              MOVE   'CAT1'           TO     L235-CAT-DESC                      
              PERFORM 620-MOVE-DATA   THRU   620-EXIT                           
                          VARYING PTR FROM 1 BY 1                               
                          UNTIL   PTR >  9                                      
              PERFORM 640-PRINT-UPDATE-LIST  THRU  640-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-UPD-CAT2 =   'Y'                                               
              MOVE    2               TO     WS-PRINT-IND                       
              MOVE   'CAT2'           TO     L235-CAT-DESC                      
              PERFORM 620-MOVE-DATA   THRU   620-EXIT                           
                          VARYING PTR FROM 1 BY 1                               
                          UNTIL   PTR >  9                                      
              PERFORM 640-PRINT-UPDATE-LIST  THRU  640-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-UPD-CAT3 =   'Y'                                               
              MOVE    3               TO     WS-PRINT-IND                       
              MOVE   'CAT3'           TO     L235-CAT-DESC                      
              PERFORM 620-MOVE-DATA   THRU   620-EXIT                           
                          VARYING PTR FROM 1 BY 1                               
                          UNTIL   PTR >  9                                      
              PERFORM 640-PRINT-UPDATE-LIST  THRU  640-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-UPD-CAT4 =   'Y'                                               
              MOVE    4               TO     WS-PRINT-IND                       
              MOVE   'CAT4'           TO     L235-CAT-DESC                      
              PERFORM 620-MOVE-DATA   THRU   620-EXIT                           
                          VARYING PTR FROM 1 BY 1                               
                          UNTIL   PTR >  9                                      
              PERFORM 640-PRINT-UPDATE-LIST  THRU  640-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-UPD-BAL  =   'Y'                                               
              MOVE    5               TO     WS-PRINT-IND                       
              MOVE   'BAL '           TO     L235-CAT-DESC                      
              PERFORM 620-MOVE-DATA   THRU   620-EXIT                           
                          VARYING PTR FROM 1 BY 1                               
                          UNTIL   PTR >  9                                      
              PERFORM 640-PRINT-UPDATE-LIST  THRU  640-EXIT                     
           END-IF.                                                              
                                                                                
           IF (WS-UPD-CAT1 = 'Y') OR (WS-UPD-CAT2 = 'Y') OR                     
              (WS-UPD-CAT3 = 'Y') OR (WS-UPD-CAT4 = 'Y') OR                     
              (WS-UPD-BAL  = 'Y')                                               
               MOVE   SPACES    TO    L235-REC                                  
               WRITE  L235-REC  AFTER 1                                         
               ADD    1         TO    WS-LINE-CTR                               
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    MOVE DATA FIELDS  TO  PRINT DETAIL FIELDS                   *        
      ******************************************************************        
       620-MOVE-DATA.                                                           
           IF PRINT-UPDATE-CAT1                                                 
              MOVE  K100-CAT1-ACCEPT (PTR)  TO  L235-ROOM-TYP-BEF (PTR)         
              MOVE  WS-CAT1-ACCEPT   (PTR)  TO  L235-ROOM-TYP-AFT (PTR)         
              MOVE  WS-CAT1-ACCEPT   (PTR)  TO  K100-CAT1-ACCEPT  (PTR).        
                                                                                
           IF PRINT-UPDATE-CAT2                                                 
              MOVE  K100-CAT2-ACCEPT (PTR)  TO  L235-ROOM-TYP-BEF (PTR)         
              MOVE  WS-CAT2-ACCEPT   (PTR)  TO  L235-ROOM-TYP-AFT (PTR)         
              MOVE  WS-CAT2-ACCEPT   (PTR)  TO  K100-CAT2-ACCEPT  (PTR).        
                                                                                
           IF PRINT-UPDATE-CAT3                                                 
              MOVE  K100-CAT3-ACCEPT (PTR)  TO  L235-ROOM-TYP-BEF (PTR)         
              MOVE  WS-CAT3-ACCEPT   (PTR)  TO  L235-ROOM-TYP-AFT (PTR)         
              MOVE  WS-CAT3-ACCEPT   (PTR)  TO  K100-CAT3-ACCEPT  (PTR).        
                                                                                
           IF PRINT-UPDATE-CAT4                                                 
            MOVE WS-K100-CAT4-ACCEPT (PTR) TO  L235-ROOM-TYP-BEF (PTR)          
            MOVE WS-CAT4-ACCEPT      (PTR) TO  L235-ROOM-TYP-AFT (PTR).         
                                                                                
           IF PRINT-UPDATE-BAL                                                  
              MOVE  K100-BAL-ALLOCN  (PTR)  TO  L235-ROOM-TYP-BEF (PTR)         
              MOVE  WS-TOTAL-BAL     (PTR)  TO  L235-ROOM-TYP-AFT (PTR)         
              MOVE  WS-TOTAL-BAL    (PTR) TO  K100-BAL-ALLOCN   (PTR).          
                                                                                
       620-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    PRINTS THE UPDATE DETAIL LINES                              *        
      ******************************************************************        
       640-PRINT-UPDATE-LIST.                                                   
           IF WS-LINE-CTR > 54                                                  
              PERFORM 660-PRINT-HEADING   THRU  660-EXIT                        
           END-IF.                                                              
                                                                                
           ADD  1                   TO   WS-DETL-CTR.                           
           MOVE WS-DETL-CTR         TO   L235-SERIAL-NO.                        
                                                                                
           MOVE WS-K100-ESTATE      TO   L235-ESTATE.                           
           MOVE WS-K100-NEIGH       TO   L235-NEIGHBOURHOOD.                    
           MOVE WS-K100-CONTRACT    TO   L235-CONTRACT.                         
           MOVE WS-K100-BLOCK       TO   L235-BLOCK.                            
                                                                                
           WRITE L235-REC FROM L235-UPD-DETL-BEFORE AFTER 1.                    
           WRITE L235-REC FROM L235-UPD-DETL-AFTER  AFTER 1.                    
           ADD   2    TO  WS-LINE-CTR.                                          
                                                                                
       640-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    PRINTS THE HEADING LINES                                    *        
      ******************************************************************        
       660-PRINT-HEADING.                                                       
                                                                                
           ADD    1                              TO    WS-PAGE-CTR.             
           MOVE   WS-PAGE-CTR                    TO    L235-PAGE-CTR.           
           WRITE  L235-REC FROM  L235-HDG-LINE   AFTER ADVANCING PAGE.          
           WRITE  L235-REC FROM  L235-SHDG1-LINE AFTER ADVANCING 1.             
           WRITE  L235-REC FROM  L235-SHDG2-LINE AFTER ADVANCING 1.             
           WRITE  L235-REC FROM  L235-DETL-TITLE AFTER ADVANCING 3.             
           MOVE   SPACES   TO    L235-REC.                                      
           WRITE  L235-REC AFTER 1.                                             
                                                                                
           MOVE   7        TO    WS-LINE-CTR.                                   
                                                                                
       660-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    UPDATE VSAM FILE BM06K100 (BLOCK SUMMARY FILE)              *        
      ******************************************************************        
       700-REWRITE-BM06K100.                                                    
           REWRITE K100-REC.                                                    
           IF WS-K100-STATUS = 00                                               
              ADD   1     TO   WS-UPDT-K100-CNT                                 
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING BM06K100, STAT ' WS-K100-STATUS           
              MOVE     WS-K100-STATUS     TO    RETURN-CODE                     
              PERFORM  900-CLOSE-ROUTINE  THRU  900-EXIT.                       
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    DISPLAY NECESSARY STATISTICS                                *        
      ******************************************************************        
       800-DISPLAY-STATISTICS.                                                  
           MOVE  WS-READ-F120-CNT   TO   WS-EDITED-READ-F120.                   
           MOVE  WS-READ-K100-CNT   TO   WS-EDITED-READ-K100.                   
           MOVE  WS-UPDT-K100-CNT   TO   WS-EDITED-UPDT-K100.                   
                                                                                
           DISPLAY '***********************************************'.           
           DISPLAY ' PROGRAM-ID     :  BP13C235                    '.           
           DISPLAY ' CONTROL TOTALS :                              '.           
           DISPLAY '***********************************************'.           
           DISPLAY '                                               '.           
           DISPLAY ' NO. OF RECORDS READ  FROM BM06F120  =  '                   
                     WS-EDITED-READ-F120.                                       
           DISPLAY '                                               '.           
           DISPLAY ' NO. OF RECORDS READ  FROM BM06K100  =  '                   
                     WS-EDITED-READ-K100.                                       
           DISPLAY ' NO. OF RECORDS UPDATED TO BM06K100  =  '                   
                     WS-EDITED-UPDT-K100.                                       
           DISPLAY '***********************************************'.           
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    CLOSE ALL FILES   AND   STOP RUN                            *        
      ******************************************************************        
       900-CLOSE-ROUTINE.                                                       
                                                                                
           CLOSE   BM06F120                                                     
                   BM06K100                                                     
                   BP13L235.                                                    
                                                                                
           IF WS-K100-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BM06K100, STATUS = ' WS-K100-STATUS        
              MOVE     WS-K100-STATUS    TO    RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
