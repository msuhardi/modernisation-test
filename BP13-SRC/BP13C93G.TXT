       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C93G.                                                  
      *AUTHOR.       ZDD1.                                                      
      *DATE-WRITTEN. 28/05/2015.                                                
      * ========================================================== *            
      * SOC (BP13)                                                 *            
      * ========================================================== *            
      *1.MATCH SALES ORDER FILE BP13F410 WITH SOC HISTORY BP13K893 *            
      *  TO OUTPUT RECORD TO BP13F800 FORMAT                       *            
      *2.THIS FILE RUN AS INPUT FILE TO BP13C92F TO CREATE BP13FW3A*            
      *  BP13FW3B                                                  *            
      *                                                            *            
      *  INPUT   :  BP13F410                                       *            
      *          :  BP13K893                                       *            
      *  OUTPUT  :  BP13F800                                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
           SELECT BP13K893 ASSIGN TO BP13K893                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K893-KEY-FLD                               
                  FILE STATUS     IS WS-K893-STATUS.                            
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F410                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13F410.                                                           
                                                                                
       FD  BP13F800                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13F800.                                                           
                                                                                
       FD  BP13K893                                                             
           RECORD CONTAINS 2050 CHARACTERS.                                     
       COPY BP13K893.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-F410-EOF                   PIC X    VALUE 'N'.                    
       01  WS-K893-FOUND                 PIC X    VALUE 'N'.                    
       01  WS-EOF-K893                   PIC X    VALUE 'N'.                    
       01  WS-F410-NFND                  PIC 9(5) VALUE ZEROS.                  
       01  WS-K893-NFND                  PIC 9(5) VALUE ZEROS.                  
       01  WS-K893-STATUS                PIC 99   VALUE ZEROS.                  
       01  WS-F800-WRT                   PIC 9(5) VALUE ZEROS.                  
       01  WS-K893-READ-CNT              PIC 9(5) VALUE ZEROS.                  
       01  WS-F800-REC                   PIC X(2000) VALUE SPACES.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F410-READ              PIC 9(7) VALUE ZEROS.                  
           05  WS-K893-READ              PIC 9(7) VALUE ZEROS.                  
           05  WS-K893-WRITE             PIC 9(7) VALUE ZEROS.                  
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F410    THRU 200-EXIT.                             
            PERFORM 300-PROCESS-F410 THRU 300-EXIT                              
                    UNTIL WS-F410-EOF = 'Y'.                                    
            PERFORM 900-CLOSE-FILES  THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F410                                                 
                       BP13K893                                                 
                OUTPUT BP13F800.                                                
                                                                                
           IF WS-K893-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K893-STATUS ' WS-K893-STATUS              
              MOVE WS-K893-STATUS       TO RETURN-CODE                          
              PERFORM 900-CLOSE-FILES THRU 900-EXIT.                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F410.                                                           
      *-------------------------------------------------------------            
           READ BP13F410 AT END                                                 
                MOVE 'Y' TO WS-F410-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1         TO WS-F410-READ.                                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-F410.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                TO BP13K893-MASTER.                       
           INITIALIZE                    BP13K893-MASTER.                       
           MOVE SPACES                TO WS-F800-REC.                           
           INITIALIZE                    WS-F800-REC.                           
           MOVE 'N'                   TO WS-K893-FOUND.                         
                                                                                
           PERFORM 400-START-K893   THRU 400-EXIT                               
           PERFORM 410-READ-K893    THRU 410-EXIT                               
               UNTIL WS-EOF-K893 = 'Y'  OR                                      
                     K893-NUM-REGN-HIST NOT = F410-NUM-REGN                     
           IF WS-K893-FOUND = 'Y'                                               
              WRITE BP13F800-MASTER FROM WS-F800-REC                            
              ADD 1                  TO WS-F800-WRT                             
           ELSE                                                                 
              ADD 1                  TO WS-K893-NFND                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F410    THRU 200-EXIT.                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
       400-START-K893.                                                          
      *-------------------------------------------------------------            
                                                                                
           INITIALIZE  BP13K893-MASTER                                          
                                                                                
           MOVE 'N'                        TO WS-K893-FOUND                     
                                                                                
           MOVE SPACES                     TO K893-KEY-FLD                      
           MOVE F410-NUM-REGN              TO K893-NUM-REGN-HIST                
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD                                   
                                                                                
           EVALUATE WS-K893-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 23                                                          
                    DISPLAY 'KEY NOT FOUND IN BP13K893!'                        
                    DISPLAY 'KEY : ' K893-KEY-FLD                               
               WHEN OTHER                                                       
                    DISPLAY 'ERROR LOCATING KEY IN BP13K893!'                   
                    DISPLAY 'KEY : ' K893-KEY-FLD                               
                    DISPLAY 'FILE STATUS :' WS-K893-STATUS                      
                    MOVE WS-K893-STATUS    TO RETURN-CODE                       
                    PERFORM 900-CLOSE-FILES THRU 900-EXIT                       
           END-EVALUATE.                                                        
                                                                                
      *----------*                                                              
       400-EXIT.                                                                
      *----------*                                                              
           EXIT.                                                                
                                                                                
       410-READ-K893.                                                           
      *-------------------------------------------------------------            
                                                                                
           READ BP13K893 NEXT                                                   
                AT END MOVE 'Y' TO WS-EOF-K893                                  
                GO TO 410-EXIT.                                                 
                                                                                
           EVALUATE WS-K893-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                 IF  K893-NUM-REGN-HIST = F410-NUM-REGN                         
                    ADD  1                 TO WS-K893-READ-CNT                  
                    MOVE 'Y'               TO WS-K893-FOUND                     
                    MOVE BP13K893-MASTER(1:2000) TO WS-F800-REC                 
                 END-IF                                                         
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K893!'                           
                    DISPLAY 'K893-NUM-REGN :' K893-NUM-REGN                     
                    DISPLAY 'FILE STATUS : ' WS-K893-STATUS                     
                    MOVE WS-K893-STATUS    TO RETURN-CODE                       
                    PERFORM 900-CLOSE-FILES THRU 900-EXIT                       
           END-EVALUATE.                                                        
                                                                                
      *---------*                                                               
       410-EXIT.                                                                
      *---------*                                                               
           EXIT.                                                                
                                                                                
       900-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY '-- EDTK893 CONTROL TOTAL -----------------'.                
           DISPLAY '1) NO OF REC READ    BP13F410 = ' WS-F410-READ.             
           DISPLAY '2) NO OF REC NOT FOUND IN K893= ' WS-K893-NFND.             
           DISPLAY '3) NO OF REC WRITTEN IN F800  = ' WS-F800-WRT               
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE BP13F410                                                       
                 BP13K893                                                       
                 BP13F800.                                                      
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K893, STATUS ' WS-K893-STATUS          
              MOVE WS-K893-STATUS        TO RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
