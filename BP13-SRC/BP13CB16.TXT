       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB16.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  14/10/2016.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *   OBJECTIVE    : GENERATE MONTHLY REPORT TO CHECK SERS PORT *           
      *                  CASES                                      *           
      *                                                             *           
      *   INPUT FILES  : BP13F800                                   *           
      *                  BP17K000                                   *           
      *                  BM06K110                                   *           
      *                  BM06K100                                   *           
      *   OUTPUT FILE  : BP13LB16                                   *           
      *                                                             *           
      * =========================================================== *           
      * CHQ REQ   DATE      BY    DESCRIPTIONS                      *           
      * --------  --------  ----  --------------------------------- *           
      * BP136454  14102016  SMR2  NEW PROGRAM                       *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT BP17K000 ASSIGN TO BP17K000                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K000-KEY-FLD                               
                  ALTERNATE KEY   IS K000-NUM-REGNO                             
                  FILE STATUS     IS WS-K000-STATUS.                            
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K110-KEY-FLD                               
                  FILE STATUS     IS WS-K110-STATUS.                            
                                                                                
           SELECT BM06K100 ASSIGN TO BM06K100                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K100-KEY-FLD                               
                  FILE STATUS     IS WS-K100-STATUS.                            
                                                                                
           SELECT BP13LB16 ASSIGN TO BP13LB16.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F800.                                                           
                                                                                
       FD   BP17K000                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP17K000.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K100.                                                           
                                                                                
       FD   BP13LB16                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  200 CHARACTERS.                                    
       01   BP13LB16-REC.                                                       
            05  LB16-DTE-BAL           PIC X(06).                               
            05  FILLER1                PIC X(01).                               
            05  LB16-ALLO-CAT          PIC X(03).                               
            05  FILLER2                PIC X(01).                               
            05  LB16-REGNO             PIC X(08).                               
            05  FILLER3                PIC X(01).                               
            05  LB16-SERS-BLK          PIC X(05).                               
            05  FILLER4                PIC X(01).                               
            05  LB16-SERS-STREET       PIC X(32).                               
            05  FILLER5                PIC X(01).                               
            05  LB16-SERS-UNIT         PIC X(11).                               
            05  FILLER6                PIC X(01).                               
            05  LB16-FLAT-TYPE         PIC X(02).                               
            05  FILLER7                PIC X(01).                               
            05  LB16-CDE-STAT          PIC X(01).                               
            05  FILLER8                PIC X(01).                               
            05  LB16-PORT-TAG          PIC X(01).                               
            05  FILLER9                PIC X(01).                               
            05  LB16-ALLO-SCH          PIC X(03).                               
            05  FILLER10               PIC X(01).                               
            05  LB16-ELIG-SCH          PIC X(03).                               
            05  FILLER11               PIC X(01).                               
            05  LB16-DTE-SEL           PIC X(08).                               
            05  FILLER12               PIC X(01).                               
            05  LB16-SEL-BLK           PIC X(05).                               
            05  FILLER13               PIC X(01).                               
            05  LB16-SEL-STREET        PIC X(32).                               
            05  FILLER14               PIC X(01).                               
            05  LB16-SEL-UNIT          PIC X(11).                               
            05  FILLER15               PIC X(01).                               
            05  LB16-DTE-PCD           PIC X(06).                               
            05  FILLER16               PIC X(01).                               
            05  LB16-DTE-CANCEL        PIC X(08).                               
            05  FILLER17               PIC X(01).                               
            05  LB16-DTE-TP            PIC X(08).                               
            05  FILLER18               PIC X(01).                               
            05  LB16-DTE-KEY-ISSUE     PIC X(08).                               
            05  FILLER19               PIC X(01).                               
            05  FILLER                 PIC X(20).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-STATUS.                                                           
           05  WS-K000-STATUS          PIC 9(2)  VALUE ZEROES.                  
           05  WS-K110-STATUS          PIC 9(2)  VALUE ZEROES.                  
           05  WS-K100-STATUS          PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F800-EOF             PIC X(1)  VALUE 'N'.                     
           05  WS-FOUND-SERS           PIC X(1)  VALUE 'N'.                     
           05  WS-FOUND-PIDB           PIC X(1)  VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800-READ            PIC 9(6)  VALUE ZEROES.                  
           05  WS-LB16-WRITE           PIC 9(6)  VALUE ZEROES.                  
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CUR-CCYYMMDD.                                                 
               10  WS-CURR-CCYY        PIC X(4).                                
               10  WS-CURR-MM          PIC X(2).                                
               10  WS-CURR-DD          PIC X(2).                                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-QTR                  PIC X(2)  VALUE SPACES.                  
                                                                                
      *-------------------------------------------------------*                 
      *    COPY BOOK FOR SUBROUTINE - BP13C913.               *                 
      *-------------------------------------------------------*                 
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------*                 
      *    LINKAGE VARIABLES                                  *                 
      *-------------------------------------------------------*                 
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH         PIC X(4).                                
           05  WS-LINK-NUM-ACC         PIC X(4).                                
           05  WS-LINK-NUM-CHK         PIC X(1).                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F800      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-REC        THRU 3000-EXIT                       
             UNTIL WS-F800-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F800                                                 
                       BP17K000                                                 
                       BM06K110                                                 
                       BM06K100                                                 
                OUTPUT BP13LB16.                                                
                                                                                
           IF WS-K000-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP17K000, STATUS: ' WS-K000-STATUS         
              MOVE WS-K000-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BM06K110, STATUS: ' WS-K110-STATUS         
              MOVE WS-K110-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BM06K100, STATUS: ' WS-K100-STATUS         
              MOVE WS-K100-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-CCYYMMDD.                  
                                                                                
           MOVE SPACES                     TO BP13LB16-REC.                     
           INITIALIZE                         BP13LB16-REC.                     
                                                                                
           STRING 'BAL DATE;ALLOC MODE;REGN NO;SERS BLK;SERS STREET;'           
                  'SERS UNIT;FT;STAT CODE;PORT TAG;ALLOC CODE;'                 
                  'ELIG CODE;SEL DATE;SEL FLAT BLK;SEL FLAT STREET;'            
                  'SEL FLAT UNIT;PCD;CANCEL DATE;APPMT TP DATE;'                
                  'KEY ISSUE DATE;'                                             
                  DELIMITED BY SIZE INTO BP13LB16-REC.                          
           WRITE BP13LB16-REC.                                                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
           READ BP13F800 AT END                                                 
                MOVE 'Y'                   TO WS-F800-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F800-READ.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-PROCESS-REC.                                                        
      *-------------------------------------------------------------            
           MOVE SPACES                     TO BP13LB16-REC.                     
           INITIALIZE                         BP13LB16-REC.                     
                                                                                
           PERFORM 3100-MOVE-FILLER        THRU 3100-EXIT.                      
                                                                                
           MOVE F800-DTE-BALLOT            TO LB16-DTE-BAL.                     
           MOVE F800-NUM-ALLO-CAT          TO LB16-ALLO-CAT.                    
           MOVE F800-NUM-REGN              TO LB16-REGNO.                       
           MOVE F800-NUM-FLAT-TYPE         TO LB16-FLAT-TYPE.                   
           MOVE F800-NUM-BALLOT-STATUS     TO LB16-CDE-STAT.                    
           MOVE F800-NUM-PORT-ELIG-TAG     TO LB16-PORT-TAG.                    
           MOVE F800-NUM-ALLO-SCHEME       TO LB16-ALLO-SCH.                    
           MOVE F800-NUM-ELIG-SCHEME       TO LB16-ELIG-SCH.                    
           IF F800-DTE-BK-APPT NOT = SPACES AND LOW-VALUES AND ZEROES           
              MOVE F800-DTE-BK-APPT        TO LB16-DTE-SEL                      
           END-IF.                                                              
           IF F800-DTE-CANCEL NOT = SPACES AND LOW-VALUES AND ZEROES            
              MOVE F800-DTE-CANCEL         TO LB16-DTE-CANCEL                   
           END-IF.                                                              
           IF F800-DTE-SO-APPT NOT = SPACES AND LOW-VALUES AND ZEROES           
              MOVE F800-DTE-SO-APPT        TO LB16-DTE-TP                       
           END-IF.                                                              
                                                                                
           PERFORM 4000-READ-BP17K000      THRU 4000-EXIT.                      
           IF WS-FOUND-SERS = 'Y'                                               
              MOVE SPACES                  TO BP13COMM8-REC                     
              INITIALIZE                      BP13COMM8-REC                     
                                                                                
              MOVE SPACES                  TO WS-LINK-REC                       
              MOVE K000-NUM-SCH-ACC(1:4)   TO WS-LINK-NUM-SCH                   
              MOVE K000-NUM-SCH-ACC(5:4)   TO WS-LINK-NUM-ACC                   
              MOVE K000-NUM-SCH-ACC(9:1)   TO WS-LINK-NUM-CHK                   
                                                                                
              PERFORM 5000-CALL-BP13C913   THRU 5000-EXIT                       
              IF WS-FOUND-PIDB = 'Y'                                            
                 MOVE COMM8-NUM-BLK        TO LB16-SERS-BLK                     
                 MOVE COMM8-NME-STREET     TO LB16-SERS-STREET                  
                 STRING '#' COMM8-NUM-LEVEL '-' COMM8-NUM-UNIT-MAIN             
                        COMM8-NUM-UNIT-SUB                                      
                        DELIMITED BY SIZE INTO LB16-SERS-UNIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                      
              MOVE SPACES                  TO BP13COMM8-REC                     
              INITIALIZE                      BP13COMM8-REC                     
                                                                                
              MOVE SPACES                  TO WS-LINK-REC                       
              MOVE F800-NUM-SCH            TO WS-LINK-NUM-SCH                   
              MOVE F800-NUM-ACC            TO WS-LINK-NUM-ACC                   
              MOVE F800-NUM-ACC-CK         TO WS-LINK-NUM-CHK                   
                                                                                
              PERFORM 5000-CALL-BP13C913   THRU 5000-EXIT                       
              IF WS-FOUND-PIDB = 'Y'                                            
                 MOVE COMM8-NUM-BLK        TO LB16-SEL-BLK                      
                 MOVE COMM8-NME-STREET     TO LB16-SEL-STREET                   
                 STRING '#' COMM8-NUM-LEVEL '-' COMM8-NUM-UNIT-MAIN             
                        COMM8-NUM-UNIT-SUB                                      
                        DELIMITED BY SIZE INTO LB16-SEL-UNIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 6000-READ-BM06K110      THRU 6000-EXIT.                      
                                                                                
           WRITE BP13LB16-REC.                                                  
           ADD 1                           TO WS-LB16-WRITE.                    
                                                                                
           PERFORM 2000-READ-BP13F800      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3100-MOVE-FILLER.                                                        
      *-------------------------------------------------------------            
           MOVE ';'                        TO FILLER1    FILLER2                
                                              FILLER3    FILLER4                
                                              FILLER5    FILLER6                
                                              FILLER7    FILLER8                
                                              FILLER9    FILLER10               
                                              FILLER11   FILLER12               
                                              FILLER13   FILLER14               
                                              FILLER15   FILLER16               
                                              FILLER17   FILLER18               
                                              FILLER19.                         
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-READ-BP17K000.                                                      
      *-------------------------------------------------------------            
           MOVE 'N'                        TO WS-FOUND-SERS.                    
           MOVE F800-NUM-REGN              TO K000-NUM-REGNO.                   
                                                                                
           READ BP17K000 KEY IS K000-NUM-REGNO.                                 
                                                                                
           EVALUATE WS-K000-STATUS                                              
           WHEN ZEROES                                                          
                MOVE 'Y'                   TO WS-FOUND-SERS                     
                                                                                
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FND IN BP17K000 ' WS-K000-STATUS            
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READ BP17K000, STATUS ' WS-K000-STATUS           
                MOVE WS-K000-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-CALL-BP13C913.                                                      
      *-------------------------------------------------------------            
      * TO CALL SUBROUTINE BP13C913, GETTING ADDRESS                            
           MOVE 'N'                        TO WS-FOUND-PIDB.                    
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
           WHEN ZEROES                                                          
                MOVE 'Y'                   TO WS-FOUND-PIDB                     
                                                                                
           WHEN 100                                                             
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE : '                     
                         COMM8-CDE-SYSERR                                       
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       6000-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                     TO K110-REC.                         
           INITIALIZE                         K110-REC.                         
                                                                                
           MOVE F800-NUM-SCH-ACC           TO K110-KEY-FLD.                     
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN ZEROES                                                          
                IF K110-DTE-KEY-ISSUED NOT = SPACES AND LOW-VALUES AND          
                                             ZEROES                             
                   MOVE K110-DTE-KEY-ISSUED   TO LB16-DTE-KEY-ISSUE             
                END-IF                                                          
                PERFORM 7000-READ-BM06K100 THRU 7000-EXIT                       
                                                                                
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FND IN BP13K110 ' WS-K110-STATUS            
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READ BP13K110, STATUS ' WS-K110-STATUS           
                MOVE WS-K110-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       7000-READ-BM06K100.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                     TO K100-REC.                         
           INITIALIZE                         K100-REC.                         
                                                                                
           MOVE K110-AIX1                  TO K100-KEY-FLD.                     
                                                                                
           READ BM06K100.                                                       
                                                                                
           EVALUATE WS-K100-STATUS                                              
           WHEN ZEROES                                                          
                IF K100-DTE-PACD NOT = SPACES AND LOW-VALUES AND ZEROES         
                   PERFORM 7100-DECODE-PCD THRU 7100-EXIT                       
                END-IF                                                          
                                                                                
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FND IN BM06K100 ' WS-K100-STATUS            
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READ BM06K100, STATUS ' WS-K100-STATUS           
                MOVE WS-K100-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       7100-DECODE-PCD.                                                         
      *-------------------------------------------------------------            
           MOVE SPACES                     TO WS-QTR.                           
                                                                                
           IF K100-NUM-PACD = 'Q' OR SPACES OR LOW-VALUES                       
              EVALUATE K100-DTE-PACD(5:2)                                       
              WHEN '01'                                                         
              WHEN '02'                                                         
              WHEN '03'                                                         
                   MOVE '1Q'               TO WS-QTR                            
              WHEN '04'                                                         
              WHEN '05'                                                         
              WHEN '06'                                                         
                   MOVE '2Q'               TO WS-QTR                            
              WHEN '07'                                                         
              WHEN '08'                                                         
              WHEN '09'                                                         
                   MOVE '3Q'               TO WS-QTR                            
              WHEN '10'                                                         
              WHEN '11'                                                         
              WHEN '12'                                                         
                   MOVE '4Q'               TO WS-QTR                            
              END-EVALUATE                                                      
              STRING K100-DTE-PACD(1:4) WS-QTR                                  
              DELIMITED BY SIZE INTO LB16-DTE-PCD                               
           ELSE                                                                 
              IF K100-NUM-PACD = 'M'                                            
                 MOVE K100-DTE-PACD(1:6)   TO LB16-DTE-PCD                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY '* ======================================= *'.               
           DISPLAY '   BP13CB16 CONTROL TOTAL     : ' WS-CUR-CCYYMMDD.          
           DISPLAY '* --------------------------------------- *'.               
           DISPLAY '   NO OF REC READ    BP13F800 : ' WS-F800-READ.             
           DISPLAY '   NO OF REC WRITTEN BP13LB16 : ' WS-LB16-WRITE.            
           DISPLAY '* ======================================= *'.               
                                                                                
           CLOSE BP13F800                                                       
                 BP17K000                                                       
                 BM06K110                                                       
                 BM06K100                                                       
                 BP13LB16.                                                      
                                                                                
           IF WS-K000-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP17K000, STATUS: ' WS-K000-STATUS         
              MOVE WS-K000-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BM06K110, STATUS: ' WS-K110-STATUS         
              MOVE WS-K110-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BM06K100, STATUS: ' WS-K100-STATUS         
              MOVE WS-K100-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
