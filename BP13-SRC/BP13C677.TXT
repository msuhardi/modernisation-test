       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C677.                                                 
      *AUTHOR.        LSB.                                                      
      *DATE-WRITTEN.  11/09/97.                                                 
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *    OBJECTIVE   : PROGRAM TO SELECT THE LONGER MONTH    *                
      *                  RESIDE FOR CASES WITH DIFFERENT       *                
      *                  MONTH RESIDE BUT SAME FLAT.           *                
      *                                                        *                
      *                  UPDATE FILE WITH LONGER MONTH RESIDE  *                
      *                  BUT 10-YR OCCUPN REMAIN AS "NO"       *                
      *                                                        *                
      *    INPUT FILE  : BP13F558                              *                
      *                : BP13F500                              *                
      *   OUTPUT FILE  : BP13F520                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG REF BY   DATE     DETAILS                          *                
      * -----------------------------                          *                
      * BP130377 LSB 970911 - NEW PGM                          *                
      * BP130445 SSS 980206 - EXPAND F558 RECL FROM 150 TO 400 *                
      * BP130458 LSB 980304 - ADD BP13F713                     *                
      * BP130445 LSB 980317 - OUTPUT UPDATE BP13F520 FOR UDPATE*                
      *                       BP13F558 IN BP13C693             *                
      *                       BYPASS ELIG-SCH= JSS , OR        *                
      * BP130652 NOR 990219 - EXPAND F500, F510 & F520 RECL    *                
      *                       FROM 300 TO 500 CHARACTERS       *                
      * BP130897 AKT 000803 - EXPAND F558 RECL FROM 400 TO 600 *                
      * BP132603 SSS 050304 - CATER FOR 5-YR                   *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
           SELECT BP13F713 ASSIGN TO BP13F713.                                  
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
           SELECT BP13F510 ASSIGN TO BP13F510.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F713                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 20 CHARACTERS                                        
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F713.                                                           
                                                                                
       FD  BP13F500                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F500.                                                           
                                                                                
       FD  BP13F520                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F520.                                                           
                                                                                
       FD  BP13F510                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F510.                                                           
                                                                                
       FD  BP13F558                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F558.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05 WS-F500-READ                PIC 9(6)    VALUE ZEROS.              
           05 WS-F520-WRITE               PIC 9(6)    VALUE ZEROS.              
           05 WS-F510-WRITE               PIC 9(6)    VALUE ZEROS.              
           05 WS-F520-UPDATE              PIC 9(6)    VALUE ZEROS.              
           05 WS-F558-READ                PIC 9(6)    VALUE ZEROS.              
           05 WS-MR                       PIC 9(6)    VALUE ZEROS.              
           05 WS-NMR-F558                 PIC 9(6)    VALUE ZEROS.              
           05 WS-NMR-F500                 PIC 9(6)    VALUE ZEROS.              
           05 WS-MTH-RESIDE               PIC 9(3)    VALUE ZEROS.              
                                                                                
       01  WS-CHK-MONTH                   PIC 9(3)    VALUE ZEROS.              
                                                                                
       01  WS-EOF-F500                    PIC 9       VALUE ZERO.               
           88 EOF-F500                    VALUE 1.                              
                                                                                
       01  WS-EOF-F558                    PIC 9       VALUE ZERO.               
           88 EOF-F558                    VALUE 1.                              
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 210-READ-F500      THRU 210-EXIT.                            
           PERFORM 220-READ-F558      THRU 220-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL F558-NUM-REGN = HIGH-VALUES                                  
             AND   F500-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F500                                                 
                       BP13F558                                                 
                       BP13F713                                                 
               OUTPUT  BP13F520                                                 
                       BP13F510.                                                
                                                                                
           READ BP13F713 AT END                                                 
                DISPLAY 'BP13F713-REC EMPTY'                                    
                PERFORM  999-CLOSE-FILES.                                       
                                                                                
           IF F713-CDE-COHORT = 'OP'                                            
              MOVE 60  TO WS-CHK-MONTH                                          
           ELSE                                                                 
              MOVE 60  TO WS-CHK-MONTH                                          
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       210-READ-F500.                                                           
      *-------------------------------------------------------------            
           READ BP13F500 AT END                                                 
                MOVE HIGH-VALUES TO F500-NUM-REGN                               
                GO TO 210-EXIT.                                                 
                                                                                
           ADD  1                       TO WS-F500-READ.                        
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       220-READ-F558.                                                           
      *-------------------------------------------------------------            
           READ BP13F558 AT END                                                 
                MOVE HIGH-VALUES TO F558-NUM-REGN                               
                GO TO 220-EXIT.                                                 
                                                                                
           ADD  1                       TO WS-F558-READ.                        
                                                                                
       220-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-RTN.                                                         
      *-------------------------------------------------------------            
           MOVE ZEROS TO  WS-MTH-RESIDE.                                        
           IF F500-NUM-REGN = F558-NUM-REGN                                     
               ADD 1 TO WS-MR                                                   
               PERFORM 420-BYPASS-UPDATE THRU 420-EXIT                          
                  UNTIL F558-NUM-REGN NOT = F500-NUM-REGN                       
                  OR    F558-NUM-REGN = HIGH-VALUES                             
               PERFORM 410-UPDATE-10YR-OCCUPN THRU 410-EXIT                     
               PERFORM 210-READ-F500 THRU 210-EXIT                              
           ELSE                                                                 
           IF F500-NUM-REGN > F558-NUM-REGN                                     
               ADD 1 TO WS-NMR-F558                                             
               PERFORM 220-READ-F558 THRU 220-EXIT                              
           ELSE                                                                 
              IF F500-NUM-REGN < F558-NUM-REGN                                  
                 ADD 1 TO WS-NMR-F500                                           
                 ADD 1 TO WS-F520-WRITE                                         
                 WRITE BP13F520-REC FROM BP13F500-REC                           
                 PERFORM 210-READ-F500 THRU 210-EXIT.                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       410-UPDATE-10YR-OCCUPN.                                                  
      *-------------------------------------------------------------            
                                                                                
           IF F500-CDE-ELIG-SCH = 'JSS' OR 'OR '                                
               NEXT SENTENCE                                                    
           ELSE                                                                 
               MOVE  WS-MTH-RESIDE TO  F500-NUM-MTH-RESIDE                      
               ADD 1               TO  WS-F510-WRITE                            
               WRITE BP13F510-REC FROM BP13F500-REC.                            
                                                                                
           WRITE BP13F520-REC FROM BP13F500-REC.                                
           ADD 1 TO WS-F520-UPDATE.                                             
           ADD 1 TO WS-F520-WRITE.                                              
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
       420-BYPASS-UPDATE.                                                       
      *-------------------------------------------------------------            
           IF F500-NUM-REGN = F558-NUM-REGN                                     
             IF F558-NUM-MTH-RESIDE = SPACES OR LOW-VALUES                      
                MOVE ZEROS                  TO  F558-NUM-MTH-RESIDE             
             END-IF                                                             
                                                                                
             IF F558-NUM-MTH-RESIDE < WS-CHK-MONTH                              
                IF F558-NUM-MTH-RESIDE > WS-MTH-RESIDE                          
                  MOVE F558-NUM-MTH-RESIDE  TO  WS-MTH-RESIDE.                  
                                                                                
           PERFORM 220-READ-F558 THRU 220-EXIT.                                 
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           CLOSE BP13F500                                                       
                 BP13F558                                                       
                 BP13F713                                                       
                 BP13F520                                                       
                 BP13F510.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C677 ================*'.              
           DISPLAY '1)   NO. OF F500 RECORDS READ  = ' WS-F500-READ.            
           DISPLAY '2)   NO. OF F558 RECORDS READ  = ' WS-F558-READ.            
           DISPLAY '3)   CASE #MATCH               = ' WS-MR.                   
           DISPLAY '4)   CASE #NMR F558            = ' WS-NMR-F558.             
           DISPLAY '5)   CASE #NMR F500            = ' WS-NMR-F500.             
           DISPLAY '6)   CASE WRITE F520           = ' WS-F520-WRITE.           
           DISPLAY '7)   CASE UPDATE F520          = ' WS-F520-UPDATE.          
           DISPLAY '8)   CASE WRITE F510           = ' WS-F510-WRITE.           
           DISPLAY 'NOTE : 1) = 6)                   '.                         
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
