       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CA40.                                                 
      *AUTHOR.        KARL MAGALONA CABUG.                                      
      *DATE-WRITTEN.  23/04/2014.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *  OBJECTIVE :                                               *            
      *                                                            *            
      *    TO E-MAIL THE MONTHLY REPORT OF PRINTED PCD             *            
      *                                                            *            
      *                                                            *            
      *  INPUT     :  1. BP13F205 - Accumulated ctrl file          *            
      *  OUTPUT    :  1. BP13MAIL - Email report                   *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF    DATE    BY   DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP135308 20140423 KAM4  NEW PROGRAM                        *            
      * BP136082 20160126 KVL1  REPLACED SU1 W/ HWM2 IN THE TO     *            
      *                         REPLACED TH1 W/ KB3 IN THE CC & RE-*            
      *                         PLACED ALL ADRESSEE TO LONG NAME   *            
      * BP136477 20161018 RJB1  REPLACED LIM KIM HUAT TO YEE MIEN  *            
      * BP136851 20170629 PCL4  TO UPDATE EMAIL RECIPIENTS         *            
      * BP139030 20220107 ZAR7  replace Chua Yee Mien to Lim Kok Chun           
      * BP139564 20230603 KAC1  replace Lim Kok Chun to Zany *                  
      *                        replace Lau hong_huat to Hee_Wan_Meng            
      *                       replace kong_Yoke_Pheng to David_Soh              
      *                        replace Lau soo chiew to Janice Oh               
      * BP139175 20240423 KAC1 replace Zany to Soffia_RAPHAY@hdb.gov.sg         
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F205 ASSIGN        TO BP13F205.                           
           SELECT BP13MAIL ASSIGN        TO BP13MAIL.                           
                                                                                
           SELECT BM06K510 ASSIGN TO BM06K510                                   
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K510-ESTCDE                         
                           FILE STATUS   IS WS-K510-STATUS.                     
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F205                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 80 CHARACTERS.                                      
       COPY BP13F205.                                                           
                                                                                
       FD   BM06K510                                                            
            RECORD CONTAINS  60 CHARACTERS.                                     
       COPY BM06K510.                                                           
                                                                                
       FD  BP13MAIL                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING  MODE  IS F.                                               
       01  MAIL-PRTREC                PIC X(132).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F205-READ           PIC 9(05) VALUE ZEROES.                   
           05  WS-SERIAL              PIC 9(03) VALUE ZEROES.                   
           05  WS-WRITE-OUT           PIC 9(03) VALUE ZEROES.                   
           05  WS-F205-EOF            PIC X(01) VALUE SPACES.                   
               88  F205-EOF                     VALUE 'Y'.                      
           05  WS-CHAR7               PIC X(07) VALUE SPACES.                   
           05  WS-NUM7 REDEFINES WS-CHAR7.                                      
               10 WS-TMP-PRINT-CNT    PIC 9(07).                                
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K510-STATUS         PIC 9(02) VALUE ZEROES.                   
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-DATE                PIC X(8)   VALUE SPACES.                  
           05  WS-DTE-NUM REDEFINES WS-DATE.                                    
               10 WS-CCYY            PIC 9(04).                                 
               10 WS-MM              PIC 9(02).                                 
               10 WS-DD              PIC 9(02).                                 
                                                                                
       01  WS-PREV-MONTH.                                                       
           05 WS-PREV-MM             PIC 9(02).                                 
           05 WS-PREV-MON REDEFINES WS-PREV-MM.                                 
              10 WS-PREVIOUS-MM      PIC X(02).                                 
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
      *For production.                                                          
       01  MAIL-HDR1.                                                           
           05  FILLER                 PIC X(12)  VALUE 'HELO SGPHDB1'.          
           05  FILLER                 PIC X(88)  VALUE SPACES.                  
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER                 PIC X(11)  VALUE 'MAIL FROM:<'.           
           05  MAIL-SENDID            PIC X(04)  VALUE 'OPCP'.                  
           05  FILLER                 PIC X(09)  VALUE '@SGPHDB1>'.             
           05  FILLER                 PIC X(76)  VALUE SPACES.                  
                                                                                
      ***For testing ENV****                                                    
      *01  MAIL-HDR1.                                                           
      *    05  FILLER                 PIC X(12)  VALUE 'HELO SGPHDB3'.          
      *    05  FILLER                 PIC X(120) VALUE SPACES.                  
      *01  MAIL-HDR2.                                                           
      *    05  FILLER                 PIC X(11)  VALUE 'MAIL FROM:<'.           
      *    05  MAIL-SENDID            PIC X(07)  VALUE 'CS933**'.               
      *    05  FILLER                 PIC X(09)  VALUE '@SGPHDB3>'.             
      *    05  FILLER                 PIC X(105) VALUE SPACES.                  
      *******************************                                           
       01  MAIL-DTL1.                                                           
           05  MAIL-RCPT              PIC X(09)  VALUE 'RCPT TO:<'.             
           05  MAIL-RCPT-LN           PIC X(123) VALUE SPACES.                  
                                                                                
       01  MAIL-DTL2.                                                           
           05  FILLER                 PIC X(04)  VALUE 'DATA'.                  
           05  FILLER                 PIC X(128) VALUE SPACES.                  
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER                 PIC X(05)  VALUE 'FROM:'.                 
           05  MAIL-SENDMAILID        PIC X(26)  VALUE                          
               'SOC System - Email Alert'.                                      
           05  FILLER                 PIC X(101) VALUE SPACES.                  
                                                                                
       01  MAIL-DTL4.                                                           
           05  MAIL-TO                PIC X(4)   VALUE 'TO:<'.                  
           05  MAIL-TO-LN             PIC X(128) VALUE SPACES.                  
                                                                                
       01  MAIL-DTL5.                                                           
           05  FILLER                 PIC X(10)  VALUE 'SUBJECT : '.            
           05  FILLER                 PIC X(19)  VALUE                          
               'Monthly Printed PCD'.                                           
           05  FILLER                 PIC X(103) VALUE SPACES.                  
                                                                                
       01  MAIL-DTL8.                                                           
           05 FILLER                  PIC X(6)   VALUE 'DATE: '.                
           05 MAIL-DATE               PIC X(10)  VALUE SPACES.                  
           05 FILLER                  PIC X(116) VALUE SPACES.                  
                                                                                
       01  MAIL-DTL9.                                                           
           05  FILLER                 PIC X(22)  VALUE                          
               'Dear Officer in-charge'.                                        
           05  FILLER                 PIC X(110) VALUE SPACES.                  
                                                                                
       01  MAIL-DTL9A.                                                          
           05  FILLER                 PIC X(10)  VALUE SPACES.                  
           05  FILLER                 PIC X(46)  VALUE                          
               'Below is a monthly report of printed PCD forms'.                
           05  FILLER                 PIC X(87)  VALUE SPACES.                  
                                                                                
       01  MAIL-BODYA7.                                                         
           05 FILLER                  PIC X(05)  VALUE SPACES.                  
           05 FILLER                  PIC X(52)  VALUE                          
              'SNO ESTATE  NEIGH CONTRACT BLOCK   ENTERED PCD   CHO'.           
           05 FILLER                  PIC X(52)  VALUE                          
              'SEN FORM USER ID DATE PRINTED NO. OF RECORDS PRINTED'.           
           05 FILLER                  PIC X(23)  VALUE SPACES.                  
                                                                                
       01  MAIL-BODYA8.                                                         
           05 FILLER                  PIC X(05)  VALUE SPACES.                  
           05 FILLER                  PIC X(52)  VALUE                          
              '=== ======= ===== ======== ===== =============== ==='.           
           05 FILLER                  PIC X(52)  VALUE                          
              '======== ======= ============ ======================'.           
           05 FILLER                  PIC X(23)  VALUE SPACES.                  
                                                                                
       01  MAIL-BODYA9.                                                         
           05 FILLER                  PIC X(05)  VALUE SPACES.                  
           05 MAIL-SER-NO             PIC X(03)  VALUE SPACES.                  
           05 FILLER                  PIC X(01)  VALUE SPACES.                  
           05 MAIL-ESTATE.                                                      
              10 MAIL-EST-CDE         PIC X(03)  VALUE SPACES.                  
              10 MAIL-EST-FILL        PIC X(01)  VALUE SPACES.                  
              10 MAIL-EST-NME         PIC X(03)  VALUE SPACES.                  
           05 FILLER                  PIC X(02)  VALUE SPACES.                  
           05 MAIL-NEIGH              PIC X(04)  VALUE SPACES.                  
           05 FILLER                  PIC X(03)  VALUE SPACES.                  
           05 MAIL-CONTRACT           PIC X(04)  VALUE SPACES.                  
           05 FILLER                  PIC X(03)  VALUE SPACES.                  
           05 MAIL-BLK-NO             PIC X(05)  VALUE SPACES.                  
           05 FILLER                  PIC X(02)  VALUE SPACES.                  
           05 MAIL-ENTERED-PCD        PIC X(15)  VALUE SPACES.                  
           05 FILLER                  PIC X(05)  VALUE SPACES.                  
           05 MAIL-CHOSEN-FORM        PIC X(01)  VALUE SPACES.                  
           05 FILLER                  PIC X(06)  VALUE SPACES.                  
           05 MAIL-USER-ID            PIC X(07)  VALUE SPACES.                  
           05 FILLER                  PIC X(02)  VALUE SPACES.                  
           05 MAIL-DATE-PRINT         PIC X(10)  VALUE SPACES.                  
           05 FILLER                  PIC X(07)  VALUE SPACES.                  
           05 MAIL-PRINT-CNT          PIC Z(07)  VALUE SPACES.                  
           05 FILLER                  PIC X(34)  VALUE SPACES.                  
                                                                                
      ****Add CC HERE*****                                                      
       01  MAIL-CC-DTL1A.                                                       
           05  FILLER                 PIC X(09)  VALUE 'RCPT TO:<'.             
           05  FILLER                 PIC X(25)  VALUE                          
               'Soffia_RAPHAY@hdb.gov.sg>'.                                     
           05  FILLER                 PIC X(98)  VALUE SPACES.                  
       01  MAIL-CC-DTL1B.                                                       
           05  FILLER                 PIC X(4)   VALUE 'CC:<'.                  
           05  FILLER                 PIC X(25)  VALUE                          
               'Soffia_RAPHAY@hdb.gov.sg>'.                                     
           05  FILLER                 PIC X(103) VALUE SPACES.                  
                                                                                
       01  MAIL-CC-DTL2A.                                                       
           05  FILLER                 PIC X(09)  VALUE 'RCPT TO:<'.             
           05  FILLER                 PIC X(25)  VALUE                          
               'Janice_ML_OH@hdb.gov.sg>'.                                      
           05  FILLER                 PIC X(98)  VALUE SPACES.                  
       01  MAIL-CC-DTL2B.                                                       
           05  FILLER                 PIC X(4)   VALUE 'CC:<'.                  
           05  FILLER                 PIC X(25)  VALUE                          
               'Janice_ML_OH@hdb.gov.sg>'.                                      
           05  FILLER                 PIC X(103) VALUE SPACES.                  
                                                                                
       01  MAIL-CC-DTL3A.                                                       
           05  FILLER                 PIC X(09)  VALUE 'RCPT TO:<'.             
           05  FILLER                 PIC X(26)  VALUE                          
               'DERRICK_WK_LOH@HDB.GOV.SG>'.                                    
           05  FILLER                 PIC X(97)  VALUE SPACES.                  
       01  MAIL-CC-DTL3B.                                                       
           05  FILLER                 PIC X(4)   VALUE 'CC:<'.                  
           05  FILLER                 PIC X(26)  VALUE                          
               'DERRICK_WK_LOH@HDB.GOV.SG>'.                                    
           05  FILLER                 PIC X(102) VALUE SPACES.                  
                                                                                
      *01  MAIL-CC-DTL4A.                                                       
      *    05  FILLER                 PIC X(09)  VALUE 'RCPT TO:<'.             
      *    05  FILLER                 PIC X(15)  VALUE                          
      *        'TH1@hdb.gov.sg>'.                                               
      *    05  FILLER                 PIC X(108) VALUE SPACES.                  
      *01  MAIL-CC-DTL4B.                                                       
      *    05  FILLER                 PIC X(4)   VALUE 'CC:<'.                  
      *    05  FILLER                 PIC X(15)  VALUE                          
      *        'TH1@hdb.gov.sg>'.                                               
      *    05  FILLER                 PIC X(113) VALUE SPACES.                  
                                                                                
       01  MAIL-CC-DTL4A.                                                       
           05  FILLER                 PIC X(09)  VALUE 'RCPT TO:<'.             
           05  FILLER                 PIC X(25)  VALUE                          
               'HEE_Wan_Meng@hdb.gov.sg>'.                                      
           05  FILLER                 PIC X(87)  VALUE SPACES.                  
       01  MAIL-CC-DTL4B.                                                       
           05  FILLER                 PIC X(4)   VALUE 'CC:<'.                  
           05  FILLER                 PIC X(25)  VALUE                          
               'HEE_Wan_Meng@hdb.gov.sg>'.                                      
           05  FILLER                 PIC X(92)  VALUE SPACES.                  
                                                                                
       01  MAIL-CC-DTL5A.                                                       
           05  FILLER                 PIC X(09)  VALUE 'RCPT TO:<'.             
           05  FILLER                 PIC X(27)  VALUE                          
               'David_FG_SOH@hdb.gov.sg>'.                                      
           05  FILLER                 PIC X(96)  VALUE SPACES.                  
       01  MAIL-CC-DTL5B.                                                       
           05  FILLER                 PIC X(4)   VALUE 'CC:<'.                  
           05  FILLER                 PIC X(27)  VALUE                          
               'David_FG_SOH@hdb.gov.sg>'.                                      
           05  FILLER                 PIC X(101) VALUE SPACES.                  
                                                                                
       01  MAIL-CC-DTL6A.                                                       
           05  FILLER                 PIC X(09)  VALUE 'RCPT TO:<'.             
           05  FILLER                 PIC X(26)  VALUE                          
               'TENG_SIEW_HONG@HDB.GOV.SG>'.                                    
           05  FILLER                 PIC X(97)  VALUE SPACES.                  
       01  MAIL-CC-DTL6B.                                                       
           05  FILLER                 PIC X(4)   VALUE 'CC:<'.                  
           05  FILLER                 PIC X(26)  VALUE                          
               'TENG_SIEW_HONG@HDB.GOV.SG>'.                                    
           05  FILLER                 PIC X(102) VALUE SPACES.                  
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                  PIC X      VALUE '.'.                     
           05 FILLER                  PIC X(131) VALUE SPACES.                  
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                  PIC X(132) VALUE SPACES.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-ROUTINE         THRU 1000-EXIT.                   
            PERFORM 2000-READ-BP13F205        THRU 2000-EXIT.                   
            PERFORM 3000-PROCESS-DETAIL       THRU 3000-EXIT                    
              UNTIL F205-EOF.                                                   
                                                                                
            IF WS-SERIAL > 0                                                    
               WRITE MAIL-PRTREC              FROM MAIL-END                     
            END-IF.                                                             
                                                                                
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            OPEN  INPUT BP13F205                                                
                        BM06K510                                                
                 OUTPUT BP13MAIL.                                               
                                                                                
           IF WS-K510-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'ERROR OPENING - BM06K510 : ' WS-K510-STATUS              
              MOVE     WS-K510-STATUS            TO RETURN-CODE                 
              PERFORM  9000-CLOSE-ROUTINE      THRU  9000-EXIT                  
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURRENT-DATE.                  
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO MAIL-DATE.                             
                                                                                
           IF WS-DD = 01                                                        
              COMPUTE WS-PREV-MM = WS-MM - 1                                    
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F205.                                                      
      *-------------------------------------------------------------            
           READ BP13F205                                                        
                AT END MOVE 'Y'         TO WS-F205-EOF                          
                GO TO  2000-EXIT.                                               
                                                                                
           ADD 1 TO WS-F205-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DETAIL.                                                     
      *-------------------------------------------------------------            
           IF (F205-KEY-FLD NOT = SPACES AND LOW-VALUES) AND                    
              (F205-NUM-OPTION = 'PC')                                          
              IF (F205-DTE-UPDATE(5:2) = WS-DATE(5:2)) OR                       
                 (F205-DTE-UPDATE(5:2) = WS-PREVIOUS-MM)                        
                 IF WS-SERIAL = ZERO                                            
                    PERFORM 3100-CREATE-MAIL-HEADER  THRU 3100-EXIT             
                    PERFORM 3200-WRITE-OUTMAIL       THRU 3200-EXIT             
                 ELSE                                                           
                    PERFORM 3200-WRITE-OUTMAIL       THRU 3200-EXIT             
                 END-IF                                                         
                                                                                
                 ADD 1      TO WS-WRITE-OUT                                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F205                THRU 2000-EXIT.            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-CREATE-MAIL-HEADER.                                                 
      *-------------------------------------------------------------            
           MOVE SPACES                          TO MAIL-RCPT-LN                 
                                                   MAIL-TO-LN.                  
                                                                                
      *    STRING 'SU1' DELIMITED BY SPACES                                     
      *           '@hdb.gov.sg' '>'                                             
      *           DELIMITED BY SIZE INTO MAIL-TO-OIC.                           
      *    MOVE MAIL-TO-OIC           TO MAIL-TO-OIC-2.                         
           STRING MAIL-RCPT DELIMITED BY SIZE                                   
                  'HEE_WAN_MENG@HDB.GOV.SG' '>'                                 
                  DELIMITED BY SPACE INTO MAIL-DTL1                             
           STRING MAIL-TO   'HEE_WAN_MENG@HDB.GOV.SG' '>'                       
                  DELIMITED BY SPACE INTO MAIL-DTL4                             
                                                                                
           WRITE MAIL-PRTREC                  FROM MAIL-HDR1.                   
           WRITE MAIL-PRTREC                  FROM MAIL-HDR2.                   
           WRITE MAIL-PRTREC                  FROM MAIL-DTL1.                   
      *cc                                                                       
           WRITE MAIL-PRTREC                  FROM MAIL-CC-DTL1A.               
           WRITE MAIL-PRTREC                  FROM MAIL-CC-DTL2A.               
           WRITE MAIL-PRTREC                  FROM MAIL-CC-DTL3A.               
           WRITE MAIL-PRTREC                  FROM MAIL-CC-DTL4A.               
           WRITE MAIL-PRTREC                  FROM MAIL-CC-DTL5A.               
           WRITE MAIL-PRTREC                  FROM MAIL-CC-DTL6A.               
           WRITE MAIL-PRTREC                  FROM MAIL-DTL2.                   
           WRITE MAIL-PRTREC                  FROM MAIL-DTL3.                   
                                                                                
           WRITE MAIL-PRTREC                  FROM MAIL-DTL4.                   
      *cc                                                                       
           WRITE MAIL-PRTREC                  FROM MAIL-CC-DTL1B.               
           WRITE MAIL-PRTREC                  FROM MAIL-CC-DTL2B.               
           WRITE MAIL-PRTREC                  FROM MAIL-CC-DTL3B.               
           WRITE MAIL-PRTREC                  FROM MAIL-CC-DTL4B.               
           WRITE MAIL-PRTREC                  FROM MAIL-CC-DTL5B.               
           WRITE MAIL-PRTREC                  FROM MAIL-CC-DTL6B.               
                                                                                
           WRITE MAIL-PRTREC                  FROM MAIL-DTL5.                   
                                                                                
           WRITE MAIL-PRTREC                  FROM MAIL-SPACES.                 
           WRITE MAIL-PRTREC                  FROM MAIL-DTL8.                   
           WRITE MAIL-PRTREC                  FROM MAIL-SPACES.                 
                                                                                
           WRITE MAIL-PRTREC                  FROM MAIL-DTL9.                   
           WRITE MAIL-PRTREC                  FROM MAIL-SPACES.                 
           WRITE MAIL-PRTREC                  FROM MAIL-DTL9A.                  
                                                                                
           WRITE MAIL-PRTREC                  FROM MAIL-SPACES.                 
           WRITE MAIL-PRTREC                  FROM MAIL-SPACES.                 
           WRITE MAIL-PRTREC                  FROM MAIL-BODYA7.                 
           WRITE MAIL-PRTREC                  FROM MAIL-BODYA8.                 
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-WRITE-OUTMAIL.                                                      
      *-------------------------------------------------------------            
           PERFORM 3300-READ-BM06K510         THRU 3300-EXIT.                   
                                                                                
           MOVE SPACES                          TO MAIL-BODYA9.                 
           ADD  1                               TO WS-SERIAL.                   
           MOVE WS-SERIAL                       TO MAIL-SER-NO.                 
           MOVE F205-NUM-NT-ZONE                TO MAIL-EST-CDE.                
           MOVE '/'                             TO MAIL-EST-FILL.               
           MOVE K510-ALPHA-NTCDE                TO MAIL-EST-NME.                
           MOVE F205-NUM-FLAT-TYPE              TO MAIL-NEIGH.                  
           MOVE F205-DTE-ALLOCN                 TO MAIL-CONTRACT.               
           MOVE F205-DTE-ALLOCN-NEW             TO MAIL-BLK-NO.                 
           MOVE F205-PRINT-NOTE                 TO MAIL-ENTERED-PCD.            
           IF F205-FORM-OPTION = 'A' OR 'B'                                     
              MOVE F205-FORM-OPTION             TO MAIL-CHOSEN-FORM             
           END-IF.                                                              
           MOVE F205-NUM-USERID                 TO MAIL-USER-ID.                
           STRING F205-DTE-UPDATE(1:4) '/' F205-DTE-UPDATE(5:2) '/'             
                  F205-DTE-UPDATE(7:2) DELIMITED BY SIZE                        
                                              INTO MAIL-DATE-PRINT.             
           MOVE F205-TOT-PRINT                  TO WS-CHAR7.                    
           MOVE WS-TMP-PRINT-CNT                TO MAIL-PRINT-CNT.              
                                                                                
           WRITE MAIL-PRTREC                  FROM MAIL-BODYA9.                 
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3300-READ-BM06K510.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                              TO K510-REC.                
           INITIALIZE                                  K510-REC.                
                                                                                
           MOVE F205-NUM-NT-ZONE                    TO K510-ESTCDE.             
                                                                                
           READ BM06K510.                                                       
                                                                                
           EVALUATE WS-K510-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 23                                                          
                    MOVE SPACES                     TO K510-ALPHA-NTCDE         
                                                                                
               WHEN OTHER                                                       
                    MOVE WS-K510-STATUS             TO RETURN-CODE              
                    DISPLAY 'ERROR READING BM06K100: ' WS-K510-STATUS           
                            ' KEY : ' K510-ESTCDE                               
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
           END-EVALUATE.                                                        
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
            DISPLAY ' '.                                                        
            DISPLAY ' ------ BP13CA40 CONTROL TOTALS ------'.                   
            DISPLAY ' '.                                                        
            DISPLAY '  NO. OF BP13F205 RECS READ     = ' WS-F205-READ.          
            DISPLAY '  NO. OF RECORD WRITE TO OUTFILE= ' WS-WRITE-OUT.          
            DISPLAY ' '.                                                        
                                                                                
            CLOSE BP13F205                                                      
                  BP13MAIL.                                                     
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
