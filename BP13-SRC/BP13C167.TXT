       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C167.                                                 
      *AUTHOR.        LSB.                                                      
      *DATE-WRITTEN.  17/08/1998.                                               
      * =========================================================== *           
      *                   SYSTEM OF COMMITMENT (SOC)                *           
      * =========================================================== *           
      *  OBJECTIVES : 1. PROGRAM TO MATCH SOC APPLN/ESENTIAL WITH   *           
      *                  LESSEE/OCCUPIER IN RDB FOR CASES FOUND IN  *           
      *                  DEBARMENT DEBARRED UNDER CREDIT ASSESSMENT *           
      *                                                             *           
      *               2. OUTPUT FILE FOR REPORTING ONLY             *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      *                                                             *           
      * OUTPUT FILE P13F558A DEBAR-EFF BEFORE DTE-APPLN, ESSENTAIL  *           
      *                      TALLY                                  *           
      *             P13F558B DEBAR-EFF BEFORE DTE-APPLN, ESSENTAIL  *           
      *                      NOT TALLY                              *           
      *             P13F558C DEBAR-EFF AFTER  DTE-APPLN, NOT NEED   *           
      *                      TO CHECK ESSENTAIL TALLY WITH RDB      *           
      * ----------------------------------------------------------- *           
      *                                                             *           
      * CHG REF  DATE     BY   DESCRIPTION                          *           
      * -------- -------- ---- -----------                          *           
      * BP130542 19980815 LSB  NEW PRGM                             *           
      *          19980828 LSB  STORE RDB ON LATEST FLAT OCCUP       *           
      * BP130645 19990420 SRA  TO CHANGE F558-NUM-DEBARMENT TO F558 *           
      *                        -NUM-DEBARMENT-2                     *           
      * BP130630 19990607 NOR  CHGE F715 F716 EXPAND TO 350         *           
      * BP130897 20000731 AKT  EXPAND F558 RECL FROM 400 TO 600     *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT BP13F716 ASSIGN TO BP13F716.                                  
           SELECT P13F558A ASSIGN TO P13F558A.                                  
           SELECT P13F558B ASSIGN TO P13F558B.                                  
           SELECT P13F558C ASSIGN TO P13F558C.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F716                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 350 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F716.                                                           
                                                                                
       FD  BP13F558                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F558.                                                           
                                                                                
       FD  P13F558A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F558A-REC                   PIC X(600).                           
                                                                                
       FD  P13F558B                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F558B-REC                   PIC X(600).                           
                                                                                
       FD  P13F558C                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F558C-REC                   PIC X(600).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05 WS-F716-READ                PIC 9(8)    VALUE ZEROS.              
           05 WS-F558-READ                PIC 9(8)    VALUE ZEROS.              
           05 WS-OUTPUT-A                 PIC 9(8)    VALUE ZEROS.              
           05 WS-OUTPUT-B                 PIC 9(8)    VALUE ZEROS.              
           05 WS-OUTPUT-C                 PIC 9(8)    VALUE ZEROS.              
           05 WS-MR-REGN-CNT              PIC 9(8)    VALUE ZEROS.              
           05 WS-F558-UNMATCH             PIC 9(8)    VALUE ZEROS.              
           05 WS-OCCUP-F716-STORED        PIC 9(2)    VALUE 0.                  
           05 WS-OCCUP-F558-STORED        PIC 9(2)    VALUE 0.                  
           05 WS-OCCP-NOTFD               PIC X(1)    VALUE SPACES.             
           05 WS-F558-FOUND               PIC X(1)    VALUE SPACES.             
           05 WS-AFTER-CUTOFF             PIC X(1)    VALUE SPACES.             
           05 WS-PREV-REGN                PIC X(8)    VALUE SPACES.             
           05 WS-F716-SCH-ACC             PIC X(9)    VALUE SPACES.             
                                                                                
       01  WS-FILE-POINTER.                                                     
           05 WS-F558-SUB                 PIC 9(2)    VALUE 0.                  
           05 WS-F558-PTR                 PIC 9(2)    VALUE 0.                  
           05 WS-F716-SUB                 PIC 9(2)    VALUE 0.                  
                                                                                
       01  WS-EOF-F558                    PIC X.                                
           88 EOF-F558                    VALUE 'Y'.                            
                                                                                
       01  WS-EOF-F716                    PIC X.                                
           88 EOF-F716                    VALUE 'Y'.                            
                                                                                
       COPY WSF716.                                                             
       COPY WSF558.                                                             
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-YY               PIC X(2).                                    
           05  WS-MM               PIC X(2).                                    
           05  WS-DD               PIC X(2).                                    
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD        PIC X(2).                                    
           05  FILLER              PIC X         VALUE '/'.                     
           05  WS-EDITED-MM        PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X         VALUE '/'.                     
           05  WS-EDITED-YY        PIC X(2)      VALUE SPACES.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F558      THRU 200-EXIT.                            
           PERFORM 300-READ-F716      THRU 300-EXIT.                            
           PERFORM 400-PROCESS-RTN    THRU 400-EXIT                             
             UNTIL F558-NUM-REGN = HIGH-VALUE                                   
             AND   F716-NUM-REGN = HIGH-VALUE.                                  
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F558                                                 
                       BP13F716                                                 
                OUTPUT P13F558A                                                 
                       P13F558B                                                 
                       P13F558C.                                                
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F558.                                                           
      *-------------------------------------------------------------            
           READ BP13F558 AT END                                                 
                MOVE HIGH-VALUE TO F558-NUM-REGN                                
                MOVE 'Y'        TO WS-EOF-F558                                  
                GO TO 200-EXIT.                                                 
                                                                                
           ADD  1 TO WS-F558-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-F716.                                                           
      *-------------------------------------------------------------            
           READ BP13F716 AT END                                                 
                MOVE HIGH-VALUE TO F716-NUM-REGN                                
                MOVE 'Y'        TO WS-EOF-F716                                  
                GO TO 300-EXIT.                                                 
                                                                                
           ADD  1 TO WS-F716-READ.                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-PROCESS-RTN.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES TO WS-AFTER-CUTOFF.                                      
           MOVE SPACES TO WS-OCCP-NOTFD.                                        
           MOVE SPACES TO WS-PREV-REGN.                                         
           MOVE SPACES TO WS-F716-SCH-ACC.                                      
                                                                                
           PERFORM 500-INITIALIZE THRU 500-EXIT.                                
                                                                                
           IF F558-NUM-REGN = F716-NUM-REGN                                     
                                                                                
              MOVE F558-NUM-REGN TO WS-PREV-REGN                                
              ADD 1 TO WS-MR-REGN-CNT                                           
              MOVE F716-NUM-SCH-ACCNT TO WS-F716-SCH-ACC                        
              PERFORM 600-STORE-F716 THRU 600-EXIT                              
                          UNTIL F716-NUM-REGN NOT = WS-PREV-REGN                
                          OR    EOF-F716                                        
              PERFORM 700-READ-REQ-BY-REGN THRU 700-EXIT                        
                          UNTIL F558-NUM-REGN NOT = WS-PREV-REGN                
                          OR    EOF-F558                                        
              PERFORM 800-WRITE-OUTPUT THRU 800-EXIT                            
           ELSE                                                                 
              IF F558-NUM-REGN < F716-NUM-REGN                                  
                 MOVE F558-NUM-REGN TO WS-PREV-REGN                             
                 PERFORM 850-UNMATCH-BP13F558 THRU 850-EXIT                     
                          UNTIL F558-NUM-REGN NOT = WS-PREV-REGN                
                          OR    EOF-F558                                        
                 PERFORM 860-WRITE-UNMATCH THRU 860-EXIT                        
              ELSE                                                              
                 PERFORM 300-READ-F716 THRU 300-EXIT.                           
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-INITIALIZE.                                                          
      *-------------------------------------------------------------            
           MOVE 1      TO WS-F716-SUB.                                          
           MOVE ZEROES TO WS-OCCUP-F716-STORED.                                 
           INITIALIZE WS-F716-HSEHOLD.                                          
                                                                                
           MOVE 1      TO WS-F558-SUB.                                          
           MOVE ZEROES TO WS-OCCUP-F558-STORED.                                 
           INITIALIZE WS-F558-RECORD.                                           
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       600-STORE-F716.                                                          
      *-------------------------------------------------------------            
                                                                                
           IF F716-NUM-SCH-ACCNT  =  WS-F716-SCH-ACC                            
               MOVE BP13F716-REC TO WS-F716-REC(WS-F716-SUB)                    
               ADD 1 TO WS-F716-SUB                                             
               ADD 1 TO WS-OCCUP-F716-STORED                                    
           END-IF.                                                              
                                                                                
           PERFORM 300-READ-F716 THRU 300-EXIT.                                 
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       700-READ-REQ-BY-REGN.                                                    
      *-------------------------------------------------------------            
                                                                                
                                                                                
           MOVE SPACES TO WS-F558-FOUND.                                        
           PERFORM 710-REQUEST-MATCH-RDB THRU 710-EXIT                          
                   VARYING WS-F558-PTR FROM 1 BY 1                              
                   UNTIL WS-F558-PTR > WS-OCCUP-F716-STORED                     
                   OR WS-F558-FOUND = 'Y'.                                      
                                                                                
           IF WS-F558-FOUND = SPACES                                            
              MOVE 'Y' TO WS-OCCP-NOTFD.                                        
                                                                                
           PERFORM 720-STORE-F558 THRU 720-EXIT.                                
           PERFORM 200-READ-F558 THRU 200-EXIT.                                 
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       710-REQUEST-MATCH-RDB.                                                   
                                                                                
           IF F558-NUM-NRIC = WS-F716-NUM-NRIC(WS-F558-PTR)                     
              MOVE 'Y' TO WS-F558-FOUND.                                        
                                                                                
                                                                                
           IF F558-NUM-DEBARMENT-2 = 'CA' OR 'CB' OR 'CC' OR 'CD' OR            
                                   'CE' OR 'CF' OR 'CH' OR 'CI' OR 'CJ'         
               IF F558-DTE-ISSUE-REGN < F558-DTE-DEBAR-EFF                      
                   MOVE SPACES TO WS-AFTER-CUTOFF                               
               ELSE                                                             
                   MOVE 'Y'    TO WS-AFTER-CUTOFF                               
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
       710-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       720-STORE-F558.                                                          
      *-------------------------------------------------------------            
           MOVE BP13F558-REC TO WS-F558-REC(WS-F558-SUB)                        
           ADD 1 TO WS-F558-SUB.                                                
           ADD 1 TO WS-OCCUP-F558-STORED.                                       
                                                                                
       720-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-WRITE-OUTPUT.                                                        
      *-------------------------------------------------------------            
           IF WS-AFTER-CUTOFF = 'Y'                                             
               PERFORM 830-OUTPUT-P13F558-C THRU 830-EXIT                       
                   VARYING WS-F558-SUB FROM 1 BY 1                              
                   UNTIL WS-F558-SUB > WS-OCCUP-F558-STORED                     
           ELSE                                                                 
               IF WS-OCCP-NOTFD = 'Y'                                           
                   PERFORM 820-OUTPUT-P13F558-B THRU 820-EXIT                   
                   VARYING WS-F558-SUB FROM 1 BY 1                              
                   UNTIL WS-F558-SUB > WS-OCCUP-F558-STORED                     
               ELSE                                                             
                   PERFORM 810-OUTPUT-P13F558-A THRU 810-EXIT                   
                   VARYING WS-F558-SUB FROM 1 BY 1                              
                   UNTIL WS-F558-SUB > WS-OCCUP-F558-STORED                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
      * OUTOUT FILE A FOR DEBAR-EFF < DTE-APPLN BUT SOC TALLY RDB               
       810-OUTPUT-P13F558-A.                                                    
                                                                                
           ADD 1 TO WS-OUTPUT-A.                                                
           WRITE P13F558A-REC FROM WS-F558-REC(WS-F558-SUB).                    
                                                                                
       810-EXIT.                                                                
           EXIT.                                                                
                                                                                
      * OUTOUT FILE B FOR DEBAR-EFF < DTE-APPLN BUT SOC NOT TALLY RDB           
       820-OUTPUT-P13F558-B.                                                    
                                                                                
           ADD 1 TO WS-OUTPUT-B.                                                
           WRITE P13F558B-REC FROM WS-F558-REC(WS-F558-SUB).                    
                                                                                
       820-EXIT.                                                                
           EXIT.                                                                
                                                                                
      * OUTOUT FILE C FOR DEBAR-EFF > DTE-APPLN                                 
       830-OUTPUT-P13F558-C.                                                    
                                                                                
           ADD 1 TO WS-OUTPUT-C.                                                
           WRITE P13F558C-REC FROM WS-F558-REC(WS-F558-SUB).                    
                                                                                
       830-EXIT.                                                                
           EXIT.                                                                
                                                                                
       850-UNMATCH-BP13F558.                                                    
                                                                                
           PERFORM 720-STORE-F558 THRU 720-EXIT.                                
                                                                                
           IF F558-NUM-DEBARMENT-2 NOT = SPACES                                 
               IF F558-DTE-ISSUE-REGN < F558-DTE-DEBAR-EFF                      
                   MOVE SPACES TO WS-AFTER-CUTOFF                               
               ELSE                                                             
                   MOVE 'Y'    TO WS-AFTER-CUTOFF                               
               END-IF                                                           
           END-IF.                                                              
           ADD 1 TO WS-F558-UNMATCH.                                            
                                                                                
           PERFORM 200-READ-F558 THRU 200-EXIT.                                 
                                                                                
       850-EXIT.                                                                
           EXIT.                                                                
                                                                                
       860-WRITE-UNMATCH.                                                       
           IF WS-AFTER-CUTOFF = 'Y'                                             
               PERFORM 830-OUTPUT-P13F558-C THRU 830-EXIT                       
                   VARYING WS-F558-SUB FROM 1 BY 1                              
                   UNTIL WS-F558-SUB > WS-OCCUP-F558-STORED                     
           ELSE                                                                 
               PERFORM 820-OUTPUT-P13F558-B THRU 820-EXIT                       
                   VARYING WS-F558-SUB FROM 1 BY 1                              
                   UNTIL WS-F558-SUB > WS-OCCUP-F558-STORED                     
           END-IF.                                                              
                                                                                
       860-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           CLOSE BP13F558                                                       
                 BP13F716                                                       
                 P13F558A                                                       
                 P13F558B                                                       
                 P13F558C.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C167 ================*'.              
           DISPLAY '1.  NO. OF F558 RECORDS READ     = ' WS-F558-READ.          
           DISPLAY '2.  NO. OF F716 RECORDS READ     = ' WS-F716-READ.          
           DISPLAY '3.  NO. OF F558 REGNO MATCH      = ' WS-MR-REGN-CNT.        
           DISPLAY '4.  NO OF  F558 UNMATCH          = '                        
                                                     WS-F558-UNMATCH.           
           DISPLAY '5.  NO. P13F558A RECORDS OUTPUT  = ' WS-OUTPUT-A.           
           DISPLAY '6.  NO. P13F558B RECORDS OUTPUT  = ' WS-OUTPUT-B.           
           DISPLAY '7.  NO. P13F558C RECORDS OUTPUT  = ' WS-OUTPUT-C.           
           DISPLAY ' '.                                                         
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
