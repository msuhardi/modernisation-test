       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C35H.                                                  
      *DATE-WRITTEN. 29 OCT 2015.                                               
      *AUTHOR.       KRISTINE LANTIN.                                           
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) To extract exception records of payment of  *            
      *                sales levy for HDB, bank or no loan with    *            
      *                resale code status of QN/LN/SD              *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP135823 20151029 KVL1  NEW PROGRAM                        *            
      * BP138047 20191125 KAC1  RECOMPILE FOR BP27 COPYBOOK UPDATE *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN           TO BP13F800.                       
           SELECT BP13L35G  ASSIGN           TO BP13L35G.                       
                                                                                
           SELECT BP27K800  ASSIGN           TO BP27K800                        
                            ORGANIZATION     IS INDEXED                         
                            ACCESS MODE      IS RANDOM                          
                            RECORD KEY       IS K800-NUM-HLA                    
                            FILE STATUS      IS BP27K800-STATUS.                
                                                                                
           SELECT BB24K510  ASSIGN           TO BB24K510                        
                            ORGANIZATION     IS INDEXED                         
                            ACCESS MODE      IS DYNAMIC                         
                            RECORD KEY       IS K510-KEY-FLD                    
                            FILE STATUS      IS BB24K510-STATUS.                
                                                                                
           SELECT BB24K500  ASSIGN           TO BB24K500                        
                            ORGANIZATION     IS INDEXED                         
                            ACCESS MODE      IS RANDOM                          
                            RECORD KEY       IS K500-KEY-FLD                    
                            FILE STATUS      IS BB24K500-STATUS.                
                                                                                
           SELECT BP13K022  ASSIGN           TO BP13K022                        
                            ORGANIZATION     IS INDEXED                         
                            ACCESS MODE      IS RANDOM                          
                            RECORD KEY       IS K022-KEY-FLD                    
                            ALTERNATE KEY    IS K022-NUM-REGN                   
                            FILE STATUS      IS BP13K022-STATUS.                
                                                                                
           SELECT BP13K024  ASSIGN           TO BP13K024                        
                            ORGANIZATION     IS INDEXED                         
                            ACCESS MODE      IS RANDOM                          
                            RECORD KEY       IS K024-KEY-FLD                    
                            FILE STATUS      IS BP13K024-STATUS.                
                                                                                
           SELECT BP13K162  ASSIGN           TO BP13K162                        
                            ORGANIZATION     IS INDEXED                         
                            ACCESS MODE      IS RANDOM                          
                            RECORD KEY       IS K162-KEY-FLD                    
                            FILE STATUS      IS BP13K162-STATUS.                
                                                                                
           SELECT BP13L35H ASSIGN TO BP13L35H.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F800                                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE  IS  F.                                               
       COPY BP13F800.                                                           
                                                                                
       FD  BP13L35G                                                             
           RECORD CONTAINS 11   CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       01  BP13L35G-REC.                                                        
           05 BP13L35G-NUM-SCH-ACC   PIC X(11).                                 
                                                                                
       FD  BP27K800                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
       COPY BP27K800.                                                           
                                                                                
       FD  BB24K510                                                             
           RECORD CONTAINS 50   CHARACTERS.                                     
       COPY BB24K510.                                                           
                                                                                
       FD  BB24K500                                                             
           RECORD CONTAINS 1700 CHARACTERS.                                     
       COPY BB24K500.                                                           
                                                                                
       FD  BP13K022                                                             
           RECORD CONTAINS 100  CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD  BP13K024                                                             
           RECORD CONTAINS 100  CHARACTERS.                                     
       COPY BP13K024.                                                           
                                                                                
       FD  BP13K162                                                             
           RECORD CONTAINS 600  CHARACTERS.                                     
       COPY BP13K162.                                                           
                                                                                
       FD  BP13L35H                                                             
           RECORD CONTAINS 200  CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  BP13L35H-REC.                                                        
           05 BP13L35H-NUM-REGN      PIC X(08).                                 
           05 BP13L35H-NRIC          PIC X(09).                                 
           05 BP13L35H-RESALE-STATUS PIC X(02).                                 
           05 BP13L35H-NUM-HDBREF    PIC X(11).                                 
           05 BP13L35H-NUM-CSM       PIC X(38).                                 
           05 BP13L35H-LOAN-TYPE     PIC X(01).                                 
           05 BP13L35H-OIC-EMAIL     PIC X(60).                                 
           05 BP13L35H-HYBRID-TAG    PIC X(01).                                 
           05 BP13L35H-K162-TAG      PIC X(01).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP27K800-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  BB24K510-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  BB24K500-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  BP13K022-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  BP13K024-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  BP13K162-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  WS-FBM6-READ-CNT    PIC 9(06)  VALUE ZEROES.                         
       01  WS-F800-READ-CNT    PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-CNT        PIC 9(06)  VALUE ZEROES.                         
       01  WS-HYBRID-TAG-QN    PIC X      VALUE 'N'.                            
       01  WS-HYBRID-TAG-LN    PIC X      VALUE 'N'.                            
       01  WS-FBM6-EOF         PIC X      VALUE 'N'.                            
       01  WS-F800-EOF         PIC X      VALUE 'N'.                            
       01  WS-K510-EOF         PIC X      VALUE 'N'.                            
       01  WS-OIC-EXIST-TAG    PIC X      VALUE 'N'.                            
       01  WS-K162-TAG         PIC X      VALUE SPACE.                          
       01  WS-I                PIC 9(02)  VALUE ZEROES.                         
       01  WS-R                PIC 9(02)  VALUE ZEROES.                         
       01  WS-CUR-DATE.                                                         
           05 WS-CCYY          PIC 9(04).                                       
           05 WS-MM            PIC 9(02).                                       
           05 WS-DD            PIC 9(02).                                       
       01  WS-ARRAY-DTLS.                                                       
           05 WS-NRIC          OCCURS 4 TIMES                                   
                               PIC X(09)  VALUE SPACES.                         
           05 WS-TEMP-REC.                                                      
              10 WS-TEMP-DTL   OCCURS 10 TIMES.                                 
                 15 WS-TEMP-NUM-REGN      PIC X(08).                            
                 15 WS-TEMP-NRIC          PIC X(09).                            
                 15 WS-TEMP-RESALE-STATUS PIC X(02).                            
                 15 WS-TEMP-NUM-HDBREF    PIC X(11).                            
                 15 WS-TEMP-NUM-CSM       PIC X(38).                            
                 15 WS-TEMP-LOAN-TYPE     PIC X(01).                            
                 15 WS-TEMP-OIC-EMAIL     PIC X(60).                            
                 15 WS-TEMP-HYBRID-TAG    PIC X(01).                            
                 15 WS-TEMP-K162-TAG      PIC X(01).                            
       01  WS-SAVE-NRIC        PIC X(09)  VALUE SPACES.                         
       01  WS-NAME-OIC         PIC X(30)  VALUE SPACES.                         
       01  WS-CSM              PIC X(38)  VALUE SPACES.                         
       01  WS-EMAIL            PIC X(60)  VALUE SPACES.                         
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13CCNT                        *        
      *----------------------------------------------------------------*        
       01  WS-BP13CCNT-LINK.                                                    
           05  LINK-CNTCT-TYPE         PIC X(3).                                
           05  LINK-STAFF-NICK         PIC X(6).                                
           05  LINK-STAFF-NUM          PIC X(6).                                
           05  LINK-STAFF-CNTCT        PIC X(50).                               
           05  LINK-SYSERR             PIC X(4).                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
           MOVE SPACES                     TO WS-TEMP-REC                       
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13L35G  THRU 2000-EXIT.                          
           PERFORM 2100-READ-BP13F800  THRU 2100-EXIT.                          
           IF WS-FBM6-EOF = 'Y' OR WS-F800-EOF = 'Y'                            
              PERFORM 4000-WARNING-EMPTY THRU 4000-EXIT                         
           ELSE                                                                 
              PERFORM 3000-MAIN-PROCESS  THRU 3000-EXIT                         
                UNTIL WS-FBM6-EOF = 'Y' AND WS-F800-EOF = 'Y'                   
           END-IF.                                                              
                                                                                
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13L35G                                                 
                       BP27K800                                                 
                       BB24K510                                                 
                       BB24K500                                                 
                       BP13K022                                                 
                       BP13K024                                                 
                       BP13K162                                                 
                OUTPUT BP13L35H.                                                
                                                                                
           IF BP27K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP27K800 OPEN ERROR, STATUS : ' BP27K800-STATUS          
              MOVE     BP27K800-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BB24K510-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BB24K510 OPEN ERROR, STATUS : ' BB24K510-STATUS          
              MOVE     BB24K510-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BB24K500-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BB24K500 OPEN ERROR, STATUS : ' BB24K500-STATUS          
              MOVE     BB24K500-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K022-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K022 OPEN ERROR, STATUS : ' BP13K022-STATUS          
              MOVE     BP13K022-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K024-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K024 OPEN ERROR, STATUS : ' BP13K024-STATUS          
              MOVE     BP13K024-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K162-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K162 OPEN ERROR, STATUS : ' BP13K162-STATUS          
              MOVE     BP13K162-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13L35G.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13L35G AT END                                                 
                         MOVE 'Y'  TO WS-FBM6-EOF                               
                         NOT AT END                                             
                         ADD  1    TO WS-FBM6-READ-CNT                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800 AT END                                                 
                         MOVE 'Y'  TO WS-F800-EOF                               
                         NOT AT END                                             
                         ADD  1    TO WS-F800-READ-CNT                          
           END-READ.                                                            
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-READ-BP27K800.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES       TO K800-NUM-HLA                                    
           MOVE F800-NUM-HLA TO K800-NUM-HLA                                    
                                                                                
           READ BP27K800.                                                       
                                                                                
           EVALUATE BP27K800-STATUS                                             
             WHEN 00                                                            
                IF K800-DTE-HLA-EXPIRY > WS-CUR-DATE                            
                   MOVE F800-NUM-NRIC1       TO WS-NRIC(1)                      
                   MOVE F800-NUM-NRIC2       TO WS-NRIC(2)                      
                   MOVE F800-NUM-NRIC3       TO WS-NRIC(3)                      
                   MOVE F800-NUM-NRIC4       TO WS-NRIC(4)                      
                   MOVE 1                    TO WS-R                            
                   PERFORM VARYING WS-I FROM 1  BY 1 UNTIL WS-I > 4             
                     OR WS-NRIC(WS-I) = SPACES                                  
                     PERFORM 2300-READ-BB24K510 THRU 2300-EXIT                  
                   END-PERFORM                                                  
                END-IF                                                          
             WHEN 23                                                            
                CONTINUE                                                        
             WHEN OTHER                                                         
                DISPLAY 'BP27K800 READ ERROR, STATUS : '                        
                         BP27K800-STATUS                                        
                MOVE BP27K800-STATUS TO RETURN-CODE                             
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2300-READ-BB24K510.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                TO K510-KEY-FLD                           
           MOVE WS-NRIC(WS-I)         TO K510-NUM-UIN                           
           MOVE 'N'                   TO WS-K510-EOF.                           
                                                                                
           START BB24K510 KEY >= K510-KEY-FLD.                                  
                                                                                
           EVALUATE BB24K510-STATUS                                             
              WHEN 00                                                           
                 PERFORM 2310-READNEXT-BB24K510                                 
                    THRU 2310-EXIT                                              
                   UNTIL WS-K510-EOF = 'Y'                                      
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR STARTBROWSE BB24K510. STATUS IS '               
                            BB24K510-STATUS                                     
                 MOVE BB24K510-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2310-READNEXT-BB24K510.                                                  
      *-------------------------------------------------------------            
                                                                                
           READ BB24K510 NEXT RECORD.                                           
                                                                                
           EVALUATE BB24K510-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                 IF K510-NUM-UIN    = WS-NRIC(WS-I)                             
                    IF K510-CDE-STATUS = 'S'                                    
                       PERFORM 2400-READ-BB24K500 THRU 2400-EXIT                
                    END-IF                                                      
                 ELSE                                                           
                    MOVE 'Y' TO   WS-K510-EOF                                   
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 MOVE 'Y' TO   WS-K510-EOF                                      
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READ NEXT BB24K510. STATUS IS '                 
                            BB24K510-STATUS                                     
                 MOVE BB24K510-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2310-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2400-READ-BB24K500.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES            TO K500-KEY-FLD                               
           MOVE K510-NUM-HDB-REF  TO K500-NUM-HDB-REF                           
           MOVE K510-DTE-APPROVAL TO K500-DTE-APPROVAL                          
                                                                                
           READ BB24K500.                                                       
                                                                                
           EVALUATE BB24K500-STATUS                                             
             WHEN 00                                                            
                IF K500-CDE-RESALE-STATUS = 'QN' OR                             
                   K500-CDE-RESALE-STATUS = 'LN' OR                             
                   K500-CDE-RESALE-STATUS = 'SD'                                
                   IF WS-NRIC(WS-I) = K500-NUM-UIN-SELLER1   OR                 
                      WS-NRIC(WS-I) = K500-NUM-UIN-SELLER2   OR                 
                      WS-NRIC(WS-I) = K500-NUM-UIN-SELLER3   OR                 
                      WS-NRIC(WS-I) = K500-NUM-UIN-SELLER4   OR                 
                     (WS-NRIC(WS-I) = K500-NUM-UIN-SPOUSE1   AND                
                      K500-CDE-MAR-STATUS-SELLER1 NOT = 'D') OR                 
                     (WS-NRIC(WS-I) = K500-NUM-UIN-SPOUSE2   AND                
                      K500-CDE-MAR-STATUS-SELLER2 NOT = 'D') OR                 
                     (WS-NRIC(WS-I) = K500-NUM-UIN-SPOUSE3   AND                
                      K500-CDE-MAR-STATUS-SELLER3 NOT = 'D') OR                 
                     (WS-NRIC(WS-I) = K500-NUM-UIN-SPOUSE4   AND                
                      K500-CDE-MAR-STATUS-SELLER4 NOT = 'D')                    
                         PERFORM 2500-READ-BP13K022  THRU 2500-EXIT             
                         PERFORM 2800-READ-BP13K162  THRU 2800-EXIT             
                         PERFORM 5000-MOVE-TO-TEMP   THRU 5000-EXIT             
                   END-IF                                                       
                END-IF                                                          
             WHEN 23                                                            
                CONTINUE                                                        
             WHEN OTHER                                                         
                DISPLAY 'BB24K500 READ ERROR, STATUS : '                        
                         BB24K500-STATUS                                        
                MOVE BB24K500-STATUS TO RETURN-CODE                             
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2500-READ-BP13K022.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES        TO K022-NUM-REGN.                                 
           MOVE F800-NUM-REGN TO K022-NUM-REGN.                                 
                                                                                
           READ BP13K022 KEY  IS K022-NUM-REGN.                                 
                                                                                
           EVALUATE BP13K022-STATUS                                             
             WHEN 00                                                            
                IF K022-NUM-OIC NOT = SPACES  AND LOW-VALUES                    
                   MOVE 'Y'    TO WS-OIC-EXIST-TAG                              
                   PERFORM 2600-READ-BP13K024 THRU 2600-EXIT                    
                   PERFORM 2700-CALL-BP13CCNT THRU 2700-EXIT                    
                ELSE                                                            
                   MOVE 'N'    TO WS-OIC-EXIST-TAG                              
                END-IF                                                          
             WHEN 23                                                            
                MOVE    'N'    TO WS-OIC-EXIST-TAG                              
             WHEN OTHER                                                         
                DISPLAY 'BP13K022 READ ERROR, STATUS : '                        
                         BP13K022-STATUS                                        
                MOVE BP27K800-STATUS TO RETURN-CODE                             
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2600-READ-BP13K024.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES        TO K024-KEY-FLD.                                  
           MOVE K022-NUM-OIC  TO K024-KEY-FLD.                                  
                                                                                
           READ BP13K024.                                                       
                                                                                
           EVALUATE BP13K024-STATUS                                             
             WHEN 00                                                            
                MOVE K024-NME-OIC TO WS-NAME-OIC                                
             WHEN 23                                                            
                DISPLAY 'OIC NAME NOT FOUND IN BP13K024 - '                     
                         K022-NUM-OIC                                           
                MOVE SPACES       TO WS-NAME-OIC                                
             WHEN OTHER                                                         
                DISPLAY 'BP13K024 READ ERROR, STATUS : '                        
                         BP13K024-STATUS                                        
                MOVE BP13K024-STATUS TO RETURN-CODE                             
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2700-CALL-BP13CCNT.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                     TO WS-BP13CCNT-LINK.                 
           MOVE 'EML'                      TO LINK-CNTCT-TYPE.                  
           MOVE K022-NUM-OIC               TO LINK-STAFF-NICK.                  
           MOVE SPACES                     TO LINK-STAFF-NUM.                   
                                                                                
           CALL 'BP13CCNT' USING WS-BP13CCNT-LINK.                              
                                                                                
           EVALUATE LINK-SYSERR                                                 
           WHEN ZEROS                                                           
              MOVE LINK-STAFF-CNTCT        TO WS-EMAIL                          
           WHEN 100                                                             
              DISPLAY 'OIC NOT FOUND IN OFFICE_CNTCT TABLE - '                  
                  K022-NUM-OIC  ', REGN : ' F800-NUM-REGN                       
              MOVE  SPACES                 TO WS-EMAIL                          
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING OFFICE_CNTCT TABLE = '                     
                       LINK-SYSERR                                              
              MOVE  SPACES                 TO WS-EMAIL                          
           END-EVALUATE.                                                        
                                                                                
       2700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2800-READ-BP13K162.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES        TO K162-KEY-FLD.                                  
           MOVE SPACE         TO WS-K162-TAG.                                   
           MOVE F800-NUM-REGN TO K162-NUM-REGNO.                                
           MOVE WS-NRIC(WS-I) TO K162-NUM-NRIC.                                 
                                                                                
           READ BP13K162.                                                       
                                                                                
           EVALUATE BP13K162-STATUS                                             
             WHEN 00                                                            
                MOVE 'Y'          TO WS-K162-TAG                                
             WHEN 23                                                            
                MOVE 'N'          TO WS-K162-TAG                                
             WHEN OTHER                                                         
                DISPLAY 'BP13K162 READ ERROR, STATUS : '                        
                         BP13K162-STATUS                                        
                MOVE BP13K162-STATUS TO RETURN-CODE                             
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF WS-FBM6-EOF = 'N' AND WS-F800-EOF = 'N'                           
              IF BP13L35G-NUM-SCH-ACC = F800-NUM-SCH-ACC                        
                 PERFORM 2200-READ-BP27K800      THRU 2200-EXIT                 
                 MOVE 'N'                        TO WS-HYBRID-TAG-QN            
                 MOVE 'N'                        TO WS-HYBRID-TAG-LN            
                 MOVE  0                         TO WS-I                        
                 PERFORM VARYING WS-R FROM 1 BY 1 UNTIL WS-R > 10               
                    OR WS-TEMP-NRIC(WS-R) = SPACES                              
                     ADD 1 TO WS-I                                              
                 END-PERFORM                                                    
                 IF WS-I > 1                                                    
                    PERFORM VARYING WS-R FROM 1 BY 1 UNTIL WS-R > WS-I          
                         OR WS-HYBRID-TAG-QN = 'Y'                              
                       PERFORM 5010-CHECK-HYBRID-QN-CASE                        
                          THRU 5010-EXIT                                        
                    END-PERFORM                                                 
                    PERFORM VARYING WS-R FROM 1 BY 1 UNTIL WS-R > WS-I          
                         OR WS-HYBRID-TAG-LN = 'Y'                              
                       PERFORM 5020-CHECK-HYBRID-LN-CASE                        
                          THRU 5020-EXIT                                        
                    END-PERFORM                                                 
                    IF WS-HYBRID-TAG-LN = 'Y' AND                               
                       WS-HYBRID-TAG-QN = 'Y'                                   
                       PERFORM VARYING WS-R FROM 1 BY 1                         
                               UNTIL WS-R > WS-I                                
                         MOVE '*'  TO WS-TEMP-HYBRID-TAG(WS-R)                  
                       END-PERFORM                                              
                    ELSE                                                        
                       PERFORM VARYING WS-R FROM 1 BY 1                         
                               UNTIL WS-R > WS-I                                
                         MOVE ' '  TO WS-TEMP-HYBRID-TAG(WS-R)                  
                       END-PERFORM                                              
                    END-IF                                                      
                 ELSE                                                           
                    MOVE SPACE     TO WS-TEMP-HYBRID-TAG(WS-I)                  
                 END-IF                                                         
                 PERFORM VARYING WS-R FROM 1 BY 1 UNTIL WS-R > WS-I             
                    PERFORM 5100-MOVE-TO-OUTPUT   THRU 5100-EXIT                
                 END-PERFORM                                                    
                 MOVE SPACES                      TO WS-TEMP-REC                
                 PERFORM 2000-READ-BP13L35G       THRU 2000-EXIT                
                 PERFORM 2100-READ-BP13F800       THRU 2100-EXIT                
              ELSE                                                              
                IF BP13L35G-NUM-SCH-ACC < F800-NUM-SCH-ACC                      
                   PERFORM 2000-READ-BP13L35G     THRU 2000-EXIT                
                ELSE                                                            
                  IF BP13L35G-NUM-SCH-ACC > F800-NUM-SCH-ACC                    
                     PERFORM 2100-READ-BP13F800   THRU 2100-EXIT                
                END-IF                                                          
              END-IF                                                            
           ELSE                                                                 
             IF WS-FBM6-EOF = 'Y'                                               
                PERFORM 2100-READ-BP13F800        THRU 2100-EXIT                
                  UNTIL WS-F800-EOF = 'Y'                                       
             ELSE                                                               
               IF WS-F800-EOF = 'Y'                                             
                  PERFORM 2000-READ-BP13L35G      THRU 2000-EXIT                
                    UNTIL WS-FBM6-EOF = 'Y'                                     
               END-IF                                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-WARNING-EMPTY.                                                      
      *-------------------------------------------------------------            
                                                                                
           IF WS-FBM6-EOF = 'Y'                                                 
              DISPLAY 'FILE BP13L35G IS EMPTY. '                                
           END-IF.                                                              
                                                                                
           IF WS-F800-EOF = 'Y'                                                 
              DISPLAY 'FILE BP13F800 IS EMPTY. '                                
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-MOVE-TO-TEMP.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE  SPACES                 TO WS-TEMP-DTL(WS-R).                   
           MOVE  F800-NUM-REGN          TO WS-TEMP-NUM-REGN(WS-R).              
           MOVE  WS-NRIC(WS-I)          TO WS-TEMP-NRIC(WS-R).                  
           MOVE  K500-CDE-RESALE-STATUS TO WS-TEMP-RESALE-STATUS(WS-R).         
           MOVE  K500-NUM-HDB-REF       TO WS-TEMP-NUM-HDBREF(WS-R).            
           MOVE  F800-NUM-LOAN-TAG      TO WS-TEMP-LOAN-TYPE(WS-R).             
           MOVE  WS-K162-TAG            TO WS-TEMP-K162-TAG(WS-R).              
           MOVE  SPACES                 TO WS-CSM                               
                                                                                
           IF WS-OIC-EXIST-TAG = 'Y'                                            
              STRING '(' K022-NUM-OIC ') ' WS-NAME-OIC                          
              DELIMITED BY SIZE INTO WS-CSM                                     
              MOVE  WS-CSM              TO WS-TEMP-NUM-CSM(WS-R)                
              MOVE  WS-EMAIL            TO WS-TEMP-OIC-EMAIL(WS-R)              
           END-IF.                                                              
                                                                                
           ADD 1                        TO WS-R.                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5010-CHECK-HYBRID-QN-CASE.                                               
      *-------------------------------------------------------------            
                                                                                
           IF WS-TEMP-RESALE-STATUS(WS-R) = 'QN' OR 'SD'                        
              MOVE 'Y' TO WS-HYBRID-TAG-QN                                      
           END-IF.                                                              
                                                                                
       5010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5020-CHECK-HYBRID-LN-CASE.                                               
      *-------------------------------------------------------------            
                                                                                
           IF WS-TEMP-RESALE-STATUS(WS-R) = 'LN'                                
              MOVE 'Y' TO WS-HYBRID-TAG-LN                                      
           END-IF.                                                              
                                                                                
       5020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5100-MOVE-TO-OUTPUT.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE  SPACES                   TO BP13L35H-REC.                      
           MOVE  WS-TEMP-NUM-REGN(WS-R)   TO BP13L35H-NUM-REGN.                 
           MOVE  WS-TEMP-NRIC(WS-R)       TO BP13L35H-NRIC.                     
           MOVE  WS-TEMP-RESALE-STATUS(WS-R)                                    
                                          TO BP13L35H-RESALE-STATUS.            
           MOVE  WS-TEMP-NUM-HDBREF(WS-R) TO BP13L35H-NUM-HDBREF.               
           MOVE  WS-TEMP-LOAN-TYPE(WS-R)  TO BP13L35H-LOAN-TYPE.                
           MOVE  WS-TEMP-NUM-CSM(WS-R)    TO BP13L35H-NUM-CSM.                  
           MOVE  WS-TEMP-OIC-EMAIL(WS-R)  TO BP13L35H-OIC-EMAIL.                
           MOVE  WS-TEMP-HYBRID-TAG(WS-R) TO BP13L35H-HYBRID-TAG.               
           MOVE  WS-TEMP-K162-TAG(WS-R)   TO BP13L35H-K162-TAG.                 
           WRITE BP13L35H-REC.                                                  
           ADD 1 TO WS-WRITE-CNT.                                               
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
                                                                                
           DISPLAY '--- BP13C35H CONTROL TOTAL ------'.                         
           DISPLAY ' NO OF REC READ FROM BP13L35G  = '                          
                     WS-FBM6-READ-CNT.                                          
           DISPLAY ' NO OF REC READ FROM BP13F800  = '                          
                     WS-F800-READ-CNT.                                          
           DISPLAY ' NO OF REC WRITTEN TO BP13L35H = '                          
                     WS-WRITE-CNT.                                              
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13F800                                                 
                       BP13L35G                                                 
                       BP27K800                                                 
                       BB24K510                                                 
                       BB24K500                                                 
                       BP13K022                                                 
                       BP13K024                                                 
                       BP13L35H.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
