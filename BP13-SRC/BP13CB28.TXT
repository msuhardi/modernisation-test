       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB28.                                                 
      *DATE-WRITTEN.  22/08/2007.                                               
      *AUTHOR.        ED1.                                                      
      * ========================================================== *            
      *  SYSTEM OF COMMITMENT (BP13)                               *            
      * ---------------------------------------------------------- *            
      *   OBJECTIVE :  MATCH BP13F593 WITH BP13F800 BY NRIC        *            
      *                AND CREATE P13F593A FOR BP13F593 NOTFND IN  *            
      *                BP13F800                                    *            
      *                                                            *            
      *   INPUT   : BP13F593                                       *            
      *             BP13F800                                       *            
      *   OUTPUT  : P13F593A - F593 NOT FND IN F800 CASES          *            
      *                                                            *            
      * CHG-NO    CHG-DATE     BY   DESCRIPTION                    *            
      * ------    --------     --   -----------                    *            
      * BP133111  22/08/2007   ED1  NEW PROGRAM                    *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
           SELECT BP13F593 ASSIGN TO BP13F593.                                  
           SELECT P13F593A ASSIGN TO P13F593A.                                  
           SELECT P13F593B ASSIGN TO P13F593B.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F593                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F593.                                                           
                                                                                
       FD  BP13F800                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 30 CHARACTERS                                        
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01 BP13F800-REC.                                                         
          05 F800-NUM-REGN      PIC X(8).                                       
          05 F800-NUM-NRIC      PIC X(9).                                       
          05 F800-DTE-BALLOT    PIC X(6).                                       
          05 F800-NUM-ALLO-CAT  PIC X(3).                                       
          05 FILLER             PIC X(4).                                       
                                                                                
       FD  P13F593A                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  WS-F593A-REC   PIC X(2000).                                          
                                                                                
       FD  P13F593B                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  WS-F593B-REC   PIC X(2000).                                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-F800-READ             PIC 9(7)    VALUE ZERO.                     
       01  WS-F593-READ             PIC 9(7)    VALUE ZERO.                     
       01  WS-F593A-WRITE           PIC 9(7)    VALUE ZERO.                     
       01  WS-F593B-WRITE           PIC 9(7)    VALUE ZERO.                     
       01  WS-F59A-WRITE            PIC 9(7)    VALUE ZERO.                     
       01  WS-F59B-WRITE            PIC 9(7)    VALUE ZERO.                     
       01  WS-F593-NOTMATCH         PIC 9(7)    VALUE ZERO.                     
       01  WS-F593-MATCH            PIC 9(7)    VALUE ZERO.                     
       01  WS-F800-NOTMATCH         PIC 9(7)    VALUE ZERO.                     
       01  WS-EOF-F593              PIC X       VALUE 'N'.                      
       01  WS-EOF-F800              PIC X       VALUE 'N'.                      
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       1000-MAIN-LOGIC.                                                         
           PERFORM 2000-INITIALIZATION THRU 2000-EXIT.                          
           PERFORM 3000-READ-F593 THRU 3000-EXIT.                               
           PERFORM 4000-READ-F800 THRU 4000-EXIT.                               
           PERFORM 5000-PROCESS   THRU 5000-EXIT                                
              UNTIL F800-NUM-NRIC = HIGH-VALUES                                 
               AND  F593-NUM-NRIC = HIGH-VALUES.                                
                                                                                
           PERFORM 9000-FINALIZATION THRU 9000-EXIT.                            
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2000-INITIALIZATION.                                                     
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F800                                                 
                       BP13F593                                                 
                OUTPUT P13F593A                                                 
                       P13F593B.                                                
                                                                                
           MOVE 'N' TO WS-EOF-F593 WS-EOF-F800.                                 
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3000-READ-F593.                                                          
      *-------------------------------------------------------------            
           READ BP13F593 AT END MOVE 'Y' TO WS-EOF-F593                         
                                MOVE HIGH-VALUES TO F593-NUM-NRIC               
               GO TO 3000-EXIT.                                                 
           ADD 1 TO WS-F593-READ.                                               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4000-READ-F800.                                                          
      *-------------------------------------------------------------            
           READ BP13F800 AT END MOVE 'Y' TO WS-EOF-F800                         
                                MOVE HIGH-VALUES TO F800-NUM-NRIC               
               GO TO 4000-EXIT.                                                 
           ADD 1 TO WS-F800-READ.                                               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-PROCESS.                                                            
      *-------------------------------------------------------------            
           IF F593-NUM-NRIC = F800-NUM-NRIC                                     
              PERFORM 7000-PROCESS-MATCHED THRU 7000-EXIT                       
              PERFORM 3000-READ-F593       THRU 3000-EXIT                       
              PERFORM 4000-READ-F800       THRU 4000-EXIT                       
           ELSE                                                                 
                                                                                
           IF F593-NUM-NRIC  <  F800-NUM-NRIC                                   
              ADD 1 TO WS-F593-NOTMATCH                                         
              PERFORM 6000-PROCESS-UNMATCHED THRU 6000-EXIT                     
              PERFORM 3000-READ-F593 THRU 3000-EXIT                             
           ELSE                                                                 
              ADD 1 TO WS-F800-NOTMATCH                                         
              PERFORM 4000-READ-F800         THRU 4000-EXIT                     
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-PROCESS-UNMATCHED.                                                  
      *-------------------------------------------------------------            
           MOVE BP13F593-REC    TO WS-F593A-REC.                                
           WRITE                   WS-F593A-REC.                                
           ADD   1              TO WS-F593A-WRITE.                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       7000-PROCESS-MATCHED.                                                    
      *-------------------------------------------------------------            
                                                                                
           IF F593-DTE-LAST-QTR   = F800-DTE-BALLOT                             
               MOVE F800-NUM-REGN     TO F593-NUM-SUCC-REGN                     
               MOVE F800-DTE-BALLOT   TO F593-DTE-SUCC-BALLOT                   
               MOVE F800-NUM-ALLO-CAT TO F593-NUM-SUCC-ALLO-CAT                 
           END-IF.                                                              
                                                                                
           MOVE BP13F593-REC    TO WS-F593B-REC.                                
           WRITE                   WS-F593B-REC.                                
           ADD   1              TO WS-F593B-WRITE.                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9000-FINALIZATION.                                                       
      *-------------------------------------------------------------            
           DISPLAY '*------- BP13CB28 -------*'.                                
           DISPLAY ' F593-READ     : ' WS-F593-READ.                            
           DISPLAY ' F800-READ     : ' WS-F800-READ.                            
           DISPLAY ' F593A NOTMACT : ' WS-F593-NOTMATCH.                        
           DISPLAY ' F593B MATCH   : ' WS-F593-MATCH.                           
           DISPLAY ' F593A WRITTEN : ' WS-F59A-WRITE.                           
           DISPLAY ' F593B WRITTEN : ' WS-F59B-WRITE.                           
                                                                                
           CLOSE      BP13F800                                                  
                      BP13F593                                                  
                      P13F593A                                                  
                      P13F593B.                                                 
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
