       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CL04.                                                 
      *AUTHOR.        BALA AMUTHAN.                                             
      *DATE-WRITTEN.  16/08/2006.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT  (SOC)                                 *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE  :                                               *           
      *    1. NUMBER OF DBSS LETTERS OF OFFER RAISED.               *           
      *                                                             *           
      *  INPUT FILES:  1. BP13LC01 (INFO FOR OFFER LETTERS)         *           
      *                                                             *           
      *     REPORT  :  1. BP13LL04 (DBSS LETTER OF OFFER)           *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  BY   ON      DESCRIPTION                           *           
      * -------- --   --      -----------                           *           
      * BP132873 BA2  160806  NEW PROGRAM                           *           
      * BP132838 BA2  151206  CHECK DTE-OFFER-PRINTED-TAG           *           
      * BP133196 RB12 270808  ADD CA OIC IN REPORT                  *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13LC01 ASSIGN TO BP13LC01.                                  
                                                                                
           SELECT BP13LL04 ASSIGN TO BP13LL04.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13LC01                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13LC01.                                                           
                                                                                
       FD  BP13LL04                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  PRINT-REC           PIC X(132).                                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
      *---------------------------------------------------------------*         
      *       SWITCHES    VARIABLE.                                   *         
      *---------------------------------------------------------------*         
       01  WS-SWITCH.                                                           
           05  WS-LC01-END           PIC X(1)       VALUE 'N'.                  
               88 CS-LC01-END                       VALUE 'Y'.                  
                                                                                
      *---------------------------------------------------------------*         
      *       RECORD-COUNT VARIABLE.                                  *         
      *---------------------------------------------------------------*         
       01  WS-COUNT.                                                            
           05  WS-LC01-READ          PIC 9(5) COMP  VALUE ZEROS.                
           05  WS-L04A-WRITE         PIC 9(5) COMP  VALUE ZEROS.                
                                                                                
      *---------------------------------------------------------------*         
      *       OTHER VARIABLES.                                        *         
      *---------------------------------------------------------------*         
       01  WS-DATE.                                                             
           05 WS-DATE-CHAR           PIC X(8).                                  
           05 WS-DATE-NUM REDEFINES WS-DATE-CHAR                                
                                     PIC 9(8).                                  
                                                                                
       01  WS-UNIT-INFO.                                                        
           05 LVL-NO                 PIC X(02).                                 
           05 FILLER                 PIC X(01) VALUE '-'.                       
           05 MAIN-NO                PIC X(04).                                 
           05 WS-SLASH               PIC X(01) VALUE '-'.                       
           05 SUB-NO                 PIC X(03).                                 
                                                                                
       01  WS-PREV-VALUES.                                                      
           05 WS-PREV-PRJCT          PIC X(03) VALUE SPACES.                    
           05 WS-PREV-FLAT           PIC X(02) VALUE SPACES.                    
                                                                                
       77  WS-LINE-CNT               PIC 9(2)      VALUE 60.                    
       77  WS-PAGE-NO                PIC 9(4) COMP VALUE ZEROS.                 
       77  WS-SNO                    PIC 9(4) COMP VALUE ZEROS.                 
       77  WS-CNT                    PIC 9(1)      VALUE ZEROS.                 
                                                                                
      *---------------------------------------------------------------*         
      *       HEADINGS FOR REPORT                                     *         
      *---------------------------------------------------------------*         
       01  CL04-PR-HEAD-01.                                                     
           05  FILLER                PIC X(8)  VALUE 'BP13LL04'.                
           05  FILLER                PIC X(4)  VALUE SPACES.                    
           05  FILLER                PIC X(8)  VALUE 'HDB     '.                
           05  FILLER                PIC X(24) VALUE SPACES.                    
           05  FILLER                PIC X(20) VALUE                            
                                      'S Y S T E M   O F   '.                   
           05  FILLER                PIC X(19) VALUE                            
                                       'C O M M I T M E N T'.                   
           05  FILLER                PIC X(15) VALUE SPACES.                    
           05  FILLER                PIC X(7)  VALUE 'DATE :'.                  
           05  CL04-DATE             PIC X(10) VALUE SPACES.                    
           05  FILLER                PIC X(4)  VALUE SPACES.                    
           05  FILLER                PIC X(7)  VALUE 'PAGE : '.                 
           05  CL04-PAGENO           PIC ZZZ9 .                                 
                                                                                
       01  CL04-PR-HEAD-02.                                                     
           05  FILLER                PIC X(44)   VALUE SPACES.                  
           05  FILLER                PIC X(38)   VALUE                          
               'NUMBER OF DBSS LETTERS OF OFFER RAISED'.                        
           05  FILLER                PIC X(50)   VALUE SPACES.                  
                                                                                
       01  CL04-PR-HEAD-03.                                                     
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(15)   VALUE                          
               'PROJECT      : '.                                               
           05  CL04-PRJ              PIC X(30)   VALUE SPACES.                  
           05  FILLER                PIC X(85)   VALUE SPACES.                  
                                                                                
       01  CL04-PR-HEAD-04.                                                     
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(15)   VALUE                          
               'FLAT TYPE    : '.                                               
           05  CL04-FLT-TYP          PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(113)  VALUE SPACES.                  
                                                                                
       01  CL04-PR-HEAD-05.                                                     
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(04)   VALUE                          
               'S/NO'.                                                          
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(25)   VALUE                          
               'DATE LETTER OF OFFER SENT'.                                     
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(22)   VALUE                          
               'ACCEPTANCE LETTER DATE'.                                        
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(16)   VALUE                          
               'HLE APPROVE DATE'.                                              
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(10)   VALUE                          
               'NO OF DAYS'.                                                    
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(10)   VALUE                          
               'HLE REF NO'.                                                    
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(11)   VALUE                          
               'DBSS REF NO'.                                                   
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(10)   VALUE                          
               'HDB REF NO'.                                                    
           05  FILLER                PIC X(34)   VALUE SPACES.                  
                                                                                
       01  CL04-PR-HEAD-05A.                                                    
           05  FILLER                PIC X(77)   VALUE SPACES.                  
           05  FILLER                PIC X(07)   VALUE                          
               'OVERDUE'.                                                       
                                                                                
       01  CL04-PR-HEAD-06.                                                     
           05  FILLER                PIC X(08)   VALUE SPACES.                  
           05  FILLER                PIC X(07)   VALUE                          
               'ADDRESS'.                                                       
           05  FILLER                PIC X(86)   VALUE SPACES.                  
           05  FILLER                PIC X(06)   VALUE                          
               'CA OIC'.                                                        
           05  FILLER                PIC X(25)   VALUE SPACES.                  
                                                                                
       01  CL04-PR-HEAD-07.                                                     
           05  FILLER                PIC X(08)   VALUE SPACES.                  
           05  FILLER                PIC X(04)   VALUE                          
               'NRIC'.                                                          
           05  FILLER                PIC X(10)   VALUE SPACES.                  
           05  FILLER                PIC X(04)   VALUE                          
               'NAME'.                                                          
           05  FILLER                PIC X(106)  VALUE SPACES.                  
                                                                                
       01  CL04-PR-HEAD-08.                                                     
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  CL04-SNO              PIC ZZZ9    VALUE SPACES.                  
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  CL04-LTR-OFFER-DTE    PIC X(10)   VALUE SPACES.                  
           05  FILLER                PIC X(17)   VALUE SPACES.                  
           05  CL04-LTR-ACCPT-DTE    PIC X(10)   VALUE SPACES.                  
           05  FILLER                PIC X(14)   VALUE SPACES.                  
           05  CL04-DOC-APPR-DTE     PIC X(12)   VALUE SPACES.                  
           05  FILLER                PIC X(05)   VALUE SPACES.                  
           05  CL04-OVER-DUE-DAY     PIC Z(3)9   VALUE SPACES.                  
           05  FILLER                PIC X(09)   VALUE SPACES.                  
           05  CL04-HLE-REF-NO       PIC X(09)   VALUE SPACES.                  
           05  FILLER                PIC X(03)   VALUE SPACES.                  
           05  CL04-DBSS-REF-NO      PIC X(08)   VALUE SPACES.                  
           05  FILLER                PIC X(04)   VALUE SPACES.                  
           05  CL04-SCH-ACC          PIC X(14)   VALUE SPACES.                  
           05  FILLER                PIC X(29)   VALUE SPACES.                  
                                                                                
       01  CL04-PR-HEAD-09.                                                     
           05  FILLER                PIC X(08)   VALUE SPACES.                  
           05  CL04-ADDRESS          PIC X(60)   VALUE SPACES.                  
           05  FILLER                PIC X(33)   VALUE SPACES.                  
           05  CL04-CA-OIC           PIC X(05)   VALUE SPACES.                  
           05  FILLER                PIC X(26)   VALUE SPACES.                  
                                                                                
       01  CL04-PR-HEAD-10.                                                     
           05  FILLER                PIC X(08)   VALUE SPACES.                  
           05  CL04-NRIC             PIC X(09)   VALUE SPACES.                  
                                                                                
           05  FILLER                PIC X(05)   VALUE SPACES.                  
           05  CL04-NAME             PIC X(66)   VALUE SPACES.                  
           05  FILLER                PIC X(43)   VALUE SPACES.                  
                                                                                
       01  CL04-PR-HEAD-ROLL.                                                   
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FLLER                 PIC X(123)  VALUE ALL '-'.                 
           05  FILLER                PIC X(07)   VALUE SPACES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPENING-ROUTINE THRU 1000-EXIT.                         
           PERFORM 2000-READ-LC01       THRU 2000-EXIT.                         
           PERFORM 3000-MAIN-PROCESS   THRU 3000-EXIT                           
                   UNTIL CS-LC01-END.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT BP13LC01                                                  
               OUTPUT BP13LL04.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE-CHAR(7:2) '/' WS-DATE-CHAR(5:2) '/'                   
                  WS-DATE(1:4) DELIMITED BY SIZE INTO CL04-DATE.                
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-LC01.                                                          
      *-------------------------------------------------------------            
           READ BP13LC01 AT END MOVE 'Y' TO WS-LC01-END                         
                                GO TO 2000-EXIT.                                
           ADD 1 TO WS-LC01-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF LC01-DTE-OFFER-LTR-TAG = 'Y'                                      
              PERFORM 3100-MOVE-DETAIL-PRINT   THRU 3100-EXIT                   
                                                                                
              PERFORM 3200-MOVE-ADDRES-PRINT   THRU 3200-EXIT                   
                                                                                
              PERFORM 3300-MOVE-NAME-IC-PRINT  THRU 3300-EXIT                   
                      VARYING WS-CNT FROM 1 BY 1 UNTIL WS-CNT > 4 OR            
                      LC01-NUM-NRIC(WS-CNT) = SPACES OR LOW-VALUES OR           
                                                              ZEROES            
              MOVE LC01-KEY-FLD                TO WS-PREV-VALUES                
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-LC01           THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-MOVE-DETAIL-PRINT.                                                  
      *-------------------------------------------------------------            
           ADD 1                         TO WS-SNO.                             
           MOVE  WS-SNO                  TO CL04-SNO.                           
                                                                                
           MOVE LC01-NME-PRJCT           TO CL04-PRJ.                           
           MOVE LC01-NUM-FT              TO CL04-FLT-TYP.                       
                                                                                
           IF LC01-DTE-OFFER-LTR-SENT NOT = SPACES AND ZEROES AND               
                                                   LOW-VALUES                   
              STRING LC01-DTE-OFFER-LTR-SENT(7:2) '/'                           
                     LC01-DTE-OFFER-LTR-SENT(5:2) '/'                           
                     LC01-DTE-OFFER-LTR-SENT(1:4)                               
                     DELIMITED BY SIZE INTO CL04-LTR-OFFER-DTE                  
           ELSE                                                                 
              MOVE SPACES                TO CL04-LTR-OFFER-DTE                  
           END-IF.                                                              
                                                                                
           IF LC01-DTE-OFFER-LTR-ACCEPT NOT = SPACES AND ZEROES AND             
                                                   LOW-VALUES                   
              STRING LC01-DTE-OFFER-LTR-ACCEPT(7:2) '/'                         
                     LC01-DTE-OFFER-LTR-ACCEPT(5:2) '/'                         
                     LC01-DTE-OFFER-LTR-ACCEPT(1:4)                             
                     DELIMITED BY SIZE INTO CL04-LTR-ACCPT-DTE                  
           ELSE                                                                 
              MOVE SPACES                TO CL04-LTR-ACCPT-DTE                  
           END-IF.                                                              
                                                                                
           IF LC01-DTE-HLE-APPROVAL NOT = SPACES AND ZEROES AND                 
                                          LOW-VALUES                            
              STRING LC01-DTE-HLE-APPROVAL(7:2) '/'                             
                     LC01-DTE-HLE-APPROVAL(5:2) '/'                             
                     LC01-DTE-HLE-APPROVAL(1:4)                                 
                     DELIMITED BY SIZE INTO CL04-DOC-APPR-DTE                   
           ELSE                                                                 
              MOVE SPACES                TO CL04-DOC-APPR-DTE                   
           END-IF.                                                              
                                                                                
           MOVE LC01-NUM-DAYS-OVERDUE4   TO CL04-OVER-DUE-DAY.                  
           MOVE LC01-NUM-HLE             TO CL04-HLE-REF-NO.                    
           MOVE LC01-NUM-REGN            TO CL04-DBSS-REF-NO.                   
                                                                                
           IF LC01-NUM-HDB-REF NOT = SPACES AND LOW-VALUES AND ZEROES           
              STRING LC01-NUM-HDB-REF(1:4) '-'                                  
                     LC01-NUM-HDB-REF(5:4) '-'                                  
                     LC01-NUM-HDB-REF(9:1) '-'                                  
                     LC01-NUM-HDB-REF(10:2)                                     
                     DELIMITED BY SIZE INTO CL04-SCH-ACC                        
           ELSE                                                                 
              MOVE SPACES                TO CL04-SCH-ACC                        
           END-IF.                                                              
                                                                                
           PERFORM    6000-HEAD-CHECK-WRITE THRU 6000-EXIT.                     
           MOVE CL04-PR-HEAD-08          TO PRINT-REC.                          
           PERFORM    6100-WRITE-REPORT  THRU 6100-EXIT.                        
           ADD +1                        TO WS-L04A-WRITE.                      
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-MOVE-ADDRES-PRINT.                                                  
      *-------------------------------------------------------------            
           MOVE LC01-NUM-LEVEL         TO   LVL-NO.                             
           MOVE LC01-NUM-MAIN-UNIT     TO   MAIN-NO.                            
           IF LC01-NUM-SUB-UNIT = SPACES OR LOW-VALUES                          
              MOVE SPACES              TO   WS-SLASH                            
                                            SUB-NO                              
           ELSE                                                                 
              MOVE LC01-NUM-SUB-UNIT   TO   SUB-NO                              
              MOVE '-'                 TO   WS-SLASH                            
           END-IF.                                                              
                                                                                
           STRING  'BLK ' LC01-NUM-BLK                                          
                   ' # '  WS-UNIT-INFO                                          
                   '   '  LC01-NME-STREET                                       
                     DELIMITED BY SIZE INTO CL04-ADDRESS.                       
                                                                                
           MOVE LC01-NUM-OIC-CA          TO CL04-CA-OIC.                        
                                                                                
           MOVE CL04-PR-HEAD-09          TO PRINT-REC.                          
           PERFORM    6100-WRITE-REPORT  THRU 6100-EXIT.                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3300-MOVE-NAME-IC-PRINT.                                                 
      *-------------------------------------------------------------            
           MOVE LC01-NUM-NRIC(WS-CNT)    TO CL04-NRIC.                          
           MOVE LC01-NME-HA(WS-CNT)      TO CL04-NAME.                          
           MOVE CL04-PR-HEAD-10          TO PRINT-REC.                          
           PERFORM    6100-WRITE-REPORT  THRU 6100-EXIT.                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-HEAD-CHECK-WRITE.                                                   
      *-------------------------------------------------------------            
           IF WS-LINE-CNT > 51                                                  
              PERFORM 6200-WRITE-HEAD  THRU 6200-EXIT                           
           ELSE                                                                 
              IF WS-PREV-VALUES NOT = LC01-KEY-FLD                              
                 PERFORM 6200-WRITE-HEAD  THRU 6200-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6100-WRITE-REPORT.                                                       
      *-------------------------------------------------------------            
           WRITE   PRINT-REC.                                                   
           ADD +1                  TO WS-LINE-CNT.                              
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6200-WRITE-HEAD.                                                         
      *-------------------------------------------------------------            
           ADD +1                  TO WS-PAGE-NO.                               
           MOVE WS-PAGE-NO         TO CL04-PAGENO.                              
                                                                                
           MOVE CL04-PR-HEAD-01    TO PRINT-REC.                                
           WRITE   PRINT-REC AFTER PAGE.                                        
                                                                                
           MOVE CL04-PR-HEAD-02    TO PRINT-REC.                                
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE CL04-PR-HEAD-03    TO PRINT-REC.                                
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE CL04-PR-HEAD-04    TO PRINT-REC.                                
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE CL04-PR-HEAD-05    TO PRINT-REC.                                
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE CL04-PR-HEAD-05A   TO PRINT-REC.                                
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE CL04-PR-HEAD-06    TO PRINT-REC.                                
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE CL04-PR-HEAD-07    TO PRINT-REC.                                
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE CL04-PR-HEAD-ROLL  TO PRINT-REC.                                
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE 09                 TO WS-LINE-CNT.                              
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           IF WS-L04A-WRITE <= 0                                                
              PERFORM 6200-WRITE-HEAD   THRU 6200-EXIT                          
              MOVE 'NO RECORDS FOUND'   TO   PRINT-REC                          
              PERFORM 6100-WRITE-REPORT THRU 6100-EXIT                          
           END-IF.                                                              
                                                                                
           DISPLAY '------- BP13CL04 -------------'.                            
           DISPLAY 'NO OF LC01 READ    (BP13LC01) : '                           
                                               WS-LC01-READ.                    
           DISPLAY 'NO OF L04A WRITTEN (BP13LL04) : '                           
                                               WS-L04A-WRITE.                   
                                                                                
           CLOSE BP13LC01                                                       
                 BP13LL04.                                                      
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
