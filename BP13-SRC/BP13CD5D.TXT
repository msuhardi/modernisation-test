       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD5D.                                                 
       AUTHOR.        JGO1.                                                     
       DATE-WRITTEN.  16 JAN 2015.                                              
      ************************************************************              
      *   SYSTEM NAME : SOC                                      *              
      *                                                          *              
      *   OBJECTIVE:                                             *              
      *      GENERATE INTERFACE FILE TO DU04 ON CASES WITH SALES *              
      *      ORDER APPOINTMENT DATE LATER THAN CURRENT DATE      *              
      *                                                          *              
      * CHG-NO  BY   ON       DESCRIPTION                        *              
      *-------  ---  ------   ----------                         *              
      *BP135610 JGO1 16/01/15 INITIAL CODING                     *              
      *BP135601 JGO1 27/01/15 PASS UNIT ID                       *              
      *BP135610 JGO1 27/01/15 MOVE 00010101 TO FQ10-DTE-KEY-CLCT *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN TO BP13F800.                                 
           SELECT SY02F001  ASSIGN TO SY02F001.                                 
           SELECT DU04FQ10  ASSIGN TO DU04FQ10.                                 
                                                                                
           SELECT BP13K435  ASSIGN BP13K435                                     
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE IS DYNAMIC                              
                            RECORD KEY IS K435-KEY-FLD                          
                            FILE STATUS IS WS-K435-STATUS.                      
                                                                                
           SELECT BP13K431  ASSIGN BP13K431                                     
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE IS RANDOM                               
                            RECORD KEY IS K431-KEY-FLD                          
                            FILE STATUS IS WS-K431-STATUS.                      
                                                                                
           SELECT BM06K510  ASSIGN BM06K510                                     
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE IS RANDOM                               
                            RECORD KEY IS K510-ESTCDE                           
                            FILE STATUS IS WS-K510-STATUS.                      
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K435                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K435.                                                           
                                                                                
       FD   BP13K431                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K431.                                                           
                                                                                
       FD   BM06K510                                                            
            RECORD CONTAINS  60 CHARACTERS.                                     
       COPY BM06K510.                                                           
                                                                                
       FD   DU04FQ10                                                            
            RECORD CONTAINS 600 CHARACTERS.                                     
       COPY DU04FQ10.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
      *-------------------------                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------                                                
       01  WS-FIRST-READ                PIC X      VALUE 'Y'.                   
       01  WS-FLAGS.                                                            
           05  WS-F800-EOF              PIC X(01) VALUE SPACE.                  
           05  WS-F001-EOF              PIC X(01) VALUE SPACE.                  
               88 EOF-F001                            VALUE 'Y'.                
           05  WS-K435-EOF              PIC X(01) VALUE SPACE.                  
               88 EOF-K435                            VALUE 'Y'.                
           05  WS-K435-FND              PIC X(01) VALUE SPACE.                  
               88 FND-K435                            VALUE 'Y'.                
           05  WS-K435-STATUS           PIC 9(02) VALUE ZEROES.                 
           05  WS-K431-STATUS           PIC 9(02) VALUE ZEROES.                 
           05  WS-K510-STATUS           PIC 9(02) VALUE ZEROES.                 
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY                PIC 9(4).                             
           05  WS-SYS-MM                  PIC 9(2).                             
           05  WS-SYS-DD                  PIC 9(2).                             
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD               PIC 9(2).                             
           05  FILLER                     PIC X(1)  VALUE '/'.                  
           05  WS-EDITED-MM               PIC 9(2).                             
           05  FILLER                     PIC X(1)  VALUE '/'.                  
           05  WS-EDITED-CCYY             PIC 9(4) VALUE 0.                     
                                                                                
       01  WS-NUM-HSEMAIN.                                                      
           05  WS-NUM-HSE-MAIN         PIC X(4).                                
           05  WS-NUM-HSE-SUB.                                                  
               10  WS-NUM-HSE-SUB1     PIC X.                                   
               10  WS-NUM-HSE-SUB2     PIC XX.                                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-PIDB-FND         PIC 9(5)    VALUE 0.                     
           05  WS-CNT-PIDB-NOTFND      PIC 9(5)    VALUE 0.                     
           05  WS-CNT-F800-READ        PIC 9(5)    VALUE 0.                     
           05  WS-CNT-F800-W-APPT      PIC 9(5)    VALUE 0.                     
           05  WS-CNT-FQ10-WRITE       PIC 9(5)    VALUE 0.                     
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13C913                        *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH     PIC X(4).                                
               10  WS-LINK-NUM-ACC     PIC X(5).                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *---------------------------------------------------------------*         
      *        LINKAGE FOR BP13C916                                   *         
      *---------------------------------------------------------------*         
       01  WS-LINK-REC-C916.                                                    
           05  WS-LINK-BLDNG-GL             PIC X(5).                           
                                                                                
       01  WS-BP13C916-REC.                                                     
           05  C916-NME-LOCAL-PRCNCT        PIC X(40).                          
           05  C916-CDE-SYSERR              PIC S9(3).                          
           05  C916-CDE-ERROR               PIC X(5).                           
                                                                                
                                                                                
      *-------------------------                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------                                                
       0000-MAIN.                                                               
                                                                                
            PERFORM 1000-INITIALIZE     THRU 1000-EXIT.                         
            PERFORM 2000-PROCESS        THRU 2000-EXIT                          
                    UNTIL WS-F800-EOF = 'Y'                                     
            PERFORM 9999-CLOSING        THRU 9999-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
            OPEN INPUT  BP13F800                                                
                        SY02F001                                                
                        BP13K435                                                
                        BM06K510                                                
                        BP13K431                                                
            OPEN OUTPUT DU04FQ10.                                               
                                                                                
            READ SY02F001 AT END MOVE 'Y'  TO WS-F001-EOF.                      
            IF EOF-F001                                                         
               DISPLAY 'RECORD NOT FOUND IN SY01F001 '                          
               DISPLAY 'PROCESSING TERMINATED '                                 
               PERFORM 9999-CLOSING       THRU 9999-EXIT                        
            ELSE                                                                
               MOVE F001-DTE-CURRENT      TO   WS-SYSTEM-DATE                   
            END-IF.                                                             
                                                                                
            IF WS-K435-STATUS NOT = 00 AND 97                                   
               DISPLAY 'BP13K435 - OPEN ERROR (' WS-K435-STATUS ')'             
               PERFORM 9999-CLOSING       THRU 9999-EXIT                        
            END-IF.                                                             
                                                                                
            IF WS-K431-STATUS NOT = 00 AND 97                                   
               DISPLAY 'BP13K431 - OPEN ERROR (' WS-K431-STATUS ')'             
               PERFORM 9999-CLOSING       THRU 9999-EXIT                        
            END-IF.                                                             
                                                                                
            IF WS-K510-STATUS NOT = 00 AND 97                                   
               DISPLAY 'BM06K510 - OPEN ERROR (' WS-K510-STATUS ')'             
               PERFORM 9999-CLOSING       THRU 9999-EXIT                        
            END-IF.                                                             
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1500-READ-F800.                                                          
                                                                                
            READ BP13F800                                                       
                 AT END MOVE 'Y' TO WS-F800-EOF                                 
                 GO TO 2000-EXIT.                                               
            ADD 1 TO WS-CNT-F800-READ.                                          
                                                                                
       1500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2000-PROCESS.                                                            
                                                                                
            INITIALIZE DU04FQ10-REC.                                            
            MOVE SPACES TO DU04FQ10-REC.                                        
            PERFORM 1500-READ-F800       THRU 1500-EXIT.                        
            IF F800-DTE-SO-APPT > WS-SYSTEM-DATE                                
               ADD 1 TO WS-CNT-F800-W-APPT                                      
               IF F800-NUM-SCH-ACC NOT = SPACES AND ZEROES                      
                                         AND LOW-VALUE                          
                  PERFORM 3000-GET-PIDB        THRU 3000-EXIT                   
                  PERFORM 4000-GET-KEY-INFO    THRU 4000-EXIT                   
               END-IF                                                           
               PERFORM 5000-WRITE-DETAIL    THRU 5000-EXIT                      
            END-IF.                                                             
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3000-GET-PIDB.                                                           
                                                                                
            MOVE  SPACES                  TO  BP13COMM8-REC.                    
            INITIALIZE                        BP13COMM8-REC.                    
            MOVE  F800-NUM-SCH-ACC(1:9)   TO  WS-LINK-REC.                      
            CALL 'BP13C913'     USING  WS-LINK-REC,                             
                                        BP13COMM8-REC.                          
            IF COMM8-CDE-SYSERR = 0                                             
               ADD 1 TO WS-CNT-PIDB-FND                                         
               PERFORM 3500-MOVE-PIDB-DETLS THRU 3500-EXIT                      
            ELSE                                                                
               ADD 1 TO WS-CNT-PIDB-NOTFND                                      
            END-IF.                                                             
                                                                                
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3500-MOVE-PIDB-DETLS.                                                    
                                                                                
            MOVE COMM8-NUM-BLDNG-GL      TO FQ10-NUM-BLDNG-GL.                  
            MOVE COMM8-UNIT-ID           TO FQ10-NUM-UNIT-ID.                   
            MOVE COMM8-NUM-BLK           TO FQ10-NUM-BLDNG.                     
            MOVE COMM8-NME-STREET        TO FQ10-NME-STREET.                    
            MOVE COMM8-NUM-LEVEL         TO FQ10-NUM-LEVEL.                     
            MOVE COMM8-NUM-UNIT-MAIN     TO WS-NUM-HSE-MAIN.                    
            MOVE COMM8-NUM-UNIT-SUB      TO WS-NUM-HSE-SUB.                     
            MOVE WS-NUM-HSEMAIN          TO FQ10-NUM-UNIT.                      
            MOVE COMM8-NUM-ESTATE-CIM    TO FQ10-NUM-ESTATE.                    
            MOVE COMM8-NUM-NGHBHD-CIM    TO FQ10-NUM-CNTRC-NHD-ORGL.            
            MOVE COMM8-NUM-CNTRCT-CIM    TO FQ10-NUM-CNTRC-ORGL.                
                                                                                
            MOVE COMM8-NUM-BLDNG-GL      TO WS-LINK-BLDNG-GL.                   
            CALL 'BP13C916' USING WS-LINK-REC-C916 WS-BP13C916-REC.             
            MOVE C916-NME-LOCAL-PRCNCT TO FQ10-NME-LOCAL-PRCNCT.                
                                                                                
            PERFORM 3600-READ-BM06K510   THRU 3600-EXIT.                        
                                                                                
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3600-READ-BM06K510.                                                      
                                                                                
           MOVE COMM8-NUM-ESTATE-CIM    TO K510-ESTCDE.                         
                                                                                
           READ BM06K510.                                                       
                                                                                
           IF WS-K510-STATUS = 00 OR 02                                         
              MOVE K510-ESTATE-NAME     TO FQ10-NME-ESTATE                      
           ELSE                                                                 
              IF WS-K510-STATUS = 23                                            
                 MOVE SPACES            TO FQ10-NME-ESTATE                      
              ELSE                                                              
                 DISPLAY 'BM06K510 - READ ERROR (' WS-K510-STATUS ')'           
                 PERFORM 9999-CLOSING        THRU 9999-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-GET-KEY-INFO.                                                       
                                                                                
            MOVE 'N'                    TO WS-K435-FND.                         
            MOVE F800-NUM-SCH-ACC       TO K435-NUM-HDB-REF.                    
            PERFORM 4100-START-BP13K435 THRU 4100-EXIT.                         
            IF NOT EOF-K435                                                     
               PERFORM 4200-READ-NEXT-BP13K435 THRU 4200-EXIT                   
               IF K435-NUM-HDB-REF = F800-NUM-SCH-ACC                           
                  MOVE K435-NUM-KEY-LOCN   TO FQ10-NUM-KEY-HLDR-GROUP           
                  MOVE K435-NUM-BORROWER   TO FQ10-NUM-KEY-HLDR-USERID          
                  MOVE 'Y'                 TO WS-K435-FND                       
                  PERFORM 4300-READ-BP13K431 THRU 4300-EXIT                     
               ELSE                                                             
                  MOVE 'Y'                 TO WS-K435-EOF                       
            END-IF.                                                             
            IF WS-K435-FND NOT = 'Y'                                            
                  MOVE 'EAPG'            TO  FQ10-NUM-KEY-HLDR-GROUP            
            END-IF.                                                             
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4100-START-BP13K435.                                                     
                                                                                
           MOVE 'N'              TO WS-K435-EOF.                                
                                                                                
           START BP13K435 KEY >= K435-NUM-HDB-REF.                              
                                                                                
           EVALUATE WS-K435-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'                   TO WS-K435-EOF                   
               WHEN OTHER                                                       
                    DISPLAY 'BP13K435 - START ERROR(' WS-K435-STATUS ')'        
                    MOVE WS-K435-STATUS        TO RETURN-CODE                   
                    PERFORM 9999-CLOSING        THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4200-READ-NEXT-BP13K435.                                                 
                                                                                
           READ BP13K435 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y' TO WS-K435-EOF                                         
           END-READ.                                                            
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4300-READ-BP13K431.                                                      
                                                                                
           MOVE K435-NUM-BORROWER  TO K431-NUM-BORROWER.                        
                                                                                
           READ BP13K431.                                                       
                                                                                
           IF WS-K431-STATUS = 00 OR 02                                         
              MOVE K431-NME-BORROWER    TO FQ10-NME-KEY-HLDR                    
           ELSE                                                                 
              IF WS-K431-STATUS = 23                                            
                 MOVE SPACES            TO FQ10-NME-KEY-HLDR                    
              ELSE                                                              
                 DISPLAY 'BP13K431 - READ ERROR (' WS-K431-STATUS ')'           
                 PERFORM 9999-CLOSING        THRU 9999-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-WRITE-DETAIL.                                                       
                                                                                
            MOVE WS-SYSTEM-DATE          TO FQ10-DTE-INTRFC.                    
            MOVE F800-DTE-SO-APPT        TO FQ10-DTE-APNMT.                     
            MOVE F800-NUM-NEW-TOWN       TO FQ10-NUM-CNTRC-EST-ORGL.            
            MOVE '00010101'              TO FQ10-DTE-KEY-CLCT.                  
            WRITE DU04FQ10-REC.                                                 
            ADD 1 TO WS-CNT-FQ10-WRITE.                                         
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       9999-CLOSING.                                                            
                                                                                
            DISPLAY '****************************'.                             
            DISPLAY '* F800 READ    : ' WS-CNT-F800-READ                        
            DISPLAY '* F800 W/ APPT : ' WS-CNT-F800-W-APPT                      
            DISPLAY '* FQ10 WRITTEN : ' WS-CNT-FQ10-WRITE                       
                                                                                
            CLOSE BP13F800                                                      
                  DU04FQ10                                                      
                  BM06K510                                                      
                  BP13K435                                                      
                  BP13K431                                                      
                  SY02F001.                                                     
                                                                                
            STOP RUN.                                                           
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
