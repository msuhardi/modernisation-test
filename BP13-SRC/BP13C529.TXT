       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C529.                                                 
      *AUTHOR.        MICHAEL ARRIOLA.                                          
      *DATE-WRITTEN.  13/11/2008.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                             
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: TO MATCH BP13KD15 AND BP13F205 BY NT-ZONE      *            
      *             & OUTPUT TO BP13F200.                          *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133403  MSD1  20081103  NEW PGM                          *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F205  ASSIGN        TO BP13F205.                          
           SELECT BP13KD15  ASSIGN        TO BP13KD15                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD15-KEY-FLD                       
                            FILE STATUS   IS BP13KD15-STATUS.                   
           SELECT BP13F200  ASSIGN        TO BP13F200.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F205                                                            
            RECORD CONTAINS  80  CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F205.                                                           
                                                                                
       FD   BP13KD15                                                            
            RECORD CONTAINS  300 CHARACTERS.                                    
       COPY BP13KD15.                                                           
                                                                                
       FD   BP13F200                                                            
            RECORD CONTAINS  300 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F200.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-FILE-STATUS.                                                       
          05 WS-KD15-EOF                  PIC X(1) VALUE 'N'.                   
          05 WS-KD15-FND                  PIC X(1) VALUE 'N'.                   
          05 BP13KD15-STATUS              PIC 9(2) VALUE ZEROES.                
       01 WS-COUNTERS.                                                          
          05 WS-F205-READ                 PIC 9(8) VALUE ZEROES.                
          05 WS-KD15-READ                 PIC 9(8) VALUE ZEROES.                
          05 WS-F200-WRITE                PIC 9(8) VALUE ZEROES.                
       01 WS-VARIABLES.                                                         
          05 WS-HHTY-QUEUE-SERIAL         PIC X(5).                             
          05 FILLER REDEFINES WS-HHTY-QUEUE-SERIAL.                             
             10 WS-NUM-HHTY-QUEUE-SERIAL  PIC 9(5).                             
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F205  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
              UNTIL F205-NUM-NT-ZONE = HIGH-VALUES.                             
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BP13KD15                                                 
               OUTPUT  BP13F200.                                                
                                                                                
           IF BP13KD15-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13KD15 : ' BP13KD15-STATUS             
              MOVE     BP13KD15-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F205.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F205 AT END                                                 
                MOVE  HIGH-VALUES  TO  F205-NUM-NT-ZONE                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1   TO WS-F205-READ.                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-START-BP13KD15.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES           TO  KD15-KEY-FLD.                              
           MOVE F205-NUM-NT-ZONE TO  KD15-NUM-PRJCT.                            
                                                                                
           START BP13KD15 KEY >= KD15-KEY-FLD.                                  
           EVALUATE BP13KD15-STATUS                                             
           WHEN 00                                                              
                CONTINUE                                                        
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y'         TO WS-KD15-EOF                                 
                DISPLAY 'BP13KD15 RECORD NOT FOUND  = ' KD15-KEY-FLD            
           WHEN OTHER                                                           
                DISPLAY 'ERROR START BP13KD15, STATUS ' BP13KD15-STATUS         
                        ',  NUM-PROJECT ' KD15-NUM-PRJCT                        
                MOVE BP13KD15-STATUS TO RETURN-CODE                             
                PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'             TO  WS-KD15-FND                                 
                                    WS-KD15-EOF.                                
                                                                                
           PERFORM 2100-START-BP13KD15 THRU 2100-EXIT.                          
                                                                                
           IF WS-KD15-EOF = 'N'                                                 
              MOVE ZEROES       TO  WS-NUM-HHTY-QUEUE-SERIAL                    
              PERFORM 3100-READNEXT-BP13KD15 THRU 3100-EXIT                     
                 UNTIL KD15-NUM-PRJCT NOT = F205-NUM-NT-ZONE                    
                    OR WS-KD15-EOF = 'Y'.                                       
                                                                                
           PERFORM 2000-READ-BP13F205 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-READNEXT-BP13KD15.                                                  
      *-------------------------------------------------------------            
                                                                                
           READ BP13KD15 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-KD15-EOF.                                 
                                                                                
           EVALUATE BP13KD15-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                IF KD15-NUM-PRJCT = F205-NUM-NT-ZONE                            
                   IF KD15-DTE-DBSS-CONFIRM(1:2) NOT = SPACES AND '##'          
                                                   AND LOW-VALUES               
                      PERFORM 4000-WRITE-BP13F200 THRU 4000-EXIT                
                   END-IF                                                       
                END-IF                                                          
              WHEN 10                                                           
              WHEN 23                                                           
                MOVE 'Y'         TO WS-KD15-EOF                                 
              WHEN OTHER                                                        
                MOVE BP13KD15-STATUS         TO RETURN-CODE                     
                DISPLAY 'READ ERROR BP13KD15, STATUS ' BP13KD15-STATUS          
                        ',  NUM-PROJECT ' KD15-NUM-PRJCT                        
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-WRITE-BP13F200.                                                     
      *-------------------------------------------------------------            
                                                                                
            MOVE 'Y'           TO WS-KD15-FND.                                  
            ADD              1 TO WS-KD15-READ.                                 
                                                                                
            MOVE SPACES        TO BP13F200-REC.                                 
            INITIALIZE            BP13F200-REC.                                 
                                                                                
            MOVE 'DBS'         TO F200-NUM-NT-ZONE.                             
            MOVE 'DBS'         TO F200-NUM-SELTYPE.                             
            MOVE KD15-NUM-REGN TO F200-NUM-REGN.                                
                                                                                
            ADD              1 TO WS-NUM-HHTY-QUEUE-SERIAL.                     
            MOVE WS-HHTY-QUEUE-SERIAL                                           
                               TO F200-NUM-HHTY-QUEUE-SERIAL.                   
                                                                                
            WRITE BP13F200-REC.                                                 
            ADD              1 TO WS-F200-WRITE.                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13C529 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ  FROM BP13F205 : ' WS-F205-READ.            
           DISPLAY 'NO OF RECS READ  FROM BP13KD15 : ' WS-KD15-READ.            
           DISPLAY 'NO OF RECS WRITTEN TO BP13F200 : ' WS-F200-WRITE.           
                                                                                
           CLOSE BP13F205                                                       
                 BP13KD15                                                       
                 BP13F200.                                                      
                                                                                
           IF BP13KD15-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13KD15 : ' BP13KD15-STATUS             
              MOVE BP13KD15-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
