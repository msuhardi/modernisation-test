      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.    BP13C133.                                                 
      *AUTHOR.        EG1.                                                      
      *DATE-WRITTEN.  28/03/96.                                                 
                                                                                
      ******************************************************************        
      *  PROGRAM NAME : BP13C133                                       *        
      *  OBJECTIVE    : 1. TO EXTRACT K885-RECORDS WITH CONFIRM FLAG = *        
      *                    'Y' AND ATTACHED THE CORRESPONDING FSA DATE *        
      *                    AND AGE.                                    *        
      *  INPUT/OUTPUT FILES :                                          *        
      *  ====================                                          *        
      *  TYPE  FILE NAME                   DD NAME   ACCESS    KEYLEN  *        
      *  ----  ---------                   -------   ------    ------  *        
      *  I     BP13K882                    BP13K882   KSDS.            *        
      *  I     BP13K885                    BP13K885   KSDS             *        
      *  O     BP13FOUT                    BP13FOUT   SEQ.             *        
      *================================================================*        
      * MODIFICATION HISTORY:                                          *        
      * ---------------------                                          *        
      * CHG REQ. AUTHOR  DATE     DESCRIPTIONS                         *        
      * =======  ====== =======   =====================================*        
      * BP130103 EG1    26031996  TO CREATE THE AGING FILE FOR PRINTING*        
      *                           OF AGEING REPORT FOR SUB-TRANSFER    *        
      *                           AT PROGRAM BP13C134.                 *        
      * BP130227 LJL    12051997  TO FILTER OUT SUSPENSE = ZEROS CASES.*        
      * BP130418 CLT    25081998  TO CATER FOR Y2K.                    *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      *================================================================*        
       CONFIGURATION SECTION.                                                   
      *================================================================*        
       SOURCE-COMPUTER.  IBM-4341.                                              
       OBJECT-COMPUTER.  IBM-4341.                                              
                                                                                
      *================================================================*        
       INPUT-OUTPUT SECTION.                                                    
      *================================================================*        
      *----------------------------------------------------------------*        
       FILE-CONTROL.                                                            
      *----------------------------------------------------------------*        
           SELECT BP13K885 ASSIGN TO BP13K885                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS          IS SEQUENTIAL                                 
                  RECORD KEY      IS K885-KEY-FLD                               
                  FILE STATUS     IS K885-STATUS.                               
                                                                                
           SELECT BP13K882 ASSIGN TO BP13K882                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS          IS DYNAMIC                                    
                  RECORD KEY      IS K882-KEY-FLD                               
                  FILE STATUS     IS K882-STATUS.                               
                                                                                
           SELECT BP13FOUT ASSIGN TO BP13FOUT.                                  
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *================================================================*        
       FILE SECTION.                                                            
      *================================================================*        
                                                                                
       FD   BP13K882                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K882.                                                           
                                                                                
       FD   BP13K885                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K885.                                                           
                                                                                
       FD   BP13FOUT                                                            
            LABEL RECORDS ARE OMITTED                                           
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 420 CHARACTERS                                      
            RECORDING MODE IS F.                                                
                                                                                
       01   FOUT-REC                    PIC X(420).                             
                                                                                
      *================================================================*        
       WORKING-STORAGE SECTION.                                                 
      *================================================================*        
      *----------------------------------------------------------------*        
      *    ERROR HANDLING VARIABLES                                    *        
      *----------------------------------------------------------------*        
       01 WS-ERR-MSG                     PIC X(80) VALUE SPACES.                
       01 WS-ERR-CDE                     PIC X(2)  VALUE SPACES.                
                                                                                
      *----------------------------------------------------------------*        
      *    CONSTANTS                                                   *        
      *----------------------------------------------------------------*        
       01 WS-MONTH-FILLER.                                                      
          05 FILLER                      PIC X(4) VALUE '0131'.                 
          05 FILLER                      PIC X(4) VALUE '0228'.                 
          05 FILLER                      PIC X(4) VALUE '0331'.                 
          05 FILLER                      PIC X(4) VALUE '0430'.                 
          05 FILLER                      PIC X(4) VALUE '0531'.                 
          05 FILLER                      PIC X(4) VALUE '0630'.                 
          05 FILLER                      PIC X(4) VALUE '0731'.                 
          05 FILLER                      PIC X(4) VALUE '0831'.                 
          05 FILLER                      PIC X(4) VALUE '0930'.                 
          05 FILLER                      PIC X(4) VALUE '1031'.                 
          05 FILLER                      PIC X(4) VALUE '1130'.                 
          05 FILLER                      PIC X(4) VALUE '1231'.                 
       01 WS-MONTH-TABLE REDEFINES WS-MONTH-FILLER.                             
          05 WS-MONTH-OCCURS  OCCURS 12 TIMES.                                  
             10 WS-MONTH                 PIC X(2).                              
             10 WS-MONTH-MAX-DAYS        PIC 9(2).                              
                                                                                
      *----------------------------------------------------------------*        
      *    FLAG VARIABLES                                              *        
      *----------------------------------------------------------------*        
       01 WS-FLAGS.                                                             
          05 WS-K885-VALID-FL            PIC X.                                 
          05 WS-K882-VALID-FL            PIC X.                                 
                                                                                
      *----------------------------------------------------------------*        
      *    FILE STATUS                                                 *        
      *----------------------------------------------------------------*        
       01 WS-FILE-STATUS-VAR.                                                   
          05 K800-STATUS                 PIC X(2).                              
          05 K885-STATUS                 PIC X(2).                              
          05 K882-STATUS                 PIC X(2).                              
                                                                                
       01 WS-EOF-STATUS-VAR.                                                    
          05 WS-K885-EOF                 PIC X(1) VALUE 'N'.                    
          05 WS-K882-EOF                 PIC X(1) VALUE 'N'.                    
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE AREA FOR COUNTER                            *        
      *----------------------------------------------------------------*        
       01  WS-COUNTERS.                                                         
           05 WS-CNT-K885-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-K882-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-WRITE-FOUT          PIC 9(07) VALUE 0.                     
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE AREA FOR DATE VARIABLES                     *        
      *----------------------------------------------------------------*        
       01  WS-CURRENT-DATE.                                                     
           05 WS-CURR-CC                 PIC X(02) VALUE SPACES.                
           05 WS-CURR-YYMMDD.                                                   
              10 WS-CURR-YY              PIC X(02) VALUE SPACES.                
              10 WS-CURR-MM              PIC X(02) VALUE SPACES.                
              10 WS-CURR-DD              PIC X(02) VALUE SPACES.                
                                                                                
       01  WS-COMP1-DATE.                                                       
           05 WS-COMP1-CCYY              PIC 9(4).                              
           05 WS-COMP1-MM                PIC 9(2).                              
           05 WS-COMP1-DD                PIC 9(2).                              
                                                                                
       01  WS-COMP2-DATE.                                                       
           05 WS-COMP2-CCYY              PIC 9(4).                              
           05 WS-COMP2-MM                PIC 9(2).                              
           05 WS-COMP2-DD                PIC 9(2).                              
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE AREA FOR COMPUTE VARIABLES.                 *        
      *----------------------------------------------------------------*        
       01  WS-COMPUTE-VARIABLES.                                                
           05 WS-DIFF-DAYS               PIC S9(2)    VALUE +0.                 
           05 WS-DIFF-MONTHS             PIC S9(4)    VALUE +0.                 
           05 WS-DIFF-YEARS              PIC S9(4)    VALUE +0.                 
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE AREA FOR MISC.  VARIABLES                   *        
      *----------------------------------------------------------------*        
       01  WS-FOUT-LAYOUT.                                                      
           05 WS-K885-REC                PIC X(400).                            
           05 WS-FSA-DATE                PIC X(08).                             
           05 WS-AGE-MONTHS              PIC 9(2).                              
           05 WS-AGE-DAYS                PIC 9(2).                              
           05 FILLER                     PIC X(8).                              
                                                                                
       01  WS-SAVE-VARIABLES.                                                   
           05 WS-SAVE-NUM-REGN           PIC X(08) VALUE SPACES.                
           05 WS-SAVE-FSA-DATE           PIC X(08) VALUE SPACES.                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *================================================================*        
       0000-MAIN-ROUTINE.                                                       
      *================================================================*        
                                                                                
           PERFORM 1000-INITIALIZATION      THRU 1000-EXIT.                     
           PERFORM 2000-PROCESS-ROUTINE     THRU 2000-EXIT                      
                   UNTIL WS-K885-EOF = 'Y'.                                     
           PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       1000-INITIALIZATION.                                                     
      *================================================================*        
           OPEN INPUT  BP13K882                                                 
                       BP13K885                                                 
                OUTPUT BP13FOUT.                                                
                                                                                
           IF K885-STATUS NOT EQUAL 00 AND 97                                   
              STRING 'BP13K885 OPENING ERROR (' K885-STATUS ')'                 
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
           IF K882-STATUS NOT EQUAL 00 AND 97                                   
              STRING 'BP13K882 OPENING ERROR (' K882-STATUS ')'                 
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
                                                                                
           READ  BP13K885 AT END                                                
                 MOVE 'Y'          TO  WS-K885-EOF.                             
                                                                                
           IF WS-K885-EOF = 'Y'                                                 
              STRING 'NO K885 RECORDS FOUND!'                                   
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
              PERFORM 9900-HANDLE-ABEND            THRU 9900-EXIT               
           ELSE                                                                 
              ADD 1                TO WS-CNT-K885-READ                          
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       2000-PROCESS-ROUTINE.                                                    
      *================================================================*        
           IF (K885-NUM-CONFIRM = 'Y'  AND                                      
              K885-AMT-SUSPENSE  NOT = ZEROES)                                  
              PERFORM 2100-GET-FSA-DATE         THRU 2100-EXIT                  
              MOVE    K885-NUM-REGN             TO WS-SAVE-NUM-REGN             
              PERFORM 2200-PROCESS-REGN-CASE    THRU 2200-EXIT                  
                UNTIL WS-K885-EOF = 'Y' OR                                      
                      K885-NUM-REGN NOT = WS-SAVE-NUM-REGN                      
           ELSE                                                                 
              READ  BP13K885 AT END                                             
                    MOVE 'Y'        TO WS-K885-EOF                              
                    GO  TO 2000-EXIT                                            
                    END-READ                                                    
              ADD   1   TO  WS-CNT-K885-READ                                    
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       2100-GET-FSA-DATE.                                                       
      *================================================================*        
           MOVE LOW-VALUES                TO K882-KEY-FLD.                      
           MOVE K885-NUM-REGN             TO K882-NUM-REGN-SUBTRF.              
           PERFORM 8100-START-BP13K882    THRU 8100-EXIT.                       
           PERFORM 8150-READNEXT-BP13K882 THRU 8150-EXIT                        
             UNTIL WS-K882-EOF = 'Y'                                            
                                                                                
           IF WS-SAVE-FSA-DATE NUMERIC                                          
              ADD     1                  TO   WS-CNT-K882-READ                  
              PERFORM 3000-COMPUTE-AGE   THRU 3000-EXIT                         
           ELSE                                                                 
              IF WS-SAVE-FSA-DATE = SPACES                                      
                 DISPLAY 'MISSING K882-REC OR INVALID FSA-DATE'                 
                         '(' K885-NUM-REGN ')'                                  
                 MOVE 'N'                TO WS-K882-VALID-FL                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       2200-PROCESS-REGN-CASE.                                                  
      *================================================================*        
           IF WS-K882-VALID-FL = 'Y'                                            
              MOVE SPACES                TO WS-FOUT-LAYOUT                      
              INITIALIZE WS-FOUT-LAYOUT                                         
              MOVE BP13K885-REC          TO WS-K885-REC                         
              MOVE WS-SAVE-FSA-DATE      TO WS-FSA-DATE                         
              MOVE WS-DIFF-MONTHS        TO WS-AGE-MONTHS                       
              MOVE WS-DIFF-DAYS          TO WS-AGE-DAYS                         
              PERFORM 4000-WRITE-FOUT    THRU 4000-EXIT                         
           END-IF.                                                              
                                                                                
           READ  BP13K885 AT END                                                
                 MOVE 'Y'        TO WS-K885-EOF                                 
                 GO  TO 2200-EXIT                                               
                 END-READ.                                                      
                                                                                
           ADD   1   TO  WS-CNT-K885-READ.                                      
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3000-COMPUTE-AGE.                                                        
      *================================================================*        
           MOVE WS-CURRENT-DATE            TO WS-COMP1-DATE.                    
           MOVE WS-SAVE-FSA-DATE           TO WS-COMP2-DATE.                    
           IF WS-COMP1-DATE >= WS-COMP2-DATE                                    
              COMPUTE WS-DIFF-YEARS  = WS-COMP1-CCYY - WS-COMP2-CCYY            
              IF WS-DIFF-YEARS > 0                                              
                 COMPUTE WS-DIFF-MONTHS = WS-COMP1-MM                           
                                        + (WS-DIFF-YEARS * 12)                  
                                        - WS-COMP2-MM                           
              ELSE                                                              
                 COMPUTE WS-DIFF-MONTHS = WS-COMP1-MM - WS-COMP2-MM             
              END-IF                                                            
              COMPUTE WS-DIFF-DAYS   = WS-COMP1-DD   - WS-COMP2-DD              
              IF WS-DIFF-DAYS < 0                                               
                 IF WS-COMP1-DD >= WS-MONTH-MAX-DAYS(WS-COMP1-MM)               
                   COMPUTE WS-DIFF-DAYS = WS-MONTH-MAX-DAYS(WS-COMP2-MM)        
                                        - WS-COMP2-DD                           
                 ELSE                                                           
                   SUBTRACT 1           FROM WS-DIFF-MONTHS                     
                   COMPUTE WS-DIFF-DAYS = WS-MONTH-MAX-DAYS(WS-COMP2-MM)        
                                        - WS-COMP2-DD + WS-COMP1-DD             
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'FSA DATE > CURRENT DATE (' K885-NUM-REGN ')'             
              INITIALIZE WS-DIFF-YEARS WS-DIFF-MONTHS WS-DIFF-DAYS              
              MOVE 'N'                     TO WS-K882-VALID-FL                  
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       4000-WRITE-FOUT.                                                         
      *================================================================*        
           WRITE FOUT-REC              FROM WS-FOUT-LAYOUT.                     
           ADD   1                     TO   WS-CNT-WRITE-FOUT.                  
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       8100-START-BP13K882.                                                     
      *================================================================*        
            MOVE 'Y'                 TO WS-K882-VALID-FL.                       
            MOVE 'N'                 TO WS-K882-EOF.                            
            MOVE SPACES              TO WS-SAVE-FSA-DATE.                       
            START BP13K882 KEY >= K882-KEY-FLD                                  
            INVALID KEY   MOVE 'Y'        TO WS-K882-EOF                        
                          MOVE 'N'        TO WS-K882-VALID-FL                   
                          DISPLAY 'MISSING K882 RECORD(' K885-NUM-REGN          
                                  ')'                                           
                          GO TO 8100-EXIT.                                      
                                                                                
            IF K882-STATUS NOT = '00'                                           
               STRING 'BP13K882 START ERROR CODE(' K882-STATUS ')'              
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       8150-READNEXT-BP13K882.                                                  
      *================================================================*        
            READ  BP13K882 NEXT                                                 
                  AT END  MOVE 'Y'        TO WS-K882-EOF                        
                          GO TO 8150-EXIT.                                      
                                                                                
            IF K882-STATUS NOT = '00'                                           
               STRING 'BP13K882 READ ERROR CODE(' K882-STATUS ')'               
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF K882-NUM-REGN-SUBTRF = K885-NUM-REGN                             
               MOVE K882-DTE-FSA-SIGNED   TO WS-SAVE-FSA-DATE                   
            ELSE                                                                
               MOVE 'Y'                   TO WS-K882-EOF                        
            END-IF.                                                             
                                                                                
       8150-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9900-HANDLE-ABEND.                                                       
      *================================================================*        
            CLOSE   BP13K885                                                    
                    BP13K882                                                    
                    BP13FOUT.                                                   
                                                                                
            DISPLAY '***************************************'                   
            DISPLAY '*    BP13C133  ABEND TOTAL            *'                   
            DISPLAY '***************************************'                   
            DISPLAY '* NO. OF K882 READ : ' WS-CNT-K882-READ.                   
            DISPLAY '* NO. OF K885 READ : ' WS-CNT-K885-READ.                   
            DISPLAY '* NO. OF VALID RECS: ' WS-CNT-WRITE-FOUT.                  
            DISPLAY '***************************************'                   
            DISPLAY '* ABEND DESC: ' WS-ERR-MSG.                                
            MOVE '4095'               TO RETURN-CODE.                           
                                                                                
            STOP RUN.                                                           
       9900-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9999-CLOSE-FILES.                                                        
      *================================================================*        
            CLOSE   BP13K885                                                    
                    BP13K882                                                    
                    BP13FOUT.                                                   
                                                                                
            DISPLAY '***************************************'                   
            DISPLAY '*    BP13C133  ABEND TOTAL            *'                   
            DISPLAY '***************************************'                   
            DISPLAY '* NO. OF K882 READ : ' WS-CNT-K882-READ.                   
            DISPLAY '* NO. OF K885 READ : ' WS-CNT-K885-READ.                   
            DISPLAY '* NO. OF VALID RECS: ' WS-CNT-WRITE-FOUT.                  
            DISPLAY '***************************************'                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
