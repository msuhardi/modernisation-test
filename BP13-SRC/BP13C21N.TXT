      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C21N.                                                 
      *AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
      *DATE-WRITTEN.  11 JUNE 2008.                                             
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : CONVERT F240 FILE FORMAT TO K249            *           
      *                 ADD RECORDS TO BP13K249                     *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13F240                                              *           
      *   2.  BP13K062                                              *           
      *                                                             *           
      *   INPUT/OUTPUT:                                             *           
      *   1.  BP13K249                                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP133367 02/07/2008 RB12 NEW PROGRAM                        *           
      * BP133367 22/10/2008 RB12 INCLUDE BE IN NUM LIMIT PROCESSING,*           
      *                          POPULATE NEW ALTERNATE KEY FIELDS, *           
      *                          ROUND UP NUM LIMIT                 *           
      * BP133736 28/10/2009 RB12 MOVE F240 NUM ZONE TO K249 FOR NPL *           
      * BP133933 03/08/2010 RB12 RETRIEVE PROJECT NAME FROM BP13K062*           
      * BP133978 13/08/2010 RB12 MOVE NUM BTO ZONE FROM K240 TO K249*           
      * BP134471 15/03/2012 ESA1 CATER FOR SPR QUOTA                *           
      * BP134853 15/05/2013 ESA1 CATER FOR UPDATE OF BP13K255       *           
      * BP135111 29/10/2013 ESA1 CATER FOR 3GEN                     *           
      * BP135135 14/11/2013 ESA1 CATER FOR BONUS FLATS              *           
      * BP135363 12/08/2014 ADD READING OF BP13K813 TO GET PROJNAME *           
      * BP135783 25/05/2015 ESA1 TO CATER FOR K249-CDE-NT           *           
      * BP136010 16/11/2015 ESA1 TO CATER FOR 4 BYTES AVAIL-CAT     *           
      * BP139545 31/10/2023 SC54 POLICY CHANGE STARTING FROM 202310 *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F240 ASSIGN       TO BP13F240.                            
                                                                                
           SELECT BP13K249 ASSIGN       TO BP13K249                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K249-KEY-FLD                         
                           FILE STATUS  IS WS-K249-STATUS                       
                           ACCESS MODE  IS DYNAMIC.                             
                                                                                
           SELECT BP13K255 ASSIGN       TO BP13K255                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K255-KEY-FLD                         
                           FILE STATUS  IS WS-K255-STATUS                       
                           ACCESS MODE  IS DYNAMIC.                             
                                                                                
           SELECT BP13K813 ASSIGN       TO BP13K813                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K813-KEY-FLD                         
                           FILE STATUS  IS WS-K813-STATUS                       
                           ACCESS MODE  IS DYNAMIC.                             
                                                                                
           SELECT BM06K510 ASSIGN       TO BM06K510                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K510-ESTCDE                          
                           ALTERNATE KEY IS K510-NT-NAME                        
                           FILE STATUS  IS WS-K510-STATUS                       
                           ACCESS MODE  IS DYNAMIC.                             
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F240                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13K240.                                                           
                                                                                
       FD   BP13K249                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K249.                                                           
                                                                                
       FD   BP13K255                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K255.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BM06K510                                                            
            RECORD CONTAINS 60 CHARACTERS.                                      
       COPY BM06K510.                                                           
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY         PIC 9999.                                    
           05  WS-CUR-MM           PIC 99.                                      
           05  WS-CUR-DD           PIC 99.                                      
                                                                                
       01  WS-K249-STATUS           PIC 9(02)  VALUE ZEROES.                    
       01  WS-K813-STATUS           PIC 9(02)  VALUE ZEROES.                    
       01  WS-K255-STATUS           PIC 9(02)  VALUE ZEROES.                    
       01  WS-K510-STATUS           PIC 9(02)  VALUE ZEROES.                    
       01  WS-K249-FND              PIC X      VALUE SPACES.                    
       01  WS-K813-FND              PIC X      VALUE SPACES.                    
       01  WS-K255-FND              PIC X      VALUE SPACES.                    
       01  WS-F240-EOF              PIC X      VALUE SPACES.                    
       01  WS-F240-READ-CNT         PIC 9(08)  VALUE ZEROES.                    
       01  WS-F240-READ-ERR         PIC 9(08)  VALUE ZEROES.                    
       01  WS-K249-WRITE-CNT        PIC 9(08)  VALUE ZEROES.                    
       01  WS-K249-WRITE-ERR        PIC 9(08)  VALUE ZEROES.                    
       01  WS-K255-WRITE-CNT        PIC 9(08)  VALUE ZEROES.                    
       01  WS-K255-WRITE-ERR        PIC 9(08)  VALUE ZEROES.                    
       01  WS-K249-REWRITE-CNT      PIC 9(08)  VALUE ZEROES.                    
       01  WS-K249-REWRITE-ERR      PIC 9(08)  VALUE ZEROES.                    
       01  WS-PREV-KEY.                                                         
           05  WS-NUM-NEW-TOWN      PIC X(20).                                  
           05  WS-NUM-FLAT-TYPE     PIC X(09).                                  
           05  WS-NUM-SELECTION     PIC X(03).                                  
           05  WS-DTE-BALLOT        PIC X(06).                                  
           05  WS-NUM-BONUS         PIC X(01).                                  
       01  WS-PREV-BTO-ZONE         PIC X(03)  VALUE SPACES.                    
       01  WS-PREV-CDE-NT           PIC X(03)  VALUE SPACES.                    
       01  WS-NUM-ZONE              PIC X(10)  VALUE SPACES.                    
       01  WS-AVAIL-TOT             PIC 9(04)  VALUE ZEROES.                    
       01  WS-AVAIL-TOTAL           PIC 9(04)  VALUE ZEROES.                    
       01  WS-AVAIL-CAT             PIC 9(04)  VALUE ZEROES.                    
       01  WS-AVAIL-CAT1            PIC 9(04)  VALUE ZEROES.                    
       01  WS-AVAIL-CAT2            PIC 9(04)  VALUE ZEROES.                    
       01  WS-AVAIL-CAT3            PIC 9(04)  VALUE ZEROES.                    
       01  WS-AVAIL-SPR             PIC 9(04)  VALUE ZEROES.                    
       01  WS-NUM-TOTAL             PIC 9(04)  VALUE ZEROES.                    
       01  WS-NUM-TOTAL-GEN3        PIC 9(04)  VALUE ZEROES.                    
       01  WS-NUM-UNIT-CAT1         PIC 9(04)  VALUE ZEROES.                    
       01  WS-NUM-UNIT-CAT2         PIC 9(04)  VALUE ZEROES.                    
       01  WS-NUM-UNIT-CAT3         PIC 9(04)  VALUE ZEROES.                    
       01  WS-NUM-UNIT-SPR          PIC 9(04)  VALUE ZEROES.                    
       01  WS-NUM-LIMIT             PIC 9(03)V99 VALUE ZEROES.                  
       01  WS-NUM-LIMIT-REDEF REDEFINES WS-NUM-LIMIT.                           
           05  WS-NUM-LIMIT-UNIT    PIC 9(03).                                  
           05  WS-NUM-LIMIT-DECIMAL PIC 9(02).                                  
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-PRE-PROCESS      THRU 1000-EXIT.                        
           PERFORM 2000-READ-F240        THRU 2000-EXIT.                        
           PERFORM 3000-PROCESS-REC      THRU 3000-EXIT                         
                   UNTIL WS-F240-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-PRE-PROCESS.                                                        
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F240                                                 
                       BP13K813                                                 
                       BM06K510                                                 
                I-O    BP13K249                                                 
                       BP13K255.                                                
                                                                                
           IF WS-K249-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13K249 - OPEN ERROR: ' WS-K249-STATUS                  
              MOVE WS-K249-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K255-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13K255 - OPEN ERROR: ' WS-K255-STATUS                  
              MOVE WS-K255-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13K813 - OPEN ERROR: ' WS-K813-STATUS                  
              MOVE WS-K813-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K510-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13K510 - OPEN ERROR: ' WS-K510-STATUS                  
              MOVE WS-K510-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F240.                                                          
      *---------------*                                                         
                                                                                
           READ BP13F240 AT END                                                 
                PERFORM 3050-UPD-BP13K249 THRU 3050-EXIT                        
                MOVE 'Y' TO WS-F240-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F240-READ-CNT.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           IF (WS-PREV-KEY NOT = K240-ALT-KEY5) AND                             
              (WS-PREV-KEY NOT = SPACES AND LOW-VALUES)                         
              PERFORM 3050-UPD-BP13K249        THRU 3050-EXIT                   
              MOVE K240-ALT-KEY5               TO   WS-PREV-KEY                 
              MOVE K240-NUM-BTO-ZONE           TO   WS-PREV-BTO-ZONE            
              MOVE K240-CDE-NT                 TO   WS-PREV-CDE-NT              
              IF K240-NUM-SELECTION = 'NPL'                                     
                 MOVE K240-NUM-ZONE            TO   WS-NUM-ZONE                 
              ELSE                                                              
                 MOVE SPACES                   TO   WS-NUM-ZONE                 
              END-IF                                                            
              MOVE ZEROES                      TO   WS-AVAIL-TOTAL              
                                                    WS-AVAIL-CAT1               
                                                    WS-AVAIL-CAT2               
                                                    WS-AVAIL-CAT3               
                                                    WS-AVAIL-SPR                
                                                    WS-NUM-TOTAL                
                                                    WS-NUM-TOTAL-GEN3           
                                                    WS-NUM-UNIT-CAT1            
                                                    WS-NUM-UNIT-CAT2            
                                                    WS-NUM-UNIT-CAT3            
                                                    WS-NUM-UNIT-SPR             
              PERFORM 3500-COMPUTE-TOTALS      THRU 3500-EXIT                   
           ELSE                                                                 
              PERFORM 3500-COMPUTE-TOTALS      THRU 3500-EXIT                   
              IF WS-PREV-KEY = SPACES OR LOW-VALUES                             
                 MOVE K240-ALT-KEY5            TO   WS-PREV-KEY                 
                 MOVE K240-NUM-BTO-ZONE        TO   WS-PREV-BTO-ZONE            
                 MOVE K240-CDE-NT              TO   WS-PREV-CDE-NT              
                 IF K240-NUM-SELECTION = 'NPL'                                  
                    MOVE K240-NUM-ZONE         TO   WS-NUM-ZONE                 
                 ELSE                                                           
                    MOVE SPACES                TO   WS-NUM-ZONE                 
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F240              THRU 2000-EXIT.                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3050-UPD-BP13K249.                                                       
      *-------------------*                                                     
                                                                                
           IF WS-PREV-KEY = SPACES OR LOW-VALUES                                
              GO TO 3050-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 3100-READ-BP13K249          THRU 3100-EXIT.                  
           IF WS-PREV-BTO-ZONE NOT = SPACES AND LOW-VALUES                      
               PERFORM 3150-READ-BP13K813      THRU 3150-EXIT                   
           END-IF.                                                              
           PERFORM 3200-MOVE-FIELDS            THRU 3200-EXIT.                  
           IF WS-K249-FND = 'Y'                                                 
              PERFORM 3300-REWRITE-BP13K249    THRU 3300-EXIT                   
           ELSE                                                                 
              PERFORM 3400-WRITE-BP13K249      THRU 3400-EXIT                   
           END-IF.                                                              
                                                                                
           PERFORM 4000-READ-BP13K255          THRU 4000-EXIT.                  
                                                                                
           IF WS-K255-FND = 'N'                                                 
              PERFORM 4200-WRITE-BP13K255      THRU 4200-EXIT                   
           END-IF.                                                              
                                                                                
       3050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3100-READ-BP13K249.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                     TO BP13K249-REC.                     
           INITIALIZE                         BP13K249-REC.                     
                                                                                
           MOVE WS-NUM-NEW-TOWN            TO K249-NUM-NEW-TOWN.                
           MOVE WS-NUM-FLAT-TYPE           TO K249-NUM-FLAT-TYPE.               
           MOVE WS-NUM-SELECTION           TO K249-NUM-SELECTION.               
           MOVE WS-DTE-BALLOT              TO K249-DTE-BALLOT.                  
           MOVE WS-NUM-BONUS               TO K249-NUM-BONUS.                   
           MOVE 'N'                        TO WS-K249-FND.                      
                                                                                
           READ BP13K249 KEY IS K249-KEY-FLD.                                   
                                                                                
           EVALUATE WS-K249-STATUS                                              
             WHEN 00                                                            
                MOVE 'Y' TO WS-K249-FND                                         
             WHEN 23                                                            
                CONTINUE                                                        
             WHEN OTHER                                                         
                DISPLAY 'BP13K249 - READ ERROR: ' WS-K249-STATUS                
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3150-READ-BP13K813.                                                      
      *-------------------*                                                     
                                                                                
           MOVE 'N'               TO WS-K813-FND.                               
                                                                                
           MOVE SPACES            TO BP13K813-REC.                              
           INITIALIZE                BP13K813-REC.                              
                                                                                
           MOVE WS-PREV-BTO-ZONE  TO K813-NUM-ZONE.                             
                                                                                
           START BP13K813 KEY >= K813-KEY-FLD                                   
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    READ BP13K813 NEXT RECORD                                   
                    IF K813-NUM-ZONE = WS-PREV-BTO-ZONE                         
                       MOVE 'Y'           TO WS-K813-FND                        
                    ELSE                                                        
                       DISPLAY 'BP13K813 - NOT FND: ' K813-KEY-FLD              
                    END-IF                                                      
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K813 - READ ERROR: ' WS-K813-STATUS            
           END-EVALUATE.                                                        
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3200-MOVE-FIELDS.                                                        
      *-----------------*                                                       
                                                                                
           MOVE WS-NUM-TOTAL       TO   K249-NUM-TOTAL.                         
           MOVE WS-NUM-TOTAL-GEN3  TO   K249-NUM-TOTAL-GEN3.                    
           MOVE WS-AVAIL-TOTAL     TO   K249-NUM-AVAIL-TOTAL.                   
                                                                                
           IF K249-NUM-SELECTION = 'BTO' OR 'BE '                               
      ** CR - BP139545 START                                                    
              IF K249-DTE-BALLOT < '202310'                                     
                                                                                
                 COMPUTE WS-NUM-LIMIT = (K249-NUM-TOTAL -                       
                                         K249-NUM-TOTAL-GEN3) * 0.05            
                 IF WS-NUM-LIMIT-DECIMAL > 0                                    
                    ADD 1             TO   WS-NUM-LIMIT                         
                 END-IF                                                         
                 IF WS-NUM-LIMIT < 10                                           
                    MOVE 10           TO   K249-NUM-LIMIT                       
                 ELSE                                                           
                    MOVE WS-NUM-LIMIT TO   K249-NUM-LIMIT                       
                 END-IF                                                         
                                                                                
              ELSE                                                              
                                                                                
                 PERFORM 3210-COMPUTE-LIMIT-202310 THRU 3210-EXIT               
                                                                                
              END-IF                                                            
      ** CR - BP139545 END.                                                     
                                                                                
           END-IF.                                                              
                                                                                
           MOVE WS-AVAIL-CAT1      TO   K249-NUM-AVAIL-CAT1.                    
           MOVE WS-AVAIL-CAT2      TO   K249-NUM-AVAIL-CAT2.                    
           MOVE WS-AVAIL-CAT3      TO   K249-NUM-AVAIL-CAT3.                    
           MOVE WS-AVAIL-SPR       TO   K249-NUM-AVAIL-SPR.                     
                                                                                
           MOVE WS-NUM-UNIT-CAT1   TO   K249-NUM-UNIT-CAT1.                     
           MOVE WS-NUM-UNIT-CAT2   TO   K249-NUM-UNIT-CAT2.                     
           MOVE WS-NUM-UNIT-CAT3   TO   K249-NUM-UNIT-CAT3.                     
           MOVE WS-NUM-UNIT-SPR    TO   K249-NUM-UNIT-SPR.                      
                                                                                
           MOVE 'BP13K240'         TO   K249-FILE-ORIG.                         
                                                                                
           MOVE WS-CUR-DATE        TO   K249-DTE-UPDATE.                        
                                                                                
           ACCEPT K249-TME-UPDATE  FROM TIME.                                   
                                                                                
           MOVE 'BP13C21N'         TO   K249-NUM-USERID.                        
                                                                                
           MOVE WS-NUM-ZONE        TO   K249-NUM-ZONE.                          
           MOVE K249-NUM-NEW-TOWN  TO   K249-NUM-NEW-TOWN1.                     
           MOVE K249-NUM-FLAT-TYPE TO   K249-NUM-FLAT-TYPE1.                    
           MOVE K249-NUM-SELECTION TO   K249-NUM-SELECTION1.                    
           MOVE K249-DTE-BALLOT    TO   K249-DTE-BALLOT1.                       
                                                                                
           MOVE WS-PREV-BTO-ZONE   TO   K249-NUM-BTO-ZONE.                      
           MOVE WS-PREV-CDE-NT     TO   K249-CDE-NT.                            
                                                                                
           IF K249-NUM-SELECTION =  'BTO'                                       
              IF WS-K813-FND = 'Y'                                              
                 MOVE K813-NME-PROJECT-FLAT-AVAI                                
                                   TO   K249-NME-PROJECT                        
              ELSE                                                              
                 MOVE SPACES       TO   K249-NME-PROJECT                        
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES          TO   K249-NME-PROJECT                        
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ** CR - BP139545 START                                                    
      *-------------------------*                                               
       3210-COMPUTE-LIMIT-202310.                                               
      *-------------------------*                                               
                                                                                
      *NOTE: IN BP13CH03, IF NUMBER OF FLAT IS >= TO LIMIT, IT IS               
      * CONSIDER AS WITHIN LIMIT, HENCE, IN ORDER TO WAIVE,                     
      * WHEN THE NUMBER OF FLAT IS <= 1O, THIS PROGRAM                          
      * WILL NEED TO SET THE LIMIT TO 11.                                       
                                                                                
      *    MOVE 10 TO WS-NUM-LIMIT.                                             
           MOVE 11 TO WS-NUM-LIMIT.                                             
                                                                                
           IF K249-NUM-FLAT-TYPE = '5-ROOM   '                                  
                                                                                
              COMPUTE WS-NUM-LIMIT = WS-NUM-LIMIT + K249-NUM-TOTAL-GEN3         
                                                                                
           END-IF.                                                              
                                                                                
           MOVE WS-NUM-LIMIT TO K249-NUM-LIMIT.                                 
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ** CR - BP139545 END.                                                     
      *---------------------*                                                   
       3300-REWRITE-BP13K249.                                                   
      *---------------------*                                                   
                                                                                
           REWRITE BP13K249-REC.                                                
                                                                                
           IF WS-K249-STATUS = 00                                               
              ADD 1 TO WS-K249-REWRITE-CNT                                      
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13K249, KEY: '                         
                       K249-KEY-FLD                                             
              ADD 1 TO WS-K249-REWRITE-ERR                                      
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3400-WRITE-BP13K249.                                                     
      *--------------------*                                                    
                                                                                
           WRITE BP13K249-REC.                                                  
                                                                                
           IF WS-K249-STATUS = 00                                               
              ADD 1 TO WS-K249-WRITE-CNT                                        
           ELSE                                                                 
              DISPLAY 'ERROR WRITING BP13K249, KEY: '                           
                       K249-KEY-FLD                                             
              ADD 1 TO WS-K249-WRITE-ERR                                        
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3500-COMPUTE-TOTALS.                                                     
      *--------------------*                                                    
                                                                                
           IF K240-NUM-TOTAL NOT NUMERIC                                        
              MOVE ZEROES       TO K240-NUM-TOTAL                               
           END-IF.                                                              
                                                                                
           IF K240-NUM-TOT-CAT1 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-TOT-CAT1                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-TOT-CAT2 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-TOT-CAT2                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-TOT-CAT3 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-TOT-CAT3                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-TOTAL-SPR NOT NUMERIC                                    
              MOVE ZEROES       TO K240-NUM-TOTAL-SPR                           
           END-IF.                                                              
                                                                                
           IF K240-NUM-MAX-CAT1 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-MAX-CAT1                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-MAX-CAT2 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-MAX-CAT2                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-MAX-CAT3 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-MAX-CAT3                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-MAX-SPR  NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-MAX-SPR                             
           END-IF.                                                              
                                                                                
           IF K240-NUM-TOTAL-GEN3 NOT NUMERIC                                   
              MOVE ZEROES       TO K240-NUM-TOTAL-GEN3                          
           END-IF.                                                              
                                                                                
           ADD K240-NUM-TOTAL TO WS-NUM-TOTAL.                                  
           ADD K240-NUM-TOTAL-GEN3 TO WS-NUM-TOTAL-GEN3.                        
                                                                                
           COMPUTE WS-AVAIL-TOT  = K240-NUM-TOTAL                               
                                 - K240-NUM-TOT-CAT1                            
                                 - K240-NUM-TOT-CAT2                            
                                 - K240-NUM-TOT-CAT3                            
                                 - K240-NUM-TOTAL-SPR.                          
                                                                                
           ADD WS-AVAIL-TOT     TO WS-AVAIL-TOTAL.                              
                                                                                
           COMPUTE WS-AVAIL-CAT  = K240-NUM-MAX-CAT1                            
                                 - K240-NUM-TOT-CAT1.                           
                                                                                
           IF WS-AVAIL-CAT > WS-AVAIL-TOT                                       
              MOVE WS-AVAIL-TOT TO WS-AVAIL-CAT                                 
           END-IF.                                                              
                                                                                
           ADD WS-AVAIL-CAT     TO WS-AVAIL-CAT1.                               
                                                                                
           IF WS-AVAIL-CAT > 0                                                  
              ADD WS-AVAIL-TOT  TO WS-NUM-UNIT-CAT1                             
           END-IF.                                                              
                                                                                
           COMPUTE WS-AVAIL-CAT  = K240-NUM-MAX-CAT2                            
                                 - K240-NUM-TOT-CAT2.                           
                                                                                
           IF WS-AVAIL-CAT > WS-AVAIL-TOT                                       
              MOVE WS-AVAIL-TOT TO WS-AVAIL-CAT                                 
           END-IF.                                                              
                                                                                
           ADD WS-AVAIL-CAT     TO WS-AVAIL-CAT2.                               
                                                                                
           IF WS-AVAIL-CAT > 0                                                  
              ADD WS-AVAIL-TOT  TO WS-NUM-UNIT-CAT2                             
           END-IF.                                                              
                                                                                
           COMPUTE WS-AVAIL-CAT  = K240-NUM-MAX-CAT3                            
                                 - K240-NUM-TOT-CAT3.                           
                                                                                
           IF WS-AVAIL-CAT > WS-AVAIL-TOT                                       
              MOVE WS-AVAIL-TOT TO WS-AVAIL-CAT                                 
           END-IF.                                                              
                                                                                
           ADD WS-AVAIL-CAT     TO WS-AVAIL-CAT3.                               
                                                                                
           IF WS-AVAIL-CAT > 0                                                  
              ADD WS-AVAIL-TOT  TO WS-NUM-UNIT-CAT3                             
           END-IF.                                                              
                                                                                
           COMPUTE WS-AVAIL-CAT  = K240-NUM-MAX-SPR                             
                                 - K240-NUM-TOTAL-SPR.                          
                                                                                
           IF WS-AVAIL-CAT > WS-AVAIL-TOT                                       
              MOVE WS-AVAIL-TOT TO WS-AVAIL-CAT                                 
           END-IF.                                                              
                                                                                
           ADD WS-AVAIL-CAT     TO WS-AVAIL-SPR.                                
                                                                                
           IF WS-AVAIL-CAT > 0                                                  
              ADD WS-AVAIL-TOT  TO WS-NUM-UNIT-SPR                              
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4000-READ-BP13K255.                                                      
      *-------------------*                                                     
           MOVE SPACES                TO BP13K255-REC.                          
           MOVE K249-NUM-SELECTION    TO K255-NUM-SELECTION.                    
                                                                                
           IF K249-NUM-SELECTION = 'BTO'                                        
              MOVE WS-PREV-BTO-ZONE   TO K255-NUM-ZONE-NT                       
           ELSE                                                                 
              PERFORM 4100-READ-BM06K510  THRU 4100-EXIT                        
              MOVE K510-ALPHA-NTCDE   TO K255-NUM-ZONE-NT                       
           END-IF.                                                              
                                                                                
           MOVE K249-DTE-BALLOT       TO K255-DTE-BALLOT.                       
           MOVE K249-NUM-FLAT-TYPE    TO K255-NUM-FLAT-TYPE.                    
                                                                                
           MOVE 'N'                        TO WS-K255-FND.                      
                                                                                
           READ BP13K255 KEY IS K255-KEY-FLD.                                   
                                                                                
           EVALUATE WS-K255-STATUS                                              
             WHEN 00                                                            
                MOVE 'Y' TO WS-K255-FND                                         
             WHEN 23                                                            
                CONTINUE                                                        
             WHEN OTHER                                                         
                DISPLAY 'BP13K255 - READ ERROR: ' WS-K255-STATUS                
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       4100-READ-BM06K510.                                                      
      *--------------------------------*                                        
                                                                                
           MOVE  K249-NUM-NEW-TOWN      TO K510-NT-NAME.                        
           READ BM06K510 KEY IS K510-NT-NAME.                                   
                                                                                
           EVALUATE WS-K510-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BM06K510: ' K510-NT-NAME        
                   MOVE SPACES     TO K510-REC                                  
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BM06K510,STATUS ' WS-K510-STATUS         
                   MOVE WS-K510-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       4200-WRITE-BP13K255.                                                     
      *--------------------*                                                    
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO K255-DTE-UPDATE.                  
           MOVE FUNCTION CURRENT-DATE(9:8) TO K255-TME-UPDATE.                  
           MOVE 'P13C21N'                  TO K255-NUM-USERID.                  
                                                                                
           WRITE BP13K255-REC.                                                  
                                                                                
           IF WS-K255-STATUS = 00                                               
              ADD 1 TO WS-K255-WRITE-CNT                                        
           ELSE                                                                 
              DISPLAY 'ERROR WRITING BP13K255, KEY: '                           
                       K255-KEY-FLD                                             
              ADD 1 TO WS-K255-WRITE-ERR                                        
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C21N  *************'.                           
           DISPLAY 'RECS READ    FROM BP13F240 : ' WS-F240-READ-CNT.            
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN IN BP13K249   : ' WS-K249-WRITE-CNT.           
           DISPLAY 'WRITE ERRORS IN BP13K249   : ' WS-K249-WRITE-ERR.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS REWRITTEN IN BP13K249 : ' WS-K249-REWRITE-CNT.         
           DISPLAY 'REWRITE ERRORS IN BP13K249 : ' WS-K249-REWRITE-ERR.         
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN IN BP13K255   : ' WS-K255-WRITE-CNT.           
           DISPLAY 'WRITE ERRORS IN BP13K255   : ' WS-K255-WRITE-ERR.           
                                                                                
           CLOSE BP13F240                                                       
                 BP13K813                                                       
                 BP13K249                                                       
                 BM06K510.                                                      
                                                                                
           IF WS-K249-STATUS NOT EQUAL 00                                       
              DISPLAY 'BP13K249 - CLOSE ERROR: ' WS-K249-STATUS                 
              MOVE WS-K249-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT EQUAL 00                                       
              DISPLAY 'BP13K813 - CLOSE ERROR: ' WS-K813-STATUS                 
              MOVE WS-K813-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K510-STATUS NOT EQUAL 00                                       
              DISPLAY 'BM06K510 - CLOSE ERROR: ' WS-K510-STATUS                 
              MOVE WS-K510-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
