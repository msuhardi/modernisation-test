      *------------------------------------------------------------*            
       IDENTIFICATION DIVISION.                                                 
      *------------------------------------------------------------*            
       PROGRAM-ID.   BP13C044.                                                  
      *AUTHOR.       SK.                                                        
      *DATE-WRITTEN. 17/02/2004.                                                
      *------------------------------------------------------------*            
      *                   SYSTEM OF COMMITMENT  (BP13)             *            
      *------------------------------------------------------------*            
      *                                                            *            
      *  OBJECTIVE   : 1.  TO RE-ASSIGN OIC (FROM PANEL P13M021)   *            
      *                                                            *            
      *  INPUT FILES : 1.  BP13F026  -  OIC RE-ASSIGN FILE         *            
      *                2.  P13F022A  -  OIC FILE (TO PRINT LETTER) *            
      *                3.  BP13K022  -  OIC FILE                   *            
      *                4.  BP13K800  -  SOC MASTER                              
      *                5.  BP13K026  -  OIC RE-ASSIGN FILE         *            
      *                6.  BP13K028  -  OIC HISTORY FILE           *            
      *------------------------------------------------------------*            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *------------------------------------------------------------*            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F026 ASSIGN TO BP13F026.                                  
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K022-KEY-FLD                               
                  FILE STATUS     IS WS-K022-STATUS.                            
                                                                                
           SELECT BP13K026 ASSIGN TO BP13K026                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K026-KEY-FLD                               
                  FILE STATUS     IS WS-K026-STATUS.                            
                                                                                
           SELECT BP13K028 ASSIGN TO BP13K028                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K028-KEY-FLD                               
                  FILE STATUS     IS WS-K028-STATUS.                            
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT P13F022A ASSIGN TO P13F022A.                                  
                                                                                
      *------------------------------------------------------------*            
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *------------------------------------------------------------*            
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
                                                                                
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K026                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
                                                                                
       COPY BP13K026.                                                           
                                                                                
       FD   BP13K028                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
                                                                                
       COPY BP13K028.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
                                                                                
       COPY BP13K800.                                                           
                                                                                
       FD   BP13F026                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
                                                                                
       COPY BP13F026.                                                           
                                                                                
       FD   P13F022A                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
                                                                                
       01   P13F022A-REC    PIC X(100).                                         
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
                                                                                
       01  WORK-AREAS.                                                          
           05  WS-F022-READ             PIC 9(08)  VALUE 0.                     
           05  WS-F026-READ             PIC 9(08)  VALUE 0.                     
           05  WS-K022-NFND             PIC 9(08)  VALUE 0.                     
                                                                                
           05  WS-K800-FOUND            PIC 9(08)  VALUE 0.                     
           05  WS-K800-CAN              PIC 9(08)  VALUE 0.                     
           05  WS-NOFLAT                PIC 9(08)  VALUE 0.                     
           05  WS-K800-NFND             PIC 9(08)  VALUE 0.                     
                                                                                
           05  WS-K022-EOF              PIC X(01)  VALUE 'N'.                   
           05  WS-F026-EOF              PIC X(01)  VALUE 'N'.                   
                                                                                
           05  WS-K022-WRITE            PIC 9(08)  VALUE 0.                     
           05  WS-K026-DELETE           PIC 9(08)  VALUE 0.                     
           05  WS-F022A-WRITE           PIC 9(08)  VALUE 0.                     
           05  WS-K028-WRITE            PIC 9(08)  VALUE 0.                     
                                                                                
           05  WS-K022-STATUS           PIC 9(02)  VALUE 0.                     
           05  WS-K026-STATUS           PIC 9(02)  VALUE 0.                     
           05  WS-K028-STATUS           PIC 9(02)  VALUE 0.                     
           05  WS-K800-STATUS           PIC 9(02)  VALUE 0.                     
                                                                                
           05  WS-COUNTER               PIC 9(03)  VALUE 1.                     
           05  WS-COUNTER1              PIC 9(03)  VALUE 0.                     
           05  WS-COUNT                 PIC 9(03)  VALUE 0.                     
           05  WS-COUNT1                PIC 9(03)  VALUE 0.                     
           05  WS-CTR                   PIC 9(03)  VALUE 0.                     
           05  WS-INTERVAL              PIC 9(02)   VALUE 1.                    
           05  WS-TOT                   PIC 9(04)   VALUE 0.                    
                                                                                
           05  WS-ROUND                 PIC 9(03)  VALUE 0.                     
           05  WS-ROUND-ED              PIC Z(03).                              
                                                                                
           05  WS-LAST                  PIC X(06)  VALUE SPACES.                
           05  WS-FROM-OIC              PIC X(05)  VALUE SPACES.                
           05  WS-FLAG                  PIC X(01)  VALUE 'N'.                   
                                                                                
       01  WS-DATE-VAR.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY          PIC 9(04).                              
               10  WS-SYS-MM            PIC 9(02).                              
               10  WS-SYS-DD            PIC 9(02).                              
                                                                                
           05  WS-EDITED-DATE           PIC X(10)  VALUE SPACES.                
                                                                                
       01  WS-F026-VAR.                                                         
           05  WS-F026-KEY-FLD          PIC X(11)  VALUE SPACES.                
           05  WS-F026-CDE-BANKLOAN     PIC X(01)  VALUE SPACES.                
           05  WS-F026-NUM-INTVL        PIC 9(02)  VALUE 0.                     
           05  WS-F026-NUM-MAX-INTVL    PIC 9(04)  VALUE 0.                     
           05  WS-F026-CDE-ROUND        PIC X(01)  VALUE SPACES.                
                                                                                
       01  WS-K022-KEY.                                                         
           05  WS-K022-NUM-OIC          PIC X(05)  VALUE SPACES.                
           05  WS-K022-NUM-REGN         PIC X(08)  VALUE SPACES.                
                                                                                
       01  WS-OIC-TABLE.                                                        
           05 WS-OIC-ASGN  OCCURS 32 TIMES.                                     
              10 WS-NUM-OIC             PIC X(05).                              
              10 WS-MAX-CNT             PIC 9(03).                              
              10 WS-NUM-CNT             PIC 9(03).                              
              10 WS-NUM-TOT             PIC 9(03).                              
                                                                                
       01 WS-K022-REC                   PIC X(100)   VALUE SPACES.              
                                                                                
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
                                                                                
      *------------------------------------------------------------*            
       0000-MAIN.                                                               
      *------------------------------------------------------------*            
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 1200-READ-F026     THRU 1200-EXIT.                           
           PERFORM 2000-PROCESS       THRU 2000-EXIT                            
                   UNTIL WS-F026-EOF = 'Y'.                                     
                                                                                
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1000-OPEN-FILES.                                                         
      *------------------------------------------------------------*            
                                                                                
           OPEN INPUT  BP13F026                                                 
                       BP13K800                                                 
                I-O    BP13K022                                                 
                       BP13K026                                                 
                       BP13K028                                                 
                OUTPUT P13F022A.                                                
                                                                                
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'OPEN BP13K022 FAIL. STATUS - '                        
                                            WS-K022-STATUS                      
                 MOVE  WS-K022-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K026-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'OPEN BP13K026 FAIL. STATUS - '                        
                                            WS-K026-STATUS                      
                 MOVE  WS-K026-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K028-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'OPEN BP13K028 FAIL. STATUS - '                        
                                            WS-K028-STATUS                      
                 MOVE  WS-K028-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'OPEN BP13K800 FAIL. STATUS - '                        
                                            WS-K800-STATUS                      
                 MOVE  WS-K800-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING  WS-SYSTEM-DATE(7:2) '/'                                      
                   WS-SYSTEM-DATE(5:2) '/'                                      
                   WS-SYSTEM-DATE(1:4)                                          
                   DELIMITED BY SIZE                                            
                   INTO WS-EDITED-DATE                                          
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1200-READ-F026.                                                          
      *------------------------------------------------------------*            
                                                                                
           READ BP13F026                                                        
                AT END                                                          
                   MOVE 'Y' TO WS-F026-EOF                                      
                NOT AT END                                                      
                   ADD 1 TO WS-F026-READ                                        
           END-READ.                                                            
                                                                                
           IF WS-LAST = SPACES OR LOW-VALUES                                    
              MOVE BP13F026-REC(1:6)       TO WS-LAST                           
              MOVE F026-NUM-FROM-OIC       TO WS-FROM-OIC                       
              MOVE F026-CDE-BANKLOAN       TO WS-F026-CDE-BANKLOAN              
              MOVE F026-NUM-INTERVAL       TO WS-F026-NUM-INTVL                 
              MOVE F026-NUM-MAX-INTERVAL   TO WS-F026-NUM-MAX-INTVL             
              MOVE F026-CDE-ROUND          TO WS-F026-CDE-ROUND                 
           END-IF.                                                              
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2000-PROCESS.                                                            
      *------------------------------------------------------------*            
                                                                                
           MOVE 'N'  TO WS-K022-EOF.                                            
                                                                                
           PERFORM 2500-WRITE-TABLE       THRU 2500-EXIT.                       
                                                                                
           PERFORM 2600-RE-ASSIGN         THRU 2600-EXIT.                       
                                                                                
           PERFORM 5000-DISPLAY-SUMMARY   THRU 5000-EXIT.                       
                                                                                
           MOVE F026-KEY-FLD              TO WS-F026-KEY-FLD.                   
           MOVE F026-NUM-FROM-OIC         TO WS-FROM-OIC.                       
           MOVE BP13F026-REC(1:6)         TO WS-LAST.                           
                                                                                
                                                                                
           MOVE F026-CDE-BANKLOAN         TO WS-F026-CDE-BANKLOAN.              
           MOVE F026-NUM-INTERVAL         TO WS-F026-NUM-INTVL.                 
           MOVE F026-NUM-MAX-INTERVAL     TO WS-F026-NUM-MAX-INTVL.             
           MOVE F026-CDE-ROUND            TO WS-F026-CDE-ROUND.                 
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2500-WRITE-TABLE.                                                        
      *------------------------------------------------------------*            
                                                                                
           MOVE 1 TO WS-CTR WS-COUNTER.                                         
           MOVE 0 TO WS-COUNTER1.                                               
                                                                                
           INITIALIZE WS-OIC-TABLE REPLACING                                    
                      ALPHANUMERIC DATA BY SPACES                               
                      NUMERIC      DATA BY ZEROS.                               
                                                                                
           PERFORM UNTIL BP13F026-REC(1:6) NOT = WS-LAST                        
                   OR WS-CTR > 32 OR WS-F026-EOF = 'Y'                          
                                                                                
              MOVE F026-NUM-TO-OIC       TO WS-NUM-OIC(WS-CTR)                  
              MOVE F026-NUM-CASES        TO WS-MAX-CNT(WS-CTR)                  
              MOVE 0                     TO WS-NUM-CNT(WS-CTR)                  
                                            WS-NUM-TOT(WS-CTR)                  
                                                                                
              ADD 1 TO WS-CTR WS-COUNTER1                                       
              PERFORM 1200-READ-F026     THRU 1200-EXIT                         
           END-PERFORM.                                                         
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2600-RE-ASSIGN.                                                          
      *------------------------------------------------------------*            
                                                                                
           MOVE 'N'                 TO WS-K022-EOF.                             
           MOVE SPACES              TO WS-K022-KEY.                             
           MOVE WS-FROM-OIC         TO WS-K022-NUM-OIC.                         
           MOVE SPACES              TO WS-K022-NUM-REGN.                        
           PERFORM 3000-READ-K022   THRU 3000-EXIT                              
                   UNTIL WS-K022-EOF = 'Y'.                                     
                                                                                
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3000-READ-K022.                                                          
      *------------------------------------------------------------*            
                                                                                
           IF WS-F026-NUM-INTVL > 0                                             
              IF WS-TOT = WS-F026-NUM-MAX-INTVL                                 
                 ADD 1    TO WS-COUNT1                                          
                 MOVE 0   TO WS-TOT                                             
                 IF WS-COUNT1 = (WS-CTR - 1)                                    
                    MOVE 'Y' TO WS-K022-EOF                                     
                    MOVE 0   TO WS-COUNT1                                       
                 END-IF                                                         
                 GO TO 3000-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE 'N'            TO WS-FLAG.                                      
           MOVE LOW-VALUES     TO K022-KEY-FLD.                                 
           MOVE WS-K022-KEY    TO K022-KEY-FLD.                                 
                                                                                
           IF WS-K022-NUM-REGN = SPACES OR LOW-VALUES                           
              START BP13K022 KEY >= K022-KEY-FLD                                
           ELSE                                                                 
              START BP13K022 KEY > K022-KEY-FLD                                 
           END-IF.                                                              
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
                 PERFORM 3050-READNEXT-K022  THRU 3050-EXIT                     
              WHEN 10                                                           
                 MOVE 'Y' TO WS-K022-EOF                                        
              WHEN 23                                                           
                 ADD 1 TO WS-K022-NFND                                          
                 DISPLAY 'REGN NOT FOUND IN BP13K022. REGN : '                  
                             K022-NUM-REGN                                      
                 MOVE 'Y' TO WS-K022-EOF                                        
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BP13K022, STATUS - '                   
                                            WS-K022-STATUS                      
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           IF WS-F026-NUM-INTVL > 0                                             
              ADD 1 TO WS-INTERVAL                                              
                                                                                
              IF WS-INTERVAL > WS-F026-NUM-INTVL                                
                 MOVE 1 TO WS-INTERVAL                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3050-READNEXT-K022.                                                      
      *------------------------------------------------------------*            
                                                                                
           PERFORM UNTIL WS-FLAG = 'Y'                                          
              READ BP13K022 NEXT RECORD                                         
                   AT END MOVE 'Y' TO WS-FLAG WS-K022-EOF                       
              END-READ                                                          
                                                                                
              IF WS-K022-STATUS = 00                                            
                 IF WS-FROM-OIC NOT = K022-NUM-OIC                              
                    MOVE 'Y' TO WS-FLAG WS-K022-EOF                             
                    MOVE 0   TO WS-ROUND                                        
                 ELSE                                                           
                   IF (WS-F026-CDE-BANKLOAN  = K022-CDE-BANKLOAN) OR            
                      (WS-F026-CDE-BANKLOAN  = 'N' AND                          
                       K022-CDE-BANKLOAN NOT = 'B')                             
                      PERFORM 3100-READ-K800      THRU 3100-EXIT                
                   END-IF                                                       
                 END-IF                                                         
              ELSE                                                              
                 ADD 1 TO WS-K022-NFND                                          
                 MOVE 'Y' TO WS-FLAG WS-K022-EOF                                
              END-IF                                                            
              MOVE K022-NUM-REGN TO WS-K022-NUM-REGN                            
           END-PERFORM.                                                         
                                                                                
       3050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3100-READ-K800.                                                          
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES         TO K800-NUM-REGN.                                
           MOVE K022-NUM-REGN  TO K800-NUM-REGN.                                
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-K800-FOUND                                         
                 IF K800-NUM-STATUS NOT = 'C'                                   
                    PERFORM 3200-REASSIGN-PROCESS THRU 3200-EXIT                
                 ELSE                                                           
                    DISPLAY 'K800-CAN CASE  : ' K022-NUM-REGN                   
                    ADD 1 TO WS-K800-CAN                                        
                 END-IF                                                         
                 MOVE 'Y' TO WS-FLAG                                            
              WHEN 23                                                           
                 DISPLAY ' NOTFD IN SOC ' K022-NUM-REGN                         
                 ADD 1 TO WS-K800-NFND                                          
                 MOVE 'N' TO WS-FLAG                                            
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BP13K800, STATUS - '                   
                                            WS-K800-STATUS                      
                 MOVE WS-K800-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3200-REASSIGN-PROCESS.                                                   
      *------------------------------------------------------------*            
                                                                                
           IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                      
              PERFORM 3500-WRITE-F022A THRU 3500-EXIT                           
           ELSE                                                                 
              ADD 1 TO WS-NOFLAT                                                
           END-IF.                                                              
                                                                                
           DISPLAY ' BF. DEL K022. OIC/REGN: ' K022-KEY-FLD.                    
                                                                                
           PERFORM 4600-WRITE-K028         THRU 4600-EXIT.                      
           PERFORM 4000-DELETE-K022        THRU 4000-EXIT.                      
                                                                                
           IF WS-F026-CDE-ROUND = 'Y'                                           
              PERFORM 4100-WRITE-K022      THRU 4100-EXIT                       
           ELSE                                                                 
              PERFORM 4200-WRITE-K022      THRU 4200-EXIT                       
           END-IF.                                                              
                                                                                
           DISPLAY ' AF. DEL K022.           ' K022-KEY-FLD                     
                                               WS-K022-WRITE.                   
           DISPLAY SPACES.                                                      
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3500-WRITE-F022A.                                                        
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES         TO P13F022A-REC.                                 
           INITIALIZE             P13F022A-REC.                                 
                                                                                
           MOVE BP13K022-REC   TO P13F022A-REC.                                 
                                                                                
           WRITE P13F022A-REC.                                                  
           ADD 1 TO WS-F022A-WRITE.                                             
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4000-DELETE-K022.                                                        
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES         TO WS-K022-REC.                                  
           INITIALIZE             WS-K022-REC.                                  
                                                                                
           MOVE BP13K022-REC   TO WS-K022-REC.                                  
                                                                                
           DELETE BP13K022.                                                     
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR DELETING BP13K022, STATUS - '                  
                                            WS-K022-STATUS                      
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4100-WRITE-K022.                                                         
      *------------------------------------------------------------*            
      * MULTIPLE ROUND RE-ASSIGNMENT                               *            
                                                                                
           MOVE SPACES        TO BP13K022-REC.                                  
           INITIALIZE            BP13K022-REC.                                  
                                                                                
           MOVE WS-K022-REC   TO BP13K022-REC.                                  
                                                                                
           IF WS-COUNTER NOT = WS-COUNTER1                                      
              IF WS-NUM-CNT(WS-COUNTER) < WS-MAX-CNT(WS-COUNTER)                
                 MOVE WS-NUM-OIC(WS-COUNTER)  TO K022-NUM-OIC                   
                 ADD 1 TO WS-NUM-CNT(WS-COUNTER)                                
                                              WS-NUM-TOT(WS-COUNTER)            
              ELSE                                                              
                 ADD 1 TO WS-COUNTER                                            
                 MOVE WS-NUM-OIC(WS-COUNTER)  TO K022-NUM-OIC                   
                 ADD 1 TO WS-NUM-CNT(WS-COUNTER)                                
                                              WS-NUM-TOT(WS-COUNTER)            
              END-IF                                                            
           ELSE                                                                 
              IF WS-NUM-CNT(WS-COUNTER) < WS-MAX-CNT(WS-COUNTER)                
                 MOVE WS-NUM-OIC(WS-COUNTER)  TO K022-NUM-OIC                   
                 ADD 1 TO WS-NUM-CNT(WS-COUNTER)                                
                                              WS-NUM-TOT(WS-COUNTER)            
              ELSE                                                              
                 MOVE 1          TO WS-COUNTER WS-COUNT                         
                 ADD 1           TO WS-ROUND                                    
                 MOVE WS-ROUND   TO WS-ROUND-ED                                 
                                                                                
                 DISPLAY ' ROUND : '  WS-ROUND-ED                               
                 DISPLAY '*---------------------------*'                        
                                                                                
                 PERFORM UNTIL WS-COUNT > WS-COUNTER1                           
                    DISPLAY ' OIC        : ' WS-NUM-OIC(WS-COUNT)               
                    DISPLAY ' MAX CNT    : ' WS-MAX-CNT(WS-COUNT)               
                    DISPLAY ' ASSIGNED   : ' WS-NUM-CNT(WS-COUNT)               
                    MOVE 0 TO WS-NUM-CNT(WS-COUNT)                              
                    ADD 1 TO WS-COUNT                                           
                 END-PERFORM                                                    
                                                                                
                 DISPLAY '*---------------------------*'                        
                                                                                
                                                                                
                 MOVE WS-NUM-OIC(WS-COUNTER)  TO K022-NUM-OIC                   
                 ADD 1 TO WS-NUM-CNT(WS-COUNTER)                                
                                              WS-NUM-TOT(WS-COUNTER)            
              END-IF                                                            
           END-IF.                                                              
                                                                                
           WRITE BP13K022-REC.                                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1 TO WS-K022-WRITE                                         
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR WRITING BP13K022, STATUS - '                   
                                            WS-K022-STATUS                      
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4200-WRITE-K022.                                                         
      *------------------------------------------------------------*            
      * NO MULTIPLE ROUND                                          *            
                                                                                
           MOVE SPACES        TO BP13K022-REC.                                  
           INITIALIZE            BP13K022-REC.                                  
                                                                                
           MOVE WS-K022-REC   TO BP13K022-REC.                                  
                                                                                
           IF WS-COUNTER NOT = WS-COUNTER1                                      
              IF WS-NUM-CNT(WS-COUNTER) < WS-MAX-CNT(WS-COUNTER)                
                 MOVE WS-NUM-OIC(WS-COUNTER)  TO K022-NUM-OIC                   
                 ADD 1 TO WS-NUM-CNT(WS-COUNTER)                                
                                              WS-NUM-TOT(WS-COUNTER)            
              ELSE                                                              
                 ADD 1 TO WS-COUNTER                                            
                 MOVE WS-NUM-OIC(WS-COUNTER)  TO K022-NUM-OIC                   
                 ADD 1 TO WS-NUM-CNT(WS-COUNTER)                                
                                              WS-NUM-TOT(WS-COUNTER)            
              END-IF                                                            
           ELSE                                                                 
              MOVE WS-NUM-OIC(WS-COUNTER)     TO K022-NUM-OIC                   
              ADD 1 TO WS-NUM-CNT(WS-COUNTER)                                   
                                              WS-NUM-TOT(WS-COUNTER)            
           END-IF.                                                              
                                                                                
           WRITE BP13K022-REC.                                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1 TO WS-K022-WRITE                                         
                 IF WS-F026-NUM-INTVL > 0                                       
                    ADD 1 TO  WS-TOT                                            
                 END-IF                                                         
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR WRITING BP13K022, STATUS - '                   
                                            WS-K022-STATUS                      
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4400-READ-K026.                                                          
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES               TO K026-KEY-FLD.                           
           MOVE WS-FROM-OIC          TO K026-NUM-FROM-OIC.                      
           MOVE WS-NUM-OIC(WS-COUNT) TO K026-NUM-TO-OIC.                        
           MOVE WS-F026-CDE-BANKLOAN TO K026-CDE-BANKLOAN.                      
                                                                                
           READ BP13K026.                                                       
                                                                                
           EVALUATE WS-K026-STATUS                                              
              WHEN 00                                                           
                 PERFORM 4500-DELETE-K026   THRU 4500-EXIT                      
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BP13K026, STATUS - '                   
                                            WS-K026-STATUS                      
                 MOVE WS-K026-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4500-DELETE-K026.                                                        
      *------------------------------------------------------------*            
                                                                                
           DELETE BP13K026.                                                     
                                                                                
           EVALUATE WS-K026-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-K026-DELETE                                        
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR DELETING BP13K026, STATUS - '                  
                                            WS-K026-STATUS                      
                 MOVE WS-K026-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4600-WRITE-K028.                                                         
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES               TO BP13K028-REC.                           
           INITIALIZE                   BP13K028-REC.                           
                                                                                
           MOVE K022-NUM-OIC         TO K028-NUM-OIC.                           
           MOVE K022-NUM-REGN        TO K028-NUM-REGN.                          
           MOVE WS-SYSTEM-DATE       TO K028-DTE-HISTORY.                       
           MOVE K022-CDE-ALLO-CAT    TO K028-CDE-ALLO-CAT.                      
           MOVE K022-CDE-BANKLOAN    TO K028-CDE-BANKLOAN.                      
           MOVE K022-CDE-ALERT       TO K028-CDE-ALERT.                         
           MOVE K022-NUM-USERID      TO K028-NUM-USERID.                        
           MOVE K022-DTE-UPDATE      TO K028-DTE-UPDATE.                        
                                                                                
           WRITE  BP13K028-REC.                                                 
                                                                                
           EVALUATE WS-K028-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-K028-WRITE                                         
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR WRITING  BP13K028, STATUS - '                  
                                            WS-K028-STATUS                      
                 MOVE WS-K028-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       5000-DISPLAY-SUMMARY.                                                    
      *------------------------------------------------------------*            
                                                                                
           DISPLAY '*------ FINAL ASSIGNMENT -------*'.                         
           MOVE 1 TO WS-COUNT.                                                  
           PERFORM UNTIL WS-COUNT > WS-COUNTER1                                 
              DISPLAY ' OIC        : ' WS-NUM-OIC(WS-COUNT)                     
              DISPLAY ' ASSIGNED   : ' WS-NUM-TOT(WS-COUNT)                     
              PERFORM 4400-READ-K026  THRU 4400-EXIT                            
              ADD 1 TO WS-COUNT                                                 
           END-PERFORM.                                                         
           DISPLAY '*-------------------------------*'.                         
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------*            
                                                                                
           CLOSE BP13K022                                                       
                 BP13K026                                                       
                 BP13K028                                                       
                 BP13K800                                                       
                 BP13F026                                                       
                 P13F022A.                                                      
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'CLOSE BP13K022 FAIL. STATUS-'                         
                                         WS-K022-STATUS                         
                 MOVE WS-K022-STATUS  TO RETURN-CODE                            
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K026-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'CLOSE BP13K026 FAIL. STATUS-'                         
                                         WS-K026-STATUS                         
                 MOVE WS-K026-STATUS  TO RETURN-CODE                            
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K028-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'CLOSE BP13K028 FAIL. STATUS-'                         
                                         WS-K028-STATUS                         
                 MOVE WS-K028-STATUS  TO RETURN-CODE                            
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'CLOSE BP13K800 FAIL. STATUS-'                         
                                         WS-K800-STATUS                         
                 MOVE WS-K800-STATUS  TO RETURN-CODE                            
           END-EVALUATE.                                                        
                                                                                
                                                                                
           DISPLAY '*----------- BP13C044 -------------*'.                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY ' 1. BP13F026 READ      : '  WS-F026-READ.                   
           DISPLAY ' 2. BP13K022 NOT FOUND : '  WS-K022-NFND.                   
           DISPLAY ' 3. BP13K800 CANCELLED : '  WS-K800-CAN.                    
           DISPLAY ' 4. BP13K800 NOT FOUND : '  WS-K800-NFND.                   
           DISPLAY ' 5. BP13K800 FOUND     : '  WS-K800-FOUND.                  
           DISPLAY ' 6. FD IN SOC NO FLAT  : '  WS-NOFLAT.                      
           DISPLAY ' 7. BP13K022 WRITE     : '  WS-K022-WRITE.                  
           DISPLAY ' 8. BP13F022 WRITE(PRT): '  WS-F022A-WRITE.                 
           DISPLAY ' 9. BP13K028 WRITE(HIS): '  WS-K022-WRITE.                  
           DISPLAY '10. BP13K026 DELETED   : '  WS-K026-DELETE.                 
           DISPLAY SPACES.                                                      
           DISPLAY 'CHK. 7 & 9 = 6 + 8'.                                        
           DISPLAY '*----------------------------------*'.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
