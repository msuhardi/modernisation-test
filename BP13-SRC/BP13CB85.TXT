      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13CB85.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  JANUARY 29, 2010.                                         
      *===============================================================*         
      *   OBJECTIVE:  TO COUNT NO OF BALLOT QTR OF NRIC IN BP13F595   *         
      *               UNDER BTO CASES.                                *         
      *===============================================================*         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F595                                  *         
      *                  2. BP13K813                                  *         
      *                                                               *         
      *     OUTPUT FILE: 1. P13F595A                                  *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP133835 29/01/2010 PCL3 NEW PROGRAM.                         *         
      * BP133835 23/03/2010 PCL3 TO READ BP13K825 AND GET NUM-INCOME. *         
      * BP133921 31/05/2010 PCL3 ADD 'NS' TO COULD NOT BOOK CODE.     *         
      * BP143041 28/07/2011 LSB1 INCLUDE UPGRADER TO COUNT            *         
      *                          F595A-CDE-INV-H, F595A-CDE-NOT-H     *         
      * BP134306 28/07/2011 LSB1 PUT INCOME IN F595-NUM-APPLN-SERIAL  *         
      * BP134306 16/08/2011 LSB1 NE - COULD NOT BOOK, PENDING         *         
      * BP134388 10/11/2011 LSB1 FOR BOOK CHG  BF OR SCH-ACC-NO       *         
      * BP134930 31/05/2013 LSB1 COUNT FOR CDE-INV-G, CDE-INV-H       *         
      * BP135039 16/12/2013 IMC1 ADD NEW BOOK-STATUS CODES            *         
      * BP135340 07/05/2014 IMC1 CHANGE BP13K816 TO BP13K813          *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F595  ASSIGN        TO BP13F595.                          
           SELECT P13F595A  ASSIGN        TO P13F595A.                          
           SELECT BP13K813  ASSIGN        TO BP13K813                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K813-KEY-FLD                       
                            FILE STATUS   IS WS-K813-STATUS.                    
           SELECT BP13K825  ASSIGN        TO BP13K825                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K825-KEY-FLD                       
                            FILE STATUS   IS WS-K825-STATUS.                    
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F595                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F595.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13K825.                                                           
                                                                                
       FD   P13F595A                                                            
            RECORDING MODE IS F.                                                
       01 P13F595A-REC.                                                         
          05 FILLER                             PIC X(405).                     
          05 F595A-NUM-APPLN-SERIAL             PIC X(05).                      
          05 FILLER                             PIC X(51).                      
          05 F595A-CDE-INV-BK                   PIC X(1).                       
          05 F595A-CDE-NOT-BK                   PIC X(1).                       
          05 F595A-CDE-INV-DBK                  PIC X(1).                       
          05 F595A-CDE-NOT-DBK                  PIC X(1).                       
          05 FILLER                             PIC X(1).                       
          05 F595A-CDE-NOT-CBK                  PIC X(1).                       
          05 F595A-NUM-QTR-REP                  PIC 9(2).                       
          05 F595A-CDE-INV-H                    PIC X(1).                       
          05 F595A-CDE-NOT-H                    PIC X(1).                       
          05 F595A-CDE-INV-G                    PIC X(1).                       
          05 F595A-CDE-NOT-G                    PIC X(1).                       
          05 F595A-CDE-INV-CBK                  PIC X(1).                       
          05 F595A-CDE-PENDING                  PIC X(1).                       
          05 F595A-NUM-REGN-REF                 PIC X(9).                       
          05 FILLER                             PIC X(8).                       
          05 F595A-NUM-ALLOC-TAG                PIC X(2).                       
          05 F595A-NUM-NE                       PIC X(1).                       
          05 FILLER                             PIC X(5).                       
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F595             PIC X(01) VALUE 'N'.                     
           05  WS-EOF-K825             PIC X(01) VALUE 'N'.                     
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K813-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K825-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F595-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(7)  VALUE ZEROES.                  
           05  WS-QTR-REP              PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-NOFLAT               PIC X(1)  VALUE 'N'.                     
           05  WS-INV-TAG              PIC X(1)  VALUE 'N'.                     
           05  WS-NIV-TAG              PIC X(1)  VALUE 'N'.                     
           05  WS-PENDING              PIC X(1)  VALUE 'N'.                     
           05  WS-NE-NOTBK             PIC X(1)  VALUE 'N'.                     
           05  WS-NE-COULD-NBK         PIC X(1)  VALUE 'N'.                     
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-PREV-KEY             PIC X(9)  VALUE SPACES.                  
           05  WS-KEY                  PIC X(9)  VALUE SPACES.                  
           05  WS-F595-QUEUE           PIC 9(5)  VALUE ZEROES.                  
           05  WS-K813-QUEUE           PIC 9(5)  VALUE ZEROES.                  
           05  WS-PAST-INV             PIC X(1)  VALUE SPACES.                  
           05  WS-PAST-NIV             PIC X(1)  VALUE SPACES.                  
           05  WS-NUM-INCOME           PIC 9(5)  VALUE ZEROES.                  
           05                          PIC X(1)  VALUE 'N'.                     
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F595      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F595 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F595                                                 
                       BP13K813                                                 
                       BP13K825                                                 
               OUTPUT  P13F595A.                                                
                                                                                
           IF WS-K813-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'ERROR OPENING - BP13K813 : ' WS-K813-STATUS              
              MOVE     WS-K813-STATUS            TO RETURN-CODE                 
              PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'ERROR OPENING - BP13K825 : ' WS-K825-STATUS              
              MOVE     WS-K825-STATUS            TO RETURN-CODE                 
              PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT                   
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F595.                                                      
      ******************************************************************        
                                                                                
           READ BP13F595             AT   END                                   
                MOVE 'Y'             TO   WS-EOF-F595                           
                GO                   TO   2000-EXIT.                            
                                                                                
           MOVE F595-NUM-NRIC1       TO   WS-KEY.                               
                                                                                
           ADD  1                    TO   WS-F595-READ.                         
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE WS-KEY                        TO WS-PREV-KEY.                   
           MOVE ZEROES                        TO WS-QTR-REP                     
                                                 WS-NUM-INCOME.                 
           MOVE SPACES                        TO WS-PAST-INV                    
                                                 WS-PAST-NIV.                   
                                                                                
           PERFORM 3100-CHK-BAL-QTR         THRU 3100-EXIT                      
             UNTIL WS-KEY NOT = WS-PREV-KEY                                     
                OR WS-EOF-F595 = 'Y'.                                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-CHK-BAL-QTR.                                                        
      ******************************************************************        
                                                                                
           ADD 1                                      TO WS-QTR-REP.            
           PERFORM 3200-CODE-HH                     THRU 3200-EXIT.             
                                                                                
           MOVE ZEROES                                TO WS-NUM-INCOME.         
           PERFORM 3700-STARTBR-BP13K825            THRU 3700-EXIT.             
           MOVE WS-NUM-INCOME                TO F595A-NUM-APPLN-SERIAL.         
                                                                                
           PERFORM 4000-WRITE-P13F595A              THRU 4000-EXIT.             
                                                                                
           IF WS-PAST-INV = SPACES                                              
              MOVE WS-INV-TAG                         TO WS-PAST-INV            
              MOVE WS-NIV-TAG                         TO WS-PAST-NIV            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F595               THRU 2000-EXIT.             
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-CODE-HH.                                                            
      ******************************************************************        
                                                                                
           MOVE SPACES                               TO P13F595A-REC.           
           INITIALIZE                                   P13F595A-REC.           
                                                                                
           MOVE SPACES                               TO WS-INV-TAG              
                                                        WS-NIV-TAG              
                                                        WS-NOFLAT               
                                                        WS-PENDING              
                                                        WS-NE-NOTBK             
                                                        WS-NE-COULD-NBK.        
                                                                                
           IF F595-CDE-BALLOT-HOUSEHOLD = SPACES OR LOW-VALUES                  
              MOVE F595-CDE-HOUSEHOLD      TO F595-CDE-BALLOT-HOUSEHOLD         
           END-IF.                                                              
                                                                                
           MOVE BP13F595-REC                      TO P13F595A-REC.              
                                                                                
           MOVE SPACES                            TO F595A-CDE-INV-BK           
                                                     F595A-CDE-NOT-BK           
                                                     F595A-CDE-INV-DBK          
                                                     F595A-CDE-NOT-DBK          
                                                     F595A-CDE-INV-CBK          
                                                     F595A-CDE-NOT-CBK          
                                                     F595A-CDE-INV-H            
                                                     F595A-CDE-NOT-H            
                                                     F595A-CDE-INV-G            
                                                     F595A-CDE-NOT-G            
                                                     F595A-CDE-PENDING          
                                                     F595A-NUM-NE.              
           MOVE ZEROES                        TO F595A-NUM-QTR-REP.             
           MOVE ZEROES                        TO F595A-NUM-APPLN-SERIAL.        
           PERFORM 3600-CHECK-STATUS          THRU 3600-EXIT.                   
                                                                                
           IF WS-QTR-REP =  1                                                   
              MOVE WS-INV-TAG                  TO WS-PAST-INV                   
              MOVE WS-NIV-TAG                  TO WS-PAST-NIV                   
           END-IF.                                                              
                                                                                
           IF WS-INV-TAG = 'Y'                                                  
              IF F595-NUM-BOOK-STATUS = 'BF' OR                                 
                      F595-NUM-REGN-REF NOT = SPACES                            
                 MOVE WS-PAST-INV              TO F595A-CDE-INV-BK              
                 MOVE WS-PAST-NIV              TO F595A-CDE-NOT-BK              
              ELSE                                                              
                 MOVE WS-PAST-INV              TO F595A-CDE-INV-DBK             
                 MOVE WS-PAST-NIV              TO F595A-CDE-NOT-DBK             
                 IF F595-NUM-BOOK-STATUS = 'NE' OR                              
                    BP13F595-REC(493:2) = 'NE'                                  
                    MOVE 'Y'                   TO WS-NE-NOTBK                   
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              MOVE WS-PAST-INV                 TO F595A-CDE-INV-CBK             
              MOVE WS-PAST-NIV                 TO F595A-CDE-NOT-CBK             
              IF F595-NUM-BOOK-STATUS = 'NE' OR                                 
                 BP13F595-REC(493:2) = 'NE'                                     
                 MOVE 'Y'                   TO WS-NE-COULD-NBK                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F595-CDE-BALLOT-HOUSEHOLD = 'G'                                   
              MOVE WS-INV-TAG                     TO F595A-CDE-INV-G            
              MOVE WS-NIV-TAG                     TO F595A-CDE-NOT-G            
           ELSE                                                                 
                                                                                
              MOVE WS-INV-TAG                     TO F595A-CDE-INV-H            
              MOVE WS-NIV-TAG                     TO F595A-CDE-NOT-H            
           END-IF.                                                              
                                                                                
           MOVE WS-QTR-REP                        TO F595A-NUM-QTR-REP.         
      *                                                                         
      * NE HAVE INVITE-DO-NOT-BOOK                                              
      *         INVITE-COUNLD-NOT-BOOK                                          
      *         PENDING                                                         
      *                                                                         
           IF WS-NE-NOTBK = 'Y'                                                 
               MOVE 'Y'                           TO F595A-NUM-NE               
               MOVE SPACES                        TO F595A-CDE-INV-DBK          
               MOVE SPACES                        TO F595A-CDE-NOT-DBK          
               MOVE SPACES                        TO F595A-CDE-PENDING          
           ELSE                                                                 
               IF WS-NE-COULD-NBK = 'Y'                                         
                   MOVE SPACES              TO F595A-NUM-NE                     
                   MOVE SPACES              TO F595A-CDE-PENDING                
               ELSE                                                             
                   MOVE WS-PENDING          TO F595A-CDE-PENDING                
               END-IF                                                           
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3300-CHECK-QUEUE.                                                        
      ******************************************************************        
                                                                                
           MOVE 'N'                         TO WS-NOFLAT.                       
                                                                                
           PERFORM 3400-READ-BP13K813     THRU 3400-EXIT.                       
                                                                                
           IF F595-NUM-NT-FT-QUEUE(1:1) NOT NUMERIC                             
              MOVE ZEROES                   TO F595-NUM-NT-FT-QUEUE(1:1)        
           END-IF.                                                              
                                                                                
           IF F595-NUM-NT-FT-QUEUE(2:1) NOT NUMERIC                             
              MOVE ZEROES                   TO F595-NUM-NT-FT-QUEUE(2:1)        
           END-IF.                                                              
                                                                                
           MOVE F595-NUM-NT-FT-QUEUE        TO WS-F595-QUEUE.                   
           PERFORM 3500-CHECK-QUEUE-K813  THRU 3500-EXIT.                       
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3400-READ-BP13K813.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                                TO BP13K813-REC.          
           INITIALIZE                                    BP13K813-REC.          
                                                                                
           MOVE F595-CDE-NT1              TO K813-NUM-ZONE.                     
      *    MOVE F595-CDE-FLAT-TYPE        TO K813-NUM-FLAT-TYPE.                
      *    MOVE F595-DTE-BALLOT           TO K813-DTE-BALLOT.                   
                                                                                
           START BP13K813 KEY >= K813-KEY-FLD                                   
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    READ BP13K813 NEXT RECORD                                   
                    IF K813-NUM-ZONE = F595-CDE-NT1                             
                       CONTINUE                                                 
                    ELSE                                                        
                       DISPLAY 'BP13K813 REC NOT FND: ' F595-CDE-NT1            
                    END-IF                                                      
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE WS-K813-STATUS               TO RETURN-CODE            
                    DISPLAY 'ERROR READING BP13K813: ' WS-K813-STATUS           
                            ' KEY : ' K813-NUM-ZONE                             
                    PERFORM  9999-CLOSE-ROUTINE THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3500-CHECK-QUEUE-K813.                                                   
      ******************************************************************        
                                                                                
           EVALUATE F595-CDE-CATEGORY                                           
               WHEN '1'                                                         
                    PERFORM 3510-CHECK-CATEGORY1 THRU 3510-EXIT                 
               WHEN '2'                                                         
                    PERFORM 3520-CHECK-CATEGORY2 THRU 3520-EXIT                 
               WHEN '3'                                                         
               WHEN '4'                                                         
                    PERFORM 3530-CHECK-CATEGORY3 THRU 3530-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3510-CHECK-CATEGORY1.                                                    
      ******************************************************************        
                                                                                
           IF K813-NUM-LAST-Q-CAT1 NOT = SPACES AND LOW-VALUES                  
              IF K813-NUM-LAST-Q-CAT1  NOT NUMERIC                              
                 MOVE ZEROES            TO K813-NUM-LAST-Q-CAT1                 
              END-IF                                                            
                                                                                
              MOVE K813-NUM-LAST-Q-CAT1 TO WS-K813-QUEUE                        
                                                                                
              IF (WS-K813-QUEUE > 0) AND                                        
                 (WS-F595-QUEUE > WS-K813-QUEUE)                                
                 MOVE 'Y'               TO WS-NOFLAT                            
              END-IF                                                            
           END-IF .                                                             
                                                                                
       3510-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3520-CHECK-CATEGORY2.                                                    
      ******************************************************************        
                                                                                
           IF K813-NUM-LAST-Q-CAT2 NOT = SPACES AND LOW-VALUES                  
              IF K813-NUM-LAST-Q-CAT2  NOT NUMERIC                              
                 MOVE ZEROES            TO K813-NUM-LAST-Q-CAT2                 
              END-IF                                                            
                                                                                
              MOVE K813-NUM-LAST-Q-CAT2 TO WS-K813-QUEUE                        
                                                                                
              IF (WS-K813-QUEUE > 0) AND                                        
                 (WS-F595-QUEUE > WS-K813-QUEUE)                                
                 MOVE 'Y'               TO WS-NOFLAT                            
              END-IF                                                            
                                                                                
           END-IF .                                                             
                                                                                
       3520-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3530-CHECK-CATEGORY3.                                                    
      ******************************************************************        
                                                                                
           IF K813-NUM-LAST-Q-CAT3 NOT = SPACES AND LOW-VALUES                  
              IF K813-NUM-LAST-Q-CAT3      NOT NUMERIC                          
                 MOVE ZEROES            TO K813-NUM-LAST-Q-CAT3                 
              END-IF                                                            
                                                                                
              MOVE K813-NUM-LAST-Q-CAT3 TO WS-K813-QUEUE                        
                                                                                
              IF (WS-K813-QUEUE > 0) AND                                        
                 (WS-F595-QUEUE > WS-K813-QUEUE)                                
                 MOVE 'Y'               TO WS-NOFLAT                            
              END-IF                                                            
           END-IF .                                                             
                                                                                
       3530-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3600-CHECK-STATUS.                                                       
      ******************************************************************        
                                                                                
           IF (F595-NUM-BOOK-STATUS = 'BF' OR 'LF' OR 'WF' OR                   
                                      'LA' OR 'LG' OR 'LS')                     
                   OR F595-NUM-REGN-REF NOT = SPACES                            
              MOVE 'Y'                                   TO WS-INV-TAG          
           ELSE                                                                 
              IF (F595-NUM-BOOK-STATUS = 'NC' OR 'NF' OR 'NV' OR                
                                         'NS' OR 'SC' OR 'SF')                  
                 MOVE 'Y'                                TO WS-NIV-TAG          
              ELSE                                                              
                 PERFORM 3300-CHECK-QUEUE              THRU 3300-EXIT           
                                                                                
                 IF WS-NOFLAT = 'Y'                                             
                    MOVE 'Y'                             TO WS-NIV-TAG          
                    MOVE 'Y'                             TO WS-PENDING          
                 ELSE                                                           
                    MOVE 'Y'                             TO WS-INV-TAG          
                 END-IF                                                         
                                                                                
                                                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3700-STARTBR-BP13K825.                                                   
      ******************************************************************        
                                                                                
           MOVE SPACES                     TO BP13K825-REC.                     
           INITIALIZE                         BP13K825-REC.                     
                                                                                
           MOVE 'N'                        TO WS-EOF-K825.                      
                                                                                
           MOVE F595-NUM-REGN              TO K825-NUM-REGN.                    
                                                                                
           START BP13K825 KEY >= K825-NUM-REGN.                                 
                                                                                
           EVALUATE WS-K825-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3800-READNEXT-BP13K825 THRU 3800-EXIT               
                    PERFORM 3900-PROCESS-BP13K825  THRU 3900-EXIT               
                            UNTIL WS-EOF-K825 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    MOVE ZEROES                TO F595A-NUM-APPLN-SERIAL        
                                                   WS-NUM-INCOME                
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13K825 : ' WS-K825-STATUS              
                          ' REGN : ' K825-NUM-REGN                              
                  PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT               
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3800-READNEXT-BP13K825.                                                  
      ******************************************************************        
                                                                                
           READ BP13K825 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-K825.                        
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3900-PROCESS-BP13K825.                                                   
      ******************************************************************        
                                                                                
           IF F595-NUM-REGN = K825-NUM-REGN                                     
              IF K825-AMT-INCOME IS NOT NUMERIC                                 
                 MOVE ZEROES                         TO K825-AMT-INCOME         
              END-IF                                                            
                                                                                
              ADD K825-AMT-INCOME                    TO WS-NUM-INCOME           
              PERFORM 3800-READNEXT-BP13K825       THRU 3800-EXIT               
           ELSE                                                                 
              MOVE 'Y'                               TO WS-EOF-K825             
           END-IF.                                                              
                                                                                
       3900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-WRITE-P13F595A.                                                     
      ******************************************************************        
                                                                                
           WRITE P13F595A-REC.                                                  
           ADD 1                                     TO WS-CNT-WRITE.           
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CB85'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F595 RECORDS READ............. : '            
                    WS-F595-READ.                                               
           DISPLAY '(2) NO OF P13F595A RECORDS WRITTEN.......... : '            
                    WS-CNT-WRITE.                                               
                                                                                
           CLOSE BP13F595                                                       
                 BP13K813                                                       
                 BP13K825                                                       
                 P13F595A.                                                      
                                                                                
           IF WS-K813-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'ERROR CLOSING - BP13K813 : ' WS-K813-STATUS              
              MOVE     WS-K813-STATUS            TO RETURN-CODE                 
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'ERROR CLOSING - BP13K825 : ' WS-K825-STATUS              
              MOVE     WS-K825-STATUS            TO RETURN-CODE                 
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
