       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH15.                                                 
      *AUTHOR.        PAULO CAMIA LEGASPI.                                      
      *DATE-WRITTEN.  11/03/2009.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                             
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: TO DECODE F800-NUM-NEW-TOWN WHEN SPACES        *            
      *             USING K813-CDE-NT FOR ALL BOOKING APPOINTMENT  *            
      *             AND F800-NUM-STATUS = 'C'.                     *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133555  PCL3  11032009  NEW PROGRAM                      *            
      * BP134353  PCL3  20110909  TO HANDLE FOR FLAT TYPE IF       *            
      *                           K813-NUM-FT-PREMIUM = 'P'        *            
      * BP135190  SMR2  04122013  EXPAND BP13K813 TO 1000          *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
           SELECT P13F800A  ASSIGN        TO P13F800A.                          
           SELECT BP13K813  ASSIGN        TO BP13K813                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K813-KEY-FLD                       
                            FILE STATUS   IS BP13K813-STATUS.                   
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   P13F800A                                                            
            RECORDING MODE IS F.                                                
       01 P13F800A-REC                 PIC X(2000).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-COUNTERS.                                                          
          05 WS-F800-READ              PIC 9(08) VALUE ZEROES.                  
          05 WS-K813-FND               PIC 9(08) VALUE ZEROES.                  
          05 WS-K813-NFND              PIC 9(08) VALUE ZEROES.                  
          05 WS-CNT-WRITE              PIC 9(08) VALUE ZEROES.                  
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 BP13K813-STATUS           PIC 9(02) VALUE ZEROES.                  
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-FT-PREMIUM             PIC X(1).                                
          05 WS-F800-EOF               PIC X(01) VALUE 'N'.                     
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
             UNTIL WS-F800-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13K813                                                 
                       BP13F800                                                 
               OUTPUT  P13F800A.                                                
                                                                                
           IF BP13K813-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K813 : ' BP13K813-STATUS             
              MOVE     BP13K813-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'              TO WS-F800-EOF.                                
                                                                                
           READ BP13F800 AT END                                                 
                MOVE 'Y'         TO WS-F800-EOF                                 
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                 TO WS-F800-READ.                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 3100-DECODE-NT           THRU 3100-EXIT                      
                                                                                
           IF WS-FT-PREMIUM = 'P'                                               
              MOVE 'P'                        TO F800-NUM-ROOM-TYPE             
           END-IF.                                                              
                                                                                
           PERFORM 3200-WRITE-P13F800A      THRU 3200-EXIT.                     
           PERFORM 2000-READ-BP13F800       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-DECODE-NT.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                         TO BP13K813-REC.                 
           INITIALIZE                             BP13K813-REC.                 
                                                                                
           MOVE SPACES                         TO K813-KEY-FLD                  
                                                  WS-FT-PREMIUM.                
                                                                                
           MOVE F800-NUM-BTO-ZONE              TO K813-NUM-ZONE.                
                                                                                
           START BP13K813 KEY >= K813-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K813-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3150-READ-BP13K813  THRU 3150-EXIT                  
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K813: ' BP13K813-STATUS         
                            ' KEY = ' K813-KEY-FLD                              
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3150-READ-BP13K813.                                                      
      *===============================================================*         
                                                                                
            READ  BP13K813 NEXT AT END                                          
                  MOVE SPACES               TO K813-KEY-FLD.                    
                                                                                
            EVALUATE BP13K813-STATUS                                            
                WHEN 00                                                         
                WHEN 02                                                         
                     IF K813-NUM-ZONE = F800-NUM-BTO-ZONE                       
                        ADD 1                         TO WS-K813-FND            
                                                                                
                        IF K813-NUM-FT-PREMIUM NOT = SPACES AND                 
                                                     LOW-VALUES                 
                           MOVE K813-NUM-FT-PREMIUM   TO WS-FT-PREMIUM          
                        END-IF                                                  
                                                                                
                        IF K813-CDE-NT NOT = SPACES AND LOW-VALUES              
                           MOVE K813-CDE-NT        TO F800-NUM-NEW-TOWN         
                        END-IF                                                  
                     ELSE                                                       
                        ADD 1                      TO WS-K813-NFND              
                     END-IF                                                     
                WHEN 23                                                         
                     ADD 1                            TO WS-K813-NFND           
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K813: ' BP13K813-STATUS         
                             ' KEY = ' K813-KEY-FLD                             
                     PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                
            END-EVALUATE.                                                       
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-WRITE-P13F800A.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES          TO P13F800A-REC.                                
           INITIALIZE              P13F800A-REC.                                
                                                                                
           IF F800-CDE-BALLOT-HOUSEHOLD = SPACES OR LOW-VALUES                  
              MOVE F800-NUM-HOUSEHOLD     TO F800-CDE-BALLOT-HOUSEHOLD          
           END-IF.                                                              
                                                                                
           WRITE P13F800A-REC              FROM BP13F800-MASTER.                
           ADD 1                             TO WS-CNT-WRITE.                   
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '------------------------------------------'.                
           DISPLAY ' BP13CH15 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ  FROM BP13F800 : ' WS-F800-READ.            
           DISPLAY 'NO OF RECS FOUND IN   BP13K813 : ' WS-K813-FND.             
           DISPLAY 'NO OF RECS NOT FOUND IN BP13K813:' WS-K813-NFND.            
           DISPLAY 'NO OF RECS WRITTEN TO P13F800A : ' WS-CNT-WRITE.            
                                                                                
           CLOSE BP13F800                                                       
                 P13F800A                                                       
                 BP13K813.                                                      
                                                                                
           IF BP13K813-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K813 : ' BP13K813-STATUS             
              MOVE BP13K813-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
