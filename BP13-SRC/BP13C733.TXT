       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C733.                                                 
       AUTHOR.        RAYATHATHA ASHOK.                                         
       DATE-WRITTEN.  30/09/92.                                                 
       DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  TO PRINT REPORT ON FLAT ALLOCATED UNDER       *                
      *          MCP SCHEME AFTER EACH PIGEONHOLING.           *                
      *                                                        *                
      *     INPUT FILES :                                      *                
      *      1. BP13K012                                       *                
      *      2. BP13F713                                       *                
      *                                                        *                
      *     OUTPUT FILES :                                     *                
      *      1. BP13L733                                       *                
      *                                                        *                
      * CHG-NO  BY   ON        DESCRIPTION                     *                
      * ----------------------------------                     *                
      * C9209D3 RAZ 30/09/92 TO CATER FOR MCP SCHEME.          *                
      * C930335 LSB 17/03/93 EXPAND BK13K760, BP13K010 FOR RDC *                
      * I920318 LJL 01/04/93 CONVERT TO COBOLII.               *                
      * C950137 GD  17/01/95 ADD PROCESSING FOR TCP            *                
      * C9506F9 LSB 07/07/95 ADD PROCESSING FOR CC1/CC2/CC3    *                
      *BP131064 ED  05082001 CHANGE BP13K720 TO BP13K760       *                
      *BP132596 SSD 23092004 CHANGE BP13K760 TO BP13K012 AND   *                
      *                      REMOVE BP13K010                   *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K012 ASSIGN TO BP13K012                                   
                ACCESS MODE  IS SEQUENTIAL                                      
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K012-KEY-FLD                                    
                FILE STATUS  IS BP13K012-STATUS.                                
                                                                                
           SELECT BP13F713 ASSIGN TO BP13F713.                                  
                                                                                
           SELECT BP13L733 ASSIGN TO BP13L733.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13K012                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K012.                                                           
                                                                                
       FD   BP13F713                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 20 CHARACTERS                                       
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F713.                                                           
                                                                                
       FD   BP13L733                                                            
            RECORD CONTAINS 133 CHARACTERS                                      
            LABEL RECORD IS OMITTED.                                            
                                                                                
       01   L733-PRINT-REC               PIC X(133).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  BP13K012-STATUS           PIC 9(02) VALUE ZEROS.                 
           05  WS-TIME.                                                         
               10  WS-TIME-HH            PIC 9(02) VALUE 0.                     
               10  WS-TIME-MM            PIC 9(02) VALUE 0.                     
               10  WS-TIME-SS            PIC 9(02) VALUE 0.                     
           05  WS-CNT-K012               PIC 9(05) VALUE 0.                     
           05  WS-CNT-PAGE               PIC 9(04) VALUE 0.                     
           05  WS-CNT-SNO                PIC 9(05) VALUE 0.                     
           05  WS-MAX-LINE               PIC 9(02) VALUE 48.                    
           05  WS-CNT-LINE               PIC 9(02) VALUE 54.                    
           05  WS-CDE-FT                 PIC X(02).                             
           05  WS-CDE-NT                 PIC X(03).                             
           05  WS-K012-EOF               PIC X     VALUE SPACES.                
               88  K012-EOF                        VALUE 'Y'.                   
           05  WS-NUM-REG                PIC 9(05) VALUE ZEROES.                
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-YY                     PIC X(02).                             
           05  WS-MM                     PIC X(02).                             
           05  WS-DD                     PIC X(02).                             
                                                                                
       01  WS-DTE-TODATE.                                                       
           05  WS-DD                     PIC X(02).                             
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-MM                     PIC X(02).                             
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-YY                     PIC X(02).                             
                                                                                
      ******************* REPORT LAYOUT *****************                       
                                                                                
       01  L733-HEAD1.                                                          
           05  FILLER                    PIC X(08) VALUE 'BP13L733'.            
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'HDBCAT3'.             
           05  FILLER                    PIC X(19) VALUE SPACES.                
           05  FILLER                    PIC X(42) VALUE                        
               'S Y S T E M     O F    C O M M I T M E N T'.                    
           05  FILLER                    PIC X(10) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'DATE : '.             
           05  L733-DTE-CURRENT          PIC X(08).                             
           05  FILLER                    PIC X(02) VALUE ' ('.                  
           05  L733-TME-HH               PIC 9(02).                             
           05  FILLER                    PIC X(01) VALUE ':'.                   
           05  L733-TME-MM               PIC 9(02).                             
           05  FILLER                    PIC X(02) VALUE ') '.                  
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'PAGE : '.             
           05  L733-CNT-PAGE             PIC ZZZ9.                              
                                                                                
       01  L733-HEAD2.                                                          
           05  FILLER                    PIC X(100) VALUE SPACES.               
           05  FILLER                    PIC X(23) VALUE                        
               'USER (APPLICATION TEAM)'.                                       
                                                                                
       01  L733-HEAD3.                                                          
           05  FILLER                    PIC X(30) VALUE SPACES.                
           05  FILLER                    PIC X(44) VALUE                        
               'REPORT ON FLAT ALLOTED UNDER MCP SCHEME'.                       
           05  FILLER                    PIC X(26) VALUE SPACES.                
           05  FILLER                    PIC X(10) VALUE                        
               'PROCESS : '.                                                    
           05  L733-HEAD-PROCESS         PIC X(15).                             
                                                                                
       01  L733-HEAD4.                                                          
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  FILLER                    PIC X(05) VALUE 'S/NO.'.               
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  FILLER                    PIC X(03) VALUE 'NT '.                 
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE 'FT'.                  
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  FILLER                    PIC X(03) VALUE 'ALC'.                 
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  FILLER                    PIC X(08) VALUE 'TOT-UNIT'.            
           05  FILLER                    PIC X(06) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'APP-MCP'.             
           05  FILLER                    PIC X(06) VALUE SPACES.                
           05  FILLER                    PIC X(08) VALUE 'APP-NMCP'.            
           05  FILLER                    PIC X(06) VALUE SPACES.                
           05  FILLER                    PIC X(08) VALUE 'FLAT-MCP'.            
           05  FILLER                    PIC X(06) VALUE SPACES.                
           05  FILLER                    PIC X(09) VALUE 'FLAT-NMCP'.           
           05  FILLER                    PIC X(06) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'REG-MCP'.             
           05  FILLER                    PIC X(06) VALUE SPACES.                
           05  FILLER                    PIC X(08) VALUE 'REG-NMCP'.            
                                                                                
       01  L733-HEAD4B.                                                         
           05  FILLER                    PIC X(23) VALUE SPACES.                
           05  FILLER                    PIC X(03) VALUE 'SCH'.                 
           05  FILLER                    PIC X(16) VALUE SPACES.                
           05  FILLER                    PIC X(09) VALUE 'NHH   UPG'.           
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(09) VALUE 'NHH   UPG'.           
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(09) VALUE 'NHH   UPG'.           
           05  FILLER                    PIC X(06) VALUE SPACES.                
           05  FILLER                    PIC X(09) VALUE 'NHH   UPG'.           
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  FILLER                    PIC X(09) VALUE 'NHH   UPG'.           
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(09) VALUE 'NHH   UPG'.           
                                                                                
       01  L733-DETAIL1.                                                        
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L733-SNO                  PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L733-NT                   PIC X(03).                             
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L733-FT                   PIC X(02).                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L733-ALLOC-SCH            PIC X(03).                             
           05  FILLER                    PIC X(07) VALUE SPACES.                
           05  L733-TOT-UNIT             PIC ZZZZ9.                             
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L733-APP-MCP-NHH          PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L733-APP-MCP-UPG          PIC ZZZZ9.                             
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L733-APP-NMCP-NHH         PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L733-APP-NMCP-UPG         PIC ZZZZ9.                             
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L733-FLAT-MCP-NHH         PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L733-FLAT-MCP-UPG         PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L733-FLAT-NMCP-NHH        PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L733-FLAT-NMCP-UPG        PIC ZZZZ9.                             
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L733-REG-MCP-NHH          PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L733-REG-MCP-UPG          PIC ZZZZ9.                             
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L733-REG-NMCP-NHH         PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L733-REG-NMCP-UPG         PIC ZZZZ9.                             
                                                                                
       01  L733-DETAIL2.                                                        
           05  FILLER                    PIC X(23) VALUE SPACES.                
           05  L733-ALLOC-SCH1           PIC X(03).                             
           05  FILLER                    PIC X(07) VALUE SPACES.                
           05  L733-TOT-UNIT1            PIC ZZZZ9.                             
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L733-APP-MCP1-NHH         PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L733-APP-MCP1-UPG         PIC ZZZZ9.                             
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L733-APP-NMCP1-NHH        PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L733-APP-NMCP1-UPG        PIC ZZZZ9.                             
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L733-FLAT-MCP1-NHH        PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L733-FLAT-MCP1-UPG        PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L733-FLAT-NMCP1-NHH       PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L733-FLAT-NMCP1-UPG       PIC ZZZZ9.                             
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L733-REG-MCP1-NHH         PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L733-REG-MCP1-UPG         PIC ZZZZ9.                             
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L733-REG-NMCP1-NHH        PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L733-REG-NMCP1-UPG        PIC ZZZZ9.                             
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-ROUTINE.                                                       
                                                                                
           PERFORM 1000-OPEN-FILES                                              
              THRU 1000-EXIT.                                                   
                                                                                
           PERFORM 2000-READ-BP13F713                                           
              THRU 2000-EXIT.                                                   
                                                                                
           PERFORM 3000-READ-BP13K012                                           
              THRU 3000-EXIT.                                                   
                                                                                
           PERFORM 4000-MAIN-ROUTINE                                            
              THRU 4000-EXIT                                                    
             UNTIL K012-EOF.                                                    
                                                                                
           PERFORM 9000-CLOSE-ROUTINE                                           
              THRU 9000-EXIT.                                                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  BP13K012                                                 
                       BP13F713                                                 
                OUTPUT BP13L733.                                                
                                                                                
           IF BP13K012-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K012,STATUS : '                        
                        BP13K012-STATUS                                         
              MOVE BP13K012-STATUS       TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT.                                                
                                                                                
           ACCEPT WS-DTE-CURRENT         FROM DATE.                             
           MOVE CORR WS-DTE-CURRENT        TO WS-DTE-TODATE.                    
           MOVE WS-DTE-TODATE              TO L733-DTE-CURRENT.                 
                                                                                
           ACCEPT  WS-TIME                 FROM TIME.                           
           MOVE WS-TIME-HH                 TO L733-TME-HH.                      
           MOVE WS-TIME-MM                 TO L733-TME-MM.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F713.                                                      
                                                                                
           READ BP13F713                AT END                                  
           GO TO 2000-EXIT.                                                     
                                                                                
            EVALUATE  F713-CDE-COHORT                                           
            WHEN '01'   MOVE 'COHORT 1       '    TO L733-HEAD-PROCESS          
            WHEN '02'   MOVE 'COHORT 2       '    TO L733-HEAD-PROCESS          
            WHEN '03'   MOVE 'COHORT 3       '    TO L733-HEAD-PROCESS          
            WHEN '04'   MOVE 'COHORT 4       '    TO L733-HEAD-PROCESS          
            WHEN '05'   MOVE 'COHORT 5       '    TO L733-HEAD-PROCESS          
            WHEN '06'   MOVE 'COHORT 6       '    TO L733-HEAD-PROCESS          
            WHEN '07'   MOVE 'COHORT 7       '    TO L733-HEAD-PROCESS          
            WHEN '08'   MOVE 'COHORT 8       '    TO L733-HEAD-PROCESS          
            WHEN 'MT'   MOVE 'MTF/SFF/RH     '    TO L733-HEAD-PROCESS          
            WHEN 'CC'   MOVE 'GRO            '    TO L733-HEAD-PROCESS          
            WHEN 'C1'   MOVE 'GRO MCP   '         TO L733-HEAD-PROCESS          
            WHEN 'C2'   MOVE 'GRO HPS-NONMCP '    TO L733-HEAD-PROCESS          
            WHEN 'C3'   MOVE 'GRO MCP/NONMCP '    TO L733-HEAD-PROCESS          
            WHEN 'PA'   MOVE 'MATURE NT - PA '    TO L733-HEAD-PROCESS          
            WHEN 'NH'   MOVE 'NHH/UPG        '    TO L733-HEAD-PROCESS          
            WHEN 'TR'   MOVE 'TRANSITIONAL   '    TO L733-HEAD-PROCESS          
            WHEN OTHER  MOVE '               '    TO L733-HEAD-PROCESS          
            END-EVALUATE.                                                       
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BP13K012.                                                      
                                                                                
            READ BP13K012 NEXT           AT END                                 
            MOVE 'Y'                     TO WS-K012-EOF                         
            GO TO 3000-EXIT.                                                    
                                                                                
            ADD 1                        TO WS-CNT-K012.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-MAIN-ROUTINE.                                                       
                                                                                
           IF  WS-CNT-LINE  >=  WS-MAX-LINE                                     
               ADD 1                     TO WS-CNT-PAGE                         
               PERFORM 5000-PRINT-HEADING                                       
                  THRU 5000-EXIT                                                
               MOVE 7                    TO WS-CNT-LINE.                        
                                                                                
           ADD 1                         TO WS-CNT-SNO.                         
                                                                                
           MOVE WS-CNT-SNO               TO L733-SNO.                           
           MOVE K012-CDE-NT-ZONE         TO L733-NT.                            
           MOVE K012-CDE-FLAT-TY         TO L733-FT.                            
           MOVE 'PA'                     TO L733-ALLOC-SCH.                     
                                                                                
           MOVE K012-NUM-TOT-PA          TO L733-TOT-UNIT.                      
                                                                                
           MOVE K012-NUM-APP-MCPH        TO L733-APP-MCP-NHH.                   
           MOVE K012-NUM-APP-MCPG        TO L733-APP-MCP-UPG.                   
           MOVE K012-NUM-APP-NOMCH       TO L733-APP-NMCP-NHH.                  
           MOVE K012-NUM-APP-NOMCG       TO L733-APP-NMCP-UPG.                  
                                                                                
           MOVE K012-NUM-TOT-MCPH        TO L733-FLAT-MCP-NHH.                  
           MOVE K012-NUM-TOT-MCPG        TO L733-FLAT-MCP-UPG.                  
           MOVE K012-NUM-TOT-NOMCH       TO L733-FLAT-NMCP-NHH.                 
           MOVE K012-NUM-TOT-NOMCG       TO L733-FLAT-NMCP-UPG.                 
                                                                                
           COMPUTE WS-NUM-REG = K012-REG-MCPH1 +                                
                                K012-REG-MCPH2 +                                
                                K012-REG-MCPH3.                                 
                                                                                
           MOVE WS-NUM-REG               TO L733-REG-MCP-NHH.                   
                                                                                
           COMPUTE WS-NUM-REG = K012-REG-MCPG1 +                                
                                K012-REG-MCPG2 +                                
                                K012-REG-MCPG3.                                 
                                                                                
           MOVE WS-NUM-REG               TO L733-REG-MCP-UPG.                   
                                                                                
           COMPUTE WS-NUM-REG = K012-REG-NOMCH1 +                               
                                K012-REG-NOMCH2 +                               
                                K012-REG-NOMCH3.                                
                                                                                
           MOVE WS-NUM-REG               TO L733-REG-NMCP-NHH.                  
                                                                                
           COMPUTE WS-NUM-REG = K012-REG-NOMCG1 +                               
                                K012-REG-NOMCG2 +                               
                                K012-REG-NOMCG3.                                
                                                                                
           MOVE WS-NUM-REG               TO L733-REG-NMCP-UPG.                  
                                                                                
           WRITE L733-PRINT-REC        FROM L733-DETAIL1 AFTER 2.               
                                                                                
           ADD 1                         TO WS-CNT-LINE.                        
                                                                                
           PERFORM 3000-READ-BP13K012                                           
              THRU 3000-EXIT.                                                   
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-PRINT-HEADING.                                                      
                                                                                
           MOVE WS-CNT-PAGE              TO L733-CNT-PAGE.                      
                                                                                
           WRITE L733-PRINT-REC        FROM L733-HEAD1 AFTER PAGE.              
           WRITE L733-PRINT-REC        FROM L733-HEAD2.                         
           WRITE L733-PRINT-REC        FROM L733-HEAD3.                         
           WRITE L733-PRINT-REC        FROM L733-HEAD4 AFTER 1.                 
           WRITE L733-PRINT-REC        FROM L733-HEAD4B.                        
           MOVE  SPACES                  TO L733-PRINT-REC.                     
           WRITE L733-PRINT-REC.                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
                                                                                
           CLOSE BP13K012                                                       
                 BP13F713                                                       
                 BP13L733.                                                      
                                                                                
           DISPLAY '*************** BP13C733 ***************'.                  
           DISPLAY ' '.                                                         
           DISPLAY 'NO. OF RECORDS READ FROM BP13K012 : ' WS-CNT-K012.          
           DISPLAY ' '.                                                         
                                                                                
           IF BP13K012-STATUS NOT = 0                                           
              DISPLAY '***  CLOSING ERROR, BP13K012-STATUS '                    
              BP13K012-STATUS.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
