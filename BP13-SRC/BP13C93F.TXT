       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C93F.                                                 
      *AUTHOR.        ZDD1.                                                     
      *DATE-WRITTEN.  20/05/2015.                                               
      ***************************************************************           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      ***************************************************************           
      * OBJECTIVE : MATCH THIS MONTH FILE BP13FW3A FROM BP13C92F    *           
      *           : WITH LAST MONTH FILE PASS TO SQL TABLE          *           
      *           : IF MATCH REPLACE BY THIS MONTH FILE             *           
      *           : IF NOT MATCH ADD ON NEW RECORD AND KEEP OLD REC *           
      * ----------------------------------------------------------- *           
      * CHG REF  DATE       BY   DESCRIPTION                        *           
      * -------- ---------- ---- -----------                        *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13FW3A          ASSIGN BP13FW3A.                            
           SELECT BP13FW31          ASSIGN BP13FW31.                            
           SELECT BP13OUT1          ASSIGN BP13OUT1.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13FW3A                RECORD CONTAINS 800 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13FW3A.                                                           
                                                                                
       FD   BP13FW31                RECORD CONTAINS 800 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       01 BP13FW31-REC PIC X(800).                                              
                                                                                
                                                                                
       FD   BP13OUT1                RECORD CONTAINS 800 CHARACTERS              
                                    LABEL RECORDS ARE OMITTED                   
                                    RECORDING MODE IS F.                        
       01   BP13OUT1-REC            PIC X(800).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FLAG-N-STATUS.                                                    
           05  WS-FW3A-EOF-FLAG             PIC X(01)  VALUE 'N'.               
               88  WS-FW3A-EOF              VALUE 'Y'.                          
           05  WS-FW31-EOF-FLAG             PIC X(01)  VALUE 'N'.               
               88  WS-FW31-EOF              VALUE 'Y'.                          
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FW3A-READ                 PIC 9(05)  VALUE ZEROS.             
           05  WS-FW31-READ                 PIC 9(05)  VALUE ZEROS.             
           05  WS-FW3A-UNMATCH              PIC 9(05)  VALUE ZEROS.             
           05  WS-FW31-UNMATCH              PIC 9(05)  VALUE ZEROS.             
           05  WS-FW31-MATCH                PIC 9(05)  VALUE ZEROS.             
           05  WS-BACK-WRTN                 PIC 9(05)  VALUE ZEROS.             
           05  WS-FW3A-WRTN                 PIC 9(05)  VALUE ZEROS.             
           05  WS-FW31-WRTN                 PIC 9(05)  VALUE ZEROS.             
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-YY                    PIC X(04)  VALUE SPACES.            
           05  WS-SYS-MM                    PIC X(02)  VALUE SPACES.            
           05  WS-SYS-DD                    PIC X(02)  VALUE SPACES.            
                                                                                
       PROCEDURE DIVISION.                                                      
      ***************************                                               
       0000-MAIN.                                                               
      ***************************                                               
           MOVE FUNCTION CURRENT-DATE(1:8)     TO  WS-SYSTEM-DATE.              
                                                                                
           PERFORM 1000-OPEN-FILES           THRU  1000-EXIT.                   
           PERFORM 2000-READ-BP13FW3A        THRU  2000-EXIT.                   
           PERFORM 2010-READ-BP13FW31        THRU  2010-EXIT.                   
           PERFORM 3000-PROCESS              THRU  3000-EXIT                    
             UNTIL WS-FW3A-EOF AND WS-FW31-EOF.                                 
           PERFORM 9000-CLOSE-FILES          THRU  9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       1000-OPEN-FILES.                                                         
      ***************************                                               
           OPEN INPUT BP13FW3A                                                  
                      BP13FW31                                                  
               OUTPUT BP13OUT1.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       2000-READ-BP13FW3A.                                                      
      ***************************                                               
           READ BP13FW3A                                                        
             AT END                                                             
                MOVE 'Y'                    TO  WS-FW3A-EOF-FLAG                
                MOVE HIGH-VALUES            TO  FW3A-NUM-REGN                   
            NOT AT END                                                          
                ADD   1                     TO  WS-FW3A-READ                    
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       2010-READ-BP13FW31.                                                      
      ***************************                                               
           READ BP13FW31                                                        
             AT END                                                             
                MOVE 'Y'                    TO  WS-FW31-EOF-FLAG                
                MOVE HIGH-VALUES            TO  BP13FW31-REC(1:8)               
            NOT AT END                                                          
                ADD  1                      TO  WS-FW31-READ                    
           END-READ.                                                            
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       3000-PROCESS.                                                            
      ***************************                                               
           IF FW3A-NUM-REGN = BP13FW31-REC(1:8)                                 
              ADD  1                          TO  WS-FW31-MATCH                 
              PERFORM 4100-OUTPUT-FW31         THRU  4100-EXIT                  
              PERFORM 2010-READ-BP13FW31       THRU  2010-EXIT                  
              PERFORM 2000-READ-BP13FW3A       THRU  2000-EXIT                  
           ELSE                                                                 
              IF FW3A-NUM-REGN > BP13FW31-REC(1:8)                              
                 ADD  1                    TO  WS-FW31-UNMATCH                  
                 PERFORM 4100-OUTPUT-FW31         THRU  4100-EXIT               
                 PERFORM 2010-READ-BP13FW31    THRU  2010-EXIT                  
              ELSE                                                              
                 IF FW3A-NUM-REGN < BP13FW31-REC(1:8)                           
                    ADD  1                       TO  WS-FW3A-UNMATCH            
                    PERFORM 4200-OUTPUT-FW3A      THRU 4200-EXIT                
                    PERFORM 2000-READ-BP13FW3A       THRU  2000-EXIT            
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       4100-OUTPUT-FW31.                                                        
      ***************************                                               
           MOVE SPACES                       TO  BP13OUT1-REC.                  
           MOVE BP13FW31-REC                 TO  BP13OUT1-REC.                  
           WRITE BP13OUT1-REC.                                                  
           ADD  1                            TO  WS-FW31-WRTN.                  
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       4200-OUTPUT-FW3A.                                                        
      ***************************                                               
           MOVE SPACES                       TO  BP13OUT1-REC.                  
           MOVE BP13FW3A-REC                 TO  BP13OUT1-REC.                  
           WRITE BP13OUT1-REC.                                                  
           ADD  1                            TO  WS-FW31-WRTN.                  
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       9000-CLOSE-FILES.                                                        
      ***************************                                               
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-YY.         
           DISPLAY '**************** MATFW31 ************************'.         
           DISPLAY ' RECORDS READ    IN BP13FW3A :   ' WS-FW3A-READ.            
           DISPLAY ' RECORDS READ    IN BP13FW31 :   ' WS-FW31-READ.            
           DISPLAY SPACES.                                                      
           DISPLAY ' FW3A NOT FOUND IN   FW31 : ' WS-FW3A-UNMATCH.              
           DISPLAY ' FW31 NOT FOUND IN   FW3A : ' WS-FW31-UNMATCH.              
           DISPLAY ' FW31 MATCHED WITH FW3A   : ' WS-FW31-MATCH.                
           DISPLAY SPACES.                                                      
           DISPLAY ' RECORD WRITTEN IN OUTPUT : ' WS-FW31-WRTN.                 
           DISPLAY '**************** -------- ************************'.        
                                                                                
           CLOSE BP13FW31                                                       
                 BP13FW3A                                                       
                 BP13OUT1.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
      ********************** END OF PROGRAM ***************************         
