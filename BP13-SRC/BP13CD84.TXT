       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD84.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  24/11/2010.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO SEND EMAIL OF PAYMENT OF SURVEY FEES            *        
      *                                                                *        
      * INPUT  FILES : BP13FOUT                                        *        
      *                BP13KA10                                        *        
      *                SY02F001                                        *        
      * OUTPUT FILES : BP13MAIL.                                       *        
      *                                                                *        
      * CHG-NO    BY    ON    - DESCRIPTION                            *        
      * ---------------------------------                              *        
      * BP133967 JB8  20101124  NEW PROGRAM                            *        
      * BP134269 IMC1 20120620  REMOVE LKC13 ADD "LSC13"               *        
      * BP134571 IMC1 20120712  REMOVE LSC13 ADD LKH2                  *        
      * BP135147 PCL4 20131126  TO CHANGE RECIPIENT FROM SC34 TO th1   *        
      * BP135339 IMC1 20140515  TO ADD RECIPIENT FOR NEW PROJECTS      *        
      * BP136082 PCL4 20160113  TO UPDATE EMAIL RECIPIENTS             *        
      * BP136142 PCL4 20160324  TO REMOVE SU1 AS EMAIL RECIPIENT       *        
      * BP136477 RJB1 20161018  TO REMOVE LIM KIM HUAT                 *        
      * BP138757 KV8  20210723  REPLACEING KATHIJAH TO SUZANNE         *        
      * BP139030 ZAR7 20220107  REPLACE CHUA YEE MIEN TO LIM KOK CHUN           
      * BP139276 KAC1 20240405  REPLACING LIM KOK CHUN BY              *        
      *                                   KATHIJAH_BEE_ALI_MOHAMED.    *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13FOUT ASSIGN       TO BP13FOUT.                            
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
           SELECT BP13KA10 ASSIGN       TO BP13KA10                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KA10-NUM-PRJCT                       
                           FILE STATUS  IS WS-KA10-STATUS.                      
           SELECT BP13MAIL ASSIGN       TO BP13MAIL.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13FOUT                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   BP13FOUT-REC.                                                       
            05  FOUT-NUM-PRJCT      PIC X(03).                                  
            05  FOUT-NUM-REGN       PIC X(08).                                  
            05  FOUT-NUM-SCH-ACC    PIC X(11).                                  
            05  FOUT-DTE-CONFIRM    PIC X(08).                                  
            05  FOUT-ADDRESS.                                                   
                10  FOUT-BLOCK-NO   PIC X(05).                                  
                10  FOUT-LEVEL      PIC X(02).                                  
                10  FOUT-MAIN-UNIT  PIC X(04).                                  
                10  FOUT-SUB-UNIT   PIC X(03).                                  
            05  FOUT-FLOOR-AREA     PIC 9(05)V99.                               
            05  FOUT-AMT-SURVEY     PIC 9(07)V99.                               
            05  FILLER              PIC X(40).                                  
                                                                                
       FD   BP13KA10                                                            
            RECORD CONTAINS 800 CHARACTERS.                                     
       COPY BP13KA10.                                                           
                                                                                
       FD   BP13MAIL                                                            
            RECORD CONTAINS 80  CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS IS OMITTED                                            
            RECORDING MODE IS F.                                                
       01   MAIL-PRTREC             PIC X(80).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-VARIABLES.                                                        
           05  CNT-FOUT-READ                PIC 9(07) VALUE ZEROS.              
           05  CNT-SNO                      PIC 9(07) VALUE ZEROS.              
           05  WS-FOUT-EOF                  PIC X(01) VALUE SPACE.              
               88  FOUT-EOF                           VALUE 'Y'.                
           05  WS-KA10-FND                  PIC X(01) VALUE SPACE.              
               88  KA10-FND                           VALUE 'Y'.                
           05  WS-NUM-PRJCT                 PIC X(03) VALUE SPACES.             
           05  WS-KA10-STATUS               PIC 9(02) VALUE ZEROES.             
           05  WS-DTE-CONFIRM               PIC X(08) VALUE SPACES.             
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1A-RCPT.                                                     
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(50)  VALUE                                 
                'CHONG_YENG_MUI@HDB.GOV.SG>'.                                   
           05  FILLER          PIC X(30)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1B-RCPT.                                                     
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(50)  VALUE                                 
                'IVY_M_BALANAG@HDB.GOV.SG>'.                                    
           05  FILLER          PIC X(30)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1C-RCPT.                                                     
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(50)  VALUE                                 
                'KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.                         
           05  FILLER          PIC X(30)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1D-RCPT.                                                     
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(50)  VALUE                                 
                'SERENA_JM_TEH@HDB.GOV.SG>'.                                    
           05  FILLER          PIC X(30)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1G-RCPT.                                                     
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(50)  VALUE                                 
      *         'KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.                         
                'SUZANNE_SC_HENG@HDB.GOV.SG>'.                                  
           05  FILLER          PIC X(30)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1H-RCPT.                                                     
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(20)  VALUE 'bala2@hdb.gov.sg>'.            
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1I-RCPT.                                                     
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(20)  VALUE 'KYP1@HDB.GOV.SG>'.             
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1J-RCPT.                                                     
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(20)  VALUE 'YD1@HDB.GOV.SG>'.              
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1K-RCPT.                                                     
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(20)  VALUE 'LWK10@HDB.GOV.SG>'.            
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1L-RCPT.                                                     
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(20)  VALUE 'SHA2@HDB.GOV.SG>'.             
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL3-DATA.                                                      
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4-FROM.                                                      
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(26)  VALUE                                 
               'SOC System - Email Alert'.                                      
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5A-TO1.                                                      
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(50)  VALUE                                 
                'CHONG_YENG_MUI@HDB.GOV.SG>'.                                   
           05  FILLER          PIC X(30)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5B-TO2.                                                      
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(50)  VALUE                                 
                'IVY_M_BALANAG@HDB.GOV.SG>'.                                    
           05  FILLER          PIC X(30)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5C-TO3.                                                      
           05  FILLER          PIC X(4)  VALUE 'TO:<'.                          
           05  FILLER          PIC X(20)  VALUE 'bala2@hdb.gov.sg>'.            
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5D-TO4.                                                      
           05  FILLER          PIC X(4)  VALUE 'TO:<'.                          
           05  FILLER          PIC X(20)  VALUE 'KYP1@HDB.GOV.SG>'.             
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5E-TO5.                                                      
           05  FILLER          PIC X(4)  VALUE 'TO:<'.                          
           05  FILLER          PIC X(20)  VALUE 'YD1@HDB.GOV.SG>'.              
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5F-TO6.                                                      
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(20)  VALUE 'LWK10@HDB.GOV.SG>'.            
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5G-TO7.                                                      
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(20)  VALUE 'SHA2@HDB.GOV.SG>'.             
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5C-CC1.                                                      
           05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
           05  FILLER          PIC X(50)  VALUE                                 
                'KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                         
           05  FILLER          PIC X(30)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5D-CC2.                                                      
           05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
           05  FILLER          PIC X(50)  VALUE                                 
                'SERENA_JM_TEH@HDB.GOV.SG>'.                                    
           05  FILLER          PIC X(30)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5D-CC5.                                                      
           05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
           05  FILLER          PIC X(50)  VALUE                                 
      *         'KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.                         
                'SUZANNE_SC_HENG@HDB.GOV.SG>'.                                  
           05  FILLER          PIC X(30)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6-SUBJECT.                                                   
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  FILLER          PIC X(35)  VALUE                                 
               'Payment of Survey fees to Developer'.                           
                                                                                
       01  MAIL-DTL7-DATE.                                                      
           05 FILLER                   PIC X(6)   VALUE 'DATE: '.               
           05 MAIL-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY1-TITLE-01.                                                 
           05 FILLER                   PIC X(80)  VALUE                         
              'The survey fees of the following registration numbers (ar        
      -       'range in ascending '.                                            
                                                                                
       01  MAIL-BODY2-TITLE-02.                                                 
           05 FILLER                   PIC X(80)  VALUE                         
              'order) are deducted from CPF/cash and ready for payment t        
      -       'o the developers.'.                                              
                                                                                
       01  MAIL-BODY3-DEVELOPER.                                                
           05 FILLER                   PIC X(12)  VALUE 'Developer : '.         
           05 MAIL-DEVELOPER           PIC X(40)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY4-PROJECT.                                                  
           05 FILLER                   PIC X(12)  VALUE 'Project   : '.         
           05 MAIL-CDE-PRJCT           PIC X(03)  VALUE SPACES.                 
           05 FILLER                   PIC X(03)  VALUE ' - '.                  
           05 MAIL-NME-PRJCT           PIC X(30)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY5-ADDRESS.                                                  
           05 FILLER                   PIC X(12)  VALUE 'Address   : '.         
           05 MAIL-STREET              PIC X(32)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY6-CONFIRM.                                                  
           05 FILLER                   PIC X(29)  VALUE                         
              'Date of Final Confirmation : '.                                  
           05 MAIL-DTE-CONFIRM         PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY7-HEADER.                                                   
           05 FILLER                   PIC X(80)  VALUE                         
              'S/N Regn No  Sch Acc No     Address            Floor Area        
      -       ' Survey Fee'.                                                    
                                                                                
       01  MAIL-BODY8-DASH.                                                     
           05 FILLER                   PIC X(80)  VALUE                         
              '--- -------- -------------- ------------------ ----------        
      -       ' ----------'.                                                    
                                                                                
       01  MAIL-BODY9-DETAIL.                                                   
           05  MAIL-SNO                PIC ZZ9    VALUE ZEROES.                 
           05  FILLER                  PIC X(01)  VALUE SPACES.                 
           05  MAIL-REGN-NO            PIC X(08)  VALUE SPACES.                 
           05  FILLER                  PIC X(01)  VALUE SPACES.                 
           05  MAIL-SCH-ACC            PIC X(14)  VALUE SPACES.                 
           05  FILLER                  PIC X(01)  VALUE SPACES.                 
           05  MAIL-FLAT-ADDR          PIC X(18)  VALUE SPACES.                 
           05  FILLER                  PIC X(05)  VALUE SPACES.                 
           05  MAIL-FLOOR-AREA         PIC ZZ9.99 VALUE ZEROES.                 
           05  FILLER                  PIC X(04)  VALUE SPACES.                 
           05  MAIL-AMT-SURVEY         PIC ZZZ9.99 VALUE ZEROS.                 
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 8000-OPEN-FILES            THRU 8000-EXIT.                   
                                                                                
           PERFORM 8001-READ-BP13FOUT         THRU 8001-EXIT                    
           PERFORM UNTIL FOUT-EOF                                               
              PERFORM 1000-PROCESS-RECORDS    THRU 1000-EXIT                    
              PERFORM 8001-READ-BP13FOUT      THRU 8001-EXIT                    
           END-PERFORM.                                                         
                                                                                
           IF CNT-SNO > 0                                                       
              WRITE MAIL-PRTREC FROM MAIL-END                                   
           END-IF.                                                              
                                                                                
           PERFORM 9000-CLOSE-FILES           THRU 9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-PROCESS-RECORDS.                                                    
      *-------------------------------------------------------------            
                                                                                
           IF FOUT-NUM-PRJCT NOT = WS-NUM-PRJCT                                 
              IF WS-NUM-PRJCT = SPACES OR LOW-VALUES                            
                 PERFORM 1100-PRINT-MAIL-HEADER   THRU 1100-EXIT                
              END-IF                                                            
              MOVE FOUT-NUM-PRJCT                   TO WS-NUM-PRJCT             
              PERFORM 1200-PRINT-MAIL-PRJCT       THRU 1200-EXIT                
              PERFORM 1300-PRINT-MAIL-HEADER      THRU 1300-EXIT                
              MOVE FOUT-DTE-CONFIRM                 TO WS-DTE-CONFIRM           
           END-IF.                                                              
                                                                                
           IF FOUT-DTE-CONFIRM NOT = WS-DTE-CONFIRM                             
              PERFORM 1300-PRINT-MAIL-HEADER      THRU 1300-EXIT                
              MOVE FOUT-DTE-CONFIRM                 TO WS-DTE-CONFIRM           
           END-IF.                                                              
                                                                                
           MOVE SPACES                  TO MAIL-BODY9-DETAIL                    
           INITIALIZE                      MAIL-BODY9-DETAIL.                   
                                                                                
           ADD 1                        TO CNT-SNO                              
           MOVE CNT-SNO                 TO MAIL-SNO.                            
           MOVE FOUT-NUM-REGN           TO MAIL-REGN-NO.                        
           STRING FOUT-NUM-SCH-ACC(1:4) '-'                                     
                  FOUT-NUM-SCH-ACC(5:4) '-'                                     
                  FOUT-NUM-SCH-ACC(9:1) '-'                                     
                  FOUT-NUM-SCH-ACC(10:2)                                        
           DELIMITED BY SIZE   INTO MAIL-SCH-ACC.                               
                                                                                
           STRING FOUT-BLOCK-NO                                                 
             ' #' FOUT-LEVEL                                                    
             '-'  FOUT-MAIN-UNIT FOUT-SUB-UNIT                                  
           DELIMITED BY SIZE INTO MAIL-FLAT-ADDR                                
                                                                                
           MOVE FOUT-FLOOR-AREA         TO MAIL-FLOOR-AREA                      
           MOVE FOUT-AMT-SURVEY         TO MAIL-AMT-SURVEY.                     
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-BODY9-DETAIL.                            
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1100-PRINT-MAIL-HEADER.                                                  
      *-------------------------------------------------------------            
                                                                                
           STRING F001-DTE-CURRENT(7:2) '/'                                     
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
           DELIMITED BY SIZE INTO MAIL-DATE                                     
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-HDR1                                     
           WRITE MAIL-PRTREC FROM MAIL-HDR2                                     
                                                                                
      * RCPT TO                                                                 
           WRITE MAIL-PRTREC FROM MAIL-DTL1A-RCPT                               
           WRITE MAIL-PRTREC FROM MAIL-DTL1B-RCPT                               
                                                                                
           EVALUATE FOUT-NUM-PRJCT                                              
               WHEN 'D07'                                                       
                   WRITE MAIL-PRTREC FROM MAIL-DTL1H-RCPT                       
               WHEN 'D08'                                                       
                   WRITE MAIL-PRTREC FROM MAIL-DTL1I-RCPT                       
               WHEN 'D09'                                                       
      * CYM11 INCLUDED ALREADY                                                  
                   CONTINUE                                                     
               WHEN 'D10'                                                       
                   WRITE MAIL-PRTREC FROM MAIL-DTL1J-RCPT                       
               WHEN 'D11'                                                       
                   WRITE MAIL-PRTREC FROM MAIL-DTL1K-RCPT                       
               WHEN 'D12'                                                       
                   WRITE MAIL-PRTREC FROM MAIL-DTL1L-RCPT                       
               WHEN 'D13'                                                       
                   WRITE MAIL-PRTREC FROM MAIL-DTL1H-RCPT                       
           END-EVALUATE.                                                        
                                                                                
      * RCPT CC                                                                 
           WRITE MAIL-PRTREC FROM MAIL-DTL1C-RCPT.                              
           WRITE MAIL-PRTREC FROM MAIL-DTL1D-RCPT.                              
           WRITE MAIL-PRTREC FROM MAIL-DTL1G-RCPT.                              
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL3-DATA.                               
           WRITE MAIL-PRTREC FROM MAIL-DTL4-FROM.                               
           WRITE MAIL-PRTREC FROM MAIL-DTL5A-TO1.                               
           WRITE MAIL-PRTREC FROM MAIL-DTL5B-TO2.                               
                                                                                
           EVALUATE FOUT-NUM-PRJCT                                              
               WHEN 'D07'                                                       
                   WRITE MAIL-PRTREC FROM MAIL-DTL5C-TO3                        
               WHEN 'D08'                                                       
                   WRITE MAIL-PRTREC FROM MAIL-DTL5D-TO4                        
               WHEN 'D09'                                                       
      * CYM11 INCLUDED ALREADY                                                  
                   CONTINUE                                                     
               WHEN 'D10'                                                       
                   WRITE MAIL-PRTREC FROM MAIL-DTL5E-TO5                        
               WHEN 'D11'                                                       
                   WRITE MAIL-PRTREC FROM MAIL-DTL5F-TO6                        
               WHEN 'D12'                                                       
                   WRITE MAIL-PRTREC FROM MAIL-DTL5G-TO7                        
               WHEN 'D13'                                                       
                   WRITE MAIL-PRTREC FROM MAIL-DTL5C-TO3                        
           END-EVALUATE.                                                        
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL5C-CC1.                               
           WRITE MAIL-PRTREC FROM MAIL-DTL5D-CC2.                               
           WRITE MAIL-PRTREC FROM MAIL-DTL5D-CC5.                               
           WRITE MAIL-PRTREC FROM MAIL-DTL6-SUBJECT.                            
           WRITE MAIL-PRTREC FROM MAIL-DTL7-DATE.                               
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1200-PRINT-MAIL-PRJCT.                                                   
      *-------------------------------------------------------------            
                                                                                
           PERFORM 8002-READ-BP13KA10       THRU 8002-EXIT.                     
           MOVE KA10-NME-DVLPR-PARENT         TO MAIL-DEVELOPER                 
           MOVE KA10-NUM-PRJCT                TO MAIL-CDE-PRJCT                 
           MOVE KA10-NME-PRJCT                TO MAIL-NME-PRJCT                 
           MOVE KA10-NME-STREET               TO MAIL-STREET                    
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY1-TITLE-01.                          
           WRITE MAIL-PRTREC FROM MAIL-BODY2-TITLE-02.                          
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY3-DEVELOPER.                         
           WRITE MAIL-PRTREC FROM MAIL-BODY4-PROJECT.                           
           WRITE MAIL-PRTREC FROM MAIL-BODY5-ADDRESS.                           
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1300-PRINT-MAIL-HEADER.                                                  
      *-------------------------------------------------------------            
                                                                                
           STRING FOUT-DTE-CONFIRM(7:2) '/'                                     
                  FOUT-DTE-CONFIRM(5:2) '/'                                     
                  FOUT-DTE-CONFIRM(1:4)                                         
                  DELIMITED BY SIZE INTO MAIL-DTE-CONFIRM.                      
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY6-CONFIRM.                           
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY7-HEADER.                            
           WRITE MAIL-PRTREC FROM MAIL-BODY8-DASH.                              
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
                                                                                
       1300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT BP13FOUT                                                  
                      SY02F001                                                  
                      BP13KA10                                                  
               OUTPUT BP13MAIL.                                                 
                                                                                
           IF WS-KA10-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KA10 - OPEN ERROR (' WS-KA10-STATUS ')'              
              MOVE WS-KA10-STATUS            TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8001-READ-BP13FOUT.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FOUT                                                        
             AT END                                                             
                MOVE 'Y'             TO WS-FOUT-EOF                             
                GO                   TO 8001-EXIT.                              
                                                                                
           ADD  1                    TO CNT-FOUT-READ.                          
                                                                                
       8001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8002-READ-BP13KA10.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE FOUT-NUM-PRJCT                TO KA10-NUM-PRJCT.                
                                                                                
           READ BP13KA10.                                                       
                                                                                
           IF WS-KA10-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-KA10-STATUS = 23                                            
                 MOVE SPACES                  TO KA10-NME-PRJCT                 
                                                 KA10-NME-DVLPR-PARENT          
              ELSE                                                              
                 DISPLAY 'BP13KA10 - READ ERROR (' WS-KA10-STATUS ')'           
                 MOVE WS-KA10-STATUS          TO RETURN-CODE                    
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8002-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CD84'.                                
           DISPLAY 'CURRENT DATE   : ' MAIL-DATE.                               
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BP13FOUT READ     : ' CNT-FOUT-READ.                 
                                                                                
           CLOSE BP13FOUT                                                       
                 SY02F001                                                       
                 BP13MAIL                                                       
                 BP13KA10.                                                      
                                                                                
           IF WS-KA10-STATUS NOT = 00                                           
              DISPLAY 'BP13KA10 - CLOSE ERROR (' WS-KA10-STATUS ')'             
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
