       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C45J.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  13/01/2017.                                               
      * ========================================================== *            
      * OBJECTIVE    : DAILY PROGRAM TO CREATE APPT FOR AUTO       *            
      *                SCHEDULED CASES FOR THE DAY                 *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13FC20                               *            
      *                 2.) BM06K110                               *            
      *                 3.) BP13K820                               *            
      *                 4.) BP13K857                               *            
      *                 5.) BP13K435                               *            
      * I-O    FILES :  1.) BP13K452                               *            
      *                 2.) BP13K454                               *            
      *                 3.) BP13K800                               *            
      *                 4.) BP13K310                               *            
      *                 5.) BP13KC30                               *            
      * OUTPUT FILES :  1.) BP13F203                               *            
      *                 2.) BP13F23B                               *            
      * LISTING      :  1.) BP13L45J                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136561 KSJ3 13/01/2017 NEW PROGRAM                       *            
      * BP136797 KSJ3 25/05/2017 TO CATER FOR K800-DTE-TP-ORIG     *            
      * BP136980 RJB1 12/10/2017 TO CATER FOR SATP                 *            
      * BP138334 KAC1 22/06/2020 TO CATER APPT-TYPE 'SP'           *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FC20  ASSIGN        TO BP13FC20.                          
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            ALTERNATE KEY IS K110-REGN-NO                       
                            FILE STATUS   IS WS-K110-STATUS.                    
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            FILE STATUS   IS WS-K820-STATUS.                    
                                                                                
           SELECT BP13K857  ASSIGN        TO BP13K857                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K857-KEY-FLD                       
                            FILE STATUS   IS WS-K857-STATUS.                    
                                                                                
           SELECT BP13K435  ASSIGN        TO BP13K435                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K435-KEY-FLD                       
                            FILE STATUS   IS WS-K435-STATUS.                    
                                                                                
           SELECT BP13K452  ASSIGN        TO BP13K452                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K452-KEY-FLD                       
                            FILE STATUS   IS WS-K452-STATUS.                    
                                                                                
           SELECT BP13K454  ASSIGN        TO BP13K454                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K454-KEY-FLD                       
                            ALTERNATE KEY IS K454-ALT1-KEY                      
                            FILE STATUS   IS WS-K454-STATUS.                    
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS WS-K800-STATUS.                    
                                                                                
           SELECT BP13K310  ASSIGN        TO BP13K310                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K310-KEY-FLD                       
                            FILE STATUS   IS WS-K310-STATUS.                    
                                                                                
           SELECT BP13KC30  ASSIGN        TO BP13KC30                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KC30-KEY-FLD                       
                            ALTERNATE KEY IS KC30-ALT-KEY2                      
                            FILE STATUS   IS WS-KC30-STATUS.                    
                                                                                
           SELECT BP13F203  ASSIGN        TO BP13F203.                          
                                                                                
           SELECT BP13F23B  ASSIGN        TO BP13F23B.                          
                                                                                
           SELECT BP13L45J  ASSIGN        TO BP13L45J.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FC20                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 150 CHARACTERS.                                      
           COPY BP13FC20.                                                       
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
           COPY BM06K110.                                                       
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
           COPY BP13K820.                                                       
                                                                                
       FD  BP13K857                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
           COPY BP13K857.                                                       
                                                                                
       FD  BP13K435                                                             
           RECORD CONTAINS 250 CHARACTERS.                                      
           COPY BP13K435.                                                       
                                                                                
       FD  BP13K452                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
           COPY BP13K452.                                                       
                                                                                
       FD  BP13K454                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
           COPY BP13K454.                                                       
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13K800.                                                       
                                                                                
       FD  BP13K310                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
           COPY BP13K310.                                                       
                                                                                
       FD  BP13KC30                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
           COPY BP13KC30.                                                       
                                                                                
       FD  BP13F203                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 200 CHARACTERS.                                      
           COPY BP13F203.                                                       
                                                                                
       FD  BP13F23B                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 200 CHARACTERS.                                      
           COPY BP13F23B.                                                       
                                                                                
       FD  BP13L45J                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13L45J-REC                  PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
      *-------------------------------------------------------------            
      *    AV02C002 COMM-AREA                                                   
      *-------------------------------------------------------------            
       COPY AV02COMM.                                                           
                                                                                
      *-------------------------------------------------------------            
      *    BP13C913 COMM-AREA                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH       PIC X(4)   VALUE SPACES.               
               10  WS-LINK-NUM-SCH       PIC X(5)   VALUE SPACES.               
                                                                                
       COPY P13COMM8.                                                           
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K110-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K820-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K857-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K435-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K452-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K454-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K800-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K310-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-KC30-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-FC20-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-FC20-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K820-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K857-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K452-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K454-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K800-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K310-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KC30-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F203-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F23B-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-L45J-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-L45J-LINE          PIC 9(2)   VALUE 60.                   
           05  WS-CNT-L45J-PAGE          PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-L45J-SNO           PIC 9(5)   VALUE ZEROES.               
           05  WS-I                      PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-FC20-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-KC30-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-KC30-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K452-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K454-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K435-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K435-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K800-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K820-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K857-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K110-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K310-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-PIDB-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-TIME-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-HP-FND                 PIC X(1)   VALUE 'N'.                  
           05  WS-EMAIL-FND              PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE ZEROES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-NUM-DESC               PIC X(60)  VALUE SPACES.               
           05  WS-SQL-CODE               PIC 9(4)   VALUE 0.                    
           05  WS-K435-DTE-KEY-BORROW    PIC X(8)   VALUE SPACES.               
           05  WS-K435-NUM-KEY-LOCN      PIC X(4)   VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      *    REPORT LAYOUT FOR EXCEPTIONS                                         
      *-------------------------------------------------------------            
       01  L45J-PR-HEAD-01.                                                     
           05  FILLER                    PIC X(14)   VALUE 'BP13L45J'.          
           05  FILLER                    PIC X(8)    VALUE 'HDB3    '.          
           05  FILLER                    PIC X(25)   VALUE SPACES.              
           05  FILLER                    PIC X(39)   VALUE                      
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(13)   VALUE SPACES.              
           05  FILLER                    PIC X(7)    VALUE 'DATE: '.            
           05  L45J-DTE-CURRENT          PIC X(10)   VALUE SPACES.              
           05  FILLER                    PIC X(3)    VALUE SPACES.              
           05  FILLER                    PIC X(7)    VALUE 'PAGE: '.            
           05  L45J-PAGE                 PIC ZZZ9.                              
                                                                                
       01  L45J-PR-HEAD-02.                                                     
           05  FILLER                    PIC X(41)   VALUE SPACES.              
           05  FILLER                    PIC X(50)   VALUE                      
              'EXCEPTION REPORT ON CREATING SELF BOOKING SMS FILE'.             
                                                                                
       01  L45J-PR-HEAD-03.                                                     
           05  FILLER                    PIC X(132)  VALUE                      
              '    S/NO   REGN      ERROR DESCRIPTION'.                         
                                                                                
       01  L45J-PR-DTL.                                                         
           05  FILLER                    PIC X(4)    VALUE SPACES.              
           05  L45J-SNO                  PIC ZZZZ9.                             
           05  FILLER                    PIC X(2)    VALUE SPACES.              
           05  L45J-NUM-REGN             PIC X(8).                              
           05  FILLER                    PIC X(2)    VALUE SPACES.              
           05  L45J-NUM-DESC             PIC X(60).                             
                                                                                
       01  L45J-PR-DASH-LINE.                                                   
           05  FILLER                    PIC X(4)    VALUE SPACES.              
           05  FILLER                    PIC X(90)   VALUE ALL '-'.             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 2000-BP13FC20-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-FC20-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13FC20                                                 
                       BM06K110                                                 
                       BP13K820                                                 
                       BP13K857                                                 
                       BP13K435                                                 
                I-O    BP13K452                                                 
                       BP13K454                                                 
                       BP13K800                                                 
                       BP13K310                                                 
                       BP13KC30                                                 
                OUTPUT BP13F203                                                 
                       BP13F23B                                                 
                       BP13L45J.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 ERROR ' WS-K820-STATUS                  
              MOVE     WS-K820-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K857 ERROR ' WS-K857-STATUS                  
              MOVE     WS-K857-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K435-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K435 ERROR ' WS-K435-STATUS                  
              MOVE     WS-K435-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K452-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K452 ERROR ' WS-K452-STATUS                  
              MOVE     WS-K452-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K454-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K454 ERROR ' WS-K454-STATUS                  
              MOVE     WS-K454-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K310-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K310 ERROR ' WS-K310-STATUS                  
              MOVE     WS-K310-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KC30-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC30 ERROR ' WS-KC30-STATUS                  
              MOVE     WS-KC30-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET CURRENT DATE                                                     
      *-------------------------------------------------------------            
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13FC20-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13FC20                                                        
              AT END MOVE 'Y'              TO   WS-FC20-EOF.                    
                                                                                
           IF WS-FC20-EOF NOT = 'Y'                                             
              ADD 1                        TO   WS-CNT-FC20-READ                
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO   WS-NUM-DESC.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET SCHEME ACCOUNT FROM SOC MASTER                                   
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13K800-MASTER.                
           MOVE FC20-NUM-REGN              TO   K800-NUM-REGN.                  
                                                                                
           PERFORM 3100-BP13K800-READ      THRU 3100-EXIT.                      
                                                                                
           IF WS-K800-FND = 'N'                                                 
              MOVE 'REGN NOT FOUND IN SOC MASTER'                               
                                           TO   WS-NUM-DESC                     
              PERFORM 7900-PRINT-ERROR     THRU 7900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    CHECK KEY LOCATION                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                        TO   WS-K435-EOF                     
                                                WS-K435-FND.                    
           MOVE SPACES                     TO   BP13K435-REC.                   
           MOVE K800-NUM-SCH-ACC           TO   K435-NUM-HDB-REF.               
                                                                                
           PERFORM 3210-BP13K435-STARTBR   THRU 3210-EXIT.                      
           PERFORM 3220-BP13K435-READNEXT  THRU 3220-EXIT                       
                   UNTIL WS-K435-EOF = 'Y'.                                     
                                                                                
           IF WS-K435-FND = 'Y'                                                 
              IF WS-K435-NUM-KEY-LOCN NOT = 'SOU '                              
                 MOVE 'KEY HAS BEEN BORROWED'                                   
                                           TO   WS-NUM-DESC                     
                 PERFORM 7900-PRINT-ERROR  THRU 7900-EXIT                       
                 GO TO 3000-CONTINUE-READ-NEXT                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    GET FLAT INVENTORY                                                   
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   K110-REC.                       
           MOVE K800-NUM-SCH-ACC           TO   K110-SCH-ACC-NO.                
                                                                                
           PERFORM 3300-BM06K110-READ      THRU 3300-EXIT.                      
                                                                                
           IF WS-K110-FND NOT = 'Y'                                             
              MOVE 'SCHEME ACCOUNT NOT FOUND IN FLAT INVENTORY'                 
                                           TO   WS-NUM-DESC                     
              PERFORM 7900-PRINT-ERROR     THRU 7900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    GET HANDPHONE                                                        
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13K820-REC.                   
           MOVE FC20-NUM-REGN              TO   K820-NUM-REGN.                  
           MOVE K800-NUM-NRIC1             TO   K820-NUM-NRIC.                  
                                                                                
           PERFORM 3400-BP13K820-READ      THRU 3400-EXIT.                      
                                                                                
           IF WS-K820-FND NOT = 'Y'                                             
              MOVE 'REGN NOT FOUND IN SOC OCCUP FILE'                           
                                           TO   WS-NUM-DESC                     
              PERFORM 7900-PRINT-ERROR     THRU 7900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
           IF K820-NUM-HP-PGR = SPACES OR LOW-VALUES OR ZEROES                  
              CONTINUE                                                          
           ELSE                                                                 
              IF K820-NUM-HP-PGR(1:1) NOT = '3' AND '8' AND '9'                 
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE 'Y'                  TO   WS-HP-FND                       
                 PERFORM 4000-CREATE-SMS   THRU 4000-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    GET EMAIL ADDRESS                                                    
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13K857-REC.                   
           MOVE FC20-NUM-REGN              TO   K857-NUM-REGN.                  
                                                                                
           PERFORM 3500-BP13K857-READ      THRU 3500-EXIT.                      
                                                                                
           IF (WS-K857-FND = 'Y') AND                                           
              (K857-NUM-EMAIL NOT = SPACES AND LOW-VALUES AND ZEROES)           
              MOVE 'Y'                     TO   WS-EMAIL-FND                    
              PERFORM 4100-CREATE-EMAIL    THRU 4100-EXIT                       
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    IF HP OR EMAIL = Y, CREATE BP13FC2A                                  
      *-------------------------------------------------------------            
           IF (WS-HP-FND = 'Y') OR (WS-EMAIL-FND = 'Y')                         
              PERFORM 4300-UPD-SELFBOOK-SLOT    THRU 4300-EXIT                  
              PERFORM 4400-UPD-WORKPLAN         THRU 4400-EXIT                  
              PERFORM 4500-UPD-APPT-TABLE       THRU 4500-EXIT                  
              PERFORM 4600-UPD-SOC-MASTER       THRU 4600-EXIT                  
              PERFORM 4700-UPD-TRANS-FILE       THRU 4700-EXIT                  
           ELSE                                                                 
              MOVE 'NO HANDPHONE AND EMAIL ADDRESS IN FILE'                     
                                           TO   WS-NUM-DESC                     
              PERFORM 7900-PRINT-ERROR     THRU 7900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
                                                                                
       3000-CONTINUE-READ-NEXT.                                                 
                                                                                
           PERFORM 2000-BP13FC20-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-BP13K800-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K800-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K800-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K800 ERROR '    WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3210-BP13K435-STARTBR.                                                   
      *=============================================================            
                                                                                
           START BP13K435 KEY >= K435-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K435-STATUS                                              
           WHEN 00                                                              
              CONTINUE                                                          
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO   WS-K435-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START BP13K435 ERROR '   WS-K435-STATUS                  
              MOVE    WS-K435-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3220-BP13K435-READNEXT.                                                  
      *=============================================================            
                                                                                
           READ BP13K435 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K435-STATUS                                              
           WHEN 00                                                              
              IF K435-NUM-HDB-REF NOT = K800-NUM-SCH-ACC                        
                 MOVE 'Y'                  TO   WS-K435-EOF                     
              ELSE                                                              
                 MOVE 'Y'                  TO   WS-K435-FND                     
                 IF K435-DTE-KEY-BORROW > WS-K435-DTE-KEY-BORROW                
                    MOVE K435-DTE-KEY-BORROW                                    
                                           TO   WS-K435-DTE-KEY-BORROW          
                    MOVE K435-NUM-KEY-LOCN TO   WS-K435-NUM-KEY-LOCN            
                 END-IF                                                         
              END-IF                                                            
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO   WS-K435-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READNEXT BP13K435 ERROR ' WS-K435-STATUS                 
              MOVE    WS-K435-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3220-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3300-BM06K110-READ.                                                      
      *=============================================================            
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K110-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K110-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BM06K110 ERROR '    WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3400-BP13K820-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K820-FND                     
              ADD   1                      TO   WS-CNT-K820-READ                
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K820-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K820 ERROR '    WS-K820-STATUS                  
              MOVE     WS-K820-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3500-BP13K857-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K857.                                                       
                                                                                
           EVALUATE WS-K857-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K857-FND                     
              ADD   1                      TO   WS-CNT-K857-READ                
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K857-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K857 ERROR '    WS-K857-STATUS                  
              MOVE     WS-K857-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4000-CREATE-SMS.                                                         
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13F203-REC.                   
           MOVE K800-NUM-ALLO-CAT          TO   F203-NUM-ALLO-MODE.             
           MOVE K800-NUM-NEW-TOWN          TO   F203-NUM-NEW-TOWN.              
           MOVE K800-NUM-FLAT-TYPE         TO   F203-NUM-FLAT-TYPE.             
           MOVE 'W2'                       TO   F203-TYPE-APPMT.                
           MOVE K800-NUM-CAT               TO   F203-NUM-ETHNIC.                
           MOVE FC20-NUM-REGN              TO   F203-NUM-REGN.                  
           MOVE K800-DTE-BALLOT            TO   F203-DTE-BALLOT.                
           MOVE FC20-DTE-APPT-AUTO         TO   F203-DTE-APPMT.                 
           MOVE K800-NUM-NT-FT-QUEUE       TO   F203-NUM-QUEUE.                 
           MOVE FC20-TME-APPT-AUTO         TO   F203-TME-APPMT.                 
           MOVE K800-NUM-NRIC1             TO   F203-NUM-NRIC.                  
           MOVE K820-NME-OCCP              TO   F203-NME-NRIC.                  
           MOVE K820-NUM-HP-PGR            TO   F203-NUM-HP.                    
           MOVE 'HA1'                      TO   F203-NUM-APLCNT.                
           MOVE K800-NUM-ZONE              TO   F203-NUM-ZONE.                  
           MOVE 'A'                        TO   F203-NUM-STATUS.                
                                                                                
           WRITE BP13F203-REC.                                                  
           ADD 1                           TO   WS-CNT-F203-WRITE.              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4100-CREATE-EMAIL.                                                       
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET ADDRESS                                                          
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   WS-LINK-REC                     
                                                BP13COMM8-REC.                  
           MOVE K800-NUM-SCH-ACC           TO   WS-LINK-REC.                    
                                                                                
           PERFORM 8100-CALL-BP13C913      THRU 8100-EXIT.                      
                                                                                
           IF WS-PIDB-FND NOT = 'Y'                                             
              GO TO 4100-EXIT.                                                  
                                                                                
      *-------------------------------------------------------------            
      *    WRITE ACTUAL RECORD                                                  
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13F23B-REC.                   
           MOVE FC20-NUM-REGN              TO   F23B-NUM-REGN.                  
           MOVE 'W2'                       TO   F23B-NUM-APPT-TYPE.             
           MOVE K857-NUM-EMAIL             TO   F23B-NUM-EMAIL.                 
           MOVE K110-DTE-ACCEPTANCE        TO   F23B-DTE-ACCEPTANCE.            
           MOVE COMM8-NUM-BLK              TO   F23B-NUM-BLK.                   
           MOVE COMM8-NUM-LEVEL            TO   F23B-NUM-LEVEL.                 
           MOVE COMM8-NUM-UNIT-MAIN        TO   F23B-NUM-UNIT-MAIN.             
           MOVE COMM8-NUM-UNIT-SUB         TO   F23B-NUM-UNIT-SUB.              
           MOVE COMM8-NME-STREET           TO   F23B-NME-STREET.                
           MOVE COMM8-NUM-POSTAL-CODE      TO   F23B-NUM-POSTAL-CODE.           
           MOVE FC20-DTE-APPT-AUTO         TO   F23B-DTE-APPT-AUTO.             
           MOVE FC20-TME-APPT-AUTO         TO   F23B-TME-APPT-AUTO.             
                                                                                
           WRITE BP13F23B-REC.                                                  
           ADD 1                           TO   WS-CNT-F23B-WRITE.              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4300-UPD-SELFBOOK-SLOT.                                                  
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13KC30-REC.                   
           MOVE FC20-DTE-SENT-SELF-BK      TO   KC30-DTE-SENT-SELF-BK.          
           MOVE FC20-NUM-APPT-TYPE         TO   KC30-NUM-APPT-TYPE.             
           MOVE FC20-DTE-APPT-AUTO         TO   KC30-DTE-APPT.                  
           MOVE FC20-TME-APPT-AUTO         TO   KC30-TME-APPT.                  
                                                                                
           MOVE 'N'                        TO   WS-KC30-EOF WS-KC30-FND.        
                                                                                
           PERFORM 4310-BP13KC30-STARTBR   THRU 4310-EXIT.                      
           PERFORM 4320-BP13KC30-READNEXT  THRU 4320-EXIT                       
                   UNTIL WS-KC30-EOF = 'Y'                                      
                      OR WS-KC30-FND = 'Y'.                                     
                                                                                
           IF WS-KC30-FND = 'Y'                                                 
              MOVE FC20-NUM-REGN           TO   KC30-NUM-REGN                   
              MOVE 'B'                     TO   KC30-NUM-STATUS                 
              MOVE 'AUTOBOOK'              TO   KC30-NUM-NRIC-SELF-BK           
              MOVE 'AUTOBOOK'              TO   KC30-NUM-USERID-SELF-BK         
              MOVE WS-DTE-CURR-8           TO   KC30-DTE-UPDATE-SELF-BK         
              MOVE FUNCTION CURRENT-DATE(9:8)                                   
                                           TO   KC30-TME-UPDATE-SELF-BK         
                                                                                
              PERFORM 4330-BP13KC30-REWRITE     THRU 4330-EXIT                  
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4310-BP13KC30-STARTBR.                                                   
      *=============================================================            
                                                                                
           START BP13KC30 KEY >= KC30-KEY-FLD.                                  
                                                                                
           EVALUATE WS-KC30-STATUS                                              
           WHEN 00                                                              
              CONTINUE                                                          
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO   WS-KC30-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START BP13KC30 ERROR '   WS-KC30-STATUS                  
              MOVE    WS-KC30-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4310-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4320-BP13KC30-READNEXT.                                                  
      *=============================================================            
                                                                                
           READ BP13KC30 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-KC30-STATUS                                              
           WHEN 00                                                              
              IF FC20-DTE-SENT-SELF-BK NOT = KC30-DTE-SENT-SELF-BK OR           
                 FC20-NUM-APPT-TYPE    NOT = KC30-NUM-APPT-TYPE    OR           
                 FC20-DTE-APPT-AUTO    NOT = KC30-DTE-APPT         OR           
                 FC20-TME-APPT-AUTO    NOT = KC30-TME-APPT                      
                 MOVE 'Y'                  TO   WS-KC30-EOF                     
              ELSE                                                              
                 IF KC30-NUM-STATUS NOT = 'B'                                   
                    MOVE 'Y'               TO   WS-KC30-FND                     
                 END-IF                                                         
              END-IF                                                            
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO   WS-KC30-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READNEXT BP13KC30 ERROR ' WS-KC30-STATUS                 
              MOVE    WS-KC30-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4320-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4330-BP13KC30-REWRITE.                                                   
      *=============================================================            
                                                                                
           REWRITE BP13KC30-REC.                                                
                                                                                
           EVALUATE WS-KC30-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-KC30-UPD                 
                                                                                
           WHEN OTHER                                                           
              STRING 'REWRITE ERROR ON SBK SLOT: '                              
                      WS-KC30-STATUS '/' KC30-KEY-FLD                           
                      DELIMITED BY SIZE    INTO WS-NUM-DESC                     
              PERFORM 7900-PRINT-ERROR     THRU 7900-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4330-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4400-UPD-WORKPLAN.                                                       
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13K452-REC.                   
           MOVE FC20-DTE-APPT-AUTO         TO   K452-DTE-APPT.                  
                                                                                
           MOVE 'N'                        TO   WS-K452-FND WS-TIME-FND.        
                                                                                
           PERFORM 4410-BP13K452-READ      THRU 4410-EXIT.                      
                                                                                
           IF WS-K452-FND = 'Y'                                                 
      *-------------------------------------------------------------            
      *    ADD 1 TO TOTAL BOOKED (AM)                                           
      *-------------------------------------------------------------            
              IF FC20-TME-APPT-AUTO > 0759 AND                                  
                 FC20-TME-APPT-AUTO < 1201                                      
                 PERFORM VARYING WS-I FROM 1 BY 1                               
                           UNTIL WS-I > 18                                      
                              OR WS-TIME-FND = 'Y'                              
                    IF FC20-TME-APPT-AUTO = K452-TME-SLOT(WS-I)                 
                       MOVE 'Y'               TO   WS-TIME-FND                  
                                                                                
                       IF FC20-NUM-APPT-TYPE = 'TP' OR 'SP'                     
                          ADD 1            TO   K452-NUM-TOT-TP-SB(WS-I)        
                       ELSE                                                     
                          ADD 1            TO   K452-NUM-TOT-SA-SB(WS-I)        
                       END-IF                                                   
                    END-IF                                                      
                 END-PERFORM                                                    
              END-IF                                                            
                                                                                
      *-------------------------------------------------------------            
      *    ADD 1 TO TOTAL BOOKED (PM)                                           
      *-------------------------------------------------------------            
              IF FC20-TME-APPT-AUTO > 1200 AND                                  
                 FC20-TME-APPT-AUTO < 1701                                      
                 PERFORM VARYING WS-I FROM 1 BY 1                               
                           UNTIL WS-I > 18                                      
                              OR WS-TIME-FND = 'Y'                              
                    IF FC20-TME-APPT-AUTO = K452-PM-TME-SLOT(WS-I)              
                       MOVE 'Y'               TO   WS-TIME-FND                  
                                                                                
                       IF FC20-NUM-APPT-TYPE = 'TP' OR 'SP'                     
                          ADD 1            TO   K452-PM-TOT-TP-SB(WS-I)         
                       ELSE                                                     
                          ADD 1            TO   K452-PM-TOT-SA-SB(WS-I)         
                       END-IF                                                   
                    END-IF                                                      
                 END-PERFORM                                                    
              END-IF                                                            
                                                                                
      *-------------------------------------------------------------            
      *    ADD 1 TO TOTAL BOOKED (OT)                                           
      *-------------------------------------------------------------            
              IF FC20-TME-APPT-AUTO > 1700 AND                                  
                 FC20-TME-APPT-AUTO < 2001                                      
                 PERFORM VARYING WS-I FROM 1 BY 1                               
                           UNTIL WS-I > 18                                      
                              OR WS-TIME-FND = 'Y'                              
                    IF FC20-TME-APPT-AUTO = K452-OT-TME-SLOT(WS-I)              
                       MOVE 'Y'               TO   WS-TIME-FND                  
                                                                                
                       IF FC20-NUM-APPT-TYPE = 'TP' OR 'SP'                     
                          ADD 1            TO   K452-OT-TOT-TP-SB(WS-I)         
                       ELSE                                                     
                          ADD 1            TO   K452-OT-TOT-SA-SB(WS-I)         
                       END-IF                                                   
                    END-IF                                                      
                 END-PERFORM                                                    
              END-IF                                                            
                                                                                
              PERFORM 4420-BP13K452-REWRITE     THRU 4420-EXIT                  
           END-IF.                                                              
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4410-BP13K452-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K452.                                                       
                                                                                
           EVALUATE WS-K452-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K452-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K452-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K452 ERROR '    WS-K452-STATUS                  
              MOVE    WS-K452-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4410-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4420-BP13K452-REWRITE.                                                   
      *=============================================================            
                                                                                
           REWRITE BP13K452-REC.                                                
                                                                                
           EVALUATE WS-K452-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K452-UPD                 
                                                                                
           WHEN OTHER                                                           
              STRING 'REWRITE ERROR ON WORKPLAN: '                              
                      WS-K452-STATUS '/' K452-KEY-FLD                           
                      DELIMITED BY SIZE    INTO WS-NUM-DESC                     
              PERFORM 7900-PRINT-ERROR     THRU 7900-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4420-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4500-UPD-APPT-TABLE.                                                     
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13K454-REC.                   
           MOVE FC20-DTE-APPT-AUTO         TO   K454-DTE-APPT.                  
           MOVE FC20-TME-APPT-AUTO         TO   K454-TME-APPT.                  
           MOVE FC20-NUM-APPT-TYPE         TO   K454-CDE-APPT-TYPE.             
           MOVE FC20-NUM-REGN              TO   K454-NUM-REGN.                  
           MOVE KC30-DTE-APPT              TO   K454-DTE-APPT2.                 
           MOVE K110-ESTATE                TO   K454-CDE-ESTATE.                
           MOVE K800-NUM-SCH-ACC           TO   K454-SCH-ACCT.                  
           MOVE 'W'                        TO   K454-CDE-APPT                   
                                                K454-CDE-SUB-APPT-TYPE.         
           MOVE SPACES                     TO   K454-NUM-UPDATE-BP13K800        
           MOVE 'Y'                        TO   K454-CDE-PHLINE-AVAIL.          
           MOVE 'Y'                        TO   K454-NUM-SELF-BOOK.             
           MOVE WS-DTE-CURR-8              TO   K454-DTE-UPDATE.                
           MOVE 'AUTOBOOK'                 TO   K454-NUM-USERID.                
                                                                                
           PERFORM 4510-BP13K454-WRITE     THRU 4510-EXIT.                      
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4510-BP13K454-WRITE.                                                     
      *=============================================================            
                                                                                
           WRITE BP13K454-REC.                                                  
                                                                                
           EVALUATE WS-K454-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K454-WRITE               
                                                                                
           WHEN OTHER                                                           
              STRING 'WRITE ERROR ON APPT FILE: '                               
                      WS-K454-STATUS '/' K454-KEY-FLD                           
                      DELIMITED BY SIZE    INTO WS-NUM-DESC                     
              PERFORM 7900-PRINT-ERROR     THRU 7900-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4510-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4600-UPD-SOC-MASTER.                                                     
      *=============================================================            
                                                                                
           IF K800-DTE-1ST-TP-LETTER = SPACES OR LOW-VALUES OR ZEROES           
              MOVE WS-DTE-CURR-8           TO   K800-DTE-1ST-TP-LETTER          
           ELSE                                                                 
              IF K800-CDE-APPT-TYPE NOT = 'TP' OR 'SP'                          
                 MOVE WS-DTE-CURR-8        TO   K800-DTE-1ST-TP-LETTER          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE 'W'                        TO   K800-CDE-APPT.                  
                                                                                
           EVALUATE FC20-NUM-APPT-TYPE                                          
           WHEN 'TP'                                                            
              MOVE 'WT'                    TO   K800-CDE-APPT-TYPE              
              MOVE FC20-DTE-APPT-AUTO      TO   K800-DTE-SO-APPT                
              MOVE FC20-TME-APPT-AUTO      TO   K800-TME-SO-APPT                
           WHEN 'SP'                                                            
              MOVE 'WP'                    TO   K800-CDE-APPT-TYPE              
              MOVE FC20-DTE-APPT-AUTO      TO   K800-DTE-SO-APPT                
              MOVE FC20-TME-APPT-AUTO      TO   K800-TME-SO-APPT                
           END-EVALUATE.                                                        
                                                                                
           IF K800-DTE-TP-ORIG = SPACES OR LOW-VALUES OR ZEROES                 
              MOVE FC20-DTE-APPT-AUTO      TO   K800-DTE-TP-ORIG                
           END-IF.                                                              
                                                                                
           PERFORM 4610-BP13K800-REWRITE   THRU 4610-EXIT.                      
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4610-BP13K800-REWRITE.                                                   
      *=============================================================            
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K800-UPD                 
                                                                                
           WHEN OTHER                                                           
              STRING 'REWRITE ERROR ON SOC MASTER: '                            
                      WS-K800-STATUS                                            
                      DELIMITED BY SIZE    INTO WS-NUM-DESC                     
              PERFORM 7900-PRINT-ERROR     THRU 7900-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4610-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4700-UPD-TRANS-FILE.                                                     
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13K310-TRANS-REC.             
           MOVE FC20-NUM-APPT-TYPE         TO   K310-CDE-TRANS.                 
           MOVE FC20-NUM-REGN              TO   K310-REGN-NO.                   
                                                                                
           PERFORM 4710-BP13K310-READ      THRU 4710-EXIT.                      
                                                                                
      *-------------------------------------------------------------            
      *    MOVE TRANS VALUES                                                    
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13K310-TRANS-REC.             
           MOVE FC20-NUM-REGN              TO   K310-REGN-NO.                   
           MOVE FC20-NUM-APPT-TYPE         TO   K310-CDE-TRANS.                 
           MOVE K800-NUM-SCH-ACC           TO   K310-SCH-ACC.                   
           MOVE FC20-DTE-APPT-AUTO         TO   K310-DTE-TRANS.                 
           MOVE WS-DTE-CURR-8              TO   K310-DTE-UPDATE.                
           MOVE 'BP13C45J'                 TO   K310-USERID.                    
                                                                                
           IF FC20-NUM-APPT-TYPE = 'TP' OR 'SP' OR 'SR' OR 'SL'                 
              IF K110-DTE-SO-EXECUTING = ZEROES                                 
                 IF K110-ALLOCN-TYPE = 'I'                                      
                    MOVE '10'              TO   K310-CDE-SALES-TYPE             
                 ELSE                                                           
                    MOVE '01'              TO   K310-CDE-SALES-TYPE             
              ELSE                                                              
                 MOVE    '10'              TO   K310-CDE-SALES-TYPE             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-K310-FND NOT = 'Y'                                             
              PERFORM 4720-BP13K310-WRITE       THRU 4720-EXIT                  
           ELSE                                                                 
              PERFORM 4730-BP13K310-REWRITE     THRU 4730-EXIT                  
           END-IF.                                                              
                                                                                
       4700-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4710-BP13K310-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K310.                                                       
                                                                                
           EVALUATE WS-K310-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K310-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K310-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K310 ERROR '    WS-K310-STATUS                  
              MOVE    WS-K310-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4710-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4720-BP13K310-WRITE.                                                     
      *=============================================================            
                                                                                
           WRITE BP13K310-TRANS-REC.                                            
                                                                                
           EVALUATE WS-K310-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K310-WRITE               
                                                                                
           WHEN OTHER                                                           
              STRING 'WRITE ERROR ON TRANS FILE: '                              
                      WS-K310-STATUS                                            
                      DELIMITED BY SIZE    INTO WS-NUM-DESC                     
              PERFORM 7900-PRINT-ERROR     THRU 7900-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4720-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4730-BP13K310-REWRITE.                                                   
      *=============================================================            
                                                                                
           REWRITE BP13K310-TRANS-REC.                                          
                                                                                
           EVALUATE WS-K310-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K310-WRITE               
                                                                                
           WHEN OTHER                                                           
              STRING 'REWRITE ERROR ON TRANS FILE: '                            
                      WS-K310-STATUS                                            
                      DELIMITED BY SIZE    INTO WS-NUM-DESC                     
              PERFORM 7900-PRINT-ERROR     THRU 7900-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4730-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       7900-PRINT-ERROR.                                                        
      *=============================================================            
                                                                                
           IF WS-CNT-L45J-LINE > 54                                             
              PERFORM 7910-PRINT-ERROR-HDR THRU 7910-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-L45J-SNO                 
                                                WS-CNT-L45J-LINE                
                                                WS-CNT-FC20-SKIP.               
                                                                                
           MOVE WS-CNT-L45J-SNO            TO   L45J-SNO.                       
           MOVE FC20-NUM-REGN              TO   L45J-NUM-REGN.                  
           MOVE WS-NUM-DESC                TO   L45J-NUM-DESC.                  
                                                                                
           WRITE BP13L45J-REC              FROM L45J-PR-DTL.                    
           ADD 1                           TO   WS-CNT-L45J-WRITE.              
                                                                                
       7900-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       7910-PRINT-ERROR-HDR.                                                    
      *=============================================================            
                                                                                
           ADD 1                           TO   WS-CNT-L45J-PAGE.               
           MOVE WS-CNT-L45J-PAGE           TO   L45J-PAGE.                      
           MOVE WS-DTE-CURR-10             TO   L45J-DTE-CURRENT.               
                                                                                
           WRITE BP13L45J-REC FROM L45J-PR-HEAD-01 AFTER PAGE.                  
           WRITE BP13L45J-REC FROM L45J-PR-HEAD-02.                             
           WRITE BP13L45J-REC FROM L45J-PR-DASH-LINE AFTER 2.                   
           WRITE BP13L45J-REC FROM L45J-PR-HEAD-03.                             
           WRITE BP13L45J-REC FROM L45J-PR-DASH-LINE.                           
                                                                                
           MOVE SPACES                     TO   BP13L45J-REC.                   
           WRITE BP13L45J-REC.                                                  
                                                                                
           MOVE 7                          TO   WS-CNT-L45J-LINE.               
                                                                                
       7910-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8000-CALL-AV02C002.                                                      
      *=============================================================            
                                                                                
           CALL 'AV02C002' USING LINK-REC.                                      
                                                                                
           IF WS-RETURN = 'EID'                                                 
              MOVE 99                      TO   RETURN-CODE                     
              DISPLAY 'ERROR IN DATE : '        WS-IN-DATE                      
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8100-CALL-BP13C913.                                                      
      *=============================================================            
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
           WHEN ZEROES                                                          
           WHEN SPACES                                                          
           WHEN LOW-VALUES                                                      
              MOVE 'Y'                     TO   WS-PIDB-FND                     
                                                                                
           WHEN 100                                                             
              MOVE 'N'                     TO   WS-PIDB-FND                     
                                                                                
           WHEN OTHER                                                           
              MOVE 99                      TO   RETURN-CODE                     
              MOVE COMM8-CDE-SYSERR        TO   WS-SQL-CODE                     
              DISPLAY 'ERROR CALLING BP13C913. CODE: ' WS-SQL-CODE              
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13FC20                                                       
                 BM06K110                                                       
                 BP13K820                                                       
                 BP13K857                                                       
                 BP13K435                                                       
                 BP13K452                                                       
                 BP13K454                                                       
                 BP13K800                                                       
                 BP13K310                                                       
                 BP13KC30                                                       
                 BP13F203                                                       
                 BP13F23B                                                       
                 BP13L45J.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K820 ERROR ' WS-K820-STATUS                  
              MOVE     WS-K820-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K857 ERROR ' WS-K857-STATUS                  
              MOVE     WS-K857-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K435-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K435 ERROR ' WS-K435-STATUS                  
              MOVE     WS-K435-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K452-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K452 ERROR ' WS-K452-STATUS                  
              MOVE     WS-K452-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K454-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K454 ERROR ' WS-K454-STATUS                  
              MOVE     WS-K454-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K310-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K310 ERROR ' WS-K310-STATUS                  
              MOVE     WS-K310-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-KC30-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC30 ERROR ' WS-KC30-STATUS                  
              MOVE     WS-KC30-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C45J CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1.  NO OF BP13FC20 READ     : ' WS-CNT-FC20-READ.          
           DISPLAY ' 2.  NO OF BP13FC20 SKIPPED  : ' WS-CNT-FC20-SKIP.          
           DISPLAY ' 3.  NO OF BP13K820 READ     : ' WS-CNT-K820-READ.          
           DISPLAY ' 4.  NO OF BP13K857 READ     : ' WS-CNT-K857-READ.          
           DISPLAY ' 5.  NO OF BP13K452 UPDATED  : ' WS-CNT-K452-UPD.           
           DISPLAY ' 6.  NO OF BP13K454 WRITTEN  : ' WS-CNT-K454-WRITE.         
           DISPLAY ' 7.  NO OF BP13K800 UPDATED  : ' WS-CNT-K800-UPD.           
           DISPLAY ' 8.  NO OF BP13K310 WRITTEN  : ' WS-CNT-K310-WRITE.         
           DISPLAY ' 9.  NO OF BP13KC30 UPDATED  : ' WS-CNT-KC30-UPD.           
           DISPLAY ' 10. NO OF BP13F203 WRITTEN  : ' WS-CNT-F203-WRITE.         
           DISPLAY ' 11. NO OF BP13F23B WRITTEN  : ' WS-CNT-F23B-WRITE.         
           DISPLAY ' 12. NO OF BP13L45J WRITTEN  : ' WS-CNT-L45J-WRITE.         
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
