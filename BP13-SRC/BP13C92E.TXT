       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C92E.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  05 MAY 2017.                                              
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *   OBJECTIVE   : UPDATE THE TOTAL COUNT FIELDS IN BP13FW3C   *           
      *                                                             *           
      *   INPUT FILES : BP13FW3C - BOOKING SUMMARY                  *           
      *                 BP13FW3D - BOOKING DETAILS                  *           
      *                 SY02F001 - GET CURRENT DATE                 *           
      *                 BP13KQ60 - GET LAST QUEUE                   *           
      *                 BP13K813 - GET BOOKING START AND END DATE   *           
      *                 BP13K76A - GET APPL DETAILS                 *           
      *   OUTPUT FILE : P13FW3CA - UPDATED FILE                     *           
      *                                                             *           
      * =========================================================== *           
      * CHQ REQ  DATE       BY   DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136299 07/05/2015 FNP1 NEW PROGRAM                        *           
      * BP136299 19/05/2017 SMR2 CATER FOR LAST QUEUE               *           
      * BP136299 22/05/2017 SMR2 CREATE ADDITIONAL RECORD/S FOR     *           
      *                          MULTIPLE PROJECT NAME              *           
      * BP136299 24/05/2017 SMR2 USE NUM-APPL INSTEAD OF NUM-DEMAND *           
      * BP136299 30/05/2017 SMR2 DO NOT POPULATE NUM-FLAT-MGPS      *           
      * BP136832 31/05/2017 SMR2 CATER FOR MGPS-FT '3P'             *           
      * BP136856 03/07/2017 FNP1 MOVE '9' TO NUM-BONUS FOR DUP FW3C *           
      * BP139071 03/02/2022 KR13 CHANGE FW3C-FLAT-TYP AS 2F IF 1A   *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FW3C ASSIGN       TO BP13FW3C.                            
                                                                                
           SELECT BP13FW3D ASSIGN       TO BP13FW3D.                            
                                                                                
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
                                                                                
           SELECT BP13KQ60 ASSIGN       TO BP13KQ60                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KQ60-KEY-FLD                         
                           FILE STATUS  IS WS-KQ60-STATUS.                      
                                                                                
           SELECT BP13K813 ASSIGN       TO BP13K813                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K813-KEY-FLD                         
                           FILE STATUS  IS WS-K813-STATUS.                      
                                                                                
           SELECT BP13K76A ASSIGN       TO BP13K76A                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K76A-KEY-FLD                         
                           FILE STATUS  IS WS-K76A-STATUS.                      
                                                                                
           SELECT P13FW3CA ASSIGN       TO P13FW3CA.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       COPY SY02F001.                                                           
                                                                                
       FD   BP13FW3C                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       COPY BP13FW3C.                                                           
                                                                                
       FD   BP13FW3D                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       COPY BP13FW3D.                                                           
                                                                                
       FD   BP13KQ60                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13KQ60.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K76A                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13K76A.                                                           
                                                                                
       FD   P13FW3CA                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       01   P13FW3CA-REC                    PIC X(500).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-KQ60-STATUS               PIC 9(02)   VALUE ZEROES.           
           05  WS-K813-STATUS               PIC 9(02)   VALUE ZEROES.           
           05  WS-K76A-STATUS               PIC 9(02)   VALUE ZEROES.           
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-KQ60-EOF                  PIC X(01)   VALUE 'N'.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FW3C-READ                 PIC 9(06)   VALUE ZEROES.           
           05  WS-FW3D-READ                 PIC 9(06)   VALUE ZEROES.           
           05  WS-K76A-READ                 PIC 9(06)   VALUE ZEROES.           
           05  WS-FW3CA-WRITE               PIC 9(06)   VALUE ZEROES.           
           05  WS-PROJ-CNT                  PIC 9(01)   VALUE ZEROES.           
           05  WS-I                         PIC 9(01)   VALUE ZEROES.           
           05  WS-CHAR-CNT                  PIC 9(03)   VALUE ZEROES.           
           05  WS-CHAR-MAX                  PIC 9(03)   VALUE ZEROES.           
                                                                                
       01  WS-FW3C-COUNTERS.                                                    
           05  WS-TOT-APPL                  PIC 9(05)   VALUE ZEROES.           
           05  WS-TOT-BALLOT                PIC 9(05)   VALUE ZEROES.           
           05  WS-TOT-CANCEL                PIC 9(05)   VALUE ZEROES.           
           05  WS-TOT-INVITED               PIC 9(05)   VALUE ZEROES.           
           05  WS-APPL-MGPS-22              PIC 9(03)   VALUE ZEROES.           
           05  WS-APPL-MGPS-23              PIC 9(03)   VALUE ZEROES.           
           05  WS-APPL-MGPS-23P             PIC 9(03)   VALUE ZEROES.           
           05  WS-APPL-MGPS-24              PIC 9(03)   VALUE ZEROES.           
           05  WS-APPL-MGPS-25              PIC 9(03)   VALUE ZEROES.           
           05  WS-APPL-MGPS-32              PIC 9(03)   VALUE ZEROES.           
           05  WS-APPL-MGPS-33              PIC 9(03)   VALUE ZEROES.           
           05  WS-APPL-MGPS-33P             PIC 9(03)   VALUE ZEROES.           
           05  WS-APPL-MGPS-34              PIC 9(03)   VALUE ZEROES.           
           05  WS-APPL-MGPS-35              PIC 9(03)   VALUE ZEROES.           
           05  WS-APPL-MGPS-3P2             PIC 9(03)   VALUE ZEROES.           
           05  WS-APPL-MGPS-3P3             PIC 9(03)   VALUE ZEROES.           
           05  WS-APPL-MGPS-3P3P            PIC 9(03)   VALUE ZEROES.           
           05  WS-APPL-MGPS-3P4             PIC 9(03)   VALUE ZEROES.           
           05  WS-APPL-MGPS-3P5             PIC 9(03)   VALUE ZEROES.           
           05  WS-BOOK-MGPS-22              PIC 9(03)   VALUE ZEROES.           
           05  WS-BOOK-MGPS-23              PIC 9(03)   VALUE ZEROES.           
           05  WS-BOOK-MGPS-23P             PIC 9(03)   VALUE ZEROES.           
           05  WS-BOOK-MGPS-24              PIC 9(03)   VALUE ZEROES.           
           05  WS-BOOK-MGPS-25              PIC 9(03)   VALUE ZEROES.           
           05  WS-BOOK-MGPS-32              PIC 9(03)   VALUE ZEROES.           
           05  WS-BOOK-MGPS-33              PIC 9(03)   VALUE ZEROES.           
           05  WS-BOOK-MGPS-33P             PIC 9(03)   VALUE ZEROES.           
           05  WS-BOOK-MGPS-34              PIC 9(03)   VALUE ZEROES.           
           05  WS-BOOK-MGPS-35              PIC 9(03)   VALUE ZEROES.           
           05  WS-BOOK-MGPS-3P2             PIC 9(03)   VALUE ZEROES.           
           05  WS-BOOK-MGPS-3P3             PIC 9(03)   VALUE ZEROES.           
           05  WS-BOOK-MGPS-3P3P            PIC 9(03)   VALUE ZEROES.           
           05  WS-BOOK-MGPS-3P4             PIC 9(03)   VALUE ZEROES.           
           05  WS-BOOK-MGPS-3P5             PIC 9(03)   VALUE ZEROES.           
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CURR-DATE                 PIC X(08)   VALUE SPACES.           
           05  WS-DTE-BKAPPMT-DATE          PIC X(08)   VALUE SPACES.           
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-FW3C-KEY.                                                     
               10  WS-FW3C-NT               PIC X(03)   VALUE SPACES.           
               10  WS-FW3C-FT               PIC X(02)   VALUE SPACES.           
               10  WS-FW3C-SELECTION        PIC X(03)   VALUE SPACES.           
               10  WS-FW3C-BALLOT           PIC X(06)   VALUE SPACES.           
           05  WS-FW3D-KEY.                                                     
               10  WS-FW3D-NT               PIC X(03)   VALUE SPACES.           
               10  WS-FW3D-FT               PIC X(02)   VALUE SPACES.           
               10  WS-FW3D-SELECTION        PIC X(03)   VALUE SPACES.           
               10  WS-FW3D-BALLOT           PIC X(06)   VALUE SPACES.           
           05  WS-FW3C-FLAT-TYPE            PIC X(02)   VALUE SPACES.           
           05  WS-LAST-QUEUE                PIC X(05)   VALUE SPACES.           
           05  WS-DTE-BOOKING-START         PIC X(08)   VALUE SPACES.           
           05  WS-DTE-BOOKING-END           PIC X(08)   VALUE SPACES.           
           05  WS-BP13FW3C-REC              PIC X(500) VALUE SPACES.            
           05  WS-TEMP-NME-PROJECT          PIC X(180) VALUE SPACES.            
           05  WS-ARRAY-VARS OCCURS 3 TIMES.                                    
               10  WS-NME-PROJECT           PIC X(180) VALUE SPACES.            
           05  WS-NUM-BONUS                 PIC 9      VALUE 0.                 
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13FW3C       THRU 2000-EXIT.                     
           PERFORM 2500-READ-BP13FW3D       THRU 2500-EXIT.                     
           PERFORM 3000-PROCESS-RECS        THRU 3000-EXIT                      
             UNTIL WS-FW3C-KEY = HIGH-VALUES                                    
               AND WS-FW3D-KEY = HIGH-VALUES.                                   
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13FW3C                                                 
                       BP13FW3D                                                 
                       SY02F001                                                 
                       BP13KQ60                                                 
                       BP13K813                                                 
                       BP13K76A                                                 
                OUTPUT P13FW3CA.                                                
                                                                                
           IF WS-KQ60-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR BP13KQ60, STATUS : ' WS-KQ60-STATUS           
              MOVE WS-KQ60-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR BP13K813, STATUS : ' WS-K813-STATUS           
              MOVE WS-K813-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K76A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR BP13K76A, STATUS : ' WS-K76A-STATUS           
              MOVE WS-K76A-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2000-READ-BP13FW3C.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13FW3C AT END                                                 
                MOVE HIGH-VALUES            TO WS-FW3C-KEY                      
                GO TO 2000-EXIT.                                                
                                                                                
           MOVE FW3C-NUM-NT                 TO WS-FW3C-NT.                      
           MOVE FW3C-NUM-FT                 TO WS-FW3C-FT.                      
           MOVE FW3C-NUM-SELECTION          TO WS-FW3C-SELECTION.               
           MOVE FW3C-DTE-BALLOT             TO WS-FW3C-BALLOT.                  
           IF (FW3C-DTE-BALLOT >= '201511') AND                                 
              (FW3C-NUM-FT-CHAR2 = '2 ')                                        
              MOVE '2F'                     TO WS-FW3C-FLAT-TYPE                
           ELSE                                                                 
              MOVE FW3C-NUM-FT-CHAR2        TO WS-FW3C-FLAT-TYPE                
           END-IF.                                                              
                                                                                
           IF WS-FW3C-FLAT-TYPE = '1A'                                          
             MOVE '2F'                      TO WS-FW3C-FLAT-TYPE                
           END-IF                                                               
                                                                                
           ADD 1                            TO WS-FW3C-READ.                    
                                                                                
           MOVE ZEROES                      TO WS-FW3C-COUNTERS.                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2500-READ-BP13FW3D.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13FW3D AT END                                                 
                MOVE HIGH-VALUES            TO WS-FW3D-KEY                      
                GO TO 2500-EXIT.                                                
                                                                                
           MOVE FW3D-NUM-NT-ZONE            TO WS-FW3D-NT.                      
           MOVE FW3D-NUM-FLAT-TYPE          TO WS-FW3D-FT.                      
           MOVE FW3D-NUM-ALLO-CAT           TO WS-FW3D-SELECTION.               
           MOVE FW3D-DTE-BALLOT             TO WS-FW3D-BALLOT.                  
                                                                                
           ADD 1                            TO WS-FW3D-READ.                    
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-RECS.                                                       
      *-----------------*                                                       
                                                                                
           MOVE ZEROES                         TO WS-NUM-BONUS.                 
                                                                                
           IF WS-FW3C-KEY = WS-FW3D-KEY                                         
              PERFORM 4000-MOVE-DETAILS        THRU 4000-EXIT                   
                UNTIL WS-FW3D-KEY > WS-FW3C-KEY                                 
                   OR WS-FW3D-KEY = HIGH-VALUES                                 
                                                                                
              PERFORM 5000-STARTBR-BP13KQ60    THRU 5000-EXIT                   
              PERFORM 5500-READ-BP13K813       THRU 5500-EXIT                   
              PERFORM 6000-READ-BP13K76A       THRU 6000-EXIT                   
              PERFORM 7000-WRITE-P13FW3CA      THRU 7000-EXIT                   
                                                                                
              PERFORM 2000-READ-BP13FW3C       THRU 2000-EXIT                   
           ELSE                                                                 
              IF WS-FW3C-KEY < WS-FW3D-KEY                                      
                 PERFORM 2000-READ-BP13FW3C    THRU 2000-EXIT                   
              ELSE                                                              
                 IF WS-FW3C-KEY > WS-FW3D-KEY                                   
                    PERFORM 2500-READ-BP13FW3D THRU 2500-EXIT                   
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4000-MOVE-DETAILS.                                                       
      *------------------*                                                      
                                                                                
           IF (FW3D-DTE-BKAPPMT-DATE-MGPS NOT = SPACES AND LOW-VALUES)          
              AND (FW3D-DTE-BKAPPMT-DATE-MGPS <= F001-DTE-CURRENT) AND          
              ((FW3D-DTE-BKAPPMT-DATE-MGPS <= FW3D-DTE-BKAPPMT-DATE) OR         
               (FW3D-DTE-BKAPPMT-DATE = SPACES OR LOW-VALUES))                  
              MOVE FW3D-DTE-BKAPPMT-DATE-MGPS TO WS-DTE-BKAPPMT-DATE            
           ELSE                                                                 
              MOVE FW3D-DTE-BKAPPMT-DATE      TO WS-DTE-BKAPPMT-DATE            
           END-IF.                                                              
                                                                                
           IF (WS-DTE-BKAPPMT-DATE NOT = SPACES AND LOW-VALUES) AND             
              (WS-DTE-BKAPPMT-DATE <= F001-DTE-CURRENT)                         
              ADD 1                         TO WS-TOT-INVITED                   
           END-IF.                                                              
                                                                                
           EVALUATE FW3D-NUM-MGPS-FT-PAIR                                       
           WHEN '2 2 '                                                          
                ADD 1                       TO WS-BOOK-MGPS-22                  
           WHEN '2 3 '                                                          
                ADD 1                       TO WS-BOOK-MGPS-23                  
           WHEN '2 3P'                                                          
                ADD 1                       TO WS-BOOK-MGPS-23P                 
           WHEN '2 4 '                                                          
                ADD 1                       TO WS-BOOK-MGPS-24                  
           WHEN '2 5 '                                                          
                ADD 1                       TO WS-BOOK-MGPS-25                  
           WHEN '3 2 '                                                          
                ADD 1                       TO WS-BOOK-MGPS-32                  
           WHEN '3 3 '                                                          
                ADD 1                       TO WS-BOOK-MGPS-33                  
           WHEN '3 3P'                                                          
                ADD 1                       TO WS-BOOK-MGPS-33P                 
           WHEN '3 4 '                                                          
                ADD 1                       TO WS-BOOK-MGPS-34                  
           WHEN '3 5 '                                                          
                ADD 1                       TO WS-BOOK-MGPS-35                  
           WHEN '3P2 '                                                          
                ADD 1                       TO WS-BOOK-MGPS-3P2                 
           WHEN '3P3 '                                                          
                ADD 1                       TO WS-BOOK-MGPS-3P3                 
           WHEN '3P3P'                                                          
                ADD 1                       TO WS-BOOK-MGPS-3P3P                
           WHEN '3P4 '                                                          
                ADD 1                       TO WS-BOOK-MGPS-3P4                 
           WHEN '3P5 '                                                          
                ADD 1                       TO WS-BOOK-MGPS-3P5                 
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2500-READ-BP13FW3D    THRU 2500-EXIT.                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       5000-STARTBR-BP13KQ60.                                                   
      *----------------------*                                                  
                                                                                
           MOVE SPACES                      TO BP13KQ60-REC.                    
           INITIALIZE                          BP13KQ60-REC.                    
                                                                                
           MOVE 'N'                         TO WS-KQ60-EOF.                     
           MOVE SPACES                      TO WS-LAST-QUEUE.                   
                                                                                
           MOVE WS-FW3C-SELECTION           TO KQ60-NUM-SALE-MODE.              
           MOVE WS-FW3C-NT                  TO KQ60-NUM-ZONE.                   
           MOVE WS-FW3C-FLAT-TYPE           TO KQ60-NUM-FLAT-TYPE.              
           MOVE WS-FW3C-BALLOT              TO KQ60-DTE-BALLOT.                 
                                                                                
           START BP13KQ60 KEY >= KQ60-KEY-FLD.                                  
                                                                                
           EVALUATE WS-KQ60-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
                PERFORM 5100-READNEXT-KQ60  THRU 5100-EXIT                      
                  UNTIL WS-KQ60-EOF = 'Y'                                       
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
                CONTINUE                                                        
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'START ERROR BP13KQ60, STATUS : ' WS-KQ60-STATUS        
                        ' KEY : ' KQ60-KEY-FLD                                  
                MOVE WS-KQ60-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       5100-READNEXT-KQ60.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13KQ60 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-KQ60-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
                IF KQ60-NUM-SALE-MODE = WS-FW3C-SELECTION AND                   
                   KQ60-NUM-ZONE = WS-FW3C-NT AND                               
                   KQ60-NUM-FLAT-TYPE = WS-FW3C-FLAT-TYPE AND                   
                   KQ60-DTE-BALLOT = WS-FW3C-BALLOT                             
                   IF KQ60-DTE-BKAPPMT <= F001-DTE-CURRENT                      
                      MOVE KQ60-NUM-LAST-QUEUE TO WS-LAST-QUEUE                 
                   ELSE                                                         
                      MOVE 'Y'              TO WS-KQ60-EOF                      
                   END-IF                                                       
                ELSE                                                            
                   MOVE 'Y'                 TO WS-KQ60-EOF                      
                END-IF                                                          
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13KQ60 : ' KQ60-KEY-FLD          
                MOVE 'Y'                    TO WS-KQ60-EOF                      
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'READ ERROR BP13KQ60, STATUS : ' WS-KQ60-STATUS         
                        ' KEY : ' KQ60-KEY-FLD                                  
                MOVE WS-KQ60-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       5500-READ-BP13K813.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO BP13K813-REC.                    
           INITIALIZE                          BP13K813-REC.                    
                                                                                
           MOVE SPACES                      TO WS-DTE-BOOKING-START             
                                               WS-DTE-BOOKING-END.              
                                                                                
           MOVE WS-FW3C-NT                  TO K813-NUM-ZONE.                   
           MOVE WS-FW3C-FLAT-TYPE           TO K813-NUM-FLAT-TYPE.              
           MOVE WS-FW3C-BALLOT              TO K813-DTE-BALLOT.                 
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
           WHEN ZEROES                                                          
                MOVE K813-DTE-BOOKING-START TO WS-DTE-BOOKING-START             
                MOVE K813-DTE-BOOKING-END   TO WS-DTE-BOOKING-END               
                                                                                
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K813 : ' K813-KEY-FLD          
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K813 : ' WS-K813-STATUS              
                MOVE WS-K813-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       6000-READ-BP13K76A.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO BP13K76A-REC.                    
           INITIALIZE                          BP13K76A-REC.                    
                                                                                
           MOVE WS-FW3C-SELECTION           TO K76A-NUM-SALE-MODE-HIST.         
           MOVE WS-FW3C-BALLOT              TO K76A-DTE-BALLOT-HIST.            
           MOVE WS-FW3C-NT                  TO K76A-NUM-CHOICE-HIST.            
           MOVE WS-FW3C-FLAT-TYPE           TO K76A-NUM-FT-HIST.                
                                                                                
           READ BP13K76A.                                                       
                                                                                
           EVALUATE WS-K76A-STATUS                                              
           WHEN ZEROES                                                          
                ADD 1                       TO WS-K76A-READ                     
                PERFORM 6500-MOVE-DATA-FROM-BP13K76A THRU 6500-EXIT             
                                                                                
           WHEN 23                                                              
                DISPLAY 'KEY-FLD NOT FOUND IN BP13K76A : ' K76A-KEY-FLD         
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K76A : ' WS-K76A-STATUS              
                MOVE WS-K76A-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------*                                           
       6500-MOVE-DATA-FROM-BP13K76A.                                            
      *-----------------------------*                                           
                                                                                
           IF K76A-NUM-BALLOTED IS NOT NUMERIC                                  
              MOVE ZEROS TO K76A-NUM-BALLOTED                                   
           END-IF.                                                              
           IF K76A-NUM-CANCEL   IS NOT NUMERIC                                  
              MOVE ZEROS TO K76A-NUM-CANCEL                                     
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-22 IS NOT NUMERIC                              
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-22                               
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-23 IS NOT NUMERIC                              
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-23                               
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-23P IS NOT NUMERIC                             
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-23P                              
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-24 IS NOT NUMERIC                              
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-24                               
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-25 IS NOT NUMERIC                              
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-25                               
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-32 IS NOT NUMERIC                              
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-32                               
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-33 IS NOT NUMERIC                              
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-33                               
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-33P IS NOT NUMERIC                             
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-33P                              
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-34 IS NOT NUMERIC                              
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-34                               
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-35 IS NOT NUMERIC                              
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-35                               
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-3P2 IS NOT NUMERIC                             
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-3P2                              
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-3P3 IS NOT NUMERIC                             
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-3P3                              
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-3P3P IS NOT NUMERIC                            
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-3P3P                             
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-3P4 IS NOT NUMERIC                             
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-3P4                              
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-3P5 IS NOT NUMERIC                             
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-3P5                              
           END-IF.                                                              
                                                                                
           MOVE K76A-NUM-APPL          TO WS-TOT-APPL.                          
           MOVE K76A-NUM-BALLOTED      TO WS-TOT-BALLOT.                        
           MOVE K76A-NUM-CANCEL        TO WS-TOT-CANCEL.                        
                                                                                
           MOVE K76A-NUM-APPL-MGPS-22  TO WS-APPL-MGPS-22.                      
           MOVE K76A-NUM-APPL-MGPS-23  TO WS-APPL-MGPS-23.                      
           MOVE K76A-NUM-APPL-MGPS-23P TO WS-APPL-MGPS-23P.                     
           MOVE K76A-NUM-APPL-MGPS-24  TO WS-APPL-MGPS-24.                      
           MOVE K76A-NUM-APPL-MGPS-25  TO WS-APPL-MGPS-25.                      
           MOVE K76A-NUM-APPL-MGPS-32  TO WS-APPL-MGPS-32.                      
           MOVE K76A-NUM-APPL-MGPS-33  TO WS-APPL-MGPS-33.                      
           MOVE K76A-NUM-APPL-MGPS-33P TO WS-APPL-MGPS-33P.                     
           MOVE K76A-NUM-APPL-MGPS-34  TO WS-APPL-MGPS-34.                      
           MOVE K76A-NUM-APPL-MGPS-35  TO WS-APPL-MGPS-35.                      
           MOVE K76A-NUM-APPL-MGPS-3P2 TO WS-APPL-MGPS-3P2.                     
           MOVE K76A-NUM-APPL-MGPS-3P3 TO WS-APPL-MGPS-3P3.                     
           MOVE K76A-NUM-APPL-MGPS-3P3P TO WS-APPL-MGPS-3P3P.                   
           MOVE K76A-NUM-APPL-MGPS-3P4 TO WS-APPL-MGPS-3P4.                     
           MOVE K76A-NUM-APPL-MGPS-3P5 TO WS-APPL-MGPS-3P5.                     
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       7000-WRITE-P13FW3CA.                                                     
      *--------------------*                                                    
                                                                                
           MOVE WS-DTE-BOOKING-START        TO FW3C-DTE-BOOKING-START.          
           MOVE WS-DTE-BOOKING-END          TO FW3C-DTE-BOOKING-END.            
           MOVE WS-TOT-APPL                 TO FW3C-NUM-TOT-APPL.               
           MOVE WS-TOT-BALLOT               TO FW3C-NUM-TOT-BALLOT.             
           MOVE WS-TOT-CANCEL               TO FW3C-NUM-TOT-CANCEL.             
           MOVE WS-TOT-INVITED              TO FW3C-NUM-TOT-INVITED.            
           MOVE WS-LAST-QUEUE               TO FW3C-NUM-LAST-QUEUE.             
           MOVE WS-APPL-MGPS-22             TO FW3C-NUM-APPL-MGPS-22.           
           MOVE WS-APPL-MGPS-23             TO FW3C-NUM-APPL-MGPS-23.           
           MOVE WS-APPL-MGPS-23P            TO FW3C-NUM-APPL-MGPS-23P.          
           MOVE WS-APPL-MGPS-24             TO FW3C-NUM-APPL-MGPS-24.           
           MOVE WS-APPL-MGPS-25             TO FW3C-NUM-APPL-MGPS-25.           
           MOVE WS-APPL-MGPS-32             TO FW3C-NUM-APPL-MGPS-32.           
           MOVE WS-APPL-MGPS-33             TO FW3C-NUM-APPL-MGPS-33.           
           MOVE WS-APPL-MGPS-33P            TO FW3C-NUM-APPL-MGPS-33P.          
           MOVE WS-APPL-MGPS-34             TO FW3C-NUM-APPL-MGPS-34.           
           MOVE WS-APPL-MGPS-35             TO FW3C-NUM-APPL-MGPS-35.           
           MOVE WS-APPL-MGPS-3P2            TO FW3C-NUM-APPL-MGPS-3P2.          
           MOVE WS-APPL-MGPS-3P3            TO FW3C-NUM-APPL-MGPS-3P3.          
           MOVE WS-APPL-MGPS-3P3P           TO FW3C-NUM-APPL-MGPS-3P3P.         
           MOVE WS-APPL-MGPS-3P4            TO FW3C-NUM-APPL-MGPS-3P4.          
           MOVE WS-APPL-MGPS-3P5            TO FW3C-NUM-APPL-MGPS-3P5.          
           MOVE WS-BOOK-MGPS-22             TO FW3C-NUM-BOOK-MGPS-22.           
           MOVE WS-BOOK-MGPS-23             TO FW3C-NUM-BOOK-MGPS-23.           
           MOVE WS-BOOK-MGPS-23P            TO FW3C-NUM-BOOK-MGPS-23P.          
           MOVE WS-BOOK-MGPS-24             TO FW3C-NUM-BOOK-MGPS-24.           
           MOVE WS-BOOK-MGPS-25             TO FW3C-NUM-BOOK-MGPS-25.           
           MOVE WS-BOOK-MGPS-32             TO FW3C-NUM-BOOK-MGPS-32.           
           MOVE WS-BOOK-MGPS-33             TO FW3C-NUM-BOOK-MGPS-33.           
           MOVE WS-BOOK-MGPS-33P            TO FW3C-NUM-BOOK-MGPS-33P.          
           MOVE WS-BOOK-MGPS-34             TO FW3C-NUM-BOOK-MGPS-34.           
           MOVE WS-BOOK-MGPS-35             TO FW3C-NUM-BOOK-MGPS-35.           
           MOVE WS-BOOK-MGPS-3P2            TO FW3C-NUM-BOOK-MGPS-3P2.          
           MOVE WS-BOOK-MGPS-3P3            TO FW3C-NUM-BOOK-MGPS-3P3.          
           MOVE WS-BOOK-MGPS-3P3P           TO FW3C-NUM-BOOK-MGPS-3P3P.         
           MOVE WS-BOOK-MGPS-3P4            TO FW3C-NUM-BOOK-MGPS-3P4.          
           MOVE WS-BOOK-MGPS-3P5            TO FW3C-NUM-BOOK-MGPS-3P5.          
                                                                                
           MOVE BP13FW3C-REC                TO WS-BP13FW3C-REC.                 
                                                                                
           PERFORM 7100-INSPECT-PROJECT     THRU 7100-EXIT.                     
                                                                                
           MOVE ZEROES                      TO WS-I.                            
           PERFORM 7200-WRITE-ALL-BP13FW3C  THRU 7200-EXIT                      
             UNTIL WS-I > WS-PROJ-CNT.                                          
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       7100-INSPECT-PROJECT.                                                    
      *---------------------*                                                   
                                                                                
           MOVE ZEROES                      TO WS-PROJ-CNT.                     
           INSPECT FW3C-NME-PROJECT TALLYING WS-PROJ-CNT FOR ALL '/'.           
           EVALUATE WS-PROJ-CNT                                                 
           WHEN ZEROES                                                          
                MOVE FW3C-NME-PROJECT       TO WS-NME-PROJECT(1)                
                                                                                
           WHEN 1                                                               
                MOVE ZEROES                 TO WS-CHAR-CNT                      
                INSPECT FW3C-NME-PROJECT TALLYING WS-CHAR-CNT                   
                        FOR CHARACTERS BEFORE INITIAL '/'                       
                MOVE FW3C-NME-PROJECT(1:WS-CHAR-CNT)                            
                                            TO WS-NME-PROJECT(1)                
                ADD 2                       TO WS-CHAR-CNT                      
                COMPUTE WS-CHAR-MAX = 180 - WS-CHAR-CNT + 1                     
                MOVE FW3C-NME-PROJECT(WS-CHAR-CNT:WS-CHAR-MAX)                  
                                            TO WS-NME-PROJECT(2)                
                                                                                
           WHEN 2                                                               
                MOVE ZEROES                 TO WS-CHAR-CNT                      
                INSPECT FW3C-NME-PROJECT TALLYING WS-CHAR-CNT                   
                        FOR CHARACTERS BEFORE INITIAL '/'                       
                MOVE FW3C-NME-PROJECT(1:WS-CHAR-CNT)                            
                                            TO WS-NME-PROJECT(1)                
                ADD 2                       TO WS-CHAR-CNT                      
                COMPUTE WS-CHAR-MAX = 180 - WS-CHAR-CNT + 1                     
                MOVE FW3C-NME-PROJECT(WS-CHAR-CNT:WS-CHAR-MAX)                  
                                            TO WS-TEMP-NME-PROJECT              
                MOVE ZEROES                 TO WS-CHAR-CNT                      
                INSPECT WS-TEMP-NME-PROJECT TALLYING WS-CHAR-CNT                
                        FOR CHARACTERS BEFORE INITIAL '/'                       
                MOVE WS-TEMP-NME-PROJECT(1:WS-CHAR-CNT)                         
                                            TO WS-NME-PROJECT(2)                
                ADD 2                       TO WS-CHAR-CNT                      
                COMPUTE WS-CHAR-MAX = 180 - WS-CHAR-CNT + 1                     
                MOVE WS-TEMP-NME-PROJECT(WS-CHAR-CNT:WS-CHAR-MAX)               
                                            TO WS-NME-PROJECT(3)                
           END-EVALUATE.                                                        
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       7200-WRITE-ALL-BP13FW3C.                                                 
      *------------------------*                                                
                                                                                
           ADD 1                            TO WS-I.                            
           MOVE WS-BP13FW3C-REC             TO BP13FW3C-REC.                    
                                                                                
           IF FW3C-NME-PROJECT NOT = SPACES AND LOW-VALUES                      
              IF WS-NME-PROJECT(WS-I) = SPACES OR LOW-VALUES                    
                 GO TO 7200-EXIT                                                
              END-IF                                                            
              MOVE ZEROES                   TO WS-CHAR-CNT                      
              INSPECT WS-NME-PROJECT(WS-I) TALLYING WS-CHAR-CNT                 
                      FOR LEADING SPACES                                        
              ADD 1                         TO WS-CHAR-CNT                      
              COMPUTE WS-CHAR-MAX = 180 - WS-CHAR-CNT + 1                       
              MOVE WS-NME-PROJECT(WS-I)(WS-CHAR-CNT:WS-CHAR-MAX)                
                                            TO FW3C-NME-PROJECT                 
                                                                                
              IF WS-I > 1                                                       
                 MOVE 99999                 TO FW3C-NUM-TOT-APPL                
                                               FW3C-NUM-TOT-BALLOT              
                                               FW3C-NUM-TOT-CANCEL              
                 ADD 1                      TO WS-NUM-BONUS                     
                 MOVE WS-NUM-BONUS          TO FW3C-NUM-BONUS                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           WRITE P13FW3CA-REC               FROM BP13FW3C-REC.                  
           ADD 1                            TO WS-FW3CA-WRITE.                  
                                                                                
       7200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '*===========================================*'.             
           DISPLAY '  BP13C92E CONTROL TOTAL     : ' F001-DTE-CURRENT.          
           DISPLAY '*-------------------------------------------*'.             
           DISPLAY '  RECS READ FROM BP13FW3C    : ' WS-FW3C-READ.              
           DISPLAY '  RECS READ FROM BP13FW3D    : ' WS-FW3D-READ.              
           DISPLAY '  RECS READ FROM BP13K76A    : ' WS-K76A-READ.              
           DISPLAY '  RECS WRITTEN IN P13FW3CA   : ' WS-FW3CA-WRITE.            
           DISPLAY '*===========================================*'.             
                                                                                
           CLOSE BP13FW3C                                                       
                 BP13FW3D                                                       
                 SY02F001                                                       
                 BP13KQ60                                                       
                 BP13K813                                                       
                 BP13K76A                                                       
                 P13FW3CA.                                                      
                                                                                
           IF WS-KQ60-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13KQ60, STATUS : ' WS-KQ60-STATUS          
              MOVE WS-KQ60-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K813, STATUS : ' WS-K813-STATUS          
              MOVE WS-K813-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K76A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K76A, STATUS : ' WS-K76A-STATUS          
              MOVE WS-K76A-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
