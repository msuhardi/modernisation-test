      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C30E.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  SEP 20, 2018.                                             
      *===============================================================*         
      *   OBJECTIVE:  REPORT ON CPF USAGE                                       
      *===============================================================*         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP137489 20/09/2018 PCL4 NEW PROGRAM.                         *         
      * BP137630 24/01/2019 PCL4 TO ADD CHECK FOR UNQUALIFIED APPS    *         
      * BP139030 07/01/2022 ZAR7  REPLACE CHUA YEE MIEN TO LIM KOK CHUN         
      * BP139276 05/04/2024 KAC1  REPLACING LIM KOK CHUN BY           *         
      *                                     KATHIJAH_BEE_ALI_MOHAMED  *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F410  ASSIGN        TO BP13F410.                         
                                                                                
           SELECT  BP13K893  ASSIGN        TO BP13K893                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K893-KEY-FLD                      
                             FILE STATUS   IS WS-K893-STATUS.                   
                                                                                
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
           SELECT  BP13K40C  ASSIGN        TO BP13K40C                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K40C-KEY-FLD                      
                             FILE STATUS   IS WS-K40C-STATUS.                   
                                                                                
           SELECT  P13F30E1  ASSIGN        TO P13F30E1.                         
           SELECT  P13F30E2  ASSIGN        TO P13F30E2.                         
           SELECT  MAILC30E  ASSIGN        TO MAILC30E.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F410                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F410.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BP13K40C                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K40C.                                                           
                                                                                
       FD   P13F30E1                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  200 CHARACTERS.                                    
       01   P13F30E1-REC               PIC X(200).                              
                                                                                
                                                                                
       FD   P13F30E2                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  200 CHARACTERS.                                    
       01   P13F30E2-REC               PIC X(200).                              
                                                                                
       FD   MAILC30E                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  200 CHARACTERS.                                    
       01   MAILC30E-REC               PIC X(200).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  L30E-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L30E'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L30E-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L30E-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  L30E-HEADER02.                                                       
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(53)     VALUE                          
               'REPORT ON APPLICATIONS COLLECTED KEYS WITH RETENTION '.         
           05  FILLER              PIC X(43)     VALUE                          
               'OF OA MONEY '.                                                  
           05  L30E-DTE-HDR        PIC X(10)     VALUE SPACES.                  
                                                                                
       01  L30E-HEADER03.                                                       
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE '  S/N'.                  
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE 'REGN NO '.               
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(02)    VALUE 'FT'.                     
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(02)    VALUE 'LT'.                     
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(10)    VALUE 'LOAN AMT'.               
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(06)    VALUE 'CONTRA'.                 
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(04)    VALUE 'BFSL'.                   
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(12)    VALUE 'CPF APPRV-RA'.           
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE 'ALC MODE'.               
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(07)    VALUE 'ALC SCH'.                
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(07)    VALUE 'ELG SCH'.                
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(13)    VALUE 'KEY ISSUE DTE'.          
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(09)    VALUE 'HA1 NRIC '.              
           05  FILLER              PIC X(02)    VALUE '& '.                     
           05  FILLER              PIC X(10)    VALUE 'RET OA AMT'.             
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(09)    VALUE 'HA2 NRIC '.              
           05  FILLER              PIC X(02)    VALUE '& '.                     
           05  FILLER              PIC X(10)    VALUE 'RET OA AMT'.             
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(09)    VALUE 'HA3 NRIC '.              
           05  FILLER              PIC X(02)    VALUE '& '.                     
           05  FILLER              PIC X(10)    VALUE 'RET OA AMT'.             
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(09)    VALUE 'HA4 NRIC '.              
           05  FILLER              PIC X(02)    VALUE '& '.                     
           05  FILLER              PIC X(10)    VALUE 'RET OA AMT'.             
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(14)    VALUE 'TOT RET OA AMT'.         
           05  FILLER              PIC X(01)    VALUE SPACES.                   
                                                                                
       01  L30E-HEADER04.                                                       
           05  FILLER PIC X(15) VALUE SPACES.                                   
           05  FILLER PIC X(23) VALUE 'NO. OF UNQUALIFIED APPS'.                
           05  FILLER PIC X(01) VALUE SPACES.                                   
           05  FILLER PIC X(23) VALUE 'NO. OF QUALIFIED APPS'.                  
           05  FILLER PIC X(01) VALUE SPACES.                                   
           05  FILLER PIC X(26) VALUE 'RET OA ($1 - $5000)       '.             
           05  FILLER PIC X(01) VALUE SPACES.                                   
           05  FILLER PIC X(26) VALUE 'RET OA ($5001 - $10,000)  '.             
           05  FILLER PIC X(01) VALUE SPACES.                                   
           05  FILLER PIC X(26) VALUE 'RET OA ($10,001 - $20,000)'.             
           05  FILLER PIC X(01) VALUE SPACES.                                   
           05  FILLER PIC X(26) VALUE 'RET OA ($20,001 - $30,000)'.             
           05  FILLER PIC X(01) VALUE SPACES.                                   
           05  FILLER PIC X(26) VALUE 'RET OA ($30,001 - $40,000)'.             
                                                                                
                                                                                
       01  L30E-DETAIL-C30E.                                                    
           05  L30E-C30E-MTH      PIC X(14)  VALUE SPACES.                      
           05  FILLER             PIC X(01)  VALUE SPACES.                      
           05  L30E-C30E-NOT      PIC ZZZZ9.                                    
           05  FILLER             PIC X(19)  VALUE SPACES.                      
           05  L30E-C30E-OK       PIC ZZZZ9.                                    
           05  FILLER             PIC X(19)  VALUE SPACES.                      
           05  L30E-C30E-5K       PIC ZZZZ9.                                    
           05  FILLER             PIC X(22)  VALUE SPACES.                      
           05  L30E-C30E-10K      PIC ZZZZ9.                                    
           05  FILLER             PIC X(22)  VALUE SPACES.                      
           05  L30E-C30E-20K      PIC ZZZZ9.                                    
           05  FILLER             PIC X(22)  VALUE SPACES.                      
           05  L30E-C30E-30K      PIC ZZZZ9.                                    
           05  FILLER             PIC X(22)  VALUE SPACES.                      
           05  L30E-C30E-40K      PIC ZZZZ9.                                    
           05  FILLER             PIC X(22)  VALUE SPACES.                      
                                                                                
                                                                                
       01  L30E-DETAIL01.                                                       
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L30E-SN            PIC X(05)        VALUE SPACES.                
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L30E-REGN          PIC X(08)        VALUE SPACES.                
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L30E-FT            PIC X(02)        VALUE SPACES.                
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L30E-LT            PIC X(02)        VALUE SPACES.                
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L30E-AMT-LOAN      PIC $$$$$$9.99.                               
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L30E-CONTRA        PIC X(06)        VALUE SPACES.                
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L30E-BFSL          PIC X(04)        VALUE SPACES.                
           05  FILLER             PIC X(06)        VALUE SPACES.                
           05  L30E-CPF-RA        PIC X(01)        VALUE SPACES.                
           05  FILLER             PIC X(07)        VALUE SPACES.                
           05  L30E-ALC-MODE      PIC X(08)        VALUE SPACES.                
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L30E-ALC-SCH       PIC X(07)        VALUE SPACES.                
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L30E-ELG-SCH       PIC X(07)        VALUE SPACES.                
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L30E-DTE-ISSUE     PIC X(13)        VALUE SPACES.                
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L30E-NRIC1         PIC X(09)        VALUE SPACES.                
           05  FILLER             PIC X(02)        VALUE SPACES.                
           05  L30E-RET-OA1       PIC $$$$$$9.99.                               
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L30E-NRIC2         PIC X(09)        VALUE SPACES.                
           05  FILLER             PIC X(02)        VALUE SPACES.                
           05  L30E-RET-OA2       PIC $$$$$$9.99.                               
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L30E-NRIC3         PIC X(09)        VALUE SPACES.                
           05  FILLER             PIC X(02)        VALUE SPACES.                
           05  L30E-RET-OA3       PIC $$$$$$9.99.                               
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L30E-NRIC4         PIC X(09)        VALUE SPACES.                
           05  FILLER             PIC X(02)        VALUE SPACES.                
           05  L30E-RET-OA4       PIC $$$$$$9.99.                               
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L30E-TOT-RET-OA    PIC $$$$$$$$9.99.                             
                                                                                
       01  WS-REPORT-DTL.                                                       
           05  WS-EMPTY-RPT.                                                    
               10 FILLER                  PIC X(30) VALUE SPACES.               
               10 FILLER                  PIC X(21)                             
                                          VALUE '<   EMPTY  REPORT   >'.        
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(50)                               
               VALUE 'RCPT TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT3A            PIC X(50)                               
               VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.           
           05  WS-MAIL-RPT4             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT5             PIC X(29)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT6             PIC X(50)                               
               VALUE 'TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT6A            PIC X(50)                               
               VALUE 'TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                
           05  WS-MAIL-RPT7             PIC X(80)                               
               VALUE 'SUBJECT: REPORT ON APPS COLLTD KEYS WITH RET'.            
                                                                                
                                                                                
                                                                                
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-F410-EOF             PIC X(1)  VALUE 'N'.                     
           05  WS-K40C-FND             PIC X(1)  VALUE 'N'.                     
           05  WS-CPF-RA               PIC X(1)  VALUE 'N'.                     
           05  WS-K40C-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K893-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-SYSTEM-DATE          PIC 9(8)  VALUE ZEROES.                  
                                                                                
       01 WS-CURR-DATE.                                                         
           05  WS-CURR-CCYY            PIC 9(04).                               
           05  WS-CURR-MM              PIC 9(02).                               
           05  WS-CURR-DD              PIC 9(02).                               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F410-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(08) VALUE ZEROES.                  
           05  WS-SNO-ZZZZ9            PIC ZZZZ9 VALUE ZEROES.                  
           05  WS-SNO1                 PIC 9(05) VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
                                                                                
       01  WS-C30E-CNT1                PIC 9(5)  VALUE ZEROES.                  
       01  WS-C30E-CNT2                PIC 9(5)  VALUE ZEROES.                  
       01  WS-C30E-CNT-5K              PIC 9(5)  VALUE ZEROES.                  
       01  WS-C30E-CNT-10K             PIC 9(5)  VALUE ZEROES.                  
       01  WS-C30E-CNT-20K             PIC 9(5)  VALUE ZEROES.                  
       01  WS-C30E-CNT-30K             PIC 9(5)  VALUE ZEROES.                  
       01  WS-C30E-CNT-40K             PIC 9(5)  VALUE ZEROES.                  
                                                                                
       01  WS-AUG-RET-OA               PIC 9(8)V99 VALUE ZEROES.                
       01  WS-C30E-RET-OA              PIC 9(8)V99 VALUE ZEROES.                
       01  WS-TOT-RET-OA               PIC 9(8)V99 VALUE ZEROES.                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE            THRU 1000-EXIT.                 
           PERFORM 2000-READ-BP13F410           THRU 2000-EXIT.                 
                                                                                
           PERFORM 3000-PROCESS-RECORDS         THRU 3000-EXIT                  
             UNTIL WS-F410-EOF = 'Y'.                                           
                                                                                
           PERFORM 4000-WRITE-P13F30E1          THRU 4000-EXIT                  
           PERFORM 4001-WRITE-MAILC30E          THRU 4001-EXIT                  
                                                                                
           PERFORM 9999-CLOSE-ROUTINE           THRU 9999-EXIT.                 
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BP13K40C                                                 
                       BP13K800                                                 
                       BP13K893                                                 
               OUTPUT  P13F30E1                                                 
                       P13F30E2                                                 
                       MAILC30E.                                                
                                                                                
           IF WS-K40C-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K40C - ERROR OPENING : ' WS-K40C-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K893 - ERROR OPENING : ' WS-K893-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 7000-PRINT-HEADER       THRU 7000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F410.                                                      
      ******************************************************************        
                                                                                
           READ BP13F410           AT END                                       
                MOVE 'Y'           TO WS-F410-EOF                               
                GO                 TO 2000-EXIT.                                
                                                                                
           ADD  1                  TO WS-F410-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       2100-READ-BP13K800.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K800-MASTER.                    
           INITIALIZE                       BP13K800-MASTER.                    
                                                                                
           MOVE F410-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    PERFORM 2200-START-BP13K893  THRU 2200-EXIT                 
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2200-START-BP13K893.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO BP13K893-MASTER             
                                                    BP13K800-MASTER.            
           INITIALIZE                               BP13K893-MASTER             
                                                    BP13K800-MASTER.            
                                                                                
           MOVE SPACES                           TO K893-KEY-FLD.               
           MOVE F410-NUM-REGN                    TO K893-NUM-REGN-HIST.         
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K893-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 2300-READ-BP13K893  THRU 2300-EXIT                  
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K893: ' WS-K893-STATUS          
                            ' REGN = ' F410-NUM-REGN                            
                    PERFORM  9999-CLOSE-ROUTINE THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2300-READ-BP13K893.                                                      
      ******************************************************************        
                                                                                
            READ  BP13K893 NEXT AT END                                          
                  MOVE SPACES               TO K893-KEY-FLD.                    
                                                                                
            EVALUATE WS-K893-STATUS                                             
                WHEN 00                                                         
                WHEN 02                                                         
                     IF K893-NUM-REGN-HIST = F410-NUM-REGN                      
                        MOVE BP13K893-MASTER(1:2000) TO BP13K800-MASTER         
                     END-IF                                                     
                WHEN 23                                                         
                     CONTINUE                                                   
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K893 : ' WS-K893-STATUS         
                             ' REGN = ' K893-NUM-REGN-HIST                      
                     PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                
            END-EVALUATE.                                                       
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 2100-READ-BP13K800               THRU 2100-EXIT.             
           PERFORM 3100-READ-BP13K40C               THRU 3100-EXIT.             
                                                                                
           PERFORM 3002-PROCESS-C30E-APPS           THRU 3002-EXIT.             
           PERFORM 3200-WRITE-P13F30E2              THRU 3200-EXIT              
                                                                                
           PERFORM 2000-READ-BP13F410               THRU 2000-EXIT.             
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3002-PROCESS-C30E-APPS.                                                  
      ******************************************************************        
                                                                                
           MOVE 'N'                            TO WS-CPF-RA.                    
                                                                                
           PERFORM 3003-INIT-BP13F410-FLDS   THRU 3003-EXIT.                    
                                                                                
           IF (F410-AMT-RETIRE-CPF1 > 0) OR                                     
              (F410-AMT-RETIRE-CPF2 > 0) OR                                     
              (F410-AMT-RETIRE-CPF3 > 0) OR                                     
              (F410-AMT-RETIRE-CPF4 > 0)                                        
                                                                                
             IF (F410-AMT-CPF1-BALANCE > F410-AMT-CPF1-INIT-BALANCE) OR         
                (F410-AMT-CPF2-BALANCE > F410-AMT-CPF2-INIT-BALANCE) OR         
                (F410-AMT-CPF3-BALANCE > F410-AMT-CPF3-INIT-BALANCE) OR         
                (F410-AMT-CPF4-BALANCE > F410-AMT-CPF4-INIT-BALANCE)            
                 MOVE 'Y'        TO WS-CPF-RA                                   
             END-IF                                                             
                                                                                
           END-IF.                                                              
                                                                                
           COMPUTE WS-C30E-RET-OA = K40C-AMT-CPF1-RESERVE +                     
                                    K40C-AMT-CPF2-RESERVE +                     
                                    K40C-AMT-CPF3-RESERVE +                     
                                    K40C-AMT-CPF4-RESERVE.                      
                                                                                
           IF (WS-C30E-RET-OA >= 1) AND                                         
              (WS-C30E-RET-OA <= 5000)                                          
              ADD 1       TO WS-C30E-CNT-5K                                     
           ELSE                                                                 
              IF (WS-C30E-RET-OA > 5000) AND                                    
                 (WS-C30E-RET-OA <= 10000)                                      
                  ADD 1       TO WS-C30E-CNT-10K                                
              ELSE                                                              
                 IF (WS-C30E-RET-OA > 10000) AND                                
                    (WS-C30E-RET-OA <= 20000)                                   
                    ADD 1       TO WS-C30E-CNT-20K                              
                 ELSE                                                           
                    IF (WS-C30E-RET-OA > 20000) AND                             
                       (WS-C30E-RET-OA <= 30000)                                
                       ADD 1       TO WS-C30E-CNT-30K                           
                    ELSE                                                        
                       IF (WS-C30E-RET-OA > 30000)                              
                          ADD 1       TO WS-C30E-CNT-40K                        
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF (F410-NUM-REPAY-PERIOD = ZEROES) OR                               
              (K800-NUM-BUY-SELL-TAG = 'B')    OR                               
              (K800-NUM-LOAN-TAG     = 'B')    OR                               
              (WS-CPF-RA             = 'Y')    OR                               
              (F410-NUM-CONTRA-TYPE NOT = SPACES AND LOW-VALUES)                
              ADD 1                 TO WS-C30E-CNT1                             
           ELSE                                                                 
              ADD 1                 TO WS-C30E-CNT2                             
           END-IF.                                                              
                                                                                
       3002-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3003-INIT-BP13F410-FLDS.                                                 
      ******************************************************************        
                                                                                
           IF F410-AMT-RETIRE-CPF1 NOT NUMERIC                                  
              MOVE ZEROES           TO F410-AMT-RETIRE-CPF1                     
           END-IF.                                                              
                                                                                
           IF F410-AMT-RETIRE-CPF2 NOT NUMERIC                                  
              MOVE ZEROES           TO F410-AMT-RETIRE-CPF2                     
           END-IF.                                                              
                                                                                
           IF F410-AMT-RETIRE-CPF3 NOT NUMERIC                                  
              MOVE ZEROES           TO F410-AMT-RETIRE-CPF3                     
           END-IF.                                                              
                                                                                
           IF F410-AMT-RETIRE-CPF4 NOT NUMERIC                                  
              MOVE ZEROES           TO F410-AMT-RETIRE-CPF4                     
           END-IF.                                                              
                                                                                
           IF F410-AMT-CPF1-BALANCE NOT NUMERIC                                 
              MOVE ZEROES           TO F410-AMT-CPF1-BALANCE                    
           END-IF.                                                              
                                                                                
           IF F410-AMT-CPF2-BALANCE NOT NUMERIC                                 
              MOVE ZEROES           TO F410-AMT-CPF2-BALANCE                    
           END-IF.                                                              
                                                                                
           IF F410-AMT-CPF3-BALANCE NOT NUMERIC                                 
              MOVE ZEROES           TO F410-AMT-CPF3-BALANCE                    
           END-IF.                                                              
                                                                                
           IF F410-AMT-CPF4-BALANCE NOT NUMERIC                                 
              MOVE ZEROES           TO F410-AMT-CPF4-BALANCE                    
           END-IF.                                                              
                                                                                
           IF F410-AMT-CPF1-INIT-BALANCE NOT NUMERIC                            
              MOVE ZEROES           TO F410-AMT-CPF1-INIT-BALANCE               
           END-IF.                                                              
                                                                                
           IF F410-AMT-CPF2-INIT-BALANCE NOT NUMERIC                            
              MOVE ZEROES           TO F410-AMT-CPF2-INIT-BALANCE               
           END-IF.                                                              
                                                                                
           IF F410-AMT-CPF3-INIT-BALANCE NOT NUMERIC                            
              MOVE ZEROES           TO F410-AMT-CPF3-INIT-BALANCE               
           END-IF.                                                              
                                                                                
           IF F410-AMT-CPF4-INIT-BALANCE NOT NUMERIC                            
              MOVE ZEROES           TO F410-AMT-CPF4-INIT-BALANCE               
           END-IF.                                                              
                                                                                
       3003-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-READ-BP13K40C.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                            TO BP13K40C-REC.              
           INITIALIZE                                BP13K40C-REC.              
                                                                                
           MOVE SPACES                            TO WS-K40C-FND.               
           MOVE F410-KEY-FLD                      TO K40C-KEY-FLD.              
                                                                                
           READ BP13K40C.                                                       
                                                                                
           EVALUATE WS-K40C-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                      TO WS-K40C-FND                
                                                                                
               WHEN 23                                                          
                    CONTINUE                                                    
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K40C : ' WS-K40C-STATUS          
                            ' REGN = ' K40C-KEY-FLD                             
                    PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
           IF K40C-AMT-CPF1-RESERVE NOT NUMERIC                                 
              MOVE ZEROES TO K40C-AMT-CPF1-RESERVE                              
           END-IF.                                                              
                                                                                
           IF K40C-AMT-CPF2-RESERVE NOT NUMERIC                                 
              MOVE ZEROES TO K40C-AMT-CPF2-RESERVE                              
           END-IF.                                                              
                                                                                
           IF K40C-AMT-CPF3-RESERVE NOT NUMERIC                                 
              MOVE ZEROES TO K40C-AMT-CPF3-RESERVE                              
           END-IF.                                                              
                                                                                
           IF K40C-AMT-CPF4-RESERVE NOT NUMERIC                                 
              MOVE ZEROES TO K40C-AMT-CPF4-RESERVE                              
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-WRITE-P13F30E2.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                 TO P13F30E2-REC                          
                                          MAILC30E-REC.                         
                                                                                
           INITIALIZE                     P13F30E2-REC                          
                                          MAILC30E-REC.                         
                                                                                
           ADD 1                       TO WS-SNO1.                              
           MOVE WS-SNO1                TO WS-SNO-ZZZZ9.                         
           MOVE WS-SNO-ZZZZ9           TO L30E-SN.                              
                                                                                
           MOVE F410-KEY-FLD           TO L30E-REGN.                            
           MOVE K800-NUM-FLAT-TYPE     TO L30E-FT.                              
           MOVE K800-NUM-LOAN-TAG      TO L30E-LT.                              
                                                                                
           IF F410-AMT-LOAN NOT NUMERIC                                         
              MOVE ZEROES              TO F410-AMT-LOAN                         
           END-IF.                                                              
                                                                                
           MOVE F410-AMT-LOAN          TO L30E-AMT-LOAN.                        
                                                                                
           MOVE K800-NUM-BUY-SELL-TAG  TO L30E-BFSL.                            
           MOVE F410-NUM-CONTRA-TYPE   TO L30E-CONTRA.                          
           MOVE K800-NUM-ALLO-CAT      TO L30E-ALC-MODE.                        
           MOVE K800-NUM-ALLO-SCHEME   TO L30E-ALC-SCH.                         
           MOVE K800-NUM-ELIG-SCHEME   TO L30E-ELG-SCH.                         
                                                                                
           MOVE K800-NUM-NRIC1         TO L30E-NRIC1                            
           MOVE K800-NUM-NRIC2         TO L30E-NRIC2                            
           MOVE K800-NUM-NRIC3         TO L30E-NRIC3                            
           MOVE K800-NUM-NRIC4         TO L30E-NRIC4                            
                                                                                
           MOVE WS-CPF-RA              TO L30E-CPF-RA.                          
                                                                                
           MOVE K40C-AMT-CPF1-RESERVE  TO L30E-RET-OA1.                         
           MOVE K40C-AMT-CPF2-RESERVE  TO L30E-RET-OA2.                         
           MOVE K40C-AMT-CPF3-RESERVE  TO L30E-RET-OA3.                         
           MOVE K40C-AMT-CPF4-RESERVE  TO L30E-RET-OA4.                         
                                                                                
           COMPUTE WS-TOT-RET-OA = K40C-AMT-CPF1-RESERVE +                      
                                   K40C-AMT-CPF2-RESERVE +                      
                                   K40C-AMT-CPF3-RESERVE +                      
                                   K40C-AMT-CPF4-RESERVE.                       
                                                                                
           MOVE WS-TOT-RET-OA         TO L30E-TOT-RET-OA.                       
                                                                                
           STRING F410-DTE-KEY-ISSUE(7:2) '/'                                   
                  F410-DTE-KEY-ISSUE(5:2) '/'                                   
                  F410-DTE-KEY-ISSUE(1:4)                                       
              DELIMITED BY SIZE INTO L30E-DTE-ISSUE.                            
                                                                                
                                                                                
           WRITE P13F30E2-REC       FROM L30E-DETAIL01.                         
           WRITE MAILC30E-REC       FROM L30E-DETAIL01.                         
                                                                                
           ADD 1                      TO WS-CNT-WRITE.                          
                                                                                
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-WRITE-P13F30E1.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES             TO P13F30E1-REC.                             
           INITIALIZE                 P13F30E1-REC.                             
                                                                                
           STRING '(' WS-CURR-CCYY '/' WS-CURR-MM ')'                           
               DELIMITED BY SIZE INTO L30E-C30E-MTH.                            
                                                                                
           MOVE WS-C30E-CNT1       TO L30E-C30E-NOT.                            
           MOVE WS-C30E-CNT2       TO L30E-C30E-OK.                             
           MOVE WS-C30E-CNT-5K     TO L30E-C30E-5K.                             
           MOVE WS-C30E-CNT-10K    TO L30E-C30E-10K.                            
           MOVE WS-C30E-CNT-20K    TO L30E-C30E-20K.                            
           MOVE WS-C30E-CNT-30K    TO L30E-C30E-30K.                            
           MOVE WS-C30E-CNT-40K    TO L30E-C30E-40K.                            
                                                                                
           WRITE P13F30E1-REC    FROM L30E-DETAIL-C30E.                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4001-WRITE-MAILC30E.                                                     
      ******************************************************************        
                                                                                
           PERFORM 7001-TABLE-HEADER   THRU 7001-EXIT.                          
                                                                                
           MOVE SPACES                   TO MAILC30E-REC.                       
           INITIALIZE                       MAILC30E-REC.                       
                                                                                
           STRING '(' WS-CURR-CCYY '/' WS-CURR-MM ')'                           
               DELIMITED BY SIZE INTO L30E-C30E-MTH.                            
                                                                                
           MOVE WS-C30E-CNT1             TO L30E-C30E-NOT.                      
           MOVE WS-C30E-CNT2             TO L30E-C30E-OK.                       
           MOVE WS-C30E-CNT-5K           TO L30E-C30E-5K.                       
           MOVE WS-C30E-CNT-10K          TO L30E-C30E-10K.                      
           MOVE WS-C30E-CNT-20K          TO L30E-C30E-20K.                      
           MOVE WS-C30E-CNT-30K          TO L30E-C30E-30K.                      
           MOVE WS-C30E-CNT-40K          TO L30E-C30E-40K.                      
                                                                                
           WRITE MAILC30E-REC    FROM L30E-DETAIL-C30E.                         
                                                                                
       4001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-HEADER-EMAIL.                                                       
      ******************************************************************        
                                                                                
            WRITE MAILC30E-REC FROM WS-MAIL-RPT1.                               
            WRITE MAILC30E-REC FROM WS-MAIL-RPT2.                               
            WRITE MAILC30E-REC FROM WS-MAIL-RPT3.                               
            WRITE MAILC30E-REC FROM WS-MAIL-RPT3A.                              
            WRITE MAILC30E-REC FROM WS-MAIL-RPT4.                               
            WRITE MAILC30E-REC FROM WS-MAIL-RPT5.                               
            WRITE MAILC30E-REC FROM WS-MAIL-RPT6.                               
            WRITE MAILC30E-REC FROM WS-MAIL-RPT6A.                              
                                                                                
            MOVE SPACES          TO  WS-MAIL-RPT7.                              
                                                                                
            STRING '(' WS-CURR-CCYY '/' WS-CURR-MM ')'                          
                  DELIMITED BY SIZE INTO L30E-DTE-HDR.                          
                                                                                
            STRING 'SUBJECT: REPORT ON APPS COLLECTED KEYS WITH RET '           
                   'OF OA MONEY '                                               
                   '(' WS-CURR-CCYY '/' WS-CURR-MM ')'                          
                  DELIMITED BY SIZE INTO WS-MAIL-RPT7.                          
                                                                                
            WRITE MAILC30E-REC FROM WS-MAIL-RPT7.                               
                                                                                
            STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY                   
                    DELIMITED BY SIZE INTO WS-DATE-RPT.                         
                                                                                
            WRITE MAILC30E-REC FROM WS-DATE-RPT-DTL.                            
                                                                                
            MOVE SPACES TO MAILC30E-REC.                                        
            WRITE MAILC30E-REC.                                                 
                                                                                
            WRITE MAILC30E-REC    FROM L30E-HEADER01 AFTER PAGE.                
            WRITE MAILC30E-REC    FROM L30E-HEADER02.                           
            WRITE MAILC30E-REC    FROM L30E-HEADER03 AFTER 2.                   
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           MOVE ZEROES TO WS-C30E-CNT1    WS-C30E-CNT2                          
                          WS-C30E-CNT-5K  WS-C30E-CNT-10K                       
                          WS-C30E-CNT-20K WS-C30E-CNT-30K                       
                          WS-C30E-CNT-40K.                                      
                                                                                
           MOVE ZEROES TO WS-AUG-RET-OA   WS-C30E-RET-OA WS-TOT-RET-OA.         
                                                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO WS-SYSTEM-DATE                
                                                  WS-CURR-DATE.                 
                                                                                
           IF WS-CURR-MM = 01                                                   
              MOVE 12                         TO WS-CURR-MM                     
              SUBTRACT 1                    FROM WS-CURR-CCYY                   
           ELSE                                                                 
              SUBTRACT 1                    FROM WS-CURR-MM                     
           END-IF.                                                              
                                                                                
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
              DELIMITED BY SIZE INTO L30E-DATE-HDR.                             
                                                                                
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO L30E-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO L30E-PAGE-HDR.                
                                                                                
           WRITE P13F30E2-REC    FROM L30E-HEADER01 AFTER PAGE.                 
           WRITE P13F30E2-REC    FROM L30E-HEADER02.                            
           WRITE P13F30E2-REC    FROM L30E-HEADER03 AFTER 2.                    
                                                                                
           WRITE P13F30E1-REC    FROM L30E-HEADER01 AFTER PAGE.                 
           WRITE P13F30E1-REC    FROM L30E-HEADER02.                            
                                                                                
           MOVE  SPACES          TO P13F30E1-REC.                               
           WRITE P13F30E1-REC.                                                  
                                                                                
           WRITE P13F30E1-REC    FROM L30E-HEADER04 AFTER 2.                    
                                                                                
           PERFORM 5000-HEADER-EMAIL         THRU 5000-EXIT.                    
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7001-TABLE-HEADER.                                                       
      ******************************************************************        
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
              DELIMITED BY SIZE INTO L30E-DATE-HDR.                             
                                                                                
                                                                                
           MOVE ZEROES           TO L30E-PAGE-HDR.                              
           MOVE 1                TO L30E-PAGE-HDR.                              
                                                                                
           MOVE  SPACES          TO MAILC30E-REC.                               
           WRITE MAILC30E-REC.                                                  
                                                                                
           MOVE  SPACES          TO MAILC30E-REC.                               
           WRITE MAILC30E-REC.                                                  
                                                                                
           WRITE MAILC30E-REC    FROM L30E-HEADER01 AFTER PAGE.                 
           WRITE MAILC30E-REC    FROM L30E-HEADER02.                            
                                                                                
           MOVE  SPACES          TO MAILC30E-REC.                               
           WRITE MAILC30E-REC.                                                  
                                                                                
           WRITE MAILC30E-REC    FROM L30E-HEADER04 AFTER 2.                    
                                                                                
                                                                                
       7001-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C30E'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1)  NO OF BP13F410 RECORDS READ............. : '           
                    WS-F410-READ.                                               
           DISPLAY '(2)  NO OF P13F30E2 RECORDS WRITTEN.......... : '           
                    WS-CNT-WRITE.                                               
                                                                                
           CLOSE    BP13F410                                                    
                    BP13K40C                                                    
                    BP13K800                                                    
                    BP13K893                                                    
                    P13F30E1                                                    
                    P13F30E2                                                    
                    MAILC30E.                                                   
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K893 - ERROR CLOSING : ' WS-K893-STATUS              
              MOVE WS-K893-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K40C-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K40C - ERROR CLOSING : ' WS-K40C-STATUS              
              MOVE WS-K893-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
