       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC42.                                                 
      *AUTHOR.        ED1.                                                      
      *DATE-WRITTEN.  01/12/2006.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO MATCH FD00 WITH F120                            *        
      *             AND WRITE RECORDS TO BP13FD04                      *        
      *                                                                *        
      * INPUT  FILES : BM06F120                                        *        
      *                BP13FD00                                        *        
      * OUTPUT FILES : BP13FD04.                                       *        
      *                                                                *        
      * CHG-NO    BY    ON    - DESCRIPTION                            *        
      * ---------------------------------                              *        
      * BP132937 ED1 01/12/06   NEW PROGRAM                            *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT   BM06F120      ASSIGN TO BM06F120.                           
           SELECT   BP13FD00      ASSIGN TO BP13FD00.                           
           SELECT   BP13FD04      ASSIGN TO BP13FD04.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BM06F120                RECORD CONTAINS 700 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BM06F120.                                                           
                                                                                
       FD   BP13FD00                RECORD CONTAINS 1600 CHARACTERS             
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13FD00.                                                           
                                                                                
       FD   BP13FD04                RECORD CONTAINS 1700 CHARACTERS             
                                    BLOCK CONTAINS 0 RECORDS                    
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13FD04.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  WS-F120-READ                 PIC 9(07) VALUE ZEROS.              
           05  WS-FD00-READ                 PIC 9(07) VALUE ZEROS.              
           05  WS-FD04-WRITTEN              PIC 9(07) VALUE ZEROS.              
           05  WS-UNMATCH-FD00              PIC 9(07) VALUE ZEROS.              
           05  WS-UNMATCH-F120              PIC 9(07) VALUE ZEROS.              
           05  WS-MATCH-REGN                PIC 9(07) VALUE ZEROS.              
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CUR-DD                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-MM                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-CCYY                 PIC 9(04) VALUE ZEROES.              
                                                                                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE                  PIC X(08) VALUE SPACES.             
           05  WS-SYS-TIME                  PIC X(08) VALUE SPACES.             
           05  FILLER                       PIC X(05) VALUE SPACES.             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BM06F120       THRU 2000-EXIT.                     
           PERFORM 2100-READ-BP13FD00       THRU 2100-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL F120-REGN-NO  = HIGH-VALUES AND                              
                   FD00-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 9000-CLOSING-ROUTINE     THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT BM06F120                                                  
                      BP13FD00                                                  
               OUTPUT BP13FD04.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
           MOVE WS-SYS-DATE(1:4)      TO WS-CUR-CCYY.                           
           MOVE WS-SYS-DATE(5:2)      TO WS-CUR-MM.                             
           MOVE WS-SYS-DATE(7:2)      TO WS-CUR-DD.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BM06F120.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BM06F120                                                        
             AT END MOVE HIGH-VALUES TO F120-REGN-NO                            
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD  1                    TO WS-F120-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BP13FD00.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FD00                                                        
             AT END MOVE HIGH-VALUES TO  FD00-NUM-REGN                          
             GO TO 2100-EXIT.                                                   
                                                                                
           ADD  1                    TO  WS-FD00-READ.                          
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF FD00-NUM-REGN = F120-REGN-NO                                      
              PERFORM 3100-MATCH-REGN        THRU 3100-EXIT                     
              PERFORM 2100-READ-BP13FD00     THRU 2100-EXIT                     
              PERFORM 2000-READ-BM06F120  THRU  2000-EXIT                       
           ELSE                                                                 
              IF FD00-NUM-REGN > F120-REGN-NO                                   
                 ADD   1                     TO WS-UNMATCH-F120                 
                 PERFORM 2000-READ-BM06F120  THRU 2000-EXIT                     
              ELSE                                                              
                 ADD   1                     TO WS-UNMATCH-FD00                 
                 PERFORM 2100-READ-BP13FD00  THRU 2100-EXIT                     
                                                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-MATCH-REGN.                                                         
      *-------------------------------------------------------------            
                                                                                
           ADD 1          TO WS-MATCH-REGN.                                     
                                                                                
           MOVE  SPACES          TO BP13FD04-REC.                               
           INITIALIZE               BP13FD04-REC.                               
                                                                                
           MOVE BP13FD00-REC     TO BP13FD04-REC.                               
           MOVE F120-BO-CODE     TO FD04-BO-CODE.                               
           MOVE F120-BLK-NO      TO FD04-NUM-BLK.                               
           MOVE F120-STREET-NAME TO FD04-STREET-NAME.                           
           MOVE F120-UNIT-NO     TO FD04-UNIT-NO.                               
           MOVE F120-POSTAL-CODE TO FD04-POSTAL-CODE                            
           MOVE F120-CDE-BUC     TO FD04-CDE-BUC.                               
                                                                                
           WRITE  BP13FD04-REC.                                                 
           ADD 1  TO WS-FD04-WRITTEN.                                           
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CC40'.                                
           DISPLAY 'CURRENT DATE   : ' WS-CURRENT-DATE.                         
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BP13FD00 READ     : ' WS-FD00-READ.                  
           DISPLAY 'NO. OF BM06F120 READ     : ' WS-F120-READ.                  
           DISPLAY 'NO. OF F120 UNMATCHED    : ' WS-UNMATCH-F120.               
           DISPLAY 'NO. OF FD00 UNMATCHED    : ' WS-UNMATCH-FD00.               
           DISPLAY 'NO. OF FD00/F120 MATCHED : ' WS-MATCH-REGN.                 
           DISPLAY 'NO. OF BP13FD04  WRITTEN : ' WS-FD04-WRITTEN.               
                                                                                
           CLOSE BM06F120                                                       
                 BP13FD00                                                       
                 BP13FD04.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
