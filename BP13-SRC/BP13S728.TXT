/*******************************************************************/           
/*                     SYSTEM OF COMMITMENT    (BP13)              */           
/*=================================================================*/           
/*                                                                 */           
/* OBJECTIVES :                                                    */           
/*      TO PRINT REPORT ON DECEASED LESSEE SPOUSE CASES, FOUND IN  */           
/*      RFS AS WIDOW AND FOUND IN RDB AS SPOUSE                    */           
/*-----------------------------------------------------------------*/           
/* MODIFICATIONS                                                   */           
/* =============                                                   */           
/* CHGE      DATE     BY   DESCRIPTION                             */           
/* -------- -------- ---- ---------------------------------------- */           
/* BP130573 04111998 NOR  NEW PROGRAM                              */           
/* BP130630 23011999 NOR  CHGE F715 F716 EXPAND TO 250             */           
/* BP130810 28022000 AKT  INCLUDE DIVORCE/SEPARATED                */           
/*                        TO INCLUDE BP13F500 AND CHK HOUSEHOLD,   */           
/*                        TO INCLUDE NEW FIELD SMS SALE TYP.       */           
/*******************************************************************/           
   OPTIONS NODATE NONUMBER;                                                     
                                                                                
   DATA BP13F716;                                                               
      INFILE BP13F716;                                                          
      INPUT  @01   REG        $CHAR8.                                           
             @09   NRIC       $CHAR9.                                           
             @21   REL_APP    $CHAR2.                                           
             @28   D_REGN     $CHAR8.                                           
             @51   SCHACCT    $CHAR9.                                           
             @115  D_MOVEIN   $CHAR8.                                           
             @89   CDE_OCC    $CHAR1.                                           
             @99   REL_RDB    $CHAR2.                                           
             @133  SAL_TYP    $CHAR2.                                           
            ;                                                                   
                                                                                
   LENGTH SCH_ACCT $11;                                                         
   SCH_ACCT = SUBSTR(SCHACCT,1,4)||'-'||                                        
              SUBSTR(SCHACCT,5,4)||'-'||                                        
              SUBSTR(SCHACCT,9,1);                                              
                                                                                
   LENGTH DTE_REGN $10;                                                         
   DTE_REGN = SUBSTR(D_REGN,1,4)||'/'||                                         
              SUBSTR(D_REGN,5,2)||'/'||                                         
              SUBSTR(D_REGN,7,2);                                               
                                                                                
   LENGTH DTE_MOVE $10;                                                         
   DTE_MOVE = SUBSTR(D_MOVEIN,1,4)||'/'||                                       
              SUBSTR(D_MOVEIN,5,2)||'/'||                                       
              SUBSTR(D_MOVEIN,7,2);                                             
                                                                                
                                                                                
   PROC SORT;                                                                   
      BY REG;                                                                   
                                                                                
   DATA BP13F500;                                                               
      INFILE BP13F500;                                                          
      INPUT  @123  REG        $CHAR8.                                           
             @116  HSEHLD     $CHAR1.;                                          
                                                                                
   PROC SORT;                                                                   
      BY REG;                                                                   
                                                                                
   DATA P13F716A;                                                               
   MERGE BP13F716(IN=A) BP13F500(IN=B);                                         
   BY REG;                                                                      
       IF A THEN                                                                
          DO;                                                                   
          IF HSEHLD ^= 'G' THEN OUTPUT P13F716A;                                
          END;                                                                  
                                                                                
   PROC SORT;                                                                   
      BY REG;                                                                   
                                                                                
   DATA _NULLS_;                                                                
   SET P13F716A END=EOF;                                                        
      BY REG;                                                                   
                                                                                
      OPTIONS PAGESIZE=55 LS=132 NODATE;                                        
      FILE BP13L728 HEADER=RPTHDR NOTITLES;                                     
                                                                                
                                                                                
      SNO+1;                                                                    
                                                                                
      PUT @010  SNO             Z5.                                             
          @017  REG         $CHAR8.                                             
          @027  NRIC        $CHAR9.                                             
          @040  REL_APP     $CHAR2.                                             
          @048  DTE_REGN    $CHAR10.                                            
          @060  SCH_ACCT    $CHAR11.                                            
          @077  DTE_MOVE    $CHAR10.                                            
          @093  CDE_OCC     $CHAR1.                                             
          @103  REL_RDB     $CHAR2.                                             
          @117  SAL_TYP     $CHAR2.                                             
          ;                                                                     
      RETURN;                                                                   
                                                                                
 RPTHDR:                                                                        
   LINE_CNT=4;                                                                  
   PAGE+1;                                                                      
   CUR_DATE=TODAY();                                                            
                                                                                
   PUT @002 'BP13L728  HDB'                                                     
       @042 'S Y S T E M  O F  C O M M I T M E N T'                             
       @103 'DATE : ' CUR_DATE DDMMYY10.;                                       
                                                                                
   PUT                                                                          
   @035 'REPORT ON DECEASED/DIVORCED/SEPARATED LESSEE SPOUSE (HHTY:H)'          
   @103 'PAGE : ' PAGE;                                                         
                                                                                
   PUT /;                                                                       
                                                                                
   PUT @10  'S/NO '                                                             
       @17  '  REGN  '                                                          
       @27  '   NRIC  '                                                         
       @38  'RELATION'                                                          
       @48  'REGN DATE'                                                         
       @60  'SCHEME ACCOUNT'                                                    
       @76  'MOVEIN DATE'                                                       
       @89  'CODE OCC'                                                          
       @99  'RDB RELATION'                                                      
       @113 'SMS SALE TYP'                                                      
            ;                                                                   
                                                                                
   PUT @10  '-----'                                                             
       @17  '--------'                                                          
       @27  '---------'                                                         
       @38  '--------'                                                          
       @48  '----------'                                                        
       @60  '--------------'                                                    
       @76  '-----------'                                                       
       @89  '--------'                                                          
       @99  '------------'                                                      
       @113 '------------'                                                      
            ;                                                                   
                                                                                
                                                                                
   RETURN;                                                                      
                                                                                
