       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY9G.                                                 
      *DATE-WRITTEN.  15/05/1999.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO MATCH INPUT REC   WITH F595, WHEN MATCH OUTPUT  *        
      *             F595 RECORDS                                       *        
      *                                                                *        
      * INPUT  FILES : BP13F730                                        *        
      *                BP13F595                                        *        
      * OUTPUT FILES : P13F595A                                        *        
      *                                                                *        
      ******************************************************************        
      * BP136467 10/02/2017 FNP1 BYPASS RECORD IF MATURE ESTATE       *         
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT   BP13F730  ASSIGN TO BP13F730.                               
           SELECT   BP13F595  ASSIGN TO BP13F595.                               
           SELECT   BP13K813  ASSIGN        TO BP13K813                         
                              ACCESS MODE   IS DYNAMIC                          
                              ORGANIZATION  IS INDEXED                          
                              RECORD KEY    IS K813-KEY-FLD                     
                              FILE STATUS   IS WS-K813-STATUS.                  
                                                                                
           SELECT   P13F595A  ASSIGN TO P13F595A.                               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13F730                RECORD CONTAINS 500 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13F730.                                                           
                                                                                
       FD   BP13F595                RECORD CONTAINS 500 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13F595.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   P13F595A                RECORD CONTAINS 500 CHARACTERS              
                                    LABEL RECORDS ARE OMITTED                   
                                    RECORDING MODE IS F.                        
       01   F595A-REC               PIC X(500).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-STATUS.                                                           
           05  WS-K813-STATUS               PIC 99    VALUE ZEROS.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-BP13F730-READ             PIC 9(7)  VALUE ZEROS.              
           05  WS-F595-READ                 PIC 9(7)  VALUE ZEROS.              
           05  WS-F595A-WRTN                PIC 9(7)  VALUE ZEROS.              
           05  WS-F595A-BYPASS              PIC 9(7)  VALUE ZEROS.              
           05  WS-MATURE-BYPASS             PIC 9(7)  VALUE ZEROS.              
           05  WS-BP13F730-NOTFD            PIC 9(7)  VALUE ZEROS.              
           05  WS-BP13F595-NOTFD            PIC 9(7)  VALUE ZEROS.              
           05  WS-MATCH                     PIC 9(7)  VALUE ZEROS.              
           05  WS-OUTPUT                    PIC X(1)  VALUE SPACES.             
           05  WS-JUL-REQUEST               PIC X(1)  VALUE SPACES.             
           05  WS-MATURE                    PIC X(1)  VALUE SPACES.             
           05  WS-NT                        PIC X(3)  VALUE SPACES.             
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-YY                    PIC X(4).                           
           05  WS-SYS-MM                    PIC X(2).                           
           05  WS-SYS-DD                    PIC X(2).                           
                                                                                
       PROCEDURE DIVISION.                                                      
       0000-MAIN.                                                               
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F730       THRU 2000-EXIT.                     
           PERFORM 2010-READ-BP13F595       THRU 2010-EXIT.                     
           PERFORM 3000-PROCESS             THRU 3000-EXIT                      
             UNTIL F730-NUM-NRIC1 = HIGH-VALUES AND                             
                   F595-NUM-NRIC1 = HIGH-VALUES.                                
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
           OPEN INPUT BP13F730                                                  
                      BP13F595                                                  
                      BP13K813                                                  
               OUTPUT P13F595A.                                                 
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K813 STATUS IS ' WS-K813-STATUS            
              MOVE     WS-K813-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-FILES.                                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F730.                                                      
           READ BP13F730                                                        
             AT END                                                             
                MOVE HIGH-VALUES       TO  F730-NUM-NRIC1                       
            NOT AT END                                                          
                ADD  1                 TO  WS-BP13F730-READ                     
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2010-READ-BP13F595.                                                      
           READ BP13F595                                                        
             AT END                                                             
                MOVE HIGH-VALUES       TO  F595-NUM-NRIC1                       
            NOT AT END                                                          
                ADD  1                 TO  WS-F595-READ                         
           END-READ.                                                            
                                                                                
           IF  BP13F595-REC(467:2) = SPACES                                     
               MOVE ZEROS TO BP13F595-REC(467:2)                                
           END-IF.                                                              
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-PROCESS.                                                            
                                                                                
           MOVE 'N' TO WS-JUL-REQUEST.                                          
                                                                                
           PERFORM 5000-READ-BP13K813      THRU 5000-EXIT.                      
                                                                                
           EVALUATE TRUE                                                        
           WHEN F595-NUM-NRIC1 = F730-NUM-NRIC1                                 
                ADD 1 TO WS-MATCH                                               
      *         PERFORM 4000-OUTPUT-BP13F730 THRU 4000-EXIT                     
      *           UNTIL F595-NUM-NRIC1 NOT = F730-NUM-NRIC1                     
      *                                                                         
                MOVE BP13F730-REC(467:2) TO  BP13F595-REC(467:2)                
                MOVE 'Y'                 TO  BP13F595-REC(466:1)                
                  DISPLAY 'R=' F730-NUM-NRIC1 ' D=' F730-DTE-BALLOT             
                          ' F595=' BP13F595-REC(467:2)                          
                          ' F730=' BP13F730-REC(345:2)                          
                IF WS-MATURE NOT = 'Y'                                          
                   PERFORM 4000-OUTPUT-BP13F730 THRU 4000-EXIT                  
                ELSE                                                            
                   ADD 1 TO WS-MATURE-BYPASS                                    
                END-IF                                                          
                                                                                
                PERFORM 2010-READ-BP13F595     THRU 2010-EXIT                   
                PERFORM 2000-READ-BP13F730  THRU  2000-EXIT                     
                                                                                
           WHEN F595-NUM-NRIC1 > F730-NUM-NRIC1                                 
                ADD 1 TO WS-BP13F595-NOTFD                                      
                PERFORM 2000-READ-BP13F730  THRU  2000-EXIT                     
                                                                                
           WHEN F595-NUM-NRIC1 < F730-NUM-NRIC1                                 
                IF WS-MATURE NOT = 'Y'                                          
                   PERFORM 4000-OUTPUT-BP13F730 THRU 4000-EXIT                  
                ELSE                                                            
                   ADD 1 TO WS-MATURE-BYPASS                                    
                END-IF                                                          
                PERFORM 2010-READ-BP13F595     THRU 2010-EXIT                   
                ADD 1 TO WS-BP13F730-NOTFD                                      
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-OUTPUT-BP13F730.                                                    
                                                                                
           IF BP13F595-REC(493:2) = SPACES                                      
              MOVE BP13F595-REC(290:2) TO BP13F595-REC(493:2)                   
           END-IF.                                                              
           MOVE WS-NT TO F595-CDE-NT1.                                          
           WRITE F595A-REC             FROM  BP13F595-REC.                      
           ADD   1                     TO  WS-F595A-WRTN.                       
                                                                                
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-READ-BP13K813.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                          TO WS-MATURE.                   
           MOVE SPACES                          TO WS-NT.                       
           MOVE SPACES                          TO BP13K813-REC.                
           INITIALIZE                              BP13K813-REC.                
                                                                                
           MOVE F595-CDE-NT1                    TO K813-NUM-ZONE.               
           MOVE F595-CDE-FLAT-TYPE              TO K813-NUM-FLAT-TYPE.          
           MOVE F595-DTE-BALLOT                 TO K813-DTE-BALLOT.             
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    MOVE K813-CDE-NT TO WS-NT                                   
                    IF K813-NUM-MATURE-EST-TAG = 'Y'                            
                       MOVE 'Y' TO WS-MATURE                                    
                    END-IF                                                      
               WHEN 23                                                          
                    DISPLAY 'BP13K813 NOT FND: ' K813-KEY-FLD                   
                    MOVE SPACES      TO WS-NT                                   
               WHEN OTHER                                                       
                    MOVE WS-K813-STATUS               TO RETURN-CODE            
                    DISPLAY 'ERROR READING BP13K813: ' WS-K813-STATUS           
                            ' KEY : ' K813-KEY-FLD                              
                    PERFORM  9000-CLOSE-FILES   THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
           DISPLAY 'MATBP13F730 CONTROL TOTAL           '.                      
           DISPLAY 'RUN DATE : ' WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-YY.         
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF BP13F730 READ    : ' WS-BP13F730-READ.               
           DISPLAY 'NO. OF BP13F595 READ    : ' WS-F595-READ.                   
           DISPLAY 'NO. OF BP13F595 MATCH   : ' WS-MATCH.                       
           DISPLAY 'NO. OF BP13F730 NOTFD   : ' WS-BP13F730-NOTFD.              
           DISPLAY 'NO. OF BP13F595 NOTFD   : ' WS-BP13F595-NOTFD.              
           DISPLAY 'NO. OF P13F595A WRITTEN : ' WS-F595A-WRTN.                  
           DISPLAY 'NO. OF P13F595A BYPASS  : ' WS-F595A-BYPASS.                
           DISPLAY 'NO. OF MATURE RECORD    : ' WS-MATURE-BYPASS.               
           DISPLAY '*************************************'.                     
                                                                                
           CLOSE BP13F730                                                       
                 BP13F595                                                       
                 BP13K813                                                       
                 P13F595A.                                                      
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K813 STATUS IS ' WS-K813-STATUS            
              MOVE     WS-K813-STATUS TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
