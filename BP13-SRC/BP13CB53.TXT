       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB53.                                                 
      *AUTHOR.        EDEN ROSE DINGAL                                          
      *DATE-WRITTEN.  03/01/07.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  GET HH INCOME,SIZE,RELATIONSHIP,MS,AGE,DOB  *            
      *                                                            *            
      *  INPUT FILES:  1. BP13F825                                 *            
      *                2. BP13F730                                 *            
      *                                                            *            
      *  OUTPUT FILE:  1. P13F730A                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP133111 ED1 20080103 NEW PROGRAM                          *            
      * BP133340 JB8 20080625 GET AGE,DOB,COUPLES,HA1-MS           *            
      * BP134122 PCL 20110113 TO ADD CHECK ON ETHNIC CDE           *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F825 ASSIGN        TO BP13F825.                           
           SELECT BP13F730  ASSIGN       TO BP13F730.                           
           SELECT P13F730A  ASSIGN       TO P13F730A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F730                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F730.                                                           
                                                                                
       FD  BP13F825                                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F825.                                                           
                                                                                
       FD  P13F730A                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  P13F730A-REC.                                                        
           05 FILLER               PIC X(460).                                  
           05 F730A-HH-SIZE        PIC 9(2).                                    
           05 F730A-HH-INCOME      PIC 9(5).                                    
           05 F730A-APPLN-AGE      PIC 9(4).                                    
           05 F730A-HA1-DOB        PIC 9(8).                                    
           05 F730A-COUPLES        PIC X(1).                                    
           05 F730A-HA1-MS         PIC X(1).                                    
           05 FILLER               PIC X(19).                                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  NO-F825-READ        PIC 9(6)    VALUE ZEROS.                     
           05  NO-F730-READ        PIC 9(6)    VALUE ZEROS.                     
           05  NO-F730A-WRITE      PIC 9(6)    VALUE ZEROS.                     
           05  WS-MATCHED          PIC 9(6)    VALUE ZEROS.                     
                                                                                
       01  WS-CUR-DTE               PIC X(8)  VALUE SPACES.                     
       01  WS-CUR-DTE-R REDEFINES WS-CUR-DTE.                                   
           05  WS-CUR-DTE-N         PIC 9(8).                                   
       01  F730-STATUS              PIC 99    VALUE 0.                          
       01  WS-PREV-REGN             PIC X(08) VALUE SPACES.                     
       01  WS-HA1-ETHNIC            PIC X(09) VALUE SPACES.                     
       01  WS-F730-HH-SIZE          PIC 9     VALUE 0.                          
       01  WS-F730-HH-INCOME        PIC 9(6)  VALUE 0.                          
       01  WS-DTE-REQUEST.                                                      
           05  WS-REQUEST-CYM       PIC 9(6)  VALUE ZEROES.                     
           05  WS-REQUEST-DD        PIC 9(2)  VALUE ZEROES.                     
       01  WS-DTE-BIRTH.                                                        
           05  WS-BIRTH-CYM         PIC 9(6)  VALUE ZEROES.                     
           05  WS-BIRTH-DD          PIC 9(2)  VALUE ZEROES.                     
       01  WS-AGE                   PIC 9(6)  VALUE ZEROES.                     
       01  WS-NUM-AGE REDEFINES WS-AGE.                                         
           05 WS-APPLN-AGE          PIC 9(4).                                   
           05 WS-NUM-REMNG          PIC 9(2).                                   
       01  WS-MS                    PIC X(1).                                   
       01  WS-EOF                   PIC X     VALUE 'N'.                        
       01  WS-SINGLE-FND            PIC X     VALUE 'N'.                        
       01  WS-WIDOWED-FND           PIC X     VALUE 'N'.                        
       01  WS-DIVORCED-FND          PIC X     VALUE 'N'.                        
       01  WS-SPOUSE-FND            PIC X     VALUE 'N'.                        
       01  WS-FATHER-FND            PIC X     VALUE 'N'.                        
       01  WS-MOTHER-FND            PIC X     VALUE 'N'.                        
                                                                                
       01 WS-F825-REC.                                                          
          05 FILLER             PIC X(152).                                     
          05 WS-F825-FILE-STAT  PIC X(02).                                      
          05 FILLER             PIC X(196).                                     
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPENING-ROUTINE  THRU 1000-EXIT.                        
           PERFORM 2000-READ-F825        THRU 2000-EXIT.                        
           PERFORM 2100-READ-F730        THRU 2100-EXIT.                        
           PERFORM 4000-PROCESS-FILES    THRU 4000-EXIT                         
                   UNTIL F825-NUM-REGN = HIGH-VALUES AND                        
                         F730-NUM-REGN = HIGH-VALUES.                           
           PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F825                                                 
                       BP13F730                                                 
                OUTPUT P13F730A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DTE.                            
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2000-READ-F825.                                                          
      *-------------------------------------------------------------            
            READ BP13F825 AT END                                                
                 MOVE HIGH-VALUES TO F825-NUM-REGN                              
                 GO TO 2000-EXIT.                                               
                                                                                
            ADD  1            TO NO-F825-READ.                                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2100-READ-F730.                                                          
      *-------------------------------------------------------------            
            READ BP13F730 AT END                                                
                 MOVE HIGH-VALUES TO F730-NUM-REGN                              
                 GO TO 2100-EXIT.                                               
                                                                                
            ADD  1            TO NO-F730-READ.                                  
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES          TO WS-PREV-REGN                                 
                                   WS-HA1-ETHNIC.                               
           MOVE 0               TO WS-F730-HH-SIZE.                             
           MOVE 0               TO WS-F730-HH-INCOME.                           
           MOVE ZEROES          TO WS-AGE                                       
                                   WS-MS                                        
                                   WS-DTE-BIRTH.                                
           MOVE 'N'             TO WS-SINGLE-FND                                
                                   WS-WIDOWED-FND                               
                                   WS-DIVORCED-FND                              
                                   WS-SPOUSE-FND                                
                                   WS-FATHER-FND                                
                                   WS-MOTHER-FND.                               
                                                                                
           IF F825-NUM-REGN = F730-NUM-REGN                                     
              MOVE F825-NUM-REGN TO WS-PREV-REGN                                
              PERFORM 4100-COMPUTE-HH-SIZE   THRU 4100-EXIT                     
                      UNTIL F825-NUM-REGN NOT  = WS-PREV-REGN OR                
                            F825-NUM-REGN  = HIGH-VALUES                        
                                                                                
              PERFORM 5000-WRITE-P13F730A    THRU 5000-EXIT                     
              ADD 1                            TO WS-MATCHED                    
              PERFORM 2100-READ-F730         THRU 2100-EXIT                     
           ELSE                                                                 
              IF F825-NUM-REGN > F730-NUM-REGN                                  
                 PERFORM 5000-WRITE-P13F730A THRU 5000-EXIT                     
                 PERFORM 2100-READ-F730      THRU 2100-EXIT                     
              ELSE                                                              
                 PERFORM 2000-READ-F825      THRU 2000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4100-COMPUTE-HH-SIZE.                                                    
      *-------------------------------------------------------------            
           IF F825-AMT-INCOME IS NOT NUMERIC                                    
              MOVE  0  TO F825-AMT-INCOME                                       
           END-IF.                                                              
                                                                                
           ADD 1                TO   WS-F730-HH-SIZE.                           
           ADD F825-AMT-INCOME  TO   WS-F730-HH-INCOME.                         
                                                                                
           IF F825-NUM-RELATIONSHIP = '00'                                      
              IF F825-DTE-BIRTH NOT NUMERIC                                     
                 MOVE ZEROES                  TO F825-DTE-BIRTH                 
              END-IF                                                            
                                                                                
              IF F825-NUM-NRIC = F730-NUM-NRIC1                                 
                 MOVE F825-NUM-ETHNIC         TO WS-HA1-ETHNIC                  
              END-IF                                                            
                                                                                
              MOVE F825-DTE-BIRTH             TO WS-DTE-BIRTH                   
              MOVE F730-DTE-REQUEST           TO WS-DTE-REQUEST                 
              COMPUTE WS-AGE = WS-REQUEST-CYM - WS-BIRTH-CYM                    
                                                                                
              MOVE F825-NUM-MARITAL-STATUS    TO WS-MS                          
                                                                                
              EVALUATE F825-NUM-MARITAL-STATUS                                  
                 WHEN '1 '                                                      
                    MOVE 'Y'   TO WS-SINGLE-FND                                 
                 WHEN '3 '                                                      
                    MOVE 'Y'   TO WS-WIDOWED-FND                                
                 WHEN '4 '                                                      
                 WHEN '5 '                                                      
                    MOVE 'Y'   TO WS-DIVORCED-FND                               
              END-EVALUATE                                                      
           ELSE                                                                 
              EVALUATE F825-NUM-RELATIONSHIP                                    
                 WHEN '20'                                                      
                    MOVE 'Y'   TO WS-SPOUSE-FND                                 
                 WHEN '02'                                                      
                    MOVE 'Y'   TO WS-FATHER-FND                                 
                 WHEN '12'                                                      
                    MOVE 'Y'   TO WS-MOTHER-FND                                 
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F825   THRU 2000-EXIT.                             
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       5000-WRITE-P13F730A.                                                     
      ***************************************************************           
                                                                                
           IF F730-CDE-CATEGORY = SPACES OR LOW-VALUES                          
              IF WS-HA1-ETHNIC = 0 OR 1 OR 2 OR 3 OR 4                          
                 MOVE 2                           TO F730-CDE-CATEGORY          
              ELSE IF WS-HA1-ETHNIC = 5                                         
                 MOVE 1                           TO F730-CDE-CATEGORY          
              ELSE IF WS-HA1-ETHNIC = 6                                         
                 MOVE 3                           TO F730-CDE-CATEGORY          
              ELSE IF WS-HA1-ETHNIC = 7 OR 9 OR ' '                             
                 MOVE 4                           TO F730-CDE-CATEGORY          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE BP13F730-REC      TO P13F730A-REC.                              
                                                                                
           MOVE WS-F730-HH-SIZE   TO F730A-HH-SIZE.                             
           MOVE WS-F730-HH-INCOME TO F730A-HH-INCOME.                           
           MOVE WS-DTE-BIRTH      TO F730A-HA1-DOB.                             
           MOVE WS-MS             TO F730A-HA1-MS.                              
           MOVE WS-APPLN-AGE      TO F730A-APPLN-AGE.                           
                                                                                
           IF F730-CDE-ELIG-SCH = 'FS '                                         
              MOVE '2'  TO  F730A-COUPLES                                       
           ELSE                                                                 
           IF (WS-SPOUSE-FND = 'Y') OR                                          
              (WS-FATHER-FND = 'Y' AND WS-MOTHER-FND = 'Y')                     
              MOVE '1'  TO  F730A-COUPLES                                       
           ELSE                                                                 
           IF WS-SINGLE-FND = 'Y'                                               
              MOVE '3'  TO  F730A-COUPLES                                       
           ELSE                                                                 
           IF WS-WIDOWED-FND = 'Y'                                              
              MOVE '4'  TO  F730A-COUPLES                                       
           ELSE                                                                 
           IF WS-DIVORCED-FND = 'Y'                                             
              MOVE '5'  TO  F730A-COUPLES                                       
           ELSE                                                                 
              MOVE '6'  TO  F730A-COUPLES                                       
           END-IF.                                                              
                                                                                
           WRITE P13F730A-REC                                                   
           ADD  1                 TO NO-F730A-WRITE.                            
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           CLOSE BP13F825                                                       
                 BP13F730                                                       
                 P13F730A.                                                      
                                                                                
           DISPLAY '-----------------------------------------'.                 
           DISPLAY '          PROGRAM BP13CB53 '.                               
           DISPLAY '        RUN DATE : ' WS-CUR-DTE.                            
           DISPLAY 'NO. OF F825 REC READ        > ' NO-F825-READ.               
           DISPLAY 'NO. OF F730 REC READ        > ' NO-F730-READ.               
           DISPLAY 'NO. OF F730A WRITTEN        > ' NO-F730A-WRITE.             
           DISPLAY 'NO. OF MATCHED CASES        > ' WS-MATCHED.                 
           DISPLAY '-----------------------------------------'.                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
