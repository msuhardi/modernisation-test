       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C507.                                                 
       AUTHOR.        BALA AMUTHAN.                                             
       DATE-WRITTEN.  22/03/2007.                                               
      *****************************************************************         
      * OBJECTIVE  - TO CREATE REPORT FILE                            *         
      *                                                               *         
      *                                                               *         
      * SUBROUTINE    - NIL                                           *         
      *                                                               *         
      *----------------------------------------------------------------         
      * REF NO     DATE      BY   AMENDMENTS/ENHANCEMENTS             *         
      * -------    ----      ---  -----------------------             *         
      * BP133026   20070322  BA2  NEW PROG.                           *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT  BP13F200  ASSIGN        TO   BP13F200.                       
                                                                                
           SELECT  BP13K800  ASSIGN        TO   BP13K800                        
                             ACCESS MODE   IS   RANDOM                          
                             ORGANIZATION  IS   INDEXED                         
                             RECORD KEY    IS   K800-NUM-REGN                   
                             FILE STATUS   IS   WS-K800-STATUS.                 
                                                                                
           SELECT  BP13K820  ASSIGN        TO   BP13K820                        
                             ACCESS MODE   IS   RANDOM                          
                             ORGANIZATION  IS   INDEXED                         
                             RECORD KEY    IS   K820-KEY-FLD                    
                             FILE STATUS   IS   WS-K820-STATUS.                 
                                                                                
           SELECT  BP13L507  ASSIGN        TO   BP13L507.                       
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F200                     BLOCK CONTAINS 0 RECORDS                
                                        RECORD CONTAINS 300  CHARACTERS         
                                        LABEL RECORDS ARE STANDARD              
                                        RECORDING MODE IS F.                    
       COPY BP13F200.                                                           
                                                                                
       FD  BP13K800                                                             
                                        RECORD CONTAINS 2000 CHARACTERS.        
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
                                        RECORD CONTAINS 400  CHARACTERS.        
       COPY BP13K820.                                                           
                                                                                
       FD  BP13L507                     BLOCK CONTAINS 0 RECORDS                
                                        RECORD CONTAINS 150 CHARACTERS          
                                        LABEL RECORDS ARE STANDARD              
                                        RECORDING MODE IS F.                    
                                                                                
       01  BP13L507-REC.                                                        
           05 L507-NUM-SELTYPE              PIC X(03).                  00161105
           05 L507-DTE-BALLOT               PIC X(06).                  00161105
           05 L507-NME-OCCP                 PIC X(66).                          
           05 L507-NUM-REGN                 PIC X(08).                          
           05 L507-NUM-NRIC                 PIC X(09).                          
           05 L507-NUM-NT-FT-QUEUE          PIC X(05).                          
           05 L507-DTE-BK-APPT              PIC X(08).                          
           05 L507-TME-BK-APPT              PIC X(04).                          
           05 L507-NUM-FLAT-TYPE            PIC X(02).                          
           05 FILLER                        PIC X(39).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS               PIC 99    VALUE 0.                  
           05  WS-K820-STATUS               PIC 99    VALUE 0.                  
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05 WS-CCYY                       PIC 9999.                           
           05 WS-MM                         PIC 99.                             
           05 WS-DD                         PIC 99.                             
                                                                                
       01  WS-REC-FND                       PIC X(1)  VALUE 'N'.                
           88  FND-REC                                VALUE 'Y'.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F200                  PIC 9(7)  COMP VALUE 0.             
           05  WS-CNT-L507                  PIC 9(7)  COMP VALUE 0.             
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ************************************                                      
       0000-MAIN-PARA.                                                          
      ************************************                                      
                                                                                
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13F200    THRU 2000-EXIT.                        
           PERFORM 3000-MAIN-PROCESS     THRU 3000-EXIT                         
              UNTIL F200-NUM-REGN = HIGH-VALUES.                                
           PERFORM 9999-CLOSE-ROUTINE    THRU  9999-EXIT.                       
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ************************************                                      
       1000-START-UP.                                                           
      ************************************                                      
                                                                                
           OPEN INPUT  BP13F200                                                 
                       BP13K800                                                 
                       BP13K820                                                 
               OUTPUT  BP13L507.                                                
                                                                                
           IF WS-K800-STATUS NOT = 0  AND                                       
              WS-K800-STATUS NOT = 97                                           
              DISPLAY ' OPENING ERROR, BP13K800 STATUS ' WS-K800-STATUS         
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
              PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 0  AND                                       
              WS-K820-STATUS NOT = 97                                           
              DISPLAY ' OPENING ERROR, BP13K820 STATUS ' WS-K820-STATUS         
              MOVE WS-K820-STATUS           TO RETURN-CODE                      
              PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
           ACCEPT WS-CURRENT-DATE FROM DATE YYYYMMDD.                           
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ************************************                                      
       2000-READ-BP13F200.                                                      
      ************************************                                      
                                                                                
           READ BP13F200 AT END                                                 
                MOVE HIGH-VALUES TO F200-NUM-REGN                               
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-CNT-F200.                                                
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ************************************                                      
       3000-MAIN-PROCESS.                                                       
      ************************************                                      
           MOVE SPACES          TO BP13K800-MASTER                              
                                   BP13K820-REC.                                
           MOVE F200-NUM-REGN   TO K800-NUM-REGN                                
                                   K820-NUM-REGN.                               
                                                                                
           PERFORM 3100-READ-BP13K800    THRU 3100-EXIT                         
           IF FND-REC                                                           
              IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES                     
                 MOVE K800-NUM-NRIC1 TO K820-NUM-NRIC                           
                 PERFORM 3200-READ-BP13K820         THRU 3200-EXIT              
                                                                                
              END-IF                                                            
           END-IF.                                                              
           PERFORM 2000-READ-BP13F200               THRU 2000-EXIT.             
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ************************************                                      
       3100-READ-BP13K800.                                                      
      ************************************                                      
                                                                                
           READ BP13K800                                                        
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 MOVE 'Y'    TO WS-REC-FND                                      
              WHEN 23                                                           
                 MOVE 'N'    TO WS-REC-FND                                      
                 DISPLAY 'REG NUMBER NOT FOUND AT BP13K800 - '                  
                          K800-NUM-REGN                                         
              WHEN OTHER                                                        
                 DISPLAY 'ERROR WITH READ FILE-BP13K800. STATUS IS '            
                          WS-K800-STATUS                                        
                  MOVE WS-K800-STATUS           TO RETURN-CODE                  
                 PERFORM 9999-CLOSE-ROUTINE    THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       3200-READ-BP13K820.                                                      
      ************************************                                      
                                                                                
           READ BP13K820                                                        
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 PERFORM 3300-MOVE-WRITE-C507  THRU 3300-EXIT                   
              WHEN 23                                                           
                 DISPLAY 'REG NUMBER NOT FOUND AT BP13K820 - '                  
                          K820-NUM-REGN                                         
              WHEN OTHER                                                        
                 DISPLAY 'ERROR WITH READ FILE-BP13K820. STATUS IS '            
                          WS-K820-STATUS                                        
                  MOVE WS-K820-STATUS           TO RETURN-CODE                  
                 PERFORM 9999-CLOSE-ROUTINE    THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       3300-MOVE-WRITE-C507.                                                    
      ************************************                                      
                                                                                
           MOVE F200-NUM-SELTYPE       TO  L507-NUM-SELTYPE.            00161105
           MOVE F200-DTE-BALLOT        TO  L507-DTE-BALLOT.             00161105
           MOVE K820-NME-OCCP          TO  L507-NME-OCCP.                       
           MOVE K820-NUM-REGN          TO  L507-NUM-REGN.                       
           MOVE K820-NUM-NRIC          TO  L507-NUM-NRIC.                       
           MOVE K800-NUM-NT-FT-QUEUE   TO  L507-NUM-NT-FT-QUEUE.                
           MOVE K800-DTE-BK-APPT       TO  L507-DTE-BK-APPT.                    
           MOVE K800-TME-BK-APPT       TO  L507-TME-BK-APPT.                    
           MOVE K800-NUM-FLAT-TYPE     TO  L507-NUM-FLAT-TYPE.                  
                                                                                
           WRITE BP13L507-REC.                                                  
           ADD +1                      TO  WS-CNT-L507.                         
                                                                                
       3300-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ************************************                                      
       9999-CLOSE-ROUTINE.                                                      
      ************************************                                      
                                                                                
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   TOTALS *******'.                          
           DISPLAY 'PROGRAM-ID : BP13C507'.                                     
           DISPLAY  SPACES.                                                     
                                                                                
           DISPLAY 'NUMBER OF BP13F200 RECORDS READ     : '                     
                    WS-CNT-F200.                                                
           DISPLAY 'NUMBER OF BP13L507 RECORDS WRITTEN  : '                     
                    WS-CNT-L507.                                                
                                                                                
           CLOSE BP13F200                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13L507.                                                      
                                                                                
           IF WS-K800-STATUS = 97 OR 00                                         
              CONTINUE                                                          
           ELSE                                                                 
              MOVE WS-K800-STATUS TO RETURN-CODE                                
              DISPLAY 'CLOSE BP13K800 FAIL. STATUS : ' WS-K800-STATUS           
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS = 97 OR 00                                         
              CONTINUE                                                          
           ELSE                                                                 
              MOVE WS-K820-STATUS TO RETURN-CODE                                
              DISPLAY 'CLOSE BP13K820 FAIL. STATUS : ' WS-K820-STATUS           
           END-IF.                                                              
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
