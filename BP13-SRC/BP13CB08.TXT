      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CB08.                                                 
      *AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
      *DATE-WRITTEN.  13 MARCH 2007.                                            
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO UPDATE BP13K203 BASED ON BP13K200.       *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13F200 -- SORTED BOOKING APPOINTMENT FILE           *           
      *   2.  BP13K800 -- SOC MASTER FILE                           *           
      *   3.  BP13K820 -- SOC OCCUPIER FILE                         *           
      *                                                             *           
      *   I-O FILES :                                               *           
      *   1.  BP13K203 -- SMS FILE                                  *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP133010 14/03/2007 RB12 NEW PROGRAM                        *           
      * BP133019 17/03/2007 RB12 PRODUCE REPORT FOR F200 ALLO MODE  *           
      * BP133005 26/04/2007 RB12 POPULATE HEADER BASED ON RM TYPE   *           
      * BP133062 08/05/2007 RB12 ADD CHECKING OF BALLOT DATE        *           
      * BP133090 28/06/2007 RB12 ADD QUEUE NUMBER COLUMN            *           
      * BP133107 19/07/2007 RB12 INITIALIZE RECORD BEFORE READING   *           
      * BP133125 16/08/2007 RB12 MOVE 'AT' TO TYPE APPMT FOR QUEUE  *           
      *                          NUMBERS 00001 TO 03000 FOR WIC     *           
      * BP133213 14/12/2007 CT2  MOVE 'AT' TO TYPE APPMT FOR QUEUE  *           
      *                          NUMBERS >= 02001 FOR WIB           *           
      * BP133258 18/02/2008 CT2  MOVE 'AT' TO TYPE APPMT FOR QUEUE  *           
      *                          NUMBER >= 02001 FOR WIC            *           
      * BP133311 18/04/2008 CT2  MOVE 'AT' TO TYPE APPMT FOR QUEUE  *           
      *                          NUMBER >= 02501 FOR WIB            *           
      * BP133430 10/09/2008 CT2  CHANGES DUE TO WIH.                *           
      * BP133451 16/10/2008 CT2  PASS '3R' TO ALLO-CAT IF F200      *           
      *                          PASSES '3R1'                       *           
      * BP133439 21/10/2008 CT2  PASS 'AV' AS APPT TYPE IF SEL TYPE *           
      *                          IS '3R ' AND FLAT TYPE IS '1A'     *           
      * BP133    25/03/2010 CCC5 INCLUDE PREFIX '3' FOR TEL NUM.    *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K203 ASSIGN TO BP13K203                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K203-KEY-FLD                               
                  ALTERNATE KEY   IS K203-ALT-KEY                               
                  FILE STATUS     IS BP13K203-STATUS.                           
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS BP13K800-STATUS.                           
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS BP13K820-STATUS.                           
                                                                                
           SELECT BP13K857 ASSIGN TO BP13K857                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K857-KEY-FLD                               
                  FILE STATUS     IS BP13K857-STATUS.                           
                                                                                
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
                                                                                
           SELECT BP13LB08 ASSIGN TO BP13LB08.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K203                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13K203.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS  400 CHARACTERS.                                    
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K857                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13K857.                                                           
                                                                                
       FD   BP13F200                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  300 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F200.                                                           
                                                                                
       FD   BP13LB08                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   LB08-OUTREC                   PIC X(132).                           
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-SNO                 PIC 9(04)  VALUE ZEROES.                      
       01  WS-CURR-DATE           PIC X(08)  VALUE ZEROES.                      
       01  WS-NUM-ALLO-MODE       PIC X(03)  VALUE ZEROES.                      
       01  WS-DTE-BALLOT          PIC X(06)  VALUE ZEROES.                      
       01  WS-K203-ERR-CNT        PIC 9(08)  VALUE ZEROES.                      
       01  WS-K203-FND-SW         PIC X      VALUE SPACES.                      
       01  WS-K203-WRITE-CNT      PIC 9(08)  VALUE ZEROES.                      
       01  WS-K203-DUPL-REC       PIC 9(08)  VALUE ZEROES.                      
       01  WS-K203-WRITE-ERR      PIC 9(08)  VALUE ZEROES.                      
       01  WS-K800-READ-ERR       PIC 9(08)  VALUE ZEROES.                      
       01  WS-K800-READ-CNT       PIC 9(08)  VALUE ZEROES.                      
       01  WS-K800-FND-SW         PIC X      VALUE SPACES.                      
       01  WS-K820-READ-ERR       PIC 9(08)  VALUE ZEROES.                      
       01  WS-K820-READ-CNT       PIC 9(08)  VALUE ZEROES.                      
       01  WS-K820-FND-SW         PIC X      VALUE SPACES.                      
       01  WS-K857-READ-ERR       PIC 9(08)  VALUE ZEROES.                      
       01  WS-K857-READ-CNT       PIC 9(08)  VALUE ZEROES.                      
       01  WS-K857-FND-SW         PIC X      VALUE SPACES.                      
       01  WS-F200-READ-CNT       PIC 9(08)  VALUE ZEROES.                      
       01  WS-LB08-PRNT-CTR       PIC 9(08)  VALUE ZEROES.                      
       01  WS-F200-EOF            PIC X      VALUE SPACES.                      
       01  BP13K203-STATUS        PIC 9(02)  VALUE ZEROES.                      
       01  BP13K800-STATUS        PIC 9(02)  VALUE ZEROES.                      
       01  BP13K820-STATUS        PIC 9(02)  VALUE ZEROES.                      
       01  BP13K857-STATUS        PIC 9(02)  VALUE ZEROES.                      
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-K203-EOF-FLAG   PIC X VALUE 'N'.                              
               88 WS-K203-EOF-YES       VALUE 'Y'.                              
               88 WS-K203-EOF-NO        VALUE 'N'.                              
           05  WS-FIRST-PAGE-FLAG PIC X VALUE 'Y'.                              
               88 WS-FIRST-PAGE-YES     VALUE 'Y'.                              
               88 WS-FIRST-PAGE-NO      VALUE 'N'.                              
                                                                                
       01  WS-PRINT-VARIABLES.                                                  
           05  WS-SEQ-CNT                 PIC 9(5)   VALUE ZEROES.              
           05  WS-PAGE-CNT                PIC 9(5)   VALUE 0.                   
           05  WS-LINE-CNT                PIC 9(2)   VALUE 0.                   
                                                                                
       01  WS-BLANK-LINE          PIC X(132) VALUE SPACES.                      
                                                                                
       01  WS-DASHED-LINE.                                                      
           05  WS-DASH            PIC X(132) VALUE ALL '-'.                     
                                                                                
       01  LB08-HEADER1.                                                        
           05  FILLER             PIC X(8)   VALUE 'BP13LB08'.                  
           05  FILLER             PIC X(2)   VALUE SPACES.                      
           05  FILLER             PIC X(4)   VALUE 'HDB3'.                      
           05  FILLER             PIC X(28)  VALUE SPACES.                      
           05  LB08-HEADER1-TEXT  PIC X(41)  VALUE SPACES.                      
           05  FILLER             PIC X(26)  VALUE SPACES.                      
           05  FILLER             PIC X(7)   VALUE 'DATE : '.                   
           05  LB08-DATE          PIC X(10)  VALUE SPACES.                      
           05  FILLER             PIC X(6)   VALUE SPACES.                      
                                                                                
       01  LB08-HEADER-TEXT-1     PIC X(41)  VALUE                              
                 ' QUARTERLY SALE OF 3-ROOM & SMALLER FLATS'.                   
                                                                                
       01  LB08-HEADER-TEXT-2     PIC X(41)  VALUE                              
                 'HALF YEARLY SALE OF 4-ROOM & BIGGER FLATS'.                   
                                                                                
       01  LB08-HEADER2.                                                        
           05  FILLER             PIC X(41)  VALUE SPACES.                      
           05  FILLER             PIC X(47)  VALUE                              
                 'CASES WITHOUT HANDPHONE NUMBER OR EMAIL ADDRESS'.             
           05  FILLER             PIC X(21)  VALUE SPACES.                      
           05  FILLER             PIC X(7)   VALUE 'PAGE : '.                   
           05  LB08-PAGE-NUM      PIC ZZ9    VALUE ZEROES.                      
                                                                                
       01  LB08-COLUMN-HEADER.                                                  
           05  FILLER             PIC X(31) VALUE                               
                 'SNO   REGN NO   NRIC NO    NAME'.                             
           05  FILLER             PIC X(60)  VALUE SPACES.                      
           05  FILLER             PIC X(45) VALUE                               
                 'QUEUE  APPT DATE   APPT TIME  TEL NO      '.                  
           05  FILLER             PIC X(22)  VALUE SPACES.                      
                                                                                
       01  LB08-COLUMN-HEADER2.                                                 
           05  FILLER             PIC X(91) VALUE SPACES.                       
           05  FILLER             PIC X(45) VALUE                               
                 ' NO                           (HOME)      '.                  
           05  FILLER             PIC X(22) VALUE SPACES.                       
                                                                                
       01  LB08-DETAIL1.                                                        
           05  LB08-SNO           PIC ZZZ9   VALUE ZERO.                        
           05  FILLER             PIC X(02)  VALUE SPACES.                      
           05  LB08-REGN-NO       PIC X(08)  VALUE SPACES.                      
           05  FILLER             PIC X(02)  VALUE SPACES.                      
           05  LB08-NRIC          PIC X(09)  VALUE SPACES.                      
           05  FILLER             PIC X(02)  VALUE SPACES.                      
           05  LB08-NAME          PIC X(62)  VALUE SPACES.                      
           05  FILLER             PIC X(02)  VALUE SPACES.                      
           05  LB08-QUEUE-NO      PIC X(05)  VALUE SPACES.                      
           05  FILLER             PIC X(02)  VALUE SPACES.                      
           05  LB08-APPT-DATE     PIC X(10)  VALUE SPACES.                      
           05  FILLER             PIC X(04)  VALUE SPACES.                      
           05  LB08-APPT-TIME     PIC X(05)  VALUE SPACES.                      
           05  FILLER             PIC X(04)  VALUE SPACES.                      
           05  LB08-HOME-PHONE    PIC X(08)  VALUE SPACES.                      
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F200    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F200-EOF = 'Y'.                                     
           PERFORM 4000-PRINT-REPORT THRU 4000-EXIT                             
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F200                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K857                                                 
                I-O    BP13K203                                                 
                OUTPUT BP13LB08.                                                
                                                                                
           IF BP13K203-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K203 : ' BP13K203-STATUS             
              MOVE BP13K203-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K820-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K820 : ' BP13K820-STATUS             
              MOVE BP13K820-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K857-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K857 : ' BP13K857-STATUS             
              MOVE BP13K857-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           ACCEPT WS-CURR-DATE FROM DATE YYYYMMDD.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F200.                                                          
      *---------------*                                                         
                                                                                
           READ BP13F200 AT END                                                 
                MOVE HIGH-VALUES TO BP13F200-REC                                
                MOVE 'Y' TO WS-F200-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F200-READ-CNT.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           IF F200-NUM-REGN NOT = SPACES AND LOW-VALUES AND ZEROES              
              PERFORM 3100-READ-BP13K800 THRU 3100-EXIT                         
              IF WS-K800-FND-SW = 'Y'                                           
                 MOVE SPACES             TO BP13K820-REC                        
                 INITIALIZE                 BP13K820-REC                        
                 MOVE K800-NUM-REGN      TO K820-NUM-REGN                       
                 MOVE K800-NUM-NRIC1     TO K820-NUM-NRIC                       
                 PERFORM 3200-READ-BP13K820 THRU 3200-EXIT                      
                 PERFORM 3400-MOVE-K203     THRU 3400-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F200        THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3100-READ-BP13K800.                                                      
      *-------------------*                                                     
                                                                                
           MOVE 'N'                TO WS-K800-FND-SW.                           
           MOVE SPACES             TO BP13K800-MASTER.                          
           INITIALIZE                 BP13K800-MASTER.                          
           MOVE F200-NUM-REGN      TO K800-NUM-REGN.                            
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
                                                                                
           IF BP13K800-STATUS = 0                                               
              MOVE 'Y' TO WS-K800-FND-SW                                        
              ADD   1  TO WS-K800-READ-CNT                                      
           ELSE                                                                 
           IF BP13K800-STATUS = 23                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K800:' F200-NUM-REGN             
              ADD   1  TO WS-K800-READ-ERR                                      
           ELSE                                                                 
           IF BP13K800-STATUS NOT = 0                                           
              MOVE '4'  TO RETURN-CODE                                          
              DISPLAY  'READ ERROR IN BP13K800 ' F200-NUM-REGN                  
              ADD   1   TO WS-K800-READ-ERR                                     
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3200-READ-BP13K820.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13K820 KEY IS K820-KEY-FLD.                                   
                                                                                
           IF BP13K820-STATUS = 0                                               
              MOVE 'Y' TO WS-K820-FND-SW                                        
              ADD   1  TO WS-K820-READ-CNT                                      
           ELSE                                                                 
           IF BP13K820-STATUS = 23                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K820:' K820-KEY-FLD              
              ADD   1  TO WS-K820-READ-ERR                                      
           ELSE                                                                 
           IF BP13K820-STATUS NOT = 0                                           
              MOVE '4'  TO RETURN-CODE                                          
              DISPLAY  'READ ERROR IN BP13K820 ' K820-KEY-FLD                   
              ADD   1   TO WS-K820-READ-ERR                                     
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       3400-MOVE-K203.                                                          
      *---------------*                                                         
                                                                                
           MOVE SPACES                TO BP13K203-REC.                          
           INITIALIZE                    BP13K203-REC.                          
           IF F200-CDE-PROJECT = '3R1'                                          
              MOVE '3R'               TO K203-NUM-ALLO-MODE                     
                                         WS-NUM-ALLO-MODE                       
           ELSE                                                                 
              MOVE F200-CDE-PROJECT   TO K203-NUM-ALLO-MODE                     
                                         WS-NUM-ALLO-MODE                       
           END-IF.                                                              
           MOVE F200-NUM-NT-ZONE      TO K203-NUM-NEW-TOWN.                     
           MOVE K800-NUM-FLAT-TYPE    TO K203-NUM-FLAT-TYPE.                    
           MOVE K800-NUM-CAT          TO K203-NUM-ETHNIC.                       
           MOVE F200-NUM-REGN         TO K203-NUM-REGN.                         
           MOVE F200-DTE-BALLOT       TO K203-DTE-BALLOT                        
                                         WS-DTE-BALLOT.                         
           MOVE K800-DTE-BK-APPT      TO K203-DTE-APPMT.                        
           MOVE K800-TME-BK-APPT      TO K203-TME-APPMT.                        
           MOVE K800-NUM-NT-FT-QUEUE  TO K203-NUM-QUEUE.                        
           MOVE F200-NUM-REF          TO K203-NUM-REF.                          
           MOVE K800-NUM-NRIC1        TO K203-NUM-NRIC.                         
           MOVE K820-NME-OCCP         TO K203-NME-NRIC.                         
           MOVE 'HA1'                 TO K203-NUM-APLCNT.                       
           MOVE K800-NUM-ZONE         TO K203-NUM-ZONE.                         
                                                                                
           IF K800-NUM-ALLO-CAT = '3R ' AND K800-NUM-FLAT-TYPE = '1A'           
              MOVE 'AV'               TO K203-TYPE-APPMT                        
           ELSE                                                                 
            IF K800-NUM-ALLO-CAT = '3R ' AND                                    
               K800-NUM-FLAT-TYPE NOT = '1A'                                    
                MOVE 'AP'               TO K203-TYPE-APPMT                      
            ELSE                                                                
              IF K800-NUM-ALLO-CAT = 'WIH' AND                                  
                 F200-NUM-HHTY-QUEUE-SERIAL >= '00001' AND                      
                 F200-NUM-HHTY-QUEUE-SERIAL <= '02000'                          
      *          F200-DTE-BALLOT = '200712'                                     
                 MOVE 'AQ'            TO K203-TYPE-APPMT                        
              ELSE                                                              
                 IF K800-NUM-ALLO-CAT = 'WIH' AND                               
                    F200-NUM-HHTY-QUEUE-SERIAL >= '02001'                       
      *             F200-DTE-BALLOT = '200712'                                  
                       MOVE 'AT'            TO K203-TYPE-APPMT                  
                 END-IF                                                         
              END-IF                                                            
            END-IF                                                              
           END-IF.                                                              
                                                                                
           IF K820-NUM-HP-PGR = SPACES OR LOW-VALUES OR ZEROES                  
              IF K820-NUM-PHONE(1:1) = 3 OR 8 OR 9                              
                 MOVE K820-NUM-PHONE  TO K203-NUM-HP                            
              END-IF                                                            
           ELSE                                                                 
              IF K820-NUM-HP-PGR(1:1) = 3 OR 6                                  
                 IF K820-NUM-PHONE(1:1) = 3 OR 8 OR 9                           
                    MOVE K820-NUM-PHONE TO K203-NUM-HP                          
                 END-IF                                                         
              ELSE                                                              
                 MOVE K820-NUM-HP-PGR TO K203-NUM-HP                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3500-WRITE-BP13K203   THRU 3500-EXIT.                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3500-WRITE-BP13K203.                                                     
      *--------------------*                                                    
                                                                                
           WRITE BP13K203-REC.                                                  
                                                                                
           IF BP13K203-STATUS = 0                                               
              ADD   1  TO WS-K203-WRITE-CNT                                     
           ELSE                                                                 
           IF BP13K203-STATUS = 2 OR 22                                         
              ADD   1  TO WS-K203-DUPL-REC                                      
              DISPLAY 'DUPLICATE RECORD IN BP13K203:'                           
              DISPLAY ' KEY     : ' K203-KEY-FLD                                
              DISPLAY ' ALT KEY : ' K203-ALT-KEY                                
           ELSE                                                                 
           IF BP13K203-STATUS NOT = 0                                           
              MOVE '4'  TO RETURN-CODE                                          
              DISPLAY 'WRITE ERROR, STATUS : ' BP13K203-STATUS                  
              DISPLAY ' KEY     : ' K203-KEY-FLD                                
              DISPLAY ' ALT KEY : ' K203-ALT-KEY                                
              ADD   1   TO WS-K203-WRITE-ERR                                    
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       4000-PRINT-REPORT.                                                       
      *--------------------*                                                    
                                                                                
           STRING WS-CURR-DATE(7:2) '/'                                         
                  WS-CURR-DATE(5:2) '/'                                         
                  WS-CURR-DATE(1:4)                                             
              DELIMITED BY SIZE INTO LB08-DATE.                                 
                                                                                
           MOVE SPACES TO K203-KEY-FLD.                                         
                                                                                
           MOVE WS-NUM-ALLO-MODE TO K203-NUM-ALLO-MODE.                         
                                                                                
           START BP13K203 KEY IS GREATER OR EQUAL K203-KEY-FLD                  
             END-START.                                                         
                                                                                
           EVALUATE BP13K203-STATUS                                             
                                                                                
              WHEN 00                                                           
                 PERFORM 4100-PROCESS-K203 THRU 4100-EXIT                       
                   UNTIL WS-K203-EOF-YES                                        
                                                                                
              WHEN 10                                                           
                 DISPLAY 'K203 END OF FILE'                                     
                                                                                
              WHEN 23                                                           
                 DISPLAY 'KEY NOT FOUND IN START OF K203: '                     
                          K203-KEY-FLD                                          
                                                                                
              WHEN OTHER                                                        
                 DISPLAY 'BP13K203-STATUS: ' BP13K203-STATUS                    
                                                                                
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       4100-PROCESS-K203.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13K203 NEXT RECORD                                            
             AT END SET WS-K203-EOF-YES TO TRUE.                                
                                                                                
           IF K203-NUM-ALLO-MODE NOT = WS-NUM-ALLO-MODE                         
              SET WS-K203-EOF-YES TO TRUE                                       
              GO TO 4100-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF K203-DTE-BALLOT NOT = WS-DTE-BALLOT                               
              GO TO 4100-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF K203-NUM-HP = SPACES OR LOW-VALUES                                
              PERFORM 5000-READ-BP13K857 THRU 5000-EXIT                         
              IF K857-NUM-EMAIL = SPACES OR LOW-VALUES                          
                 PERFORM 6000-PRINT-LB08 THRU 6000-EXIT                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       5000-READ-BP13K857.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES             TO BP13K857-REC.                             
           INITIALIZE                 BP13K857-REC.                             
                                                                                
           MOVE K203-NUM-REGN      TO K857-NUM-REGN.                            
                                                                                
           READ BP13K857 KEY IS K857-KEY-FLD.                                   
                                                                                
           IF BP13K857-STATUS = 0                                               
              DISPLAY 'RECORD FOUND KEY IS ' K857-KEY-FLD                       
              ADD   1  TO WS-K857-READ-CNT                                      
           ELSE                                                                 
           IF BP13K857-STATUS = 23                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K857:' K857-KEY-FLD              
              ADD   1  TO WS-K857-READ-ERR                                      
           ELSE                                                                 
           IF BP13K857-STATUS NOT = 0                                           
              MOVE '4'  TO RETURN-CODE                                          
              DISPLAY  'READ ERROR IN BP13K857 ' K857-KEY-FLD                   
              ADD   1   TO WS-K857-READ-ERR                                     
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       6000-PRINT-LB08.                                                         
      *---------------*                                                         
                                                                                
           MOVE SPACES TO LB08-OUTREC                                           
                          LB08-DETAIL1.                                         
                                                                                
           IF WS-LINE-CNT > 54 OR WS-FIRST-PAGE-YES                             
             PERFORM 6100-PRINT-HEADING THRU 6100-EXIT                          
             SET WS-FIRST-PAGE-NO    TO TRUE                                    
           END-IF.                                                              
                                                                                
           MOVE SPACES                TO BP13K820-REC.                          
           INITIALIZE                    BP13K820-REC.                          
           MOVE K203-NUM-REGN         TO K820-NUM-REGN.                         
           MOVE K203-NUM-NRIC         TO K820-NUM-NRIC.                         
           PERFORM 3200-READ-BP13K820 THRU 3200-EXIT.                           
                                                                                
           IF K820-NUM-PHONE(1:1) = 3 OR 6                                      
              MOVE K820-NUM-PHONE     TO LB08-HOME-PHONE                        
           ELSE                                                                 
              IF K820-NUM-HP-PGR(1:1) = 3 OR 6                                  
                 MOVE K820-NUM-HP-PGR TO LB08-HOME-PHONE                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD 1                      TO WS-SNO.                                
                                                                                
           MOVE WS-SNO                TO LB08-SNO.                              
           MOVE K203-NUM-REGN         TO LB08-REGN-NO.                          
           MOVE K203-NUM-NRIC         TO LB08-NRIC.                             
           MOVE K203-NME-NRIC(1:62)   TO LB08-NAME.                             
           MOVE K203-NUM-QUEUE        TO LB08-QUEUE-NO.                         
           STRING K203-DTE-APPMT(7:2) '/'                                       
                  K203-DTE-APPMT(5:2) '/'                                       
                  K203-DTE-APPMT(1:4)                                           
               DELIMITED BY SIZE    INTO LB08-APPT-DATE.                        
           STRING K203-TME-APPMT(1:2) ':'                                       
                  K203-TME-APPMT(3:2)                                           
               DELIMITED BY SIZE    INTO LB08-APPT-TIME.                        
                                                                                
           WRITE LB08-OUTREC  FROM LB08-DETAIL1.                                
           WRITE LB08-OUTREC  FROM WS-BLANK-LINE.                               
                                                                                
           ADD 1 TO WS-LB08-PRNT-CTR.                                           
           ADD 2 TO WS-LINE-CNT.                                                
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       6100-PRINT-HEADING.                                                      
      *------------------*                                                      
                                                                                
           ADD   1             TO   WS-PAGE-CNT.                                
           MOVE  WS-PAGE-CNT   TO   LB08-PAGE-NUM.                              
                                                                                
           EVALUATE WS-NUM-ALLO-MODE                                            
              WHEN '3R '                                                        
                 MOVE LB08-HEADER-TEXT-1 TO LB08-HEADER1-TEXT                   
              WHEN 'WIA'                                                        
              WHEN 'WIB'                                                        
              WHEN 'WIC'                                                        
                 MOVE LB08-HEADER-TEXT-2 TO LB08-HEADER1-TEXT                   
           END-EVALUATE.                                                        
                                                                                
           WRITE LB08-OUTREC   FROM LB08-HEADER1 AFTER PAGE.                    
           WRITE LB08-OUTREC   FROM LB08-HEADER2.                               
           WRITE LB08-OUTREC   FROM WS-BLANK-LINE.                              
           WRITE LB08-OUTREC   FROM LB08-COLUMN-HEADER.                         
           WRITE LB08-OUTREC   FROM LB08-COLUMN-HEADER2.                        
           WRITE LB08-OUTREC   FROM WS-DASHED-LINE.                             
           MOVE  6             TO   WS-LINE-CNT.                                
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CB08  *************'.                           
           DISPLAY 'RECS READ FROM  BP13F200    : ' WS-F200-READ-CNT.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN  IN BP13K203   : ' WS-K203-WRITE-CNT.          
           DISPLAY 'RECS ERROR WRITE BP13K203   : ' WS-K203-WRITE-ERR.          
           DISPLAY 'RECS FOUND    IN BP13K203   : ' WS-K203-DUPL-REC.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ   FROM BP13K800   : ' WS-K800-READ-CNT.           
           DISPLAY 'RECS NOTFND   IN BP13K800   : ' WS-K800-READ-ERR.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ   FROM BP13K820   : ' WS-K820-READ-CNT.           
           DISPLAY 'RECS NOTFND   IN BP13K820   : ' WS-K820-READ-ERR.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ   FROM BP13K857   : ' WS-K857-READ-CNT.           
           DISPLAY 'RECS NOTFND   IN BP13K857   : ' WS-K857-READ-ERR.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS PRINTED  TO BP13LB08   : ' WS-LB08-PRNT-CTR.           
                                                                                
           CLOSE BP13K203                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K857                                                       
                 BP13F200                                                       
                 BP13LB08.                                                      
                                                                                
           IF BP13K203-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K203-STATUS ' BP13K203-STATUS          
            MOVE BP13K203-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K800-STATUS ' BP13K800-STATUS          
            MOVE BP13K800-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13K820-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K820-STATUS ' BP13K820-STATUS          
            MOVE BP13K820-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13K857-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K857-STATUS ' BP13K857-STATUS          
            MOVE BP13K857-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
