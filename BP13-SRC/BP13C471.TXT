       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C471.                                                 
      *AUTHOR.        JAI.                                                      
      *DATE-WRITTEN.  MARCH 28, 1995.                                           
      *****************************************************************         
      * OBJECTIVE  - TO DELETE K600 RECORDS WHOSE ACCEPT DATE WAS MORE          
      *              THAN 3 MONTHS AGO                                          
      * SUBROUTINES   - NIL                                                     
      * IDENTICAL PGM - AB02C012                                                
      *----------------------------------------------------------------         
      * REF NO   DATE    BY   AMENDMENTS/ENHANCEMENTS                           
      * -------  ----    ---  -----------------------                           
      * CYYMMNN  YYMMDD  XXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX          
      *                       XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX          
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K600 ASSIGN       TO BP13K600                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS SEQUENTIAL                           
                           RECORD KEY   IS K600-KEY-FLD                         
                           FILE STATUS  IS K600-STATUS.                         
                                                                                
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13K600.                                                           
       COPY BP13K600.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  K600-STATUS                   PIC 9(02) VALUE 0.                     
       01  WS-K600-EOF                   PIC X(01) VALUE 'N'.                   
       01  WS-MM                         PIC 9(02) VALUE ZERO.                  
                                                                                
       01  WS-DTE-ACCEPT.                                                       
           05 WS-DTE-ACCEPT-CCYY         PIC 9(04).                             
           05 WS-DTE-ACCEPT-MM           PIC 9(02).                             
           05 WS-DTE-ACCEPT-DD           PIC 9(02).                             
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-K600-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-K600-BYPASS         PIC 9(07) VALUE 0.                     
           05 WS-CNT-K600-DEL            PIC 9(07) VALUE 0.                     
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      **************                                                            
       0000-CONTROL.                                                            
      **************                                                            
                                                                                
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
                                                                                
           PERFORM 2000-MAIN-ROUTINE     THRU 2000-EXIT                         
                UNTIL WS-K600-EOF = 'Y'.                                        
                                                                                
           PERFORM 9999-WRAP-UP          THRU 9999-EXIT.                        
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      **************                                                            
       1000-START-UP.                                                           
      **************                                                            
                                                                                
           OPEN I-O    BP13K600                                                 
                INPUT  SY02F001.                                                
                                                                                
           IF  K600-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13K600 ' K600-STATUS                  
               PERFORM 9999-WRAP-UP THRU 9999-EXIT                              
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *******************                                                       
       2000-MAIN-ROUTINE.                                                       
      *******************                                                       
                                                                                
      *---------------------------------------------------------------*         
      * READS BP13K600 AND PROCESS EACH RECORD UNTIL END OF FILE      *         
      *---------------------------------------------------------------*         
                                                                                
           READ BP13K600 AT END                                                 
                MOVE 'Y' TO WS-K600-EOF                                         
                GO       TO 2000-EXIT                                           
                END-READ.                                                       
                                                                                
           ADD  1                          TO WS-CNT-K600-READ.                 
                                                                                
           IF K600-DTE-ACCEPTED = SPACES OR LOW-VALUES                          
              ADD 1                        TO WS-CNT-K600-BYPASS                
              GO TO 2000-EXIT                                                   
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------*         
      * COMPUTE FOR DATE ACCEPTED + 3 MTHS                            *         
      *---------------------------------------------------------------*         
                                                                                
           MOVE K600-DTE-ACCEPTED          TO WS-DTE-ACCEPT                     
                                                                                
           ADD    3                        TO WS-DTE-ACCEPT-MM                  
                                                                                
           IF WS-DTE-ACCEPT-MM > 12                                             
              ADD  1                       TO WS-DTE-ACCEPT-CCYY                
              COMPUTE WS-MM = WS-DTE-ACCEPT-MM - 12                             
              MOVE  WS-MM                  TO WS-DTE-ACCEPT-MM                  
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------*         
      * IF DATE ACCEPTED IF MORE THAN 6 MONTHS AGO, DELETE RECORD     *         
      *---------------------------------------------------------------*         
                                                                                
           IF WS-DTE-ACCEPT < F001-DTE-CURRENT                                  
              PERFORM 3000-DELETE-BP13K600 THRU 3000-EXIT                       
           ELSE                                                                 
              ADD 1                        TO WS-CNT-K600-BYPASS                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *********************                                                     
       3000-DELETE-BP13K600.                                                    
      *********************                                                     
                                                                                
           DELETE BP13K600.                                                     
                                                                                
           IF K600-STATUS = ZEROS                                               
              ADD  1             TO WS-CNT-K600-DEL                             
           ELSE                                                                 
              MOVE K600-STATUS   TO RETURN-CODE                                 
              DISPLAY 'ERROR DELETING BP13K600, STATUS = '                      
                      K600-STATUS                                               
              PERFORM 9999-WRAP-UP THRU 9999-EXIT                               
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      **************                                                            
       9999-WRAP-UP.                                                            
      **************                                                            
                                                                                
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13C471'.                                     
           DISPLAY  SPACES.                                                     
           DISPLAY 'TOTAL BP13K600 RECS READ     = ' WS-CNT-K600-READ.          
           DISPLAY 'TOTAL BP13K600 RECS DELETED  = ' WS-CNT-K600-DEL.           
           DISPLAY 'TOTAL BP13K600 RECS BYPASSED = ' WS-CNT-K600-BYPASS.        
                                                                                
           CLOSE BP13K600                                                       
                 SY02F001.                                                      
                                                                                
           IF  K600-STATUS NOT = 0                                              
               MOVE K600-STATUS TO RETURN-CODE                                  
               DISPLAY 'CLOSE FAILED FOR BP13K600 ' K600-STATUS                 
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
