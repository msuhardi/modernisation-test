      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C232.                                                 
      *AUTHOR.        ROSALYN M. LUMANGLAS.                                     
      *DATE-WRITTEN.  24/09/93.                                                 
                                                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :                                              *            
      *    TO PRINT SUBMISSION REPORT FOR ALLOCATION EXERCISE.     *            
      *                                                            *            
      *  INPUT FILES : BM06CTL                                     *            
      *                BM06K010                                    *            
      *                BP13K800                                    *            
      *                                                            *            
      *  OUTPUT/S    : P13L232A                                    *            
      * ---------------------------------------------------------- *            
      * CHG-NO   BY   DATE    DETAILS                              *            
      * N940009  BAL  940325  REPLACE BM06CTL FILE W/ BP13F205     *            
      *                       AND BM06K010 W/ BP13K200 FILE.       *            
      * SOC-PH9  GD   181194  CHG REPORT TO CATER FOR ZONE INFO    *            
      * SOC-PH9  LMS  161294  TO CHANGE K200 TO F200.              *            
      * C9506F9  LMS  250895  INCL DECODING OF HHTY FOR CGO.       *            
      * BP130143 LMS  090796  INCL NEW DESIGN MODEL CODE 72 & 73   *            
      *                       CHANGE '34' TO '70'.                 *            
      * BP130297 LMS  190597  INCLUDE CUR-REGN.                    *            
      * BP130297 LMS  210697  REMOVE CUR-REGN, INCLUDE SEQ-NO.     *            
      * BP130641 SRA  190399  INCL NEW DESIGN MODEL CODE '80'.     *            
      * BP130712 LMS  180999  READ BM06K110 INSTEAD OF BP13K800    *            
      * BP130768 GBR  071299  PRT RPT FOR MATURE & NON-MATURE EST  *            
      * BP130807 GBR  160200  ALLOW DESIGN MODEL CODES '34,71,73,  *            
      *                       81,82' FOR FLAT TYP '04,05' AND      *            
      *                       '34,81,82' FOR FLAT TYP '06'.        *            
      * BP130819 MCC  060400  PRINT FOOTER1 AND FOOTER2 ONLY       *            
      *                       WHEN BP13F200 RECORD IS PRINTED      *            
      * BP130883 RAS  040800  TO CHANGE BP13C904 TO BP13C913       *            
      * BP130949 ANC  310800  MOVE K800-NUM-JT-BALLOT-REGN TO L232-*            
      *                       JB-REGN-NO IF ALLOC-SCH = 'JB'.      *            
      * BP132095 MTD  220402  EXPAND LENGTH OF BP13F200            *            
      *                       (FROM 100 TO 300)                    *            
      * BP135838 ESA1 010715  INCL NEW DESIGN MODEL CODE '92'.     *            
      * ========================================================== *            
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K110-KEY-FLD                                  
                  ALTERNATE KEY IS K110-REGN-NO                                 
                  FILE STATUS  IS WS-K110-STATUS.                               
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K800-NUM-REGN                                 
                  FILE STATUS  IS WS-K800-STATUS.                               
           SELECT P13L232A ASSIGN TO P13L232A.                                  
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F205                                                             
           BLOCK  CONTAINS 0   RECORDS                                          
           LABEL  RECORDS  ARE STANDARD                                         
           RECORD CONTAINS 80  CHARACTERS                                       
           RECORDING MODE F.                                                    
       COPY BP13F205.                                                           
                                                                                
       FD  BP13F200                                                             
           BLOCK  CONTAINS 0   RECORDS                                          
           LABEL  RECORDS  ARE STANDARD                                         
           RECORD CONTAINS 300 CHARACTERS                                       
           RECORDING MODE F.                                                    
       COPY BP13F200.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD  P13L232A                                                             
           BLOCK  CONTAINS 0   RECORDS                                          
           RECORD CONTAINS 99  CHARACTERS                                       
           LABEL  RECORDS  ARE OMITTED                                          
           RECORDING MODE F.                                                    
       01  PRINT-REC-A                    PIC X(99).                            
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01  WS-K110-STATUS                 PIC 9(02) VALUE ZEROS.                
       01  WS-K800-STATUS                 PIC 9(02) VALUE ZEROS.                
       01  WS-BP13F200-READ               PIC 9(04) VALUE ZEROS.                
       01  WS-K110-READ                   PIC 9(04) VALUE ZEROS.                
       01  WS-K800-READ                   PIC 9(04) VALUE ZEROS.                
       01  WS-L232-WRITE                  PIC 9(04) VALUE ZEROS.                
       01  WS-F205-READ-CNT               PIC 9(04) VALUE ZEROS.                
       01  EOF-F205                       PIC X(01) VALUE 'N'.                  
       01  WS-F200-EOF                    PIC X(01) VALUE 'N'.                  
       01  WS-LINE-CNT                    PIC 9(02) VALUE ZEROS.                
       01  WS-NUMBER                      PIC 9(05) VALUE ZEROS.                
       01  WS-PAGE-CNT                    PIC 9(04) VALUE ZEROES.               
       01  WS-SQL-CODE                    PIC 9(04) VALUE ZEROS.                
                                                                                
       01  WS-NEXT-F205-REC               PIC X(1)  VALUE 'N'.                  
       01  WS-F200-PRINTED                PIC X(1)  VALUE 'N'.                  
                                                                                
       01  WS-F205-KEY.                                                         
           05 WS-F205-DTE-ALLOC           PIC X(08) VALUE SPACES.               
           05 WS-F205-NT-ZONE             PIC X(03) VALUE SPACES.               
           05 WS-F205-FLAT-TYPE           PIC X(03) VALUE SPACES.               
                                                                                
       01  WS-F200-KEY.                                                         
           05 WS-F200-DTE-ALLOC           PIC X(08) VALUE SPACES.               
           05 WS-F200-NT-ZONE             PIC X(03) VALUE SPACES.               
           05 WS-F200-FLAT-TYPE           PIC X(03) VALUE SPACES.               
                                                                                
      *============================================================*            
      *              LINKAGE FOR SUBPROGRAM 'BP13C913'                          
      *============================================================*            
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-SCH            PIC X(04).                            
            05 WS-LINK-NUM-ACC            PIC X(04).                            
            05 WS-LINK-NUM-CHK            PIC X(01).                            
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *============================================================*            
      *                H E A D E R   L A Y O U T                   *            
      *============================================================*            
       01  L232-HEADER-1.                                                       
           05 FILLER                      PIC X(6)  VALUE 'PAGE :'.             
           05 L232-PAGE                   PIC ZZZ9.                             
                                                                                
       01  L232-HEADER-2.                                                       
           05 FILLER                      PIC X(20) VALUE SPACES.               
           05 FILLER                      PIC X(54) VALUE                       
             'SELECTION/SUBMISSION DETAIL LIST           ANNEXURE  A'.          
           05 FILLER                      PIC X(06) VALUE SPACES.               
                                                                                
       01  L232-HEADER-3.                                                       
           05 FILLER                      PIC X(41) VALUE SPACES.               
           05 FILLER                      PIC X(04) VALUE 'FLAT'.               
           05 FILLER                      PIC X(03) VALUE SPACES.               
           05 FILLER                      PIC X(04) VALUE 'APT'.                
                                                                                
       01  L232-HEADER-4.                                                       
           05 L232-HEADER-REGN-FORMAT     PIC X(33) VALUE SPACES.               
           05 FILLER                      PIC X(08) VALUE SPACES.               
           05 FILLER                      PIC X(04) VALUE 'TYPE'.               
           05 FILLER                      PIC X(02) VALUE SPACES.               
           05 FILLER                      PIC X(06) VALUE 'BLOCK'.              
           05 FILLER                      PIC X(02) VALUE SPACES.               
           05 FILLER                      PIC X(07) VALUE                       
             'FLAT NO'.                                                         
           05 FILLER                      PIC X(05) VALUE SPACES.               
           05 FILLER                      PIC X(11) VALUE                       
             'STREET NAME'.                                                     
                                                                                
       01  L232-HEADER-5.                                                       
           05 FILLER                      PIC X(99) VALUE ALL '-'.              
                                                                                
       01  L232-FOOTER-1.                                                       
           05 FILLER                      PIC X(20) VALUE SPACES.               
           05 FILLER                      PIC X(12) VALUE                       
             'CHECKED  BY:'.                                                    
                                                                                
       01  L232-FOOTER-2.                                                       
           05 FILLER                      PIC X(20) VALUE SPACES.               
           05 FILLER                      PIC X(12) VALUE                       
             'VERIFIED BY:'.                                                    
                                                                                
      *============================================================*            
      *                 D E T A I L    L A Y O U T                 *            
      *============================================================*            
       01  L232-DETAIL-1.                                                       
           05 L232-NT-REGN-FORMAT.                                              
              10 L232-SER-NO              PIC ZZZZ9.                            
              10 FILLER                   PIC X(01).                            
              10 L232-NT-REGN-NO          PIC X(08).                            
              10 FILLER                   PIC X(20).                            
           05 L232-ZONE-REGN-FORMAT REDEFINES L232-NT-REGN-FORMAT.              
              10 L232-QUEUE-NO            PIC X(05).                            
              10 L232-SLASH1              PIC X(01).                            
              10 L232-HHTYPE              PIC X(01).                            
              10 L232-SLASH2              PIC X(01).                            
              10 L232-SEQ-NO              PIC X(07).                            
              10 L232-SLASH3              PIC X(01).                            
              10 L232-ZONE-REGN-NO        PIC X(08).                            
              10 L232-SLASH4              PIC X(01).                            
              10 L232-JB-REGN-NO          PIC X(08).                            
              10 FILLER                   PIC X(01).                            
           05 FILLER                      PIC X(01) VALUE SPACE.                
           05 L232-LEFT-PAR               PIC X(01).                            
           05 L232-ALLOC                  PIC X(03).                            
           05 L232-RIGHT-PAR              PIC X(01).                            
           05 FILLER                      PIC X(02) VALUE SPACE.                
           05 L232-RM-TY                  PIC X(02).                            
           05 FILLER                      PIC X(03) VALUE SPACES.               
           05 L232-BLK                    PIC X(05).                            
           05 FILLER                      PIC X(03) VALUE SPACES.               
           05 L232-NUM-LEVEL              PIC X(02).                            
           05 L232-DASH                   PIC X(01).                            
           05 L232-NUM-MAIN               PIC X(04).                            
           05 L232-NUM-SUB                PIC X(03).                            
           05 FILLER                      PIC X(02) VALUE SPACES.               
           05 L232-STREET-NAME            PIC X(20).                            
                                                                                
                                                                                
      *------------------------*                                                
       PROCEDURE DIVISION.                                                      
      *------------------------*                                                
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F205      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-F205   THRU 300-EXIT                             
             UNTIL EOF-F205 = 'Y'.                                              
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-INITIALIZATION.                                                      
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F205                                                 
                       BM06K110                                                 
                       BP13K800                                                 
                OUTPUT P13L232A.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BM06K110  OPEN ERROR ' WS-K110-STATUS               
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K800  OPEN ERROR ' WS-K800-STATUS               
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           MOVE 'N'                TO WS-F200-EOF                               
                                      EOF-F205.                                 
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F205.                                                           
      *-------------------------------------------------------------            
           READ BP13F205 AT END                                                 
                MOVE 'Y'        TO EOF-F205                                     
                MOVE HIGH-VALUE TO WS-F205-KEY                                  
                GO  TO 200-EXIT                                                 
           END-READ.                                                            
                                                                                
           MOVE  F205-DTE-ALLOCN       TO  WS-F205-DTE-ALLOC.                   
           MOVE  F205-NUM-NT-ZONE      TO  WS-F205-NT-ZONE.                     
           MOVE  F205-NUM-FLAT-TYPE    TO  WS-F205-FLAT-TYPE.                   
           ADD   1                     TO  WS-F205-READ-CNT.                    
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-F205.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT BP13F200.                                                 
                                                                                
           MOVE SPACES TO WS-F200-KEY.                                          
                                                                                
           PERFORM 400-READ-F200 THRU 400-EXIT                                  
             UNTIL (WS-F200-KEY  >= WS-F205-KEY)                                
                OR (WS-F200-EOF   = 'Y').                                       
                                                                                
           PERFORM 420-PROCESS-DETAILS THRU 420-EXIT                            
             UNTIL (WS-F200-KEY NOT = WS-F205-KEY)                              
                OR (WS-F200-EOF     = 'Y').                                     
                                                                                
           IF WS-F200-PRINTED = 'Y'                                             
              WRITE PRINT-REC-A FROM L232-FOOTER-1 AFTER 2                      
              WRITE PRINT-REC-A FROM L232-FOOTER-2 AFTER 2                      
              MOVE  ZEROS TO WS-PAGE-CNT                                        
              MOVE  ZEROS TO WS-NUMBER                                          
              MOVE  'N'   TO WS-F200-PRINTED                                    
           END-IF.                                                              
                                                                                
           MOVE 'N'    TO WS-F200-EOF.                                          
                                                                                
           CLOSE BP13F200.                                                      
                                                                                
           PERFORM 200-READ-F205 THRU 200-EXIT.                                 
           MOVE 'Y' TO WS-NEXT-F205-REC.                                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-READ-F200.                                                           
      *-------------------------------------------------------------            
           READ BP13F200 AT END                                                 
                MOVE 'Y'        TO WS-F200-EOF                                  
                MOVE HIGH-VALUE TO WS-F200-KEY                                  
                GO TO 400-EXIT                                                  
           END-READ.                                                            
                                                                                
           MOVE  F200-DTE-ALLOC        TO  WS-F200-DTE-ALLOC.                   
           MOVE  F200-NUM-NT-ZONE      TO  WS-F200-NT-ZONE.                     
           MOVE  F200-NUM-FLAT-TYPE    TO  WS-F200-FLAT-TYPE.                   
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       420-PROCESS-DETAILS.                                                     
      *-------------------------------------------------------------            
           ADD   1                 TO WS-BP13F200-READ.                         
                                                                                
           MOVE SPACES             TO K110-REC.                                 
           MOVE F200-NUM-REGN      TO K110-REGN-NO.                             
                                                                                
           READ BM06K110 KEY IS K110-REGN-NO.                                   
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
                MOVE SPACES                      TO   L232-DETAIL-1             
                ADD  1                           TO   WS-K110-READ              
                PERFORM 450-CALL-BP13C913        THRU 450-EXIT                  
                PERFORM 800-PRINT-REPORT         THRU 800-EXIT                  
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN K110 ' WS-K110-STATUS              
                DISPLAY 'K110-NUM-REGN = ' K110-REGN-NO                         
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING K110 FILE  ' WS-K110-STATUS              
                MOVE WS-K110-STATUS  TO RETURN-CODE                             
                DISPLAY 'K110-REGN-NO = ' K110-REGN-NO                          
                CLOSE BP13F200                                                  
                PERFORM 999-CLOSE-FILES THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
           PERFORM 400-READ-F200         THRU 400-EXIT.                         
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       450-CALL-BP13C913.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES           TO BP13COMM8-REC.                              
           INITIALIZE               BP13COMM8-REC.                              
                                                                                
           MOVE K110-SCHEME      TO WS-LINK-NUM-SCH.                            
           MOVE K110-ACC         TO WS-LINK-NUM-ACC.                            
           MOVE K110-CHK         TO WS-LINK-NUM-CHK.                            
                                                                                
           CALL 'BP13C913' USING                                                
                 WS-LINK-REC, BP13COMM8-REC                                     
           END-CALL.                                                            
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE COMM8-CDE-SYSERR  TO WS-SQL-CODE                             
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE' WS-SQL-CODE           
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' WS-SQL-CODE                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE COMM8-NUM-UNIT-MAIN TO L232-NUM-MAIN.                           
           MOVE COMM8-NUM-UNIT-SUB  TO L232-NUM-SUB.                            
           MOVE '-'                 TO L232-DASH.                               
           MOVE COMM8-NUM-LEVEL     TO L232-NUM-LEVEL.                          
           MOVE COMM8-NUM-BLK       TO L232-BLK.                                
           MOVE COMM8-NME-STREET    TO L232-STREET-NAME.                        
                                                                                
           MOVE SPACES              TO L232-RM-TY.                              
                                                                                
           EVALUATE COMM8-CDE-TYP-ACTLUSE                                       
           WHEN '01'                                                            
                EVALUATE COMM8-CDE-MODL-DESG                                    
                WHEN '80'                                                       
                WHEN '89'                                                       
                WHEN '92'                                                       
                     MOVE '1A'    TO L232-RM-TY                                 
                WHEN OTHER                                                      
                     MOVE '1 '    TO L232-RM-TY                                 
                END-EVALUATE                                                    
           WHEN '02'                                                            
                EVALUATE COMM8-CDE-MODL-DESG                                    
                WHEN '80'                                                       
                WHEN '89'                                                       
                WHEN '92'                                                       
                     MOVE '2A'    TO L232-RM-TY                                 
                WHEN OTHER                                                      
                     MOVE '2 '    TO L232-RM-TY                                 
                END-EVALUATE                                                    
           WHEN '03'                                                            
                MOVE '3 '         TO L232-RM-TY                                 
           WHEN '04'                                                            
                EVALUATE COMM8-CDE-MODL-DESG                                    
                WHEN '06'                                                       
                     MOVE '4S'    TO L232-RM-TY                                 
                WHEN '34'                                                       
                WHEN '70'                                                       
                WHEN '71'                                                       
                WHEN '72'                                                       
                WHEN '73'                                                       
                WHEN '81'                                                       
                WHEN '82'                                                       
                     MOVE '4D'    TO L232-RM-TY                                 
                WHEN OTHER                                                      
                     MOVE '4 '    TO L232-RM-TY                                 
                END-EVALUATE                                                    
           WHEN '05'                                                            
                EVALUATE COMM8-CDE-MODL-DESG                                    
                WHEN '34'                                                       
                WHEN '70'                                                       
                WHEN '71'                                                       
                WHEN '72'                                                       
                WHEN '73'                                                       
                WHEN '81'                                                       
                WHEN '82'                                                       
                     MOVE '5D'    TO L232-RM-TY                                 
                WHEN OTHER                                                      
                     MOVE '5 '    TO L232-RM-TY                                 
                END-EVALUATE                                                    
           WHEN '06'                                                            
                EVALUATE COMM8-CDE-MODL-DESG                                    
                WHEN '34'                                                       
                WHEN '70'                                                       
                WHEN '71'                                                       
                WHEN '72'                                                       
                WHEN '73'                                                       
                WHEN '81'                                                       
                WHEN '82'                                                       
                     MOVE 'ED'    TO L232-RM-TY                                 
                WHEN OTHER                                                      
                     MOVE 'E '    TO L232-RM-TY                                 
                END-EVALUATE                                                    
           WHEN '07'                                                            
                MOVE 'H '         TO L232-RM-TY                                 
           END-EVALUATE.                                                        
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-READ-BP13K800.                                                       
      *-------------------------------------------------------------            
           INITIALIZE K800-NUM-REGN.                                            
           MOVE F200-NUM-REGN      TO K800-NUM-REGN.                            
                                                                                
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
                ADD 1 TO WS-K800-READ                                           
                CONTINUE                                                        
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN K800 ' WS-K800-STATUS              
                DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                        
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING K800 FILE  ' WS-K800-STATUS              
                MOVE WS-K800-STATUS  TO RETURN-CODE                             
                DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                        
           END-EVALUATE.                                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-PRINT-REPORT.                                                        
      *-------------------------------------------------------------            
            IF (WS-LINE-CNT = 0) OR                                             
               (WS-NEXT-F205-REC = 'Y')                                         
               PERFORM 850-PRINT-HEADING  THRU 850-EXIT                         
               MOVE 5                       TO WS-LINE-CNT                      
               MOVE 'N'                     TO WS-NEXT-F205-REC                 
            END-IF.                                                             
                                                                                
            PERFORM 900-PRINT-DETAIL      THRU 900-EXIT.                        
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       850-PRINT-HEADING.                                                       
      *-------------------------------------------------------------            
           ADD   1                       TO WS-PAGE-CNT.                        
           MOVE WS-PAGE-CNT              TO L232-PAGE.                          
                                                                                
           IF F205-NUM-NT-ZONE = 'ME' OR 'ZN'                                   
              MOVE SPACES             TO L232-HEADER-REGN-FORMAT                
              MOVE 'NO    REGN NO'    TO L232-HEADER-REGN-FORMAT                
           ELSE                                                                 
             IF F205-NUM-NT-ZONE-SW = 'Z' OR 'T'                                
                MOVE SPACES                TO L232-HEADER-REGN-FORMAT           
                MOVE 'Q NO/HT/REGN NO/JB REGN NO'  TO                           
                      L232-HEADER-REGN-FORMAT                                   
             END-IF                                                             
           END-IF.                                                              
                                                                                
           WRITE PRINT-REC-A FROM L232-HEADER-1   AFTER PAGE.                   
           WRITE PRINT-REC-A FROM L232-HEADER-2   AFTER 1.                      
           WRITE PRINT-REC-A FROM L232-HEADER-3   AFTER 2.                      
           WRITE PRINT-REC-A FROM L232-HEADER-4   AFTER 1.                      
           WRITE PRINT-REC-A FROM L232-HEADER-5   AFTER 1.                      
           MOVE SPACES         TO PRINT-REC-A.                                  
                                                                                
       850-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-PRINT-DETAIL.                                                        
      *-------------------------------------------------------------            
           IF K110-ALLOC-SCH NOT = 'PA '                                        
              MOVE '('                        TO L232-LEFT-PAR                  
              MOVE ')'                        TO L232-RIGHT-PAR                 
              MOVE K110-ALLOC-SCH             TO L232-ALLOC                     
           END-IF.                                                              
                                                                                
           IF K110-ALLOC-SCH = 'JB '                                            
              PERFORM 500-READ-BP13K800 THRU 500-EXIT                           
              IF WS-K800-STATUS = ZEROS                                         
                 MOVE '/'                        TO L232-SLASH4                 
                 MOVE K800-NUM-JT-BALLOT-REGN    TO L232-JB-REGN-NO             
              ELSE                                                              
                 MOVE SPACES                     TO L232-SLASH4                 
                 MOVE SPACES                     TO L232-JB-REGN-NO             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F205-NUM-NT-ZONE = 'ME' OR 'ZN'                                   
              MOVE SPACES                  TO L232-NT-REGN-FORMAT               
              MOVE F200-NUM-REGN           TO L232-NT-REGN-NO                   
              ADD  1                       TO WS-NUMBER                         
              MOVE WS-NUMBER               TO L232-SER-NO                       
           ELSE                                                                 
           IF F205-NUM-NT-ZONE-SW = 'Z' OR 'T'                                  
              MOVE F200-NUM-HHTY-QUEUE-SERIAL TO L232-QUEUE-NO                  
              EVALUATE F200-NUM-HHTY-QUEUE-ALPHA                                
                WHEN 'H'                                                        
                     MOVE 'F' TO L232-HHTYPE                                    
                WHEN 'G'                                                        
                     MOVE 'U' TO L232-HHTYPE                                    
                WHEN 'C'                                                        
                     MOVE 'G' TO L232-HHTYPE                                    
                WHEN OTHER                                                      
                     MOVE F200-NUM-HHTY-QUEUE-ALPHA TO L232-HHTYPE              
              END-EVALUATE                                                      
                                                                                
              MOVE F200-NUM-SEQUENCE          TO L232-SEQ-NO                    
                                                                                
              MOVE F200-NUM-REGN              TO L232-ZONE-REGN-NO              
              MOVE '/'                        TO L232-SLASH1                    
                                                 L232-SLASH2                    
                                                 L232-SLASH3                    
           END-IF.                                                              
                                                                                
           WRITE PRINT-REC-A                FROM L232-DETAIL-1 AFTER 1.         
                                                                                
           MOVE SPACES                        TO PRINT-REC-A .                  
           ADD 1                              TO WS-LINE-CNT.                   
           ADD 1                              TO WS-L232-WRITE.                 
           MOVE 'Y'                           TO WS-F200-PRINTED.               
                                                                                
           IF WS-LINE-CNT > 44                                                  
              MOVE 0 TO WS-LINE-CNT                                             
           END-IF.                                                              
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           CLOSE BP13F205                                                       
                 BM06K110                                                       
                 BP13K800                                                       
                 P13L232A.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BM06K110 CLOSE ERROR ' WS-K110-STATUS               
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K800 CLOSE ERROR ' WS-K800-STATUS               
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY ' BP13C232 CONTROL TOTAL'.                                   
           DISPLAY '----------------------------------------'.                  
           DISPLAY ' NO. OF F205 RECORDS READ = ' WS-F205-READ-CNT.             
           DISPLAY ' NO. OF F200 RECORDS READ = ' WS-BP13F200-READ.             
           DISPLAY ' NO. OF K110 RECORDS READ = ' WS-K110-READ.                 
           DISPLAY ' NO. OF K800 RECORDS READ = ' WS-K800-READ.                 
           DISPLAY ' NO. OF RECORDS IN REPORT = ' WS-L232-WRITE.                
           DISPLAY SPACES.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
