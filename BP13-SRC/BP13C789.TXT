       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C789.                                                 
      **********************************************************                
      *                                                        *                
      *    AUTHOR.       CHYEO.                                *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  THIS IS A PROGRAM TO MATCH SELECT THE         *                
      *          UPGRADER CASES AND MATCH BP13F500 TO EXTRACT  *                
      *          THE NT-REQ AND FLAT-TYPE REQ.                 *                
      *                                                        *                
      *  CHG NO   DATE   OIC  DESCRIPTION                      *                
      *  ------  -----   ---- -----------                      *                
      * N940014  280994  YCH  TO INCLUDE CDE HOUSEHOLD         *                
      * N940014  121094  YCH  CHANGE OF KEY FIELD              *                
      *  0449    220298  YCH  OCCPATION PERIOD OF APPLICANTS ON*                
      *                       WAITING LIST                     *                
      *  0441    020498  YCH  REFORMAT OF MONTH RESIDE         *                
      * BP130630 140199  NOR  CHGE F715 TO F716 EXPAND TO 350  *                
      * BP130652 190299  NOR  EXPAND F500 RECL FROM 300 TO 500 *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F716   ASSIGN TO BP13F716.                                
                                                                                
           SELECT BP13F500  ASSIGN TO BP13F500.                                 
                                                                                
           SELECT P13F716O  ASSIGN TO P13F716O.                                 
           EJECT                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F716                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORDING MODE IS F                                                 
            LABEL  RECORDS ARE  STANDARD.                                       
                                                                                
       COPY BP13F716.                                                           
                                                                                
       FD   P13F716O                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORDING MODE IS F                                                 
            LABEL  RECORDS ARE  STANDARD.                                       
                                                                                
       01   P13F716O-REC            PIC X(350).                                 
                                                                                
       FD   BP13F500                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORDING MODE IS F                                                 
            LABEL  RECORDS ARE  STANDARD.                                       
                                                                                
       COPY BP13F500.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  BP13F500-STATUS          PIC 9(2)    VALUE ZEROES.                   
       01  WS-F716-READ             PIC 9(6)    VALUE 0.                        
       01  WS-F500-READ             PIC 9(6)    VALUE 0.                        
       01  WS-F716-WRITE            PIC 9(6)    VALUE 0.                        
       01  WS-F716-BYPASS           PIC 9(6)    VALUE 0.                        
       01  WS-NUM-REGN.                                                         
           05  WS-NUM-REF           PIC X(6)    VALUE SPACES.                   
           05  FILLER               PIC X(2)    VALUE SPACES.                   
                                                                                
       01  EOF-BP13F500            PIC X        VALUE 'N'.                      
       01  EOF-BP13F716            PIC X        VALUE 'N'.                      
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-YY               PIC 9999.                                    
           05  WS-MM               PIC 99.                                      
           05  WS-DD               PIC 99.                                      
                                                                                
       01  WS-DTE-BALLOT.                                                       
           05  WS-DTE-BALLOT-YYMM.                                              
               10  WS-DTE-BALLOT-YY  PIC XXXX.                                  
               10  WS-DTE-BALLOT-MM  PIC XX.                                    
                                                                                
       PROCEDURE DIVISION.                                                      
       000-MAIN.                                                                
           OPEN INPUT  BP13F500                                                 
                       BP13F716                                                 
                OUTPUT P13F716O.                                                
                                                                                
           PERFORM 100-READ-F716-REC THRU 100-EXIT.                             
           PERFORM 150-READ-F500-REC THRU 150-EXIT.                             
           PERFORM 200-PROCESS-REC   THRU 200-EXIT                              
                   UNTIL F716-NUM-REGN = HIGH-VALUE                             
                    AND  F500-NUM-REGN = HIGH-VALUE.                            
           PERFORM 900-CLOSE-ROUTINE.                                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-READ-F716-REC.                                                       
           READ BP13F716 AT END MOVE 'Y' TO EOF-BP13F716                        
                MOVE HIGH-VALUE TO F716-NUM-REGN                                
                GO TO 100-EXIT.                                                 
           ADD 1 TO WS-F716-READ.                                               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
       150-READ-F500-REC.                                                       
           READ BP13F500 AT END MOVE 'Y' TO EOF-BP13F500                        
                MOVE HIGH-VALUE TO F500-NUM-REGN                                
                GO TO 150-EXIT.                                                 
           ADD 1 TO WS-F500-READ.                                               
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
       200-PROCESS-REC.                                                         
           IF F716-NUM-REGN = F500-NUM-REGN                                     
                                                                                
              IF F500-CDE-NT1 = SPACES OR LOW-VALUES                            
                 MOVE F500-CDE-ZONE   TO F716-CDE-NT-REQ                        
              ELSE                                                              
                 MOVE F500-CDE-NT1       TO F716-CDE-NT-REQ                     
              END-IF                                                            
                                                                                
              MOVE F500-CDE-FLAT-TYPE TO F716-CDE-FLAT-TYPE-REQ                 
              MOVE SPACES                TO WS-DTE-BALLOT                       
              IF F500-DTE-BALLOT = SPACES OR LOW-VALUES                         
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE F500-DTE-BALLOT    TO WS-DTE-BALLOT                       
              END-IF                                                            
              MOVE WS-DTE-BALLOT      TO F716-DTE-BALLOT                        
              MOVE F500-CDE-HOUSEHOLD TO F716-CDE-HOUSEHOLD                     
              MOVE ZEROES              TO F716-NUM-MTH-RESIDE(1:1)              
              MOVE F500-NUM-MTH-RESIDE TO F716-NUM-MTH-RESIDE(2:3)              
              WRITE P13F716O-REC FROM BP13F716-REC                              
              ADD 1 TO WS-F716-WRITE                                            
              MOVE F716-NUM-REGN      TO WS-NUM-REGN                            
              PERFORM 210-BYPASS-F716 THRU 210-EXIT                             
                UNTIL F716-NUM-REGN NOT = WS-NUM-REGN                           
                  OR  EOF-BP13F716 = 'Y'                                        
              PERFORM 150-READ-F500-REC THRU 150-EXIT                           
           ELSE                                                                 
              IF F716-NUM-REGN < F500-NUM-REGN                                  
                 DISPLAY 'REG NO NOT IN BP13F500 ' F716-NUM-REGN                
                 PERFORM 100-READ-F716-REC THRU 100-EXIT                        
              ELSE                                                              
                 DISPLAY 'F500 NOT FOUND ' F500-NUM-REGN                        
                 PERFORM 150-READ-F500-REC THRU 150-EXIT.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       210-BYPASS-F716.                                                         
           PERFORM 100-READ-F716-REC THRU 100-EXIT.                             
                                                                                
           IF F716-NUM-REGN = WS-NUM-REGN                                       
              ADD 1 TO WS-F716-BYPASS.                                          
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-CUR-DATE.                    
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'PROGRAM-ID : BP13C789 '.                                    
           DISPLAY 'DROP DUPLICATE REFERENCE NUMBER'.                           
           DISPLAY 'CURR-DATE  : ' WS-CUR-DATE.                                 
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM F716 ' WS-F716-READ.                
           DISPLAY 'NO OF RECORDS DROP FROM F716 ' WS-F716-BYPASS.              
           DISPLAY 'NO OF RECORDS WRITE TO F716  ' WS-F716-WRITE.               
                                                                                
           CLOSE BP13F716                                                       
                 P13F716O                                                       
                 BP13F500.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
