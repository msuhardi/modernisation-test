      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
                                                                                
       PROGRAM-ID.    BP13C24J.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
       DATE-WRITTEN.  12 APRIL 2012.                                            
                                                                                
      *===============================================================*         
      *                                                               *         
      *    SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                 *         
      *                                                               *         
      *    SYSTEM ID   :  BP13                                        *         
      *                                                               *         
      *    OBJECTIVE   :  THIS IS THE PROGRAM TO PRINT THE CHECK      *         
      *                   LIST OF APPOINTMENT LETTERS PRINTED FOR     *         
      *                   MGPS                                        *         
      *                                                               *         
      *    INPUT FILES :  1. BP13F200 - BOOKING APPOINTMENT FILE      *         
      *                   2. BP13K800 - SOC APPLN MASTER FILE         *         
      *                   3. BP13K820 - SOC OCCUPIER FILE             *         
      *                   4. BP13K830 - CORR ADDRESS FILE             *         
      *                                                               *         
      *    OUTPUT LIST :  1. BP13L24J - FINALIZED NAME LIST           *         
      *                                                               *         
      *                                                               *         
      *    CHGE-NO  BY    DATE     DESCRIPTION                        *         
      *   --------  ---- --------  --------------------------------   *         
      *   BP134513  ESA1 12042012  NEW PROGRAM(MODELLED TO BP13C246)  *         
      *   BP134521  ESA1 03052012  UPDATE REPORT LAYOUT               *         
      *===============================================================*         
                                                                                
                                                                                
      ***********************                                                   
       ENVIRONMENT DIVISION.                                                    
      ***********************                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F200 ASSIGN       TO BP13F200.                            
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS WS-K800-STATUS.                      
           SELECT BP13K820 ASSIGN       TO BP13K820                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K820-KEY-FLD                         
                           FILE STATUS  IS WS-K820-STATUS.                      
           SELECT BP13K830 ASSIGN       TO BP13K830                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K830-KEY-FLD                         
                           ALTERNATE RECORD KEY IS K830-NUM-REGN                
                           FILE STATUS  IS WS-K830-STATUS.                      
           SELECT BP13L24J ASSIGN       TO BP13L24J.                            
                                                                                
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F200                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13L24J                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   PRINT-REC                       PIC X(132).                         
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-INDICATORS.                                                       
           05  FILLER                       PIC X(10)  VALUE                    
               'INDICATORS'.                                                    
           05  WS-K200-STATUS               PIC 99     VALUE ZEROS.             
           05  WS-K800-STATUS               PIC 99     VALUE ZEROS.             
           05  WS-K820-STATUS               PIC 99     VALUE ZEROS.             
           05  WS-K830-STATUS               PIC 99     VALUE ZEROS.             
           05  WS-F200-EOF                  PIC X      VALUE 'N'.               
                                                                                
       01  WS-SAVE-AREA.                                                        
           05  WS-NAME                      PIC X(22)  VALUE SPACES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  FILLER                       PIC X(08)  VALUE                    
               'COUNTERS'.                                                      
           05  WS-NUMBER                    PIC 99     VALUE 99.                
           05  WS-SERIAL                    PIC 9(4)   VALUE ZEROS.             
           05  WS-PAGE-CNT                  PIC 9(4)   VALUE ZEROS.             
           05  WS-CNT-F200-READ             PIC 9(4)   VALUE ZEROS.             
                                                                                
       01  SW-SERS-ALLO-SCHEME              PIC X(01)  VALUE SPACES.            
           88  C-SERS-ALLO-SCHEME           VALUE 'Y'.                          
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  FILLER                       PIC X(09)  VALUE                    
               'DATE VARS'.                                                     
           05  WS-CURR-DATE                 PIC X(8)   VALUE SPACES.            
           05  WS-ALLOCN-DATE.                                                  
               10  WS-ALLOCN-YY             PIC 9(4)   VALUE 0000.              
               10  WS-ALLOCN-MM             PIC 99     VALUE 00.                
               10  WS-ALLOCN-DD             PIC 99     VALUE 00.                
           05  WS-BKAPPT-DATE.                                                  
               10  WS-BKAPPT-YY             PIC 9(4)   VALUE 0000.              
               10  WS-BKAPPT-MM             PIC 99     VALUE 00.                
               10  WS-BKAPPT-DD             PIC 99     VALUE 00.                
           05  WS-EDITED-DATE.                                                  
               10  WS-EDIT-DD               PIC 99     VALUE 00.                
               10  FILLER                   PIC X      VALUE '/'.               
               10  WS-EDIT-MM               PIC 99     VALUE 00.                
               10  FILLER                   PIC X      VALUE '/'.               
               10  WS-EDIT-YY               PIC 9(4)   VALUE 0000.              
                                                                                
           05  WS-TIME.                                                         
               10  WS-HH                    PIC 99.                             
               10  WS-MM                    PIC 99.                             
                                                                                
       COPY SERSCH.                                                             
                                                                                
      *---------------------------------------------------------------*         
      *        P R I N T     L A Y O U T                              *         
      *---------------------------------------------------------------*         
       01  L24J-HEADER-1B.                                                      
           05  FILLER                       PIC X(07)  VALUE                    
               'PAGE : '.                                                       
           05  L24J-PAGEB                   PIC ZZZ9.                           
           05  FILLER                       PIC X(31)  VALUE SPACE.             
           05  FILLER                       PIC X(39)  VALUE                    
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                       PIC X(9)   VALUE SPACES.            
           05  FILLER                       PIC X(4)   VALUE                    
               'CAT3'.                                                          
           05  FILLER                       PIC X(6)   VALUE SPACES.            
           05  FILLER                       PIC X(21)  VALUE                    
               'LISTING-ID : BP13L24J'.                                         
                                                                                
       01  L24J-HEADER-2B.                                                      
           05  FILLER                       PIC X(50)  VALUE SPACES.            
           05  FILLER                       PIC X(50)  VALUE SPACES.            
           05  FILLER                       PIC X(10)  VALUE                    
               'RUNDATE : '.                                                    
           05  CONTROL-DATEB                PIC X(10)  VALUE SPACES.            
                                                                                
       01  L24J-HEADER-3B.                                                      
           05  FILLER                       PIC X(30)  VALUE SPACES.            
           05  FILLER                       PIC X(60)  VALUE                    
            'CHECK LIST OF APPOINTMENT LETTERS PRINTED (MGPS)'.                 
           05  FILLER                       PIC X(40)  VALUE SPACES.            
                                                                                
       01  L24J-HEADER-4B.                                                      
           05  FILLER                       PIC X(5)   VALUE SPACES.            
           05  FILLER                       PIC X(16)  VALUE                    
               'DATE OF ALLOC : '.                                              
           05  L24J-ALLOC-DATEB             PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  FILLER                       PIC X(21)  VALUE                    
               '         ROOM-TYPE : '.                                         
           05  L24J-ROOM-TYPEB              PIC X(2)   VALUE SPACES.            
                                                                                
       01  L24J-LINE1B.                                                         
           05  FILLER                       PIC X(06)  VALUE                    
               'Q/NO  '.                                                        
           05  FILLER                       PIC X(03)  VALUE                    
               'HT/'.                                                           
           05  FILLER                       PIC X(10)  VALUE                    
               'REGN-NO   '.                                                    
           05  FILLER                       PIC X(05)  VALUE                    
               'ZONE '.                                                         
           05  FILLER                       PIC X(05)  VALUE                    
               'MGPS '.                                                         
           05  FILLER                       PIC X(06)  VALUE                    
               'ALSCH '.                                                        
           05  FILLER                       PIC X(17)  VALUE                    
               'DTE/TME OF APPMT '.                                             
           05  FILLER                       PIC X(27)  VALUE                    
               'CAT NAME (TRUNCATED)'.                                          
           05  FILLER                       PIC X(50)  VALUE                    
               'ADDRESS     '.                                                  
                                                                                
       01  L24J-DATA1B.                                                         
           05  L24J-Q-NO                    PIC X(5)   VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L24J-HT                      PIC X(1)   VALUE SPACE.             
           05  L24J-HT-SLASH                PIC X(1)   VALUE SPACE.             
           05  L24J-REGNOB                  PIC X(8)   VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L24J-ZONEB                   PIC X(3)   VALUE SPACES.            
           05  FILLER                       PIC X(4)   VALUE SPACES.            
           05  L24J-MGPSB                   PIC X      VALUE SPACE.             
           05  FILLER                       PIC X(4)   VALUE SPACES.            
           05  L24J-ALLOC-SCHB              PIC X(3)   VALUE SPACES.            
           05  FILLER                       PIC X(1)   VALUE SPACES.            
           05  L24J-DTE-BK-APPTB            PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(1)   VALUE SPACES.            
           05  L24J-APPT-HHB                PIC Z9     VALUE ZEROES.            
           05  L24J-DOTB                    PIC X(1)   VALUE SPACE.             
           05  L24J-APPT-MMB                PIC 9(2)   VALUE ZEROES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L24J-CATB                    PIC X(1)   VALUE SPACE.             
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L24J-APPL-NAMEB              PIC X(22)  VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L24J-BLK-NOB                 PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(1)   VALUE SPACE.             
           05  L24J-NUM-FLATB.                                                  
               10  L24J-CHARB               PIC X(1)   VALUE SPACE.             
               10  L24J-LEVELB              PIC X(2)   VALUE SPACES.            
               10  L24J-DASHB               PIC X(1)   VALUE SPACE.             
               10  L24J-HSE-MAINB           PIC X(4)   VALUE SPACES.            
               10  L24J-HSE-SUBB            PIC X(1)   VALUE SPACE.             
           05  FILLER                       PIC X(1)   VALUE SPACE.             
           05  L24J-STREET-NAMEB            PIC X(20)  VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L24J-POSTAL-CDB              PIC X(6)   VALUE SPACES.            
                                                                                
       01  L24J-DATA2B.                                                         
           05  FILLER                       PIC X(8)   VALUE SPACES.            
           05  L24J-SLASHB                  PIC X(1)   VALUE SPACES.            
           05  L24J-JB-REGNB                PIC X(8)   VALUE SPACES.            
           05  FILLER                       PIC X(42)  VALUE SPACES.            
           05  L24J-COAPPL-NAMEB            PIC X(22)  VALUE SPACES.            
                                                                                
                                                                                
                                                                                
      *********************                                                     
       PROCEDURE DIVISION.                                                      
      *********************                                                     
                                                                                
                                                                                
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
      ******************************************************************        
                                                                                
           PERFORM 100-OPEN-FILES-RTN      THRU   100-EXIT.                     
           PERFORM 200-READ-BP13F200       THRU   200-EXIT.                     
           PERFORM 300-PROCESS-F200        THRU   300-EXIT                      
                       UNTIL WS-F200-EOF = 'Y'.                                 
           PERFORM 900-CLOSE-FILES-RTN     THRU   900-EXIT.                     
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       100-OPEN-FILES-RTN.                                                      
      ******************************************************************        
           OPEN INPUT  BP13F200                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K830                                                 
                OUTPUT BP13L24J.                                                
                                                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K800 = ' WS-K800-STATUS              
              MOVE    WS-K800-STATUS  TO RETURN-CODE                            
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K820 = ' WS-K820-STATUS              
              MOVE    WS-K820-STATUS  TO RETURN-CODE                            
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K830 = ' WS-K830-STATUS              
              MOVE    WS-K830-STATUS  TO RETURN-CODE                            
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
           MOVE 99  TO WS-NUMBER.                                               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-READ-BP13F200.                                                       
      ******************************************************************        
                                                                                
           READ BP13F200                                                        
              AT END MOVE 'Y'   TO WS-F200-EOF                                  
           END-READ.                                                            
                                                                                
           ADD     1            TO WS-CNT-F200-READ                             
                                                                                
           IF WS-CNT-F200-READ = 1                                              
              IF F200-NUM-MGPS-TAG = 'C'                                        
                 MOVE 'Y'       TO WS-F200-EOF                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-F200.                                                        
      ******************************************************************        
                                                                                
           PERFORM 500-READ-K800           THRU  500-EXIT.                      
                                                                                
           PERFORM 200-READ-BP13F200  THRU  200-EXIT.                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      ******************************************************************        
       500-READ-K800.                                                           
      ******************************************************************        
                                                                                
           MOVE F200-NUM-REGN  TO   K800-NUM-REGN.                              
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              MOVE K800-NUM-BTO-ZONE TO L24J-ZONEB                              
              PERFORM 550-READ-K820 THRU 550-EXIT                               
           ELSE                                                                 
           IF WS-K800-STATUS = 23                                               
              MOVE SPACES TO L24J-APPL-NAMEB                                    
                             L24J-COAPPL-NAMEB                                  
                             L24J-ZONEB                                         
              DISPLAY 'RECORD NOT FOUND IN BP13K800 ' WS-K800-STATUS            
              DISPLAY '       K800-NUM-REGN = ' K800-NUM-REGN                   
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K800 = ' WS-K800-STATUS                
              DISPLAY '      K800-NUM-REGN    = ' K800-NUM-REGN                 
              MOVE    WS-K800-STATUS  TO  RETURN-CODE                           
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
           PERFORM 700-GET-ADDRESS    THRU 700-EXIT.                            
           PERFORM 800-PROCESS-PRINT  THRU 800-EXIT.                            
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       550-READ-K820.                                                           
      ******************************************************************        
           MOVE    K800-NUM-REGN    TO    K820-NUM-REGN.                        
           MOVE    K800-NUM-NRIC1   TO    K820-NUM-NRIC.                        
           PERFORM 560-ACCESS-K820  THRU  560-EXIT.                             
           MOVE    WS-NAME          TO    L24J-APPL-NAMEB.                      
                                                                                
                                                                                
           IF K800-NUM-NRIC2(2:1)  NOT =  '#'    AND                            
              K800-NUM-NRIC2       NOT =  SPACES AND LOW-VALUES                 
              MOVE     K800-NUM-REGN   TO   K820-NUM-REGN                       
              MOVE     K800-NUM-NRIC2  TO   K820-NUM-NRIC                       
              PERFORM  560-ACCESS-K820 THRU 560-EXIT                            
              MOVE     WS-NAME         TO   L24J-COAPPL-NAMEB                   
           ELSE                                                                 
              MOVE     SPACES          TO   L24J-COAPPL-NAMEB.                  
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       560-ACCESS-K820.                                                         
      ******************************************************************        
                                                                                
           READ BP13K820 KEY IS K820-KEY-FLD.                                   
                                                                                
           IF WS-K820-STATUS = 00                                               
              MOVE    K820-NME-OCCP  TO WS-NAME                                 
           ELSE                                                                 
           IF WS-K820-STATUS = 23                                               
              MOVE    SPACES         TO WS-NAME                                 
              DISPLAY 'RECORD NOT FOUND IN K820 ' WS-K820-STATUS                
              DISPLAY '       K820-KEY-FLD = ' K820-KEY-FLD                     
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K820 = ' WS-K820-STATUS                
              DISPLAY '      K820-KEY-FLD     = ' K820-KEY-FLD                  
              MOVE    WS-K820-STATUS  TO RETURN-CODE                            
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
       560-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       700-GET-ADDRESS.                                                         
      ******************************************************************        
           MOVE F200-NUM-REGN TO K830-NUM-REGN.                                 
                                                                                
           READ BP13K830 KEY  IS K830-NUM-REGN.                                 
                                                                                
           IF WS-K830-STATUS = 00 OR 02                                         
              IF K830-NUM-MAIL-ADDR   =  SPACES OR LOW-VALUES                   
                 IF K830-NUM-ADDR-TYPE = 'H'                                    
                    MOVE SPACES                 TO L24J-STREET-NAMEB            
                    MOVE K830-NUM-BLK           TO L24J-BLK-NOB                 
                    MOVE K830-NUM-LVL-CHAR      TO L24J-CHARB                   
                    MOVE K830-NUM-LVL-NUM       TO L24J-LEVELB                  
                    MOVE K830-NUM-LVL-DASH      TO L24J-DASHB                   
                    MOVE K830-NUM-HSEMAIN       TO L24J-HSE-MAINB               
                    MOVE K830-NUM-HSESUB        TO L24J-HSE-SUBB                
                    MOVE K830-NME-STREET-BLDG   TO L24J-STREET-NAMEB            
                    MOVE K830-NUM-POSTAL        TO L24J-POSTAL-CDB              
                 ELSE                                                           
                    MOVE SPACES                 TO L24J-STREET-NAMEB            
                    MOVE K830-NUM-BLK           TO L24J-BLK-NOB                 
                    MOVE K830-NUM-FLAT          TO L24J-NUM-FLATB               
                    MOVE K830-NME-STREET-BLDG   TO L24J-STREET-NAMEB            
                    MOVE K830-NUM-POSTAL        TO L24J-POSTAL-CDB              
                 END-IF                                                         
              ELSE                                                              
                 IF K830-NUM-MAIL-ADDR-TYPE =  'H'                              
                    MOVE SPACES                 TO L24J-STREET-NAMEB            
                    MOVE K830-NUM-MAIL-BLK      TO L24J-BLK-NOB                 
                    MOVE K830-NUM-MAIL-LVL-CHAR TO L24J-CHARB                   
                    MOVE K830-NUM-MAIL-LVL-NUM  TO L24J-LEVELB                  
                    MOVE K830-NUM-MAIL-LVL-DASH TO L24J-DASHB                   
                    MOVE K830-NUM-MAIL-HSEMAIN  TO L24J-HSE-MAINB               
                    MOVE K830-NUM-MAIL-HSESUB   TO L24J-HSE-SUBB                
                    MOVE K830-NME-MAIL-STR-BLDG TO L24J-STREET-NAMEB            
                    MOVE K830-NUM-MAIL-POSTAL   TO L24J-POSTAL-CDB              
                 ELSE                                                           
                    MOVE SPACES                 TO L24J-STREET-NAMEB            
                    MOVE K830-NUM-MAIL-BLK      TO L24J-BLK-NOB                 
                    MOVE K830-NUM-MAIL-FLAT     TO L24J-NUM-FLATB               
                    MOVE K830-NME-MAIL-STR-BLDG TO L24J-STREET-NAMEB            
                    MOVE K830-NUM-MAIL-POSTAL   TO L24J-POSTAL-CDB              
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS = 23                                               
              MOVE SPACES    TO  L24J-BLK-NOB    L24J-STREET-NAMEB              
                                 L24J-NUM-FLATB  L24J-POSTAL-CDB                
              DISPLAY 'RECORD NOT FOUND IN BP13K830 ' WS-K830-STATUS            
              DISPLAY '       K830-NUM-REGN  = ' K830-NUM-REGN.                 
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 02 AND 23                             
              DISPLAY 'ERROR READING BP13K830 = ' WS-K830-STATUS                
              DISPLAY '      K830-NUM-REGN    = ' K830-NUM-REGN                 
              MOVE    WS-K830-STATUS  TO  RETURN-CODE                           
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       800-PROCESS-PRINT.                                                       
      ******************************************************************        
                                                                                
           IF WS-NUMBER > 15                                                    
              PERFORM 821-PRINT-HEADING THRU 821-EXIT                           
           END-IF.                                                              
                                                                                
           PERFORM 841-PRINT-DETAIL   THRU   841-EXIT.                          
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       821-PRINT-HEADING.                                                       
      ******************************************************************        
                                                                                
      *--> 821-PRINT-HEADING - FOR ZONE                                         
                                                                                
           ADD    1                   TO  WS-PAGE-CNT.                          
           MOVE   ZEROS               TO  WS-NUMBER.                            
           MOVE   WS-PAGE-CNT         TO  L24J-PAGEB.                           
           WRITE  PRINT-REC         FROM  L24J-HEADER-1B AFTER PAGE.            
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
           STRING WS-CURR-DATE (7:2) '/'                                        
                  WS-CURR-DATE (5:2) '/'                                        
                  WS-CURR-DATE (1:4)                                            
                  DELIMITED BY SIZE                                             
                  INTO CONTROL-DATEB.                                           
           WRITE  PRINT-REC         FROM  L24J-HEADER-2B AFTER 1.               
           WRITE  PRINT-REC         FROM  L24J-HEADER-3B AFTER 2.               
           MOVE   F200-DTE-ALLOC      TO  WS-ALLOCN-DATE.                       
           MOVE   WS-ALLOCN-DD        TO  WS-EDIT-DD.                           
           MOVE   WS-ALLOCN-MM        TO  WS-EDIT-MM.                           
           MOVE   WS-ALLOCN-YY        TO  WS-EDIT-YY.                           
           MOVE   WS-EDITED-DATE      TO  L24J-ALLOC-DATEB.                     
           MOVE   F200-NUM-FLAT-TYPE  TO  L24J-ROOM-TYPEB.                      
           WRITE  PRINT-REC         FROM  L24J-HEADER-4B AFTER 2.               
           WRITE  PRINT-REC         FROM  L24J-LINE1B    AFTER 2.               
           MOVE   SPACES              TO  PRINT-REC.                            
                                                                                
       821-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       841-PRINT-DETAIL.                                                        
      ******************************************************************        
                                                                                
      *--> 841-PRINT-DETAIL - FOR ZONE                                          
           ADD   1                         TO   WS-NUMBER.                      
           MOVE  K800-NUM-REGN             TO   L24J-REGNOB.                    
           MOVE  K800-NUM-ALLO-SCHEME      TO   L24J-ALLOC-SCHB.                
           MOVE  K800-NUM-CAT              TO   L24J-CATB.                      
           MOVE  F200-DTE-BKAPPMT-DATE-MGPS TO   WS-BKAPPT-DATE.                
           MOVE  WS-BKAPPT-DD              TO   WS-EDIT-DD.                     
           MOVE  WS-BKAPPT-MM              TO   WS-EDIT-MM.                     
           MOVE  WS-BKAPPT-YY              TO   WS-EDIT-YY.                     
           MOVE  WS-EDITED-DATE            TO   L24J-DTE-BK-APPTB.              
           MOVE  F200-TME-BKAPPMT-MGPS     TO   WS-TIME.                        
                                                                                
           MOVE  WS-HH                     TO   L24J-APPT-HHB.                  
           MOVE '.'                        TO   L24J-DOTB.                      
           MOVE  WS-MM                     TO   L24J-APPT-MMB.                  
                                                                                
           EVALUATE F200-NUM-HHTY-QUEUE-ALPHA                                   
             WHEN 'H'                                                           
                  MOVE 'F' TO L24J-HT                                           
             WHEN 'G'                                                           
                  MOVE 'U' TO L24J-HT                                           
             WHEN 'C'                                                           
                  MOVE 'G' TO L24J-HT                                           
             WHEN OTHER                                                         
                  MOVE F200-NUM-HHTY-QUEUE-ALPHA TO L24J-HT                     
           END-EVALUATE.                                                        
                                                                                
           MOVE F200-NUM-MGPS-QUEUE-SERIAL TO   L24J-Q-NO.                      
           MOVE F200-NUM-MGPS-TAG          TO   L24J-MGPSB.                     
                                                                                
           IF (K800-NUM-JT-BALLOT-MTF = 1 OR 2 OR 3 OR 5) OR                    
              (K800-NUM-ALLO-SCHEME   = 'JB ')                                  
               MOVE '/'                     TO   L24J-SLASHB                    
               MOVE F200-NUM-JB-PARENT-REGN TO   L24J-JB-REGNB                  
           ELSE                                                                 
               MOVE SPACES                  TO   L24J-SLASHB                    
               MOVE SPACES                  TO   L24J-JB-REGNB                  
           END-IF.                                                              
                                                                                
           WRITE   PRINT-REC   FROM   L24J-DATA1B AFTER 2.                      
           WRITE   PRINT-REC   FROM   L24J-DATA2B AFTER 1.                      
                                                                                
       841-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       888-CHK-SERS-ALSCH.                                                      
      *-------------------------------------------------------------            
      *----------------------------------------------------------------         
      * SEARCH SERS TABLE TO CHECK ALLOC SCHEME                                 
      *----------------------------------------------------------------         
           SET WS-SERS-PTR TO 1.                                                
           SEARCH WS-SERS-SCH                                                   
              AT END                                                            
                 MOVE 'N'      TO SW-SERS-ALLO-SCHEME                           
              WHEN WS-SERS-SCHEME (WS-SERS-PTR) = K800-NUM-ALLO-SCHEME          
                 MOVE 'Y'      TO SW-SERS-ALLO-SCHEME                           
           END-SEARCH.                                                          
                                                                                
       888-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       900-CLOSE-FILES-RTN.                                                     
      ******************************************************************        
           CLOSE BP13F200                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13L24J.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K800  CLOSE ERROR ' WS-K800-STATUS              
              MOVE    WS-K800-STATUS  TO    RETURN-CODE.                        
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K820  CLOSE ERROR ' WS-K820-STATUS              
              MOVE    WS-K820-STATUS  TO    RETURN-CODE.                        
                                                                                
           IF WS-K830-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K830  CLOSE ERROR ' WS-K830-STATUS              
              MOVE    WS-K830-STATUS  TO    RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
