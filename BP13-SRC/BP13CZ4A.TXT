       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CZ4A.                                                 
      *AUTHOR.        SMP3.                                                     
      *DATE-WRITTEN.  10/02/2021.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    : GET THE LARGER REGNO BETWEERN REGNO AND JB  *            
      *                REGNO                                       *            
      *                                                            *            
      * INPUT FILES  : BP13F740  BP13.F740.MATUREBA.ACCA.JB        *            
      *                BP13K740  BP13.K740.MATUREBA                *            
      * OUTPUT FILES : BP13LZ4A  LARGER REGNO + RANDOM NUMBER      *            
      *                BP13F74A  BACKUP OF THE LARGER REGNO        *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP138600  SMP3  10022021  NEW PROGRAM                      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN        TO BP13F740.                           
                                                                                
           SELECT BP13K740 ASSIGN        TO BP13K740                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K740-KEY-FLD                        
                           ALTERNATE KEY IS K740-ALT1-KEY                       
                           FILE STATUS   IS WS-K740-STATUS.                     
                                                                                
           SELECT BP13LZ4A ASSIGN        TO BP13LZ4A.                           
                                                                                
           SELECT BP13F74A ASSIGN        TO BP13F74A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F740                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K740                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K740.                                                           
                                                                                
       FD   BP13LZ4A                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS  50 CHARACTERS.                                     
       01   BP13LZ4A-REC.                                                       
            05  LZ4A-NUM-REGN             PIC X(08).                            
            05  LZ4A-NUM-RANDOM           PIC X(28).                            
            05  FILLER                    PIC X(14).                            
                                                                                
       FD   BP13F74A                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       01   BP13F74A-REC                  PIC X(500).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K740-STATUS             PIC 9(02) VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ               PIC 9(08) VALUE ZEROES.               
           05  WS-F740-BYPASS             PIC 9(08) VALUE ZEROES.               
           05  WS-LZ4A-WRITE              PIC 9(08) VALUE ZEROES.               
           05  WS-K740-FND                PIC 9(08) VALUE ZEROES.               
           05  WS-K740-NTFND              PIC 9(08) VALUE ZEROES.               
           05  WS-F74A-WRITE              PIC 9(08) VALUE ZEROES.               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF                PIC X(01) VALUE 'N'.                  
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY            PIC 9(4).                             
               10  WS-SYS-MM              PIC 9(2).                             
               10  WS-SYS-DD              PIC 9(2).                             
           05  WS-EDITED-DATE             PIC X(10).                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-F740        THRU 3000-EXIT                      
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13K740                                                 
               OUTPUT  BP13LZ4A                                                 
                       BP13F74A.                                                
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K740, STATUS: ' WS-K740-STATUS         
              MOVE WS-K740-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-SYSTEM-DATE.                  
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE.                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F740.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F740 AT END                                                 
                MOVE 'Y'                    TO WS-F740-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F740-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-F740.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF F740-NUM-REGN > F740-NUM-JB-REGN                                  
              ADD 1                         TO WS-F740-BYPASS                   
              PERFORM 2000-READ-BP13F740    THRU 2000-EXIT                      
              GO TO 3000-EXIT.                                                  
                                                                                
           PERFORM 4000-WRITE-BP13LZ4A      THRU 4000-EXIT.                     
           PERFORM 5000-READ-BP13K741       THRU 5000-EXIT.                     
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-WRITE-BP13LZ4A.                                                     
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES                      TO BP13LZ4A-REC.                    
           INITIALIZE                          BP13LZ4A-REC.                    
                                                                                
           MOVE F740-NUM-JB-REGN            TO LZ4A-NUM-REGN.                   
           MOVE F740-NUM-RANDOM             TO LZ4A-NUM-RANDOM.                 
                                                                                
           WRITE BP13LZ4A-REC.                                                  
           ADD 1                            TO WS-LZ4A-WRITE.                   
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       5000-READ-BP13K741.                                                      
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES                      TO BP13K740-REC.                    
           INITIALIZE                          BP13K740-REC.                    
                                                                                
           MOVE F740-NUM-JB-REGN            TO K740-ALT1-KEY.                   
                                                                                
           READ BP13K740 KEY IS K740-ALT1-KEY.                                  
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
                ADD 1                       TO WS-K740-FND                      
                PERFORM 6000-WRITE-BP13F74A THRU 6000-EXIT                      
                                                                                
           WHEN 23                                                              
                ADD 1                       TO WS-K740-NTFND                    
                DISPLAY 'RECORD NOT FOUND IN BP13K740: ' K740-ALT1-KEY          
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K740, STATUS: '                      
                         WS-K740-STATUS                                         
                MOVE WS-K740-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-WRITE-BP13F74A.                                                     
      *-------------------------------------------------------------            
                                                                                
           WRITE BP13F74A-REC               FROM BP13K740-REC.                  
           ADD 1                            TO WS-F74A-WRITE.                   
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '========================================'.                  
           DISPLAY '     PROGRAM : BP13CZ4A  ' WS-EDITED-DATE.                  
           DISPLAY '========================================'.                  
           DISPLAY ' '.                                                         
           DISPLAY ' 1. BP13F740 RECORDS READ    : ' WS-F740-READ.              
           DISPLAY ' 2. BP13F740 RECORDS BYPASS  : ' WS-F740-BYPASS.            
           DISPLAY ' 3. BP13LZ4A RECORDS WRITTEN : ' WS-LZ4A-WRITE.             
           DISPLAY ' 4. BP13K740 RECORDS FOUND   : ' WS-K740-FND.               
           DISPLAY ' 5. BP13K740 RECORDS NOT FND : ' WS-K740-NTFND.             
           DISPLAY ' 6. BP13F74A RECORDS WRITTEN : ' WS-F74A-WRITE.             
           DISPLAY ' '.                                                         
           DISPLAY ' CHECK : '.                                                 
           DISPLAY '    1 = 2 + 3 '.                                            
           DISPLAY '    3 = 4 + 5 '.                                            
           DISPLAY '    4 = 6 '.                                                
           DISPLAY ' '.                                                         
           DISPLAY '========================================'.                  
                                                                                
           CLOSE BP13F740                                                       
                 BP13K740                                                       
                 BP13LZ4A                                                       
                 BP13F74A.                                                      
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K740, STATUS: ' WS-K740-STATUS         
              MOVE WS-K740-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
