       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CL02.                                                 
      *AUTHOR.        BALA AMUTHAN.                                             
      *DATE-WRITTEN.  14/08/2006.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT  (SOC)                                 *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE  :                                               *           
      *    1. DBSS LETTER OF OFFER TO BE RAISED AFTER BOOKING       *           
      *       10 DAYS.                                              *           
      *                                                             *           
      *  INPUT FILES:  1. BP13LC01 (INFO FOR OFFER LETTERS)         *           
      *                                                             *           
      *     REPORT  :  1. BP13LL02 (DBSS LETTER OF OFFER)           *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  BY   ON      DESCRIPTION                           *           
      * -------- --   --      -----------                           *           
      * BP132873 BA2  140806  NEW PROGRAM                           *           
      * BP133196 RB12 270808  ADD CA OIC IN REPORT                  *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13LC01 ASSIGN TO BP13LC01.                                  
                                                                                
           SELECT BP13LL02 ASSIGN TO BP13LL02.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13LC01                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13LC01.                                                           
                                                                                
       FD  BP13LL02                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  PRINT-REC           PIC X(132).                                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
      *---------------------------------------------------------------*         
      *       SWITCHES    VARIABLE.                                   *         
      *---------------------------------------------------------------*         
       01  WS-SWITCH.                                                           
           05  WS-LC01-END           PIC X(1)       VALUE 'N'.                  
               88 CS-LC01-END                       VALUE 'Y'.                  
                                                                                
      *---------------------------------------------------------------*         
      *       RECORD-COUNT VARIABLE.                                  *         
      *---------------------------------------------------------------*         
       01  WS-COUNT.                                                            
           05  WS-LC01-READ          PIC 9(5) COMP  VALUE ZEROS.                
           05  WS-L02A-WRITE         PIC 9(5) COMP  VALUE ZEROS.                
                                                                                
      *---------------------------------------------------------------*         
      *       OTHER VARIABLES.                                        *         
      *---------------------------------------------------------------*         
       01  WS-DATE.                                                             
           05 WS-DATE-CHAR           PIC X(8).                                  
           05 WS-DATE-NUM REDEFINES WS-DATE-CHAR                                
                                     PIC 9(8).                                  
                                                                                
       01  WS-UNIT-INFO.                                                        
           05 LVL-NO                 PIC X(02).                                 
           05 FILLER                 PIC X(01) VALUE '-'.                       
           05 MAIN-NO                PIC X(04).                                 
           05 WS-SLASH               PIC X(01) VALUE '-'.                       
           05 SUB-NO                 PIC X(03).                                 
                                                                                
       01  WS-PREV-VALUES.                                                      
           05 WS-PREV-PRJCT          PIC X(03) VALUE SPACES.                    
           05 WS-PREV-FLAT           PIC X(02) VALUE SPACES.                    
                                                                                
       77  WS-LINE-CNT               PIC 9(2)      VALUE 60.                    
       77  WS-PAGE-NO                PIC 9(4) COMP VALUE ZEROS.                 
       77  WS-SNO                    PIC 9(4) COMP VALUE ZEROS.                 
       77  WS-CNT                    PIC 9(1)      VALUE ZEROS.                 
                                                                                
      *---------------------------------------------------------------*         
      *       HEADINGS FOR REPORT                                     *         
      *---------------------------------------------------------------*         
       01  CL02-PR-HEAD-01.                                                     
           05  FILLER                PIC X(8)  VALUE 'BP13LL02'.                
           05  FILLER                PIC X(4)  VALUE SPACES.                    
           05  FILLER                PIC X(8)  VALUE 'HDB     '.                
           05  FILLER                PIC X(24) VALUE SPACES.                    
           05  FILLER                PIC X(20) VALUE                            
                                      'S Y S T E M   O F   '.                   
           05  FILLER                PIC X(19) VALUE                            
                                       'C O M M I T M E N T'.                   
           05  FILLER                PIC X(15) VALUE SPACES.                    
           05  FILLER                PIC X(7)  VALUE 'DATE :'.                  
           05  CL02-DATE             PIC X(10) VALUE SPACES.                    
           05  FILLER                PIC X(4)  VALUE SPACES.                    
           05  FILLER                PIC X(7)  VALUE 'PAGE : '.                 
           05  CL02-PAGENO           PIC ZZZ9 .                                 
                                                                                
       01  CL02-PR-HEAD-02.                                                     
           05  FILLER                PIC X(17)   VALUE SPACES.                  
           05  FILLER                PIC X(97)   VALUE                          
               'NUMBER OF LETTERS OF OFFER (DBSS) NOT RAISED (MORE THAN         
      -        '10 DAYS AFTER BOOKING)'.                                        
           05  FILLER                PIC X(20)   VALUE SPACES.                  
                                                                                
       01  CL02-PR-HEAD-03.                                                     
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(15)   VALUE                          
               'PROJECT      : '.                                               
           05  CL02-PRJ              PIC X(30)   VALUE SPACES.                  
           05  FILLER                PIC X(85)   VALUE SPACES.                  
                                                                                
       01  CL02-PR-HEAD-04.                                                     
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(15)   VALUE                          
               'FLAT TYPE    : '.                                               
           05  CL02-FLT-TYP          PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(113)  VALUE SPACES.                  
                                                                                
       01  CL02-PR-HEAD-05.                                                     
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(04)   VALUE                          
               'S/NO'.                                                          
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(12)   VALUE                          
               'BOOKING DATE'.                                                  
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(16)   VALUE                          
               'HLE APPROVE DATE'.                                              
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(18)   VALUE                          
               'NO OF DAYS OVERDUE'.                                            
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(10)   VALUE                          
               'HLE REF NO'.                                                    
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(11)   VALUE                          
               'DBSS REF NO'.                                                   
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(06)   VALUE                          
               'OIC PE'.                                                        
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(06)   VALUE                          
               'OIC CA'.                                                        
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(11)   VALUE                          
               'HLE STATUS '.                                                   
           05  FILLER                PIC X(04)   VALUE SPACES.                  
           05  FILLER                PIC X(11)   VALUE                          
               'HDB REF NO'.                                                    
           05  FILLER                PIC X(10)   VALUE SPACES.                  
                                                                                
       01  CL02-PR-HEAD-06.                                                     
           05  FILLER                PIC X(08)   VALUE SPACES.                  
           05  FILLER                PIC X(07)   VALUE                          
               'ADDRESS'.                                                       
           05  FILLER                PIC X(25)   VALUE SPACES.                  
           05  FILLER                PIC X(17)   VALUE                          
               'FROM BOOKING DATE'.                                             
           05  FILLER                PIC X(15)   VALUE SPACES.                  
           05  FILLER                PIC X(06)   VALUE                          
               'CA OIC'.                                                        
           05  FILLER                PIC X(54)   VALUE SPACES.                  
                                                                                
       01  CL02-PR-HEAD-07.                                                     
           05  FILLER                PIC X(08)   VALUE SPACES.                  
           05  FILLER                PIC X(04)   VALUE                          
               'NRIC'.                                                          
           05  FILLER                PIC X(10)   VALUE SPACES.                  
           05  FILLER                PIC X(04)   VALUE                          
               'NAME'.                                                          
           05  FILLER                PIC X(106)  VALUE SPACES.                  
                                                                                
       01  CL02-PR-HEAD-08.                                                     
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  CL02-SNO              PIC ZZZ9    VALUE SPACES.                  
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  CL02-BOOK-DTE         PIC X(10)   VALUE SPACES.                  
           05  FILLER                PIC X(04)   VALUE SPACES.                  
           05  CL02-DOC-APPR-DTE     PIC X(12)   VALUE SPACES.                  
           05  FILLER                PIC X(05)   VALUE SPACES.                  
           05  CL02-OVER-DUE-DAY     PIC Z(3)9   VALUE SPACES.                  
           05  FILLER                PIC X(17)   VALUE SPACES.                  
           05  CL02-HLE-REF-NO       PIC X(09)   VALUE SPACES.                  
           05  FILLER                PIC X(03)   VALUE SPACES.                  
           05  CL02-DBSS-REF-NO      PIC X(08)   VALUE SPACES.                  
           05  FILLER                PIC X(05)   VALUE SPACES.                  
           05  CL02-OIC-PE           PIC X(05)   VALUE SPACES.                  
           05  FILLER                PIC X(03)   VALUE SPACES.                  
           05  CL02-OIC-CA           PIC X(05)   VALUE SPACES.                  
           05  FILLER                PIC X(03)   VALUE SPACES.                  
           05  CL02-HLE-STATUS       PIC X(14)   VALUE SPACES.                  
           05  FILLER                PIC X(01)   VALUE SPACES.                  
           05  CL02-SCH-ACC          PIC X(14)   VALUE SPACES.                  
           05  FILLER                PIC X(06)   VALUE SPACES.                  
                                                                                
       01  CL02-PR-HEAD-09.                                                     
           05  FILLER                PIC X(08)   VALUE SPACES.                  
           05  CL02-ADDRESS          PIC X(60)   VALUE SPACES.                  
           05  FILLER                PIC X(04)   VALUE SPACES.                  
           05  CL02-CA-OIC           PIC X(05)   VALUE SPACES.                  
           05  FILLER                PIC X(55)   VALUE SPACES.                  
                                                                                
       01  CL02-PR-HEAD-10.                                                     
           05  FILLER                PIC X(08)   VALUE SPACES.                  
           05  CL02-NRIC             PIC X(09)   VALUE SPACES.                  
                                                                                
           05  FILLER                PIC X(05)   VALUE SPACES.                  
           05  CL02-NAME             PIC X(66)   VALUE SPACES.                  
           05  FILLER                PIC X(43)   VALUE SPACES.                  
                                                                                
       01  CL02-PR-LINE.                                                        
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FLLER                 PIC X(123)  VALUE ALL '-'.                 
           05  FILLER                PIC X(07)   VALUE SPACES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPENING-ROUTINE THRU 1000-EXIT.                         
           PERFORM 2000-READ-LC01       THRU 2000-EXIT.                         
           PERFORM 3000-MAIN-PROCESS   THRU 3000-EXIT                           
                   UNTIL CS-LC01-END.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT BP13LC01                                                  
               OUTPUT BP13LL02.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE-CHAR(7:2) '/' WS-DATE-CHAR(5:2) '/'                   
                  WS-DATE(1:4) DELIMITED BY SIZE INTO CL02-DATE.                
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-LC01.                                                          
      *-------------------------------------------------------------            
           READ BP13LC01 AT END MOVE 'Y' TO WS-LC01-END                         
                                GO TO 2000-EXIT.                                
           ADD 1 TO WS-LC01-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
           IF LC01-NUM-DAYS-OVERDUE2 > 10                                       
              PERFORM 3100-MOVE-DETAIL-PRINT   THRU 3100-EXIT                   
              PERFORM 3200-MOVE-ADDRES-PRINT   THRU 3200-EXIT                   
              PERFORM 3300-MOVE-NAME-IC-PRINT  THRU 3300-EXIT                   
                      VARYING WS-CNT FROM 1 BY 1 UNTIL WS-CNT > 4 OR            
                      LC01-NUM-NRIC(WS-CNT) = SPACES OR LOW-VALUES OR           
                                                            ZEROES              
                                                                                
              MOVE LC01-KEY-FLD                TO WS-PREV-VALUES                
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-LC01           THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-MOVE-DETAIL-PRINT.                                                  
      *-------------------------------------------------------------            
           ADD 1                         TO WS-SNO.                             
           MOVE  WS-SNO                  TO CL02-SNO.                           
                                                                                
           MOVE LC01-NME-PRJCT           TO CL02-PRJ.                           
           MOVE LC01-NUM-FT              TO CL02-FLT-TYP.                       
                                                                                
           IF LC01-DTE-ACCEPT NOT = SPACES AND ZEROES AND LOW-VALUES            
              STRING LC01-DTE-ACCEPT(7:2) '/' LC01-DTE-ACCEPT(5:2) '/'          
                     LC01-DTE-ACCEPT(1:4)                                       
                     DELIMITED BY SIZE INTO CL02-BOOK-DTE                       
           ELSE                                                                 
              MOVE SPACES                TO CL02-BOOK-DTE                       
           END-IF.                                                              
                                                                                
           IF LC01-DTE-HLE-APPROVAL NOT = SPACES AND ZEROES AND                 
                                          LOW-VALUES                            
              STRING LC01-DTE-HLE-APPROVAL(7:2) '/'                             
                     LC01-DTE-HLE-APPROVAL(5:2) '/'                             
                     LC01-DTE-HLE-APPROVAL(1:4)                                 
                     DELIMITED BY SIZE INTO CL02-DOC-APPR-DTE                   
           ELSE                                                                 
              MOVE SPACES                TO CL02-DOC-APPR-DTE                   
           END-IF.                                                              
                                                                                
                                                                                
           MOVE LC01-NUM-DAYS-OVERDUE2   TO CL02-OVER-DUE-DAY.                  
           MOVE LC01-NUM-HLE             TO CL02-HLE-REF-NO.                    
           MOVE LC01-NUM-REGN            TO CL02-DBSS-REF-NO.                   
           MOVE LC01-NUM-OIC-PE          TO CL02-OIC-PE.                        
           MOVE LC01-NUM-OIC-CA          TO CL02-OIC-CA.                        
           PERFORM 3150-CHECK-CASE-STATUS   THRU 3150-EXIT.                     
                                                                                
           IF LC01-NUM-HDB-REF NOT = SPACES AND LOW-VALUES AND ZEROES           
              STRING LC01-NUM-HDB-REF(1:4) '-'                                  
                     LC01-NUM-HDB-REF(5:4) '-'                                  
                     LC01-NUM-HDB-REF(9:1) '-'                                  
                     LC01-NUM-HDB-REF(10:2)                                     
                     DELIMITED BY SIZE INTO CL02-SCH-ACC                        
           ELSE                                                                 
              MOVE SPACES                TO CL02-SCH-ACC                        
           END-IF.                                                              
                                                                                
           PERFORM    6000-HEAD-CHECK-WRITE THRU 6000-EXIT.                     
           MOVE CL02-PR-HEAD-08          TO PRINT-REC.                          
           PERFORM    6100-WRITE-REPORT  THRU 6100-EXIT.                        
           ADD +1                        TO WS-L02A-WRITE.                      
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3150-CHECK-CASE-STATUS.                                                  
      *-------------------------------------------------------------            
           EVALUATE LC01-NUM-HLE-STATUS                                         
              WHEN 'PP'                                                         
                MOVE 'PENDING PE    '    TO CL02-HLE-STATUS                     
              WHEN 'KE'                                                         
                MOVE 'KIV PE        '    TO CL02-HLE-STATUS                     
              WHEN 'AE'                                                         
                MOVE 'COMPLETE PRE-E'    TO CL02-HLE-STATUS                     
              WHEN 'PRJ'                                                        
                MOVE 'PENDING REJECT'    TO CL02-HLE-STATUS                     
              WHEN 'REJ'                                                        
                MOVE 'REJECTED      '    TO CL02-HLE-STATUS                     
              WHEN 'PCN'                                                        
                MOVE 'PENDING CANCEL'    TO CL02-HLE-STATUS                     
              WHEN 'CAN'                                                        
                MOVE 'CANCELLED     '    TO CL02-HLE-STATUS                     
              WHEN 'EXP'                                                        
                MOVE 'EXPIRED       '    TO CL02-HLE-STATUS                     
           END-EVALUATE.                                                        
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-MOVE-ADDRES-PRINT.                                                  
      *-------------------------------------------------------------            
                                                                                
           MOVE LC01-NUM-LEVEL         TO   LVL-NO.                             
           MOVE LC01-NUM-MAIN-UNIT     TO   MAIN-NO.                            
           IF LC01-NUM-SUB-UNIT = SPACES OR LOW-VALUES                          
              MOVE SPACES              TO   WS-SLASH                            
                                            SUB-NO                              
           ELSE                                                                 
              MOVE LC01-NUM-SUB-UNIT   TO   SUB-NO                              
              MOVE '-'                 TO   WS-SLASH                            
           END-IF.                                                              
                                                                                
           STRING  'BLK ' LC01-NUM-BLK                                          
                   ' # '  WS-UNIT-INFO                                          
                   '   '  LC01-NME-STREET                                       
                     DELIMITED BY SIZE INTO CL02-ADDRESS.                       
                                                                                
           MOVE LC01-NUM-OIC-CA          TO CL02-CA-OIC.                        
                                                                                
           MOVE CL02-PR-HEAD-09          TO PRINT-REC.                          
           PERFORM    6100-WRITE-REPORT  THRU 6100-EXIT.                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3300-MOVE-NAME-IC-PRINT.                                                 
      *-------------------------------------------------------------            
           MOVE LC01-NUM-NRIC(WS-CNT)    TO CL02-NRIC.                          
           MOVE LC01-NME-HA(WS-CNT)      TO CL02-NAME.                          
           MOVE CL02-PR-HEAD-10          TO PRINT-REC.                          
           PERFORM    6100-WRITE-REPORT  THRU 6100-EXIT.                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-HEAD-CHECK-WRITE.                                                   
      *-------------------------------------------------------------            
           IF WS-LINE-CNT > 51                                                  
              PERFORM 6200-WRITE-HEAD  THRU 6200-EXIT                           
           ELSE                                                                 
              IF LC01-KEY-FLD NOT = WS-PREV-VALUES                              
                 PERFORM 6200-WRITE-HEAD  THRU 6200-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6100-WRITE-REPORT.                                                       
      *-------------------------------------------------------------            
           WRITE   PRINT-REC.                                                   
           ADD +1                  TO WS-LINE-CNT.                              
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6200-WRITE-HEAD.                                                         
      *-------------------------------------------------------------            
           ADD +1                  TO WS-PAGE-NO.                               
           MOVE WS-PAGE-NO         TO CL02-PAGENO.                              
                                                                                
           MOVE CL02-PR-HEAD-01    TO PRINT-REC.                                
           WRITE   PRINT-REC AFTER PAGE.                                        
                                                                                
           MOVE CL02-PR-HEAD-02    TO PRINT-REC.                                
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE CL02-PR-HEAD-03    TO PRINT-REC.                                
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE CL02-PR-HEAD-04    TO PRINT-REC.                                
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE CL02-PR-HEAD-05    TO PRINT-REC.                                
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE CL02-PR-HEAD-06    TO PRINT-REC.                                
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE CL02-PR-HEAD-07    TO PRINT-REC.                                
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE CL02-PR-LINE       TO PRINT-REC.                                
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE 09                 TO WS-LINE-CNT.                              
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           IF WS-L02A-WRITE <= 0                                                
              PERFORM 6200-WRITE-HEAD   THRU 6200-EXIT                          
              MOVE 'NO RECORDS FOUND'   TO   PRINT-REC                          
              PERFORM 6100-WRITE-REPORT THRU 6100-EXIT                          
           END-IF.                                                              
                                                                                
           DISPLAY '------- BP13CL02 -------------'.                            
           DISPLAY 'NO OF LC01 READ    (BP13LC01) : '                           
                                               WS-LC01-READ.                    
           DISPLAY 'NO OF L02A WRITTEN (BP13LL02) : '                           
                                               WS-L02A-WRITE.                   
                                                                                
           CLOSE BP13LC01                                                       
                 BP13LL02.                                                      
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
