       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C26B.                                                 
       AUTHOR.        ELGER DE GUZMAN                                           
      *DATE-WRITTEN.  06/01/2015.                                               
      * ===============================================================*        
      * SYSTEM NAME (SYSTEM ID)                                        *        
      * ===============================================================*        
      * OBJECTIVE   : PROGRAM TO COPY THE CPF BALANCE OF REQUESTED     *        
      *               REGN BY THE APPOINTMENT TEAM.                             
      *                                                                         
      * OUTPUT DESCRIPTION:                                            *        
      *                                                                *        
      * BP13K68A : REWRITE RECORD TO UPDATE CPF BALANCE.               *        
      *                                                                *        
      *   INPUT FILES :  1.  AB02F875  - CPF BALANCE WITH 2ND CA       *        
      *                  2.  BP13K800  - SOC APPLICATION MASTER        *        
      *                  3.  BP13K410  - SALES ORDER FILE              *        
      *   I-O         :  1.  BP13K68A  - WORKSHEET/CASHIER ORDER FILE  *        
      *                                                                *        
      *   OUTPUT      :  1.  BP13F875  - OTHER CPF BALANCE             *        
      *                  2.  BP13L26B  - ERROR REPORT                  *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ------------------------------------*        
      *                                                                *        
      *   BP135619  ESD1  06/01/15 NEW PROGRAM                         *        
      *   BP135682  ESD1  02/03/15 ADD K410 AND ERROR RPT              *        
      *   BP136460  KSJ3  02/03/17 EXPAND BP13K68A (1500-1600)         *        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AB02F875  ASSIGN        TO AB02F875.                          
           SELECT BP13F875  ASSIGN        TO BP13F875.                          
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K800-NUM-REGN                   
                            FILE STATUS      IS WS-K800-STATUS.                 
                                                                                
           SELECT BP13K410  ASSIGN        TO BP13K410                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K410-NUM-REGN                   
                            FILE STATUS      IS WS-K410-STATUS.                 
                                                                                
           SELECT BP13K68A  ASSIGN        TO BP13K68A                           
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K68A-NUM-REGN                   
                            FILE STATUS      IS WS-K68A-STATUS.                 
                                                                                
           SELECT BP13L26B  ASSIGN        TO BP13L26B.                          
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  AB02F875                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 1500 CHARACTERS.                                     
       COPY AB02F875.                                                           
                                                                                
       FD  BP13F875                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 1500 CHARACTERS.                                     
       01  BP13F875-REC           PIC X(1500).                                  
                                                                                
       FD  BP13L26B                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 130 CHARACTERS.                                      
       01  BP13L26B-REC            PIC X(130).                                  
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K410                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K410.                                                           
                                                                                
       FD  BP13K68A                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13K68A.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY                  PIC 9(04).                          
           05  WS-CUR-MM                    PIC 9(02).                          
           05  WS-CUR-DD                    PIC 9(02).                          
                                                                                
       01  WS-TIME                          PIC 9(08)  VALUE 0.                 
       01  WS-SKIP-REC                      PIC X      VALUE 'N'.               
       01  WS-FOUND-K68A                    PIC X      VALUE SPACE.             
       01  WS-F875-EOF                      PIC X      VALUE SPACE.             
       01  WS-REWRITE-K68A                  PIC 9(7)   VALUE ZERO.              
       01  WS-NON-APPT-CPF                  PIC 9(7)   VALUE ZERO.              
       01  WS-F875-COUNT-I                  PIC 9(7)   VALUE ZERO.              
       01  WS-ERR-CTR                       PIC 9(7)   VALUE ZERO.              
       01  WS-FILE-STATUS.                                                      
           05 WS-K800-STATUS              PIC 99    VALUE ZEROS.                
           05 WS-K410-STATUS              PIC 99    VALUE ZEROS.                
           05 WS-K68A-STATUS              PIC 99    VALUE ZEROS.                
                                                                                
       01  WS-PRINT-VARIABLES.                                                  
           05 L26B-HDG1.                                                        
              10 FILLER                   PIC X(55)   VALUE SPACES.             
              10 FILLER                   PIC X(22)   VALUE                     
                'SYSTEM  OF  COMMITMENT'.                                       
              10 FILLER                   PIC X(55)   VALUE SPACES.             
                                                                                
           05 L26B-HDG2.                                                        
              10 FILLER                   PIC X(50)   VALUE 'BP13L26B'.         
              10 FILLER                   PIC X(32)   VALUE                     
                ' CPF BACKUP REQUEST ERROR REPORT'.                             
              10 FILLER                   PIC X(25)   VALUE SPACES.             
              10 FILLER                   PIC X(07)   VALUE 'DATE : '.          
              10 L26B-SYS-DATE            PIC X(10).                            
              10 FILLER                   PIC X(05)   VALUE SPACES.             
                                                                                
           05 L26B-HDG3.                                                        
              10 FILLER                   PIC X(06)   VALUE                     
                'S/NO. '.                                                       
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(20)   VALUE                     
                'REGISTRATION NO.    '.                                         
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(14)   VALUE                     
                 'SCHEME ACCOUNT'.                                              
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(17)   VALUE                     
                'ERROR DESCRIPTION'.                                            
                                                                                
           05 L26B-HDG4.                                                        
              10 FILLER                   PIC X(06)   VALUE                     
                '======'.                                                       
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(20)   VALUE                     
                '================    '.                                         
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(14)   VALUE                     
                 '=============='.                                              
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(17)   VALUE                     
                '================='.                                            
                                                                                
           05 L26B-DTL1.                                                        
              10 L26B-SNO                 PIC ZZZZZ9  VALUE ZEROES.             
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 L26B-REGN                PIC X(8)    VALUE SPACES.             
              10 FILLER                   PIC X(17)   VALUE SPACES.             
              10 L26B-SCH-ACCT            PIC X(11)   VALUE SPACES.             
              10 FILLER                   PIC X(08)   VALUE SPACES.             
              10 L26B-ERR-DESC            PIC X(50)   VALUE SPACES.             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-DATA    THRU 3000-EXIT                          
           UNTIL WS-F875-EOF = 'Y'.                                             
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  AB02F875                                                 
                       BP13K800                                                 
                       BP13K410                                                 
                I-O    BP13K68A                                                 
                OUTPUT BP13F875                                                 
                       BP13L26B.                                                
                                                                                
           INITIALIZE  BP13K800-MASTER AB02F875-REC BP13K410-REC                
                       BP13F875-REC BP13K68A-REC.                               
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K410 ERROR ' WS-K410-STATUS                  
              MOVE     WS-K410-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K68A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K68A ERROR ' WS-K68A-STATUS                  
              MOVE     WS-K68A-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-TIME.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-READ-INPUT.                                                         
      *=============================================================*           
                                                                                
            READ AB02F875                                                       
                 AT END MOVE 'Y'  TO WS-F875-EOF                                
            END-READ.                                                           
                                                                                
            IF WS-F875-EOF NOT = 'Y'                                            
               ADD 1 TO WS-F875-COUNT-I                                         
            END-IF.                                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-PROCESS-DATA.                                                       
      *=============================================================*           
                                                                                
           MOVE SPACES  TO BP13K800-MASTER BP13K410-REC                         
                           BP13F875-REC BP13K68A-REC                            
                                                                                
           INITIALIZE  BP13K800-MASTER BP13K410-REC                             
                       BP13F875-REC BP13K68A-REC.                               
                                                                                
           MOVE     F875-NUM-REGN       TO   K68A-NUM-REGN                      
           PERFORM  3500-READ-BP13K68A  THRU 3500-EXIT                          
                                                                                
           IF WS-FOUND-K68A = 'Y'                                               
               MOVE 'N'   TO  WS-SKIP-REC                                       
               PERFORM 4000-RETRIEVE-BP13K800 THRU 4000-EXIT                    
               PERFORM 5000-CREATE-BP13K68A   THRU 5000-EXIT                    
           ELSE                                                                 
               WRITE BP13F875-REC FROM AB02F875-REC                             
               ADD 1 TO WS-NON-APPT-CPF                                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3100-READ-BP13K800.                                                      
      *================================================================*        
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                IF K800-NUM-SCH-ACC = SPACES OR LOW-VALUES                      
                   ADD    1    TO    WS-ERR-CTR                                 
                   MOVE 'SCHEME ACCOUNT BLANK IN SOC MASTER'                    
                                              TO L26B-ERR-DESC                  
                   PERFORM 6000-CREATE-REPORT THRU 6000-EXIT                    
                END-IF                                                          
              WHEN 23                                                           
                MOVE    F875-NUM-REGN       TO  K410-NUM-REGN                   
                PERFORM 3200-READ-BP13K410 THRU 3200-EXIT                       
              WHEN OTHER                                                        
                MOVE  WS-K800-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13K800 FILE : ' WS-K800-STATUS         
                DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                        
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3200-READ-BP13K410.                                                      
      *================================================================*        
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE WS-K410-STATUS                                              
              WHEN 00                                                           
                CONTINUE                                                        
              WHEN 23                                                           
                MOVE  WS-K410-STATUS TO RETURN-CODE                             
                DISPLAY 'RECORD NOT FOUND BP13K410 : ' K410-NUM-REGN            
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
              WHEN OTHER                                                        
                MOVE  WS-K410-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13K410 FILE : ' WS-K410-STATUS         
                DISPLAY 'K410-NUM-REGN = ' K410-NUM-REGN                        
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3500-READ-BP13K68A.                                                      
      *================================================================*        
                                                                                
           READ BP13K68A.                                                       
                                                                                
           EVALUATE WS-K68A-STATUS                                              
              WHEN 00                                                           
                MOVE 'Y' TO WS-FOUND-K68A                                       
              WHEN 23                                                           
                MOVE 'N' TO WS-FOUND-K68A                                       
              WHEN OTHER                                                        
                MOVE  WS-K68A-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13K68A FILE : ' WS-K68A-STATUS         
                DISPLAY 'K68A-NUM-REGN = ' K68A-NUM-REGN                        
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4000-RETRIEVE-BP13K800.                                                  
      *================================================================*        
                                                                                
           MOVE    F875-NUM-REGN       TO   K800-NUM-REGN                       
           PERFORM 3100-READ-BP13K800  THRU 3100-EXIT.                          
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       5000-CREATE-BP13K68A.                                                    
      *================================================================*        
                                                                                
           IF WS-FOUND-K68A = 'Y' AND WS-SKIP-REC = 'N'                         
              MOVE SPACES TO BP13K68A-REC                                       
              INITIALIZE BP13K68A-REC                                           
                                                                                
              MOVE F875-NUM-SYSTEM          TO K68A-NUM-SYSTEM                  
              MOVE F875-NUM-HDB-REF         TO K68A-NUM-HDB-REF                 
              MOVE F875-NUM-REGN            TO K68A-NUM-REGN                    
              MOVE F875-NUM-CASE            TO K68A-NUM-CASE                    
              MOVE F875-AMT-LOAN-GRANT      TO K68A-AMT-LOAN-GRANT              
              MOVE F875-DTE-LOAN-COMM       TO K68A-DTE-LOAN-COMM               
              MOVE F875-NUM-LOAN-SCHEME     TO K68A-NUM-LOAN-SCHEME             
              MOVE F875-AMT-VALUATION-LIMIT TO K68A-AMT-VALUATION-LIMIT         
              MOVE F875-AMT-VALUATION-LIMIT-TAG                                 
                                        TO K68A-AMT-VALUATION-LIMIT-TAG         
              MOVE F875-LSE1-DETAILS(1:294) TO K68A-LSE1-DETAILS                
              MOVE F875-LSE2-DETAILS(1:294) TO K68A-LSE2-DETAILS                
              MOVE F875-LSE3-DETAILS(1:294) TO K68A-LSE3-DETAILS                
              MOVE F875-LSE4-DETAILS(1:294) TO K68A-LSE4-DETAILS                
                                                                                
              MOVE F875-NUM-USERID          TO K68A-NUM-USERID                  
              MOVE F875-NUM-OFFICE          TO K68A-NUM-OFFICE                  
              MOVE F875-NUM-ORIGINATOR      TO K68A-NUM-ORIGINATOR              
                                                                                
              MOVE ZEROES                   TO K68A-AMT-ORIGIN-VAL-LIMIT        
                                               K68A-MLP-VL-PCT                  
                                               K68A-AMT-AHWL1                   
                                               K68A-EARMK-OA-AMT1               
                                               K68A-AMT-AHWL2                   
                                               K68A-EARMK-OA-AMT2               
                                               K68A-AMT-AHWL3                   
                                               K68A-EARMK-OA-AMT3               
                                               K68A-AMT-AHWL4                   
                                               K68A-EARMK-OA-AMT4               
                                                                                
              PERFORM 5200-REWRITE-BP13K68A THRU 5200-EXIT                      
              ADD 1 TO WS-REWRITE-K68A                                          
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5200-REWRITE-BP13K68A.                                                   
      *================================================================*        
                                                                                
           REWRITE BP13K68A-REC                                                 
                                                                                
           IF WS-K68A-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              MOVE  WS-K68A-STATUS TO RETURN-CODE                               
              DISPLAY 'ERROR REWRITING BP13K68A FILE : ' WS-K68A-STATUS         
              DISPLAY 'K68A-NUM-REGN = ' K68A-NUM-REGN                          
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       6000-CREATE-REPORT.                                                      
      *================================================================*        
                                                                                
           IF WS-ERR-CTR = 1                                                    
              STRING WS-CUR-DATE(7:2) '/'                                       
                     WS-CUR-DATE(5:2) '/'                                       
                     WS-CUR-DATE(1:4)                                           
                     DELIMITED BY SIZE                                          
                     INTO L26B-SYS-DATE                                         
              END-STRING                                                        
              WRITE BP13L26B-REC FROM L26B-HDG1                                 
              WRITE BP13L26B-REC FROM L26B-HDG2                                 
              MOVE SPACES TO BP13L26B-REC                                       
              WRITE BP13L26B-REC                                                
              WRITE BP13L26B-REC FROM L26B-HDG3                                 
              WRITE BP13L26B-REC FROM L26B-HDG4                                 
              MOVE SPACES TO BP13L26B-REC                                       
              WRITE BP13L26B-REC                                                
           END-IF.                                                              
                                                                                
           MOVE WS-ERR-CTR        TO  L26B-SNO                                  
           MOVE F875-NUM-REGN     TO  L26B-REGN                                 
           MOVE K800-NUM-SCH-ACC  TO  L26B-SCH-ACCT                             
           WRITE BP13L26B-REC    FROM L26B-DTL1                                 
           MOVE  'Y'              TO  WS-SKIP-REC.                              
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
           DISPLAY 'TOTAL INPUT RECORDS        : ' WS-F875-COUNT-I              
           DISPLAY 'TOTAL REWRITE TO BP13K68A  : ' WS-REWRITE-K68A.             
           DISPLAY 'TOTAL NON CPF REQUEST      : ' WS-NON-APPT-CPF.             
           DISPLAY 'TOTAL RECORDS WITH ERROR   : ' WS-ERR-CTR.                  
           DISPLAY '        '                                                   
                                                                                
           CLOSE BP13K800                                                       
                 BP13K410                                                       
                 BP13K68A                                                       
                 BP13L26B                                                       
                 AB02F875                                                       
                 BP13F875.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K410 ERROR ' WS-K410-STATUS                  
              MOVE     WS-K410-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
           IF WS-K68A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K68A ERROR ' WS-K68A-STATUS                  
              MOVE     WS-K68A-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
