      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.   BP13CF7C.                                                  
       AUTHOR.       ELAINE S ARGA.                                             
      *DATE-WRITTEN. 30 NOVEMBER 2015.                                          
                                                                                
      ******************************************************************        
      *                    SYSTEM OF COMMITMENT (BP13)                 *        
      *================================================================*        
      *  OBJECTIVE:  1.  TO UPDATE BP13K010                            *        
      *                                                                *        
      *  FILES : BP13F740   - INPUT                                    *        
      *          BP13K010   - I-O                                      *        
      *                                                                *        
      *  MODIFICATIONS :                                               *        
      *  CHGE REQ #  DATE          AUTHOR   REMARKS                    *        
      *  ----------  --------      ------   -----------                *        
      *  BP136096    30112015      ESA1     NEW PROGRAM                *        
      *  BP136107    02022016      SMR2     REMOVE HARDCODING OF ELDER *        
      *  BP137183    04122017      SMR2     CATER FOR WHEN BALANCE FLAT*        
      *                                     IS LESS THAN TOT-ELDER-MIN *        
      *  BP138924    11102021      SMP3     REMOVE HARDCODING ON       *        
      *                                     ELDER-SPS AND ELDER-NONSPS *        
      ******************************************************************        
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K010 ASSIGN TO BP13K010                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K010-KEY-FLD                               
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740                                                            
            RECORDING MODE  IS   F                                              
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K010                                                            
            RECORD CONTAINS 600 CHARACTERS.                                     
       COPY BP13K010.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-CURR-DATE                 PIC X(8).                           
           05  WS-F740-KEY-FLD.                                                 
               10  WS-F740-CDE-NT1          PIC X(3).                           
               10  WS-F740-CDE-FLAT-TYPE    PIC X(2).                           
           05  WS-PREV-KEY-FLD.                                                 
               10  WS-PREV-CDE-NT1          PIC X(3).                           
               10  WS-PREV-CDE-FLAT-TYPE    PIC X(2).                           
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  WS-F740-EOF                  PIC X     VALUE 'N'.                
           05  WS-K010-FND                  PIC X     VALUE 'N'.                
           05  WS-K010-STATUS               PIC 9(2)  VALUE ZEROS.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ                 PIC 9(5)  VALUE ZEROS.              
           05  WS-K010-REWRITE              PIC 9(5)  VALUE ZEROS.              
           05  WS-CNT-SIM-ELDER             PIC 9(5)  VALUE ZEROS.              
           05  WS-CNT-SIM-NELDER            PIC 9(5)  VALUE ZEROS.              
           05  WS-CNT-SIM-SPS               PIC 9(5)  VALUE ZEROS.              
           05  WS-CNT-SIM-NSPS              PIC 9(5)  VALUE ZEROS.              
           05  WS-ELDERLY-PCT               PIC 9(3)  VALUE ZEROS.              
                                                                                
      *------------------------*                                                
       PROCEDURE DIVISION.                                                      
      *------------------------*                                                
                                                                                
      ******************************************************************        
       0000-CONTROL.                                                            
      ******************************************************************        
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-BP13F740 THRU 2000-EXIT.                           
           PERFORM 3000-PROCESS       THRU 3000-EXIT                            
                   UNTIL WS-F740-EOF = 'Y'.                                     
           MOVE WS-PREV-KEY-FLD    TO K010-KEY-FLD                              
           PERFORM 4000-READ-BP13K010 THRU 4000-EXIT.                           
           IF WS-K010-FND = 'Y'                                                 
              PERFORM 5000-REWRITE-BP13K010 THRU 5000-EXIT                      
           END-IF.                                                              
           PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT  BP13F740                                                 
                I-O    BP13K010.                                                
                                                                                
           IF WS-K010-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K010-STATUS ' WS-K010-STATUS              
              MOVE WS-K010-STATUS       TO RETURN-CODE                          
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F740.                                                      
      ******************************************************************        
           READ BP13F740                                                        
                AT END MOVE 'Y' TO WS-F740-EOF                                  
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F740-READ.                                               
                                                                                
           IF WS-F740-READ = 1                                                  
              MOVE F740-CDE-NT1        TO WS-PREV-CDE-NT1                       
              MOVE F740-CDE-FLAT-TYPE  TO WS-PREV-CDE-FLAT-TYPE                 
           END-IF.                                                              
                                                                                
           MOVE F740-CDE-NT1           TO WS-F740-CDE-NT1.                      
           MOVE F740-CDE-FLAT-TYPE     TO WS-F740-CDE-FLAT-TYPE.                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS.                                                            
      ******************************************************************        
                                                                                
           IF WS-F740-KEY-FLD = WS-PREV-KEY-FLD                                 
              IF F740-NUM-ELDERLY = 'F' OR 'S'                                  
                 IF ((F740-NUM-APPS NOT = SPACES AND LOW-VALUES) AND            
                     (F740-NUM-APPS-ELIG-TAG = '1' OR 'T')) OR                  
                    ((F740-NUM-MCPS = 'P' OR 'C' OR 'M') AND                    
                      (F740-CDE-RESIDENT-HA1 = '1' OR 'A' OR 'T'))              
                    ADD 1    TO   WS-CNT-SIM-SPS                                
                 ELSE                                                           
                    ADD 1    TO   WS-CNT-SIM-NSPS                               
                 END-IF                                                         
                 ADD 1       TO   WS-CNT-SIM-ELDER                              
              ELSE                                                              
                 ADD 1       TO   WS-CNT-SIM-NELDER                             
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES             TO K010-KEY-FLD                           
              MOVE WS-PREV-KEY-FLD    TO K010-KEY-FLD                           
              PERFORM 4000-READ-BP13K010     THRU 4000-EXIT                     
              IF WS-K010-FND = 'Y'                                              
                 PERFORM 5000-REWRITE-BP13K010 THRU 5000-EXIT                   
              END-IF                                                            
              MOVE ZEROES    TO   WS-CNT-SIM-ELDER WS-CNT-SIM-NELDER            
                                  WS-CNT-SIM-SPS   WS-CNT-SIM-NSPS              
              IF F740-NUM-ELDERLY = 'F' OR 'S'                                  
                 IF ((F740-NUM-APPS NOT = SPACES AND LOW-VALUES) AND            
                     (F740-NUM-APPS-ELIG-TAG = '1' OR 'T')) OR                  
                    ((F740-NUM-MCPS = 'P' OR 'C' OR 'M') AND                    
                      (F740-CDE-RESIDENT-HA1 = '1' OR 'A' OR 'T'))              
                    ADD 1    TO   WS-CNT-SIM-SPS                                
                 ELSE                                                           
                    ADD 1    TO   WS-CNT-SIM-NSPS                               
                 END-IF                                                         
                 ADD  1      TO   WS-CNT-SIM-ELDER                              
              ELSE                                                              
                 ADD  1      TO   WS-CNT-SIM-NELDER                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE F740-CDE-NT1           TO WS-PREV-CDE-NT1.                      
           MOVE F740-CDE-FLAT-TYPE     TO WS-PREV-CDE-FLAT-TYPE.                
                                                                                
           PERFORM 2000-READ-BP13F740  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4000-READ-BP13K010.                                                      
      ******************************************************************        
           MOVE 'N'           TO WS-K010-FND.                                   
           READ BP13K010.                                                       
                                                                                
           EVALUATE WS-K010-STATUS                                              
              WHEN 00                                                           
                   MOVE 'Y'       TO WS-K010-FND                                
              WHEN 23                                                           
                   DISPLAY 'ERROR READING BP13K010, STATUS '                    
                   WS-K010-STATUS ' KEY ' K010-KEY-FLD                          
                   INITIALIZE BP13K010-TENDER                                   
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13K010, STATUS '                    
                   WS-K010-STATUS ' KEY ' K010-KEY-FLD                          
                   INITIALIZE BP13K010-TENDER                                   
           END-EVALUATE.                                                        
                                                                                
           IF K010-NUM-TOT-ELDER-MIN NOT NUMERIC                                
              MOVE ZEROES        TO K010-NUM-TOT-ELDER-MIN                      
           END-IF.                                                              
                                                                                
           IF K010-NUM-TOT-PA NOT NUMERIC                                       
              MOVE ZEROES        TO K010-NUM-TOT-PA                             
           END-IF.                                                              
                                                                                
           IF K010-REG-PA1 NOT NUMERIC                                          
              MOVE ZEROES        TO K010-REG-PA1                                
           END-IF.                                                              
                                                                                
           IF K010-REG-PA2 NOT NUMERIC                                          
              MOVE ZEROES        TO K010-REG-PA2                                
           END-IF.                                                              
                                                                                
           IF K010-REG-PA3 NOT NUMERIC                                          
              MOVE ZEROES        TO K010-REG-PA3                                
           END-IF.                                                              
                                                                                
           IF K010-NUM-TOT-ELDER NOT NUMERIC                                    
              MOVE ZEROES        TO K010-NUM-TOT-ELDER                          
           END-IF.                                                              
                                                                                
           IF K010-NUM-REG-ELDER        NOT NUMERIC                             
              MOVE ZEROES        TO K010-NUM-REG-ELDER                          
           END-IF.                                                              
                                                                                
           IF K010-NUM-REG-NON-ELDER    NOT NUMERIC                             
              MOVE ZEROES        TO K010-NUM-REG-NON-ELDER                      
           END-IF.                                                              
                                                                                
           IF K010-NUM-REG-ELDER-SPS    NOT NUMERIC                             
              MOVE ZEROES        TO K010-NUM-REG-ELDER-SPS                      
           END-IF.                                                              
                                                                                
           IF K010-NUM-REG-ELDER-NONSPS NOT NUMERIC                             
              MOVE ZEROES        TO K010-NUM-REG-ELDER-NONSPS                   
           END-IF.                                                              
                                                                                
           IF K010-NUM-APP-ELDER-SPS NOT NUMERIC                                
              MOVE ZEROES        TO K010-NUM-APP-ELDER-SPS                      
           END-IF.                                                              
                                                                                
           IF K010-NUM-APP-ELDER-NONSPS NOT NUMERIC                             
              MOVE ZEROES        TO K010-NUM-APP-ELDER-NONSPS                   
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       5000-REWRITE-BP13K010.                                                   
      ******************************************************************        
                                                                                
           COMPUTE K010-NUM-TOT-ELDER  = (K010-NUM-TOT-PA -                     
                  (K010-REG-PA1 + K010-REG-PA2 + K010-REG-PA3))                 
           COMPUTE WS-ELDERLY-PCT ROUNDED =                                     
                  (K010-NUM-TOT-ELDER * (K010-NUM-ELDER / 100)) + .49           
                                                                                
           DISPLAY 'K010-KEY-FLD     : ' K010-KEY-FLD.                          
           DISPLAY '  NUM-TOT-ELDER(BF): ' K010-NUM-TOT-ELDER.                  
                                                                                
           IF K010-NUM-TOT-ELDER < K010-NUM-TOT-ELDER-MIN                       
              CONTINUE                                                          
           ELSE                                                                 
              IF K010-NUM-TOT-ELDER-MIN >= WS-ELDERLY-PCT OR                    
                 WS-CNT-SIM-ELDER <= K010-NUM-TOT-ELDER-MIN                     
                 MOVE K010-NUM-TOT-ELDER-MIN TO  K010-NUM-TOT-ELDER             
              ELSE                                                              
                 MOVE WS-ELDERLY-PCT         TO  K010-NUM-TOT-ELDER             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           DISPLAY '  NUM-TOT-ELDER(AF): ' K010-NUM-TOT-ELDER.                  
                                                                                
           COMPUTE K010-NUM-TOT-NON-ELDER      = ((K010-NUM-TOT-PA -            
                  (K010-REG-PA1 + K010-REG-PA2 + K010-REG-PA3)) -               
                   K010-NUM-TOT-ELDER).                                         
                                                                                
           DISPLAY '  NUM-TOT-NON-ELDER: ' K010-NUM-TOT-NON-ELDER.              
                                                                                
           COMPUTE K010-NUM-TOT-ELDER-NONSPS   =                                
                  (K010-NUM-TOT-ELDER * K010-NUM-ELDER-NONSPS) / 100.           
           COMPUTE K010-NUM-TOT-ELDER-SPS      =                                
                   K010-NUM-TOT-ELDER - K010-NUM-TOT-ELDER-NONSPS.              
                                                                                
           MOVE  WS-CNT-SIM-ELDER  TO  K010-NUM-APP-ELDER.                      
           MOVE  WS-CNT-SIM-NELDER TO  K010-NUM-APP-NON-ELDER.                  
           MOVE  WS-CNT-SIM-SPS    TO  K010-NUM-APP-ELDER-SPS.                  
           MOVE  WS-CNT-SIM-NSPS   TO  K010-NUM-APP-ELDER-NONSPS.               
                                                                                
           DISPLAY '  APP-ELDER-SPS:    ' K010-NUM-APP-ELDER-SPS                
           DISPLAY '  APP-ELDER-NONSPS: ' K010-NUM-APP-ELDER-NONSPS             
           DISPLAY '  TOT-ELDER-SPS:    ' K010-NUM-TOT-ELDER-SPS                
           DISPLAY '  TOT-ELDER-NONSPS: ' K010-NUM-TOT-ELDER-NONSPS             
           IF K010-NUM-APP-ELDER-SPS < K010-NUM-TOT-ELDER-SPS                   
              IF K010-NUM-APP-ELDER-NONSPS > K010-NUM-TOT-ELDER-NONSPS          
                 COMPUTE K010-NUM-TOT-ELDER-NONSPS =                            
                         K010-NUM-TOT-ELDER-NONSPS +                            
                      (K010-NUM-TOT-ELDER-SPS - K010-NUM-APP-ELDER-SPS)         
                 DISPLAY '  ADJ TOT-ELDER-NONSPS: '                             
                         K010-NUM-TOT-ELDER-NONSPS                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K010-NUM-APP-ELDER-NONSPS < K010-NUM-TOT-ELDER-NONSPS             
              IF K010-NUM-APP-ELDER-SPS > K010-NUM-TOT-ELDER-SPS                
                 COMPUTE K010-NUM-TOT-ELDER-SPS =                               
                         K010-NUM-TOT-ELDER-SPS +                               
                        (K010-NUM-TOT-ELDER-NONSPS -                            
                         K010-NUM-APP-ELDER-NONSPS)                             
                 DISPLAY '  ADJ TOT-ELDER-SPS: '                                
                         K010-NUM-TOT-ELDER-SPS                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           REWRITE BP13K010-TENDER.                                             
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN 00                                                              
              ADD   1            TO   WS-K010-REWRITE                           
           WHEN OTHER                                                           
              DISPLAY 'ERROR REWRITING BP13K010, STATUS ' WS-K010-STATUS        
                                                  ' KEY ' K010-KEY-FLD          
              MOVE    WS-K010-STATUS TO RETURN-CODE                             
              GO TO   9000-CLOSE-FILES                                          
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-FILES.                                                        
      ******************************************************************        
           DISPLAY ' '.                                                         
           DISPLAY ' *****  BP13CF7C  ***** '.                                  
           DISPLAY ' NO. OF REC READ FROM F740  = ' WS-F740-READ.               
           DISPLAY ' NO. OF REC UPDATED         = ' WS-K010-REWRITE.            
                                                                                
           CLOSE BP13F740                                                       
                 BP13K010.                                                      
                                                                                
           IF WS-K010-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'CLOSING ERROR, K010-STATUS ' WS-K010-STATUS              
              MOVE WS-K010-STATUS       TO RETURN-CODE                          
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
      * >>>>>>>>>>>>>>>>>>>>>>> END OF BP13C740 <<<<<<<<<<<<<<<<<<<<<< *        
      *----------------------------------------------------------------*        
