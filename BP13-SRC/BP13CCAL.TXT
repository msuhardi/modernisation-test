       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CCAL.                                                 
      *AUTHOR.        RJB1.                                                     
      *DATE-WRITTEN.  09/12/2015.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO CREATE A REPORT FOR STAMPING OF TOL DOCUMENTS   *        
      *                                                                *        
      * INPUT  FILE  : BP13F462                                        *        
      * I-O    FILE  : BP13K462                                        *        
      * OUTPUT FILE  : BP13MAIL                                        *        
      *                                                                *        
      * CHG-NO    BY    ON    - DESCRIPTION                            *        
      * ---------------------------------                              *        
      * BP136020 RJB1 20151209  NEW PROGRAM                            *        
      * BP137155 PCL4 20180108  TO UPDATE EMAIL RECIPIENTS             *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F462 ASSIGN       TO BP13F462.                            
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
           SELECT BP13MAIL ASSIGN       TO BP13MAIL.                            
           SELECT BP13K462 ASSIGN       TO BP13K462                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K462-NUM-REGN                        
                           FILE STATUS  IS WS-K462-STATUS.                      
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13K462                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K462.                                                           
                                                                                
       FD   BP13F462                                                            
            RECORD CONTAINS 1000 CHARACTERS                                     
            RECORDING MODE IS F.                                                
       COPY BP13F462.                                                           
                                                                                
       FD   BP13MAIL                                                            
            RECORD CONTAINS 80  CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS IS OMITTED                                            
            RECORDING MODE IS F.                                                
       01   MAIL-PRTREC             PIC X(80).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-VARIABLES.                                                        
           05  CNT-F462-READ                PIC 9(07) VALUE ZEROS.              
           05  WS-CNT-UPDATE                PIC 9(07) VALUE ZEROS.              
           05  CNT-SNO                      PIC 9(07) VALUE ZEROS.              
           05  WS-F462-EOF                  PIC X(01) VALUE SPACE.              
               88  F462-EOF                           VALUE 'Y'.                
           05  WS-K462-EOF                  PIC X     VALUE 'N'.                
           05  WS-K462-FOUND-TAG            PIC X     VALUE 'N'.                
           05  WS-K462-STATUS               PIC 9(2)  VALUE ZEROS.              
           05  WS-K462-NFND                 PIC 9(8)  VALUE ZEROS.              
           05  WS-K462-FND                  PIC 9(8)  VALUE ZEROS.              
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1A-RCPT.                                                     
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(60)  VALUE                                 
                               'BALASUBRAMANIAM_ARUNASALAM@HDB.GOV.SG>'.        
                                                                                
       01  MAIL-DTL1B-RCPT.                                                     
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(60)  VALUE                                 
                               'MUHAMMED_SHARIFF_SAHROM@HDB.GOV.SG>'.           
                                                                                
       01  MAIL-DTL1C-RCPT.                                                     
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(60)  VALUE                                 
                       'NOORJAHAN_SHAIK_MOHAMED_GULEMKADER@HDB.GOV.SG>'.        
                                                                                
       01  MAIL-DTL3-DATA.                                                      
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4-FROM.                                                      
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(26)  VALUE                                 
               'SOC System - Email Alert'.                                      
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5A-TO1.                                                      
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(60)  VALUE                                 
                               'BALASUBRAMANIAM_ARUNASALAM@HDB.GOV.SG>'.        
                                                                                
       01  MAIL-DTL5B-TO2.                                                      
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(60)  VALUE                                 
                               'MUHAMMED_SHARIFF_SAHROM@HDB.GOV.SG>'.           
                                                                                
       01  MAIL-DTL5C-TO3.                                                      
           05  FILLER          PIC X(4)  VALUE 'TO:<'.                          
           05  FILLER          PIC X(60) VALUE                                  
                       'NOORJAHAN_SHAIK_MOHAMED_GULEMKADER@HDB.GOV.SG>'.        
                                                                                
       01  MAIL-DTL6-SUBJECT.                                                   
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  FILLER          PIC X(35)  VALUE                                 
               'Stamping of TOL Documents'.                                     
                                                                                
       01  MAIL-DTL7-DATE.                                                      
           05 FILLER                   PIC X(6)   VALUE 'DATE: '.               
           05 MAIL-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY1-TITLE-01.                                                 
           05 FILLER                   PIC X(80)  VALUE                         
              'The TOL documents have been executed and signed. Please p        
      -       'roceed with        '.                                            
                                                                                
       01  MAIL-BODY2-TITLE-02.                                                 
           05 FILLER                   PIC X(80)  VALUE                         
              'stamping of the relevant TOL documents.'.                        
                                                                                
                                                                                
       01  MAIL-BODY7-HEADER.                                                   
           05 FILLER                   PIC X(80)  VALUE                         
              'S/N Regn No  TOL Signed Date '.                                  
                                                                                
       01  MAIL-BODY8-DASH.                                                     
           05 FILLER                   PIC X(80)  VALUE                         
              '--- -------- --------------- '.                                  
                                                                                
       01  MAIL-BODY9-DETAIL.                                                   
           05  MAIL-SNO                PIC ZZ9    VALUE ZEROES.                 
           05  FILLER                  PIC X(01)  VALUE SPACES.                 
           05  MAIL-REGN-NO            PIC X(08)  VALUE SPACES.                 
           05  FILLER                  PIC X(01)  VALUE SPACES.                 
           05  MAIL-TOL-DTE            PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
       01  MAIL-NIL.                                                            
           05  FILLER                  PIC X(17)  VALUE                         
                                          'NO RECORDS FOUND.'.                  
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 8000-OPEN-FILES            THRU 8000-EXIT.                   
                                                                                
           PERFORM 8001-READ-BP13F462         THRU 8001-EXIT                    
           PERFORM UNTIL F462-EOF                                               
              PERFORM 1000-PROCESS-RECORDS    THRU 1000-EXIT                    
              PERFORM 8001-READ-BP13F462      THRU 8001-EXIT                    
           END-PERFORM.                                                         
                                                                                
           IF CNT-SNO > 0                                                       
              WRITE MAIL-PRTREC FROM MAIL-END                                   
           END-IF.                                                              
                                                                                
           PERFORM 9000-CLOSE-FILES           THRU 9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-PROCESS-RECORDS.                                                    
      *-------------------------------------------------------------            
                                                                                
           IF F462-DTE-TOL-SIGNED = SPACES OR LOW-VALUES                        
              GO TO 1000-EXIT                                                   
           ELSE                                                                 
              IF F462-TOL-REPORT-TAG = 'Y'                                      
                 GO TO 1000-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF CNT-SNO < 1                                                       
              PERFORM 1100-PRINT-MAIL-HEADER THRU 1100-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE SPACES                  TO MAIL-BODY9-DETAIL                    
           INITIALIZE                      MAIL-BODY9-DETAIL.                   
                                                                                
           ADD 1                        TO CNT-SNO                              
           MOVE CNT-SNO                 TO MAIL-SNO.                            
           MOVE F462-NUM-REGN           TO MAIL-REGN-NO.                        
           STRING F462-DTE-TOL-SIGNED(7:2) '/'                                  
                  F462-DTE-TOL-SIGNED(5:2) '/'                                  
                  F462-DTE-TOL-SIGNED(1:4)                                      
           DELIMITED BY SIZE   INTO MAIL-TOL-DTE.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-BODY9-DETAIL.                            
                                                                                
           PERFORM 8002-UPDT-BP13K462 THRU 8002-EXIT.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1100-PRINT-MAIL-HEADER.                                                  
      *-------------------------------------------------------------            
                                                                                
           STRING F001-DTE-CURRENT(7:2) '/'                                     
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
           DELIMITED BY SIZE INTO MAIL-DATE                                     
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-HDR1                                     
           WRITE MAIL-PRTREC FROM MAIL-HDR2                                     
                                                                                
      * RCPT TO                                                                 
           WRITE MAIL-PRTREC FROM MAIL-DTL1A-RCPT                               
           WRITE MAIL-PRTREC FROM MAIL-DTL1B-RCPT                               
           WRITE MAIL-PRTREC FROM MAIL-DTL1C-RCPT                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL3-DATA.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL4-FROM.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL5A-TO1.                               
           WRITE MAIL-PRTREC FROM MAIL-DTL5B-TO2.                               
           WRITE MAIL-PRTREC FROM MAIL-DTL5C-TO3.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL6-SUBJECT.                            
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL7-DATE.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY1-TITLE-01.                          
           WRITE MAIL-PRTREC FROM MAIL-BODY2-TITLE-02.                          
           WRITE MAIL-PRTREC FROM MAIL-BODY7-HEADER.                            
           WRITE MAIL-PRTREC FROM MAIL-BODY8-DASH.                              
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT BP13F462                                                  
                      SY02F001                                                  
                  I-O BP13K462                                                  
               OUTPUT BP13MAIL.                                                 
                                                                                
           IF WS-K462-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K462 - ERROR OPENING : ' WS-K462-STATUS              
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8001-READ-BP13F462.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F462                                                        
             AT END                                                             
                MOVE 'Y'             TO WS-F462-EOF                             
                GO                   TO 8001-EXIT.                              
                                                                                
           ADD  1                    TO CNT-F462-READ.                          
                                                                                
       8001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8002-UPDT-BP13K462.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE F462-NUM-REGN        TO K462-NUM-REGN.                          
                                                                                
           READ BP13K462.                                                       
                                                                                
           EVALUATE WS-K462-STATUS                                              
           WHEN 00                                                              
              CONTINUE                                                          
           WHEN 23                                                              
              DISPLAY 'REGN NOT FOUND ON BP13K462 = ' K462-NUM-REGN             
              GO TO 8002-EXIT                                                   
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K462 , '                               
                   'STATUS = ' WS-K462-STATUS                                   
              MOVE WS-K462-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-EVALUATE.                                                        
                                                                                
           MOVE 'Y' TO K462-TOL-REPORT-TAG.                                     
                                                                                
           REWRITE BP13K462-REC.                                                
                                                                                
           EVALUATE WS-K462-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              ADD 1                         TO WS-CNT-UPDATE                    
           WHEN 22                                                              
              DISPLAY 'BP13K462 DUPLICATE ERROR : ' WS-K462-STATUS              
              DISPLAY 'BP13K462 KEY             : ' K462-NUM-REGN               
           WHEN OTHER                                                           
              DISPLAY 'BP13K462 REWRITE ERROR : ' WS-K462-STATUS                
              DISPLAY 'BP13K462 KEY           : ' K462-NUM-REGN                 
              MOVE WS-K462-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       8002-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8900-WRITE-NIL.                                                          
      *-------------------------------------------------------------            
           STRING F001-DTE-CURRENT(7:2) '/'                                     
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
           DELIMITED BY SIZE INTO MAIL-DATE                                     
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-HDR1                                     
           WRITE MAIL-PRTREC FROM MAIL-HDR2                                     
                                                                                
      * RCPT TO                                                                 
           WRITE MAIL-PRTREC FROM MAIL-DTL1A-RCPT                               
           WRITE MAIL-PRTREC FROM MAIL-DTL1B-RCPT                               
           WRITE MAIL-PRTREC FROM MAIL-DTL1C-RCPT                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL3-DATA.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL4-FROM.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL5A-TO1.                               
           WRITE MAIL-PRTREC FROM MAIL-DTL5B-TO2.                               
           WRITE MAIL-PRTREC FROM MAIL-DTL5C-TO3.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL6-SUBJECT.                            
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL7-DATE.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-NIL.                                     
                                                                                
       8900-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           IF CNT-SNO < 1                                                       
              PERFORM 8900-WRITE-NIL         THRU 8900-EXIT                     
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CCAL'.                                
           DISPLAY 'CURRENT DATE   : ' MAIL-DATE.                               
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BP13F462 READ     : ' CNT-F462-READ.                 
           DISPLAY 'NO. OF BP13K462 UPDATED  : ' WS-CNT-UPDATE.                 
                                                                                
           CLOSE BP13F462                                                       
                 SY02F001                                                       
                 BP13K462                                                       
                 BP13MAIL.                                                      
                                                                                
           IF WS-K462-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR, BP13K462-STATUS ' WS-K462-STATUS          
              MOVE WS-K462-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
