       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         BP13C54E.                                            
      *AUTHOR.             ZARITA SARMIENTO.                                    
      *DATE-WRITTEN.       14 MAY 2009.                                         
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  PROGRAM TO PROCESS THE MCP APPLICANTS       *            
      *                WITH 'AI' AND 'RI' TRANSACTIONS             *            
      *                                                            *            
      *                                                            *            
      *  INPUT FILE :  1. BP13FA40                                 *            
      *                2. BP13K800                                 *            
      *                3. BP13KB78                                 *            
      *                4. BP04K010                                 *            
      *                                                            *            
      *  I-O           1. BP13K853                                 *            
      *                2. BP13K665                                 *            
      *                                                            *            
      *  OUTPUT FILE:  1. BP04F010                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP133534  ZDD1 20032009 NEW PROGRAM                         *           
      * BP133702  ZDD1 27102009 INCLUDE P04K020 FOR DEBARMENT PERIOD*           
      * BP135599  PCL4 11122014 TO UPDATE K010-INTERFACE-SYS       *            
      * BP138645  KR13 15042021 GET CSTMR_SRC & ADDR FOR K010      *            
      * BP138645  MRR5 28062021 REPLACE READ P04K020 WITH CALL TO  *            
      *                         BP04C905 TO READ DBRMNT_REASON TABLE            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
            SELECT BP13FA40 ASSIGN TO BP13FA40                                  
                            FILE STATUS  IS WS-FA40-STAT.                       
                                                                                
            SELECT BP13K800 ASSIGN TO BP13K800                                  
                            FILE STATUS  IS WS-K800-STAT                        
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN.                      
                                                                                
            SELECT BP13KB78 ASSIGN TO BP13KB78                                  
                            FILE STATUS  IS WS-KB78-STAT                        
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KB78-KEY-FLD.                       
                                                                                
            SELECT BP04K010 ASSIGN TO BP04K010                                  
                            FILE STATUS  IS WS-K010-STAT                        
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K010-PRIME-KEY.                     
                                                                                
            SELECT BP13K665 ASSIGN TO BP13K665                                  
                            FILE STATUS  IS WS-K665-STAT                        
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS WS-K665-KEY-FLD.                    
                                                                                
            SELECT BP13K853 ASSIGN TO BP13K853                                  
                            FILE STATUS  IS WS-K853-STAT                        
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K853-KEY-FLD.                       
                                                                                
            SELECT BP13K820 ASSIGN TO BP13K820                                  
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            ALTERNATE RECORD KEY IS K820-NUM-NRIC               
                                         WITH DUPLICATES                        
                            FILE STATUS  IS WS-K820-STAT.                       
                                                                                
            SELECT BP04F010 ASSIGN TO BP04F010                                  
                            FILE STATUS  IS WS-F010-STAT.                       
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13FA40                                                            
                BLOCK CONTAINS     0 RECORDS                                    
                RECORD CONTAINS  150 CHARACTERS                                 
                LABEL RECORDS ARE STANDARD                                      
                RECORDING MODE IS F.                                            
       COPY BP13FA40.                                                           
                                                                                
       FD   BP13K800                                                            
                RECORD CONTAINS  2000 CHARACTERS.                               
       COPY BP13K800.                                                           
                                                                                
       FD   BP13KB78                                                            
                RECORD CONTAINS  3300 CHARACTERS.                               
       COPY BP13KB78.                                                           
                                                                                
       FD   BP04K010                                                            
                RECORD CONTAINS  300 CHARACTERS.                                
       COPY P04K010.                                                            
                                                                                
       FD  BP13K665                                                             
                RECORD CONTAINS 50 CHARACTERS.                                  
       01  WS-BP13K665-REC.                                                     
           05  WS-K665-KEY-FLD         PIC X(08).                               
           05  FILLER                  PIC X(42).                               
                                                                                
       FD   BP13K853                                                            
                RECORD CONTAINS  250  CHARACTERS.                               
       COPY BP13K853.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP04F010                                                            
                RECORD CONTAINS  300  CHARACTERS                                
                LABEL RECORDS ARE STANDARD                                      
                RECORDING MODE IS F.                                            
       COPY P04F010.                                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01 WS-VARIABLES.                                                         
          10 WS-FA40-READ-CTR         PIC 9(8) VALUE ZEROES.                    
          10 WS-FA40-AI-CTR           PIC 9(8) VALUE ZEROES.                    
          10 WS-FA40-RI-CTR           PIC 9(8) VALUE ZEROES.                    
          10 WS-K800-TOTAL-CTR        PIC 9(8) VALUE ZEROES.                    
          10 WS-KB78-READ-CTR         PIC 9(8) VALUE ZEROES.                    
          10 WS-K010-READ-CTR         PIC 9(8) VALUE ZEROES.                    
          10 WS-K010-NOT-FND          PIC 9(8) VALUE ZEROES.                    
          10 WS-K665-READ-CTR         PIC 9(8) VALUE ZEROES.                    
          10 WS-K800-NOT-FND          PIC 9(8) VALUE ZEROES.                    
          10 WS-K800-VALID-RI         PIC 9(8) VALUE ZEROES.                    
          10 WS-K800-BYPASS-RI        PIC 9(8) VALUE ZEROES.                    
          10 WS-KB78-NOT-FND          PIC 9(8) VALUE ZEROES.                    
          10 WS-K800-SCH-NOT-MATCH    PIC 9(8) VALUE ZEROES.                    
          10 WS-K800-READ-CTR-AI      PIC 9(8) VALUE ZEROES.                    
          10 WS-F010-WRITE-CTR        PIC 9(8) VALUE ZEROES.                    
          10 WS-K853-WRITE-CTR        PIC 9(8) VALUE ZEROES.                    
          10 WS-K853-REWRITE-CTR      PIC 9(8) VALUE ZEROES.                    
          10 WS-K853-NOT-FND          PIC 9(8) VALUE ZEROES.                    
          10 WS-K665-WRITE-CTR        PIC 9(8) VALUE ZEROES.                    
          10 WS-K010-BYPASS           PIC 9(8) VALUE ZEROES.                    
          10 WS-NRIC-PROCESSED        PIC 9(8) VALUE ZEROES.                    
                                                                                
          10 WS-FA40-STAT             PIC X(2) VALUE SPACES.                    
          10 WS-K800-STAT             PIC X(2) VALUE SPACES.                    
          10 WS-KB78-STAT             PIC X(2) VALUE SPACES.                    
          10 WS-K010-STAT             PIC X(2) VALUE SPACES.                    
          10 WS-F010-STAT             PIC X(2) VALUE SPACES.                    
          10 WS-K665-STAT             PIC X(2) VALUE SPACES.                    
          10 WS-K853-STAT             PIC X(2) VALUE SPACES.                    
          10 WS-K820-STAT             PIC X(2) VALUE SPACES.                    
                                                                                
          10 WS-NEW-REGN              PIC X(1)  VALUE SPACE.                    
          10 WS-KB78-FOUND            PIC X(1)  VALUE SPACE.                    
          10 WS-K010-FOUND            PIC X(1)  VALUE SPACE.                    
          10 WS-K853-FOUND            PIC X(1)  VALUE SPACE.                    
          10 WS-SUB                   PIC 9(1)  VALUE ZERO.                     
          10 WS-EOF-FA40              PIC X(1)  VALUE 'N'.                      
          10 WS-EOF-K010              PIC X(1)  VALUE 'N'.                      
          10 WS-SCH-ACC-MATCH         PIC X(1)  VALUE 'N'.                      
          10 WS-SKIP-REC              PIC X(1)  VALUE 'N'.                      
          10 WS-VALID-WRITE-AI        PIC X(1)  VALUE 'N'.                      
          10 WS-VALID-WRITE-RI        PIC X(1)  VALUE 'N'.                      
          10 WS-DEBARMENT-TAG         PIC X(1)  VALUE SPACE.                    
          10 WS-PROCESS               PIC X(2)  VALUE SPACES.                   
          10 WS-NME-PARENT            PIC X(66) VALUE SPACES.                   
          10 WS-MARSTAT               PIC X(3)  VALUE SPACES.                   
          10 WS-CURR-DATE             PIC X(8)  VALUE SPACES.                   
          10 WS-LAST-YR               PIC X(4)  VALUE SPACES.                   
                                                                                
       01 WS-COMM-AREA.                                                         
          10 WS-UIN-KEY               PIC X(9) VALUE SPACES.                    
          10 WS-VALID-FLAG            PIC X(1) VALUE SPACE.                     
          10 WS-NUM-UFIN-TYPE         PIC X(3) VALUE SPACE.                     
                                                                                
       COPY BP13K665.                                                           
                                                                                
      *----------------------*                                                  
      * LINKAGE FOR BP13C913 *                                                  
      *----------------------*                                                  
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH          PIC X(4).                           
               10  WS-LINK-NUM-ACC          PIC X(5).                           
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *----------------------*                                                  
      * LINKAGE FOR BP04C905 *                                                  
      *----------------------*                                                  
       01  LINK-REC.                                                            
           05  LINK-NUM-REASON    PIC X(3).                                     
                                                                                
       COPY P04CC905.                                                           
                                                                                
       PROCEDURE DIVISION.                                                      
      ****************************                                              
       0000-MAIN-CONTROL.                                                       
      ****************************                                              
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13FA40  THRU 2000-EXIT.                          
           PERFORM 3000-CHECK-PROCESS  THRU 3000-EXIT                           
                   UNTIL WS-EOF-FA40 = 'Y'.                                     
           PERFORM 9999-CLOSE-FILES    THRU 9999-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       1000-OPEN-FILES.                                                         
      ****************************                                              
                                                                                
           OPEN INPUT  BP13FA40                                                 
                       BP13K800                                                 
                       BP13KB78                                                 
                       BP13K820                                                 
                       BP04K010                                                 
                I-O    BP13K853                                                 
                       BP13K665                                                 
                OUTPUT BP04F010.                                                
                                                                                
           IF WS-K800-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN OPENING BP13K800 STATUS - '                     
                              WS-K800-STAT                                      
              MOVE WS-K800-STAT              TO RETURN-CODE                     
              PERFORM 9999-CLOSE-FILES       THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K820-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN OPENING BP13K820 STATUS - '                     
                              WS-K820-STAT                                      
              MOVE WS-K820-STAT              TO RETURN-CODE                     
              PERFORM 9999-CLOSE-FILES       THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-KB78-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN OPENING BP13KB78 STATUS - '                     
                              WS-KB78-STAT                                      
              MOVE WS-KB78-STAT              TO RETURN-CODE                     
              PERFORM 9999-CLOSE-FILES       THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K010-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN OPENING BP04K010 STATUS - '                     
                              WS-K010-STAT                                      
              MOVE WS-K010-STAT              TO RETURN-CODE                     
              PERFORM 9999-CLOSE-FILES       THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K665-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN OPENING BP13K665 STATUS - '                     
                              WS-K665-STAT                                      
              MOVE WS-K665-STAT              TO RETURN-CODE                     
              PERFORM 9999-CLOSE-FILES       THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K853-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN OPENING BP13K853 STATUS - '                     
                              WS-K853-STAT                                      
              MOVE WS-K853-STAT              TO RETURN-CODE                     
              PERFORM 9999-CLOSE-FILES       THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           ACCEPT WS-CURR-DATE    FROM   DATE YYYYMMDD.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       2000-READ-BP13FA40.                                                      
      ****************************                                              
           READ BP13FA40 AT END                                                 
                MOVE 'Y' TO WS-EOF-FA40                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1      TO WS-FA40-READ-CTR.                                      
           MOVE SPACE TO WS-NEW-REGN.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3000-CHECK-PROCESS.                                                      
      ****************************                                              
                                                                                
           IF FA40-CDE-TRANS = 'AI'                                             
              ADD 1     TO WS-FA40-AI-CTR                                       
              MOVE 'AI' TO  WS-PROCESS                                          
           ELSE                                                                 
              IF FA40-CDE-TRANS = 'RI'                                          
                 ADD 1     TO WS-FA40-RI-CTR                                    
                 MOVE 'RI' TO WS-PROCESS                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 4000-PROCESS-RECORDS      THRU 4000-EXIT.                    
           PERFORM 2000-READ-BP13FA40        THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       4000-PROCESS-RECORDS.                                                    
      ****************************                                              
                                                                                
           MOVE SPACES        TO K800-NUM-REGN                                  
                                 KB78-KEY-FLD                                   
                                 WS-KB78-FOUND.                                 
           MOVE FA40-REGN-NO  TO K800-NUM-REGN                                  
                                 KB78-KEY-FLD.                                  
                                                                                
           PERFORM 6000-READ-BP13K800 THRU 6000-EXIT.                           
                                                                                
           IF WS-SCH-ACC-MATCH NOT = 'Y' AND WS-PROCESS = 'AI'                  
              GO TO 4000-EXIT                                                   
           ELSE                                                                 
              IF WS-SKIP-REC = 'Y' AND WS-PROCESS = 'RI'                        
                GO TO 4000-EXIT                                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 7000-READ-BP13KB78 THRU 7000-EXIT.                           
                                                                                
           IF WS-KB78-FOUND = 'Y'                                               
              PERFORM 7200-VALIDATE-NRIC            THRU   7200-EXIT            
               VARYING WS-SUB FROM 1 BY 1                                       
                 UNTIL WS-SUB > 2                                               
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       6000-READ-BP13K800.                                                      
      ****************************                                              
           READ BP13K800                                                        
                                                                                
           ADD 1 TO WS-K800-TOTAL-CTR                                           
                                                                                
           EVALUATE WS-K800-STAT                                                
             WHEN 00                                                            
                IF WS-PROCESS = 'AI'                                            
                                                                                
                  IF FA40-SCH-ACC = K800-NUM-SCH-ACC                            
                    ADD 1    TO WS-K800-READ-CTR-AI                             
                    MOVE 'Y' TO WS-SCH-ACC-MATCH                                
                  ELSE                                                          
                    ADD 1    TO WS-K800-SCH-NOT-MATCH                           
                    MOVE 'N' TO WS-SCH-ACC-MATCH                                
                  END-IF                                                        
                                                                                
                ELSE                                                            
                                                                                
                   IF WS-PROCESS = 'RI'                                         
                     IF K800-NUM-SCH-ACC  = SPACES OR LOW-VALUES                
                        ADD 1    TO WS-K800-VALID-RI                            
                        MOVE 'N' TO WS-SKIP-REC                                 
                     ELSE                                                       
                        ADD 1    TO WS-K800-BYPASS-RI                           
                        MOVE 'Y' TO WS-SKIP-REC                                 
                     END-IF                                                     
                   END-IF                                                       
                                                                                
                END-IF                                                          
             WHEN 23                                                            
                  ADD 1     TO WS-K800-NOT-FND                                  
                  MOVE 'Y'  TO WS-SKIP-REC                                      
                  MOVE 'N'  TO WS-SCH-ACC-MATCH                                 
                  DISPLAY 'REG NO NOT FOUND IN BP13K800' FA40-REGN-NO           
                  DISPLAY 'K800 STATUS CODE..' WS-K800-STAT                     
             WHEN OTHER                                                         
                  DISPLAY 'ERROR READING BP13K800 '                             
                  MOVE WS-K800-STAT TO RETURN-CODE                              
                  DISPLAY 'STATUS CODE..K800 - ' WS-K800-STAT                   
                  PERFORM 9999-CLOSE-FILES  THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       7000-READ-BP13KB78.                                                      
      ****************************                                              
           READ BP13KB78.                                                       
                                                                                
                                                                                
           EVALUATE WS-KB78-STAT                                                
             WHEN 00                                                            
                 ADD 1    TO WS-KB78-READ-CTR                                   
                 MOVE 'Y' TO WS-KB78-FOUND                                      
             WHEN 23                                                            
                 ADD 1    TO WS-KB78-NOT-FND                                    
                 MOVE 'N' TO WS-KB78-FOUND                                      
                 DISPLAY 'REG NO NOT FOUND IN BP13KB78' FA40-REGN-NO            
                 DISPLAY 'KB78 STATUS CODE..' WS-KB78-STAT                      
             WHEN OTHER                                                         
                  DISPLAY 'ERROR READING BP13KB78 '                             
                  MOVE WS-KB78-STAT TO RETURN-CODE                              
                  DISPLAY 'KB78 STATUS CODE..' WS-KB78-STAT                     
                  PERFORM 9999-CLOSE-FILES  THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       7200-VALIDATE-NRIC.                                                      
      ****************************                                              
                                                                                
           MOVE SPACES    TO K010-PRIME-KEY                                     
                             WS-UIN-KEY                                         
                             WS-NME-PARENT                                      
                             WS-MARSTAT.                                        
           MOVE 'N'       TO WS-VALID-WRITE-AI                                  
                             WS-VALID-WRITE-RI                                  
                                                                                
           IF WS-SUB = 1                                                        
              IF KB78-NUM-CHILD1-PARENT-UIN = SPACES OR LOW-VALUES              
                 GO TO 7200-EXIT                                                
              ELSE                                                              
                MOVE KB78-NUM-CHILD1-PARENT-UIN     TO K010-PRIME-KEY           
                                                       WS-UIN-KEY               
                MOVE KB78-NME-CHILD1-PARENT         TO WS-NME-PARENT            
                MOVE KB78-NUM-CHILD1-PARENT-MARSTAT TO                          
                           WS-MARSTAT                                           
                MOVE 'Y'                           TO WS-NEW-REGN               
                ADD 1                              TO WS-NRIC-PROCESSED         
              END-IF                                                            
           ELSE                                                                 
             IF WS-SUB = 2                                                      
               IF KB78-NUM-CHILD2-PARENT-UIN = SPACES OR LOW-VALUES             
                  GO TO 7200-EXIT                                               
               ELSE                                                             
                  MOVE KB78-NUM-CHILD2-PARENT-UIN   TO K010-PRIME-KEY           
                                                       WS-UIN-KEY               
                  MOVE KB78-NME-CHILD2-PARENT       TO WS-NME-PARENT            
                  MOVE KB78-NUM-CHILD2-PARENT-MARSTAT TO                        
                           WS-MARSTAT                                           
                  MOVE 'N'                          TO WS-NEW-REGN              
                  ADD 1                            TO WS-NRIC-PROCESSED         
               END-IF                                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
           IF WS-PROCESS = 'AI'                                                 
             PERFORM 7210-START-BP04K010 THRU 7210-EXIT                         
                                                                                
             IF WS-K010-FOUND = 'Y'                                             
                                                                                
               PERFORM 7220-READ-NEXT-K010   THRU 7220-EXIT                     
                   UNTIL WS-EOF-K010 = 'Y'                                      
                   OR   K010-NUM-UINFIN NOT  = WS-UIN-KEY                       
                                                                                
               IF WS-VALID-WRITE-AI = 'Y'                                       
                  PERFORM 7300-MOVE-DETAILS   THRU 7300-EXIT                    
                  PERFORM 7500-WRITE-F010     THRU 7500-EXIT                    
                  PERFORM 7600-PROCESS-K853   THRU 7600-EXIT                    
               END-IF                                                           
             ELSE                                                               
               IF WS-K010-FOUND = 'N'                                           
                  PERFORM 7300-MOVE-DETAILS   THRU 7300-EXIT                    
                  PERFORM 7500-WRITE-F010     THRU 7500-EXIT                    
                  PERFORM 7600-PROCESS-K853   THRU 7600-EXIT                    
               END-IF                                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
           IF WS-PROCESS = 'RI'                                                 
              PERFORM 7600-PROCESS-K853   THRU 7600-EXIT                        
                                                                                
                  IF WS-K853-FOUND = 'Y'                                        
                    MOVE WS-UIN-KEY TO        K010-NUM-UINFIN                   
                    MOVE FA40-SCH-ACC(1:9)  TO K010-EX-SCH-ACCTNO               
                    MOVE 'MA'               TO K010-REASON-CODE                 
                                                                                
                    READ BP04K010                                               
                                                                                
                    EVALUATE WS-K010-STAT                                       
                    WHEN 00                                                     
                    WHEN 02                                                     
                         IF FA40-LESSEE-CODE   = K010-LSE-TEN-CODE              
                          PERFORM 7500-WRITE-F010  THRU 7500-EXIT               
                          ADD 1 TO WS-K010-READ-CTR                             
                         ELSE                                                   
                           ADD 1 TO WS-K010-NOT-FND                             
                         END-IF                                                 
                    WHEN 23                                                     
                     ADD 1 TO WS-K010-NOT-FND                                   
                     DISPLAY 'RECORD  NOT FOUND K010 ' K010-PRIME-KEY           
                     DISPLAY 'K010 STATUS CODE..' WS-K010-STAT                  
                    WHEN OTHER                                                  
                     DISPLAY 'ERROR READING BP04K010 '                          
                     MOVE WS-K010-STAT TO RETURN-CODE                           
                     DISPLAY 'K010 STATUS CODE..' WS-K010-STAT                  
                     PERFORM 9999-CLOSE-FILES  THRU 9999-EXIT                   
                    END-EVALUATE                                                
                  END-IF                                                        
           END-IF.                                                              
                                                                                
       7200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       7210-START-BP04K010.                                                     
      ****************************                                              
                                                                                
           START BP04K010 KEY IS >= K010-PRIME-KEY.                             
                                                                                
           EVALUATE WS-K010-STAT                                                
               WHEN 00                                                          
               WHEN 02                                                          
                     MOVE 'N'               TO WS-EOF-K010                      
                     MOVE 'Y'               TO WS-K010-FOUND                    
               WHEN 23                                                          
                    MOVE 'N'                TO WS-K010-FOUND                    
                    MOVE SPACES             TO P04F010-REC                      
                    INITIALIZE                 P04F010-REC                      
                    ADD 1                   TO WS-K010-NOT-FND                  
                    MOVE 'SL'               TO K010-DEBREF-AO                   
                    MOVE FA40-SCH-ACC(1:9)  TO K010-EX-SCH-ACCTNO               
                    MOVE FA40-LESSEE-CODE   TO K010-LSE-TEN-CODE                
                    MOVE 'MA'               TO K010-REASON-CODE                 
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP04K010..' WS-K010-STAT             
                    MOVE WS-K010-STAT TO RETURN-CODE                            
                    PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT            
           END-EVALUATE.                                                        
                                                                                
       7210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       7220-READ-NEXT-K010.                                                     
      ****************************                                              
                                                                                
           READ BP04K010 NEXT RECORD                                            
                                                                                
           EVALUATE WS-K010-STAT                                                
               WHEN 00                                                          
               WHEN 22                                                          
                 IF K010-NUM-UINFIN        =  WS-UIN-KEY                        
                    MOVE SPACES            TO P04F010-REC                       
                    INITIALIZE                P04F010-REC                       
                   IF FA40-SCH-ACC(1:9)    = K010-EX-SCH-ACCTNO AND             
                      K010-LSE-TEN-CODE    =  FA40-LESSEE-CODE                  
                     AND K010-REASON-CODE  = 'MA'                               
                       ADD 1               TO WS-K010-BYPASS                    
                       MOVE 'Y'            TO WS-EOF-K010                       
                       MOVE 'N'            TO WS-VALID-WRITE-AI                 
                   ELSE                                                         
                       MOVE SPACES             TO P04F010-REC                   
                       INITIALIZE P04F010-REC                                   
                       MOVE 'Y'                TO WS-EOF-K010                   
                       ADD 1                   TO WS-K010-NOT-FND               
                       MOVE 'SL'               TO K010-DEBREF-AO                
                       MOVE FA40-SCH-ACC(1:9)  TO K010-EX-SCH-ACCTNO            
                       MOVE FA40-LESSEE-CODE   TO K010-LSE-TEN-CODE             
                       MOVE 'MA'               TO K010-REASON-CODE              
                       MOVE 'Y'                TO WS-VALID-WRITE-AI             
                   END-IF                                                       
                 ELSE                                                           
                    MOVE SPACES             TO P04F010-REC                      
                    INITIALIZE P04F010-REC                                      
                    MOVE 'Y'                TO WS-EOF-K010                      
                    ADD 1                   TO WS-K010-NOT-FND                  
                    MOVE 'SL'               TO K010-DEBREF-AO                   
                    MOVE FA40-SCH-ACC(1:9)  TO K010-EX-SCH-ACCTNO               
                    MOVE FA40-LESSEE-CODE   TO K010-LSE-TEN-CODE                
                    MOVE 'MA'               TO K010-REASON-CODE                 
                    MOVE 'Y'                TO WS-VALID-WRITE-AI                
                 END-IF                                                         
                                                                                
               WHEN 10                                                          
                    MOVE SPACES                TO P04F010-REC                   
                    INITIALIZE                    P04F010-REC                   
                    ADD 1                   TO WS-K010-NOT-FND                  
                    MOVE 'Y'                TO WS-EOF-K010                      
                    MOVE 'SL'               TO K010-DEBREF-AO                   
                    MOVE FA40-SCH-ACC(1:9)  TO K010-EX-SCH-ACCTNO               
                    MOVE FA40-LESSEE-CODE   TO K010-LSE-TEN-CODE                
                    MOVE 'MA'               TO K010-REASON-CODE                 
                    MOVE 'Y'                TO WS-VALID-WRITE-AI                
               WHEN OTHER                                                       
                 DISPLAY 'ERROR OPENING BP04K010..' WS-K010-STAT                
                 MOVE WS-K010-STAT TO RETURN-CODE                               
                 PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT               
           END-EVALUATE.                                                        
                                                                                
       7220-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       7300-MOVE-DETAILS.                                                       
      ****************************                                              
           MOVE SPACES TO WS-VALID-FLAG.                                        
                                                                                
                                                                                
           IF WS-UIN-KEY IS NOT = SPACES  AND LOW-VALUES                        
              CALL 'AV02C013' USING WS-UIN-KEY WS-VALID-FLAG                    
                       WS-NUM-UFIN-TYPE                                         
           ELSE                                                                 
              MOVE 'X'             TO K010-NRIC-PREFIX                          
              MOVE WS-UIN-KEY(2:8) TO K010-NRIC-NUM                             
           END-IF.                                                              
                                                                                
           IF WS-VALID-FLAG = 'Y'                                               
              MOVE WS-UIN-KEY      TO  K010-NUM-UINFIN                          
           ELSE                                                                 
              MOVE 'X' TO K010-NRIC-PREFIX                                      
              MOVE WS-UIN-KEY(2:8) TO K010-NRIC-NUM                             
           END-IF.                                                              
                                                                                
                                                                                
                                                                                
           IF WS-NEW-REGN = 'Y'                                                 
              PERFORM 7400-GET-DEBARMENT-NO   THRU 7400-EXIT                    
           END-IF.                                                              
                                                                                
           MOVE K665-NUM-LAST-DEBARMENT     TO K010-DEB-REF-NO.                 
           MOVE 'MA'                        TO K010-REASON-CODE                 
                                               LINK-NUM-REASON.                 
           MOVE FA40-SCH-ACC(1:9)           TO K010-EX-SCH-ACCTNO.              
           MOVE FA40-LESSEE-CODE            TO K010-LSE-TEN-CODE.               
           MOVE FA40-DTE-TRANS              TO K010-EFF-DATE.                   
           MOVE WS-NME-PARENT               TO K010-NAME.                       
                                                                                
           PERFORM 7350-CALL-BP04C905       THRU 7350-EXIT.                     
                                                                                
           IF C905-QTY-PERIOD1 NOT = SPACES AND LOW-VALUES                      
              MOVE C905-QTY-PERIOD1         TO K010-PERIOD-DEBAR                
           ELSE                                                                 
              MOVE '999'                    TO K010-PERIOD-DEBAR                
           END-IF.                                                              
                                                                                
           MOVE WS-CURR-DATE                TO K010-INCLUSION-DATE.             
           MOVE WS-CURR-DATE                TO K010-LUPDATE.                    
                                                                                
       7300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       7350-CALL-BP04C905.                                                      
      ****************************                                              
                                                                                
           MOVE SPACES                 TO P04CC905-COMMAREA.                    
           INITIALIZE                     P04CC905-COMMAREA.                    
                                                                                
           CALL 'BP04C905' USING LINK-REC,                                      
                                 P04CC905-COMMAREA.                             
                                                                                
           IF C905-ERR-CDE = ZEROES                                             
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'BP04C905 ERROR, SQL CODE = ' C905-ERR-CDE                
           END-IF.                                                              
                                                                                
       7350-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       7400-GET-DEBARMENT-NO.                                                   
      ****************************                                              
                                                                                
           MOVE  LOW-VALUES       TO WS-K665-KEY-FLD.                           
                                                                                
           START BP13K665 KEY IS >= WS-K665-KEY-FLD                             
                 INVALID KEY                                                    
                 DISPLAY 'READING ERROR, K665-STATUS ' WS-K665-STAT             
                 MOVE WS-K665-STAT         TO RETURN-CODE                       
                 PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                       
                                                                                
           PERFORM 7410-READ-BP13K665 THRU 7410-EXIT                            
                   UNTIL WS-K665-KEY-FLD = HIGH-VALUES.                         
                                                                                
           IF K665-KEY-FLD = SPACES OR LOW-VALUES                               
              MOVE 0                TO  K665-NUM-LAST-DEBREF-ID                 
           END-IF.                                                              
                                                                                
           IF WS-LAST-YR < WS-CURR-DATE(1:4)                                    
              MOVE 0                TO  K665-NUM-LAST-DEBREF-ID                 
           END-IF.                                                              
                                                                                
           ADD   1                  TO  K665-NUM-LAST-DEBREF-ID.                
           MOVE  'SL'               TO  K665-NUM-DEBREF-AO                      
                                        K665-NUM-LAST-DEBREF-AO.                
           MOVE  WS-CURR-DATE       TO  K665-DTE-ISSUE-DEBARMENT.               
           MOVE  WS-CURR-DATE(1:4)  TO  K665-NUM-LAST-DEBREF-YR.                
           MOVE  'P13C54E'          TO  K665-NUM-USERID.                        
                                                                                
           IF WS-DEBARMENT-TAG = 'Y'                                            
              REWRITE WS-BP13K665-REC FROM BP13K665-REC                         
           ELSE                                                                 
              WRITE WS-BP13K665-REC FROM BP13K665-REC.                          
                                                                                
           EVALUATE WS-K665-STAT                                                
           WHEN 00                                                              
              MOVE  'Y'               TO  WS-DEBARMENT-TAG                      
              ADD   1                 TO  WS-K665-WRITE-CTR                     
           WHEN 22                                                              
              REWRITE WS-BP13K665-REC FROM BP13K665-REC                         
                      INVALID KEY                                               
                      DISPLAY 'REWRITING ERROR, K665-STAT   '                   
                      WS-K665-STAT                                              
                      MOVE WS-K665-STAT         TO RETURN-CODE                  
                      PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                   
              END-REWRITE                                                       
              ADD   1                 TO  WS-K665-WRITE-CTR                     
              MOVE  'Y'               TO   WS-DEBARMENT-TAG                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITING ERROR, K665-STATUS ' WS-K665-STAT                
              MOVE WS-K665-STAT       TO   RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
           END-EVALUATE.                                                        
       7400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       7410-READ-BP13K665.                                                      
      ****************************                                              
                                                                                
           READ BP13K665 NEXT                                                   
                AT END MOVE HIGH-VALUES TO WS-K665-KEY-FLD                      
                GO TO 7410-EXIT.                                                
                                                                                
           ADD 1 TO WS-K665-READ-CTR.                                           
                                                                                
           IF WS-K665-KEY-FLD NOT = SPACES AND LOW-VALUES                       
              MOVE WS-BP13K665-REC TO BP13K665-REC                              
              MOVE WS-BP13K665-REC(31:4) TO WS-LAST-YR                          
           END-IF.                                                              
                                                                                
       7410-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       7500-WRITE-F010.                                                         
      ****************************                                              
           MOVE 'BP13'        TO K010-INTERFACE-SYS.                            
                                                                                
           IF K010-AMT-OWING NOT NUMERIC                                        
              MOVE ZEROES              TO K010-AMT-OWING                        
           END-IF.                                                              
                                                                                
           PERFORM 7505-READ-BP13K820  THRU 7505-EXIT.                          
                                                                                
           PERFORM 7510-GET-ADDRESS    THRU 7510-EXIT.                          
                                                                                
           WRITE P04F010-REC  FROM P04K010-REC.                                 
           ADD 1              TO   WS-F010-WRITE-CTR.                           
                                                                                
       7500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       7505-READ-BP13K820.                                                      
      ****************************                                              
           INITIALIZE  BP13K820-REC                                             
                                                                                
           MOVE SPACES                     TO K820-KEY-FLD                      
           MOVE K010-NUM-UINFIN(1:9)       TO K820-NUM-NRIC                     
                                                                                
           START BP13K820 KEY >= K820-NUM-NRIC                                  
                                                                                
           EVALUATE WS-K820-STAT                                                
               WHEN 00                                                          
               WHEN 02                                                          
                    READ BP13K820 NEXT                                          
                    IF WS-K820-STAT = 00 OR 02                                  
                       IF K820-NUM-NRIC = K010-NUM-UINFIN(1:9)                  
                          MOVE K820-NUM-CSTMR-SOURCE                            
                                            TO K010-NUM-CSTMR-SOURCE            
                       END-IF                                                   
                    END-IF                                                      
               WHEN 23                                                          
                    DISPLAY 'KEY NOT FOUND IN BP13K820!' K820-KEY-FLD           
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K820!'                          
                             ' / KEY : ' K820-KEY-FLD                           
                             ' / FILE STATUS :' WS-K820-STAT                    
                    PERFORM 9999-CLOSE-FILES  THRU 9999-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       7505-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------                                                     
       7510-GET-ADDRESS.                                                        
      *--------------------                                                     
           MOVE SPACES                 TO BP13COMM8-REC.                        
           INITIALIZE                     BP13COMM8-REC.                        
                                                                                
           MOVE FA40-SCH-ACC           TO WS-LINK-REC.                          
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           IF (COMM8-CDE-SYSERR = 0) AND                                        
              (COMM8-NUM-MGMT   NOT = SPACES)                                   
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'BP13C913 ERROR, SQL CODE = ' COMM8-CDE-SYSERR            
           END-IF                                                               
                                                                                
           MOVE COMM8-NUM-BLK            TO K010-BLOCK.                         
           MOVE COMM8-NME-STREET         TO K010-STREETNAME.                    
           MOVE COMM8-NUM-POSTAL-CODE    TO K010-POSTCODE.                      
                                                                                
           STRING    COMM8-NUM-LEVEL                                            
                     COMM8-NUM-UNIT-MAIN COMM8-NUM-UNIT-SUB                     
                     DELIMITED BY SIZE INTO K010-HOUSE-NO.                      
                                                                                
       7510-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       7600-PROCESS-K853.                                                       
      ****************************                                              
           MOVE SPACES        TO K853-KEY-FLD.                                  
           MOVE WS-UIN-KEY    TO K853-NUM-NRIC.                                 
           MOVE KB78-NUM-REGN TO K853-NUM-REGN.                                 
                                                                                
           READ BP13K853.                                                       
                                                                                
           EVALUATE WS-K853-STAT                                                
             WHEN 00                                                            
             WHEN 02                                                            
               IF WS-PROCESS = 'RI'                                             
                  IF K853-DEB-REF-NO NOT =  SPACES AND LOW-VALUES               
                    MOVE 'Y'             TO WS-K853-FOUND                       
                    MOVE K853-DEB-REF-NO TO K010-DEB-REF-NO                     
                    MOVE SPACES          TO K853-DEB-REF-NO                     
                                            K853-CDE-DEBAR                      
                    MOVE WS-CURR-DATE    TO K853-DTE-UPDATE                     
                    MOVE 'BP13C54E'      TO K853-NUM-USERID                     
                                                                                
                    REWRITE BP13K853-REC                                        
                      INVALID KEY                                               
                      DISPLAY 'REWRITING ERROR, K853-STAT   '                   
                      WS-K853-STAT                                              
                      MOVE WS-K853-STAT    TO RETURN-CODE                       
                      PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                   
                    END-REWRITE                                                 
                                                                                
                    ADD 1 TO WS-K853-REWRITE-CTR                                
                  ELSE                                                          
                    MOVE 'N' TO WS-K853-FOUND                                   
                    ADD 1                      TO WS-K853-NOT-FND               
                  END-IF                                                        
               ELSE                                                             
                  IF WS-PROCESS = 'AI'                                          
                    MOVE K010-DEB-REF-NO   TO K853-DEB-REF-NO                   
                    MOVE 'D'               TO K853-CDE-DEBAR                    
                    MOVE WS-CURR-DATE      TO K853-DTE-UPDATE                   
                    MOVE 'BP13C54E'        TO K853-NUM-USERID                   
                    REWRITE BP13K853-REC                                        
                        INVALID KEY                                             
                        DISPLAY 'REWRITING ERROR, K853-STAT   '                 
                        WS-K853-STAT                                            
                        MOVE WS-K853-STAT        TO RETURN-CODE                 
                        PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                 
                    END-REWRITE                                                 
                    ADD 1                        TO WS-K853-REWRITE-CTR         
                  END-IF                                                        
               END-IF                                                           
             WHEN 23                                                            
               IF WS-PROCESS = 'AI'                                             
                MOVE SPACES             TO BP13K853-REC                         
                INITIALIZE                 BP13K853-REC                         
                MOVE WS-UIN-KEY         TO K853-NUM-NRIC                        
                MOVE KB78-NUM-REGN      TO K853-NUM-REGN                        
                MOVE K010-DEB-REF-NO    TO K853-DEB-REF-NO                      
                MOVE 'D'                TO K853-CDE-DEBAR                       
                MOVE WS-NME-PARENT      TO K853-NME-OCCP                        
                MOVE K800-NUM-NEW-TOWN  TO K853-NUM-NEW-TOWN                    
                MOVE K800-NUM-FLAT-TYPE TO K853-NUM-FLAT-TYPE                   
                MOVE K800-DTE-BALLOT    TO K853-DTE-BALLOT                      
                MOVE WS-MARSTAT         TO K853-NUM-OCCP-MT                     
                MOVE WS-CURR-DATE       TO K853-DTE-UPDATE                      
                MOVE 'BP13C54E'         TO K853-NUM-USERID                      
                WRITE BP13K853-REC                                              
                      INVALID KEY                                               
                      DISPLAY 'WRITING ERROR, K853-STAT   '                     
                      WS-K853-STAT                                              
                      MOVE WS-K853-STAT        TO RETURN-CODE                   
                      PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                   
                END-WRITE                                                       
                ADD 1                          TO WS-K853-WRITE-CTR             
               ELSE                                                             
                  IF WS-PROCESS = 'RI'                                          
                    ADD 1                      TO WS-K853-NOT-FND               
                    MOVE 'N' TO WS-K853-FOUND                                   
                    DISPLAY 'NO RECORD FOUND ON BP13K853..' WS-UIN-KEY          
                      '/' K853-NUM-REGN                                         
                  END-IF                                                        
               END-IF                                                           
             WHEN OTHER                                                         
              DISPLAY 'PROCESS ERROR, K853-STATUS ' WS-K853-STAT                
              MOVE WS-K853-STAT        TO   RETURN-CODE                         
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       7600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       9999-CLOSE-FILES.                                                        
      ****************************                                              
           DISPLAY ' '.                                                         
           DISPLAY '---- BP13C54E CONTROL LIST ----'.                           
           IF WS-PROCESS = 'RI'                                                 
           DISPLAY ' TOTAL FA40 READ RI      : ' WS-FA40-RI-CTR                 
           DISPLAY ' TOTAL K800 FOUND        : ' WS-K800-VALID-RI               
           DISPLAY ' TOTAL K800 BYPASS RI    : ' WS-K800-BYPASS-RI              
           DISPLAY ' TOTAL K800 NOT FND      : ' WS-K800-NOT-FND                
           DISPLAY ' TOTAL K800 NOT MATCH    : ' WS-K800-SCH-NOT-MATCH          
           DISPLAY ' TOTAL KB78 FOUND        : ' WS-KB78-READ-CTR               
           DISPLAY ' TOTAL KB78 NOT FND      : ' WS-KB78-NOT-FND                
           DISPLAY ' TOTAL NRIC PROCESSED    : ' WS-NRIC-PROCESSED              
           DISPLAY ' TOTAL K853 REWRITE      : ' WS-K853-REWRITE-CTR            
           DISPLAY ' TOTAL K853 BYPASS       : ' WS-K853-NOT-FND                
           DISPLAY ' TOTAL K010 FOUND(NRIC)  : ' WS-K010-READ-CTR               
           DISPLAY ' TOTAL K010 NOT FND(NRIC): ' WS-K010-NOT-FND                
           DISPLAY ' TOTAL F010 WRITE        : ' WS-F010-WRITE-CTR              
           ELSE IF WS-PROCESS = 'AI'                                            
           DISPLAY ' TOTAL FA40 READ AI      : ' WS-FA40-AI-CTR                 
           DISPLAY ' TOTAL K800 FOUND        : ' WS-K800-READ-CTR-AI            
           DISPLAY ' TOTAL K800 NOT FND      : ' WS-K800-NOT-FND                
           DISPLAY ' TOTAL K800 NOT MATCH    : ' WS-K800-SCH-NOT-MATCH          
           DISPLAY ' TOTAL KB78 FOUND        : ' WS-KB78-READ-CTR               
           DISPLAY ' TOTAL KB78 NOT FND      : ' WS-KB78-NOT-FND                
           DISPLAY ' TOTAL NRIC PROCESSED    : ' WS-NRIC-PROCESSED              
           DISPLAY ' TOTAL K010 NOT FND(NRIC): ' WS-K010-NOT-FND                
           DISPLAY ' TOTAL K010 BYPASS(NRIC) : ' WS-K010-BYPASS                 
           DISPLAY ' TOTAL K853 WRITE        : ' WS-K853-WRITE-CTR              
           DISPLAY ' TOTAL K853 REWRITE      : ' WS-K853-REWRITE-CTR            
           DISPLAY ' TOTAL F010 WRITE        : ' WS-F010-WRITE-CTR              
           END-IF.                                                              
                                                                                
           CLOSE  BP13FA40                                                      
                  BP13K800                                                      
                  BP13KB78                                                      
                  BP04K010                                                      
                  BP13K665                                                      
                  BP13K820                                                      
                  BP13K853                                                      
                  BP04F010.                                                     
                                                                                
           IF WS-K800-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN CLOSING BP13K800 STATUS - '                     
                              WS-K800-STAT                                      
              MOVE WS-K800-STAT              TO RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K820-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN CLOSING BP13K820 STATUS - '                     
                              WS-K820-STAT                                      
              MOVE WS-K820-STAT              TO RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-KB78-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN CLOSING BP13KB78 STATUS - '                     
                              WS-KB78-STAT                                      
              MOVE WS-KB78-STAT              TO RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K010-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN CLOSING BP04K010 STATUS - '                     
                              WS-K010-STAT                                      
              MOVE WS-K010-STAT              TO RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K665-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN CLOSING BP13K665 STATUS - '                     
                              WS-K665-STAT                                      
              MOVE WS-K665-STAT              TO RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K853-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN CLOSING BP13K853 STATUS - '                     
                              WS-K853-STAT                                      
              MOVE WS-K853-STAT              TO RETURN-CODE                     
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
