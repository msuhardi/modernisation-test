       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH01.                                                 
      *AUTHOR.        MSD1.                                                     
      *DATE-WRITTEN.  10/06/2008.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: E-APPLICATION ALLOW APPLICANT TO RETRIEVE PAST *            
      *             APPLICATION.                                   *            
      *             THIS PROGRAM WILL MATCH CURRENT APPLICATION    *            
      *             IN BP13K730/BP13K820 WITH PAST RECORD BP13KD00 *            
      *             IF MATCH, CHECK PERSONAL PARTICULAR, COFN.     *            
      *             TAG CHANGES IN BP13KB00 SALES PORTAL FILE,     *            
      *             THIS INDICATOR WILL HELP SOU TO PERFORM        *            
      *             PRE-E CHECK.                                   *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133354  MSD1  20080610  NEW PGM                          *            
      * BP139280  EAA2  20231116  EXPAND BP13F757 FROM 3000 TO 4000*            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F757  ASSIGN        TO BP13F757.                          
                                                                                
           SELECT BP13KH00  ASSIGN        TO BP13KH00                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS SEQUENTIAL                         
                            RECORD KEY    IS KH00-KEY-FLD                       
                            FILE STATUS   IS BP13KH00-STATUS.                   
                                                                                
           SELECT BP13K825  ASSIGN        TO BP13K825                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS SEQUENTIAL                         
                            RECORD KEY    IS K825-KEY-FLD                       
                            FILE STATUS   IS BP13K825-STATUS.                   
                                                                                
           SELECT BP13K730  ASSIGN        TO BP13K730                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K730-KEY-NEW                       
                            FILE STATUS   IS BP13K730-STATUS.                   
                                                                                
           SELECT BP13KB00  ASSIGN        TO BP13KB00                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS KB00-KEY-FLD                       
                            FILE STATUS   IS BP13KB00-STATUS.                   
                                                                                
           SELECT BP13K060  ASSIGN  TO  BP13K060                                
                  ACCESS       IS RANDOM                                        
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY   IS K060-KEY-FLD                                  
                  FILE STATUS  IS BP13K060-STATUS.                              
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F757                                                            
            RECORD CONTAINS 4000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F757.                                                           
                                                                                
       FD  BP13KH00                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13KH00.                                                           
                                                                                
       FD  BP13K825                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K825.                                                           
                                                                                
       FD  BP13K730                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K730.                                                           
                                                                                
       FD  BP13KB00                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13KB00.                                                           
                                                                                
       FD  BP13K060                                                             
           RECORD CONTAINS 25 CHARACTERS.                                       
       COPY BP13K060.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-FILE-STATUS.                                                       
          05 BP13KH00-STATUS             PIC 9(02)    VALUE ZEROES.             
          05 BP13K825-STATUS             PIC 9(02)    VALUE ZEROES.             
          05 BP13K730-STATUS             PIC 9(02)    VALUE ZEROES.             
          05 BP13KB00-STATUS             PIC 9(02)    VALUE ZEROES.             
          05 BP13K060-STATUS             PIC 9(02)    VALUE ZEROES.             
       01 WS-COUNTERS.                                                          
          05 WS-SUB                      PIC 9(02)    VALUE ZEROES.             
          05 WS-F757-READ                PIC 9(08)    VALUE ZEROES.             
          05 WS-NEW-CNT                  PIC 9(08)    VALUE ZEROES.             
          05 WS-MATCH                    PIC 9(08)    VALUE ZEROES.             
          05 WS-REPEAT-CNT               PIC 9(08)    VALUE ZEROES.             
          05 WS-COFN-REC-Y               PIC 9(08)    VALUE ZEROES.             
          05 WS-COFN-REC-N               PIC 9(08)    VALUE ZEROES.             
          05 WS-COFN-REC-SPACE           PIC 9(08)    VALUE ZEROES.             
          05 WS-CHG-REC-Y                PIC 9(08)    VALUE ZEROES.             
          05 WS-CHG-REC-N                PIC 9(08)    VALUE ZEROES.             
          05 WS-CHG-REC-SPACE            PIC 9(08)    VALUE ZEROES.             
          05 WS-KB00-NTFND               PIC 9(08)    VALUE ZEROES.             
          05 WS-K825-NTFND               PIC 9(08)    VALUE ZEROES.             
          05 WS-KH00-NTFND               PIC 9(08)    VALUE ZEROES.             
          05 WS-KB00-REWRITE             PIC 9(08)    VALUE ZEROES.             
       01 WS-VARIABLES.                                                         
          05 WS-NUM-REPEAT               PIC X(01)    VALUE SPACES.             
          05 WS-NUM-COFN                 PIC X(01)    VALUE SPACES.             
          05 WS-NUM-OCCP-CHG             PIC X(01)    VALUE SPACES.             
          05 WS-K825-NUM-ROLE            PIC X(05)    VALUE SPACES.             
       01 WS-SWITCHES.                                                          
          05 WS-KH00-EOF                 PIC X(01)    VALUE 'N'.                
          05 WS-K825-EOF                 PIC X(01)    VALUE 'N'.                
          05 WS-K825-MATCH               PIC X(01)    VALUE SPACES.             
          05 WS-KH00-MATCH               PIC X(01)    VALUE SPACES.             
       01 WS-TABLES.                                                            
          05 WS-KH00-LIST.                                                      
             10 WS-KH00-NRIC             OCCURS 10 TIMES.                       
                15 WS-KH00-REC           PIC X(500)   VALUE SPACES.             
          05 WS-K825-LIST.                                                      
             10 WS-K825-NRIC             OCCURS 10 TIMES.                       
                15 WS-K825-REC           PIC X(200)   VALUE SPACES.             
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F757  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
              UNTIL F757-NUM-REGN = HIGH-VALUES.                                
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F757                                                 
                       BP13KH00                                                 
                       BP13K825                                                 
                       BP13K730                                                 
                       BP13K060                                                 
                I-O    BP13KB00.                                                
                                                                                
           IF BP13KH00-STATUS  NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING - BP13KH00 : ' BP13KH00-STATUS             
              MOVE     BP13KH00-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K825-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K825 : ' BP13K825-STATUS             
              MOVE     BP13K825-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K730-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K730 : ' BP13K730-STATUS             
              MOVE     BP13K730-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13KB00-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13KB00 : ' BP13KB00-STATUS             
              MOVE     BP13KB00-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K060-STATUS     NOT =  00 AND 97                              
              DISPLAY 'ERROR OPENING - BP13K060 : ' BP13K060-STATUS             
              MOVE  BP13K060-STATUS         TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F757.                                                      
      *-------------------------------------------------------------            
           READ BP13F757                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F757-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1    TO WS-F757-READ                                     
           END-READ.                                                            
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
           MOVE 'N'                     TO WS-NUM-REPEAT.                       
                                                                                
           MOVE SPACES                  TO WS-NUM-COFN                          
                                           WS-NUM-OCCP-CHG.                     
                                                                                
           IF F757-NUM-REGN-RETRIEVE NOT = SPACES AND LOW-VALUES                
              MOVE 'N'                     TO WS-KH00-MATCH                     
                                              WS-K825-MATCH                     
              PERFORM VARYING WS-SUB FROM 01 BY 01 UNTIL WS-SUB > 10            
                MOVE SPACES TO WS-KH00-REC(WS-SUB) WS-K825-REC(WS-SUB)          
              END-PERFORM                                                       
                                                                                
              PERFORM 4100-STORE-BP13K825  THRU 4100-EXIT                       
              PERFORM 4300-STORE-BP13KH00  THRU 4300-EXIT                       
                                                                                
              IF WS-KH00-MATCH = 'Y' AND  WS-K825-MATCH = 'Y'                   
                 MOVE 'R'                  TO WS-NUM-REPEAT                     
                 ADD 1                     TO WS-MATCH                          
                                              WS-REPEAT-CNT                     
                                                                                
                 MOVE 01                   TO WS-SUB                            
                 PERFORM 4500-READ-TABLE THRU 4500-EXIT                         
                                                                                
                 MOVE 'N'                  TO WS-NUM-COFN                       
                                                WS-NUM-OCCP-CHG                 
                                                                                
                 PERFORM 5000-MATCH-RECORD THRU 5000-EXIT                       
                    UNTIL KH00-NUM-REGN = HIGH-VALUES                           
                      AND K825-NUM-REGN = HIGH-VALUES                           
              ELSE                                                              
                 ADD  01                  TO WS-NEW-CNT                         
              END-IF                                                            
           ELSE                                                                 
              ADD  01                      TO WS-NEW-CNT                        
           END-IF.                                                              
                                                                                
           PERFORM 6000-UPDATE-BP13KB00     THRU 6000-EXIT.                     
                                                                                
           IF WS-NUM-COFN = 'Y'                                                 
              ADD 1 TO WS-COFN-REC-Y                                            
           ELSE                                                                 
              IF WS-NUM-COFN = 'N'                                              
                 ADD 1 TO WS-COFN-REC-N                                         
              ELSE                                                              
                 ADD 1 TO WS-COFN-REC-SPACE                                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-NUM-OCCP-CHG = 'Y'                                             
              ADD 1 TO WS-CHG-REC-Y                                             
           ELSE                                                                 
              IF WS-NUM-OCCP-CHG = 'N'                                          
                 ADD 1 TO WS-CHG-REC-N                                          
              ELSE                                                              
                 ADD 1 TO WS-CHG-REC-SPACE                                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F757       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-STORE-BP13K825.                                                     
      *-------------------------------------------------------------            
           MOVE 'N'                        TO WS-K825-EOF.                      
           MOVE 00                         TO WS-SUB.                           
                                                                                
           MOVE SPACES                     TO BP13K825-REC.                     
           INITIALIZE                         BP13K825-REC.                     
                                                                                
           MOVE F757-NUM-REGN              TO K825-NUM-REGN.                    
           MOVE SPACES                     TO K825-NUM-NRIC.                    
                                                                                
           START BP13K825 KEY >= K825-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K825-STATUS                                             
           WHEN 00                                                              
              PERFORM 4200-READNXT-BP13K825 THRU 4200-EXIT                      
                 UNTIL K825-NUM-REGN NOT = F757-NUM-REGN                        
                    OR WS-KH00-EOF       = 'Y'                                  
           WHEN 10                                                              
           WHEN 23                                                              
              DISPLAY 'BP13K825 RECORD NOT FOUND  = ' K825-KEY-FLD              
           WHEN OTHER                                                           
              DISPLAY 'ERROR STARTBR - BP13K825 : ' BP13K825-STATUS             
                      ', K825-KEY-FLD : ' K825-KEY-FLD                          
              MOVE     BP13K825-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-EVALUATE.                                                        
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4200-READNXT-BP13K825.                                                   
      *-------------------------------------------------------------            
           READ BP13K825 NEXT RECORD.                                           
                                                                                
           IF K825-AMT-INCOME NOT NUMERIC                                       
              MOVE ZEROES             TO K825-AMT-INCOME                        
           END-IF.                                                              
           IF K825-AMT-INCOME-TP NOT NUMERIC                                    
               MOVE ZEROES             TO K825-AMT-INCOME-TP                    
           END-IF.                                                              
                                                                                
           EVALUATE BP13K825-STATUS                                             
           WHEN 00                                                              
              IF K825-NUM-REGN = F757-NUM-REGN                                  
                 MOVE 'Y'             TO WS-K825-MATCH                          
                 PERFORM 7000-CONVERT-CODES THRU 7000-EXIT                      
                 ADD 1                TO WS-SUB                                 
                 MOVE BP13K825-REC    TO WS-K825-REC(WS-SUB)                    
              END-IF                                                            
           WHEN 10                                                              
           WHEN 23                                                              
              ADD 1                   TO WS-K825-NTFND                          
              MOVE    'Y'             TO WS-K825-EOF                            
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING - BP13K825 : ' BP13K825-STATUS             
                      ', K825-KEY-FLD : ' K825-KEY-FLD                          
              PERFORM  9000-CLOSE-ROUTINE   THRU 9000-EXIT                      
           END-EVALUATE.                                                        
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4300-STORE-BP13KH00.                                                     
      *-------------------------------------------------------------            
           MOVE 'N'                        TO WS-KH00-EOF.                      
           MOVE 00                         TO WS-SUB.                           
                                                                                
           MOVE SPACES                     TO BP13KH00-REC.                     
           INITIALIZE                         BP13KH00-REC.                     
                                                                                
           MOVE F757-NUM-REGN-RETRIEVE     TO KH00-NUM-REGN.                    
           MOVE SPACES                     TO KH00-NUM-NRIC.                    
                                                                                
           START BP13KH00 KEY >= KH00-KEY-FLD.                                  
                                                                                
           EVALUATE BP13KH00-STATUS                                             
           WHEN 00                                                              
              PERFORM 4400-READNXT-BP13KH00 THRU 4400-EXIT                      
                 UNTIL KH00-NUM-REGN NOT = F757-NUM-REGN-RETRIEVE               
                    OR WS-KH00-EOF       = 'Y'                                  
           WHEN 10                                                              
           WHEN 23                                                              
              DISPLAY 'BP13KH00 RECORD NOT FOUND  = ' KH00-KEY-FLD              
           WHEN OTHER                                                           
              DISPLAY 'ERROR STARTBR - BP13KH00 : ' BP13KH00-STATUS             
                      ', KH00-KEY-FLD : ' KH00-KEY-FLD                          
              MOVE     BP13KH00-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-EVALUATE.                                                        
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4400-READNXT-BP13KH00.                                                   
      *-------------------------------------------------------------            
           READ BP13KH00 NEXT RECORD.                                           
                                                                                
           EVALUATE BP13KH00-STATUS                                             
           WHEN 00                                                              
              IF KH00-NUM-REGN = F757-NUM-REGN-RETRIEVE                         
                 MOVE 'Y'                   TO WS-KH00-MATCH                    
                 ADD 1                      TO WS-SUB                           
                 IF KH00-AMT-INCOME NOT NUMERIC                                 
                    MOVE ZEROES             TO KH00-AMT-INCOME                  
                 END-IF                                                         
                 IF KH00-AMT-INCOME-TP NOT NUMERIC                              
                    MOVE ZEROES             TO KH00-AMT-INCOME-TP               
                 END-IF                                                         
                 MOVE BP13KH00-REC          TO WS-KH00-REC(WS-SUB)              
              END-IF                                                            
           WHEN 10                                                              
           WHEN 23                                                              
              ADD 1                         TO WS-KH00-NTFND                    
              MOVE    'Y'                   TO WS-KH00-EOF                      
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING - BP13KH00 : ' BP13KH00-STATUS             
                      ', KH00-KEY-FLD : ' KH00-KEY-FLD                          
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-EVALUATE.                                                        
       4400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4500-READ-TABLE.                                                         
      *-------------------------------------------------------------            
           MOVE WS-KH00-REC (WS-SUB)        TO BP13KH00-REC.                    
           IF  KH00-NUM-REGN = SPACES OR LOW-VALUES                             
               MOVE HIGH-VALUES             TO KH00-NUM-REGN                    
           END-IF.                                                              
                                                                                
           MOVE WS-K825-REC (WS-SUB)        TO BP13K825-REC.                    
           IF  K825-NUM-REGN = SPACES OR LOW-VALUES                             
               MOVE HIGH-VALUES             TO K825-NUM-REGN                    
           END-IF.                                                              
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       5000-MATCH-RECORD.                                                       
      *-------------------------------------------------------------            
           PERFORM 5100-READ-BP13K730 THRU 5100-EXIT                            
           IF (WS-K825-NUM-ROLE = 'MNAPP' OR 'COAPP') AND                       
              (KH00-NUM-NRIC NOT = K825-NUM-NRIC)                               
              MOVE 'Y'                  TO WS-NUM-COFN                          
                                           WS-NUM-OCCP-CHG                      
           ELSE                                                                 
              IF (KH00-NME-OCCP           NOT = K825-NME-OCCP)     OR           
                 (KH00-NUM-ETHNIC         NOT = K825-NUM-ETHNIC)   OR           
                 (KH00-NUM-SEX            NOT = K825-NUM-SEX)      OR           
                 (KH00-NUM-MARITAL-STATUS NOT =                                 
                                          K825-NUM-MARITAL-STATUS) OR           
                 (KH00-DTE-BIRTH          NOT = K825-DTE-BIRTH)    OR           
                 (KH00-NUM-CITIZENSHIP    NOT =                                 
                                          K825-NUM-CITIZENSHIP)    OR           
                 (KH00-NUM-RELATIONSHIP   NOT =                                 
                                          K825-NUM-RELATIONSHIP)   OR           
                 (KH00-AMT-INCOME         NOT =  K825-AMT-INCOME)               
                   MOVE 'Y'             TO WS-NUM-OCCP-CHG                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD   01                     TO   WS-SUB.                            
           PERFORM 4500-READ-TABLE    THRU 4500-EXIT.                           
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       5100-READ-BP13K730.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                     TO BP13K730-REC.                     
           INITIALIZE                         BP13K730-REC.                     
                                                                                
           MOVE K825-NUM-REGN              TO K730-NUM-REGN.                    
                                                                                
           READ BP13K730.                                                       
                                                                                
           IF BP13K730-STATUS = 00                                              
              EVALUATE K825-NUM-NRIC                                            
                 WHEN K730-NUM-NRIC1                                            
                      MOVE     'MNAPP'     TO WS-K825-NUM-ROLE                  
                 WHEN K730-NUM-NRIC2                                            
                 WHEN K730-NUM-NRIC3                                            
                 WHEN K730-NUM-NRIC4                                            
                      MOVE      'COAPP'    TO WS-K825-NUM-ROLE                  
                 WHEN OTHER                                                     
                      MOVE      'OCC  '    TO WS-K825-NUM-ROLE                  
                 END-EVALUATE                                                   
                                                                                
           ELSE                                                                 
              IF BP13K730-STATUS = 23                                           
                 DISPLAY 'BP13K730 RECORD NOT FOUND'                            
                 MOVE SPACES               TO WS-K825-NUM-ROLE                  
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K730,STATUS ' BP13K730-STATUS          
                 MOVE BP13K730-STATUS        TO RETURN-CODE                     
                 PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-UPDATE-BP13KB00.                                                    
      *-------------------------------------------------------------            
           MOVE F757-NUM-REGN                  TO KB00-NUM-REGN.                
           READ BP13KB00.                                                       
           IF BP13KB00-STATUS = 00                                              
              PERFORM 6100-REWRITE-BP13KB00 THRU 6100-EXIT                      
           ELSE                                                                 
              IF BP13KB00-STATUS = 23                                           
                 ADD  1                        TO WS-KB00-NTFND                 
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13KB00,STATUS ' BP13KB00-STATUS          
                 MOVE BP13KB00-STATUS          TO RETURN-CODE                   
                 PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6100-REWRITE-BP13KB00.                                                   
      *-------------------------------------------------------------            
           MOVE WS-NUM-REPEAT               TO KB00-NUM-REPEAT                  
           MOVE WS-NUM-COFN                 TO KB00-NUM-COFN                    
           MOVE WS-NUM-OCCP-CHG             TO KB00-NUM-OCCP-CHG                
                                                                                
           REWRITE BP13KB00-REC.                                                
                                                                                
           IF BP13KB00-STATUS = 00                                              
              ADD  1                        TO WS-KB00-REWRITE                  
           ELSE                                                                 
              DISPLAY 'ERROR REWRITE BP13KB00,STATUS ' BP13KB00-STATUS          
              MOVE BP13KB00-STATUS          TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       7000-CONVERT-CODES.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES          TO K060-KEY-FLD.                                
           MOVE 42              TO K060-SERIAL-NO.                              
           MOVE K825-NUM-ETHNIC TO K060-CODE.                                   
           PERFORM 7100-READ-BP13K060 THRU 7100-EXIT.                           
           IF BP13K060-STATUS = 00                                              
              MOVE K060-DESC1(1:1) TO K825-NUM-ETHNIC                           
           END-IF.                                                              
                                                                                
           MOVE SPACES          TO K060-KEY-FLD.                                
           MOVE 43              TO K060-SERIAL-NO.                              
           MOVE K825-NUM-SEX    TO K060-CODE.                                   
           PERFORM 7100-READ-BP13K060 THRU 7100-EXIT.                           
           IF BP13K060-STATUS = 00                                              
              MOVE K060-DESC1(1:1) TO K825-NUM-SEX                              
           END-IF.                                                              
                                                                                
           MOVE SPACES          TO K060-KEY-FLD.                                
           MOVE 44              TO K060-SERIAL-NO.                              
           MOVE K825-NUM-MARITAL-STATUS TO K060-CODE.                           
           PERFORM 7100-READ-BP13K060 THRU 7100-EXIT.                           
           IF BP13K060-STATUS = 00                                              
              MOVE K060-DESC1(1:1) TO K825-NUM-MARITAL-STATUS                   
           END-IF.                                                              
       7000-EXIT.                                                               
           EXIT.                                                                
      *-------------------------------------------------------------            
       7100-READ-BP13K060.                                                      
      *-------------------------------------------------------------            
           READ BP13K060.                                                       
                                                                                
           IF BP13K060-STATUS = 00                                              
              CONTINUE                                                          
           ELSE                                                                 
              IF BP13K060-STATUS = 23                                           
                 DISPLAY 'RECORD NOT FOUND IN BP13K060, KEY: '                  
                         K060-KEY-FLD ' ' K825-NUM-REGN                         
              ELSE                                                              
                 DISPLAY 'ERROR WITH READING BP13K060, STATUS '                 
                          BP13K060-STATUS                                       
                         ', KEY: ' K060-KEY-FLD                                 
              END-IF                                                            
           END-IF.                                                              
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13CH01 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ FROM BP13F757  : ' WS-F757-READ.            
           DISPLAY 'NO OF RECS NOT FND IN BP13K825 : ' WS-K825-NTFND.           
           DISPLAY 'NO OF RECS NOT FND IN BP13KH00 : ' WS-KH00-NTFND.           
           DISPLAY 'NO OF RECS FD IN KH00/K825     : ' WS-MATCH.                
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECS NEW CASES           : ' WS-NEW-CNT.              
           DISPLAY 'NO OF RECS REPEATED            : ' WS-REPEAT-CNT.           
           DISPLAY 'NO OF RECS COFN=Y BY CASE      : ' WS-COFN-REC-Y.           
           DISPLAY 'NO OF RECS COFN=N BY CASE      : ' WS-COFN-REC-N.           
           DISPLAY 'NO OF RECS CHG PART=Y BY CASE  : ' WS-CHG-REC-Y.            
           DISPLAY 'NO OF RECS CHG PART=N BY CASE  : ' WS-CHG-REC-N.            
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECS COFN=SPACES BY CASE : '                          
                                                   WS-COFN-REC-SPACE.           
           DISPLAY 'NO OF RECS CHG PART=SPACE BY CASE:'                         
                                                   WS-CHG-REC-SPACE.            
           DISPLAY 'NO OF RECS UPDATED IN BP13KB00 : ' WS-KB00-REWRITE.         
           DISPLAY 'NO OF RECS NOT FND IN BP13KB00 : ' WS-KB00-NTFND.           
                                                                                
           CLOSE BP13F757                                                       
                 BP13KH00                                                       
                 BP13K825                                                       
                 BP13K730                                                       
                 BP13KB00                                                       
                 BP13K060.                                                      
                                                                                
           IF BP13KH00-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13KH00 : ' BP13KH00-STATUS             
              MOVE BP13KH00-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13K825-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K825 : ' BP13K825-STATUS             
              MOVE BP13K825-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13K730-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K730 : ' BP13K730-STATUS             
              MOVE BP13K730-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13KB00-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13KB00 : ' BP13KB00-STATUS             
              MOVE BP13KB00-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13K060-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K060 : ' BP13K060-STATUS             
              MOVE BP13K060-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
