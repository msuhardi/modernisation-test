      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C951.                                                 
       AUTHOR.        ABH.                                                      
      *DATE-WRITTEN.  26/06/06.                                                 
                                                                                
      * ============================================================ *          
      *                SYSTEM OF COMMITMENT  (BP13)                  *          
      * ============================================================ *          
      *    OBJECTIVE                                                 *          
      *      1.  THIS SUBROUTINE COMPUTES THE LOAN EXPIRY DATE AND   *          
      *          BALANCE REPAY PERIOD.                               *          
      *                                                              *          
      * ============================================================ *          
      * CHG-NO   BY   DATE    DESCRIPTION                            *          
      * -------- ---  ------  -----------                            *          
      * BP132772 ABH  280606  NEW PROGRAM                            *          
      * BP133095 BA3  031007  CHANGE NUMBER OF DECIMAL PLACES OF     *          
      *                        THE REPAYMENT PERIOD FROM TWO TO SIX. *          
      * ============================================================ *          
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
       FILE SECTION.                                                            
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-COMP-VARIABLES.                                                   
           05  WS-MONTHS-LOAN-COMM          PIC 9(5)   VALUE ZERO.              
           05  WS-DTE-LOAN-COMM             PIC 9(8).                           
           05  WS-DTE-LOAN-EXP-INT          PIC 9(8).                           
           05  WS-DTE-LOAN-EXP-NEW          PIC 9(8).                           
           05  WS-NUM-EFFECTIVE             PIC 9(5).                           
           05  WS-NUM-EXPIRY                PIC 9(5).                           
           05  WS-NUM-DAY-DIFF              PIC 9(3).                           
           05  WS-BAL-REPAY-PERIOD          PIC 9(3).9(6).                      
                                                                                
       01  WS-DTE-EFF-CCYYMMDD.                                                 
           05  WS-DTE-EFF-CCYY              PIC 9(4).                           
           05  WS-DTE-EFF-MM                PIC 9(2).                           
           05  WS-DTE-EFF-DD                PIC 9(2).                           
                                                                                
       01  WS-DTE-EXPIRY-CCYYMMDD.                                              
           05  WS-DTE-EXPIRY-CCYY           PIC 9(4).                           
           05  WS-DTE-EXPIRY-MM             PIC 9(2).                           
           05  WS-DTE-EXPIRY-DD             PIC 9(2).                           
                                                                                
       LINKAGE SECTION.                                                         
       01  C951-FIELD.                                                          
           05  C951-EFFECTIVE-DATE         PIC X(08).                           
           05  C951-EXPIRY-DATE-IN         PIC X(08).                           
           05  C951-AMT-LOAN-BAL           PIC 9(7)V99.                         
           05  C951-NUM-LOAN-REPYMT-PERIOD PIC 99.                              
           05  C951-DTE-LOAN-EXPIRY-CYMD   PIC X(08).                           
           05  C951-BAL-REPAY-PERIOD       PIC 9(2)V9(6).                       
                                                                                
                                                                                
      *-------------------------------------*                                   
       PROCEDURE DIVISION USING C951-FIELD.                                     
      *-------------------------------------*                                   
                                                                                
      ******************************************************************        
       1000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM  2000-MOVE-PARAMETERS    THRU 2000-EXIT.                     
           PERFORM  3000-PROCESS-EXPIRY-DTE THRU 3000-EXIT.                     
           PERFORM  9000-GOBACK-ROUTINE     THRU 9000-EXIT.                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-MOVE-PARAMETERS.                                                    
      ******************************************************************        
                                                                                
            MOVE C951-EFFECTIVE-DATE   TO WS-DTE-EFF-CCYYMMDD.                  
            MOVE C951-EXPIRY-DATE-IN   TO WS-DTE-EXPIRY-CCYYMMDD.               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-EXPIRY-DTE.                                                 
      ******************************************************************        
                                                                                
           IF C951-AMT-LOAN-BAL NOT NUMERIC                                     
              MOVE 0 TO C951-AMT-LOAN-BAL.                                      
                                                                                
           IF C951-AMT-LOAN-BAL = 0                                             
      *_____  COMPUTE FOR LOAN EXPIRY DATE                                      
                                                                                
              COMPUTE WS-MONTHS-LOAN-COMM = WS-DTE-EFF-CCYY * 12 +              
                                            WS-DTE-EFF-MM                       
                                                                                
              COMPUTE WS-DTE-EFF-CCYY = WS-DTE-EFF-CCYY +                       
                                    C951-NUM-LOAN-REPYMT-PERIOD                 
                                                                                
              MOVE WS-DTE-EFF-CCYYMMDD TO WS-DTE-LOAN-COMM                      
                                                                                
              COMPUTE WS-DTE-LOAN-EXP-INT =                                     
                         FUNCTION INTEGER-OF-DATE(WS-DTE-LOAN-COMM)             
              SUBTRACT 1 FROM WS-DTE-LOAN-EXP-INT                               
                                                                                
              COMPUTE WS-DTE-LOAN-EXP-NEW =                                     
                      FUNCTION DATE-OF-INTEGER(WS-DTE-LOAN-EXP-INT)             
                                                                                
              MOVE WS-DTE-LOAN-EXP-NEW   TO WS-DTE-EFF-CCYYMMDD                 
              MOVE WS-DTE-EFF-CCYYMMDD   TO C951-DTE-LOAN-EXPIRY-CYMD           
           ELSE                                                                 
      *_____  COMPUTE FOR BALANCE REPAY PERIOD                                  
                                                                                
              COMPUTE WS-NUM-EFFECTIVE = (WS-DTE-EFF-CCYY * 12)                 
                                       +  WS-DTE-EFF-MM                         
                                                                                
              COMPUTE WS-NUM-EXPIRY    = (WS-DTE-EXPIRY-CCYY * 12)              
                                       +  WS-DTE-EXPIRY-MM + 1                  
                                                                                
              COMPUTE WS-NUM-DAY-DIFF = WS-NUM-EXPIRY - WS-NUM-EFFECTIVE        
                                                                                
              COMPUTE WS-BAL-REPAY-PERIOD ROUNDED = WS-NUM-DAY-DIFF / 12        
                                                                                
              MOVE WS-BAL-REPAY-PERIOD TO C951-BAL-REPAY-PERIOD                 
                                                                                
           END-IF.                                                              
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      ******************************************************************        
       9000-GOBACK-ROUTINE.                                                     
      ******************************************************************        
                                                                                
           GOBACK.                                                              
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
