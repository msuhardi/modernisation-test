       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C831.                                                 
      *AUTHOR.        YPH.                                                      
      *DATE-WRITTEN.  15/01/95.                                                 
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *    (ORIGINALLY FROM BP08)                              *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      PROGRAM TO OUTPUT INFO RECORDS ON:                *                
      *      (1) APPLN AND ADDRESS DETAIL FR SOC MASTER FILE   *                
      *      (2) HA1 & HA2 INFO FR SOC MASTER FILE             *                
      *      (3) OCCUPIERS INFO FR SOC OCCUPIER FILE           *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *      1  BP13F800 - SOC MASTER FILE                     *                
      *      2  BP13K820 - SOC OCCUPIER FILE                   *                
      *      3  BP13K060 - CODE TABLE                          *                
      *                                                        *                
      *     OUTPUT FILES:                                      *                
      *      1  BP08F550 - CFSSOC FILE                         *                
      *                                                        *                
      * CHG-NO  BY  ON     DESCRIPTION                         *                
      * ------- --- ------ -----------                         *                
      *BP130063 YPH 150195                                     *                
      *BP130418 SIA 230398 CATER FOR Y2000                     *                
      *BP130551 SRN 101198 DATE APPLN                          *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT P08F550      ASSIGN       TO P08F550.                         
                                                                                
           SELECT BP13F800     ASSIGN       TO BP13F800.                        
                                                                                
           SELECT BP13K820     ASSIGN       TO BP13K820                         
                               ACCESS MODE  IS DYNAMIC                          
                               ORGANIZATION IS INDEXED                          
                               RECORD KEY   IS K820-KEY-FLD                     
                               FILE STATUS  IS BP13K820-STATUS.                 
                                                                                
           SELECT BP13K830     ASSIGN       TO BP13K830                         
                               ACCESS MODE  IS DYNAMIC                          
                               ORGANIZATION IS INDEXED                          
                               RECORD KEY   IS K830-KEY-FLD                     
                               ALTERNATE RECORD KEY IS K830-NUM-REGN            
                               FILE STATUS  IS BP13K830-STATUS.                 
                                                                                
           SELECT BP13K060     ASSIGN       TO BP13K060                         
                               ACCESS MODE  IS DYNAMIC                          
                               ORGANIZATION IS INDEXED                          
                               RECORD KEY   IS K060-KEY-FLD                     
                               FILE STATUS  IS BP13K060-STATUS.                 
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *                                                                         
       COPY P08F550.                                                            
      *                                                                         
       FD   BP13F800                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORD    IS STANDARD                                         
            RECORDING MODE  IS F.                                               
       COPY BP13F800.                                                           
      *                                                                         
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
      *                                                                         
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
      *                                                                         
       FD   BP13K060                                                            
            RECORD CONTAINS 25 CHARACTERS.                                      
       COPY BP13K060.                                                           
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  WORK-AREA.                                                           
           05 BP13K820-STATUS          PIC 9(02) VALUE ZEROS.                   
           05 BP13K830-STATUS          PIC 9(02) VALUE ZEROS.                   
           05 BP13K060-STATUS          PIC 9(02) VALUE ZEROS.                   
           05 WS-CNT-F800-READ         PIC 9(08) VALUE ZEROS.                   
           05 WS-CNT-K820-READ         PIC 9(08) VALUE ZEROS.                   
           05 WS-CNT-K830-READ         PIC 9(08) VALUE ZEROS.                   
           05 WS-CNT-HEADER            PIC 9(08) VALUE ZEROS.                   
           05 WS-CNT-WRITE             PIC 9(08) VALUE ZEROS.                   
           05 WS-FOUND-SW              PIC 9(01) VALUE 0.                       
                                                                                
       01  WS-K060-KEY.                                                         
           05 WS-SERIAL-NO             PIC 9(02).                               
           05 WS-CDE-CODE              PIC X(02).                               
                                                                                
       01  WS-CUR-DATE.                                                         
           05 WS-YY                    PIC 9(04).                               
           05 WS-MM                    PIC 9(02).                               
           05 WS-DD                    PIC 9(02).                               
                                                                                
       01  WS-DTE-UPP-CHECK.                                                    
           05 WS-UPP-YYMMDD.                                                    
              10 WS-UPP-YYMM.                                                   
                 15 WS-UPP-YY          PIC X(04).                               
                 15 WS-UPP-MM          PIC X(02).                               
              10 WS-UPP-DD             PIC X(02).                               
                                                                                
       01  WS-DTE-LOW-CHECK.                                                    
           05 WS-LOW-YYMMDD.                                                    
              10 WS-LOW-YYMM.                                                   
                 15 WS-LOW-YY          PIC X(04).                               
                 15 WS-LOW-MM          PIC X(02).                               
              10 WS-LOW-DD             PIC X(02).                               
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *-----------------*                                                       
       0000-MAIN-ROUTINE.                                                       
      *-----------------*                                                       
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
                                                                                
           PERFORM 2000-READ-BP13F800     THRU 2000-EXIT.                       
                                                                                
           PERFORM 1050-CHECK-DATE-RANGE  THRU 1050-EXIT.                       
                                                                                
           PERFORM 3000-PROCESS-F800      THRU 3000-EXIT                        
             UNTIL F800-NUM-REGN = HIGH-VALUES.                                 
                                                                                
           PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT.                       
                                                                                
           STOP RUN.                                                            
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------*                                                     
       1000-INITIALIZATION.                                                     
      *-------------------*                                                     
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13K820                                                 
                       BP13K830                                                 
                       BP13K060                                                 
                OUTPUT P08F550.                                                 
                                                                                
           IF BP13K820-STATUS NOT = 0 AND 97                                    
              DISPLAY ' OPENING ERROR, BP13K820-STATUS '                        
                        BP13K820-STATUS                                         
              MOVE BP13K820-STATUS       TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT                                                 
           END-IF.                                                              
                                                                                
           IF BP13K830-STATUS NOT = 0 AND 97                                    
              DISPLAY ' OPENING ERROR, BP13K830-STATUS '                        
                        BP13K830-STATUS                                         
              MOVE BP13K830-STATUS       TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT                                                 
           END-IF.                                                              
                                                                                
           IF BP13K060-STATUS NOT = 0 AND 97                                    
              DISPLAY ' OPENING ERROR, BP13K060-STATUS  '                       
                        BP13K060-STATUS                                         
              MOVE BP13K060-STATUS       TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT                                                 
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       1050-CHECK-DATE-RANGE.                                                   
      *---------------------*                                                   
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
      *                                                                         
           DISPLAY '***  BP13C831  ***'.                                        
           DISPLAY 'CURRENT        DATE :  ' WS-CUR-DATE.                       
      *                                                                         
           MOVE   WS-YY         TO   WS-LOW-YY   WS-UPP-YY.                     
           MOVE   WS-MM         TO   WS-LOW-MM   WS-UPP-MM.                     
                                                                                
      ******************************************************************        
      * THE PROGRAM IS NOW TO RUN ONCE IN FIFTEEN DAYS. IF THE PROGRAM *        
      * IS RUN BEFORE 15TH OF THE MONTH THEN THE LOWER AND UPPER DATES *        
      * ARE 15TH AND 99 OF THE PREVIOUS MONTH. IF THE PROGRAM IS RUN   *        
      * AFTER 14TH THEN THE LOWER AND UPPER DATES ARE 1ST AND 14TH OF  *        
      * THE CURRENT MONTH.                                             *        
      ******************************************************************        
                                                                                
           MOVE '00'            TO   WS-LOW-DD.                                 
           MOVE '99'            TO   WS-UPP-DD.                                 
                                                                                
           IF WS-MM > 01                                                        
              SUBTRACT  1       FROM WS-MM                                      
              MOVE WS-MM        TO   WS-LOW-MM  WS-UPP-MM                       
              MOVE WS-YY        TO   WS-UPP-YY  WS-LOW-YY                       
           ELSE                                                                 
           IF WS-MM = 1                                                         
              MOVE     12       TO   WS-LOW-MM  WS-UPP-MM                       
              SUBTRACT 1        FROM WS-YY                                      
              MOVE WS-YY        TO   WS-UPP-YY  WS-LOW-YY                       
           END-IF.                                                              
                                                                                
           DISPLAY 'PRE-E RANGE OF DATE :  ' WS-DTE-LOW-CHECK                   
                                    '  TO  ' WS-DTE-UPP-CHECK.                  
           DISPLAY '------------------'.                                        
                                                                                
       1050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2000-READ-BP13F800.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13F800 NEXT AT END                                            
              MOVE HIGH-VALUES           TO F800-NUM-REGN                       
              GO TO 2000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD 1                         TO WS-CNT-F800-READ.                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-F800.                                                       
      *-----------------*                                                       
                                                                                
           IF (F800-DTE-REGN   > WS-DTE-UPP-CHECK) OR                           
              (F800-DTE-REGN   < WS-DTE-LOW-CHECK)                              
              GO TO 3000-READNEXT                                               
           END-IF.                                                              
                                                                                
           MOVE SPACES                   TO P08F550-HEADER-REC                  
                                            BP13K830-REC.                       
           ADD  1                        TO WS-CNT-HEADER.                      
                                                                                
           MOVE SPACES                   TO F550-NUM-REGNO.                     
           MOVE F800-NUM-REGN            TO F550-NUM-REGNO                      
                                            K830-NUM-REGN.                      
           MOVE '##'                     TO F550-RELA-COD.                      
           MOVE WS-CNT-HEADER            TO F550-NRIC.                          
           MOVE '#'                      TO F550-NRIC-1ST.                      
                                                                                
           PERFORM 3800-READ-K830                                               
              THRU 3800-EXIT.                                                   
                                                                                
           MOVE K830-NUM-MAIN-BLK        TO F550-BLK-NO.                        
           MOVE K830-NUM-FLAT            TO F550-HSE-NO.                        
           MOVE K830-NME-STREET          TO F550-STREET.                        
           MOVE K830-NUM-POSTAL          TO F550-PD.                            
                                                                                
           IF   F800-NUM-ZONE > SPACES                                          
                MOVE F800-NUM-ZONE       TO F550-NEWTOWN                        
           ELSE                                                                 
                MOVE F800-NUM-NEW-TOWN   TO F550-NEWTOWN                        
           END-IF.                                                              
                                                                                
           MOVE F800-NUM-FLAT-TYPE (1:1) TO F550-ROOM.                          
           MOVE '1'                      TO F550-DIFF-SCH.                      
           MOVE F800-NUM-ALLO-SCHEME     TO F550-ALLOC-SCH.                     
           MOVE F800-NUM-ELIG-SCHEME     TO F550-ELIG-SCH.                      
                                                                                
           MOVE F800-DTE-REGN            TO F550-APPLN-DATE                     
           ADD  1                        TO WS-CNT-WRITE.                       
           WRITE P08F550-HEADER-REC.                                            
                                                                                
           MOVE SPACES                   TO P08F550-DETAIL-REC                  
                                            BP13K820-REC.                       
           MOVE F800-NUM-NRIC1           TO F550-NUM-UINFIN.                    
           MOVE F800-NUM-NRIC1           TO K820-NUM-NRIC.                      
           MOVE SPACES                   TO F550-NUM-REGNO.                     
           MOVE F800-NUM-REGN            TO F550-NUM-REGNO                      
                                            K820-NUM-REGN.                      
                                                                                
                                                                                
           PERFORM 3600-READ-K820                                               
              THRU 3600-EXIT.                                                   
                                                                                
           MOVE K820-NUM-RELATIONSHIP    TO F550-RELA-COD.                      
           MOVE 'APPLICANT'              TO F550-RELA-DESC.             06210039
           MOVE K820-NME-OCCP            TO F550-NAME.                          
           PERFORM 3400-GET-DETAILS      THRU 3400-EXIT                         
           ADD  1                        TO WS-CNT-WRITE.                       
           WRITE P08F550-DETAIL-REC.                                            
                                                                                
           IF F800-NUM-NRIC2 (2:1) NOT = '#' AND SPACES AND LOW-VALUES          
              MOVE SPACES                TO P08F550-DETAIL-REC                  
                                            BP13K820-REC                        
                                            F550-NUM-REGNO                      
              MOVE F800-NUM-REGN         TO F550-NUM-REGNO                      
                                            K820-NUM-REGN                       
              MOVE F800-NUM-NRIC2        TO F550-NUM-UINFIN                     
              MOVE F800-NUM-NRIC2        TO K820-NUM-NRIC                       
                                                                                
              PERFORM 3600-READ-K820                                            
                 THRU 3600-EXIT                                                 
                                                                                
              MOVE K820-NUM-RELATIONSHIP TO F550-RELA-COD                       
              MOVE K820-NME-OCCP         TO F550-NAME                           
                                                                                
              MOVE K820-DTE-BIRTH        TO F550-BIRTH-DATE                     
                                                                                
              PERFORM 3300-GET-RELATION  THRU 3300-EXIT                         
              PERFORM 3400-GET-DETAILS   THRU 3400-EXIT                         
                                                                                
              ADD  1                     TO WS-CNT-WRITE                        
              WRITE P08F550-DETAIL-REC                                          
           END-IF.                                                              
                                                                                
           PERFORM 4000-START-BP13K820                                          
              THRU 4000-EXIT.                                                   
                                                                                
           IF WS-FOUND-SW = 0                                                   
              PERFORM 4500-WRITE-OCCUPIERS                                      
                 THRU 4500-EXIT                                                 
                UNTIL K820-NUM-REGN NOT = F800-NUM-REGN OR                      
                      K820-NUM-REGN = HIGH-VALUES                               
           END-IF.                                                              
                                                                                
      *-------------*                                                           
       3000-READNEXT.                                                           
      *-------------*                                                           
                                                                                
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------*                                                           
       3300-GET-RELATION.                                                       
      *-------------*                                                           
      *RETRIEVE RELATIONSHIP                                                    
            MOVE '35'                    TO WS-SERIAL-NO                        
            MOVE K820-NUM-RELATIONSHIP   TO WS-CDE-CODE                         
            PERFORM 3500-READ-K060       THRU 3500-EXIT                         
            MOVE K060-DESC               TO F550-RELA-DESC.             06210039
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------*                                                           
       3400-GET-DETAILS.                                                        
      *-------------*                                                           
                                                                                
      *RETRIEVE SEX                                                             
            MOVE '43'                    TO WS-SERIAL-NO                        
            MOVE K820-NUM-SEX            TO WS-CDE-CODE                         
            PERFORM 3500-READ-K060       THRU 3500-EXIT                         
            MOVE K060-DESC               TO F550-SEX.                   06210039
                                                                                
      *RETRIEVE MARITAL STATUS                                                  
            MOVE '44'                    TO WS-SERIAL-NO                        
            MOVE K820-NUM-MARITAL-STATUS TO WS-CDE-CODE                         
            PERFORM 3500-READ-K060       THRU 3500-EXIT                         
            MOVE K060-DESC1              TO F550-MAR-STATUS.            06210039
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------*                                                           
       3500-READ-K060.                                                  06260039
      *-------------*                                                           
           INITIALIZE BP13K060-REC.                                             
                                                                                
           MOVE WS-K060-KEY              TO K060-KEY-FLD                05130039
                                                                                
           READ BP13K060.                                               06190039
                                                                                
           EVALUATE BP13K060-STATUS                                             
             WHEN 0                                                             
              CONTINUE                                                          
             WHEN 23                                                            
              DISPLAY 'RECORD NOT FOUND IN BP13K060!!!'                         
              DISPLAY 'K060-KEY-FLD:-' K060-KEY-FLD                             
             WHEN OTHER                                                         
              DISPLAY 'ERROR READING BP13K030'                                  
              DISPLAY 'FILE-STATUS:-' BP13K060-STATUS                           
              GO TO 9000-CLOSE-ROUTINE                                          
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                       06260039
           EXIT.                                                        06270039
                                                                        06280039
      *--------------*                                                          
       3600-READ-K820.                                                  06260039
      *--------------*                                                          
                                                                        06280039
           READ BP13K820.                                               06190039
                                                                        06280039
           IF BP13K820-STATUS = ZEROES                                  06200039
              ADD 1                      TO WS-CNT-K820-READ                    
           ELSE                                                         06220039
              DISPLAY 'READ ERROR, BP13K820-STATUS '                            
                       BP13K820-STATUS ' ' K820-KEY-FLD                         
              MOVE SPACES                TO BP13K820-REC                06230039
           END-IF.                                                              
                                                                        06280039
       3600-EXIT.                                                       06260039
           EXIT.                                                        06270039
                                                                        06280039
      *--------------*                                                          
       3800-READ-K830.                                                  06260039
      *--------------*                                                          
                                                                        06280039
           READ BP13K830 KEY IS K830-NUM-REGN.                          06190039
                                                                        06280039
           IF BP13K830-STATUS = ZEROES                                  06200039
              ADD 1                      TO WS-CNT-K830-READ                    
           ELSE                                                         06220039
              DISPLAY 'READ ERROR, BP13K830-STATUS '                            
                       BP13K830-STATUS ' ' K830-KEY-FLD                         
              MOVE SPACES                TO BP13K830-REC                06230039
           END-IF.                                                              
                                                                        06280039
       3800-EXIT.                                                       06260039
           EXIT.                                                        06270039
                                                                        06280039
      *--------------------*                                                    
       4000-START-BP13K820.                                                     
      *--------------------*                                                    
                                                                        06280039
           MOVE 0                        TO WS-FOUND-SW.                        
           MOVE SPACES                   TO BP13K820-REC.                       
           MOVE F800-NUM-REGN            TO K820-NUM-REGN.                      
                                                                                
           START BP13K820 KEY = K820-NUM-REGN.                                  
                                                                                
           IF BP13K820-STATUS NOT = 00                                          
              IF BP13K820-STATUS = 23 OR 10                                     
                 MOVE 1                  TO WS-FOUND-SW                         
              ELSE                                                              
                 DISPLAY ' START   ERROR, BP13K820-STATUS '                     
                           BP13K820-STATUS                                      
                 MOVE BP13K820-STATUS    TO RETURN-CODE                         
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       4500-WRITE-OCCUPIERS.                                                    
      *--------------------*                                                    
                                                                                
           READ BP13K820 NEXT AT END                                            
              MOVE HIGH-VALUES           TO K820-NUM-REGN                       
              GO TO 4500-EXIT                                                   
           END-READ.                                                            
                                                                                
           IF K820-NUM-REGN NOT = F800-NUM-REGN                                 
              GO TO 4500-EXIT                                                   
           ELSE                                                                 
              IF (K820-NUM-NRIC    = F800-NUM-NRIC1) OR                         
                 (K820-NUM-NRIC    = F800-NUM-NRIC2)                            
                 GO TO 4500-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD  1                        TO WS-CNT-K820-READ.                   
                                                                                
           MOVE SPACES                   TO P08F550-DETAIL-REC.                 
           MOVE SPACES                   TO F550-NUM-REGNO                      
           MOVE K820-NUM-REGN            TO F550-NUM-REGNO                      
           MOVE K820-NUM-RELATIONSHIP    TO F550-RELA-COD                       
           MOVE K820-NUM-NRIC            TO F550-NUM-UINFIN.                    
           MOVE K820-NME-OCCP            TO F550-NAME.                          
                                                                                
           MOVE K820-DTE-BIRTH           TO F550-BIRTH-DATE                     
           PERFORM 3300-GET-RELATION     THRU 3300-EXIT.                        
           PERFORM 3400-GET-DETAILS      THRU 3400-EXIT.                        
                                                                                
           ADD  1                        TO WS-CNT-WRITE.                       
           WRITE P08F550-DETAIL-REC.                                            
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       9000-CLOSE-ROUTINE.                                                      
      *------------------*                                                      
                                                                                
      ****************************************************************          
      *            CLOSE FILES AND END PROCESSING                    *          
      ****************************************************************          
                                                                                
           CLOSE BP13F800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13K060                                                       
                 P08F550.                                                       
                                                                                
           DISPLAY ' ** BP13C831 ** '.                                          
           DISPLAY '  '.                                                        
           DISPLAY 'NO OF REC READ FROM PREREGN FILE    (BP13F800) : '          
                   WS-CNT-F800-READ.                                            
           DISPLAY 'NO OF REC READ FROM ADDRESS FILE    (BP13K830) : '          
                   WS-CNT-K830-READ.                                            
           DISPLAY 'NO OF REC READ FROM OCCUPIER FILE   (BP13K820) : '          
                   WS-CNT-K820-READ.                                            
           DISPLAY 'NO OF HEADERS CREATED               (P08F550)  : '          
                   WS-CNT-HEADER.                                               
           DISPLAY 'NO OF RECORDS CREATED               (P08F550)  : '          
                   WS-CNT-WRITE.                                                
           DISPLAY '  '.                                                        
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
