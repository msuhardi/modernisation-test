      *--------------------------------------------------------*                
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------*                
       PROGRAM-ID.     BP13C73C.                                                
      *AUTHOR.         IRVIN CHING.                                             
      *DATE-WRITTEN.   27/05/2014.                                              
      *--------------------------------------------------------*                
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1. PROGRAM TO CREATE TCP/GRO/MGPS/TPS TABLE       *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP135340 IMC1 300514  NEW PROGRAM                      *                
      * BP136239 SMR2 160516  EXPAND LENGTH OF BP13K762 TO 200 *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730  ASSIGN        TO BP13F730.                          
                                                                                
           SELECT BP13K813  ASSIGN        TO BP13K813                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K813-KEY-FLD                       
                            FILE STATUS   IS K813-STATUS.                       
                                                                                
           SELECT BP13K762   ASSIGN TO BP13K762.                                
                                                                                
           SELECT BP13L73C   ASSIGN TO BP13L73C.                                
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
                                                                                
       FD   BP13F730                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K762                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13K762.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13L73C                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   BP13L73C-REC    PIC X(100).                                         
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
       01  WORK-AREAS.                                                          
           05 WS-READ-F730              PIC 9(8)    VALUE 0.                    
           05 WS-READ-K762              PIC 9(8)    VALUE 0.                    
           05 WS-NUM-MGPS               PIC 9(6)    VALUE 0.                    
           05 WS-NUM-GRO                PIC 9(6)    VALUE 0.                    
           05 WS-NUM-TCP                PIC 9(6)    VALUE 0.                    
           05 WS-NUM-REN                PIC 9(6)    VALUE 0.                    
           05 WS-NUM-TOTAL              PIC 9(6)    VALUE 0.                    
           05 WS-OTHER                  PIC 9(6)    VALUE 0.                    
           05 WS-NUM-FLAT-SUPPLY        PIC 9(6)    VALUE 0.                    
           05 WS-LOW-SUPPLY             PIC 9(3)    VALUE 0.                    
           05 WS-PUB-SUPPLY             PIC 9(3)    VALUE 0.                    
           05 WS-MATCH                  PIC 9(8)    VALUE 0.                    
           05 WS-NOT-MATCH-F730         PIC 9(8)    VALUE 0.                    
           05 WS-NOT-MATCH-K762         PIC 9(8)    VALUE 0.                    
           05 WS-WRITE-L73C             PIC 9(8)    VALUE 0.                    
           05 WS-L73C-STATUS            PIC 9(02)   VALUE ZEROES.               
           05 WS-F730-EOF               PIC X(1)    VALUE 'N'.                  
           05 WS-K762-EOF               PIC X(1)    VALUE 'N'.                  
           05 K813-STATUS               PIC 9(2)    VALUE 0.                    
           05 K762-STATUS               PIC 9(2)    VALUE 0.                    
                                                                                
       01  WS-EDITED-DATE               PIC X(10).                              
                                                                                
       01  WS-F730-KEY.                                                         
           05 WS-CDE-NT1                PIC X(03) VALUE SPACES.                 
           05 WS-CDE-FT                 PIC X(02) VALUE SPACES.                 
                                                                                
       01  L73C-HEADER.                                                         
           05 FILLER                    PIC X(30) VALUE 'TOWN/EST'.             
           05 FILLER                    PIC X(01) VALUE ';'.                    
           05 FILLER                    PIC X(13) VALUE 'FT CAT'.               
           05 FILLER                    PIC X(01) VALUE ';'.                    
           05 FILLER                    PIC X(06) VALUE 'SUPPLY'.               
           05 FILLER                    PIC X(01) VALUE ';'.                    
           05 FILLER                    PIC X(06) VALUE 'GRO'.                  
           05 FILLER                    PIC X(01) VALUE ';'.                    
           05 FILLER                    PIC X(06) VALUE 'TCP'.                  
           05 FILLER                    PIC X(01) VALUE ';'.                    
           05 FILLER                    PIC X(06) VALUE 'MGPS'.                 
           05 FILLER                    PIC X(01) VALUE ';'.                    
           05 FILLER                    PIC X(06) VALUE 'TPSREN'.               
           05 FILLER                    PIC X(01) VALUE ';'.                    
           05 FILLER                    PIC X(06) VALUE 'TOTAL'.                
           05 FILLER                    PIC X(01) VALUE ';'.                    
           05 FILLER                    PIC X(06) VALUE 'PUBLIC'.               
           05 FILLER                    PIC X(01) VALUE ';'.                    
                                                                                
       01  L73C-FORMAT.                                                         
           05 L73C-NUM-NT               PIC X(30) VALUE SPACES.                 
           05 FILLER                    PIC X(01) VALUE ';'.                    
           05 L73C-NUM-FT               PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(01) VALUE '-'.                    
           05 L73C-NUM-FT-DESC          PIC X(10) VALUE SPACES.                 
           05 FILLER                    PIC X(01) VALUE ';'.                    
           05 L73C-NUM-FLAT-SUPPLY      PIC 9(06) VALUE ZEROES.                 
           05 FILLER                    PIC X(01) VALUE ';'.                    
           05 L73C-NUM-GRO              PIC 9(06) VALUE ZEROES.                 
           05 FILLER                    PIC X(01) VALUE ';'.                    
           05 L73C-NUM-TCP              PIC 9(06) VALUE ZEROES.                 
           05 FILLER                    PIC X(01) VALUE ';'.                    
           05 L73C-NUM-MGPS             PIC 9(06) VALUE ZEROES.                 
           05 FILLER                    PIC X(01) VALUE ';'.                    
           05 L73C-NUM-REN              PIC 9(06) VALUE ZEROES.                 
           05 FILLER                    PIC X(01) VALUE ';'.                    
           05 L73C-NUM-TOTAL            PIC 9(06) VALUE ZEROES.                 
           05 FILLER                    PIC X(01) VALUE ';'.                    
           05 L73C-NUM-PUB-SUPPLY       PIC 9(06) VALUE ZEROES.                 
           05 FILLER                    PIC X(01) VALUE ';'.                    
                                                                                
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
      *--------------------------------------------------------*                
       0000-MAIN.                                                               
      *--------------------------------------------------------*                
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F730     THRU 2000-EXIT.                           
           PERFORM 3000-READ-K762     THRU 3000-EXIT.                           
                                                                                
           PERFORM 4000-COMPARE       THRU 4000-EXIT                            
             UNTIL K762-KEY-FLD = HIGH-VALUES                                   
                OR WS-K762-EOF  = 'Y'                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILES.                                                         
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13K813                                                 
                       BP13K762                                                 
               OUTPUT  BP13L73C.                                                
                                                                                
           IF K813-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K813-STATUS ' K813-STATUS                    
              MOVE K813-STATUS              TO RETURN-CODE                      
              PERFORM 9999-CLOSE-ROUTINE    THRU 9999-EXIT                      
           END-IF.                                                              
                                                                                
           IF K762-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K762-STATUS ' K762-STATUS                    
              MOVE K762-STATUS              TO RETURN-CODE                      
              PERFORM 9999-CLOSE-ROUTINE    THRU 9999-EXIT                      
           END-IF.                                                              
                                                                                
           WRITE BP13L73C-REC FROM L73C-HEADER.                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-READ-F730.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F730                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO WS-F730-KEY                              
                   MOVE 'Y' TO WS-F730-EOF                                      
                NOT AT END                                                      
                   MOVE F730-CDE-NT1             TO WS-CDE-NT1                  
                   MOVE F730-CDE-FLAT-TYPE       TO WS-CDE-FT                   
                   ADD 1 TO WS-READ-F730                                        
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3000-READ-K762.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13K762                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO K762-KEY-FLD                             
                   MOVE 'Y' TO WS-K762-EOF                                      
                NOT AT END                                                      
                   ADD 1 TO WS-READ-K762                                        
           END-READ.                                                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-COMPARE.                                                            
      *--------------------------------------------------------*                
                                                                                
           IF WS-F730-KEY      = K762-KEY-FLD                                   
              ADD 1 TO WS-MATCH                                                 
              PERFORM 4100-COUNT-FLAT  THRU 4100-EXIT                           
                UNTIL WS-F730-KEY NOT = K762-KEY-FLD                            
                   OR WS-F730-EOF = 'Y'                                         
              PERFORM 4200-WRITE-L73C  THRU 4200-EXIT                           
              PERFORM 3000-READ-K762   THRU 3000-EXIT                           
           ELSE                                                                 
              IF WS-F730-KEY     > K762-KEY-FLD                                 
                 ADD 1 TO WS-NOT-MATCH-K762                                     
                 PERFORM 4200-WRITE-L73C  THRU 4200-EXIT                        
                 PERFORM 3000-READ-K762   THRU 3000-EXIT                        
              ELSE                                                              
                 ADD 1 TO WS-NOT-MATCH-F730                                     
                 PERFORM 2000-READ-F730   THRU 2000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4100-COUNT-FLAT.                                                         
      *--------------------------------------------------------*                
                                                                                
           EVALUATE TRUE                                                        
             WHEN F730-CDE-ALLOC-SCH = 'GRO'                                    
               ADD 1     TO WS-NUM-GRO                                          
             WHEN F730-CDE-ALLOC-SCH = 'TCP'                                    
               ADD 1     TO WS-NUM-TCP                                          
             WHEN F730-NUM-MGPS = 'C' OR 'P'                                    
               ADD 1     TO WS-NUM-MGPS                                         
             WHEN F730-CDE-ALLOC-SCH = 'TPS'                                    
             WHEN F730-CDE-ALLOC-SCH = 'REN'                                    
             WHEN F730-CDE-ALLOC-SCH = 'RH'                                     
               ADD 1     TO WS-NUM-REN                                          
             WHEN OTHER                                                         
               ADD 1     TO WS-OTHER                                            
           END-EVALUATE                                                         
                                                                                
           PERFORM 2000-READ-F730   THRU 2000-EXIT.                             
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4200-WRITE-L73C.                                                         
      *--------------------------------------------------------*                
                                                                                
           INITIALIZE BP13L73C-REC                                              
                      L73C-FORMAT                                               
                      WS-LOW-SUPPLY                                             
                      WS-NUM-TOTAL                                              
                      WS-PUB-SUPPLY.                                            
                                                                                
           PERFORM 4300-READ-BP13K813   THRU 4300-EXIT                          
                                                                                
           MOVE K762-NUM-SUPPLY   TO L73C-NUM-FLAT-SUPPLY                       
           MOVE WS-NUM-GRO        TO L73C-NUM-GRO                               
           MOVE WS-NUM-TCP        TO L73C-NUM-TCP                               
           MOVE WS-NUM-REN        TO L73C-NUM-REN                               
           MOVE WS-NUM-MGPS       TO L73C-NUM-MGPS                              
           COMPUTE WS-NUM-TOTAL = WS-NUM-GRO + WS-NUM-TCP + WS-NUM-REN +        
                                  WS-NUM-MGPS                                   
                                                                                
           COMPUTE WS-LOW-SUPPLY = K762-NUM-SUPPLY * 0.15                       
                                                                                
           IF WS-NUM-TOTAL <= WS-LOW-SUPPLY                                     
              MOVE WS-NUM-TOTAL   TO L73C-NUM-TOTAL                             
           ELSE                                                                 
              MOVE WS-LOW-SUPPLY  TO L73C-NUM-TOTAL                             
           END-IF                                                               
                                                                                
           COMPUTE WS-PUB-SUPPLY = K762-NUM-SUPPLY - L73C-NUM-TOTAL             
                                                                                
           MOVE WS-PUB-SUPPLY     TO L73C-NUM-PUB-SUPPLY                        
                                                                                
           WRITE BP13L73C-REC FROM L73C-FORMAT.                                 
                                                                                
           ADD 1 TO WS-WRITE-L73C.                                              
                                                                                
           INITIALIZE WS-NUM-GRO                                                
                      WS-NUM-TCP                                                
                      WS-NUM-REN                                                
                      WS-NUM-MGPS                                               
                      WS-OTHER.                                                 
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4300-READ-BP13K813.                                                      
      *===============================================================*         
           INITIALIZE BP13K813-REC                                              
                                                                                
           MOVE K762-NUM-CHOICE      TO K813-NUM-ZONE                           
           MOVE K762-NUM-FT          TO K813-NUM-FLAT-TYPE                      
           MOVE K762-DTE-BALLOT      TO K813-DTE-BALLOT                         
                                                                                
           READ BP13K813                                                        
                                                                                
           IF K813-STATUS = 00 OR 02                                            
              MOVE K813-NME-PROJECT-LTR    TO L73C-NUM-NT                       
              MOVE K813-NUM-FLAT-TYPE      TO L73C-NUM-FT                       
              MOVE K813-NUM-FLAT-CATEGORY  TO L73C-NUM-FT-DESC                  
           ELSE                                                                 
              IF K813-STATUS = 23                                               
                 DISPLAY 'BP13K813 - NOT FND: ' K813-KEY-FLD                    
                 MOVE K813-NUM-ZONE        TO L73C-NUM-NT                       
                 MOVE K813-NUM-FLAT-TYPE   TO L73C-NUM-FT                       
              ELSE                                                              
                 DISPLAY 'BP13K813 - ERROR READING , '                          
                         ' STATUS : ' K813-STATUS                               
                         ' KEY : ' K813-KEY-FLD                                 
                 GO TO 9999-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           DISPLAY '*********** BP13C73C **************'.                       
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS READ (BP13F730) :' WS-READ-F730.             
           DISPLAY 'NO. OF RECORDS READ (BP13K762) :' WS-READ-K762.             
           DISPLAY 'NO. OF RECORDS WRITE(BP13L73C) :' WS-WRITE-L73C.            
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS MATCH           :' WS-MATCH.                 
           DISPLAY 'NO. OF RECORDS NOT MATCH K762  :' WS-NOT-MATCH-K762.        
           DISPLAY 'NO. OF RECORDS NOT MATCH F730  :' WS-NOT-MATCH-F730.        
           DISPLAY '************************************'.                      
                                                                                
           CLOSE       BP13F730                                                 
                       BP13K762                                                 
                       BP13K813                                                 
                       BP13L73C.                                                
                                                                                
           IF K813-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K813-STATUS ' K813-STATUS                   
              MOVE K813-STATUS              TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF K762-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K762-STATUS ' K762-STATUS                   
              MOVE K762-STATUS              TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
