      *****************************************************************         
       IDENTIFICATION DIVISION.                                                 
      *****************************************************************         
       PROGRAM-ID.    BP13CY06.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  13/07/2011.                                               
      *****************************************************************         
      *OBJECTIVE - TO BYPASS MATURE ESTATE AND HARDLUCK CASES         *         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F595                                  *         
      *                  2. BP13F813                                  *         
      *     OUTPUT FILE:                                              *         
      *                  1. P13F595A                                  *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------   --------  ---  ------------------------             *         
      * BP134300  13/07/11  JB8  NEW PROGRAM.                         *         
      * BP135190  04/12/13  SMR2 EXPAND BP13K813 TO 1000              *         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F595  ASSIGN        TO BP13F595.                         
           SELECT  BP13F813  ASSIGN        TO BP13F813.                         
           SELECT  P13F595A  ASSIGN        TO P13F595A.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F595                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE  IS  F.                                              
       COPY BP13F595.                                                           
                                                                                
       FD   BP13F813                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 1000 CHARACTERS                                     
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE  IS  F.                                              
       COPY BP13F813.                                                           
                                                                                
       FD   P13F595A                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE  IS  F.                                              
       01   P13F595A-REC    PIC X(500).                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  VARIABLES.                                                           
           05  CNT-F595-READ           PIC 9(7)  VALUE ZEROES.                  
           05  CNT-F813-READ           PIC 9(7)  VALUE ZEROES.                  
           05  CNT-F595A-WRITE         PIC 9(7)  VALUE ZEROES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F595      THRU 2000-EXIT.                      
           PERFORM 2010-READ-BP13F813      THRU 2010-EXIT.                      
                                                                                
           PERFORM 2050-PROCESS-RECORDS    THRU 2050-EXIT                       
             UNTIL F595-CDE-NT1  = HIGH-VALUES                                  
               AND F813-NUM-ZONE = HIGH-VALUES.                                 
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F595                                                 
                       BP13F813                                                 
                OUTPUT P13F595A.                                                
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2000-READ-BP13F595.                                                      
      ******************************************************************        
                                                                                
           READ BP13F595                                                        
             AT END                                                             
                MOVE HIGH-VALUES   TO F595-CDE-NT1                              
                GO                 TO 2000-EXIT.                                
                                                                                
           ADD 1                   TO CNT-F595-READ.                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2010-READ-BP13F813.                                                      
      ******************************************************************        
                                                                                
           READ BP13F813                                                        
             AT END                                                             
                MOVE HIGH-VALUES   TO F813-NUM-ZONE                             
                GO                 TO 2010-EXIT.                                
                                                                                
           ADD 1                   TO CNT-F813-READ.                            
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2050-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF F595-CDE-NT1 = F813-NUM-ZONE                                      
              PERFORM 2000-READ-BP13F595         THRU 2000-EXIT                 
           ELSE                                                                 
              IF F595-CDE-NT1 < F813-NUM-ZONE                                   
                 WRITE P13F595A-REC FROM BP13F595-REC                           
                 ADD 1                             TO CNT-F595A-WRITE           
                 PERFORM 2000-READ-BP13F595      THRU 2000-EXIT                 
              ELSE                                                              
                 PERFORM 2010-READ-BP13F813      THRU 2010-EXIT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CY06'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY 'NO OF BP13F595 RECORDS READ................. : '            
                    CNT-F595-READ.                                              
           DISPLAY 'NO OF BP13F595 RECORDS READ................. : '            
                    CNT-F813-READ.                                              
           DISPLAY 'NO OF P13F595A RECORDS WRITTEN.............. : '            
                    CNT-F595A-WRITE.                                            
                                                                                
           CLOSE BP13F595                                                       
                 BP13F813                                                       
                 P13F595A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
