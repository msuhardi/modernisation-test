       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C91K.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  02/02/2017.                                               
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE  :  FORMAT BLOCK                                   *         
      *                                                               *         
      *  INPUT      :  BP13FTMP  RECORDS TO BE PROCESSED              *         
      *  OUTPUT     :  BP13F100  FORMATTED RECORDS                    *         
      *                                                               *         
      *===============================================================*         
      * CHG-REF#  BY  DDMMCCYY DESCRIPTION                            *         
      * -------- ---- -------- -------------------------------------- *         
      * BP136609 SMR2 02022017 NEW PROGRAM                            *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FTMP ASSIGN TO BP13FTMP.                                  
           SELECT BP13F100 ASSIGN TO BP13F100.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13FTMP                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 800 CHARACTERS.                                     
       01   BP13FTMP-REC.                                                       
            05  FTMP-NUM-BLOCK             PIC X(05).                           
            05  FTMP-NUM-LEVEL             PIC X(02).                           
            05  FTMP-NUM-MAIN              PIC X(04).                           
            05  FTMP-NUM-ESTATE            PIC X(03).                           
            05  FTMP-NUM-NGHBRD            PIC X(04).                           
            05  FTMP-NUM-CNTRCT            PIC X(04).                           
            05  FTMP-NUM-TYP-ACTUSE        PIC X(02).                           
            05  FTMP-NUM-WARD              PIC X(03).                           
            05  FTMP-NUM-NT                PIC X(03).                           
            05  FTMP-NUM-ALLO-CAT          PIC X(03).                           
            05  FTMP-DTE-BALLOT            PIC X(06).                           
            05  FILLER                     PIC X(761).                          
                                                                                
       FD   BP13F100                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 800 CHARACTERS.                                     
       COPY BP13F100.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FTMP-READ                PIC 9(08)   VALUE ZEROES.            
           05  WS-F100-WRITE               PIC 9(08)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-FTMP-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-CHAR-FLAG                PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-EDT-DATE                 PIC X(10)   VALUE SPACES.            
           05  WS-CUR-DATE                 PIC X(08)   VALUE SPACES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-EDIT1                    PIC Z(07)9.                          
                                                                                
       01  WS-FORMAT-INPUT.                                                     
           05  WS-BLK                      PIC X(05)   VALUE SPACES.            
           05  WS-BLK1                     PIC X(05)   VALUE SPACES.            
           05  WS-SUB                      PIC 9(01)   VALUE ZEROES.            
           05  WS-SUB1                     PIC 9(01)   VALUE ZEROES.            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13FTMP       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECS        THRU 3000-EXIT                      
             UNTIL WS-FTMP-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BP13FTMP                                                 
                OUTPUT BP13F100.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-CUR-DATE.                     
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4)  DELIMITED BY SIZE                           
                  INTO WS-EDT-DATE.                                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13FTMP.                                                      
      *---------------------------------------------------------------*         
           READ BP13FTMP AT END                                                 
                MOVE 'Y'                    TO WS-FTMP-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-FTMP-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-RECS.                                                       
      *---------------------------------------------------------------*         
           PERFORM 4000-WRITE-BP13F100      THRU 4000-EXIT.                     
           PERFORM 2000-READ-BP13FTMP       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-WRITE-BP13F100.                                                     
      *---------------------------------------------------------------*         
           MOVE SPACES                      TO BP13F100-REC.                    
           INITIALIZE                          BP13F100-REC.                    
                                                                                
           MOVE SPACES                      TO WS-BLK WS-BLK1.                  
           MOVE 5                           TO WS-SUB1.                         
           MOVE FTMP-NUM-BLOCK              TO WS-BLK.                          
           PERFORM 5000-RIGHT-JUSTIFY       THRU 5000-EXIT.                     
                                                                                
           MOVE FTMP-NUM-LEVEL              TO F100-NUM-HDB-REF(1:2).           
           MOVE FTMP-NUM-MAIN               TO F100-NUM-HDB-REF(3:4).           
           MOVE WS-BLK1                     TO F100-NUM-HDB-REF(7:5).           
           MOVE FTMP-NUM-ESTATE             TO F100-NUM-ESTATE.                 
           MOVE FTMP-NUM-NGHBRD             TO F100-NUM-NGHBRD.                 
           MOVE FTMP-NUM-CNTRCT             TO F100-NUM-CNTRCT.                 
           MOVE WS-BLK1                     TO F100-NUM-BLOCK.                  
           MOVE FTMP-NUM-TYP-ACTUSE         TO F100-NUM-TYP-ACTUSE.             
           MOVE FTMP-NUM-LEVEL              TO F100-NUM-LEVEL.                  
           MOVE FTMP-NUM-MAIN               TO F100-NUM-MAIN.                   
           MOVE FTMP-NUM-WARD               TO F100-NUM-WARD.                   
           MOVE FTMP-NUM-NT                 TO F100-NUM-NT.                     
           MOVE FTMP-NUM-ALLO-CAT           TO F100-NUM-ALLO-CAT.               
           MOVE FTMP-DTE-BALLOT             TO F100-DTE-BALLOT.                 
                                                                                
           WRITE BP13F100-REC.                                                  
           ADD 1                            TO WS-F100-WRITE.                   
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5000-RIGHT-JUSTIFY.                                                      
      *---------------------------------------------------------------*         
           PERFORM VARYING WS-SUB FROM 5 BY -1 UNTIL WS-SUB < 1                 
              IF WS-BLK(WS-SUB:1) NOT = SPACES AND LOW-VALUES                   
                 IF WS-BLK(WS-SUB:1) IS NOT NUMERIC                             
                    MOVE 'Y'                TO WS-CHAR-FLAG                     
                 ELSE                                                           
                    MOVE 'N'                TO WS-CHAR-FLAG                     
                 END-IF                                                         
                 IF WS-CHAR-FLAG = 'Y' AND WS-SUB1 = 5                          
                    MOVE WS-BLK(WS-SUB:1)   TO WS-BLK1(WS-SUB1:1)               
                 END-IF                                                         
                 IF WS-CHAR-FLAG = 'N'                                          
                    COMPUTE WS-SUB1 = WS-SUB1 - 1                               
                    MOVE WS-BLK(WS-SUB:1)   TO WS-BLK1(WS-SUB1:1)               
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13C91K       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-FTMP-READ                TO WS-EDIT1.                        
           DISPLAY 'NO OF RECS READ FROM BP13FTMP    : ' WS-EDIT1.              
           MOVE WS-F100-WRITE               TO WS-EDIT1.                        
           DISPLAY 'NO OF RECS WRITTEN TO BP13F100   : ' WS-EDIT1.              
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           CLOSE   BP13FTMP                                                     
                   BP13F100.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
