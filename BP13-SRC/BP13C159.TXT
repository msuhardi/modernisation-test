       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C159.                                                 
      *AUTHOR.        SELVY.                                                    
      *DATE-WRITTEN.  24/06/1999.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *   OBJECTIVE   :  PROGRAM TO UPDATE HOUSEHOLD TYPE AND 10-YR*            
      *                  TO REN REQUEST FILE.                      *            
      *                                                            *            
      *   INPUT FILES :  1.BP13F500 - DATA ENTRY FILE FOR REQUEST  *            
      *                               FOR REGN                     *            
      *                  2.BP13K730 - DATA ENTRY FILE FOR REQUEST  *            
      *                               FOR REGN                     *            
      *                                                            *            
      * CHG REF  DATE      OIC   REMARKS                           *            
      * -------- --------  ----  -----------------------------     *            
      * BP130671 19990624  SRA   NEW PROGRAM                       *            
      * BP130652 19990917  LSB   EXPAND F500,K730 RECL FROM 300 TO *            
      *                          500                               *            
      * BP130718 19991115  RASM  TO UPDATE K730-NUM-MTH-SUBLET AND *            
      *                          K730-CDE-PARENT-CHILD             *            
      * BP132674 20050328  CG7   ADDED F500 FIELDS TO BE MOVED TO  *            
      *                          K730 FIELDS IN SECTION 2100       *            
      * BP133026 20070330  BALA  UPDATE BP13K800                   *            
      * BP135279 20140325  YCH2  DELETE STATEMENT THAT CHECK       *            
      *                          HOUSEHOLD TYPE = SPACES           *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F500  ASSIGN       TO BP13F500.                           
                                                                                
           SELECT BP13K730  ASSIGN       TO BP13K730                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K730-KEY-NEW                        
                            FILE STATUS  IS WS-K730-STATUS.                     
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F500           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               LABEL RECORD IS STANDARD                         
                               RECORDING MODE  IS  F.                           
       COPY BP13F500.                                                           
                                                                                
       FD   BP13K730           RECORD CONTAINS 500 CHARACTERS.                  
       COPY BP13K730.                                                           
                                                                                
       FD   BP13K800           RECORD CONTAINS 2000 CHARACTERS.                 
       COPY BP13K800.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K730-STATUS           PIC 9(2)   VALUE 00.                    
           05  WS-K800-STATUS           PIC 9(2)   VALUE 00.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F500-READ             PIC 9(4)   VALUE ZEROES.                
           05  WS-K730-READ             PIC 9(4)   VALUE ZEROES.                
           05  WS-K730-REWRT            PIC 9(4)   VALUE ZEROES.                
           05  WS-K730-NOTFND           PIC 9(4)   VALUE ZEROES.                
           05  WS-K800-READ             PIC 9(4)   VALUE ZEROES.                
           05  WS-K800-REWRT            PIC 9(4)   VALUE ZEROES.                
           05  WS-K800-NOTFND           PIC 9(4)   VALUE ZEROES.                
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  WS-F500-EOF              PIC X      VALUE 'N'.                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 1500-F500-READ    THRU 1500-EXIT.                            
           PERFORM 2000-MAIN-ROUTINE THRU 2000-EXIT                             
                       UNTIL WS-F500-EOF = 'Y'.                                 
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT BP13F500                                                  
                I-O   BP13K730                                                  
                      BP13K800.                                                 
                                                                                
           IF WS-K730-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K730 ERROR ' WS-K730-STATUS                  
              MOVE     WS-K730-STATUS TO RETURN-CODE                            
              PERFORM  9000-CLOSE-FILES THRU 9000-EXIT.                         
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS TO RETURN-CODE                            
              PERFORM  9000-CLOSE-FILES THRU 9000-EXIT.                         
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1500-F500-READ.                                                          
      *-------------------------------------------------------------            
           READ BP13F500 AT END MOVE 'Y'  TO WS-F500-EOF                        
                            GO TO 1500-EXIT.                                    
                                                                                
           ADD  1                         TO WS-F500-READ.                      
                                                                                
       1500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE F500-NUM-REGN              TO K730-NUM-REGN.                    
           READ BP13K730.                                                       
           EVALUATE WS-K730-STATUS                                              
           WHEN 00                                                              
                PERFORM 2100-BP13K730-FOUND THRU 2100-EXIT                      
           WHEN 23                                                              
                ADD 1                      TO WS-K730-NOTFND                    
                DISPLAY "K730 REC NOT FND, STATUS:" WS-K730-STATUS              
                DISPLAY "K730-KEY-FLD  = "  F500-NUM-REGN                       
           WHEN OTHER                                                           
                MOVE WS-K730-STATUS        TO RETURN-CODE                       
                DISPLAY "BP13K730 READ FILE ERROR:" WS-K730-STATUS              
                PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2200-READ-BP13K800     THRU 2200-EXIT.                       
           PERFORM 1500-F500-READ         THRU 1500-EXIT.                       
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2100-BP13K730-FOUND.                                                     
      *-------------------------------------------------------------            
           ADD 1                         TO WS-K730-READ.                       
      *                                                                         
      *  HOUSEHOLD TYPE IS UPDATED IF BP13SR9B NOT SUCCESSFUL                   
      *                                                                         
      *    IF K730-CDE-HOUSEHOLD = SPACES OR LOW-VALUES                         
      *       MOVE F500-CDE-HOUSEHOLD       TO K730-CDE-HOUSEHOLD               
      *       MOVE F500-NUM-5YR-OCCUPN      TO K730-NUM-5YR-OCCUPN              
      *       MOVE F500-NUM-MTH-RESIDE      TO K730-NUM-MTH-RESIDE              
      *       MOVE F500-NUM-MTH-SUBLET      TO K730-NUM-MTH-SUBLET              
      *       MOVE F500-CDE-PARENT-CHILD    TO K730-CDE-PARENT-CHILD            
      *    END-IF.                                                              
                                                                                
           IF F500-NUM-SMS-STATUS NOT = SPACES AND LOW-VALUES                   
              MOVE F500-NUM-SMS-STATUS   TO K730-NUM-SMS-STATUS                 
           END-IF.                                                              
                                                                                
           IF F500-NUM-SMS-FT NOT = SPACES AND LOW-VALUES                       
              MOVE F500-NUM-SMS-FT       TO K730-NUM-SMS-FT                     
           END-IF.                                                              
                                                                                
           IF F500-NUM-RSL-STATUS NOT = SPACES AND LOW-VALUES                   
              MOVE F500-NUM-RSL-STATUS   TO K730-NUM-RSL-STATUS                 
           END-IF.                                                              
                                                                                
           IF F500-NUM-RSL-FT NOT = SPACES AND LOW-VALUES                       
              MOVE F500-NUM-RSL-FT       TO K730-NUM-RSL-FT                     
           END-IF.                                                              
                                                                                
           IF F500-NUM-RDB-STATUS NOT = SPACES AND LOW-VALUES                   
              MOVE F500-NUM-RDB-STATUS   TO K730-NUM-RDB-STATUS                 
           END-IF.                                                              
                                                                                
           IF F500-NUM-RDB-FT NOT = SPACES AND LOW-VALUES                       
              MOVE F500-NUM-RDB-FT       TO K730-NUM-RDB-FT                     
           END-IF.                                                              
                                                                                
           PERFORM 2500-K730-REWRITE THRU 2500-EXIT.                            
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2200-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                     TO BP13K800-MASTER.                  
           MOVE F500-NUM-REGN              TO K800-NUM-REGN.                    
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
                PERFORM 2300-BP13K800-FOUND THRU 2300-EXIT                      
           WHEN 23                                                              
                ADD 1                      TO WS-K800-NOTFND                    
                DISPLAY "K800 REC NOT FND, STATUS:" WS-K800-STATUS              
                DISPLAY "K800-NUM-REGN  = "  K800-NUM-REGN                      
           WHEN OTHER                                                           
                MOVE WS-K800-STATUS        TO RETURN-CODE                       
                DISPLAY "BP13K800 READ FILE ERROR:" WS-K800-STATUS              
                PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2300-BP13K800-FOUND.                                                     
      *-------------------------------------------------------------            
           ADD 1                         TO WS-K800-READ.                       
           MOVE F500-CDE-HOUSEHOLD       TO K800-NUM-HOUSEHOLD.                 
           MOVE F500-NUM-5YR-OCCUPN      TO K800-NUM-5YR-OCCUPN.                
           MOVE F500-NUM-MTH-RESIDE      TO K800-NUM-MTH-RESIDE.                
           MOVE F500-NUM-MTH-SUBLET      TO K800-NUM-MTH-SUBLET.                
           MOVE F500-CDE-PARENT-CHILD    TO K800-NUM-PARENT-CHILD.              
                                                                                
           IF F500-NUM-SMS-STATUS NOT = SPACES AND LOW-VALUES                   
              MOVE F500-NUM-SMS-STATUS   TO K800-NUM-SMS-STATUS                 
           END-IF.                                                              
                                                                                
           IF F500-NUM-SMS-FT NOT = SPACES AND LOW-VALUES                       
              MOVE F500-NUM-SMS-FT       TO K800-NUM-SMS-FT                     
           END-IF.                                                              
                                                                                
           IF F500-NUM-RSL-STATUS NOT = SPACES AND LOW-VALUES                   
              MOVE F500-NUM-RSL-STATUS   TO K800-NUM-RSL-STATUS                 
           END-IF.                                                              
                                                                                
           IF F500-NUM-RSL-FT NOT = SPACES AND LOW-VALUES                       
              MOVE F500-NUM-RSL-FT       TO K800-NUM-RSL-FT                     
           END-IF.                                                              
                                                                                
           IF F500-NUM-RDB-STATUS NOT = SPACES AND LOW-VALUES                   
              MOVE F500-NUM-RDB-STATUS   TO K800-NUM-RDB-STATUS                 
           END-IF.                                                              
                                                                                
           IF F500-NUM-RDB-FT NOT = SPACES AND LOW-VALUES                       
              MOVE F500-NUM-RDB-FT       TO K800-NUM-RDB-FT                     
           END-IF.                                                              
                                                                                
           PERFORM 2600-K800-REWRITE THRU 2600-EXIT.                            
                                                                                
       2300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2500-K730-REWRITE.                                                       
      *-------------------------------------------------------------            
                                                                                
           REWRITE BP13K730-REC.                                                
           EVALUATE WS-K730-STATUS                                              
           WHEN 00                                                              
                ADD 1                    TO WS-K730-REWRT                       
           WHEN OTHER                                                           
                MOVE WS-K730-STATUS      TO RETURN-CODE                         
                DISPLAY "ERROR WHILE REWRITING:" WS-K730-STATUS                 
                PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2600-K800-REWRITE.                                                       
      *-------------------------------------------------------------            
                                                                                
           REWRITE BP13K800-MASTER.                                             
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
                ADD 1                    TO WS-K800-REWRT                       
           WHEN OTHER                                                           
                MOVE WS-K800-STATUS      TO RETURN-CODE                         
                DISPLAY "ERROR WHILE REWRITING:" WS-K800-STATUS                 
                PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13C159 CONTROL '.                                         
           DISPLAY '-----------------------------------'.                       
           DISPLAY 'TOTAL F500 REC READ      = ' WS-F500-READ.                  
           DISPLAY 'TOTAL K730 REC READ      = ' WS-K730-READ.                  
           DISPLAY 'TOTAL K730 REC REWRITE   = ' WS-K730-REWRT.                 
           DISPLAY 'TOTAL K730 REC RECNFND   = ' WS-K730-NOTFND.                
           DISPLAY 'TOTAL K800 REC READ      = ' WS-K800-READ.                  
           DISPLAY 'TOTAL K800 REC REWRITE   = ' WS-K800-REWRT.                 
           DISPLAY 'TOTAL K800 REC RECNFND   = ' WS-K800-NOTFND.                
                                                                                
           CLOSE      BP13F500                                                  
                      BP13K730                                                  
                      BP13K800.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
