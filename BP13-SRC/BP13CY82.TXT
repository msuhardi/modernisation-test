      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CY82.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  10/06/2011.                                               
      *****************************************************************         
      * OBJECTIVE -                                                   *         
      *   MATCH WITH BP13F205 TO EXTRACT HIST REC THAT MATCHED        *         
      *   THE LAUNCH DATE, CLOSE-DATE, MODE OF SALES.                 *         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F205                                  *         
      *                  2. BP13F595                                  *         
      *                  3. BP13K813                                  *         
      *                                                               *         
      *     OUTPUT FILE:                                              *         
      *                  1. P13F595A                                  *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------   --------  ---  ------------------------             *         
      * BP134247  10062011  PCL3 NEW PROGRAM.                         *         
      * BP135190  04122013  SMR2 EXPAND BP13K813 TO 1000              *         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F205  ASSIGN        TO BP13F205.                         
           SELECT  BP13F595  ASSIGN        TO BP13F595.                         
           SELECT  P13F595A  ASSIGN        TO P13F595A.                         
           SELECT  P13F595B  ASSIGN        TO P13F595B.                         
           SELECT  BP13K813  ASSIGN        TO BP13K813                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K813-KEY-FLD                      
                             FILE STATUS   IS WS-K813-STATUS.                   
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13F205                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 80 CHARACTERS            
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F595                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 500 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F595.                                                           
                                                                                
       FD   P13F595A                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 500 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01  P13F595A-REC                PIC X(500).                              
                                                                                
       FD   P13F595B                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 500 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01  P13F595B-REC                PIC X(500).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  WORK-FILE-VARIABLE.                                                  
           05  WS-EOF-BP13F595         PIC X     VALUE 'N'.                     
           05  WS-K813-BYPASS          PIC X     VALUE 'N'.                     
           05  WS-K813-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-BP13F595-READ        PIC 9(7)  VALUE ZEROES.                  
           05  WS-P13F595A-WRITTEN     PIC 9(7)  VALUE ZEROES.                  
           05  WS-P13F595B-WRITTEN     PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-BYPASS           PIC 9(7)  VALUE ZEROES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F205      THRU 2000-EXIT.                      
           PERFORM 2100-READ-BP13F595      THRU 2100-EXIT                       
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
                   UNTIL WS-EOF-BP13F595   = 'Y'                                
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BP13F595                                                 
                       BP13K813                                                 
                OUTPUT P13F595A                                                 
                       P13F595B.                                                
                                                                                
           IF WS-K813-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'ERROR OPENING - BP13K813 : ' WS-K813-STATUS              
              MOVE     WS-K813-STATUS            TO RETURN-CODE                 
              PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT                   
           END-IF.                                                              
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2000-READ-BP13F205.                                                      
      ******************************************************************        
                                                                                
           READ BP13F205           AT END                                       
                GO                 TO 9999-CLOSE-ROUTINE.                       
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2100-READ-BP13F595.                                                      
      ******************************************************************        
                                                                                
           READ BP13F595           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-BP13F595                         
                GO                 TO   2100-EXIT.                              
                                                                                
           ADD  1                  TO   WS-BP13F595-READ.                       
                                                                                
       2100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF F595-DTE-BALLOT  >= F205-DTE-ALLOCN(1:6) AND                      
              F595-DTE-BALLOT  <= F205-DTE-END(1:6)    AND                      
              F595-NUM-ALLO-CAT = F205-NUM-SELECTION                            
                                                                                
              PERFORM 4100-READ-BP13K813             THRU 4100-EXIT             
                                                                                
              IF WS-K813-BYPASS = 'Y'                                           
                 ADD 1                                 TO WS-CNT-BYPASS         
              ELSE                                                              
                 PERFORM 4000-WRITE-P13F595A         THRU 4000-EXIT             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F595-DTE-BALLOT   = F205-DTE-END(1:6)                             
              IF WS-K813-BYPASS = 'Y'                                           
                 ADD 1                                 TO WS-CNT-BYPASS         
              ELSE                                                              
                 PERFORM 5000-WRITE-P13F595B         THRU 5000-EXIT             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-BP13F595                   THRU 2100-EXIT.         
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
      *********************                                                     
       4000-WRITE-P13F595A.                                                     
      *********************                                                     
                                                                                
           WRITE P13F595A-REC          FROM BP13F595-REC.                       
           ADD 1                         TO WS-P13F595A-WRITTEN.                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4100-READ-BP13K813.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                          TO BP13K813-REC.                
           INITIALIZE                              BP13K813-REC.                
                                                                                
           MOVE 'N'                             TO WS-K813-BYPASS.              
           MOVE F595-CDE-NT1                    TO K813-NUM-ZONE.               
           MOVE F595-CDE-FLAT-TYPE              TO K813-NUM-FLAT-TYPE.          
           MOVE F595-DTE-BALLOT                 TO K813-DTE-BALLOT.             
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    IF K813-NUM-MATURE-EST-TAG = 'Y'                            
                       MOVE 'Y'                 TO WS-K813-BYPASS               
                    END-IF                                                      
                                                                                
               WHEN 23                                                          
                    MOVE 'N'                    TO WS-K813-BYPASS               
                                                                                
               WHEN OTHER                                                       
                    MOVE WS-K813-STATUS               TO RETURN-CODE            
                    DISPLAY 'ERROR READING BP13K813: ' WS-K813-STATUS           
                            ' KEY : ' K813-KEY-FLD                              
                    PERFORM  9999-CLOSE-ROUTINE THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       5000-WRITE-P13F595B.                                                     
      *********************                                                     
                                                                                
           WRITE P13F595B-REC          FROM BP13F595-REC.                       
           ADD 1                         TO WS-P13F595B-WRITTEN.                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CY82'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13F595 RECORDS READ..........: '                    
                    WS-BP13F595-READ.                                           
           DISPLAY 'NO OF P13F595A RECORDS WRITTEN.......: '                    
                    WS-P13F595A-WRITTEN.                                        
           DISPLAY 'NO OF P13F595B RECORDS WRITTEN.......: '                    
                    WS-P13F595B-WRITTEN.                                        
           DISPLAY 'NO OF MATURE-EST-TAG = "Y"  .........: '                    
                    WS-CNT-BYPASS.                                              
                                                                                
           CLOSE BP13F205                                                       
                 BP13F595                                                       
                 P13F595A                                                       
                 P13F595B                                                       
                 BP13K813.                                                      
                                                                                
           IF WS-K813-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'ERROR CLOSING - BP13K813 : ' WS-K813-STATUS              
              MOVE     WS-K813-STATUS            TO RETURN-CODE                 
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
