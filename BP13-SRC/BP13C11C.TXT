       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C11C.                                                  
      *AUTHOR.       ELGER DE GUZMAN.                                           
      *DATE-WRITTEN. 29/11/2013.                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT                                       *            
      * ========================================================== *            
      * PURPOSE     : MONTHLY REPORT FOR AHG/SHG DISBURSEMENTS     *            
      *               AND RECOVERIES.                              *            
      *                                                            *            
      * INPUT       : BP13FD05 - TEMP FILE &&FD05                  *            
      *             : BP13KD09 - BP13.KD09.GRANT                   *            
      *                                                            *            
      * OUTPUT      : BP1311CA - WITH CANCELLATION DATE            *            
      *             : BP1311CB - WITH NO CANCELLATION DATE         *            
      * BP135129 ESD1 29/11/2013 NEW PROGRAM                                    
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13FD05   ASSIGN       TO BP13FD05.                          
                                                                                
           SELECT BP13KD09  ASSIGN       TO BP13KD09                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KD09-NUM-REGN                       
                            FILE STATUS  IS WS-KD09-STATUS.                     
                                                                                
           SELECT BP1311CA   ASSIGN       TO BP1311CA.                          
           SELECT BP1311CB   ASSIGN       TO BP1311CB.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13FD05                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BP13FD05.                                                           
                                                                                
       FD   BP13KD09.                                                           
       COPY BP13KD09.                                                           
                                                                                
       FD   BP1311CA                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01 BP1311CA-REC            PIC X(200).                                   
                                                                                
       FD   BP1311CB                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01 BP1311CB-REC            PIC X(200).                                   
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-KD09-STATUS                PIC 99     VALUE 00.                   
       01  WS-EOF-FD05                   PIC X      VALUE 'N'.                  
       01  WS-FIRST-A                    PIC X      VALUE 'Y'.                  
       01  WS-FIRST-B                    PIC X      VALUE 'Y'.                  
                                                                                
       01  WS-CTR.                                                              
           05  WS-FD05-CNT-READ          PIC 9(7)   VALUE ZEROES.               
           05  WS-KD09-CNT-FND           PIC 9(7)   VALUE ZEROES.               
           05  WS-KD09-CNT-NFND          PIC 9(7)   VALUE ZEROES.               
           05  WS-W-CANCEL-DTE           PIC 9(7)   VALUE ZEROES.               
           05  WS-WO-CANCEL-DTE          PIC 9(7)   VALUE ZEROES.               
                                                                                
       01  WS-DATE.                                                             
           05  WS-DATE-YYYY              PIC X(4)   VALUE SPACES.               
           05  WS-DATE-MM                PIC X(2)   VALUE SPACES.               
           05  WS-DATE-DD                PIC X(2)   VALUE SPACES.               
                                                                                
       01  WS-RPT-HEADER1.                                                      
           05  FILLER                    PIC X(6)   VALUE 'IC NO.'.             
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  FILLER                    PIC X(18)                              
                                         VALUE 'SCHEME ACCOUNT NO.'.            
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  FILLER                    PIC X(16)                              
                                         VALUE 'REGISTRATION NO.'.              
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  FILLER                    PIC X(18)                              
                                         VALUE 'AHG DISBURSED DATE'.            
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  FILLER                    PIC X(18)                              
                                         VALUE 'SHG DISBURSED DATE'.            
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  FILLER                    PIC X(05)  VALUE 'AHG $'.              
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  FILLER                    PIC X(10)  VALUE 'AHG RECY $'.         
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  FILLER                    PIC X(05)  VALUE 'SHG $'.              
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  FILLER                    PIC X(10)  VALUE 'SHG RECY $'.         
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  FILLER                    PIC X(05)  VALUE 'SUG $'.              
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  FILLER                    PIC X(15)                              
                                         VALUE 'KEY ISSUED DATE'.               
                                                                                
       01  WS-RPT-DETAIL1.                                                      
           05  WS-IC-NO                  PIC X(9)   VALUE SPACES.               
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  WS-SCHEME-ACCT            PIC X(11)  VALUE SPACES.               
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  WS-REG-NO                 PIC X(8)   VALUE SPACES.               
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  WS-AHG-DISBURSE-DTE       PIC X(8)   VALUE SPACES.               
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  WS-SHG-DISBURSE-DTE       PIC X(8)   VALUE SPACES.               
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  WS-AHG-GRANT              PIC ZZZZZ.ZZ VALUE ZEROES.             
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  WS-AHG-RECOVER            PIC ZZZZZ.ZZ VALUE ZEROES.             
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  WS-SHG-GRANT              PIC ZZZZZ.ZZ VALUE ZEROES.             
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  WS-SHG-RECOVER            PIC ZZZZZ.ZZ VALUE ZEROES.             
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  WS-SUG-GRANT              PIC ZZZZZ.ZZ VALUE ZEROES.             
           05  FILLER                    PIC X(1)   VALUE ','.                  
           05  WS-KEY-ISSUED-DTE         PIC X(8)   VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-ROUTINE         THRU  1000-EXIT.                  
            PERFORM 2000-READ-BP13FD05        THRU  2000-EXIT                   
                         UNTIL WS-EOF-FD05 = 'Y'.                               
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            OPEN INPUT  BP13FD05                                                
                        BP13KD09                                                
                 OUTPUT BP1311CA                                                
                        BP1311CB                                                
                                                                                
           IF WS-KD09-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-KD09-STATUS ' WS-KD09-STATUS           
              MOVE     WS-KD09-STATUS TO RETURN-CODE                            
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2000-READ-BP13FD05.                                                      
      *-------------------------------------------------------------            
           READ BP13FD05                                                        
                AT END MOVE 'Y' TO WS-EOF-FD05                                  
                GO TO  2000-EXIT.                                               
                                                                                
           ADD 1 TO WS-FD05-CNT-READ                                            
                                                                                
           PERFORM 2500-READ-BP13KD09 THRU 2500-EXIT.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2500-READ-BP13KD09.                                                      
      *-------------------------------------------------------------            
           MOVE FD05-NUM-REGN TO KD09-NUM-REGN.                                 
           READ BP13KD09 KEY IS KD09-NUM-REGN.                                  
                                                                                
           EVALUATE WS-KD09-STATUS                                              
           WHEN  00                                                             
                 ADD   1    TO WS-KD09-CNT-FND                                  
                 PERFORM 3000-CREATE-REPORT THRU 3000-EXIT                      
           WHEN  23                                                             
                 ADD   1    TO WS-KD09-CNT-NFND                                 
                 DISPLAY 'BP13KD09 RECORD NOT FOUND  = ' KD09-KEY-FLD           
           WHEN  OTHER                                                          
                 DISPLAY 'ERROR READING BP13KD09 = '                            
                                WS-KD09-STATUS                                  
                 DISPLAY '      KD09-KEY-FLD     = '                            
                                                    KD09-KEY-FLD                
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       2500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3000-CREATE-REPORT.                                                      
      *-------------------------------------------------------------            
                                                                                
            MOVE SPACES TO  WS-IC-NO  WS-SCHEME-ACCT WS-REG-NO  WS-DATE         
                            WS-AHG-DISBURSE-DTE WS-SHG-DISBURSE-DTE             
                            WS-KEY-ISSUED-DTE.                                  
            INITIALIZE      WS-AHG-GRANT WS-AHG-RECOVER WS-SHG-GRANT            
                            WS-SUG-GRANT  WS-SHG-RECOVER.                       
                                                                                
            MOVE FD05-NUM-NRIC TO  WS-IC-NO                                     
            MOVE FD05-NUM-REGN TO  WS-REG-NO                                    
            IF   KD09-NUM-SCH-ACCNT(1:1) = '#'                                  
            MOVE SPACES               TO WS-SCHEME-ACCT                         
            ELSE                                                                
            MOVE KD09-NUM-SCH-ACCNT   TO WS-SCHEME-ACCT.                        
            MOVE KD09-DTE-AHG-PAYMENT  TO  WS-DATE                              
            STRING WS-DATE-DD WS-DATE-MM  WS-DATE-YYYY                          
                   DELIMITED BY SIZE INTO WS-AHG-DISBURSE-DTE                   
            END-STRING.                                                         
                                                                                
            MOVE SPACES TO WS-DATE                                              
            MOVE KD09-DTE-SHG-PAYMENT TO  WS-DATE                               
            STRING WS-DATE-DD WS-DATE-MM  WS-DATE-YYYY                          
                   DELIMITED BY SIZE INTO WS-SHG-DISBURSE-DTE                   
            END-STRING.                                                         
                                                                                
            IF FD05-AMT-AHG-GRANT IS NOT NUMERIC                                
               MOVE ZEROES TO FD05-AMT-AHG-GRANT                                
            END-IF.                                                             
            IF FD05-AMT-AHG-GRANT-RECOVER IS NOT NUMERIC                        
               MOVE ZEROES TO FD05-AMT-AHG-GRANT-RECOVER                        
            END-IF.                                                             
            IF FD05-AMT-SHG-GRANT IS NOT NUMERIC                                
               MOVE ZEROES TO FD05-AMT-SHG-GRANT                                
            END-IF.                                                             
            IF FD05-AMT-SUG-GRANT IS NOT NUMERIC                                
               MOVE ZEROES TO FD05-AMT-SUG-GRANT                                
            END-IF.                                                             
            IF FD05-AMT-SHG-GRANT-RECOVER IS NOT NUMERIC                        
               MOVE ZEROES TO FD05-AMT-SHG-GRANT-RECOVER                        
            END-IF.                                                             
            MOVE FD05-AMT-AHG-GRANT      TO  WS-AHG-GRANT                       
            MOVE FD05-AMT-AHG-GRANT-RECOVER TO WS-AHG-RECOVER                   
            MOVE FD05-AMT-SHG-GRANT       TO  WS-SHG-GRANT                      
            MOVE FD05-AMT-SHG-GRANT-RECOVER TO WS-SHG-RECOVER                   
            MOVE FD05-AMT-SUG-GRANT       TO  WS-SUG-GRANT                      
                                                                                
            IF KD09-DTE-KEY-ISSUED = SPACES OR LOW-VALUES                       
               MOVE SPACES TO KD09-DTE-KEY-ISSUED                               
               MOVE SPACES TO WS-DATE                                           
            ELSE                                                                
            MOVE KD09-DTE-KEY-ISSUED  TO WS-DATE                                
            END-IF.                                                             
            MOVE KD09-DTE-KEY-ISSUED  TO WS-DATE                                
            STRING WS-DATE-DD WS-DATE-MM  WS-DATE-YYYY                          
                   DELIMITED BY SIZE INTO WS-KEY-ISSUED-DTE                     
            END-STRING.                                                         
                                                                                
            IF (KD09-DTE-CANCEL NOT = SPACES AND LOW-VALUES)                    
               OR KD09-NUM-SCH-ACCNT(1:1) = '#'                                 
               IF WS-FIRST-A = 'Y'                                              
                  MOVE SPACES TO  BP1311CA-REC                                  
                  WRITE BP1311CA-REC FROM WS-RPT-HEADER1                        
                  MOVE 'N' TO WS-FIRST-A                                        
               END-IF                                                           
               ADD 1 TO WS-W-CANCEL-DTE                                         
               MOVE SPACES TO  BP1311CA-REC                                     
               WRITE BP1311CA-REC FROM WS-RPT-DETAIL1                           
            ELSE                                                                
               IF WS-FIRST-B = 'Y'                                              
                  MOVE SPACES TO  BP1311CB-REC                                  
                  WRITE BP1311CB-REC FROM WS-RPT-HEADER1                        
                  MOVE 'N' TO WS-FIRST-B                                        
               END-IF                                                           
               ADD 1 TO WS-WO-CANCEL-DTE                                        
               MOVE SPACES TO  BP1311CB-REC                                     
               WRITE BP1311CB-REC FROM WS-RPT-DETAIL1                           
            END-IF.                                                             
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
            DISPLAY ' BP13C11C REPORT ----------------'.                        
            DISPLAY ' '.                                                        
            DISPLAY 'NO READ IN BP13FD05      = ' WS-FD05-CNT-READ              
            DISPLAY 'NO FOUND IN BP13KD09     = ' WS-KD09-CNT-FND               
            DISPLAY 'NO NOT FOUND IN BP13KD09 = ' WS-KD09-CNT-NFND              
            DISPLAY 'NO OF CASE WITH CANCEL DTE = ' WS-W-CANCEL-DTE             
            DISPLAY 'NO OF CASE WITH NO CANCEL DTE = ' WS-WO-CANCEL-DTE         
            DISPLAY ' '.                                                        
                                                                                
            CLOSE BP13KD09                                                      
                  BP13FD05                                                      
                  BP1311CA                                                      
                  BP1311CB.                                                     
                                                                                
            IF WS-KD09-STATUS   NOT = 00 AND 97                                 
               DISPLAY 'CLOSING ERROR BP13KD09, STAT ' WS-KD09-STATUS.          
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
