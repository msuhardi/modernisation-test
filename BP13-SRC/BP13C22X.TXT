       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C22X.                                                 
      *AUTHOR.        BHASKAR RAJU G.                                           
      *DATE-WRITTEN.  07/06/2000.                                               
      *COPY OF.       BP13C224.                                                 
      * ======================================================== *              
      * SYSTEM OF COMMITMENT (BP13)                              *              
      * ======================================================== *              
      *    OBJECTIVE  :                                          *              
      *      1.  PROGRAM TO WRITE LIST OF UNIT NO'S IN DIFFERENT *              
      *          BLOCKS AND STREETS FOR A GIVEN ZONE OR NEW TOWN.*              
      *                                                          *              
      *     INPUT FILES:                                         *              
      *           1. BP13K270                                    *              
      *           2. BP13K060                                    *              
      *           3. BM06K100                                    *              
      *           4. BB14K500                                    *              
      *           5. BB14K580                                    *              
      *                                                          *              
      *     I-O  FILES:                                          *              
      *           1. BM06F800                                    *              
      *                                                          *              
      *     OUTPUT FILES:                                        *              
      *           1. BP13F999                                    *              
      * -------------------------------------------------------- *              
      *  CHG NO     DATE     BY   DESCRIPTION                    *              
      * -------- ----------  ---  ------------------------------ *              
      * BP132562 06/10/2005  GAN  NEW PROGRAM.                   *              
      * BP132835 20/03/2006  LMS  TO CHANGE CURR SP TO OFFER SP  *              
      * BP132878 09/06/2006  GAN  REMOVAL OF F999-DTE-UPDATE     *              
      * BP133065 11/05/2007  LMS1 CATER FOR BLKS WITH NO POD BUT *              
      *                           WITH POF OPTION.               *              
      * BP133132 03/07/2008  LMS1 REMOVE CHECKING FOR SITE M.    *              
      * BP133390 30/07/2008  LMS1 TO EXPAND BP13F999 FROM 200 TO *              
      *                           300.                           *              
      * BP133430 02/10/2008  LMS1 TO ADD PRCNCT NAME.            *              
      * BP133956 23/08/2010  ESA1 TO CATER FOR MODEL-DESIGN 86,87*              
      *                           AND 88                         *              
      * BP134731 09/11/2012  PCL4 TO CATER FOR MODL-DESIGN 89    *              
      * BP135838 01/07/2015  ESA1 TO CATER FOR MODL-DESIGN 92    *              
      * BP135856 03/09/2015  ESA1 TO CATER FOR FT-STAT ='ST' &   *              
      *                           RP-CODE = '20'                 *              
      * BP136517 12/12/2016  ESA1 TO USE K580-DTE-LEASE-COMM FOR *              
      *                           LCD IN THE HARDCOPY FLATLIST   *              
      * BP136835 02/08/2017  ESA1 TO ADD READING OF BB14K990     *              
      * ======================================================== *              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13K270 ASSIGN TO BP13K270                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K270-KEY-FLD                                    
                FILE STATUS  IS BP13K270-STATUS.                                
                                                                                
           SELECT BP13K060 ASSIGN TO BP13K060                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K060-KEY-FLD                                    
                FILE STATUS  IS BP13K060-STATUS.                                
                                                                                
           SELECT BP13K260 ASSIGN TO BP13K260                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K260-KEY-FLD                                    
                FILE STATUS  IS BP13K260-STATUS.                                
                                                                                
                                                                                
           SELECT BM06K100 ASSIGN TO BM06K100                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K100-KEY-FLD                                    
                FILE STATUS  IS BM06K100-STATUS.                                
                                                                                
           SELECT BB14K500 ASSIGN TO BB14K500                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K500-KEY-FLD                                    
                FILE STATUS  IS BB14K500-STATUS.                                
                                                                                
           SELECT BB14K580 ASSIGN TO BB14K580                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K580-KEY-FLD                                    
                FILE STATUS  IS BB14K580-STATUS.                                
                                                                                
           SELECT BB14K990 ASSIGN TO BB14K990                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K990-KEY-FLD                                    
                FILE STATUS  IS BB14K990-STATUS.                                
                                                                                
           SELECT BM06F800 ASSIGN TO BM06F800.                                  
           SELECT BP13F999 ASSIGN TO BP13F999.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13K270                 RECORD CONTAINS 250 CHARACTERS.            
       COPY BP13K270.                                                           
                                                                                
       FD   BP13K060                 RECORD CONTAINS 25 CHARACTERS.             
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K260                 RECORD CONTAINS 100 CHARACTERS.            
       COPY BP13K260.                                                           
                                                                                
       FD   BB14K500                 RECORD CONTAINS 400 CHARACTERS.            
       COPY BB14K500.                                                           
                                                                                
       FD   BB14K580                 RECORD CONTAINS 300 CHARACTERS.            
       COPY BB14K580.                                                           
                                                                                
       FD   BB14K990                 RECORD CONTAINS 200 CHARACTERS.            
       COPY BB14K990.                                                           
                                                                                
       FD   BM06K100                 RECORD CONTAINS 500 CHARACTERS.            
       COPY BM06K100.                                                           
                                                                                
       FD  BM06F800                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BM06F800.                                                           
                                                                                
       FD  BP13F999                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F999.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-STATUS.                                                      
           05  BP13K270-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BP13K060-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BP13K260-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BM06K100-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BB14K500-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BB14K580-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BB14K990-STATUS              PIC 9(02)  VALUE ZEROS.             
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800-READ                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K270-READ                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K500-READ                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K580-READ                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K990-READ                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K100-READ                 PIC 9(08)  VALUE ZEROS.             
           05  WS-F999-WRITE                PIC 9(08)  VALUE ZEROS.             
           05  WS-F800-WRITE                PIC 9(08)  VALUE ZEROS.             
           05  WS-K100-NFND                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K270-NFND                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K500-NFND                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K580-NFND                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K060-NFND                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K260-NFND                 PIC 9(08)  VALUE ZEROS.             
           05  WS-K990-NFND                 PIC 9(08)  VALUE ZEROS.             
                                                                                
       01  WS-FILE-VARIABLES.                                                   
           05 WS-K500-DTE-EST-CMPL          PIC X(08)  VALUE SPACES.            
           05 WS-K500-DTE-LSE-COMM          PIC X(08)  VALUE SPACES.            
           05 WS-K500-LEASE-PERIOD-FLAT     PIC 9(02)  VALUE ZEROES.            
           05 WS-K100-CDE-BLK               PIC X(05)  VALUE SPACES.            
           05 WS-K100-DTE-KEY-AVBL          PIC X(08)  VALUE SPACES.            
           05 WS-K100-DTE-OCS               PIC X(08)  VALUE SPACES.            
           05 WS-K100-DTE-POD-DUE           PIC X(08)  VALUE SPACES.            
           05 WS-K100-DTE-POF-DUE           PIC X(08)  VALUE SPACES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-FLAT-TYPE                  PIC X(02)  VALUE SPACES.            
           05 WS-NT-ESTATE                  PIC X(27)  VALUE SPACES.            
           05 WS-NOT-MATCHED                PIC 9(08)  VALUE ZEROES.            
           05 WS-MATCH                      PIC X(01)  VALUE SPACES.            
           05 WS-K060-FND                   PIC X(01)  VALUE SPACES.            
           05 WS-CURRENT-DTE                PIC 9(08)  VALUE ZEROES.            
           05 WS-DIFF-LSE-COMM              PIC S9(08) VALUE ZEROES.            
           05 WS-DIFF-OCS                   PIC S9(08) VALUE ZEROES.            
           05 WS-DIFF-POD                   PIC S9(08) VALUE ZEROES.            
           05 WS-TMP-DTE-LSE-COMM           PIC 9(08)  VALUE ZEROES.            
           05 WS-TMP-DTE-OCS                PIC 9(08)  VALUE ZEROES.            
           05 WS-TMP-DTE-POD-DUE            PIC 9(08)  VALUE ZEROES.            
           05 WS-TMP-DATE                   PIC 9(08)  VALUE ZEROES.            
           05 WS-CURR-DATE                  PIC 9(08)  VALUE ZEROES.            
           05 WS-EDT-TMP                    PIC Z(7)9.                          
           05 WS-DEL-POSSN-DATE.                                                
              10  WS-DEL-POSSN-YYYY         PIC X(4).                           
              10  WS-DEL-POSSN-MM           PIC X(2).                           
              10  WS-DEL-POSSN-DD           PIC X(2).                           
           05  WS-8-DATE-CHAR               PIC X(8).                           
           05  WS-8-DATE  REDEFINES WS-8-DATE-CHAR.                             
               10  WS-8-DATE-YYYY           PIC 9(4).                           
               10  WS-8-DATE-MM             PIC 9(2).                           
               10  WS-8-DATE-DD             PIC 9(2).                           
           05  WS-DAY                       PIC X(2).                           
           05  WS-MONTH                     PIC X(2).                           
               88  MTH-END-WITH-30    VALUE '04' '06' '09' '11'.                
               88  MTH-END-WITH-28    VALUE '02'.                               
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  WS-F800-FLAG                 PIC X(01)  VALUE 'N'.               
               88 WS-F800-EOF                          VALUE 'Y'.               
           05  WS-K260-FND                  PIC X(01)  VALUE 'N'.               
                                                                                
       01  WS-EDT-DATE.                                                         
           05  WS-EDT-DD                    PIC 9(02).                          
           05  FILLER                       PIC X      VALUE '/'.               
           05  WS-EDT-MM                    PIC 9(02).                          
           05  FILLER                       PIC X      VALUE '/'.               
           05  WS-EDT-CCYY                  PIC 9(04).                          
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY                  PIC 9(04).                          
           05  WS-CUR-MM                    PIC 9(02).                          
           05  WS-CUR-DD                    PIC 9(02).                          
                                                                                
       01  WS-LCOMM-DTE.                                                        
           05  WS-LCOMM-YYYY                 PIC 9(4).                          
           05  WS-LCOMM-MM                   PIC 9(2).                          
           05  WS-LCOMM-DD                   PIC 9(2).                          
                                                                                
       01  WS-YEAR-COMM                      PIC 99.                            
       01  WS-MONTH-COMM                     PIC 99.                            
                                                                                
      *---------------------------------------------------------------*         
      *        LINKAGE FOR BP13C916                                   *         
      *---------------------------------------------------------------*         
       01  WS-LINK-REC.                                                         
           05  WS-LINK-BLDNG-GL             PIC X(5).                           
                                                                                
       01  WS-BP13C916-REC.                                                     
           05  C916-NME-LOCAL-PRCNCT        PIC X(40).                          
           05  C916-CDE-SYSERR              PIC S9(3).                          
           05  C916-CDE-ERROR               PIC X(5).                           
                                                                                
       PROCEDURE DIVISION.                                                      
      *-----------------------------------------------------------              
       000-MAIN-ROUTINE.                                                        
      *-----------------------------------------------------------              
           PERFORM 100-OPEN-FILES  THRU 100-EXIT.                               
           PERFORM 300-PROCESS-REC THRU 300-EXIT                                
                   UNTIL WS-F800-EOF.                                           
                                                                                
           PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-----------------------------------------------------------              
           OPEN INPUT  BP13K270                                                 
                       BP13K060                                                 
                       BP13K260                                                 
                       BM06K100                                                 
                       BB14K500                                                 
                       BB14K580                                                 
                       BB14K990                                                 
                I-O    BM06F800                                                 
                OUTPUT BP13F999.                                                
                                                                                
           IF BM06K100-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BM06K100 STATUS ' BM06K100-STATUS            
              MOVE BM06K100-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF BB14K500-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BB14K500 STATUS ' BB14K500-STATUS            
              MOVE BB14K500-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF BB14K580-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BB14K580 STATUS ' BB14K580-STATUS            
              MOVE BB14K580-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF BB14K990-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BB14K990 STATUS ' BB14K990-STATUS            
              MOVE BB14K990-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K270-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BP13K270 STATUS ' BP13K270-STATUS            
              MOVE BP13K270-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K060-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BP13K060 STATUS ' BP13K060-STATUS            
              MOVE BP13K060-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K260-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BP13K260 STATUS ' BP13K260-STATUS            
              MOVE BP13K260-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
           MOVE WS-CUR-CCYY           TO WS-EDT-CCYY.                           
           MOVE WS-CUR-MM             TO WS-EDT-MM.                             
           MOVE WS-CUR-DD             TO WS-EDT-DD.                             
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-REC.                                                         
      *-----------------------------------------------------------              
                                                                                
           MOVE SPACES                   TO WS-FILE-VARIABLES                   
                                            BP13F999-RECORD.                    
           INITIALIZE                       BP13F999-RECORD.                    
                                                                                
           PERFORM 400-BM06F800-READ     THRU 400-EXIT.                         
           IF WS-F800-EOF                                                       
              GO TO 300-EXIT                                                    
           END-IF.                                                              
                                                                                
           IF (F800-DTE-ACCEPTANCE = ZEROS OR SPACES OR LOW-VALUES) AND         
              (F800-DTE-KEY-ISSUED = ZEROS OR SPACES OR LOW-VALUES)             
              CONTINUE                                                          
           ELSE                                                                 
              ADD  1       TO WS-NOT-MATCHED                                    
              GO TO 300-EXIT                                                    
           END-IF.                                                              
                                                                                
      *** DECODE FLAT TYPE AND VALIDATE ZONE                                    
           PERFORM 440-DECODE-FLAT-TYPE  THRU 440-EXIT.                         
           MOVE WS-FLAT-TYPE             TO   F999-NUM-FLAT-TYPE.               
                                                                                
           PERFORM 410-PROCESS-F800      THRU 410-EXIT.                         
           PERFORM 420-BP13K270-READ     THRU 420-EXIT.                         
                                                                                
           PERFORM 450-BM06K100-READ      THRU 450-EXIT.                        
           PERFORM 460-BB14K500-READ      THRU 460-EXIT.                        
           PERFORM 700-WRITE-BP13F999     THRU 700-EXIT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-BM06F800-READ.                                                       
      *-----------------------------------------------------------              
           READ BM06F800 AT END                                                 
                MOVE 'Y' TO WS-F800-FLAG                                        
                GO TO 400-EXIT.                                                 
                                                                                
           ADD 1 TO WS-F800-READ.                                               
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       410-PROCESS-F800.                                                        
      *-----------------------------------------------------------              
           MOVE SPACES                 TO   K060-KEY-FLD                        
                                            WS-NT-ESTATE.                       
           MOVE '02'                   TO   K060-SERIAL-NO.                     
           MOVE F800-CDE-NT            TO   K060-CODE.                          
                                                                                
           PERFORM 600-BP13K060-READ   THRU 600-EXIT.                           
                                                                                
           IF WS-K060-FND = 'Y'                                                 
              MOVE K060-DESC              TO   F999-NME-ESTATE                  
              MOVE F800-CDE-NT            TO   F999-CDE-NT                      
           ELSE                                                                 
              MOVE SPACES                 TO   F999-NME-ESTATE                  
                                               F999-CDE-NT                      
           END-IF.                                                              
                                                                                
           IF (F800-CDE-SELTY = '3R ') AND                                      
              (F800-STREET-NAME = 'YUAN CHING RD       ')                       
              MOVE 'JW1'                  TO F999-CDE-NT                        
              MOVE 'TAMAN JURONG        ' TO F999-NME-ESTATE                    
           END-IF.                                                              
                                                                                
           IF F800-CDE-ZONE = '3R' OR 'RE' OR 'ME' OR 'ZN'                      
              MOVE SPACES                 TO   F999-NUM-ZONE                    
           ELSE                                                                 
              IF F800-CDE-SELTY = 'BTO'                                         
                 MOVE F800-NUM-BTO-OPTION TO   F999-NUM-ZONE                    
              ELSE                                                              
                 MOVE F800-CDE-ZONE       TO   F999-NUM-ZONE                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE F800-NUM-SITE             TO   F999-NUM-SITE.                   
           MOVE F800-CDE-MODL-DESG        TO   F999-NUM-MODL-DESG.              
           MOVE F800-DTE-ALLOCN           TO   F999-DTE-ALLOC.                  
           MOVE F800-SCH-ACC-NO           TO   F999-NUM-HDB-REF.                
           MOVE F800-CDE-SELTY            TO   F999-NUM-SELTY.                  
                                                                                
           MOVE F800-NUM-UPG-STATUS       TO   F999-NUM-UPG-STATUS.             
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
       420-BP13K270-READ.                                                       
      *-----------------------------------------------------------              
           MOVE LOW-VALUES           TO K270-KEY-FLD.                           
           MOVE F800-DTE-ALLOCN      TO K270-ALLOCN-DATE.                       
           MOVE F800-ESTATE          TO K270-ESTATE.                            
           MOVE F800-NEIGHBOURHOOD   TO K270-NEIGHBOURHOOD.                     
           MOVE F800-CONTRACT-NO     TO K270-CONTRACT-NO.                       
           MOVE F800-BLK-NO          TO K270-NUM-BLK.                           
                                                                                
           READ BP13K270.                                                       
           EVALUATE BP13K270-STATUS                                             
           WHEN 00                                                              
              ADD   1          TO WS-K270-READ                                  
              PERFORM 430-RETRIEVE-MAX-CAT  THRU 430-EXIT                       
           WHEN 23                                                              
              ADD   1          TO WS-K270-NFND                                  
              MOVE ZEROES      TO F999-NUM-MAX-ALLOWABLE                        
              DISPLAY 'RECORD NOT FOUND BP13K270 : ' K270-KEY-FLD               
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K270 : ' BP13K270-STATUS               
              MOVE BP13K270-STATUS            TO  RETURN-CODE                   
              PERFORM 999-CLOSE-ROUTINE       THRU 999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
       430-RETRIEVE-MAX-CAT.                                                    
      *-----------------------------------------------------------              
                                                                                
      **-- COMPUTE MAX CAT FOR PINNACLE@DUXTON                                  
      *    IF F800-CDE-SELTY = 'BTO' AND                                        
      *       F800-NUM-SITE = 'M'                                               
      *       PERFORM 432-COMPUTE-SPECIAL-MAXCAT      THRU 432-EXIT             
      *       GO TO 430-EXIT                                                    
      *    END-IF.                                                              
                                                                                
           EVALUATE F800-CDE-TYP-ACTUSE                                         
           WHEN '01'                                                            
              MOVE  K270-NUM-MAX-CAT1(1)     TO F999-NUM-MAX-CAT1               
              MOVE  K270-NUM-MAX-CAT2(1)     TO F999-NUM-MAX-CAT2               
              MOVE  K270-NUM-MAX-CAT3(1)     TO F999-NUM-MAX-CAT3               
           WHEN '02'                                                            
              MOVE  K270-NUM-MAX-CAT1(2)     TO F999-NUM-MAX-CAT1               
              MOVE  K270-NUM-MAX-CAT2(2)     TO F999-NUM-MAX-CAT2               
              MOVE  K270-NUM-MAX-CAT3(2)     TO F999-NUM-MAX-CAT3               
           WHEN '03'                                                            
              MOVE  K270-NUM-MAX-CAT1(3)     TO F999-NUM-MAX-CAT1               
              MOVE  K270-NUM-MAX-CAT2(3)     TO F999-NUM-MAX-CAT2               
              MOVE  K270-NUM-MAX-CAT3(3)     TO F999-NUM-MAX-CAT3               
           WHEN '04'                                                            
              COMPUTE F999-NUM-MAX-CAT1 = K270-NUM-MAX-CAT1(4) +                
                                          K270-NUM-MAX-CAT1(5)                  
              COMPUTE F999-NUM-MAX-CAT2 = K270-NUM-MAX-CAT2(4) +                
                                          K270-NUM-MAX-CAT2(5)                  
              COMPUTE F999-NUM-MAX-CAT3 = K270-NUM-MAX-CAT3(4) +                
                                          K270-NUM-MAX-CAT3(5)                  
           WHEN '05'                                                            
              MOVE  K270-NUM-MAX-CAT1(6)     TO F999-NUM-MAX-CAT1               
              MOVE  K270-NUM-MAX-CAT2(6)     TO F999-NUM-MAX-CAT2               
              MOVE  K270-NUM-MAX-CAT3(6)     TO F999-NUM-MAX-CAT3               
           WHEN '06'                                                            
              COMPUTE F999-NUM-MAX-CAT1 = K270-NUM-MAX-CAT1(7) +                
                                          K270-NUM-MAX-CAT1(9)                  
              COMPUTE F999-NUM-MAX-CAT2 = K270-NUM-MAX-CAT2(7) +                
                                          K270-NUM-MAX-CAT2(9)                  
              COMPUTE F999-NUM-MAX-CAT3 = K270-NUM-MAX-CAT3(7) +                
                                          K270-NUM-MAX-CAT3(9)                  
           END-EVALUATE.                                                        
                                                                                
       430-EXIT.                                                                
           EXIT.                                                                
                                                                                
       432-COMPUTE-SPECIAL-MAXCAT.                                              
      *-----------------------------------------------------------              
                                                                                
           COMPUTE F999-NUM-MAX-CAT1 = (K270-NUM-ALLOC (4) +                    
                                        K270-NUM-ALLOC (6)) * 0.25.             
                                                                                
           COMPUTE F999-NUM-MAX-CAT2 = (K270-NUM-ALLOC (4) +                    
                                        K270-NUM-ALLOC (6)) * 0.87.             
                                                                                
           COMPUTE F999-NUM-MAX-CAT3 = (K270-NUM-ALLOC (4) +                    
                                        K270-NUM-ALLOC (6)) * 0.13.             
                                                                                
       432-EXIT.                                                                
           EXIT.                                                                
                                                                                
       440-DECODE-FLAT-TYPE.                                                    
      *-----------------------------------------------------------              
                                                                                
           EVALUATE F800-CDE-TYP-ACTUSE                                         
           WHEN '01'                                                            
              IF F800-CDE-MODL-DESG = '80' OR '89' OR '92'                      
                 MOVE '1A'      TO WS-FLAT-TYPE                                 
              ELSE                                                              
                 MOVE '1 '      TO WS-FLAT-TYPE                                 
              END-IF                                                            
           WHEN '02'                                                            
              IF F800-CDE-MODL-DESG = '80' OR '89' OR '92'                      
                 MOVE '2A'      TO WS-FLAT-TYPE                                 
              ELSE                                                              
                 MOVE '2 '      TO WS-FLAT-TYPE                                 
              END-IF                                                            
           WHEN '03'                                                            
              MOVE '3 '         TO WS-FLAT-TYPE                                 
           WHEN '04'                                                            
              MOVE '4 '         TO WS-FLAT-TYPE                                 
           WHEN '05'                                                            
              MOVE '5 '         TO WS-FLAT-TYPE                                 
           WHEN '06'                                                            
              MOVE 'E '         TO WS-FLAT-TYPE                                 
           END-EVALUATE.                                                        
                                                                                
      *** DECODE FLAT CODE                                                      
                                                                                
           IF (F800-FLAT-STATUS = 'RP') OR                                      
              (F800-FLAT-STATUS = 'ST' AND F800-REPOSSESSED-CODE = '40')        
                               OR                                               
              (F800-FLAT-STATUS = 'ST' AND F800-REPOSSESSED-CODE = '20')        
              MOVE 'RP'        TO F999-NUM-FLAT-CODE                            
           ELSE                                                                 
              IF (F800-NUM-FLAT-DESC = SPACES OR LOW-VALUES) AND                
                 (F800-CDE-POF-TAG = 'Z')                                       
                 MOVE 'FF'        TO F999-NUM-FLAT-CODE                         
              ELSE                                                              
                 MOVE F800-NUM-FLAT-DESC TO F999-NUM-FLAT-CODE                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       440-EXIT.                                                                
           EXIT.                                                                
                                                                                
       450-BM06K100-READ.                                                       
      *-----------------------------------------------------------              
           MOVE LOW-VALUES           TO K100-KEY-FLD.                           
           MOVE F800-ESTATE          TO K100-ESTATE.                            
           MOVE F800-NEIGHBOURHOOD   TO K100-NEIGHBOURHOOD.                     
           MOVE F800-CONTRACT-NO     TO K100-CONTRACT-NO.                       
           MOVE F800-BLK-NO          TO K100-BLK-NO.                            
                                                                                
           READ BM06K100.                                                       
           EVALUATE BM06K100-STATUS                                             
           WHEN 00                                                              
              ADD  1      TO WS-K100-READ                                       
              MOVE K100-CDE-BLK          TO WS-K100-CDE-BLK                     
              MOVE K100-DTE-KEY-AVAIL    TO WS-K100-DTE-KEY-AVBL                
              MOVE K100-DTE-OCS-DUE      TO WS-K100-DTE-OCS                     
              MOVE K100-DTE-POD-DUE      TO WS-K100-DTE-POD-DUE                 
              MOVE K100-DTE-POF-DUE      TO WS-K100-DTE-POF-DUE                 
           WHEN 23                                                              
              MOVE SPACES                TO WS-K100-CDE-BLK                     
              MOVE '00000000'            TO WS-K100-DTE-KEY-AVBL                
                                            WS-K100-DTE-OCS                     
                                            WS-K100-DTE-POD-DUE                 
                                            WS-K100-DTE-POF-DUE                 
              DISPLAY 'RECORD NOT FOUND BM06K100 : ' K100-KEY-FLD               
              ADD   1          TO WS-K100-NFND                                  
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BM06K100 : ' BM06K100-STATUS               
              MOVE BM06K100-STATUS            TO  RETURN-CODE                   
              PERFORM 999-CLOSE-ROUTINE       THRU 999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
       460-BB14K500-READ.                                                       
      *-----------------------------------------------------------              
           MOVE LOW-VALUES           TO K500-KEY-FLD.                           
           MOVE WS-K100-CDE-BLK      TO K500-CDE-BLK.                           
                                                                                
           READ BB14K500.                                                       
           EVALUATE BB14K500-STATUS                                             
           WHEN 00                                                              
              ADD   1      TO WS-K500-READ                                      
              MOVE K500-DTE-BLDG-COMPLETE   TO WS-K500-DTE-EST-CMPL             
              MOVE K500-NUM-LEASE-PERIOD-FLAT TO                                
                   WS-K500-LEASE-PERIOD-FLAT                                    
              MOVE K500-DTE-LEASE-COMM-FLAT TO WS-K500-DTE-LSE-COMM             
           WHEN 23                                                              
              ADD   1          TO WS-K500-NFND                                  
              DISPLAY 'RECORD NOT FOUND BB14K500 : ' K500-KEY-FLD               
              MOVE SPACES       TO WS-K500-DTE-EST-CMPL                         
              MOVE ZEROES       TO WS-K500-DTE-LSE-COMM                         
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BB14K500 : ' BB14K500-STATUS               
              MOVE BB14K500-STATUS            TO  RETURN-CODE                   
              PERFORM 999-CLOSE-ROUTINE       THRU 999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       460-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       600-BP13K060-READ.                                                       
      *-----------------------------------------------------------              
                                                                                
           READ BP13K060.                                                       
           EVALUATE BP13K060-STATUS                                             
           WHEN 00                                                              
              MOVE 'Y'       TO WS-K060-FND                                     
           WHEN 23                                                              
              MOVE 'N'       TO WS-K060-FND                                     
              DISPLAY 'RECORD NOT FOUND BP13K060 : ' K060-KEY-FLD               
               ADD  1        TO WS-K060-NFND                                    
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K060 : ' BP13K060-STATUS               
              MOVE BP13K060-STATUS            TO  RETURN-CODE                   
              PERFORM 999-CLOSE-ROUTINE       THRU 999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       700-WRITE-BP13F999.                                                      
      *-----------------------------------------------------------              
           MOVE WS-CUR-DATE             TO WS-CURRENT-DTE.                      
                                                                                
           MOVE F800-NEIGHBOURHOOD      TO F999-NEIGHBOURHOOD.                  
           MOVE F800-CONTRACT-NO        TO F999-CONTRACT-NO.                    
           MOVE F800-CURR-SELLING-PRICE TO F999-AMT-SELLING-PRICE.              
           MOVE F800-BLK-NO             TO F999-BLOCK-NO.                       
           MOVE F800-LEVEL-NO           TO F999-LEVEL-NO.                       
           MOVE F800-MAIN-UNIT          TO F999-MAIN-UNIT.                      
           MOVE F800-SUB-UNIT           TO F999-SUB-UNIT.                       
           MOVE F800-STREET-NAME        TO F999-NME-STREET.                     
           MOVE F800-QTY-AREA-TOTAL     TO F999-QTY-AREA-TOTAL.                 
           MOVE F800-QTY-AREA-ORGNL     TO F999-QTY-AREA-ORGNL.                 
                                                                                
           MOVE K100-CDE-BLK TO WS-LINK-BLDNG-GL.                               
           DISPLAY 'BLDNG : ' WS-LINK-BLDNG-GL.                                 
                                                                                
           CALL 'BP13C916' USING WS-LINK-REC WS-BP13C916-REC.                   
           MOVE C916-NME-LOCAL-PRCNCT TO F999-NME-LOCAL-PRCNCT.                 
           DISPLAY 'PRCNCT: ' C916-NME-LOCAL-PRCNCT.                            
                                                                                
           IF WS-K100-DTE-KEY-AVBL = ZEROES OR SPACES OR LOW-VALUES             
              MOVE WS-K500-DTE-EST-CMPL   TO F999-DTE-ECD                       
              PERFORM 702-GET-DLVRY-POSSN-DATE THRU 702-EXIT                    
              MOVE WS-DEL-POSSN-DATE      TO F999-DTE-DLVRY-POSSESSION          
           ELSE                                                                 
              PERFORM 705-READ-BB14K580 THRU 705-EXIT                           
              IF K580-DTE-UNIT-DELIVERY = SPACES OR LOW-VALUES OR ZEROES        
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE SPACES TO F999-DTE-DLVRY-POSSESSION                       
              END-IF                                                            
              PERFORM 706-READ-BB14K990    THRU 706-EXIT                        
              IF K990-DTE-REV-LCD NOT = ZEROES AND SPACES AND LOW-VALUES        
                 MOVE K990-DTE-REV-LCD     TO WS-TMP-DTE-LSE-COMM               
              ELSE                                                              
                IF WS-K500-DTE-LSE-COMM = ZEROES OR SPACES OR LOW-VALUES        
                   PERFORM 701-GET-COMM-DATE THRU 701-EXIT                      
                   MOVE WS-K500-DTE-LSE-COMM TO WS-TMP-DTE-LSE-COMM             
                ELSE                                                            
                   MOVE WS-K500-DTE-LSE-COMM TO WS-TMP-DTE-LSE-COMM             
                END-IF                                                          
              END-IF                                                            
              PERFORM 704-CHK-COMM-DATE THRU 704-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE F999-DTE-DLVRY-POSSESSION                                       
             TO F800-DTE-DLVRY-POSSESSION.                                      
           MOVE WS-K500-DTE-LSE-COMM                                            
             TO F800-DTE-LEASE-COMM.                                            
           MOVE WS-K500-LEASE-PERIOD-FLAT                                       
             TO F800-NUM-LEASE-PERIOD-FLAT.                                     
           MOVE WS-K100-DTE-KEY-AVBL                                            
             TO F800-DTE-BLK-KEY-AVAIL.                                         
                                                                                
           IF (F800-CDE-TYP-ACTUSE = '01' OR '02') AND                          
              (F800-CDE-MODL-DESG  = '80' OR '89' OR '92')                      
               MOVE SPACES            TO F999-DTE-EXPIRY                        
               MOVE SPACES            TO F999-CDE-OCS-TAG                       
           ELSE                                                                 
              IF (WS-K100-DTE-POD-DUE NOT = ZEROES AND SPACES AND               
                                            LOW-VALUES) OR                      
                 (WS-K100-DTE-POF-DUE NOT = ZEROES AND SPACES AND               
                                            LOW-VALUES)                         
                 PERFORM 720-CHECK-POD-DUE THRU 720-EXIT                        
              ELSE                                                              
                 IF WS-K100-DTE-OCS NOT = ZEROES AND SPACES AND                 
                                          LOW-VALUES                            
                    PERFORM 710-CHECK-OCS-DUE THRU 710-EXIT                     
                 ELSE                                                           
                    MOVE SPACES            TO F999-DTE-EXPIRY                   
                    MOVE 'N'               TO F999-CDE-OCS-TAG                  
                    MOVE '00000000'        TO F999-DTE-POD-DUE                  
                    MOVE SPACES            TO F999-NUM-POD-DUE                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F800-CDE-TYP-ACTUSE = '04' AND                                    
                                F800-CDE-MODL-DESG = '36'                       
              MOVE '3STB'          TO F999-NUM-CNTRCT-TYP                       
           ELSE                                                                 
              IF F800-CDE-MODL-DESG = '81' OR '82' OR '86'                      
                                       OR '87' OR '88' OR '89'                  
                 MOVE '1PA '          TO F999-NUM-CNTRCT-TYP                    
              ELSE                                                              
                 IF F800-CDE-MODL-DESG = '34' OR '70' OR '71' OR                
                                         '72' OR '73'                           
                    MOVE '2DBP'       TO F999-NUM-CNTRCT-TYP                    
                 ELSE                                                           
                    MOVE '3STD'       TO F999-NUM-CNTRCT-TYP                    
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE K100-NUM-BTO-NEW-CONTRACT TO                                    
                F999-NUM-BTO-NEW-CONTRACT                                       
                F800-NUM-BTO-NEW-CONTRACT.                                      
                                                                                
           WRITE BP13F999-RECORD.                                               
           ADD 1     TO WS-F999-WRITE.                                          
                                                                                
           REWRITE F800-REC.                                                    
           ADD 1     TO WS-F800-WRITE.                                          
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       701-GET-COMM-DATE.                                                       
      *-------------------------------------------------------------            
      **                                                                        
      * CALCULATE K500-DTE-LEASE-COMM-FLAT IF K500-DTE-BLDG-COMPLETE            
      * NOT EQUAL TO ZEROES                                                     
      **                                                                        
           IF K500-DTE-BLDG-COMPLETE = LOW-VALUES OR SPACES OR                  
                                       '00000000'                               
              MOVE SPACES TO F999-CDE-KEY-AVAIL                                 
              GO TO 701-EXIT                                                    
           ELSE                                                                 
              CONTINUE                                                          
           END-IF.                                                              
                                                                                
           MOVE K500-DTE-BLDG-COMPLETE TO WS-LCOMM-DTE.                         
                                                                                
           MOVE K100-KEY-FLD           TO K260-KEY-FLD.                         
                                                                                
           MOVE SPACES                 TO WS-K260-FND.                          
           READ BP13K260.                                                       
                                                                                
           EVALUATE BP13K260-STATUS                                             
           WHEN 00                                                              
                MOVE 'Y'            TO WS-K260-FND                              
           WHEN 23                                                              
                ADD   1             TO WS-K260-NFND                             
                MOVE 'N'            TO WS-K260-FND                              
           WHEN OTHER                                                           
                DISPLAY 'BP13K260 FILE READ ERROR.:' BP13K260-STATUS            
                PERFORM 999-CLOSE-ROUTINE       THRU 999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
           IF WS-K260-FND = 'Y'                                                 
              ADD    13                TO WS-LCOMM-MM                           
           ELSE                                                                 
           IF K100-DTE-1ST-SALE  > '19960610'                                   
              ADD    13                TO WS-LCOMM-MM                           
           ELSE                                                                 
              ADD    10                TO WS-LCOMM-MM.                          
           DIVIDE WS-LCOMM-MM BY 12  GIVING    WS-YEAR-COMM                     
                                 REMAINDER WS-MONTH-COMM.                       
                                                                                
           IF WS-MONTH-COMM = 0                                                 
              MOVE 12                TO WS-LCOMM-MM                             
              ADD  1                 TO WS-LCOMM-YYYY                           
           ELSE                                                                 
              ADD  WS-YEAR-COMM      TO WS-LCOMM-YYYY                           
              MOVE WS-MONTH-COMM     TO WS-LCOMM-MM.                            
                                                                                
           MOVE 01               TO WS-LCOMM-DD.                                
           MOVE WS-LCOMM-DTE     TO WS-K500-DTE-LSE-COMM.                       
           MOVE 99               TO WS-K500-LEASE-PERIOD-FLAT.                  
                                                                                
           IF (F800-CDE-MODL-DESG = '80' OR '89' OR '92') AND                   
              (F800-CDE-TYP-ACTUSE = '01' OR '02')                              
              MOVE ZEROES           TO WS-K500-LEASE-PERIOD-FLAT                
              MOVE SPACES           TO WS-K500-DTE-LSE-COMM                     
           END-IF.                                                              
                                                                                
       701-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       702-GET-DLVRY-POSSN-DATE.                                                
      *-------------------------------------------------------------            
      *-----------------------------------------------------                    
      * **** COMPUTE DELIVERY POSSESSION DATE ***                               
      *     IF COMPLETION DATE NOT = SPACES                                     
      *        ADD 12 MONTHS TO COMPLETION DATE.                                
      *-----------------------------------------------------                    
           MOVE SPACES      TO WS-DEL-POSSN-DATE.                               
           IF K500-DTE-BLDG-COMPLETE = SPACES OR LOW-VALUES OR                  
                                       '00000000'                               
              MOVE SPACES TO F999-CDE-KEY-AVAIL                                 
              GO TO 702-EXIT                                                    
           END-IF.                                                              
                                                                                
           MOVE K500-DTE-BLDG-COMPLETE TO WS-8-DATE-CHAR                        
           ADD 12         TO WS-8-DATE-MM                                       
           IF WS-8-DATE-MM > 12                                                 
              ADD 1                    TO WS-8-DATE-YYYY                        
              SUBTRACT 12            FROM WS-8-DATE-MM                          
              MOVE WS-8-DATE-MM        TO WS-MONTH                              
              MOVE WS-8-DATE-DD        TO WS-DAY                                
              PERFORM 703-CHECK-DAY       THRU 703-EXIT                         
              MOVE WS-DAY              TO WS-DEL-POSSN-DD                       
              MOVE WS-8-DATE-MM        TO WS-DEL-POSSN-MM                       
              MOVE WS-8-DATE-YYYY      TO WS-DEL-POSSN-YYYY                     
           ELSE                                                                 
              MOVE WS-8-DATE-MM        TO WS-MONTH                              
              MOVE WS-8-DATE-DD        TO WS-DAY                                
              PERFORM 703-CHECK-DAY       THRU 703-EXIT                         
              MOVE WS-DAY               TO WS-DEL-POSSN-DD                      
              MOVE WS-8-DATE-MM         TO WS-DEL-POSSN-MM                      
              MOVE WS-8-DATE-YYYY       TO WS-DEL-POSSN-YYYY                    
           END-IF.                                                              
                                                                                
       702-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       703-CHECK-DAY.                                                           
      *---------------------------------------------------------------*         
           IF MTH-END-WITH-30                                                   
              IF WS-DAY = '31'                                                  
                 MOVE '30'   TO WS-DAY                                          
              END-IF                                                            
           ELSE                                                                 
              IF MTH-END-WITH-28                                                
                 IF WS-DAY = '29' OR '30' OR '31'                               
                    MOVE '28'   TO WS-DAY                                       
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       703-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       704-CHK-COMM-DATE.                                                       
      *---------------------------------------------------------------*         
           IF WS-TMP-DTE-LSE-COMM NOT = ZEROES AND SPACES AND                   
                                        LOW-VALUES                              
              COMPUTE WS-TMP-DATE =                                             
                   FUNCTION INTEGER-OF-DATE(WS-TMP-DTE-LSE-COMM)                
              COMPUTE WS-CURR-DATE =                                            
                   FUNCTION INTEGER-OF-DATE(WS-CURRENT-DTE)                     
              COMPUTE WS-DIFF-LSE-COMM = WS-TMP-DATE  -                         
                                         WS-CURR-DATE                           
                                                                                
              IF WS-DIFF-LSE-COMM < 31                                          
                 MOVE WS-K500-DTE-LSE-COMM TO F999-DTE-LEASE-COMM               
                 MOVE WS-K500-LEASE-PERIOD-FLAT TO                              
                      F999-NUM-LEASE-PERIOD-FLAT                                
                 MOVE 'Y'                  TO F999-CDE-KEY-AVAIL                
              ELSE                                                              
                 MOVE SPACES               TO F999-DTE-LEASE-COMM               
                 MOVE ZEROES               TO F999-NUM-LEASE-PERIOD-FLAT        
                 MOVE 'Y'                  TO F999-CDE-KEY-AVAIL                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       704-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       705-READ-BB14K580.                                                       
      *---------------------------------------------------------------*         
           MOVE SPACES               TO K580-KEY-FLD.                           
           MOVE F800-SCH-ACC-NO(1:9) TO K580-KEY-FLD.                           
                                                                                
           READ BB14K580.                                                       
           EVALUATE BB14K580-STATUS                                             
           WHEN 00                                                              
              ADD   1          TO WS-K580-READ                                  
           WHEN 23                                                              
              ADD   1          TO WS-K580-NFND                                  
              DISPLAY 'RECORD NOT FOUND BB14K580 : ' K580-KEY-FLD               
              MOVE  SPACES     TO K580-DTE-UNIT-DELIVERY                        
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BB14K580 : ' BB14K580-STATUS               
              MOVE BB14K580-STATUS            TO  RETURN-CODE                   
              PERFORM 999-CLOSE-ROUTINE       THRU 999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       705-EXIT.                                                                
           EXIT.                                                                
                                                                                
       706-READ-BB14K990.                                                       
      *---------------------------------------------------------------*         
           MOVE SPACES               TO K990-KEY-FLD.                           
           MOVE F800-SCH-ACC-NO(1:9) TO K990-KEY-FLD.                           
                                                                                
           READ BB14K990.                                                       
           EVALUATE BB14K990-STATUS                                             
           WHEN 00                                                              
              ADD   1          TO WS-K990-READ                                  
           WHEN 23                                                              
              ADD   1          TO WS-K990-NFND                                  
              DISPLAY 'RECORD NOT FOUND BB14K990 : ' K990-KEY-FLD               
              MOVE  SPACES     TO K990-DTE-REV-LCD                              
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BB14K990 : ' BB14K990-STATUS               
              MOVE BB14K990-STATUS            TO  RETURN-CODE                   
              PERFORM 999-CLOSE-ROUTINE       THRU 999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       706-EXIT.                                                                
           EXIT.                                                                
                                                                                
       710-CHECK-OCS-DUE.                                                       
      *-----------------------------------------------------------              
                                                                                
           MOVE WS-K100-DTE-OCS   TO WS-TMP-DTE-OCS.                            
           COMPUTE WS-DIFF-OCS  = WS-TMP-DTE-OCS -                              
                                  WS-CURRENT-DTE                                
           IF WS-DIFF-OCS  > 0                                                  
              MOVE WS-K100-DTE-OCS   TO F999-DTE-EXPIRY                         
              MOVE 'Y'               TO F999-CDE-OCS-TAG                        
           ELSE                                                                 
              MOVE SPACES            TO F999-DTE-EXPIRY                         
              MOVE 'N'               TO F999-CDE-OCS-TAG                        
           END-IF.                                                              
                                                                                
       710-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       720-CHECK-POD-DUE.                                                       
      *-----------------------------------------------------------              
                                                                                
           IF WS-K100-DTE-POD-DUE > ZEROES                                      
              MOVE WS-K100-DTE-POD-DUE TO WS-TMP-DTE-POD-DUE                    
              MOVE WS-K100-DTE-POD-DUE TO F999-DTE-POD-DUE                      
              COMPUTE WS-DIFF-POD  = WS-TMP-DTE-POD-DUE -                       
                                     WS-CURRENT-DTE                             
              IF WS-DIFF-POD  > 0                                               
                 MOVE WS-K100-DTE-POD-DUE TO F999-DTE-EXPIRY                    
                 MOVE 'Y'                 TO F999-CDE-OCS-TAG                   
                 MOVE 'N'                 TO F999-NUM-POD-DUE                   
              ELSE                                                              
                 MOVE SPACES              TO F999-DTE-EXPIRY                    
                 MOVE 'N'                 TO F999-CDE-OCS-TAG                   
                 MOVE 'Y'                 TO F999-NUM-POD-DUE                   
              END-IF                                                            
           ELSE                                                                 
              IF WS-K100-DTE-POF-DUE > ZEROES                                   
                 MOVE WS-K100-DTE-POF-DUE TO WS-TMP-DTE-POD-DUE                 
                 MOVE WS-K100-DTE-POF-DUE TO F999-DTE-POD-DUE                   
                 COMPUTE WS-DIFF-POD  = WS-TMP-DTE-POD-DUE -                    
                                        WS-CURRENT-DTE                          
                 IF WS-DIFF-POD  > 0                                            
                    MOVE WS-K100-DTE-POF-DUE TO F999-DTE-EXPIRY                 
                    MOVE 'Y'                 TO F999-CDE-OCS-TAG                
                    MOVE 'N'                 TO F999-NUM-POD-DUE                
                 ELSE                                                           
                    MOVE SPACES              TO F999-DTE-EXPIRY                 
                    MOVE 'N'                 TO F999-CDE-OCS-TAG                
                    MOVE 'Y'                 TO F999-NUM-POD-DUE                
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       720-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       999-CLOSE-ROUTINE.                                                       
      *-----------------------------------------------------------              
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13C22X       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F800-READ    TO WS-EDT-TMP.                                  
           DISPLAY 'NO OF RECS READ FROM BM06F800 : ' WS-EDT-TMP.               
           MOVE WS-K270-READ    TO WS-EDT-TMP.                                  
           DISPLAY 'NO OF RECS READ FROM BP13K270 : ' WS-EDT-TMP.               
           MOVE WS-K100-READ    TO WS-EDT-TMP.                                  
           DISPLAY 'NO OF RECS READ FROM BM06K100 : ' WS-EDT-TMP.               
           MOVE WS-K500-READ    TO WS-EDT-TMP.                                  
           DISPLAY 'NO OF RECS READ FROM BB14K500 : ' WS-EDT-TMP.               
           MOVE WS-K580-READ    TO WS-EDT-TMP.                                  
           DISPLAY 'NO OF RECS READ FROM BB14K580 : ' WS-EDT-TMP.               
           MOVE WS-F999-WRITE   TO WS-EDT-TMP.                                  
           DISPLAY 'NO OF RECS WRITE  TO BP13F999 : ' WS-EDT-TMP.               
           MOVE WS-F800-WRITE   TO WS-EDT-TMP.                                  
           DISPLAY 'NO OF RECS WRITE  TO BM06F800 : ' WS-EDT-TMP.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-NOT-MATCHED  TO WS-EDT-TMP.                                  
           DISPLAY 'NO OF RECS SKIPPED : '  WS-EDT-TMP.                         
           MOVE WS-K270-NFND    TO WS-EDT-TMP.                                  
           DISPLAY 'NO RECS NOT FOUND IN BP13K270 : '  WS-EDT-TMP.              
           MOVE WS-K060-NFND    TO WS-EDT-TMP.                                  
           DISPLAY 'NO RECS NOT FOUND IN BP13K060 : '  WS-EDT-TMP.              
           MOVE WS-K260-NFND    TO WS-EDT-TMP.                                  
           DISPLAY 'NO RECS NOT FOUND IN BP13K260 : '  WS-EDT-TMP.              
           MOVE WS-K100-NFND    TO WS-EDT-TMP.                                  
           DISPLAY 'NO RECS NOT FOUND IN BM06K100 : '  WS-EDT-TMP.              
           MOVE WS-K500-NFND    TO WS-EDT-TMP.                                  
           DISPLAY 'NO RECS NOT FOUND IN BB14K500 : '  WS-EDT-TMP.              
           MOVE WS-K580-NFND    TO WS-EDT-TMP.                                  
           DISPLAY 'NO RECS NOT FOUND IN BB14K580 : '  WS-EDT-TMP.              
                                                                                
           CLOSE BP13K270                                                       
                 BP13K060                                                       
                 BP13K260                                                       
                 BM06K100                                                       
                 BM06F800                                                       
                 BB14K500                                                       
                 BB14K580                                                       
                 BB14K990                                                       
                 BP13F999.                                                      
                                                                                
           IF BM06K100-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BM06K100-STATUS ' BM06K100-STATUS           
              MOVE BM06K100-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BP13K270-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BP13K270-STATUS ' BP13K270-STATUS           
              MOVE BP13K270-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BP13K060-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BP13K060-STATUS ' BP13K060-STATUS           
              MOVE BP13K060-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BP13K260-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BP13K260-STATUS ' BP13K260-STATUS           
              MOVE BP13K260-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BB14K500-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BB14K500-STATUS ' BB14K500-STATUS           
              MOVE BB14K500-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BB14K580-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BB14K580-STATUS ' BB14K580-STATUS           
              MOVE BB14K580-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BB14K990-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BB14K990-STATUS ' BB14K990-STATUS           
              MOVE BB14K990-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
