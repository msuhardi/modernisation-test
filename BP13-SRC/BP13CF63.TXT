       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF63.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  17/07/2014.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  UPDATE BALLOT HOUSEHOLD INTO BP13K730       *            
      *                                                            *            
      *  INPUT FILE :  BP13F730                                    *            
      *  I-O FILE   :  BP13K730                                    *            
      *                                                            *            
      * ========================================================== *            
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS           *            
      * -------- ---------- ---- --------------------------------- *            
      * BP135408 17/07/2014 SMR2 NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F730 ASSIGN       TO BP13F730.                            
                                                                                
           SELECT BP13K730 ASSIGN       TO BP13K730                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K730-NUM-REGN                        
                           FILE STATUS  IS WS-K730-STATUS.                      
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F730                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K730.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-STATUS.                                                      
           05  WS-K730-STATUS               PIC X(02)  VALUE SPACES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF                  PIC X(01)  VALUE 'N'.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ                 PIC 9(06)  VALUE ZEROES.            
           05  WS-K730-UPDATE               PIC 9(06)  VALUE ZEROES.            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F730      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-FILES      THRU 3000-EXIT                       
             UNTIL WS-F730-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT BP13F730                                                  
                I-O   BP13K730.                                                 
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K730 - OPEN ERROR, STATUS : ' WS-K730-STATUS         
              MOVE WS-K730-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F730.                                                      
      *-------------------------------------------------------------            
           READ BP13F730 AT END                                                 
                MOVE 'Y'                   TO WS-F730-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F730-READ.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           PERFORM 4000-READ-BP13K730      THRU 4000-EXIT.                      
           PERFORM 2000-READ-BP13F730      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-READ-BP13K730.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                     TO BP13K730-REC.                     
           INITIALIZE                         BP13K730-REC.                     
           MOVE F730-NUM-REGN              TO K730-NUM-REGN.                    
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE WS-K730-STATUS                                              
           WHEN ZEROES                                                          
              PERFORM 5000-UPDATE-BP13K730 THRU 5000-EXIT                       
                                                                                
           WHEN 23                                                              
              DISPLAY 'BP13K730 - RECORD NOT FOUND, KEY : '                     
                       F730-NUM-REGN                                            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K730 - READ ERROR, STATUS : ' WS-K730-STATUS         
              MOVE WS-K730-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-UPDATE-BP13K730.                                                    
      *-------------------------------------------------------------            
           MOVE F730-CDE-BALLOT-HOUSEHOLD  TO K730-CDE-BALLOT-HOUSEHOLD.        
                                                                                
           REWRITE BP13K730-REC.                                                
                                                                                
           EVALUATE WS-K730-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                        TO WS-K730-UPDATE                    
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13K730 - REWRITE ERROR, STATUS : '                     
                       WS-K730-STATUS                                           
              MOVE WS-K730-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           CLOSE BP13F730                                                       
                 BP13K730.                                                      
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K730 - CLOSE ERROR, STATUS : ' WS-K730-STATUS        
              MOVE WS-K730-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           DISPLAY ' BP13CF63 CONTROL TOTAL '.                                  
           DISPLAY '---------------------------------'.                         
           DISPLAY ' NO. OF F730 REC READ    > ' WS-F730-READ.                  
           DISPLAY ' NO. OF K730 REC UPDATED > ' WS-K730-UPDATE.                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
