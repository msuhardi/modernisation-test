      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CD43.                                                 
       AUTHOR.        JIANG BO.                                                 
       DATE-WRITTEN.  13/06/08.                                                 
      *****************************************************************         
      *OBJECTIVE : DBSS SALES ORDER FILE WILL ONLY CREATED ON THE 4TH *         
      *            OF THE FOLLOWING MONTH AFTER CLOSING OF ACCOUNT.   *         
      *            ON 4TH OF THE MONTH, SOC WILL PASS DBSS LOAN INFOR *         
      *            TO SMS AFTER THE OPENING OF NEW MONTH.             *         
      *            SOC WILL ALSO PASS THE 1ST-4TH DAY POSTING FILE TO *         
      *            SMS BECAUSE SMS TO CATCH UP THE POSTING FOR CASH/  *         
      *            CPF/VOUCHER.                                       *         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F400                                  *         
      *                  2. BP13F125                                  *         
      *                                                               *         
      *     OUTPUT FILE:                                              *         
      *                  1. AB03F001                                  *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------  ---  ------------------------             *         
      * BP133298 13/06/2008 JB8  NEW PROGRAM.                         *         
      * BP133591 30/11/2009 JB8  TO CATER FOR TRANS-TYPE              *         
      * BP134310 25/07/2011 JB8  TO CATER FOR CASES ALREADY POSTED    *         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F400  ASSIGN        TO BP13F400.                         
           SELECT  BP13F125  ASSIGN        TO BP13F125.                         
           SELECT  BP13KD64  ASSIGN        TO BP13KD64                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD64-KEY-FLD                      
                             FILE STATUS   IS WS-KD64-STATUS.                   
           SELECT  AB03F001  ASSIGN        TO AB03F001.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F400                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 2000 CHARACTERS          
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F400.                                                           
                                                                                
       FD   BP13F125                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 150 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F125.                                                           
                                                                                
       FD   BP13KD64                                                            
                                       RECORD CONTAINS 150 CHARACTERS.          
       COPY BP13KD64.                                                           
                                                                                
       FD   AB03F001                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 100 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY AB03F001.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  WS-COUNTERS.                                                         
           05  WS-F400-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-F125-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-F001-WRITE           PIC 9(7)  VALUE ZEROES.                  
           05  WS-F400-MATCH           PIC 9(7)  VALUE ZEROES.                  
           05  WS-F400-NOTMATCH        PIC 9(7)  VALUE ZEROES.                  
           05  WS-F125-MATCH           PIC 9(7)  VALUE ZEROES.                  
           05  WS-INVALID              PIC 9(7)  VALUE ZEROES.                  
           05  WS-F125-NOTMATCH        PIC 9(7)  VALUE ZEROES.                  
       01  WS-VARIABLES.                                                        
           05  WS-SYSTEM-DATE.                                                  
               15  WS-SYS-DATE         PIC X(08) VALUE SPACES.                  
               15  WS-SYS-TIME         PIC X(08) VALUE SPACES.                  
               15  FILLER              PIC X(05) VALUE SPACES.                  
           05  WS-KD64-STATUS          PIC 9(2)  VALUE ZEROES.                  
           05  WS-KD64-EOF             PIC X(1)  VALUE SPACES.                  
               88  KD64-EOF                      VALUE 'Y'.                     
           05  WS-TRANS-FND            PIC X(1)  VALUE SPACES.                  
               88  TRANS-FND                     VALUE 'Y'.                     
           05  WS-TXT-DESC             PIC X(8)  VALUE SPACES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F400      THRU 2000-EXIT.                      
           PERFORM 2200-READ-BP13F125      THRU 2200-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL F400-NUM-REGN = HIGH-VALUES                                  
               AND F125-NUM-REGN = HIGH-VALUES.                                 
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F400                                                 
                       BP13F125                                                 
                       BP13KD64                                                 
            OUTPUT     AB03F001.                                                
                                                                                
           IF WS-KD64-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD64 - OPEN ERROR (' WS-KD64-STATUS ')'              
              MOVE WS-KD64-STATUS  TO RETURN-CODE                               
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F400.                                                      
      ******************************************************************        
                                                                                
           READ BP13F400           AT   END                                     
                MOVE HIGH-VALUES   TO   F400-NUM-REGN                           
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-F400-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2200-READ-BP13F125.                                                      
      ******************************************************************        
                                                                                
           READ BP13F125           AT   END                                     
                MOVE HIGH-VALUES   TO   F125-NUM-REGN                           
                GO                 TO   2200-EXIT.                              
                                                                                
           ADD  1                  TO   WS-F125-READ.                           
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF F400-NUM-REGN = F125-NUM-REGN                                     
              ADD 1                               TO WS-F400-MATCH              
              PERFORM 3500-PROCESS-ORDER        THRU 3500-EXIT                  
                UNTIL F125-NUM-REGN NOT = F400-NUM-REGN                         
                   OR F125-NUM-REGN = HIGH-VALUES                               
              PERFORM 2000-READ-BP13F400        THRU 2000-EXIT                  
           ELSE                                                                 
              IF F400-NUM-REGN > F125-NUM-REGN                                  
                 ADD 1                            TO WS-F125-NOTMATCH           
                 PERFORM 2200-READ-BP13F125     THRU 2200-EXIT                  
              ELSE                                                              
                 ADD 1                            TO WS-F400-NOTMATCH           
                 PERFORM 2000-READ-BP13F400     THRU 2000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3500-PROCESS-ORDER.                                                      
      ******************************************************************        
                                                                                
           ADD 1                                  TO WS-F125-MATCH              
                                                                                
           IF (F125-CDE-PAYMENT-TYPE = 'INS' OR 'JVL' OR 'LS ') AND             
              (F125-CDE-TRANS-TYPE = '11' OR '12' OR '13' OR '14' OR            
                                     '15' OR '16' OR '64')                      
              PERFORM 4100-DECODE-TRANS-CODE  THRU 4100-EXIT                    
              PERFORM 4200-CHECK-EXIST-TRANS  THRU 4200-EXIT                    
              IF NOT TRANS-FND                                                  
                 PERFORM 4300-OUTPUT-AB03F001 THRU 4300-EXIT                    
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'R ' F125-NUM-REGN                                        
                      ' PAYMT= ' F125-CDE-PAYMENT-TYPE                          
              ADD 1                             TO WS-INVALID                   
           END-IF.                                                              
                                                                                
           PERFORM 2200-READ-BP13F125           THRU 2200-EXIT.                 
                                                                                
       3500-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4100-DECODE-TRANS-CODE.                                                  
      ******************************************************************        
                                                                                
           MOVE SPACES                     TO WS-TXT-DESC                       
                                                                                
           EVALUATE F125-CDE-PAYMENT-TYPE                                       
              WHEN 'INS'                                                        
                IF F125-CDE-TRANS-TYPE = '12'                                   
                    IF F125-NUM-DOC = 'ARR-L'                                   
                       MOVE 'CPF-A  '      TO WS-TXT-DESC                       
                    ELSE                                                        
                       MOVE 'CPF    '      TO WS-TXT-DESC                       
                    END-IF                                                      
                ELSE                                                            
                  IF F125-CDE-TRANS-TYPE = '11'                                 
                      MOVE 'PR      '      TO WS-TXT-DESC                       
                  ELSE                                                          
                     IF F125-CDE-TRANS-TYPE = '14'                              
                        MOVE 'JV      '    TO WS-TXT-DESC                       
                     ELSE                                                       
                        IF F125-CDE-TRANS-TYPE = '64'                           
                           MOVE 'GIRO    ' TO WS-TXT-DESC                       
                        END-IF                                                  
                     END-IF                                                     
                  END-IF                                                        
                END-IF                                                          
              WHEN 'LS '                                                        
                IF F125-CDE-TRANS-TYPE = '11'                                   
                    MOVE 'PR-L    '        TO WS-TXT-DESC                       
                ELSE                                                            
                   IF F125-CDE-TRANS-TYPE = '12'                                
                      MOVE 'CPF-L   '      TO WS-TXT-DESC                       
                   ELSE                                                         
                     IF F125-CDE-TRANS-TYPE = '64'                              
                        MOVE 'GIRO-L  '    TO WS-TXT-DESC                       
                     END-IF                                                     
                  END-IF                                                        
                END-IF                                                          
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4200-CHECK-EXIST-TRANS.                                                  
      ******************************************************************        
                                                                                
           MOVE SPACES                             TO WS-TRANS-FND.             
                                                                                
           MOVE SPACES                             TO KD64-KEY-FLD              
           INITIALIZE                                 KD64-KEY-FLD              
                                                                                
           MOVE F125-NUM-REGN                      TO KD64-NUM-REGN             
           MOVE F125-DTE-TRANS                     TO KD64-DTE-PROCESS          
                                                                                
           PERFORM 8001-START-BP13KD64           THRU 8001-EXIT                 
           IF KD64-EOF                                                          
              GO TO 4200-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 8002-READ-NEXT-BP13KD64       THRU 8002-EXIT                 
           PERFORM UNTIL KD64-EOF OR TRANS-FND                                  
              IF WS-TXT-DESC = KD64-TXT-DESC                                    
                 MOVE 'Y'                          TO WS-TRANS-FND              
              ELSE                                                              
                 PERFORM 8002-READ-NEXT-BP13KD64 THRU 8002-EXIT                 
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       4200-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4300-OUTPUT-AB03F001.                                                    
      ******************************************************************        
                                                                                
           MOVE SPACES                        TO AB03F001-REC                   
           INITIALIZE                            AB03F001-REC                   
           MOVE F125-NUM-SCH-ACCT             TO F001-NUM-HDB-REF               
           MOVE F125-DTE-TRANS                TO F001-DTE-TRANS                 
           MOVE ZEROS                         TO F001-NUM-SEQ                   
           MOVE ZEROS                         TO F001-NUM-SEQ                   
           MOVE F125-AMT-RECEIPT              TO F001-AMT-INST-LOAN             
           MOVE F125-AMT-RECEIPT              TO F001-AMT-TOTL                  
           MOVE F125-CDE-CRDR                 TO F001-CDE-CR-DR                 
           MOVE WS-SYS-DATE(5:2)              TO F001-DTE-PYMT-MTH              
           MOVE F125-NUM-RECPT-JRNO(1:8)      TO F001-NUM-REF                   
           MOVE F125-CDE-TRANS-TYPE           TO F001-CDE-TRANS-TYP             
           MOVE F125-CDE-BO                   TO F001-CDE-BO                    
           MOVE F125-NUM-LOAN-SCH             TO F001-CDE-LOAN-SCH              
           MOVE SPACES                        TO F001-CDE-INT                   
           MOVE SPACES                        TO F001-CDE-ERROR                 
           MOVE 'BP13CD43'                    TO F001-NUM-USERID                
           MOVE F125-DTE-TRANS                TO F001-DTE-DATA-ENTRY            
           IF F125-CDE-PAYMENT-TYPE = 'INS'                                     
              IF F125-CDE-TRANS-TYPE = '12'                                     
                 MOVE 'CPF     '              TO F001-TXT-DESC                  
                 MOVE 'NM'                    TO F001-CDE-TRANS-POSTNG          
              ELSE                                                              
                 IF F125-CDE-TRANS-TYPE = '11'                                  
                    MOVE 'PR     '            TO F001-TXT-DESC                  
                    MOVE 'CL'                 TO F001-CDE-TRANS-POSTNG          
                 ELSE                                                           
                    IF F125-CDE-TRANS-TYPE = '64'                               
                       MOVE 'GIRO    '        TO F001-TXT-DESC                  
                       MOVE 'GO'              TO F001-CDE-TRANS-POSTNG          
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF F125-CDE-PAYMENT-TYPE = 'LS '                                  
                 IF F125-CDE-TRANS-TYPE = '11'                                  
                    MOVE 'PR-L    '           TO F001-TXT-DESC                  
                    MOVE 'CL'                 TO F001-CDE-TRANS-POSTNG          
                 ELSE                                                           
                    IF F125-CDE-TRANS-TYPE = '12'                               
                       MOVE 'CPF-L   '        TO F001-TXT-DESC                  
                       MOVE 'NM'              TO F001-CDE-TRANS-POSTNG          
                    ELSE                                                        
                      IF F125-CDE-TRANS-TYPE = '64'                             
                         MOVE 'GIRO-L  '      TO F001-TXT-DESC                  
                         MOVE 'GO'            TO F001-CDE-TRANS-POSTNG          
                      END-IF                                                    
                   END-IF                                                       
                 END-IF                                                         
              ELSE                                                              
                 IF F125-NUM-LOAN-INTR = 'N'                                    
                    MOVE 'JV-N    '           TO F001-TXT-DESC                  
                    MOVE 'VS'                 TO F001-CDE-TRANS-POSTNG          
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           WRITE AB03F001-REC                                                   
           ADD 1                              TO WS-F001-WRITE.                 
                                                                                
       4300-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       8001-START-BP13KD64.                                                     
      ******************************************************************        
                                                                                
           MOVE 'N'                           TO WS-KD64-EOF                    
                                                                                
           START BP13KD64 KEY >= KD64-KEY-FLD                                   
                                                                                
           IF WS-KD64-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'BP13KD64 - ERROR START BROWSE ('                         
                      WS-KD64-STATUS '), KEY (' KD64-KEY-FLD ')'                
              MOVE WS-KD64-STATUS             TO RETURN-CODE                    
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       8001-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       8002-READ-NEXT-BP13KD64.                                                 
      ******************************************************************        
                                                                                
           READ BP13KD64 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'      TO WS-KD64-EOF                                    
                GO            TO 8002-EXIT                                      
           END-READ.                                                            
                                                                                
           IF WS-KD64-STATUS = 00 OR 02                                         
              IF KD64-NUM-REGN = F125-NUM-REGN AND                              
                 KD64-DTE-PROCESS = F125-DTE-TRANS                              
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE 'Y'                     TO WS-KD64-EOF                    
              END-IF                                                            
           ELSE                                                                 
              IF WS-KD64-STATUS = 23 OR 10                                      
                 MOVE 'Y'                     TO WS-KD64-EOF                    
              ELSE                                                              
                 DISPLAY 'BP13KD64 - ERROR READ NEXT RECORD ('                  
                         WS-KD64-STATUS '), KEY (' KD64-KEY-FLD ')'             
                 MOVE WS-KD64-STATUS          TO RETURN-CODE                    
                 GO TO 9999-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8002-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CD43'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F400 RECORDS READ................. : '        
                    WS-F400-READ.                                               
           DISPLAY '(2) NO OF BP13F125 RECORDS READ................. : '        
                    WS-F125-READ.                                               
           DISPLAY '(3) NO OF RECORDS MATCH F125 IN BP13F400........ : '        
                    WS-F400-MATCH.                                              
           DISPLAY '(4) NO OF RECORDS NOT MATCH F125 IN BP13F400.... : '        
                    WS-F400-NOTMATCH.                                           
           DISPLAY '(5) NO OF RECORDS MATCH F400 IN F125............ : '        
                    WS-F125-MATCH.                                              
           DISPLAY '(6) NO OF RECORDS MATCH F400 IN F125 BUT INVALID : '        
                    WS-INVALID.                                                 
           DISPLAY '(6) NO OF RECORDS NOT MATCH F400 IN F125........ : '        
                    WS-F125-NOTMATCH.                                           
           DISPLAY '(7) NO OF AB03F001 RECORDS WRITE................ : '        
                    WS-F001-WRITE.                                              
                                                                                
           CLOSE BP13F400                                                       
                 BP13F125                                                       
                 BP13KD64                                                       
                 AB03F001.                                                      
                                                                                
           IF WS-KD64-STATUS NOT = 00                                           
              DISPLAY 'BP13KD64 - CLOSE ERROR (' WS-KD64-STATUS ')'             
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
