       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CS08.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
       DATE-WRITTEN.  24 JUNE 2014.                                             
      *========================================================*                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO EXTRACT CASES WITH PENDING DOCS    *                
      *                                                        *                
      *    INPUT       : BP13.F203.SMS                         *                
      *                  BP13.K800.APPLN                       *                
      *                  BP13.KQ10.DOC.TBL                     *                
      *    OUTPUT      : BP13.F203.SMS.PD                      *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                         *                
      *--------- ---- -------- --------------------------------*                
      * BP135227 ESA1 20140624 NEW PROGRAM                     *                
      *========================================================*                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F203 ASSIGN TO BP13F203.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS K800-STATUS.                               
                                                                                
           SELECT BP13KQ10 ASSIGN TO BP13KQ10                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KQ10-KEY-FLD                               
                  FILE STATUS     IS KQ10-STATUS.                               
                                                                                
           SELECT P13F203A ASSIGN TO P13F203A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F203                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F203.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13KQ10                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13KQ10.                                                           
                                                                                
       FD  P13F203A                                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  P13F203A-REC     PIC X(200).                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 KQ10-STATUS                 PIC 9(2)  VALUE ZEROS.                
           05 K800-STATUS                 PIC 9(2)  VALUE ZEROS.                
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F203-WRITE               PIC 9(5)  VALUE ZEROS.                
           05 WS-READ-CNT-F203            PIC 9(5)  VALUE ZEROS.                
                                                                                
       01  WS-EOF-F203                    PIC X     VALUE 'N'.                  
       01  WS-APPT                        PIC X     VALUE 'N'.                  
                                                                                
       01  WS-CUR-DATE1                   PIC 9(8)  VALUE ZEROES.               
       01  WS-DATE-VALUES.                                                      
           05  WS-CUR-DATE                PIC X(8).                             
           05  WS-CUR-DATE-NUM REDEFINES WS-CUR-DATE                            
                                          PIC 9(8).                             
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F203      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL WS-EOF-F203 = 'Y'                                            
           PERFORM 999-CLOSE-ROUTINE  THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       100-INITIALIZATION.                                                      
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F203                                                 
                       BP13K800                                                 
                       BP13KQ10                                                 
                OUTPUT P13F203A.                                                
                                                                                
           IF K800-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR OPENING BP13K800  FILE, STATUS '                  
                  K800-STATUS                                                   
              MOVE K800-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF KQ10-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR OPENING BP13KQ10  FILE, STATUS '                  
                  KQ10-STATUS                                                   
              MOVE KQ10-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CUR-DATE.                        
                                                                                
      * FIND NEXT DAY                                                           
                                                                                
           COMPUTE WS-CUR-DATE-NUM =                                            
                   FUNCTION DATE-OF-INTEGER(                                    
                   FUNCTION INTEGER-OF-DATE(WS-CUR-DATE-NUM) + 2).              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       200-READ-F203.                                                           
      ******************************************************************        
                                                                                
           READ BP13F203 AT END                                                 
                MOVE 'Y' TO WS-EOF-F203                                         
                GO TO 200-EXIT.                                                 
           ADD   1     TO WS-READ-CNT-F203.                                     
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-RTN.                                                         
      ******************************************************************        
           IF WS-CUR-DATE = F203-DTE-APPMT                                      
              MOVE 'Y' TO WS-APPT                                               
              PERFORM 400-READ-K800      THRU 400-EXIT                          
              IF ((WS-APPT = 'Y') AND                                           
                  (F203-NUM-HP NOT = SPACES AND LOW-VALUES AND ZEROES))         
                 PERFORM 500-READ-KQ10   THRU 500-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F203       THRU 200-EXIT.                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-READ-K800.                                                           
      ******************************************************************        
                                                                                
           MOVE F203-NUM-REGN       TO K800-NUM-REGN.                           
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
                                                                                
           IF K800-STATUS = 0                                                   
              IF K800-NUM-STATUS = 'C'                                          
                 DISPLAY 'REC CANCELLED IN SOC MASTER: ' F203-NUM-REGN          
                 MOVE 'N' TO WS-APPT                                            
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'REC NOT FOUND IN SOC MASTER: ' F203-NUM-REGN             
              MOVE 'N' TO WS-APPT                                               
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       500-READ-KQ10.                                                           
      ******************************************************************        
                                                                                
           MOVE F203-NUM-REGN       TO KQ10-NUM-REGN.                           
           READ BP13KQ10.                                                       
                                                                                
           IF KQ10-STATUS = 0                                                   
              IF KQ10-NUM-SMS-REMINDER = 'Y'                                    
                 CONTINUE                                                       
              ELSE                                                              
                 GO TO 500-EXIT                                                 
              END-IF                                                            
              IF KQ10-DTE-DOC-CLOSE = SPACES OR LOW-VALUES OR ZEROES            
                 IF ((KQ10-DTE-DOC-SMSREMIND1-SENT = SPACES OR                  
                                        LOW-VALUES OR ZEROES) OR                
                    (KQ10-DTE-DOC-SMSREMIND3-SENT = SPACES OR                   
                                        LOW-VALUES OR ZEROES))                  
                    PERFORM 530-WRITE-F203      THRU 530-EXIT                   
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'REC NOT FOUND IN DOC TABLE : ' F203-NUM-REGN             
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       530-WRITE-F203.                                                          
      ******************************************************************        
                                                                                
           MOVE SPACES                 TO P13F203A-REC.                         
           MOVE BP13F203-REC           TO P13F203A-REC.                         
                                                                                
           WRITE P13F203A-REC.                                                  
           ADD   1                     TO WS-F203-WRITE.                        
                                                                                
       530-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-ROUTINE.                                                       
      ******************************************************************        
           CLOSE BP13F203                                                       
                 BP13K800                                                       
                 BP13KQ10                                                       
                 P13F203A.                                                      
                                                                                
           IF K800-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR CLOSING BP13K800  FILE, STATUS '                  
                  K800-STATUS                                                   
              MOVE K800-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF KQ10-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR CLOSING BP13KQ10  FILE, STATUS '                  
                  KQ10-STATUS                                                   
              MOVE KQ10-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CS08 ================*'.              
           DISPLAY '*======= RUN DATE : ' WS-CUR-DATE ' =======*'.              
           DISPLAY '    TOTAL NO OF REC READ    = ' WS-READ-CNT-F203.           
           DISPLAY '    NO. OF SMS TRANSMITTED  = ' WS-F203-WRITE.              
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
