      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13CC93.                                                 
      *AUTHOR.        EDEN ROSE DINGAL.                                         
      *DATE-WRITTEN.  11 FEB 2008.                                              
      * =========================================================== *           
      * SYSTEM OF COMMITMENT  (BP13)                                *           
      * =========================================================== *           
      *  OBJECTIVE :  1) TO PRINT BP13PC93 LETTER REMINDERS         *           
      *                  APPLICANT TO JOIN GIRO IF FOUND            *           
      *                  INSUFFICIENT CPF FUND FOR INSTAL PAYMENT.  *           
      *               2) TO PRINT BP13LC93 CONTROL REPORT           *           
      *                                                             *           
      * INPUT FILES  :  1.  BP13FD00                                *           
      *                 2.  BP13K800                                *           
      *                 3.  BP13K820                                *           
      *                 4.  BP13K830                                *           
      *                 5.  BP13KD00                                *           
      *                 6.  AC15K010                                *           
      *                 7.  SY02F001                                *           
      *                                                             *           
      * OUTPUT       :  1.  BP13LC93                                *           
      *                 2.  BP13PC93                                *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  DATE     BY   DESCRIPTION                          *           
      * -------- ------   ---  -----------                          *           
      * BP133209 20080211 ED1  NEW PGM                              *           
      * =========================================================== *           
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
         SOURCE-COMPUTER.   IBM-3090.                                           
         OBJECT-COMPUTER.   IBM-3090.                                           
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
         FILE-CONTROL.                                                          
           SELECT BP13FD00 ASSIGN TO BP13FD00.                                  
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KD00-KEY-FLD                                    
                FILE STATUS  IS WS-KD00-STATUS.                                 
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K800-NUM-REGN                                   
                FILE STATUS  IS WS-K800-STATUS.                                 
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K820-KEY-FLD                                    
                FILE STATUS  IS WS-K820-STATUS.                                 
                                                                                
           SELECT BP13K830 ASSIGN TO BP13K830                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K830-KEY-FLD                                    
                ALTERNATE RECORD KEY IS K830-NUM-REGN                           
                FILE STATUS  IS WS-K830-STATUS.                                 
                                                                                
           SELECT BP13LC93  ASSIGN TO BP13LC93.                                 
           SELECT BP13PC93  ASSIGN TO BP13PC93.                                 
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD   BP13FD00    BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 1600 CHARACTERS                         
                        LABEL RECORDS ARE STANDARD                              
                        RECORDING MODE IS F.                                    
       COPY BP13FD00.                                                           
                                                                                
       FD   BP13K800    RECORD CONTAINS 2000 CHARACTERS.                        
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820    RECORD CONTAINS 400 CHARACTERS.                         
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830    RECORD CONTAINS 250 CHARACTERS.                         
       COPY BP13K830.                                                           
                                                                                
       FD   BP13KD00    RECORD CONTAINS 1600 CHARACTERS.                        
       COPY BP13KD00.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13LC93     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 132 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01  LC93-PRTREC  PIC X(132).                                             
                                                                                
       FD  BP13PC93     RECORD CONTAINS 132 CHARACTERS                          
                        RECORDING MODE IS F.                                    
       01  PC93-PRTREC    PIC X(132).                                           
                                                                                
                                                                                
      *--------------------------------------------------------------           
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------           
       01  WS-COUNTERS.                                                         
           05 WS-AMT-ARREARS         PIC S9(6) VALUE ZEROES.                    
           05 WS-CNT-FD00-READ       PIC 9(7) VALUE ZEROES.                     
           05 WS-CNT-LC93-WRITE      PIC 9(7) VALUE ZEROES.                     
           05 WS-PC93-WRT-CTR        PIC 9(7) VALUE ZEROES.                     
           05 WS-SNO                 PIC 9(5) VALUE ZEROES.                     
           05 WS-PAGE-CTR            PIC 9(4) VALUE ZEROES.                     
           05 WS-LINE-CTR            PIC 9(2) VALUE ZEROES.                     
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 WS-K800-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-KD00-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K820-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K830-STATUS         PIC 9(2) VALUE ZEROES.                     
                                                                                
       01  WS-FLAGS.                                                            
           05 WS-FLG-FD00-EOF        PIC X    VALUE 'N'.                        
           05 WS-KD00-FOUND          PIC X    VALUE 'N'.                        
                                                                                
       01  WS-DATE-HDR               PIC X(10) VALUE SPACES.                    
       01  WS-MAX-LINE               PIC 99    VALUE 53.                        
                                                                                
       01  WS-DATE.                                                             
           05  WS-CCYY               PIC X(4).                                  
           05  WS-MM                 PIC X(2).                                  
           05  WS-MM-R REDEFINES WS-MM.                                         
               10 WS-MTH             PIC 9(2).                                  
           05  WS-DD                 PIC X(2).                                  
                                                                                
       01  WS-1ST-LTR-DTE.                                                      
           05  WS-YR                 PIC X(4).                                  
           05  WS-MO                 PIC X(2).                                  
           05  WS-MO-R REDEFINES WS-MO.                                         
               10 WS-MNTH            PIC 9(2).                                  
           05  WS-DAY                PIC X(2).                                  
                                                                                
       01  WS-TOTAL              PIC      9(7)V99 VALUE ZEROES.                 
                                                                                
       01 WS-MONTH-NAME.                                                        
          05 WS-STRING PIC X(36) VALUE                                          
                'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                         
          05 WS-MONTH  REDEFINES WS-STRING OCCURS 12 TIMES PIC XXX.             
                                                                        01010000
       01  WS-BLANK-LINE  PIC X(132)             VALUE SPACES.                  
                                                                                
       01  WS-DASH-LINE   PIC X(132)             VALUE ALL '-'.                 
                                                                                
      *-------------------------------------------------------------            
      * CONTROL REPORT                                                          
      *-------------------------------------------------------------            
       01  LC93-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13LC93'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LC93-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LC93-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
                                                                                
       01  LC93-HEADER02.                                                       
           05  FILLER              PIC X(28)     VALUE SPACES.                  
           05  FILLER              PIC X(44)     VALUE                          
               'REPORT ON ACCOUNTS WITH INSUFFICIENT CPF FOR'.                  
           05  FILLER              PIC X(14)     VALUE                          
               ' THE MONTH OF '.                                                
           05  LC93-MMM-HDR        PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X         VALUE SPACE.                   
           05  LC93-YR-HDR         PIC X(04)     VALUE SPACES.                  
                                                                                
       01  LC93-HEADER03.                                                       
           05  FILLER              PIC X(86)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'MTH IN'.                
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'AMT IN'.                
                                                                                
       01  LC93-HEADER04.                                                       
           05  FILLER              PIC X(03)     VALUE 'S/N'.                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REGN NO'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'NAME/ADDRESS'.          
           05  FILLER              PIC X(58)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'ARREARS'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'ARREARS($)'.            
                                                                                
       01 LC93-DETAILS.                                                         
          05 LC93-DTL-SERIAL      PIC ZZZZ9      VALUE ZEROES.                  
          05 FILLER               PIC X(01)      VALUE SPACES.                  
          05 LC93-REGNO           PIC X(08)      VALUE SPACES.                  
          05 FILLER               PIC X(02)      VALUE SPACES.                  
          05 LC93-NAME1           PIC X(66)      VALUE SPACES.                  
          05 FILLER               PIC X(04)      VALUE SPACES.                  
          05 LC93-MO-ARREAR       PIC      Z9.99 VALUE ZEROES.                  
          05 FILLER               PIC X(04)      VALUE SPACES.                  
          05 LC93-AMT-ARREAR      PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
                                                                                
       01 LC93-DETAIL2.                                                         
          05 FILLER               PIC X(16)     VALUE SPACES.                   
          05 LC93-NAME2           PIC X(66)     VALUE SPACES.                   
                                                                                
       01 LC93-DETAIL3.                                                         
          05 FILLER               PIC X(16)     VALUE SPACES.                   
          05 LC93-ADDRESS         PIC X(90)     VALUE SPACES.                   
                                                                                
      *-------------------------------------------------------------            
      * FORMAT LETTER                                                           
      *-------------------------------------------------------------            
       01  PC93-PR-LINE-1.                                                      
           05  FILLER                   PIC X(72)  VALUE SPACES.                
           05  PC93-HDBREF              PIC X(14)  VALUE SPACES.                
                                                                                
       01  PC93-PR-LINE-2.                                                      
           05  FILLER                   PIC X(72)  VALUE SPACES.                
           05  PC93-REGNO               PIC X(08)  VALUE SPACES.                
                                                                                
       01  PC93-PR-LINE-3.                                                      
           05  FILLER                   PIC X(72)  VALUE SPACES.                
           05  PC93-DATE-HDR            PIC X(10)  VALUE SPACES.                
                                                                                
       01  PC93-PR-LINE-4.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC93-NAME1B              PIC X(66)  VALUE SPACES.                
                                                                                
       01  PC93-PR-LINE-5.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC93-NAME2B              PIC X(66)  VALUE SPACES.                
                                                                                
       01  PC93-PR-LINE-6.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC93-BLK                 PIC X(16)  VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACE.                 
           05  PC93-FLAT                PIC X(11)  VALUE SPACES.                
                                                                                
       01  PC93-PR-LINE-7.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC93-ADDR1               PIC X(65)  VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACE.                 
                                                                                
       01  PC93-PR-LINE-8.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC93-ADDR2               PIC X(65)  VALUE SPACES.                
                                                                                
       01  PC93-PR-LINE-9.                                                      
           05  FILLER                   PIC X(10)  VALUE SPACES.                
           05  PC93-BAR-CODE            PIC X(09)  VALUE SPACES.                
                                                                                
       01  PC93-PR-LINE-10.                                                     
           05  FILLER                   PIC X(11)  VALUE SPACES.                
           05  PC93-FLAT-ADDR           PIC X(70)  VALUE SPACES.                
                                                                                
       01  PC93-PR-LINE-11.                                                     
           05  FILLER                   PIC X(81)  VALUE SPACES.                
           05  PC93-MMM                 PIC X(03)  VALUE SPACES.                
           05  FILLER                   PIC X(01)  VALUE SPACES.                
           05  PC93-YY                  PIC X(02)  VALUE SPACES.                
                                                                                
       01  PC93-PR-LINE-12.                                                     
           05  FILLER                   PIC X(63)  VALUE SPACES.                
           05  PC93-AMT                 PIC $$$$,$$9.99  VALUE ZEROS.           
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR AV02C015                                                   
      *-------------------------------------------------------------            
        01  WS-LINK-AV02C015.                                                   
            05  AV02C015-IN-POSTCODE          PIC X(6).                         
            05  AV02C015-OUT-POSTCODE-CHKSUM  PIC X(9).                         
                                                                                
       COPY AV02COMM.                                                           
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR BP13C913                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH            PIC X(04).                             
           05 WS-LINK-NUM-ACC            PIC X(04).                             
           05 WS-LINK-NUM-CHK            PIC X(01).                             
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-START-UP          THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13FD00     THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-RECORDS   THRU 3000-EXIT                        
                   UNTIL WS-FLG-FD00-EOF = 'Y'.                                 
           PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       1000-START-UP.                                                           
      *--------------------------------------------------------------           
           OPEN INPUT  BP13FD00                                                 
                       BP13K800                                                 
                       BP13KD00                                                 
                       BP13K820                                                 
                       BP13K830                                                 
                       SY02F001                                                 
               OUTPUT  BP13LC93                                                 
                       BP13PC93.                                                
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KD00 FILE ERROR ' WS-KD00-STATUS             
              MOVE     WS-KD00-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 FILE ERROR ' WS-K800-STATUS             
              MOVE     WS-K800-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 FILE ERROR ' WS-K820-STATUS             
              MOVE     WS-K820-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K830 FILE ERROR ' WS-K830-STATUS             
              MOVE     WS-K830-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT TO WS-DATE.                                    
                                                                                
           STRING WS-DD '/' WS-MM '/' WS-CCYY                                   
           DELIMITED BY SIZE                   INTO WS-DATE-HDR.                
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       2000-READ-BP13FD00.                                                      
      *--------------------------------------------------------------           
           READ BP13FD00                                                        
              AT END MOVE 'Y' TO WS-FLG-FD00-EOF                                
                     GO TO 2000-EXIT.                                           
                                                                                
           ADD 1 TO WS-CNT-FD00-READ.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       3000-PROCESS-RECORDS.                                                    
      *--------------------------------------------------------------           
           PERFORM 3200-READ-BP13KD00   THRU 3200-EXIT.                         
                                                                                
           IF WS-KD00-STATUS      = 00  AND                                     
              KD00-AMT-ARR-INSTAL > 0    AND                                    
             (KD00-NUM-GIRO-INFO  = SPACES OR LOW-VALUES OR ZEROS)              
                                                                                
              PERFORM 3400-INIT-PRNT-FIELDS   THRU 3400-EXIT                    
              PERFORM 4000-PRINT-LETTER       THRU 4000-EXIT                    
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD00   THRU 2000-EXIT.                         
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3200-READ-BP13KD00.                                                      
      *--------------------------------------------------------------           
                                                                                
           MOVE SPACES          TO  BP13KD00-REC.                               
           MOVE FD00-NUM-REGN   TO  KD00-NUM-REGN.                              
                                                                                
           READ BP13KD00.                                                       
                                                                                
           IF WS-KD00-STATUS = 00                                               
                IF KD00-AMT-ARR-INSTAL NOT NUMERIC                              
                   MOVE 0 TO KD00-AMT-ARR-INSTAL                                
                END-IF                                                          
                                                                                
           ELSE                                                                 
             IF WS-KD00-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN KD00, STATUS ' WS-KD00-STATUS           
                        ', REGN IS  '    FD00-NUM-REGN                          
             ELSE                                                               
                DISPLAY 'ERROR READING KD00, STATUS ' WS-KD00-STATUS            
                        ', REGN IS  '    FD00-NUM-REGN                          
                MOVE WS-KD00-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3400-INIT-PRNT-FIELDS.                                                   
      *--------------------------------------------------------------           
           INITIALIZE                 PC93-NAME1B                               
                                      PC93-NAME2B                               
                                      PC93-DATE-HDR                             
                                      PC93-BLK                                  
                                      PC93-ADDR1                                
                                      PC93-ADDR2                                
                                      PC93-BAR-CODE                             
                                      PC93-FLAT                                 
                                      PC93-FLAT-ADDR                            
                                      PC93-REGNO                                
                                      PC93-MMM                                  
                                      PC93-YY                                   
                                      PC93-AMT.                                 
                                                                                
           MOVE SPACES            TO  LC93-DETAILS                              
                                      LC93-DETAIL2                              
                                      LC93-DETAIL3.                             
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4000-PRINT-LETTER.                                                       
      *--------------------------------------------------------------           
           PERFORM 4100-READ-BP13K800            THRU 4100-EXIT                 
                                                                                
           IF WS-K800-STATUS = 00                                               
                                                                                
              IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES AND ZEROES          
                 MOVE SPACES                TO   BP13K820-REC                   
                 MOVE K800-NUM-NRIC1        TO   K820-NUM-NRIC                  
                 MOVE FD00-NUM-REGN         TO   K820-NUM-REGN                  
                 PERFORM 4200-READ-BP13K820 THRU 4200-EXIT                      
                 MOVE K820-NME-OCCP         TO   LC93-NAME1                     
              END-IF                                                            
                                                                                
              IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND ZEROES          
                 MOVE SPACES                TO   BP13K820-REC                   
                 MOVE K800-NUM-NRIC2        TO   K820-NUM-NRIC                  
                 MOVE FD00-NUM-REGN         TO   K820-NUM-REGN                  
                 PERFORM 4200-READ-BP13K820 THRU 4200-EXIT                      
                 MOVE K820-NME-OCCP         TO   LC93-NAME2                     
              END-IF                                                            
                                                                                
              PERFORM 4300-GET-FLAT-ADDR            THRU 4300-EXIT              
              PERFORM 4400-READ-BP13K830            THRU 4400-EXIT              
              PERFORM 5000-MOVE-REPORT-DTLS         THRU 5000-EXIT              
              PERFORM 5100-MOVE-LETTER-DTLS         THRU 5100-EXIT              
              PERFORM 6000-PRINT-ROUTINE            THRU 6000-EXIT              
                                                                                
           END-IF.                                                              
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       4100-READ-BP13K800.                                                      
      *--------------------------------------------------------------           
      *    RETRIEVE OCCUPIERS NRIC1 & NRIC2 FROM FROM BP13K820    *             
      *    PRINT LETTER ONLY IF FOUND OCCUPIER(S) IN BP13K800     *             
      *-----------------------------------------------------------*             
           MOVE FD00-NUM-REGN TO K800-NUM-REGN.                                 
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
             IF WS-K800-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K800, STATUS ' WS-K800-STATUS           
                        ', REGN IS ' FD00-NUM-REGN                              
             ELSE                                                               
                DISPLAY 'ERROR READING K800, STATUS ' WS-K800-STATUS            
                        ', REGN IS  '    FD00-NUM-REGN                          
                MOVE WS-K800-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4200-READ-BP13K820.                                                      
      *--------------------------------------------------------------           
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
             IF WS-K820-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K820, STATUS ' WS-K820-STATUS           
                        ', KEY IS '  FD00-NUM-REGN ' ' K820-NUM-NRIC            
             ELSE                                                               
                DISPLAY 'ERROR READING K820, STATUS ' WS-K820-STATUS            
                        ', KEY IS '  FD00-NUM-REGN ' ' K820-NUM-NRIC            
                MOVE WS-K820-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------*           
       4300-GET-FLAT-ADDR.                                                      
      *-------------------------------------------------------------*           
           MOVE SPACES                TO BP13COMM8-REC.                         
           MOVE K800-NUM-SCH-ACC(1:9) TO WS-LINK-REC.                           
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
             WHEN 000                                                           
                STRING 'BLK '  COMM8-NUM-BLK  ' '                               
                       '#'  COMM8-NUM-LEVEL   '-'                               
                       COMM8-NUM-UNIT-MAIN    ' ' DELIMITED BY SIZE             
                       COMM8-NME-STREET       DELIMITED BY '   '                
                       INTO PC93-FLAT-ADDR                                      
             WHEN 100                                                           
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                     
                         K800-NUM-SCH-ACC ', REGN : ' FD00-NUM-REGN             
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
                        ', AT KEY ' K800-NUM-SCH-ACC                            
                        ', REGN : ' FD00-NUM-REGN                               
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4400-READ-BP13K830.                                                      
      *-----------------------------------------------------------*             
      *    RETRIEVE ADDRESS FROM BP13K830 USING FD00 REGN NO      *             
      *-----------------------------------------------------------*             
           MOVE FD00-NUM-REGN          TO K830-NUM-REGN.                        
                                                                                
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           IF WS-K830-STATUS  = 00 OR 02                                        
              IF (K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR =  '#') AND                           
                 (K830-NUM-MAIL-LVL-NUM =  SPACES OR LOW-VALUES))               
                 IF K830-NUM-BLK-HDR NOT = SPACES AND LOW-VALUES                
                    MOVE K830-NUM-BLK         TO PC93-BLK                       
                 ELSE                                                           
                    MOVE K830-NUM-BLK(5:12)   TO PC93-BLK                       
                 END-IF                                                         
                 MOVE K830-NUM-FLAT        TO PC93-FLAT                         
                 MOVE K830-NUM-FLAT        TO PC93-FLAT                         
                 MOVE K830-NME-STREET-BLDG TO PC93-ADDR1                        
                 IF K830-NUM-POSTAL  = SPACES OR LOW-VALUES OR ZEROES           
                    MOVE SPACES            TO PC93-ADDR2                        
                 ELSE                                                           
                    STRING 'SINGAPORE '  K830-NUM-POSTAL                        
                       DELIMITED BY SIZE    INTO PC93-ADDR2                     
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-POSTAL TO AV02C015-IN-POSTCODE                
                    PERFORM 4500-CALL-AV02C015     THRU 4500-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC93-BAR-CODE          
                 END-IF                                                         
              ELSE                                                              
                 IF K830-NUM-MAIL-BLK-HDR NOT = SPACES AND LOW-VALUES           
                    MOVE K830-NUM-MAIL-BLK         TO PC93-BLK                  
                 ELSE                                                           
                    MOVE K830-NUM-MAIL-BLK(5:12)   TO PC93-BLK                  
                 END-IF                                                         
                 MOVE K830-NUM-MAIL-FLAT       TO PC93-FLAT                     
                 MOVE K830-NME-MAIL-STR-BLDG   TO PC93-ADDR1                    
                 IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES OR ZEROS        
                    MOVE SPACES                TO PC93-ADDR2                    
                 ELSE                                                           
                    STRING 'SINGAPORE '      K830-NUM-MAIL-POSTAL               
                           DELIMITED BY SIZE INTO PC93-ADDR2                    
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-MAIL-POSTAL TO AV02C015-IN-POSTCODE           
                    PERFORM 4500-CALL-AV02C015     THRU 4500-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC93-BAR-CODE          
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
             IF WS-K830-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K830, STATUS ' WS-K830-STATUS           
                        ', REGN IS  '    FD00-NUM-REGN                          
             ELSE                                                               
                DISPLAY 'ERROR READING K830, STATUS ' WS-K830-STATUS            
                        ', REGN IS  '    FD00-NUM-REGN                          
                MOVE WS-K830-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
                                                                                
           END-IF.                                                              
                                                                                
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------*                                                  
       4500-CALL-AV02C015.                                                      
      *----------------------*                                                  
                                                                                
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       5000-MOVE-REPORT-DTLS.                                                   
      *--------------------------------------------------------------           
           MOVE FD00-NUM-REGN        TO  LC93-REGNO.                            
           MOVE WS-MONTH(WS-MTH)     TO  LC93-MMM-HDR.                          
           MOVE WS-CCYY              TO  LC93-YR-HDR.                           
                                                                                
           IF KD00-AMT-ARR-INSTAL  NOT NUMERIC                                  
              MOVE 0 TO KD00-AMT-ARR-INSTAL                                     
           END-IF.                                                              
                                                                                
           MOVE KD00-NUM-MTH-INSTAL       TO  LC93-MO-ARREAR.                   
           MOVE KD00-AMT-ARR-INSTAL       TO  LC93-AMT-ARREAR.                  
                                                                                
           MOVE WS-DATE-HDR          TO  LC93-DATE-HDR.                         
                                                                                
           IF PC93-FLAT-ADDR NOT = SPACES                                       
              STRING 'BLK '  COMM8-NUM-BLK   ' '                                
                  '#'     COMM8-NUM-LEVEL '-'                                   
                          COMM8-NUM-UNIT-MAIN ' '   DELIMITED BY SIZE           
                          COMM8-NUM-UNIT-SUB  ' '   DELIMITED BY SIZE           
                          COMM8-NME-STREET          DELIMITED BY '   '          
                   ' S('  COMM8-NUM-POSTAL-CODE ')' DELIMITED BY SIZE           
                   INTO   LC93-ADDRESS                                          
              END-STRING                                                        
           END-IF.                                                              
                                                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       5100-MOVE-LETTER-DTLS.                                                   
      *--------------------------------------------------------------           
           MOVE LC93-REGNO          TO  PC93-REGNO.                             
           MOVE WS-DATE-HDR         TO  PC93-DATE-HDR.                          
           MOVE LC93-NAME1          TO  PC93-NAME1B.                            
           MOVE LC93-NAME2          TO  PC93-NAME2B.                            
           MOVE WS-MONTH(WS-MTH)    TO  PC93-MMM.                               
           MOVE WS-CCYY(3:2)        TO  PC93-YY.                                
           MOVE KD00-AMT-ARR-INSTAL TO  PC93-AMT.                               
                                                                                
           STRING FD00-NUM-SCH-ACCNT(1:4) '-'                                   
                  FD00-NUM-SCH-ACCNT(5:4) '-'                                   
                  FD00-NUM-SCH-ACCNT(9:1) '-'                                   
                  FD00-NUM-SCH-ACCNT(10:2)                                      
                  DELIMITED BY SIZE INTO PC93-HDBREF.                           
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       6000-PRINT-ROUTINE.                                                      
      *--------------------------------------------------------------           
      *-->  PRINT REPORT                                                        
           ADD   1                      TO  WS-SNO.                             
           MOVE  WS-SNO                 TO  LC93-DTL-SERIAL.                    
                                                                                
           IF WS-LINE-CTR > WS-MAX-LINE OR                                      
              WS-SNO       = 1                                                  
              ADD            1   TO WS-PAGE-CTR                                 
              MOVE ZEROES        TO LC93-PAGE-HDR                               
              MOVE WS-PAGE-CTR   TO LC93-PAGE-HDR                               
              PERFORM 6100-PRINT-HEADING THRU 6100-EXIT                         
           END-IF.                                                              
                                                                                
           WRITE LC93-PRTREC FROM LC93-DETAILS.                                 
           WRITE LC93-PRTREC FROM LC93-DETAIL2.                                 
           WRITE LC93-PRTREC FROM LC93-DETAIL3.                                 
           WRITE LC93-PRTREC FROM WS-BLANK-LINE.                                
                                                                                
           ADD 4 TO WS-LINE-CTR.                                                
           ADD   1              TO    WS-CNT-LC93-WRITE.                        
                                                                                
      *--> PRINT LETTER                                                         
           WRITE PC93-PRTREC    FROM  PC93-PR-LINE-1 AFTER PAGE.                
           WRITE PC93-PRTREC    FROM  PC93-PR-LINE-2.                           
           WRITE PC93-PRTREC    FROM  PC93-PR-LINE-3.                           
           WRITE PC93-PRTREC    FROM  PC93-PR-LINE-4 AFTER 4.                   
           WRITE PC93-PRTREC    FROM  PC93-PR-LINE-5.                           
           WRITE PC93-PRTREC    FROM  PC93-PR-LINE-6.                           
           WRITE PC93-PRTREC    FROM  PC93-PR-LINE-7.                           
           WRITE PC93-PRTREC    FROM  PC93-PR-LINE-8.                           
           WRITE PC93-PRTREC    FROM  PC93-PR-LINE-9  AFTER 4.                  
           WRITE PC93-PRTREC    FROM  PC93-PR-LINE-10 AFTER 6.                  
           WRITE PC93-PRTREC    FROM  PC93-PR-LINE-11 AFTER 3.                  
           WRITE PC93-PRTREC    FROM  PC93-PR-LINE-12 AFTER 3.                  
                                                                                
           ADD   1              TO    WS-PC93-WRT-CTR.                          
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       6100-PRINT-HEADING.                                                      
      *--------------------------------------------------------------           
            WRITE LC93-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC93-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC93-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC93-PRTREC FROM LC93-HEADER01 AFTER PAGE.                    
            WRITE LC93-PRTREC FROM LC93-HEADER02.                               
            WRITE LC93-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC93-PRTREC FROM LC93-HEADER03.                               
            WRITE LC93-PRTREC FROM LC93-HEADER04.                               
            WRITE LC93-PRTREC FROM WS-DASH-LINE.                                
                                                                                
            INITIALIZE WS-LINE-CTR.                                             
            MOVE  9 TO WS-LINE-CTR.                                             
                                                                                
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------           
       9000-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------           
           CLOSE  BP13FD00                                                      
                  BP13KD00                                                      
                  BP13K800                                                      
                  BP13K820                                                      
                  BP13K830                                                      
                  SY02F001                                                      
                  BP13LC93                                                      
                  BP13PC93.                                                     
                                                                                
                                                                                
           DISPLAY ' RUN   DATE :   ' WS-DATE-HDR.                              
           DISPLAY ' *------------ BP13CC93 AUDIT TRAIL ------------*'.         
           DISPLAY '    '.                                                      
           DISPLAY 'NO. OF RECORDS READ FROM     BP13FD00 = '                   
                   WS-CNT-FD00-READ.                                            
           DISPLAY 'NO. OF RECORDS WRITTEN   TO  BP13LC93 = '                   
                   WS-CNT-LC93-WRITE.                                           
           DISPLAY 'NO. OF RECORDS WRITTEN   TO  BP13PC93 = '                   
                   WS-PC93-WRT-CTR.                                             
                                                                                
           IF WS-KD00-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13KD00 FILE ERROR ' WS-KD00-STATUS             
              MOVE     WS-KD00-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13K800 FILE ERROR ' WS-K800-STATUS             
              MOVE     WS-K800-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13K820 FILE ERROR ' WS-K820-STATUS             
              MOVE     WS-K820-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13K830 FILE ERROR ' WS-K830-STATUS             
              MOVE     WS-K830-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
