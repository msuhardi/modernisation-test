       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C22I.                                                 
      *AUTHOR.        SAUL SIO.                                                 
      *DATE-WRITTEN.  10/08/2004.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE  : PRODUCE F120 FOR WIS NEW FLAT RECORD.         *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTIONS                       *            
      * -------- -------- ----- ------------                       *            
      * BP132551 20040810 SSS2  NEW PROGRAM                        *            
      * BP132551 20040826 SSS2  CHECK KEY-AVAIL NOT = SPACES       *            
      * BP132638 20050113 SSS2  CATER FOR BE CASES                 *            
      * BP133283 20070228 LMS1  CATER FOR 3R CASES                 *            
      * BP133430 20080910 LMS1  NEW SELECTION TYPE WIH             *            
      * BP133771 20100105 LSB1  NEW SELECTION TYPE SBF             *            
      * BP134603 20120824 IMC1  NEW SELECTION TYPE SMR,SHS,AMR     *            
      * BP135163 20140117 RJE1  NEW SELECTION TYPE BTO             *            
      * BP136836 20170812 ESA1  NEW SELECTION TYPE ROF             *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
            SELECT BP13F310 ASSIGN TO BP13F310.                                 
                                                                                
            SELECT BM06K110 ASSIGN TO BM06K110                                  
                   ACCESS MODE     IS RANDOM                                    
                   ORGANIZATION    IS INDEXED                                   
                   RECORD KEY      IS K110-SCH-ACC-NO                           
                   FILE STATUS     IS BM06K110-STATUS.                          
                                                                                
            SELECT BP13K800 ASSIGN TO BP13K800                                  
                   ACCESS MODE     IS RANDOM                                    
                   ORGANIZATION    IS INDEXED                                   
                   RECORD KEY      IS K800-NUM-REGN                             
                   FILE STATUS     IS BP13K800-STATUS.                          
                                                                                
            SELECT BM06F120 ASSIGN TO BM06F120.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F310                                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BP13F310.                                                           
                                                                                
       FD   BM06K110.                                                           
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K800.                                                           
       COPY BP13K800.                                                           
                                                                                
       FD   BM06F120                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 700 CHARACTERS                                      
            RECORDING MODE IS F.                                                
                                                                                
       COPY BM06F120.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 BM06K110-STATUS                    PIC 99     VALUE 00.               
       01 BP13K800-STATUS                    PIC 99     VALUE 00.               
       01 WS-F120-WRITE                      PIC 9(8)   VALUE ZEROS.            
       01 WS-K110-READ                       PIC 9(8)   VALUE ZEROS.            
       01 WS-K800-READ                       PIC 9(8)   VALUE ZEROS.            
       01 WS-K110-UNMATCH                    PIC 9(8)   VALUE ZEROS.            
       01 WS-K800-UNMATCH                    PIC 9(8)   VALUE ZEROS.            
       01 WS-F310-READ                       PIC 9(8)   VALUE ZEROS.            
       01 WS-K110-NOTFND                     PIC 9(8)   VALUE ZEROS.            
       01 WS-K800-NOTFND                     PIC 9(8)   VALUE ZEROS.            
       01 WS-DATE                            PIC X(8)   VALUE SPACES.           
       01 WS-RETCODE                         PIC -ZZZZ.                         
                                                                                
      *-------------------------------------------------------------            
      * BP13C913 PARAMETERS                                                     
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH       PIC X(4).                                
              10 WS-LINK-NUM-ACC       PIC X(5).                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                        
            PERFORM 2000-READ-BP13F310   THRU 2000-EXIT.                        
            PERFORM 3000-PROCESS-DETAIL  THRU 3000-EXIT                         
                    UNTIL F310-SCH-ACC = HIGH-VALUES.                           
            PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            OPEN  INPUT  BP13F310                                               
                         BM06K110                                               
                         BP13K800                                               
                  OUTPUT BM06F120.                                              
                                                                                
            IF BM06K110-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPENING ERROR K110-STATUS IS ' BM06K110-STATUS          
               MOVE BM06K110-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                        
            END-IF.                                                             
                                                                                
            IF BP13K800-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPENING ERROR K800-STATUS IS ' BP13K800-STATUS          
               MOVE BP13K800-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                        
            END-IF.                                                             
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                         
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F310.                                                      
      *-------------------------------------------------------------            
            READ BP13F310 AT END MOVE HIGH-VALUES TO F310-SCH-ACC               
                 GO TO 2000-EXIT.                                               
            ADD 1   TO WS-F310-READ.                                            
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DETAIL.                                                     
      *-------------------------------------------------------------            
            PERFORM 3100-READ-BP13K800 THRU 3100-EXIT.                          
            PERFORM 2000-READ-BP13F310    THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3100-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
            MOVE SPACES        TO K800-NUM-REGN.                                
            MOVE F310-REGN-NO  TO K800-NUM-REGN.                                
                                                                                
            READ BP13K800.                                                      
            EVALUATE BP13K800-STATUS                                            
            WHEN 00                                                             
                 ADD  1 TO WS-K800-READ                                         
                 IF K800-NUM-SCH-ACC NOT = SPACES AND                           
                                           LOW-VALUES AND                       
                                           ZEROES                               
                    PERFORM 3200-READ-BM06K110 THRU 3200-EXIT                   
                 ELSE                                                           
                    ADD  1 TO WS-K800-UNMATCH                                   
                    DISPLAY ' K800 UNMATCH  >  REGN ' K800-NUM-REGN             
                            ',  SCH ACC ' K800-NUM-SCH-ACC                      
                 END-IF                                                         
            WHEN 23                                                             
                 ADD  1 TO WS-K800-NOTFND                                       
            WHEN OTHER                                                          
                 DISPLAY 'BP13K800 READ ERROR : ' BP13K800-STATUS               
                         ', REGN IS  ' K800-NUM-REGN                            
                 MOVE BP13K800-STATUS         TO   RETURN-CODE                  
                 PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3200-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
            MOVE SPACES        TO K110-SCH-ACC-NO.                              
            MOVE F310-SCH-ACC  TO K110-SCH-ACC-NO.                              
                                                                                
            READ BM06K110                                                       
            EVALUATE BM06K110-STATUS                                            
            WHEN 00                                                             
                 ADD  1 TO WS-K110-READ                                         
                 IF (K110-NUM-SELECTION = 'WIS' OR 'WIP' OR 'WIT' OR            
                                          'WIA' OR 'WIB' OR 'WIC' OR            
                                          'SBF' OR 'BTO' OR 'ROF' OR            
                                          'SMR' OR 'SHS' OR 'AMR' OR            
                                          'WIH' OR 'BE ' OR '3R ')              
                    AND                                                         
                    (K110-REGN-NO NOT = SPACES AND                              
                     K110-REGN-NO (1:2) NOT = '##')                             
                    AND                                                         
                    (K110-FLAT-STATUS = 'NW')                                   
                    AND                                                         
                    (K110-DTE-KEY-AVAIL NOT = ZEROS AND                         
                                              SPACES AND                        
                                              LOW-VALUES)                       
                    PERFORM 3300-CREATE-F120 THRU 3300-EXIT                     
                 ELSE                                                           
                    ADD  1 TO WS-K110-UNMATCH                                   
                 END-IF                                                         
            WHEN 23                                                             
                 ADD  1 TO WS-K110-NOTFND                                       
                 DISPLAY 'BM06K110 NOT FND : ' K110-SCH-ACC-NO                  
            WHEN OTHER                                                          
                 DISPLAY 'BM06K110 READ ERROR  ' BM06K110-STATUS                
                         ', KEY-FLD  : ' K110-SCH-ACC-NO                        
                 MOVE BM06K110-STATUS         TO   RETURN-CODE                  
                 PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3300-CREATE-F120.                                                        
      *-------------------------------------------------------------            
            MOVE SPACES                 TO BP13COMM8-REC.                       
            INITIALIZE                     BP13COMM8-REC.                       
                                                                                
            MOVE K110-SCH-ACC-NO        TO WS-LINK-REC.                         
            PERFORM 3400-READ-PIDB      THRU 3400-EXIT.                         
                                                                                
            MOVE SPACES                 TO F120-REC.                            
            INITIALIZE                     F120-REC.                            
                                                                                
            MOVE K110-REC               TO F120-REC(1:500).                     
                                                                                
            MOVE COMM8-NUM-MGMT(1:2)    TO F120-BO-CODE.                        
            MOVE COMM8-NME-STREET       TO F120-STREET-NAME.                    
            MOVE COMM8-NUM-LEVEL        TO F120-LEVEL-NO.                       
            MOVE COMM8-NUM-UNIT-MAIN    TO F120-MAIN-UNIT.                      
            MOVE COMM8-NUM-UNIT-SUB     TO F120-SUB-UNIT.                       
            MOVE COMM8-NUM-POSTAL-CODE  TO F120-POSTAL-CODE.                    
            MOVE COMM8-CDE-TYP-ACTLUSE  TO F120-CDE-TYP-ACTUSE.                 
                                                                                
            WRITE F120-REC.                                                     
            ADD 1 TO WS-F120-WRITE.                                             
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3400-READ-PIDB.                                                          
      *-------------------------------------------------------------            
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
           WHEN +000                                                            
              CONTINUE                                                          
           WHEN +100                                                            
              MOVE SPACES   TO BP13COMM8-REC                                    
              INITIALIZE       BP13COMM8-REC                                    
              DISPLAY 'REC NOT FOUND IN PIDB: ' K110-SCH-ACC-NO                 
           WHEN OTHER                                                           
              MOVE COMM8-CDE-SYSERR      TO WS-RETCODE                          
              DISPLAY 'ERROR IN CALLING BP13C913, RETCODE: '                    
                      WS-RETCODE                                                
              MOVE 99                    TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
            DISPLAY ' '.                                                        
            DISPLAY '- BP13C22I CONTROL LIST -------------'                     
                    ' DATE : ' WS-DATE.                                         
            DISPLAY '--------------------------------------------'.             
            DISPLAY ' 1) TOT BP13F310 READ      : ' WS-F310-READ.               
            DISPLAY ' 2) TOT BM06K110 READ      : ' WS-K110-READ.               
            DISPLAY ' 3) TOT BM06K110 NOT FND   : ' WS-K110-NOTFND.             
            DISPLAY ' 4) TOT BM06K110 FLT UNMTH : ' WS-K110-UNMATCH.            
            DISPLAY ' 5) TOT BP13K800 READ      : ' WS-K800-READ.               
            DISPLAY ' 6) TOT BP13K800 NOT FND   : ' WS-K800-NOTFND.             
            DISPLAY ' 7) TOT BP13K800 FLT UNMTH : ' WS-K800-UNMATCH.            
            DISPLAY ' 8) TOT BM06F120 WRITE     : ' WS-F120-WRITE.              
            DISPLAY ' '.                                                        
                                                                                
            CLOSE  BP13F310                                                     
                   BM06K110                                                     
                   BP13K800                                                     
                   BM06F120.                                                    
                                                                                
            IF BM06K110-STATUS NOT = 00 AND 97                                  
               DISPLAY 'CLOSING ERROR BM06K110 : ' BM06K110-STATUS              
               MOVE BM06K110-STATUS TO RETURN-CODE                              
            END-IF.                                                             
                                                                                
            IF BP13K800-STATUS NOT = 00 AND 97                                  
               DISPLAY 'CLOSING ERROR BP13K800 : ' BP13K800-STATUS              
               MOVE BP13K800-STATUS TO RETURN-CODE                              
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
