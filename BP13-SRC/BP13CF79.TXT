       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF79.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  07/02/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  TO PREPARE FLAT AND ETHINIC QUOTA FOR       *            
      *                SHORTLISTING 2ND HUNDRED PERCENT            *            
      *                                                            *            
      *  INPUT      :  BP13K010  BP13.K010.TENDER                  *            
      *  OUTPUT     :  BP13F010  BP13.F010.TENDER.PA               *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP134782  SMR2  07022013  NEW PROGRAM                      *            
      * BP134857  SMR2  18032013  MOVE K010-NUM-PPS TO F010-NUM-PPS*            
      * BP134861  EAA1  30042013  EXPAND BP13K010 TO 600           *            
      * BP134914  SMR2  18062013  CATER FOR SAP AND AST            *            
      * BP135028  SMR2  15082013  MOVE K010-NUM-FTS TO F010-NUM-FTS*            
      * BP136055  SMR2  10122015  CATER FOR ELDERLY / REMOVE SAP   *            
      * BP137750  AM25  05082019  ADD NEW OUTPUT FILE P13F010A     *            
      * BP139516  AM25  27022023  INCLUDE NUM-CAP-UPGRADER         *            
      *============================================================*            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K010 ASSIGN TO BP13K010                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS SEQUENTIAL                                 
                  RECORD KEY      IS K010-KEY-FLD                               
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
           SELECT BP13F010 ASSIGN TO BP13F010.                                  
           SELECT P13F010A ASSIGN TO P13F010A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13K010                                                            
            RECORD CONTAINS 600 CHARACTERS.                                     
       COPY BP13K010.                                                           
                                                                                
       FD   BP13F010                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS       STANDARD.                                       
       COPY BP13F010.                                                           
                                                                                
       FD   P13F010A                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS       STANDARD.                                       
       01   P13F010A-TENDER               PIC X(600).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K010-STATUS             PIC 9(02) VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-K010-READ               PIC 9(06) VALUE ZEROES.               
           05  WS-F010-WRITE              PIC 9(06) VALUE ZEROES.               
           05  WS-F010A-WRITE             PIC 9(06) VALUE ZEROES.               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-K010-EOF                PIC X(01) VALUE 'N'.                  
                                                                                
       01  WS-DATE-TIME-VARS.                                                   
           05  WS-CURR-DATE-TIME.                                               
               10  WS-SYSTEM-DATE.                                              
                   15  WS-SYS-CCYY        PIC 9(04).                            
                   15  WS-SYS-MM          PIC 9(02).                            
                   15  WS-SYS-DD          PIC 9(02).                            
               10  WS-SYSTEM-TIME         PIC 9(08).                            
           05  WS-EDITED-DATE             PIC X(10).                            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-MAX-PA1                 PIC 9(05).                            
           05  WS-MAX-PA2                 PIC 9(05).                            
           05  WS-MAX-PA3                 PIC 9(05).                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13K010      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-BP13K010   THRU 3000-EXIT                       
             UNTIL WS-K010-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13K010                                                 
                OUTPUT BP13F010                                                 
                       P13F010A.                                                
                                                                                
           IF WS-K010-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K010 OPEN ERROR ' WS-K010-STATUS                
              MOVE WS-K010-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CURR-DATE-TIME.                
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE.                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13K010.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K010 AT END                                                 
                MOVE 'Y'                   TO WS-K010-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-K010-READ.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-BP13K010.                                                   
      *-------------------------------------------------------------            
                                                                                
           IF K010-KEY-FLD = SPACES OR LOW-VALUES                               
              PERFORM 2000-READ-BP13K010   THRU 2000-EXIT                       
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 4000-COMPUTE-PA         THRU 4000-EXIT.                      
           PERFORM 5000-WRITE-BP13F010     THRU 5000-EXIT.                      
           PERFORM 6000-WRITE-P13F010A     THRU 6000-EXIT.                      
           PERFORM 2000-READ-BP13K010      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-COMPUTE-PA.                                                         
      *--------------------------------------------------------*                
                                                                                
           COMPUTE WS-MAX-PA1 = K010-NUM-MAX-MTF1 + K010-NUM-MAX-CCC1 +         
                                K010-NUM-MAX-RH1  + K010-NUM-MAX-REN1 +         
                                K010-NUM-MAX-JB1  + K010-NUM-MAX-PA1  +         
                                K010-NUM-MAX-RDC1 + K010-NUM-MAX-TCP1.          
                                                                                
           COMPUTE WS-MAX-PA2 = K010-NUM-MAX-MTF2 + K010-NUM-MAX-CCC2 +         
                                K010-NUM-MAX-RH2  + K010-NUM-MAX-REN2 +         
                                K010-NUM-MAX-JB2  + K010-NUM-MAX-PA2  +         
                                K010-NUM-MAX-RDC2 + K010-NUM-MAX-TCP2.          
                                                                                
           COMPUTE WS-MAX-PA3 = K010-NUM-MAX-MTF3 + K010-NUM-MAX-CCC3 +         
                                K010-NUM-MAX-RH3  + K010-NUM-MAX-REN3 +         
                                K010-NUM-MAX-JB3  + K010-NUM-MAX-PA3  +         
                                K010-NUM-MAX-RDC3 + K010-NUM-MAX-TCP3.          
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-WRITE-BP13F010.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                     TO BP13F010-TENDER.                  
           INITIALIZE                         BP13F010-TENDER.                  
                                                                                
           MOVE K010-KEY-FLD               TO F010-KEY-FLD.                     
           MOVE K010-NUM-TOT-UNIT          TO F010-NUM-TOT-UNIT                 
                                              F010-NUM-TOT-PA.                  
           MOVE WS-MAX-PA1                 TO F010-NUM-MAX-PA1.                 
           MOVE WS-MAX-PA2                 TO F010-NUM-MAX-PA2.                 
           MOVE WS-MAX-PA3                 TO F010-NUM-MAX-PA3.                 
           MOVE 100                        TO F010-NUM-PA.                      
           MOVE K010-NUM-C1                TO F010-NUM-C1.                      
           MOVE K010-NUM-C2                TO F010-NUM-C2.                      
           MOVE K010-NUM-C3                TO F010-NUM-C3.                      
           MOVE K010-NUM-NHH               TO F010-NUM-NHH.                     
           MOVE K010-NUM-UPG               TO F010-NUM-UPG.                     
           MOVE K010-NUM-PPS               TO F010-NUM-PPS.                     
           MOVE K010-NUM-AST               TO F010-NUM-AST.                     
           MOVE K010-NUM-FTS               TO F010-NUM-FTS.                     
           MOVE K010-NUM-TOT-ELDER-MIN     TO F010-NUM-TOT-ELDER-MIN.           
           MOVE K010-NUM-CAP-UPGRADER      TO F010-NUM-CAP-UPGRADER.            
           MOVE WS-SYSTEM-DATE             TO F010-DTE-UPDATE.                  
           MOVE WS-SYSTEM-TIME             TO F010-TME-SYSTEM.                  
           MOVE 'P13CF79'                  TO F010-USERID.                      
                                                                                
           WRITE BP13F010-TENDER.                                               
           ADD 1                           TO WS-F010-WRITE.                    
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-WRITE-P13F010A.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                     TO BP13F010-TENDER.                  
           INITIALIZE                         BP13F010-TENDER.                  
                                                                                
           MOVE K010-KEY-FLD               TO F010-KEY-FLD.                     
           MOVE K010-NUM-TOT-UNIT          TO F010-NUM-TOT-UNIT                 
                                              F010-NUM-TOT-PA.                  
                                                                                
           IF K010-NUM-MAX-PA1 > 0                                              
              MOVE K010-NUM-TOT-UNIT       TO F010-NUM-MAX-PA1                  
           END-IF.                                                              
                                                                                
           IF K010-NUM-MAX-PA2 > 0                                              
              MOVE K010-NUM-TOT-UNIT       TO F010-NUM-MAX-PA2                  
           END-IF.                                                              
                                                                                
           IF K010-NUM-MAX-PA3 > 0                                              
              MOVE K010-NUM-TOT-UNIT       TO F010-NUM-MAX-PA3                  
           END-IF.                                                              
                                                                                
           MOVE 100                        TO F010-NUM-PA.                      
           MOVE K010-NUM-C1                TO F010-NUM-C1.                      
           MOVE K010-NUM-C2                TO F010-NUM-C2.                      
           MOVE K010-NUM-C3                TO F010-NUM-C3.                      
           MOVE K010-NUM-NHH               TO F010-NUM-NHH.                     
           MOVE K010-NUM-UPG               TO F010-NUM-UPG.                     
           MOVE K010-NUM-PPS               TO F010-NUM-PPS.                     
           MOVE K010-NUM-AST               TO F010-NUM-AST.                     
           MOVE K010-NUM-FTS               TO F010-NUM-FTS.                     
           MOVE K010-NUM-TOT-ELDER-MIN     TO F010-NUM-TOT-ELDER-MIN.           
           MOVE K010-NUM-CAP-UPGRADER      TO F010-NUM-CAP-UPGRADER.            
           MOVE WS-SYSTEM-DATE             TO F010-DTE-UPDATE.                  
           MOVE WS-SYSTEM-TIME             TO F010-TME-SYSTEM.                  
           MOVE 'P13CF79'                  TO F010-USERID.                      
                                                                                
           WRITE P13F010A-TENDER FROM BP13F010-TENDER.                          
           ADD 1                           TO WS-F010A-WRITE.                   
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '==============================================='.           
           DISPLAY '      PROGRAM : BP13CF79       ' WS-EDITED-DATE.            
           DISPLAY '==============================================='.           
           DISPLAY ' '.                                                         
           DISPLAY 'BP13K010 RECORDS READ       ' WS-K010-READ.                 
           DISPLAY 'BP13F010 RECORDS WRITTEN    ' WS-F010-WRITE.                
           DISPLAY 'P13F010A RECORDS WRITTEN    ' WS-F010A-WRITE.               
           DISPLAY ' '.                                                         
           DISPLAY '==============================================='.           
                                                                                
           CLOSE BP13K010                                                       
                 BP13F010                                                       
                 P13F010A.                                                      
                                                                                
           IF WS-K010-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'FILE BP13K010 CLOSE ERROR ' WS-K010-STATUS               
              MOVE WS-K010-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
