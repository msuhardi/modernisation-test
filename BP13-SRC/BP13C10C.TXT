      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C10C.                                                 
      *AUTHOR.        ELGER DE GUZMAN.                                          
      *DATE-WRITTEN.  03 MARCH 2011.                                            
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE  1.: CREATE AND UPDATE RECORD FOR BM06K810      *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BM06F800                                              *           
      *   2.  BM06K110                                              *           
      *                                                             *           
      *   INPUT/OUTPUT:                                             *           
      *   1.  BM06K810                                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP134309 03/03/2011 ESD1 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F800 ASSIGN       TO BM06F800.                            
                                                                                
           SELECT BM06K810 ASSIGN       TO BM06K810                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K810-KEY-FLD                         
                           FILE STATUS  IS WS-K810-STATUS                       
                           ACCESS MODE  IS DYNAMIC.                             
                                                                                
           SELECT BM06K110 ASSIGN       TO BM06K110                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K110-KEY-FLD                         
                           FILE STATUS  IS WS-K110-STATUS                       
                           ACCESS MODE  IS DYNAMIC.                             
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BM06F800                                                            
            RECORD CONTAINS 800 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BM06F800.                                                           
                                                                                
       FD   BM06K810                                                            
            RECORD CONTAINS  850 CHARACTERS.                                    
       COPY BM06K810.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS   500 CHARACTERS.                                   
       COPY BM06K110.                                                           
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-K810-STATUS           PIC 9(02)  VALUE ZEROES.                    
       01  WS-K110-STATUS           PIC 9(02)  VALUE ZEROES.                    
       01  WS-F800-EOF              PIC X      VALUE SPACES.                    
       01  WS-F800-READ-CNT         PIC 9(08)  VALUE ZEROES.                    
       01  WS-K810-WRITE-CNT        PIC 9(08)  VALUE ZEROES.                    
       01  WS-K810-REWRITE-CNT      PIC 9(08)  VALUE ZEROES.                    
       01  WS-DATE                  PIC X(8)   VALUE SPACES.                    
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-PRE-PROCESS      THRU 1000-EXIT.                        
           PERFORM 2000-READ-BM06F800    THRU 2000-EXIT.                        
           PERFORM 3000-PROCESS-REC      THRU 3000-EXIT                         
                   UNTIL WS-F800-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-PRE-PROCESS.                                                        
      *----------------*                                                        
                                                                                
           OPEN INPUT  BM06F800                                                 
                       BM06K110                                                 
                I-O    BM06K810.                                                
                                                                                
           IF WS-K810-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BM06K810 - OPEN ERROR: ' WS-K810-STATUS                  
              MOVE WS-K810-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
           IF WS-K110-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BM06K110 - OPEN ERROR: ' WS-K110-STATUS                  
              MOVE WS-K110-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
           MOVE  FUNCTION CURRENT-DATE  TO  WS-DATE.                            
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       2000-READ-BM06F800.                                                      
      *------------------*                                                      
                                                                                
           READ BM06F800 AT END                                                 
                MOVE 'Y' TO WS-F800-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F800-READ-CNT.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           PERFORM 3100-PROCESS-BM06K810  THRU 3100-EXIT.                       
           PERFORM 2000-READ-BM06F800     THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3100-PROCESS-BM06K810.                                                   
      *----------------------*                                                  
                                                                                
           PERFORM 3400-READ-BM06K110 THRU 3400-EXIT                            
                                                                                
           IF K110-DTE-ALLOCN  =  F800-DTE-ALLOCN                               
              CONTINUE                                                          
           ELSE                                                                 
              GO TO 3100-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES                TO BM06K810-REC.                          
           INITIALIZE                    BM06K810-REC.                          
                                                                                
           MOVE   F800-CDE-SELTY      TO K810-NUM-SELTY-LOG                     
           MOVE   F800-DTE-ALLOCN     TO K810-DTE-LAUNCH-LOG                    
           MOVE   F800-SCH-ACC-NO     TO K810-SCH-ACC-NO-LOG                    
           MOVE   WS-DATE             TO K810-DTE-UPDATED                       
                                                                                
           READ BM06K810 KEY IS K810-KEY-FLD                                    
                                                                                
           EVALUATE WS-K810-STATUS                                              
             WHEN 00                                                            
                  MOVE    K110-REC    TO   BM06K810-REC(1:500)                  
                  PERFORM 3200-REWRITE-BM06K810 THRU 3200-EXIT                  
                  ADD 1 TO WS-K810-REWRITE-CNT                                  
             WHEN 23                                                            
                  PERFORM 3300-WRITE-BM06K810   THRU 3300-EXIT                  
             WHEN OTHER                                                         
                  DISPLAY 'BM06K810 - READ ERROR: ' WS-K810-STATUS              
                  PERFORM  9000-CLOSE-FILES   THRU  9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3200-REWRITE-BM06K810.                                                   
      *----------------------*                                                  
                                                                                
           REWRITE BM06K810-REC                                                 
                                                                                
           EVALUATE WS-K810-STATUS                                              
             WHEN 00                                                            
                  CONTINUE                                                      
             WHEN OTHER                                                         
                  DISPLAY 'BM06K810 - REWRITE ERROR: ' WS-K810-STATUS           
                  PERFORM  9000-CLOSE-FILES   THRU  9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3300-WRITE-BM06K810.                                                     
      *----------------------*                                                  
                                                                                
           WRITE BM06K810-REC.                                                  
                                                                                
           EVALUATE WS-K810-STATUS                                              
             WHEN 00                                                            
                  ADD 1 TO WS-K810-WRITE-CNT                                    
                  MOVE    K110-REC          TO   BM06K810-REC(1:500)            
                  MOVE    F800-REC(501:300) TO   BM06K810-REC(501:300)          
                  PERFORM 3200-REWRITE-BM06K810 THRU 3200-EXIT                  
             WHEN OTHER                                                         
                  DISPLAY 'BM06K810 - WRITE ERROR: ' WS-K810-STATUS             
                  PERFORM  9000-CLOSE-FILES   THRU  9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3400-READ-BM06K110.                                                      
      *----------------------*                                                  
                                                                                
           MOVE SPACES            TO K110-REC.                                  
           INITIALIZE                K110-REC.                                  
                                                                                
           MOVE F800-SCH-ACC-NO   TO K110-KEY-FLD.                              
                                                                                
           READ BM06K110 KEY IS K110-KEY-FLD                                    
                                                                                
           EVALUATE WS-K110-STATUS                                              
             WHEN 00                                                            
                  CONTINUE                                                      
             WHEN 23                                                            
                  DISPLAY 'SCH NOT FOUND ON BM06K110 ' F800-SCH-ACC-NO          
             WHEN OTHER                                                         
                  DISPLAY 'BM06K110 - READ ERROR: ' WS-K110-STATUS              
                  PERFORM  9000-CLOSE-FILES   THRU  9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BM06C10C  *************'.                           
           DISPLAY 'RECS READ    FROM BM06F800 : ' WS-F800-READ-CNT.            
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN IN BM06K810   : ' WS-K810-WRITE-CNT.           
           DISPLAY 'RECS REWRITE IN BM06K810   : ' WS-K810-REWRITE-CNT.         
           DISPLAY '                              '.                            
                                                                                
           CLOSE BM06F800                                                       
                 BM06K810                                                       
                 BM06K110.                                                      
                                                                                
           IF WS-K810-STATUS NOT EQUAL 00                                       
              DISPLAY 'BM06K810 - CLOSE ERROR: ' WS-K810-STATUS                 
              MOVE WS-K810-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT EQUAL 00                                       
              DISPLAY 'BM06K110 - CLOSE ERROR: ' WS-K110-STATUS                 
              MOVE WS-K110-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
