       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C358.                                                    
       AUTHOR. REY ORBITA GASTADOR.                                             
      *================================================================*        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT   (SOC)                  *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1.  UPON SUB-TRANSFER, PROGRAM TO MATCH THE   *        
      *                      OCCUPIER RECORDS OF SUB-TRANSFER WITH THE *        
      *                      OCCUPIER RECORDS OF SOC.                  *        
      *                                                                *        
      *                  2.  CREATE NEW OCCUPIER FOR NEW TRANSFEREE    *        
      *                      UPDATE OCCUP INFOR FOR EXTSING OCCUPIER   *        
      *                      DELETE OCCUP FOR TRANSFEROR               *        
      *                                                                *        
      *   INPUT FILES :      BP13F890                                  *        
      *                      BP13F820                                  *        
      *                                                                *        
      *   I-O FILES   :      BP13K820                                  *        
      *                                                                *        
      *   LISTING     :      BP13L358                                  *        
      *                                                                *        
      *                                                                *        
      *   CHGE-NO   DATE   BY   DESCRIPTION                            *        
      *   -------  ------  ---  -------------------------------------  *        
      * BP130094   07/03/96 LSB1 TO CATER FOR SUB-TRANSFER ENHANCEMENTS*        
      * BP130094   05/06/96 LJL1 TO PAGE BREAK FOR EVERY CASE TO       *        
      *                          FACILITATE FILING.                    *        
      * BP130418   04/06/98 RHB  Y2K                                   *        
      * BP130850   21/09/01 AS1  WRITE CDB INFO INTO BP13F915          *        
      * BP130850   13/12/01 AS1  WRITE REGN-NO TO BP13F915             *        
      * BP134527   08/05/12 IMC1 FIXED DATA EXCEPTION ISSUE            *        
      * BP137200   08/03/18 FNP1 CHANGE 'DE' TXN-TYPE TO 'TE' IN F915           
      *================================================================*        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F890 ASSIGN TO BP13F890.                                  
           SELECT BP13F820 ASSIGN TO BP13F820.                                  
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS K800-FS.                             
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K820-KEY-FLD                         
                           FILE STATUS  IS K820-FS.                             
           SELECT BP13K060 ASSIGN TO BP13K060                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K060-KEY-FLD                         
                           FILE STATUS  IS K060-FS.                             
           SELECT BP13F915 ASSIGN TO BP13F915.                                  
           SELECT BP13L358 ASSIGN TO BP13L358.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F890                                                             
           BLOCK CONTAINS    0  RECORDS                                         
           RECORD CONTAINS 400 CHARACTERS                                       
           RECORDING MODE   IS F.                                               
       COPY BP13F890.                                                           
                                                                                
       FD  BP13F820                                                             
           BLOCK CONTAINS    0  RECORDS                                         
           RECORD CONTAINS 400 CHARACTERS                                       
           RECORDING MODE   IS F.                                               
       COPY BP13F820.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP13K060                                                             
           RECORD CONTAINS 25 CHARACTERS.                                       
       COPY BP13K060.                                                           
                                                                                
       FD  BP13F915                                                             
           BLOCK CONTAINS  0 RECORDS                                            
           RECORD CONTAINS 800 CHARACTERS                                       
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
       COPY BP13F915.                                                           
                                                                                
       FD  BP13L358                                                             
           BLOCK CONTAINS  0 RECORDS                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORD IS OMITTED                                              
           RECORDING MODE IS F.                                                 
       01  BP13L358-REC   PIC X(132).                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-WORKING-AREA.                                                     
           05  WS-FLAGS.                                                        
               10 F890-EOF             PIC X.                                   
                  88 F890-IS-EOF       VALUE 'Y'.                               
               10 F820-EOF             PIC X.                                   
                  88 F820-IS-EOF       VALUE 'Y'.                               
               10 WS-UPD-SW            PIC X.                                   
                  88 UPDATE-K820       VALUE 'Y'.                               
           05  WS-FILE-STATUS.                                                  
               10 K800-FS              PIC 99.                                  
               10 K820-FS              PIC 99.                                  
               10 K060-FS              PIC 99.                                  
           05  WS-COUNTERS.                                                     
               10 WS-SEQ               PIC 9(04)     VALUE ZEROES.              
               10 F890-READ-CTR        PIC 9(04)     VALUE ZEROES.              
               10 F820-READ-CTR        PIC 9(04)     VALUE ZEROES.              
               10 F915-WRTE-CTR        PIC 9(04)     VALUE ZEROES.              
               10 K800-READ-CTR        PIC 9(04)     VALUE ZEROES.              
               10 K820-WRTE-CTR        PIC 9(04)     VALUE ZEROES.              
               10 K820-RWRT-CTR        PIC 9(04)     VALUE ZEROES.              
               10 K820-DLET-CTR        PIC 9(04)     VALUE ZEROES.              
               10 WS-LINE-CTR          PIC 9(04)     VALUE 61.                  
               10 WS-PAGE-CTR          PIC 9(04)     VALUE ZEROES.              
           05  WS-MISC.                                                         
               10 WS-PREV-REGN         PIC X(8)      VALUE SPACES.              
               10 WS-PAGE-DATE.                                                 
                  20 WS-DD             PIC X(02).                               
                  20 FILLER            PIC X VALUE '/'.                         
                  20 WS-MM             PIC X(02).                               
                  20 FILLER            PIC X VALUE '/'.                         
                  20 WS-CCYY           PIC X(04).                               
               10 WS-UPDATE-CODE       PIC X.                                   
                                                                                
       01  WS-PRINT-AREA.                                                       
           05 WS-PRINT-LINE            PIC X(132).                              
           05 WS-PAGE-HDG1   REDEFINES WS-PRINT-LINE.                           
              10 WS-HEADER-A           PIC X(13).                               
              10 WS-HEADER-B           PIC X(31).                               
              10 WS-HEADER-C           PIC X(56).                               
              10 WS-HEADER-D           PIC X(07).                               
              10 WS-HEADER-E           PIC X(13).                               
              10 WS-HEADER-F           PIC X(07).                               
              10 WS-HEADER-G           PIC X(05).                               
              10 WS-HDER-RED  REDEFINES WS-HEADER-G.                            
                 15 WS-HDER-G          PIC ZZZ9.                                
                 15 FILLER             PIC X.                                   
           05 WS-PAGE-HDG2   REDEFINES WS-PRINT-LINE.                           
              10 FILLER                PIC X(100).                              
              10 WS-USER               PIC X(32).                               
           05 WS-SUBHDG     REDEFINES  WS-PRINT-LINE.                           
              10 FILLER                PIC X(34).                               
              10 WS-SUBHDG-A           PIC X(98).                               
           05 WS-COLUMN      REDEFINES  WS-PRINT-LINE.                          
              10 WS-SEQ-NUM            PIC X(06).                               
              10 WS-SEQ-RED   REDEFINES WS-SEQ-NUM.                             
                 15 WS-SEQ-FLD         PIC ZZZ9.                                
                 15 FILLER             PIC X(02).                               
              10 WS-REGN               PIC X(10).                               
              10 WS-NRIC               PIC X(11).                               
              10 WS-NME-OCCP           PIC X(22).                               
              10 WS-CPF                PIC X(10).                               
              10 WS-ETHNIC             PIC X(08).                               
              10 WS-ETHNIC-RED REDEFINES WS-ETHNIC.                             
                 15 FILLER             PIC X(02).                               
                 15 WS-ETHNIC-FLD      PIC X(06).                               
              10 WS-SEX                PIC X(05).                               
              10 WS-SEX-RED    REDEFINES WS-SEX.                                
                 15 FILLER             PIC X.                                   
                 15 WS-SEX-FLD         PIC X(04).                               
              10 WS-MARITAL-STATUS     PIC X(04).                               
              10 WS-DTE-BIRTH          PIC X(12).                               
              10 WS-RELATIONSHIP       PIC X(22).                               
              10 WS-CITIZENSHIP        PIC X(06).                               
              10 WS-CITZ-RED   REDEFINES WS-CITIZENSHIP.                        
                 15 FILLER             PIC X.                                   
                 15 WS-CITZ-FLD        PIC X(05).                               
              10 WS-NATIONALITY        PIC X(08).                               
              10 WS-NATLTY-RED REDEFINES WS-NATIONALITY.                        
                 15 FILLER             PIC XX.                                  
                 15 WS-NATLTY-FLD      PIC X(06).                               
              10 WS-REMARK             PIC X(08).                               
                                                                                
       77 WS-NUM-NRIC                  PIC X(9).                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *====================                                                     
       0000-MAIN-LOGIC.                                                         
      *====================                                                     
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-BP13F890 THRU 2000-EXIT.                           
           PERFORM 3000-READ-BP13F820 THRU 3000-EXIT.                           
           PERFORM 4000-PROCESS-RTN   THRU 4000-EXIT                            
                   UNTIL (F820-KEY-FLD = HIGH-VALUES)                           
                     AND (F890-KEY-FLD = HIGH-VALUES).                          
           PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT.                           
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=======================                                                  
       1000-OPEN-FILES.                                                         
      *=======================                                                  
           OPEN INPUT  BP13F890                                                 
                       BP13F820                                                 
                       BP13K800                                                 
                       BP13K060                                                 
                I-O    BP13K820                                                 
                OUTPUT BP13F915                                                 
                       BP13L358.                                                
                                                                                
           IF K800-FS NOT = ZEROES AND 97                                       
              DISPLAY 'BP13K800 - OPEN ERROR, FS = ' K800-FS                    
              GO TO    9999-CLOSE-FILES                                         
           END-IF.                                                              
                                                                                
           IF K820-FS NOT = ZEROES AND 97                                       
              DISPLAY 'BP13K820 - OPEN ERROR, FS = ' K820-FS                    
              GO TO    9999-CLOSE-FILES                                         
           END-IF.                                                              
                                                                                
           IF K060-FS NOT = ZEROES AND 97                                       
              DISPLAY 'BP13K060 - OPEN ERROR, FS = ' K060-FS                    
              GO TO    9999-CLOSE-FILES                                         
           END-IF.                                                              
                                                                                
           STRING FUNCTION CURRENT-DATE(7:2) '/'                                
                  FUNCTION CURRENT-DATE(5:2) '/'                                
                  FUNCTION CURRENT-DATE(1:4)                                    
           DELIMITED BY SIZE INTO WS-PAGE-DATE.                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================                                                
       2000-READ-BP13F890.                                                      
      *=========================                                                
           READ BP13F890 AT END                                                 
                MOVE HIGH-VALUES TO F890-KEY-FLD                                
                GO TO 2000-EXIT.                                                
           ADD  1 TO F890-READ-CTR.                                             
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================                                                
       3000-READ-BP13F820.                                                      
      *=========================                                                
           READ BP13F820 AT END                                                 
                MOVE HIGH-VALUES TO F820-KEY-FLD                                
                GO TO 3000-EXIT.                                                
           ADD  1 TO F820-READ-CTR.                                             
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *============================                                             
       4000-PROCESS-RTN.                                                        
      *============================                                             
           IF F890-KEY-FLD = F820-KEY-FLD                                       
              PERFORM 4100-COMPARE-FIELDS THRU 4100-EXIT                        
              IF UPDATE-K820                                                    
                 PERFORM 4200-UPDATE-BP13K820 THRU 4200-EXIT                    
              END-IF                                                            
              PERFORM 2000-READ-BP13F890 THRU 2000-EXIT                         
              PERFORM 3000-READ-BP13F820 THRU 3000-EXIT                         
           END-IF.                                                              
                                                                                
           IF F890-KEY-FLD < F820-KEY-FLD                                       
              WRITE BP13K820-REC FROM BP13F890-REC                              
              IF K820-FS = 00                                                   
                 ADD   1  TO K820-WRTE-CTR                                      
                 MOVE 'A' TO WS-UPDATE-CODE                                     
                 PERFORM 5100-MOVE-DETAILS THRU 5100-EXIT                       
                 PERFORM 5300-PRINT-DETAIL THRU 5300-EXIT                       
              ELSE                                                              
                 DISPLAY 'BP13K820 - WRITE ERROR, FS = ' K820-FS                
              END-IF                                                            
              PERFORM 2000-READ-BP13F890 THRU 2000-EXIT                         
           END-IF.                                                              
                                                                                
           IF F890-KEY-FLD > F820-KEY-FLD                                       
              MOVE F820-KEY-FLD      TO K820-KEY-FLD                            
              READ BP13K820                                                     
              EVALUATE K820-FS                                                  
              WHEN 00                                                           
                 MOVE 'D' TO WS-UPDATE-CODE                                     
                 PERFORM 5100-MOVE-DETAILS     THRU 5100-EXIT                   
                 PERFORM 8000-DEL-BP13F915-PSN THRU 8000-EXIT                   
                                                                                
                 DELETE BP13K820                                                
                 IF K820-FS = 00                                                
                    ADD   1  TO K820-DLET-CTR                                   
                    PERFORM 5300-PRINT-DETAIL THRU 5300-EXIT                    
                 ELSE                                                           
                    DISPLAY 'BP13K820 - DELETE ERROR, FS = ' K820-FS            
                 END-IF                                                         
              WHEN 23                                                           
                 DISPLAY 'BP13K820 - RECORD NOT FOUND,KEY=' K820-KEY-FLD        
              WHEN OTHER                                                        
                 DISPLAY 'BP13K820 - DELETE ERROR, FS = ' K820-FS               
              END-EVALUATE                                                      
              PERFORM 3000-READ-BP13F820 THRU 3000-EXIT                         
           END-IF.                                                              
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=======================                                                  
       4100-COMPARE-FIELDS.                                                     
      *=======================                                                  
           MOVE 'N'                    TO WS-UPD-SW.                            
                                                                                
           IF F890-AMT-INCOME NOT NUMERIC                                       
              MOVE ZEROES              TO F890-AMT-INCOME                       
           END-IF                                                               
                                                                                
           IF F820-AMT-INCOME NOT NUMERIC                                       
              MOVE ZEROES              TO F820-AMT-INCOME                       
           END-IF                                                               
                                                                                
           IF F890-AMT-INCOME-TP NOT NUMERIC                                    
              MOVE ZEROES              TO F890-AMT-INCOME-TP                    
           END-IF                                                               
                                                                                
           IF F820-AMT-INCOME-TP NOT NUMERIC                                    
              MOVE ZEROES              TO F820-AMT-INCOME-TP                    
           END-IF                                                               
                                                                                
           IF (F890-NUM-CPF            NOT = F820-NUM-CPF) OR                   
              (F890-NME-OCCP           NOT = F820-NME-OCCP) OR                  
              (F890-NUM-ETHNIC         NOT = F820-NUM-ETHNIC) OR                
              (F890-NUM-SEX            NOT = F820-NUM-SEX) OR                   
              (F890-NUM-MARITAL-STATUS NOT = F820-NUM-MARITAL-STATUS) OR        
              (F890-DTE-BIRTH          NOT = F820-DTE-BIRTH) OR                 
              (F890-NUM-CITIZENSHIP    NOT = F820-NUM-CITIZENSHIP) OR           
              (F890-NUM-RELATIONSHIP   NOT = F820-NUM-RELATIONSHIP) OR          
              (F890-AMT-INCOME         NOT = F820-AMT-INCOME) OR                
              (F890-NUM-NATIONALITY    NOT = F820-NUM-NATIONALITY) OR           
              (F890-AMT-INCOME-TP      NOT = F820-AMT-INCOME-TP) OR             
              (F890-NUM-LESSEE-CHECK   NOT = F820-NUM-LESSEE-CHECK) OR          
              (F890-NUM-FREEZE-OCCP    NOT = F820-NUM-FREEZE-OCCP) OR           
              (F890-NUM-NRIC-SPOUSE    NOT = F820-NUM-NRIC-SPOUSE) OR           
              (F890-NUM-HP-PGR         NOT = F820-NUM-HP-PGR) OR                
              (F890-NUM-PHONE-OFF      NOT = F820-NUM-PHONE-OFF) OR             
              (F890-NME-SPOUSE         NOT = F820-NME-SPOUSE) OR                
              (F890-NUM-EDUCATION      NOT = F820-NUM-EDUCATION) OR             
              (F890-NUM-PHONE          NOT = F820-NUM-PHONE)                    
                MOVE 'Y'               TO WS-UPD-SW                             
           END-IF.                                                              
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=======================                                                  
       4200-UPDATE-BP13K820.                                                    
      *=======================                                                  
           MOVE F820-KEY-FLD   TO K820-KEY-FLD.                                 
           READ BP13K820.                                                       
           EVALUATE K820-FS                                                     
           WHEN 00                                                              
              PERFORM  7000-UPD-BP13F915-PSN THRU 7000-EXIT                     
              PERFORM  4300-REWRITE-K820 THRU 4300-EXIT                         
           WHEN 23                                                              
              DISPLAY 'BP13K820 - RECORD NOT FOUND, KEY = ' K820-KEY-FLD        
           WHEN OTHER                                                           
              DISPLAY 'BP13K820 - READ ERROR, FS = ' K820-FS                    
           END-EVALUATE.                                                        
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=======================                                                  
       4300-REWRITE-K820.                                                       
      *=======================                                                  
           REWRITE BP13K820-REC FROM BP13F890-REC.                              
           IF K820-FS = 00                                                      
              ADD   1  TO K820-RWRT-CTR                                         
              MOVE 'U' TO WS-UPDATE-CODE                                        
              PERFORM 5100-MOVE-DETAILS THRU 5100-EXIT                          
              PERFORM 5300-PRINT-DETAIL THRU 5300-EXIT                          
           ELSE                                                                 
              DISPLAY 'BP13K820 - REWRITE ERROR, FS = '  K820-FS                
           END-IF.                                                              
       4300-EXIT.                                                               
           EXIT.                                                                
      *============================                                             
       4400-READ-K800.                                                          
      *============================                                             
                                                                                
           READ BP13K800.                                                       
           IF K800-FS = 00                                                      
              ADD 1 TO K800-READ-CTR                                            
           ELSE                                                                 
              IF K800-FS = 23                                                   
                 DISPLAY ' RECORD NOT FND IN BP13K800:' K800-NUM-REGN           
                 DISPLAY 'BP13K800 - READ ERROR, FS  ='  K800-FS                
              ELSE                                                              
                 DISPLAY 'BP13K800 - READ ERROR, FS = '  K800-FS                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *============================                                             
       5000-WRITE-HEADING.                                                      
      *============================                                             
           ADD  1                      TO WS-PAGE-CTR.                          
                                                                                
           MOVE  SPACES                TO WS-PRINT-LINE.                        
           MOVE 'BP13L358'             TO WS-HEADER-A.                          
           MOVE 'HDBCAT 3'             TO WS-HEADER-B.                          
           MOVE 'S Y S T E M   O F   C O M M I T M E N T'                       
                                       TO WS-HEADER-C.                          
           MOVE 'DATE:'                TO WS-HEADER-D.                          
           MOVE  WS-PAGE-DATE          TO WS-HEADER-E.                          
           MOVE 'PAGE:'                TO WS-HEADER-F.                          
           MOVE  WS-PAGE-CTR           TO WS-HDER-G.                            
           WRITE BP13L358-REC        FROM WS-PAGE-HDG1 AFTER PAGE.              
                                                                                
           MOVE SPACES                  TO WS-PRINT-LINE.                       
           MOVE 'USER:  AGREEMENT TEAM' TO WS-USER.                             
           WRITE BP13L358-REC         FROM WS-PAGE-HDG2.                        
                                                                                
           MOVE SPACES                 TO WS-PRINT-LINE.                        
           MOVE 'SOC - UPDATE/CREATE/DELETE SOC OCCUPIER RECORD UPON SUB        
      -              '-TRANSFER'       TO WS-SUBHDG-A.                          
           WRITE BP13L358-REC        FROM WS-SUBHDG.                            
                                                                                
           MOVE SPACES                 TO WS-PRINT-LINE.                        
           MOVE ALL '='                TO WS-PRINT-LINE.                        
           WRITE BP13L358-REC        FROM WS-PRINT-LINE.                        
                                                                                
           MOVE SPACES                 TO WS-PRINT-LINE.                        
           MOVE 'S/NO'                 TO WS-SEQ-NUM.                           
           MOVE 'REG_NUM'              TO WS-REGN.                              
           MOVE '  NRIC'               TO WS-NRIC.                              
           MOVE 'NAME'                 TO WS-NME-OCCP.                          
           MOVE '   CPF'               TO WS-CPF.                               
           MOVE 'ETHNIC'               TO WS-ETHNIC.                            
           MOVE 'SEX'                  TO WS-SEX.                               
           MOVE 'MS'                   TO WS-MARITAL-STATUS.                    
           MOVE 'BIRTH_DATE'           TO WS-DTE-BIRTH.                         
           MOVE 'RELATIONSHIP'         TO WS-RELATIONSHIP.                      
           MOVE 'CITZ'                 TO WS-CITIZENSHIP.                       
           MOVE 'NATLTY'               TO WS-NATIONALITY.                       
           MOVE 'REMARK'               TO WS-REMARK.                            
           WRITE BP13L358-REC        FROM WS-COLUMN.                            
                                                                                
           MOVE SPACES                 TO WS-PRINT-LINE.                        
           MOVE ALL '='                TO WS-PRINT-LINE.                        
           WRITE BP13L358-REC        FROM WS-PRINT-LINE.                        
                                                                                
           MOVE  6                     TO WS-LINE-CTR.                          
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *============================                                             
       5100-MOVE-DETAILS.                                                       
      *============================                                             
           IF  K820-NUM-REGN NOT =   WS-PREV-REGN                               
              PERFORM 5000-WRITE-HEADING THRU 5000-EXIT                         
           ELSE                                                                 
           IF WS-LINE-CTR > 60                                                  
              PERFORM 5000-WRITE-HEADING THRU 5000-EXIT                         
           END-IF.                                                              
           MOVE SPACES                  TO WS-PRINT-LINE.                       
                                                                                
           MOVE K820-NUM-REGN           TO WS-REGN                              
                                           WS-PREV-REGN.                        
           MOVE K820-NUM-NRIC           TO WS-NRIC.                             
           MOVE K820-NME-OCCP           TO WS-NME-OCCP.                         
           MOVE K820-NUM-CPF            TO WS-CPF.                              
                                                                                
           MOVE 42                      TO K060-SERIAL-NO.                      
           MOVE K820-NUM-ETHNIC         TO K060-CODE.                           
           PERFORM 5200-GET-FROM-K060 THRU 5200-EXIT.                           
           IF K060-FS = 00                                                      
              MOVE K060-DESC            TO WS-ETHNIC-FLD                        
           END-IF.                                                              
                                                                                
           MOVE 43                      TO K060-SERIAL-NO.                      
           MOVE K820-NUM-SEX            TO K060-CODE.                           
           PERFORM 5200-GET-FROM-K060 THRU 5200-EXIT.                           
           IF K060-FS = 00                                                      
              MOVE K060-DESC            TO WS-SEX-FLD                           
           END-IF.                                                              
                                                                                
           MOVE 44                      TO K060-SERIAL-NO.                      
           MOVE K820-NUM-MARITAL-STATUS TO K060-CODE.                           
           PERFORM 5200-GET-FROM-K060 THRU 5200-EXIT.                           
           IF K060-FS = 00                                                      
              MOVE K060-DESC            TO WS-MARITAL-STATUS                    
           END-IF.                                                              
                                                                                
           MOVE K820-DTE-BIRTH(5:2)     TO WS-DTE-BIRTH(1:2).                   
           MOVE '/'                     TO WS-DTE-BIRTH(3:1).                   
           MOVE K820-DTE-BIRTH(7:2)     TO WS-DTE-BIRTH(4:2).                   
           MOVE '/'                     TO WS-DTE-BIRTH(6:1).                   
           MOVE K820-DTE-BIRTH(1:4)     TO WS-DTE-BIRTH(7:4).                   
                                                                                
           MOVE 35                      TO K060-SERIAL-NO.                      
           MOVE K820-NUM-RELATIONSHIP   TO K060-CODE.                           
           PERFORM 5200-GET-FROM-K060 THRU 5200-EXIT.                           
           IF K060-FS = 00                                                      
              MOVE K060-DESC          TO WS-RELATIONSHIP                        
           END-IF.                                                              
                                                                                
           MOVE K820-NUM-CITIZENSHIP    TO WS-CITZ-FLD.                         
           MOVE K820-NUM-NATIONALITY    TO WS-NATLTY-FLD.                       
           EVALUATE WS-UPDATE-CODE                                              
           WHEN 'A'                                                             
             MOVE 'ADD'       TO WS-REMARK                                      
           WHEN 'D'                                                             
             MOVE 'DELETE'    TO WS-REMARK                                      
           WHEN 'U'                                                             
             MOVE 'UPDATE'    TO WS-REMARK                                      
           END-EVALUATE.                                                        
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *============================                                             
       5200-GET-FROM-K060.                                                      
      *============================                                             
           READ BP13K060.                                                       
           EVALUATE K060-FS                                                     
           WHEN 00                                                              
             CONTINUE                                                           
           WHEN 23                                                              
             DISPLAY 'BP13K060 - RECORD NOT FOUND, KEY = ' K060-KEY-FLD         
           WHEN OTHER                                                           
             DISPLAY 'BP13K060 - READ ERROR, FS  = ' K060-FS                    
           END-EVALUATE.                                                        
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *============================                                             
       5300-PRINT-DETAIL.                                                       
      *============================                                             
           ADD  1               TO WS-SEQ.                                      
           MOVE WS-SEQ          TO WS-SEQ-FLD.                                  
           WRITE BP13L358-REC FROM WS-COLUMN.                                   
           ADD   1              TO WS-LINE-CTR.                                 
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6000-CONVERT-CUSTOMER-ROLE.                                              
      ******************************************************************        
      *----------------------------------------------------------------*        
      *     CUSTOMER ROLE  CODE FOR APPLICANT    IS '011'              *        
      *     CUSTOMER ROLE  CODE FOR CO-APPLICANT IS '012'              *        
      *     CUSTOMER ROLE  CODE FOR OCCUPIER     IS '013'              *        
      *----------------------------------------------------------------*        
            IF K800-NUM-NRIC1  = WS-NUM-NRIC                                    
                                                                                
               MOVE '011'      TO   F915-NUM-ROLE                               
            ELSE                                                                
            IF K800-NUM-NRIC2  = WS-NUM-NRIC      OR                            
               K800-NUM-NRIC3  = WS-NUM-NRIC      OR                            
               K800-NUM-NRIC4  = WS-NUM-NRIC                                    
                                                                                
               MOVE '012'      TO   F915-NUM-ROLE                               
            ELSE                                                                
                                                                                
               MOVE '013'      TO   F915-NUM-ROLE                               
            END-IF.                                                             
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       6100-WRITE-BP13F915.                                                     
      ******************************************************************        
                                                                                
           WRITE BP13F915-REC.                                                  
           ADD  1   TO  F915-WRTE-CTR.                                          
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       7000-UPD-BP13F915-PSN.                                                   
      ******************************************************************        
                                                                                
           INITIALIZE  BP13F915-REC.                                            
           MOVE SPACES                   TO   BP13F915-REC.                     
                                                                                
           MOVE  'UA'                    TO   F915-TXN-TYPE.                    
           MOVE  'P'                     TO   F915-CSTMR-TYPE.                  
           MOVE  'BP13'                  TO   F915-NUM-UPDATE-SOURCE.           
           MOVE  'BP13C358'              TO   F915-NUM-UPDATE-USERID.           
      ***                                                                       
           MOVE   K820-NUM-CSTMR-SOURCE  TO   F915-NUM-CSTMR-SOURCE.            
      ***                                                                       
           MOVE  F890-NUM-REGN           TO   F915-NUM-REGN.                    
           MOVE  F890-NUM-NRIC           TO   F915-NUM-NRIC.                    
                                                                                
           MOVE  F890-NME-OCCP           TO   F915-NUM-NEW-NME-PERSON.          
           MOVE  F890-NUM-NATIONALITY    TO   F915-NUM-NTNLTY.                  
           MOVE  F890-NUM-CITIZENSHIP    TO   F915-NUM-PR-STATUS.               
           MOVE  F890-NUM-MARITAL-STATUS TO   F915-NUM-MRTL-STATUS.             
           MOVE  F890-NUM-SEX            TO   F915-NUM-SEX.                     
           MOVE  F890-NUM-ETHNIC         TO   F915-NUM-ETHNIC-GROUP.            
                                                                                
            STRING F890-DTE-BIRTH(1:4) '-' F890-DTE-BIRTH(5:2) '-'              
                   F890-DTE-BIRTH(7:2) DELIMITED BY SIZE INTO                   
                   F915-DTE-BIRTH.                                              
                                                                                
           PERFORM 6100-WRITE-BP13F915     THRU  6100-EXIT.                     
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       8000-DEL-BP13F915-PSN.                                                   
      ******************************************************************        
                                                                                
           INITIALIZE  BP13F915-REC.                                            
           MOVE SPACES                    TO    BP13F915-REC.                   
                                                                                
           MOVE  'TE'                     TO    F915-TXN-TYPE.                  
           MOVE  'P'                      TO    F915-CSTMR-TYPE.                
           MOVE  'BP13'                   TO    F915-NUM-UPDATE-SOURCE.         
           MOVE  'BP13C358'               TO    F915-NUM-UPDATE-USERID.         
                                                                                
           MOVE   K820-NUM-CSTMR-SOURCE   TO    F915-NUM-CSTMR-SOURCE.          
                                                                                
      **** TO  GET THE REGN. DATE & CUSTOMER ROLE CODE , READ K800 ****         
                                                                                
           MOVE    K820-NUM-REGN          TO    K800-NUM-REGN.                  
           PERFORM 4400-READ-K800         THRU  4400-EXIT                       
           MOVE    K820-NUM-NRIC          TO    WS-NUM-NRIC                     
           PERFORM 6000-CONVERT-CUSTOMER-ROLE   THRU 6000-EXIT.                 
                                                                                
           MOVE  K820-NUM-REGN            TO    F915-NUM-BSNS-REF.              
           MOVE  K820-NUM-NRIC            TO    F915-NUM-NRIC.                  
                                                                                
           MOVE  '0001-01-01'             TO    F915-DTE-ACPTNC.                
                                                                                
           STRING K800-DTE-REGN(1:4) '-' K800-DTE-REGN(5:2) '-'                 
                  K800-DTE-REGN(7:2) DELIMITED BY SIZE                          
                  INTO F915-DTE-ROLE-START.                                     
                                                                                
           PERFORM 6100-WRITE-BP13F915         THRU  6100-EXIT.                 
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=======================                                                  
       9999-CLOSE-FILES.                                                        
      *=======================                                                  
           DISPLAY '*****************'.                                         
           DISPLAY 'PROGRAM: BP13C358'.                                         
           DISPLAY '*****************'.                                         
           DISPLAY 'FILE STATISTICS'.                                           
           DISPLAY '@BP13F890'.                                                 
           DISPLAY '  =>RECORDS READ    = ' F890-READ-CTR.                      
           DISPLAY '@BP13F820'.                                                 
           DISPLAY '  =>RECORDS READ    = ' F820-READ-CTR.                      
           DISPLAY '@BP13K800'.                                                 
           DISPLAY '  =>RECORDS READ    = ' K800-READ-CTR.                      
           DISPLAY '@BP13K820'.                                                 
           DISPLAY '  =>RECORDS WRITE   = ' K820-WRTE-CTR.                      
           DISPLAY '  =>RECORDS REWRITE = ' K820-RWRT-CTR.                      
           DISPLAY '  =>RECORDS DELETED = ' K820-DLET-CTR.                      
           DISPLAY '@BP13F915'.                                                 
           DISPLAY '  =>RECORDS WRITE   = ' F915-WRTE-CTR.                      
           CLOSE BP13F890  BP13F820  BP13K820 BP13K800                          
                           BP13K060 BP13F915                                    
                           BP13L358.                                            
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
