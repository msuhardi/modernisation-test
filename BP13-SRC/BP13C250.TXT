       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C250.                                                 
       AUTHOR.        ANNALYN C BANTA.                                          
       DATE-WRITTEN.  11/05/94.                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO PRINT FLAT LIST                    *                
      *                                                        *                
      *    INPUT  : BM06F800                                   *                
      *    OUTPUT : BP13L250                                   *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY     DATE     DETAILS                        *                
      * ======  ===    ====     =======                        *                
      * C940936 LMS  09.09.94   TO MOVE F800-CDE-MODL-DESG &   *                
      *                         F800-CDE-CONFIG-DESG TO        *                
      *                         L250-DCODE.                    *                
      * SOC-PH9 BAL  11.10.94   TO PRINT REPORTS FOR MATURE &  *                
      *                         NON-MATURE NTS.                *                
      * SOC-PH9 GD   25.11.94   TO PRINT ZONE, HT COLS FOR NON-*                
      *                         MATURE NEW TOWNS AND SUPRESS   *                
      *                         PRINTING OF THESE COLS FOR     *                
      *                         MATURE NEW TOWNS.              *                
      * SOC-PH9 GD   29.11.94   CHG ASSIGNING OF REPORT TITLE  *                
      *BP130418 LMS  15.07.98   Y2000 CHANGES FOR BM06F800.    *                
      *BP130606 LMS  09.12.98   TO CATER FOR 3-RM REN.         *                
      *BP130641 LMS  05.03.99   TO CATER FOR SA.               *                
      *BP130768 GBR  07.12.99   TO PRINT REPORTS FOR MATURE &  *                
      *                         NON-MATURE ESTATES.            *                
      *BP130807 GBR  16.02.00   TO ALLOW DESIGN MODEL '34,71,72*                
      *                         73,81,82' FOR ROOM TYP '04,05' *                
      *                         AND '34,72,73,81,82' FOR '06'. *                
      *BP130863 GBR  02/09/00   TO INCLUDE FLAT STAT, SH, LL,  *                
      *                         PM, AREA, BO AND REMOVE DCODE, *                
      *                         HT IN THE REPORT.              *                
      *BP131059 LMS  16/08/01   TO CATER FOR BTO.              *                
      *BP132278 JF4  16/04/03   CHG BM06F800 LRECL TO 800.     *                
      *BP133610 ESA1 06/05/09   TO REPLACE BP13K767 W/ BP13K816*                
      *BP134681 ESA1 19/09/12   TO REPLACE BP13K816 W/ BP13K813*                
      *BP134731 PCL4 09/11/12   TO CATER FOR MODL-DESIGN 89    *                
      *BP135190 SMR2 04/12/13   EXPAND BP13K813 TO 1000        *                
      *BP135838 ESA1 01/07/15   TO CATER FOR MODL-DESIGN 92    *                
      *BP135856 ESA1 03/09/15   TO CATER FOR RP-CODE='20' FOR  *                
      *                         FLAT-STATUS = 'ST'             *                
      *BP136002 ESA1 09/11/15   TO CATER FOR 2-ROOM FLEXI      *                
      *BP138613 MRR5 10/03/21   CATER FOR CCA FLAT             *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F800 ASSIGN TO BM06F800.                                  
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
                                                                                
           SELECT BP13K060 ASSIGN TO BP13K060                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS DYNAMIC                                       
                  RECORD KEY   IS K060-KEY-FLD                                  
                  FILE STATUS  IS BP13K060-STATUS.                              
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K110-KEY-FLD                                  
                  FILE STATUS  IS BM06K110-STATUS.                              
                                                                                
           SELECT BM06K100 ASSIGN TO BM06K100                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K100-KEY-FLD                                  
                  FILE STATUS  IS BM06K100-STATUS.                              
                                                                                
           SELECT P13L250  ASSIGN TO P13L250.                                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BM06F800                                                             
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 CHARACTERS                                          
           RECORD CONTAINS 800 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BM06F800.                                                           
                                                                                
       FD  BP13K060                                                             
           RECORD CONTAINS 25 CHARACTERS.                                       
       COPY BP13K060.                                                           
                                                                                
       FD  BP13F205                                                             
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 CHARACTERS                                          
           RECORD CONTAINS 80 CHARACTERS                                        
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F205.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BM06K100                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K100.                                                           
                                                                                
       FD  P13L250                                                              
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 CHARACTERS                                          
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  PRINT-REC                      PIC X(132).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       COPY DESIGTAB.                                                           
       COPY BP13K813.                                                           
       COPY DJ02KL50.                                                           
       COPY DJ2C9LAC.                                                           
                                                                                
       01  BP13K060-STATUS                PIC 99     VALUE 00.                  
       01  BM06K110-STATUS                PIC 99     VALUE 00.                  
       01  BM06K100-STATUS                PIC 99     VALUE 00.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-PIDB-NFND                PIC 9(8).                             
           05 WS-K110-READ                PIC 9(8).                             
           05 WS-K110-NFND                PIC 9(8).                             
           05 WS-F800-READ                PIC 9(8).                             
           05 WS-SERIAL                   PIC 9(4).                             
           05 WS-LINE-CNT                 PIC 9(2).                             
           05 WS-PAGE-CNT                 PIC 9(4).                             
                                                                                
       01  WS-CTRS.                                                             
           05 WS-TOT-LCNT                 PIC 9(2)   VALUE 50.                  
           05 WS-REM-LCNT                 PIC 9(2)   VALUE ZEROES.              
                                                                                
       01  WS-EOF-F800                    PIC 9.                                
           88 EOF-F800                    VALUE 1.                              
                                                                                
       01  WS-NUM-SCH-ACC.                                                      
           05 WS-NUM-SCH                  PIC X(4).                             
           05 WS-NUM-ACC                  PIC X(5).                             
                                                                                
       01  WS-SQL-CODES.                                                        
           05  SQL-CODE                   PIC 9(4).                             
                                                                                
       01  WS-FLOOR                       PIC ZZZZZZZ.ZZ.                       
       01  WS-FLOOR-AREA                  PIC ZZZ.                              
       01  WS-DCODE                       PIC X(3)   VALUE SPACES.              
       01  WS-PREV-NT                     PIC XXX.                              
       01  WS-PREV-ZONE                   PIC XXX.                              
       01  WS-F800-ZONE                   PIC XXX.                              
       01  WS-NT-DESC                     PIC X(20).                            
       01  WS-PREV-RM-TYP                 PIC XX.                               
       01  WS-TITLE                       PIC X(52)  VALUE SPACES.              
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-SYS-YY                PIC X(4)  VALUE SPACES.                  
           05 WS-SYS-MM                PIC X(2)  VALUE SPACES.                  
           05 WS-SYS-DD                PIC X(2)  VALUE SPACES.                  
                                                                                
      *-----------------------------------------------------------*             
      *       LINKAGE FOR SUB-PROGRAM BP13C913                    *             
      *-----------------------------------------------------------*             
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH     PIC X(4).                                
               10  WS-LINK-NUM-ACC     PIC X(5).                                
                                                                                
      *-----------------------------------------------------------*             
      *       LINKAGE FOR SUB-PROGRAM BP13C910                    *             
      *-----------------------------------------------------------*             
       01  WS-LINK-REC-2.                                                       
           05  WS-LINK-ZONE           PIC X(03).                                
       01  WS-BP13K813-REC            PIC X(1000).                              
       01  WS-ZONE-FND-FLAG           PIC X(01).                                
       01  WS-RETURN-CODE             PIC 9(02).                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *============================================================*            
                                                                                
       01  L250-HEADING1.                                                       
           05 FILLER                      PIC X(45)  VALUE                      
              'LISTID : BP13L250'.                                              
           05 FILLER                      PIC X(57)  VALUE                      
              'S Y S T E M    O F    C O M M I T M E N T'.                      
           05 FILLER                      PIC X(7)   VALUE 'DATE : '.           
           05 L250-SYS-DATE.                                                    
              10 WS-SYS-DD                PIC 9(2)/.                            
              10 WS-SYS-MM                PIC 9(2)/.                            
              10 WS-SYS-YY                PIC 9(4).                             
           05 FILLER                      PIC X      VALUE SPACES.              
           05 FILLER                      PIC X(7)   VALUE 'PAGE : '.           
           05 L250-PAGE                   PIC ZZZ9.                             
                                                                                
       01  L250-HEADING2.                                                       
           05 FILLER                      PIC X(47)  VALUE SPACES.              
           05 L250-TITLE                  PIC X(52)  VALUE SPACES.              
           05 FILLER                      PIC X(03)  VALUE SPACES.              
           05 FILLER                      PIC X(30)  VALUE                      
              'USER : ALLOCATION TEAM'.                                         
                                                                                
       01  L250-HEADING3.                                                       
           05 FILLER                      PIC X(07)  VALUE 'S/N'.               
           05 FILLER                      PIC X(04)  VALUE 'BLK'.               
           05 FILLER                      PIC X(22) VALUE 'STREET NAME'.        
           05 FILLER                      PIC X(05)  VALUE 'FLAT'.              
           05 FILLER                      PIC X(05)  VALUE 'PRT'.               
           05 FILLER                      PIC X(24)  VALUE 'DESIGN'.            
           05 FILLER                      PIC X(13)  VALUE 'HSE-NO'.            
           05 FILLER                      PIC X(03)  VALUE 'SH'.                
           05 FILLER                      PIC X(04)  VALUE 'LL'.                
           05 FILLER                      PIC X(13)  VALUE 'SCH-ACCT'.          
           05 FILLER                      PIC X(12)  VALUE 'SEL-PRICE'.         
                                                                                
       01  L250-HEADING4.                                                       
           05 FILLER                      PIC X(33)  VALUE SPACES.              
           05 FILLER                      PIC X(05)  VALUE 'STAT'.              
           05 FILLER                      PIC X(94)  VALUE SPACES.              
                                                                                
       01  L250-LINE                      PIC X(132) VALUE ALL '-'.             
                                                                                
       01  L250-BLANK                     PIC X(132) VALUE SPACES.              
                                                                                
       01  L250-DETAIL.                                                         
           05 L250-SERIAL                 PIC ZZZ9.                             
           05 FILLER                      PIC X(1)   VALUE SPACES.              
           05 L250-BLK                    PIC X(5).                             
           05 FILLER                      PIC X(1)   VALUE SPACES.              
           05 L250-STREET                 PIC X(20).                            
           05 FILLER                      PIC X(3)   VALUE SPACES.              
           05 L250-FLAT-STATUS            PIC XXX.                              
           05 FILLER                      PIC X(2)   VALUE SPACES.              
           05 L250-CDE-PROT               PIC X.                                
           05 FILLER                      PIC X(3)   VALUE SPACES.              
           05 L250-DESIGN                 PIC X(22).                            
           05 FILLER                      PIC X(2)   VALUE SPACES.              
           05 L250-HSENO                  PIC X(12).                            
           05 FILLER                      PIC X(1)   VALUE SPACES.              
           05 L250-STOREY-HEIGHT          PIC X(02).                            
           05 FILLER                      PIC X(2)   VALUE SPACES.              
           05 L250-LIFT-LANDING           PIC X(01).                            
           05 FILLER                      PIC X(2)   VALUE SPACES.              
           05 L250-SCHACCT                PIC X(11).                            
           05 FILLER                      PIC X(2)   VALUE SPACES.              
           05 FILLER                      PIC X      VALUE '$'.                 
           05 L250-SELPRICE               PIC ZZ,ZZZ,ZZ9.                       
                                                                                
       01  L250-NOTE1.                                                          
           05 FILLER                      PIC X(02)  VALUE SPACES.              
           05 FILLER                      PIC X(55)  VALUE                      
           'NOTE : FLAT STAT (NW=NEW, ST=EX-RENTAL, RP=REPURCHASED,'.           
           05 FILLER                      PIC X(31)  VALUE                      
              ' BB=BUY-BACK, BTB=BACK-TO-BACK)'.                                
                                                                                
       01  L250-NOTE2.                                                          
           05 FILLER                      PIC X(09)  VALUE SPACES.              
           05 FILLER                      PIC X(50)  VALUE                      
           'PRT=PROTECTED, SH=STOREY HEIGHT, LL=LIFT-LANDING.'.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
      ******************************************************************        
                                                                                
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F800      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL EOF-F800.                                                    
                                                                                
      **--->TO PRINT NOTE                                                       
           MOVE ZEROES               TO WS-REM-LCNT                             
           COMPUTE WS-REM-LCNT = WS-TOT-LCNT - WS-LINE-CNT                      
           MOVE SPACES               TO PRINT-REC                               
           WRITE PRINT-REC                                                      
           WRITE PRINT-REC FROM L250-NOTE1  AFTER WS-REM-LCNT                   
           WRITE PRINT-REC FROM L250-NOTE2  AFTER 1                             
                                                                                
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       100-INITIALIZATION.                                                      
      ******************************************************************        
                                                                                
           OPEN INPUT  BM06F800                                                 
                       BP13F205                                                 
                       BP13K060                                                 
                       BM06K110                                                 
                       BM06K100                                                 
               OUTPUT  P13L250.                                                 
                                                                                
           IF BP13K060-STATUS NOT = 00 AND 97                                   
              MOVE BP13K060-STATUS      TO RETURN-CODE                          
              DISPLAY 'ERROR OPENING BP13K060 - ' BP13K060-STATUS               
              GO TO 999-CLOSE-FILES.                                            
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              MOVE BM06K110-STATUS      TO RETURN-CODE                          
              DISPLAY 'ERROR OPENING BM06K110 - ' BM06K110-STATUS               
              PERFORM 999-CLOSE-FILES   THRU 999-EXIT.                          
                                                                                
           IF BM06K100-STATUS NOT = 00 AND 97                                   
              MOVE BM06K100-STATUS      TO RETURN-CODE                          
              DISPLAY 'ERROR OPENING BM06K100 - ' BM06K100-STATUS               
              PERFORM 999-CLOSE-FILES   THRU 999-EXIT.                          
                                                                                
           CALL 'OP13K813'         USING WS-RETURN-CODE.                        
           IF WS-RETURN-CODE NOT = ZEROS AND 97                                 
              DISPLAY 'OPEN ERROR, BP13K813 STATUS ' WS-RETURN-CODE             
              MOVE WS-RETURN-CODE TO RETURN-CODE                                
              PERFORM 999-CLOSE-FILES   THRU 999-EXIT.                          
                                                                                
           CALL 'C9LAOPEN'         USING DJ2C9LAC-AREA.                         
           IF C9LA-OUT-SYS-ERR NOT = ZEROS AND 97 AND SPACES                    
              DISPLAY 'OPEN ERROR, DJ02KL50 STATUS ' C9LA-OUT-SYS-ERR           
              MOVE C9LA-OUT-SYS-ERR TO RETURN-CODE                              
              PERFORM 999-CLOSE-FILES   THRU 999-EXIT.                          
                                                                                
           MOVE ZEROS                   TO WS-F800-READ                         
                                           WS-EOF-F800                          
                                           WS-COUNTERS.                         
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYS-DATE.                           
                                                                                
           PERFORM 150-READ-BP13F205    THRU 150-EXIT.                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       150-READ-BP13F205.                                                       
      ******************************************************************        
                                                                                
      ******  NOTE : ONLY ONE RECORD WILL BE READ FROM BP13F205  *******        
                                                                                
           READ BP13F205                                                        
              AT END                                                            
                 DISPLAY 'ERROR READING BP13F205 - NO RECORD FOUND'             
                 PERFORM  999-CLOSE-FILES   THRU 999-EXIT                       
           END-READ.                                                            
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       200-READ-F800.                                                           
      ******************************************************************        
                                                                                
           READ BM06F800 AT END                                                 
                MOVE 1   TO WS-EOF-F800                                         
                GO TO 200-EXIT.                                                 
                                                                                
           IF F800-CDE-SELTY = 'BTO'                                            
              MOVE F800-NUM-BTO-OPTION TO WS-F800-ZONE                          
           ELSE                                                                 
              MOVE F800-CDE-ZONE       TO WS-F800-ZONE                          
           END-IF.                                                              
                                                                                
           ADD  1                       TO WS-F800-READ.                        
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-RTN.                                                         
      ******************************************************************        
                                                                                
           IF WS-F800-READ = 1                                                  
              MOVE F800-CDE-TYP-ACTUSE  TO WS-PREV-RM-TYP                       
              MOVE F800-CDE-NT          TO WS-PREV-NT                           
              MOVE WS-F800-ZONE         TO WS-PREV-ZONE                         
              PERFORM 400-PRINT-HEADING THRU 400-EXIT                           
              MOVE ZEROS                TO WS-SERIAL.                           
                                                                                
           IF (F800-CDE-TYP-ACTUSE NOT = WS-PREV-RM-TYP) OR                     
              (F800-CDE-NT         NOT = WS-PREV-NT)     OR                     
              (WS-F800-ZONE        NOT = WS-PREV-ZONE)                          
                                                                                
      **--->TO PRINT NOTE                                                       
              MOVE ZEROES               TO WS-REM-LCNT                          
              COMPUTE WS-REM-LCNT = WS-TOT-LCNT - WS-LINE-CNT                   
              MOVE SPACES               TO PRINT-REC                            
              WRITE PRINT-REC                                                   
              WRITE PRINT-REC FROM L250-NOTE1  AFTER WS-REM-LCNT                
              WRITE PRINT-REC FROM L250-NOTE2  AFTER 1                          
              MOVE ZEROES               TO WS-LINE-CNT                          
                                                                                
              MOVE F800-CDE-TYP-ACTUSE  TO WS-PREV-RM-TYP                       
              MOVE F800-CDE-NT          TO WS-PREV-NT                           
              MOVE WS-F800-ZONE         TO WS-PREV-ZONE                         
              PERFORM 400-PRINT-HEADING THRU 400-EXIT                           
              MOVE ZEROS                TO WS-SERIAL.                           
                                                                                
           IF WS-LINE-CNT > 50                                                  
              MOVE SPACES               TO PRINT-REC                            
              WRITE PRINT-REC FROM L250-NOTE1  AFTER 1                          
              WRITE PRINT-REC FROM L250-NOTE2  AFTER 1                          
              MOVE ZEROES               TO WS-LINE-CNT                          
                                                                                
              PERFORM 400-PRINT-HEADING THRU 400-EXIT                           
           END-IF.                                                              
                                                                                
           PERFORM 500-PRINT-DETAIL     THRU 500-EXIT.                          
           PERFORM 200-READ-F800        THRU 200-EXIT.                          
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-PRINT-HEADING.                                                       
      ******************************************************************        
           ADD 1                        TO WS-PAGE-CNT.                         
           MOVE WS-PAGE-CNT             TO L250-PAGE.                           
                                                                                
           MOVE CORR WS-SYS-DATE        TO L250-SYS-DATE.                       
                                                                                
           PERFORM 410-GET-NT-DESC    THRU 410-EXIT.                            
           PERFORM 405-GET-TITLE      THRU 405-EXIT.                            
           MOVE    WS-TITLE           TO   L250-TITLE.                          
           WRITE PRINT-REC FROM L250-HEADING1 AFTER PAGE.                       
           WRITE PRINT-REC FROM L250-HEADING2 AFTER 2.                          
           WRITE PRINT-REC FROM L250-LINE     AFTER 2.                          
           WRITE PRINT-REC FROM L250-HEADING3 AFTER 1.                          
           WRITE PRINT-REC FROM L250-HEADING4 AFTER 1.                          
           WRITE PRINT-REC FROM L250-LINE     AFTER 1.                          
           MOVE  7                      TO WS-LINE-CNT.                         
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       405-GET-TITLE.                                                           
      *----------------------------------------------------------------*        
                                                                                
           EVALUATE  WS-PREV-RM-TYP                                             
             WHEN '01'                                                          
                IF F800-CDE-MODL-DESG = '80' OR '89' OR '92'                    
                   STRING 'LIST OF 35SQM STUDIO APARTMENTS IN '                 
                           WS-NT-DESC                                           
                           DELIMITED BY SIZE                                    
                       INTO  WS-TITLE                                           
                  ELSE                                                          
                     IF F800-CDE-MODL-DESG = '74'                               
                       STRING 'LIST OF COMMUNITY CARE APARTMENTS IN '           
                               WS-NT-DESC                                       
                               DELIMITED BY SIZE                                
                           INTO  WS-TITLE                                       
                     ELSE                                                       
                       STRING 'LIST OF '        DELIMITED BY SIZE               
                               WS-PREV-RM-TYP   DELIMITED BY SPACE              
                              '-ROOM FLATS IN '  DELIMITED BY SIZE              
                               WS-NT-DESC       DELIMITED BY SIZE               
                         INTO  WS-TITLE                                         
                     END-IF                                                     
                  END-IF                                                        
            WHEN '02'                                                           
               IF F800-NUM-DESIGN-OFFER = 'Y'                                   
                  STRING 'LIST OF '        DELIMITED BY SIZE                    
                          WS-PREV-RM-TYP   DELIMITED BY SPACE                   
                         '-ROOM FLATS IN ' DELIMITED BY SIZE                    
                          WS-NT-DESC       DELIMITED BY SIZE                    
                    INTO  WS-TITLE                                              
               ELSE                                                             
                  IF F800-CDE-MODL-DESG = '80' OR '89' OR '92'                  
                     STRING 'LIST OF 45SQM STUDIO APARTMENT IN '                
                            WS-NT-DESC                                          
                            DELIMITED BY SIZE                                   
                            INTO  WS-TITLE                                      
                  ELSE                                                          
                     STRING 'LIST OF '        DELIMITED BY SIZE                 
                            WS-PREV-RM-TYP   DELIMITED BY SPACE                 
                            '-ROOM FLATS IN ' DELIMITED BY SIZE                 
                            WS-NT-DESC       DELIMITED BY SIZE                  
                            INTO  WS-TITLE                                      
                  END-IF                                                        
               END-IF                                                           
            WHEN '03'                                                           
               STRING 'LIST OF '        DELIMITED BY SIZE                       
                       WS-PREV-RM-TYP   DELIMITED BY SPACE                      
                      '-ROOM FLATS IN ' DELIMITED BY SIZE                       
                       WS-NT-DESC       DELIMITED BY SIZE                       
                 INTO  WS-TITLE                                                 
            WHEN '04'                                                           
               IF F205-NUM-NT-ZONE = 'ME'                                       
                  STRING 'LIST OF '        DELIMITED BY SIZE                    
                          WS-PREV-RM-TYP   DELIMITED BY SPACE                   
                         '-ROOM FLATS IN MATURE ESTATES '                       
                                           DELIMITED BY SIZE                    
                    INTO  WS-TITLE                                              
               ELSE                                                             
                  IF F205-NUM-NT-ZONE = 'ZN'                                    
                    STRING 'LIST OF '        DELIMITED BY SIZE                  
                            WS-PREV-RM-TYP   DELIMITED BY SPACE                 
                           '-ROOM FLATS IN NON-MATURE ESTATES '                 
                                             DELIMITED BY SIZE                  
                      INTO  WS-TITLE                                            
                  ELSE                                                          
                     STRING 'LIST OF '        DELIMITED BY SIZE                 
                             WS-PREV-RM-TYP   DELIMITED BY SPACE                
                            '-ROOM FLATS IN ' DELIMITED BY SIZE                 
                             WS-NT-DESC       DELIMITED BY SIZE                 
                       INTO  WS-TITLE                                           
                  END-IF                                                        
               END-IF                                                           
            WHEN '05'                                                           
               IF F205-NUM-NT-ZONE = 'ME'                                       
                  STRING 'LIST OF '        DELIMITED BY SIZE                    
                          WS-PREV-RM-TYP   DELIMITED BY SPACE                   
                         '-ROOM FLATS IN MATURE ESTATES '                       
                                           DELIMITED BY SIZE                    
                    INTO  WS-TITLE                                              
               ELSE                                                             
                  IF F205-NUM-NT-ZONE = 'ZN'                                    
                     STRING 'LIST OF '        DELIMITED BY SIZE                 
                             WS-PREV-RM-TYP   DELIMITED BY SPACE                
                            '-ROOM FLATS IN NON-MATURE ESTATES '                
                                              DELIMITED BY SIZE                 
                       INTO  WS-TITLE                                           
                  ELSE                                                          
                     STRING 'LIST OF '        DELIMITED BY SIZE                 
                             WS-PREV-RM-TYP   DELIMITED BY SPACE                
                            '-ROOM FLATS IN ' DELIMITED BY SIZE                 
                             WS-NT-DESC       DELIMITED BY SIZE                 
                       INTO  WS-TITLE                                           
                  END-IF                                                        
               END-IF                                                           
            WHEN '06'                                                           
               IF F205-NUM-NT-ZONE = 'ME'                                       
                  STRING 'LIST OF EXEC FLATS IN MATURE ESTATES '                
                          DELIMITED BY SIZE                                     
                          INTO  WS-TITLE                                        
               ELSE                                                             
                  IF F205-NUM-NT-ZONE = 'ZN'                                    
                    STRING 'LIST OF EXEC FLATS IN NON-MATURE ESTATES '          
                            DELIMITED BY SIZE                                   
                            INTO  WS-TITLE                                      
                  ELSE                                                          
                     STRING 'LIST OF '        DELIMITED BY SIZE                 
                             WS-PREV-RM-TYP   DELIMITED BY SPACE                
                            '-ROOM FLATS IN ' DELIMITED BY SIZE                 
                             WS-NT-DESC       DELIMITED BY SIZE                 
                     INTO  WS-TITLE                                             
                  END-IF                                                        
               END-IF                                                           
            WHEN OTHER                                                          
               STRING 'LIST OF '        DELIMITED BY SIZE                       
                       WS-PREV-RM-TYP   DELIMITED BY SPACE                      
                      '-ROOM FLATS IN ' DELIMITED BY SIZE                       
                       WS-NT-DESC       DELIMITED BY SIZE                       
                 INTO  WS-TITLE                                                 
           END-EVALUATE.                                                        
                                                                                
       405-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       410-GET-NT-DESC.                                                         
      *----------------------------------------------------------------*        
           IF F205-NUM-NT-ZONE-SW = 'Z'                                         
              IF F800-CDE-SELTY = 'BTO'                                         
                 PERFORM 420-CALL-BP13C910 THRU 420-EXIT                        
              ELSE                                                              
                 MOVE 45                TO K060-SERIAL-NO                       
                 MOVE WS-PREV-ZONE      TO K060-CODE                            
                 PERFORM 430-READ-BP13K060 THRU 430-EXIT                        
              END-IF                                                            
           ELSE                                                                 
              MOVE 02                   TO K060-SERIAL-NO                       
              MOVE WS-PREV-NT           TO K060-CODE                            
              PERFORM 430-READ-BP13K060 THRU 430-EXIT                           
           END-IF.                                                              
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       420-CALL-BP13C910.                                                       
      *----------------------------------------------------------------*        
           MOVE WS-PREV-ZONE TO WS-LINK-ZONE.                                   
           CALL 'BP13C910' USING WS-LINK-REC-2,                                 
                                 WS-BP13K813-REC,                               
                                 WS-ZONE-FND-FLAG.                              
           IF WS-ZONE-FND-FLAG = 'Y'                                            
              MOVE WS-BP13K813-REC TO BP13K813-REC                              
              MOVE K813-NME-SITE   TO WS-NT-DESC                                
           ELSE                                                                 
              MOVE SPACES          TO WS-NT-DESC                                
           END-IF.                                                              
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       430-READ-BP13K060.                                                       
      *----------------------------------------------------------------*        
           READ BP13K060.                                                       
                                                                                
           IF BP13K060-STATUS = 00                                              
              MOVE K060-DESC            TO WS-NT-DESC                           
           ELSE                                                                 
           IF BP13K060-STATUS = 23                                              
              MOVE SPACES               TO WS-NT-DESC                           
              DISPLAY 'REC NOT FOUND IN BP13K060 ' BP13K060-STATUS              
              DISPLAY 'K060-KEY-FLD = ' K060-KEY-FLD                            
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K060 ' BP13K060-STATUS                 
              DISPLAY 'K060-KEY-FLD = ' K060-KEY-FLD                            
              GO TO 999-CLOSE-FILES.                                            
                                                                                
       430-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       500-PRINT-DETAIL.                                                        
      ******************************************************************        
           ADD 1                        TO WS-SERIAL.                           
                                                                                
           MOVE WS-SERIAL               TO L250-SERIAL.                         
           MOVE F800-BLK-NO             TO L250-BLK.                            
           MOVE F800-STREET-NAME        TO L250-STREET.                         
                                                                                
           MOVE F800-CDE-MODL-DESG      TO WS-DCODE(1:2).                       
           MOVE F800-CDE-CONFIG-DESG    TO WS-DCODE(3:1).                       
           SET WS-DSGN-IND TO 1.                                                
           SEARCH WS-DESIGN-ENTRY                                               
               AT END                                                           
                  MOVE SPACES           TO L250-DESIGN                          
             WHEN WS-DSGN-CDE(WS-DSGN-IND) = WS-DCODE                           
                  MOVE WS-DSGN-TEXT(WS-DSGN-IND)                                
                                        TO L250-DESIGN.                         
                                                                                
           STRING '#' F800-LEVEL-NO '-' F800-MAIN-UNIT                          
                  DELIMITED BY SIZE   INTO L250-HSENO.                          
                                                                                
           MOVE F800-SCH-ACC-NO         TO L250-SCHACCT.                        
           MOVE F800-CURR-SELLING-PRICE TO L250-SELPRICE.                       
                                                                                
           PERFORM 550-READ-BM06K110    THRU 550-EXIT.                          
                                                                                
           WRITE PRINT-REC FROM L250-DETAIL AFTER 1.                            
           WRITE PRINT-REC FROM L250-BLANK  AFTER 1.                            
           ADD 2                        TO WS-LINE-CNT.                         
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *************************************************************             
       550-READ-BM06K110.                                                       
      *************************************************************             
                                                                                
           MOVE SPACES             TO K110-KEY-FLD                              
                                      L250-FLAT-STATUS                          
                                      L250-LIFT-LANDING                         
                                      L250-CDE-PROT                             
                                      L250-STOREY-HEIGHT.                       
                                                                                
           MOVE F800-SCH-ACC-NO    TO K110-SCH-ACC-NO.                          
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE BM06K110-STATUS                                             
           WHEN 00                                                              
              ADD 1                       TO WS-K110-READ                       
              IF K110-FLAT-STATUS      = 'RP' AND                               
                 K110-REPOSSESSED-CODE = '40'                                   
                 MOVE 'BB ' TO L250-FLAT-STATUS                                 
              ELSE                                                              
                 IF (K110-FLAT-STATUS      = 'ST' AND                           
                    (K110-REPOSSESSED-CODE = '40' OR '20'))                     
                    MOVE 'BTB' TO L250-FLAT-STATUS                              
                 ELSE                                                           
                    MOVE SPACES TO L250-FLAT-STATUS                             
                    MOVE K110-FLAT-STATUS TO L250-FLAT-STATUS                   
                 END-IF                                                         
              END-IF                                                            
              MOVE K110-CDE-PROTECTED     TO L250-CDE-PROT                      
              PERFORM 600-GET-PIDB-INFO   THRU 600-EXIT                         
              PERFORM 560-READ-BM06K100   THRU 560-EXIT                         
              PERFORM 700-LINK-DJ02C9LA   THRU 700-EXIT                         
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BM06K110 : ' K110-KEY-FLD            
              ADD 1              TO WS-K110-NFND                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BM06K110'                                  
              MOVE BM06K110-STATUS        TO RETURN-CODE                        
              PERFORM 999-CLOSE-FILES     THRU 999-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *************************************************************             
       560-READ-BM06K100.                                                       
      *************************************************************             
                                                                                
           MOVE SPACES             TO K100-KEY-FLD.                             
           MOVE K110-AIX1          TO K100-KEY-FLD.                             
                                                                                
           READ BM06K100.                                                       
                                                                                
           EVALUATE BM06K100-STATUS                                             
           WHEN 00                                                              
              CONTINUE                                                          
           WHEN 23                                                              
              MOVE SPACES    TO K100-REC                                        
              DISPLAY 'RECORD NOT FOUND IN BM06K100 : ' K100-KEY-FLD            
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BM06K100'                                  
              MOVE BM06K100-STATUS        TO RETURN-CODE                        
              PERFORM 999-CLOSE-FILES     THRU 999-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       560-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *************************************************************             
       600-GET-PIDB-INFO.                                                       
      *************************************************************             
                                                                                
           IF K110-SCH-ACC-NO NOT = SPACES AND ZEROES AND LOW-VALUES            
              MOVE SPACES               TO BP13COMM8-REC                        
              INITIALIZE                   BP13COMM8-REC                        
           END-IF.                                                              
                                                                                
           MOVE K110-SCH-ACC-NO      TO WS-LINK-REC.                            
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE SPACES        TO L250-STOREY-HEIGHT                          
              MOVE COMM8-CDE-SYSERR      TO SQL-CODE                            
              DISPLAY 'K110-SCH-ACC-NO : ' K110-SCH-ACC-NO                      
              ADD 1              TO WS-PIDB-NFND                                
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH ACC NOT FOUND IN PIDB INTERFACE TABLE : '         
                                         SQL-CODE                               
              ELSE                                                              
                 DISPLAY 'ERROR IN READING PIDB TABLE : ' SQL-CODE              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF COMM8-CDE-SYSERR = 0                                              
              MOVE COMM8-QTY-MAX-FLRLVL (2:2) TO L250-STOREY-HEIGHT             
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *************************************************************             
       700-LINK-DJ02C9LA.                                                       
      *************************************************************             
                                                                                
           MOVE SPACES                     TO DJ2C9LAC-AREA.                    
           MOVE 'BP13'                     TO C9LA-IN-NUM-SYSTEM.               
           MOVE '1'                        TO C9LA-IN-NUM-MODULE.               
           MOVE '00001'                    TO C9LA-IN-NUM-PARAMETER.            
           MOVE K100-CDE-BLK               TO C9LA-IN-NUM-BLDNG-GL.             
           MOVE COMM8-NUM-LEVEL            TO C9LA-IN-NUM-LEVEL.                
           STRING COMM8-NUM-UNIT-MAIN COMM8-NUM-UNIT-SUB                        
                  DELIMITED BY SIZE INTO C9LA-IN-NUM-UNIT                       
           END-STRING.                                                          
           MOVE ';'                        TO C9LA-DELIM1 C9LA-DELIM2           
                                              C9LA-DELIM3 C9LA-DELIM4           
                                              C9LA-DELIM5 C9LA-DELIM6.          
                                                                                
           CALL 'DJ02C9LA' USING DJ2C9LAC-AREA.                                 
           DISPLAY 'C9LA-OUT-NUM-ERRMSG: ' C9LA-OUT-NUM-ERRMSG                  
           DISPLAY 'C9LA-OUT-SYS-ERR: ' C9LA-OUT-SYS-ERR                        
           IF (C9LA-OUT-NUM-ERRMSG = SPACES AND                                 
              (C9LA-OUT-SYS-ERR     = 0 OR SPACES))                             
              MOVE 'N'       TO L250-LIFT-LANDING                               
           ELSE                                                                 
              MOVE 'Y'       TO L250-LIFT-LANDING                               
           END-IF.                                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-FILES.                                                         
      ******************************************************************        
           DISPLAY SPACES.                                                      
           DISPLAY '*=========== BP13C250 ===========*'.                        
           DISPLAY ' NO. OF F800 RECORDS READ = ' WS-F800-READ.                 
           DISPLAY ' NO. OF K110 RECORDS READ = ' WS-K110-READ.                 
           DISPLAY ' NO. OF K110 RECS NOT FND = ' WS-K110-NFND.                 
           DISPLAY ' NO. OF RECS NOT FND IN PIDB = ' WS-PIDB-NFND.              
           DISPLAY '*================================*'.                        
                                                                                
           CLOSE BM06F800                                                       
                 BP13F205                                                       
                 BP13K060                                                       
                 BM06K110                                                       
                 BM06K100.                                                      
                                                                                
           IF BP13K060-STATUS NOT = 00 AND 97                                   
              MOVE BP13K060-STATUS      TO RETURN-CODE                          
              DISPLAY 'ERROR CLOSING BP13K060 - ' BP13K060-STATUS               
              MOVE BP13K060-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              MOVE BM06K110-STATUS      TO RETURN-CODE                          
              DISPLAY 'ERROR CLOSING BM06K110 - ' BM06K110-STATUS               
              MOVE BM06K110-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           IF BM06K100-STATUS NOT = 00 AND 97                                   
              MOVE BM06K100-STATUS      TO RETURN-CODE                          
              DISPLAY 'ERROR CLOSING BM06K100 - ' BM06K100-STATUS               
              MOVE BM06K100-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           CALL 'CP13K813'         USING WS-RETURN-CODE.                        
           IF WS-RETURN-CODE NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR CLOSING BP13K813 - ' WS-RETURN-CODE                
              MOVE WS-RETURN-CODE TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           CALL 'C9LACLSE'         USING DJ2C9LAC-AREA.                         
           IF C9LA-OUT-SYS-ERR NOT = ZEROS AND 97 AND SPACES                    
              DISPLAY 'ERROR CLOSING DJ02KL50 - ' C9LA-OUT-SYS-ERR              
              MOVE C9LA-OUT-SYS-ERR TO RETURN-CODE                              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
                                                                                
