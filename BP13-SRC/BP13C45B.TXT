       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C45B.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  28/07/2016.                                               
      * ========================================================== *            
      * OBJECTIVE    : MONTHLY PROGRAM TO POPULATE BP13KC30 FOR    *            
      *                NEW MONTH APPOINTMENT                       *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13FC30                               *            
      *                 2.) BP13KC35                               *            
      *                 3.) BP13K452                               *            
      * I-O    FILES :  1.) BP13KC30                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136561 KSJ3 28/07/2016 NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FC30  ASSIGN        TO BP13FC30.                          
                                                                                
           SELECT BP13K452  ASSIGN        TO BP13K452                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K452-KEY-FLD                       
                            FILE STATUS   IS WS-K452-STATUS.                    
                                                                                
           SELECT BP13KC35  ASSIGN        TO BP13KC35                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KC35-KEY-FLD                       
                            FILE STATUS   IS WS-KC35-STATUS.                    
                                                                                
           SELECT BP13KC30  ASSIGN        TO BP13KC30                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KC30-KEY-FLD                       
                            FILE STATUS   IS WS-KC30-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FC30                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 200  CHARACTERS.                                     
           COPY BP13FC30.                                                       
                                                                                
       FD  BP13K452                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
           COPY BP13K452.                                                       
                                                                                
       FD  BP13KC35                                                             
           RECORD CONTAINS 80   CHARACTERS.                                     
           COPY BP13KC35.                                                       
                                                                                
       FD  BP13KC30                                                             
           RECORD CONTAINS 200  CHARACTERS.                                     
           COPY BP13KC30.                                                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       COPY AV02COMM.                                                           
                                                                                
      *-------------------------------------------------------------            
      *        NO OF DAYS BUYER IS ALLOWED TO BOOK APPOINTMENT                  
      *-------------------------------------------------------------            
       77  WS-BOOK-DAYS                  PIC 9(2)   VALUE 03.                   
      *-------------------------------------------------------------            
      *        NO OF APPT DAYS BUYER IS ALLOWED TO CHOOSE FROM                  
      *-------------------------------------------------------------            
       77  WS-APPT-DAYS                  PIC 9(2)   VALUE 10.                   
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K452-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-KC35-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-KC30-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-FC30-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KC30-WRT           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-SLOTS-ADD          PIC 9(2)   VALUE ZEROES.               
           05  WS-IDX                    PIC 9(2)   VALUE ZEROES.               
           05  WS-IDX2                   PIC 9(2)   VALUE ZEROES.               
           05  WS-IDX-SLOT               PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-FC30-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-KC35-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-KC35-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-KC30-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-KC30-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K452-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-SKIP-REC               PIC X(1)   VALUE 'N'.                  
           05  WS-CHK-SOCA-FL            PIC X(1)   VALUE 'N'.                  
           05  WS-SOCA-DAY-FL            PIC X(1)   VALUE 'N'.                  
           05  WS-DTE-VALID-FL           PIC X(1).                              
               88  WS-DTE-VALID-Y                   VALUE 'Y'.                  
               88  WS-DTE-VALID-N                   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
           05  WS-DTE-NEXT-MO.                                                  
               10  WS-DTE-NEXT-MO-YR     PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-NEXT-MO-MM     PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-NEXT-MO-DD     PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-WORK.                                                     
               10  WS-DTE-WORK-YR        PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-WORK-MM        PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-WORK-DD        PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-ADD                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-ADD-INT            PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-SMS-SENT           PIC X(8)   VALUE ZEROES.               
           05  WS-DTE-TMP.                                                      
               10  WS-DTE-TMP-YR         PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-TMP-MM         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-TMP-DD         PIC 9(2)   VALUE ZEROES.               
           05  WS-TIME-RUN               PIC X(8)   VALUE ZEROES.               
           05  FILLER OCCURS 10 TIMES.                                          
               10  WS-DTE-APPT           PIC X(8)   VALUE SPACES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-TMP-DTE-APPT           PIC X(8)   VALUE SPACES.               
           05  WS-TMP-TME-APPT           PIC X(4)   VALUE SPACES.               
           05  WS-NUM-BATCH              PIC X(10)  VALUE SPACES.               
           05  WS-NUM-APPT-TYPE          PIC X(2)   VALUE SPACES.               
           05  WS-NUM-DAYS               PIC 9(4)   VALUE ZEROES.               
           05  WS-QUOTIENT               PIC 9(4)   VALUE ZEROES.               
           05  WS-REMAINDER              PIC 9(4)   VALUE ZEROES.               
           05  WS-NUM-DAY-OF-WEEK        PIC 9(1)   VALUE ZEROES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 2000-BP13FC30-READ      THRU 2000-EXIT                       
                   UNTIL WS-FC30-EOF = 'Y'.                                     
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-DTE-WORK-YR NOT = WS-DTE-NEXT-MO-YR                 
                      OR WS-DTE-WORK-MM NOT = WS-DTE-NEXT-MO-MM.                
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13FC30                                                 
                       BP13KC35                                                 
                       BP13K452                                                 
                I-O    BP13KC30.                                                
                                                                                
           CALL 'OAV02001' USING LINK-REC.                                      
                                                                                
           IF WS-KC35-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC35 ERROR ' WS-KC35-STATUS                  
              MOVE     WS-KC35-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K452-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K452 ERROR ' WS-K452-STATUS                  
              MOVE     WS-K452-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KC30-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC30 ERROR ' WS-KC30-STATUS                  
              MOVE     WS-KC30-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET CURRENT DATE                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
           MOVE WS-DTE-FMT8                TO   WS-DTE-CURR-8.                  
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13FC30-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13FC30                                                        
           AT END                                                               
              PERFORM 2100-GET-NEXT-MONTH  THRU 2100-EXIT                       
              MOVE 'Y'                     TO   WS-FC30-EOF                     
              GO TO 2000-EXIT.                                                  
                                                                                
           ADD 1                           TO   WS-CNT-FC30-READ.               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2100-GET-NEXT-MONTH.                                                     
      *=============================================================            
                                                                                
           MOVE FC30-DTE-SENT-SELF-BK      TO   WS-DTE-WORK.                    
                                                                                
           DISPLAY '*******************************'.                           
           DISPLAY 'FC30 LAST DATE: ' FC30-DTE-SENT-SELF-BK.                    
                                                                                
      *-------------------------------------------------------------            
      *    GET 1ST DAY OF NEXT MONTH                                            
      *-------------------------------------------------------------            
           MOVE WS-DTE-WORK                TO   WS-DTE-NEXT-MO.                 
           ADD 1                           TO   WS-DTE-NEXT-MO-MM.              
                                                                                
           IF WS-DTE-NEXT-MO-MM > 12                                            
              MOVE 1                       TO   WS-DTE-NEXT-MO-MM               
              ADD  1                       TO   WS-DTE-NEXT-MO-YR               
           END-IF.                                                              
                                                                                
           MOVE 1                          TO   WS-DTE-NEXT-MO-DD.              
           MOVE WS-DTE-NEXT-MO             TO   WS-DTE-WORK.                    
                                                                                
           DISPLAY 'NEXT MONTH    : ' WS-DTE-NEXT-MO.                           
           DISPLAY '*******************************'.                           
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    CHECK IF DATE IS HOLIDAY                                             
      *-------------------------------------------------------------            
           MOVE 'N'                        TO   WS-SKIP-REC.                    
           MOVE 'TP'                       TO   WS-NUM-APPT-TYPE.               
           MOVE ZEROES                     TO   WS-IDX-SLOT.                    
                                                                                
           MOVE WS-DTE-WORK                TO   WS-IN-DATE.                     
           MOVE 'HD'                       TO   WS-IN-OPTION.                   
           MOVE '000'                      TO   WS-RETURN.                      
                                                                                
           PERFORM 8000-CALL-AV02C002      THRU 8000-EXIT.                      
                                                                                
           IF WS-SKIP-REC = 'Y'                                                 
              GO TO 3000-PROCEED-NEXT.                                          
                                                                                
           IF WS-RET-DAY = 'HOL' OR 'EVE'                                       
              DISPLAY 'PUBLIC HOLIDAY: '        WS-IN-DATE                      
              GO TO 3000-PROCEED-NEXT.                                          
                                                                                
      *-------------------------------------------------------------            
      *    CREATE APPOINTMENT WINDOW PERIOD                                     
      *-------------------------------------------------------------            
           MOVE 'Y'                        TO   WS-CHK-SOCA-FL.                 
           MOVE WS-DTE-WORK                TO   WS-DTE-ADD.                     
           PERFORM VARYING WS-IDX FROM 1 BY 1                                   
                     UNTIL WS-IDX > WS-APPT-DAYS                                
              IF WS-IDX = 1                                                     
                 MOVE WS-BOOK-DAYS         TO   WS-NUM-DAYS                     
              ELSE                                                              
                 MOVE 1                    TO   WS-NUM-DAYS                     
              END-IF                                                            
                                                                                
              PERFORM 3100-ADD-DAYS        THRU 3100-EXIT                       
                                                                                
              MOVE WS-DTE-ADD              TO   WS-DTE-APPT(WS-IDX)             
              PERFORM 3200-CREATE-TIMESLOT THRU 3200-EXIT                       
           END-PERFORM.                                                         
                                                                                
       3000-PROCEED-NEXT.                                                       
                                                                                
      *-------------------------------------------------------------            
      *    GET NEXT WORKING DAY                                                 
      *-------------------------------------------------------------            
           MOVE 'N'                        TO   WS-CHK-SOCA-FL.                 
           MOVE WS-DTE-WORK                TO   WS-DTE-ADD.                     
           MOVE 1                          TO   WS-NUM-DAYS.                    
           PERFORM 3100-ADD-DAYS           THRU 3100-EXIT.                      
                                                                                
           MOVE WS-DTE-ADD                 TO   WS-DTE-WORK.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-ADD-DAYS.                                                           
      *=============================================================            
                                                                                
           SET WS-DTE-VALID-N              TO   TRUE.                           
                                                                                
           PERFORM UNTIL WS-DTE-VALID-Y                                         
      *-------------------------------------------------------------            
      *    CONVERT DATE TO INTEGER                                              
      *-------------------------------------------------------------            
              COMPUTE WS-DTE-ADD-INT = FUNCTION INTEGER-OF-DATE                 
                                       (WS-DTE-ADD)                             
                                                                                
      *-------------------------------------------------------------            
      *    ADD ACTUAL NUMBER OF DAYS                                            
      *-------------------------------------------------------------            
              ADD WS-NUM-DAYS              TO   WS-DTE-ADD-INT                  
                                                                                
      *-------------------------------------------------------------            
      *    CONVERT INTEGER TO DATE                                              
      *-------------------------------------------------------------            
              COMPUTE WS-DTE-ADD     = FUNCTION DATE-OF-INTEGER                 
                                       (WS-DTE-ADD-INT)                         
                                                                                
      *-------------------------------------------------------------            
      *    CHECK FOR HOLIDAY / EVE OF PUBLIC HOLIDAY                            
      *-------------------------------------------------------------            
              MOVE WS-DTE-ADD              TO   WS-IN-DATE                      
              MOVE 'HD'                    TO   WS-IN-OPTION                    
              MOVE '000'                   TO   WS-RETURN                       
              PERFORM 8000-CALL-AV02C002   THRU 8000-EXIT                       
                                                                                
              IF WS-RET-DAY = 'HOL' OR 'EVE'                                    
                 MOVE 1                    TO   WS-NUM-DAYS                     
              ELSE                                                              
      *-------------------------------------------------------------            
      *    CHECK FOR SOCA DAY                                                   
      *-------------------------------------------------------------            
                 IF WS-CHK-SOCA-FL = 'Y'                                        
                    MOVE SPACES            TO   BP13K452-REC                    
                    MOVE WS-DTE-ADD        TO   K452-DTE-APPT                   
                                                                                
                    PERFORM 3110-BP13K452-READ  THRU 3110-EXIT                  
                                                                                
                    IF WS-K452-FND = 'Y'                                        
                       IF K452-CDE-APPT-TYPE   = 'SA' OR                        
                          K452-CDE-APPT-TYPE-A = 'SA'                           
                          MOVE 'Y'         TO   WS-SOCA-DAY-FL                  
                       ELSE                                                     
                          MOVE 'N'         TO   WS-SOCA-DAY-FL                  
                       END-IF                                                   
                    ELSE                                                        
                      DISPLAY 'DATE NOT FOUND IN WORKPLAN ' WS-DTE-ADD          
                      MOVE    99                   TO   RETURN-CODE             
                      PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT               
                    END-IF                                                      
                                                                                
                    IF WS-SOCA-DAY-FL = 'N'                                     
                       SET WS-DTE-VALID-Y     TO   TRUE                         
                    END-IF                                                      
                 ELSE                                                           
                    SET WS-DTE-VALID-Y        TO   TRUE                         
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3110-BP13K452-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K452.                                                       
                                                                                
           EVALUATE WS-K452-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K452-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K452-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K452 ERROR '    WS-K452-STATUS                  
              MOVE    WS-K452-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3200-CREATE-TIMESLOT.                                                    
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET DAY OF THE WEEK                                                  
      *-------------------------------------------------------------            
           MOVE WS-DTE-APPT(WS-IDX)        TO   WS-IN-DATE.                     
           MOVE 'DW'                       TO   WS-IN-OPTION.                   
           MOVE '000'                      TO   WS-RETURN.                      
           PERFORM 8000-CALL-AV02C002      THRU 8000-EXIT.                      
                                                                                
           EVALUATE WS-RET-DAY                                                  
           WHEN 'MON'                                                           
              MOVE 1                       TO   WS-NUM-DAY-OF-WEEK              
           WHEN 'TUE'                                                           
              MOVE 2                       TO   WS-NUM-DAY-OF-WEEK              
           WHEN 'WED'                                                           
              MOVE 3                       TO   WS-NUM-DAY-OF-WEEK              
           WHEN 'THU'                                                           
              MOVE 4                       TO   WS-NUM-DAY-OF-WEEK              
           WHEN 'FRI'                                                           
              MOVE 5                       TO   WS-NUM-DAY-OF-WEEK              
           WHEN OTHER                                                           
              MOVE 0                       TO   WS-NUM-DAY-OF-WEEK              
           END-EVALUATE.                                                        
                                                                                
           MOVE 'N'                        TO   WS-KC35-EOF.                    
                                                                                
           MOVE SPACES                     TO   BP13KC35-REC.                   
           MOVE WS-NUM-APPT-TYPE           TO   KC35-NUM-APPT-TYPE.             
           MOVE WS-NUM-DAY-OF-WEEK         TO   KC35-NUM-DAY-OF-WEEK.           
           MOVE WS-IDX                     TO   KC35-NUM-DAY.                   
           PERFORM 3210-BP13KC35-STARTBR        THRU 3210-EXIT.                 
           PERFORM 3300-PROC-TIME-APPT          THRU 3300-EXIT                  
                   UNTIL WS-KC35-EOF = 'Y'.                                     
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3210-BP13KC35-STARTBR.                                                   
      *=============================================================            
                                                                                
           START BP13KC35 KEY >= KC35-KEY-FLD.                                  
                                                                                
           EVALUATE WS-KC35-STATUS                                              
           WHEN 00                                                              
              PERFORM 3220-BP13KC35-READNEXT    THRU 3220-EXIT                  
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO   WS-KC35-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START BP13KC35 ERROR '   WS-KC35-STATUS                  
              MOVE    WS-KC35-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3220-BP13KC35-READNEXT.                                                  
      *=============================================================            
                                                                                
           READ BP13KC35 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-KC35-STATUS                                              
           WHEN 00                                                              
              IF KC35-NUM-APPT-TYPE NOT = WS-NUM-APPT-TYPE OR                   
                 KC35-NUM-DAY       NOT = WS-IDX                                
                 MOVE 'Y'                  TO   WS-KC35-EOF                     
              END-IF                                                            
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO   WS-KC35-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START BP13KC35 ERROR ' WS-KC35-STATUS                    
              MOVE    WS-KC35-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3220-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3300-PROC-TIME-APPT.                                                     
      *=============================================================            
                                                                                
           PERFORM VARYING WS-IDX2 FROM 1 BY 1                                  
                     UNTIL WS-IDX2 > KC35-NUM-SLOT                              
              ADD  1                       TO   WS-IDX-SLOT                     
              MOVE SPACES                  TO   BP13KC30-REC                    
              MOVE WS-DTE-WORK             TO   KC30-DTE-SENT-SELF-BK           
              MOVE WS-NUM-APPT-TYPE        TO   KC30-NUM-APPT-TYPE              
              MOVE WS-DTE-APPT(WS-IDX)     TO   KC30-DTE-APPT                   
              MOVE KC35-TME-APPT           TO   KC30-TME-APPT                   
              MOVE WS-IDX-SLOT             TO   KC30-NUM-SLOT                   
              STRING '#' WS-DTE-WORK(7:2) WS-DTE-WORK(5:2) WS-IDX-SLOT          
                     DELIMITED BY SIZE     INTO KC30-NUM-REGN                   
                                                                                
              ACCEPT WS-TIME-RUN           FROM TIME                            
              MOVE WS-TIME-RUN             TO   KC30-TME-CREATE                 
              MOVE WS-DTE-CURR-8           TO   KC30-DTE-CREATE                 
                                                                                
              PERFORM 3310-BP13KC30-WRITE  THRU 3310-EXIT                       
           END-PERFORM.                                                         
                                                                                
           PERFORM 3220-BP13KC35-READNEXT  THRU 3220-EXIT.                      
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3310-BP13KC30-WRITE.                                                     
      *=============================================================            
                                                                                
           WRITE BP13KC30-REC.                                                  
                                                                                
           EVALUATE WS-KC30-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              ADD 1                        TO   WS-CNT-KC30-WRT                 
              ADD 1                        TO   WS-CNT-SLOTS-ADD                
                                                                                
           WHEN 22                                                              
              DISPLAY 'REC ALREADY EXISTS IN BP13KC30: '                        
                      WS-DTE-APPT(WS-IDX) KC35-TME-APPT WS-IDX-SLOT             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITE BP13KC30 ERROR '   WS-KC30-STATUS                  
              MOVE     WS-KC30-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3310-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8000-CALL-AV02C002.                                                      
      *=============================================================            
                                                                                
           CALL 'AV02C002' USING LINK-REC.                                      
                                                                                
           IF WS-RETURN = 'EID'                                                 
              MOVE 99                      TO   RETURN-CODE                     
              DISPLAY 'ERROR IN DATE : '        WS-IN-DATE                      
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13FC30                                                       
                 BP13KC35                                                       
                 BP13K452                                                       
                 BP13KC30.                                                      
                                                                                
           CALL 'CAV02001' USING LINK-REC.                                      
                                                                                
           IF WS-KC35-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC35 ERROR ' WS-KC35-STATUS                  
              MOVE     WS-KC35-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K452-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K452 ERROR ' WS-K452-STATUS                  
              MOVE     WS-K452-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-KC30-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC30 ERROR ' WS-KC30-STATUS                  
              MOVE     WS-KC30-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C45B CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13KC30 WRITTEN   : ' WS-CNT-KC30-WRT.           
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
