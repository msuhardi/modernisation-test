       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C037.                                                 
      *AUTHOR.        JIJO FRANCIS.                                             
      *DATE-WRITTEN.  17 DEC 2003.                                              
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE   : LETTER TO INFORM APPLICANT THAT HDB WILL    *            
      *                PROCEED WITH CONSTRUCTION OF BTO FLAT       *            
      *                                                            *            
      *  INPUT FILES : 1.  BP13F800                                *            
      *                2.  BP13K820                                *            
      *                3.  BP13K830                                *            
      *                4.  BP13K022                                *            
      *                5.  BP13K024                                *            
      *                                                            *            
      *  LISTINGS    : 1.  BP13P037  - LETTER                      *            
      *                2.  BP13L037  - CONTROL LIST                *            
      *                                                            *            
      *  SUBROUNTINE : 1.  BP13C913 FOR BOOKED FLAT ADDRESS        *            
      *              : 2.  AV02COMM FOR POSTAL CODE BARCODE        *            
      *                                                            *            
      *  FORM ID: BP13TB                                           *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP132460 20031207 JF4   NEW PROGRAM                        *            
      * BP132524 20030505 SSS2  CATER FOR SENGKANG LETTER          *            
      * BP132642 20041208 SSS2  CATER FOR FERNVALE GROVE           *            
      * BP132999 20070208 SSS2  ADD 4 FIELDS IN RPT                *            
      * BP133024 20070308 SSS2  CATER FOR GEYLANG SERAI            *            
      * BP133342 20080625 LMS1  SEPARATE LETTERS FOR PROV CASES.   *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *-------------------------------------------------------------            
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            FILE STATUS   IS BP13K820-STATUS.                   
                                                                                
           SELECT BP13K830  ASSIGN        TO BP13K830                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K830-KEY-FLD                       
                            ALTERNATE KEY IS K830-NUM-REGN                      
                            FILE STATUS   IS BP13K830-STATUS.                   
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            FILE STATUS   IS BP13K022-STATUS.                   
                                                                                
           SELECT BP13K024  ASSIGN        TO BP13K024                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K024-KEY-FLD                       
                            FILE STATUS   IS BP13K024-STATUS.                   
                                                                                
           SELECT BP13P037  ASSIGN        TO BP13P037.                          
           SELECT P13P037A  ASSIGN        TO P13P037A.                          
           SELECT BP13L037  ASSIGN        TO BP13L037.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            RECORDING MODE  IS    F                                             
            LABEL RECORDS   ARE   STANDARD                                      
            BLOCK  CONTAINS    0  RECORDS                                       
            RECORD CONTAINS 2000  CHARACTERS.                                   
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS  400  CHARACTERS.                                   
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS  250  CHARACTERS.                                   
       COPY BP13K830.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS  100  CHARACTERS.                                   
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS  100  CHARACTERS.                                   
       COPY BP13K024.                                                           
                                                                                
       FD   BP13P037                                                            
            RECORDING MODE    IS  F                                             
            RECORD CONTAINS   90  CHARACTERS.                                   
       01   BP13P037-REC     PIC X(90).                                         
                                                                                
       FD   P13P037A                                                            
            RECORDING MODE    IS  F                                             
            RECORD CONTAINS   90  CHARACTERS.                                   
       01   P13P037A-REC     PIC X(90).                                         
                                                                                
       FD   BP13L037                                                            
            RECORDING MODE    IS  F                                             
            RECORD CONTAINS  132  CHARACTERS.                                   
       01   BP13L037-REC     PIC X(132).                                        
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS-VARS.                                                 
           05  BP13K820-STATUS           PIC 9(02)  VALUE ZEROES.               
           05  BP13K830-STATUS           PIC 9(02)  VALUE ZEROES.               
           05  BP13K022-STATUS           PIC 9(02)  VALUE ZEROES.               
           05  BP13K024-STATUS           PIC 9(02)  VALUE ZEROES.               
           05  BP13F800-STATUS           PIC X(01)  VALUE SPACE.                
               88  BP13F800-EOF          VALUE 'Y'.                             
                                                                                
       01  WS-DATE                       PIC X(8)   VALUE SPACES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800-READ-CNT          PIC 9(08)  VALUE ZEROES.               
           05  WS-NO-CONTACT-ADDR        PIC 9(08)  VALUE ZEROES.               
           05  WS-PIDB-NOT-CASES         PIC 9(08)  VALUE ZEROES.               
           05  WS-LETTER-PRINTED         PIC 9(08)  VALUE ZEROES.               
           05  WS-LETTER-PROV-PRINTED    PIC 9(08)  VALUE ZEROES.               
                                                                                
       01  WS-OTHER-VARS.                                                       
           05  WS-POSTAL-CODE            PIC X(06)  VALUE SPACES.               
           05  WS-NAME                   PIC X(66)  VALUE SPACES.               
           05  WS-MAX-LINES              PIC 9(02)  VALUE 55.                   
           05  WS-LINE-CNT               PIC 9(02)  VALUE 60.                   
           05  WS-PAGE-CNT               PIC 9(05)  VALUE ZEROES.               
           05  WS-SLNO                   PIC 9(06)  VALUE ZEROES.               
           05  WS-LETTER-TAG             PIC X(01)  VALUE SPACE.                
               88  PRINT-LETTER          VALUE 'Y'.                             
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR AV02C015                                                   
      *-------------------------------------------------------------            
        01  WS-LINK-AV02C015.                                                   
            05  AV02C015-IN-POSTCODE          PIC X(6).                         
            05  AV02C015-OUT-POSTCODE-CHKSUM  PIC X(9).                         
                                                                                
       COPY AV02COMM.                                                           
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR BP13C913                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH            PIC X(4).                              
           05 WS-LINK-NUM-ACC            PIC X(4).                              
           05 WS-LINK-NUM-CHK            PIC X(1).                              
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
      *-------------------------------------------------------------            
      *  LETTER                                                                 
      *-------------------------------------------------------------            
       01  P037-LETTER-PAGE-1.                                                  
           02  P037-LINE01.                                                     
               05 FILLER                 PIC X(72)  VALUE SPACES.               
               05 P037-REF-NO            PIC X(15)  VALUE SPACES.               
                                                                                
           02  P037-LINE02.                                                     
               05 FILLER                 PIC X(72)  VALUE SPACES.               
               05 P037-DATE              PIC X(10)  VALUE SPACES.               
                                                                                
           02  P037-LINE03.                                                     
               05 FILLER                 PIC X(11)  VALUE SPACES.               
               05 P037-NME-OCCP1         PIC X(66)  VALUE SPACES.               
                                                                                
           02  P037-LINE03B.                                                    
               05 FILLER                 PIC X(11)  VALUE SPACES.               
               05 P037-NME-OCCP2         PIC X(66)  VALUE SPACES.               
                                                                                
           02  P037-LINE04.                                                     
               05 FILLER                 PIC X(11)  VALUE SPACES.               
               05 P037-NUM-BLOCK         PIC X(11)  VALUE SPACES.               
               05 P037-HOUSE-LEVEL       PIC X(11)  VALUE SPACES.               
                                                                                
           02  P037-LINE04B.                                                    
               05 FILLER                 PIC X(11)  VALUE SPACES.               
               05 P037-STREET-NAME       PIC X(65)  VALUE SPACES.               
                                                                                
           02  P037-LINE04C.                                                    
               05 FILLER                 PIC X(11)  VALUE SPACES.               
               05 P037-NUM-POSTAL        PIC X(30)  VALUE SPACES.               
                                                                                
           02  P037-LINE05.                                                     
               05 FILLER                 PIC X(10)  VALUE SPACES.               
               05 P037-BAR-CODE          PIC X(09)  VALUE SPACES.               
                                                                                
           02  P037-LINE06.                                                     
               05 FILLER                 PIC X(11)  VALUE SPACES.               
               05 P037-UNIT-ADDRESS      PIC X(60)  VALUE SPACES.               
                                                                                
           02  P037-LINE07.                                                     
               05 FILLER                 PIC X(73)  VALUE SPACES.               
               05 P037-TEL-CSM           PIC X(08)  VALUE SPACES.               
                                                                                
           02  P037-LINE08.                                                     
               05 FILLER                 PIC X(16)  VALUE SPACES.               
               05 P037-MOBILE-CSM        PIC X(08)  VALUE SPACES.               
                                                                                
           02  P037-LINE09.                                                     
               05 FILLER                 PIC X(10)  VALUE SPACES.               
               05 P037-NME-CSM           PIC X(30)  VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      * REPORT LAYOUT                                                           
      *-------------------------------------------------------------            
       01  L037-HEADER-1.                                                       
           05 FILLER                     PIC X(08)  VALUE 'BP13L037'.           
           05 FILLER                     PIC X(47)  VALUE SPACES.               
           05 FILLER                     PIC X(20)  VALUE                       
              'SYSTEM OF COMMITMENT'.                                           
           05 FILLER                     PIC X(38)  VALUE SPACES.               
           05 FILLER                     PIC X(6)   VALUE 'DATE: '.             
           05 L037-DATE                  PIC X(10)  VALUE SPACES.               
                                                                                
       01  L037-HEADER-2.                                                       
           05 FILLER                     PIC X(48)  VALUE SPACES.               
           05 FILLER                     PIC X(40)  VALUE                       
              'PROCEED TO BUILD BTO FLAT LETTER PRINTED'.                       
           05 FILLER                     PIC X(25)  VALUE SPACES.               
           05 FILLER                     PIC X(6)   VALUE 'PAGE: '.             
           05 L037-PAGE                  PIC Z(4)9.                             
                                                                                
       01  L037-HEADER-3.                                                       
           05 FILLER                     PIC X(41)  VALUE                       
              ' S/NO   REGN      NT/FT   NRIC/NAME 1 - 4'.                      
           05 FILLER                     PIC X(42)  VALUE SPACES.               
           05 FILLER                     PIC X(45)  VALUE                       
              'LOAN TAG  OIC    BK FEE PAID  PROV TAG/REASON'.                  
                                                                                
       01  L037-HEADER-4.                                                       
           05 FILLER                     PIC X(132) VALUE ALL '-'.              
                                                                                
       01  L037-DETAIL-LINES.                                                   
           02  L037-DETAIL-1.                                                   
               05 L037-SLNO              PIC Z(05)9.                            
               05 FILLER                 PIC X(2)   VALUE SPACES.               
               05 L037-NUM-REGN          PIC X(8)   VALUE SPACES.               
               05 FILLER                 PIC X(2)   VALUE SPACES.               
               05 L037-NTFT              PIC X(6)   VALUE SPACES.               
               05 FILLER                 PIC X(2)   VALUE SPACES.               
               05 L037-NUM-NRIC1         PIC X(9)   VALUE SPACES.               
               05 FILLER                 PIC X(2)   VALUE SPACES.               
               05 L037-NME-OCCP1         PIC X(45)  VALUE SPACES.               
               05 FILLER                 PIC X(4)   VALUE SPACES.               
               05 L037-NUM-LOAN-TAG      PIC X(1)   VALUE SPACES.               
               05 FILLER                 PIC X(6)   VALUE SPACES.               
               05 L037-NUM-OIC           PIC X(5)   VALUE SPACES.               
               05 FILLER                 PIC X(2)   VALUE SPACES.               
               05 L037-AMT-BK-PAID       PIC Z(5).99  VALUE ZEROES.             
               05 FILLER                 PIC X(11)  VALUE SPACES.               
               05 L037-NUM-PROV-TAG      PIC X(1)   VALUE SPACE.                
               05 FILLER                 PIC X(3)   VALUE SPACES.               
               05 L037-NUM-PROV-REASON   PIC X(2)   VALUE SPACES.               
                                                                                
           02  L037-DETAIL-2.                                                   
               05 FILLER                 PIC X(26)  VALUE SPACES.               
               05 L037-NUM-NRIC2         PIC X(09)  VALUE SPACES.               
               05 FILLER                 PIC X(02)  VALUE SPACES.               
               05 L037-NME-OCCP2         PIC X(45)  VALUE SPACES.               
                                                                                
           02  L037-DETAIL-3.                                                   
               05 FILLER                 PIC X(26)  VALUE SPACES.               
               05 L037-NUM-NRIC3         PIC X(09)  VALUE SPACES.               
               05 FILLER                 PIC X(02)  VALUE SPACES.               
               05 L037-NME-OCCP3         PIC X(45)  VALUE SPACES.               
                                                                                
           02  L037-DETAIL-4.                                                   
               05 FILLER                 PIC X(26)  VALUE SPACES.               
               05 L037-NUM-NRIC4         PIC X(09)  VALUE SPACES.               
               05 FILLER                 PIC X(02)  VALUE SPACES.               
               05 L037-NME-OCCP4         PIC X(45)  VALUE SPACES.               
                                                                                
           02  L037-DETAIL-5.                                                   
               05 FILLER                 PIC X(08)  VALUE SPACES.               
               05 FILLER                 PIC X(07)  VALUE 'MAIL:'.              
               05 L037-MAIL-ADDRESS      PIC X(110) VALUE SPACES.               
                                                                                
           02  L037-DETAIL-6.                                                   
               05 FILLER                 PIC X(8)   VALUE SPACES.               
               05 FILLER                 PIC X(07)  VALUE 'FLAT:'.              
               05 L037-FLAT-ADDRESS      PIC X(110) VALUE SPACES.               
                                                                                
           02  L037-FOOTER-1.                                                   
               05 FILLER                 PIC X(25)  VALUE SPACES.               
               05 FILLER                 PIC X(34)  VALUE                       
                  'TOTAL NUMBER OF BTO CASES READ  : '.                         
               05 L037-TOTAL-CASES       PIC Z(7)9.                             
                                                                                
           02  L037-FOOTER-2.                                                   
               05 FILLER                 PIC X(25)  VALUE SPACES.               
               05 FILLER                 PIC X(34)  VALUE                       
                  'CASES NOT FOUND IN PIDB         : '.                         
               05 L037-PIDB-NOT-CASES    PIC Z(7)9.                             
                                                                                
           02  L037-FOOTER-3.                                                   
               05 FILLER                 PIC X(25)  VALUE SPACES.               
               05 FILLER                 PIC X(34)  VALUE                       
                  'CASES WITH NO CONTACT ADDRESS   : '.                         
               05 L037-NO-CONTACT-ADDR   PIC Z(7)9.                             
                                                                                
           02  L037-FOOTER-4.                                                   
               05 FILLER                 PIC X(25)  VALUE SPACES.               
               05 FILLER                 PIC X(34)  VALUE                       
                  'TOTAL NUMBER OF LETTERS PRINTED : '.                         
               05 L037-TOTAL-PRINTED     PIC Z(7)9.                             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------*           
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------*           
            PERFORM 1000-OPEN-ROUTINE         THRU  1000-EXIT.                  
            PERFORM 2000-READ-BP13F800        THRU  2000-EXIT.                  
            PERFORM 3000-PROCESS-PARA         THRU  3000-EXIT                   
                         UNTIL BP13F800-EOF.                                    
            PERFORM 8800-PRINT-CONTROL        THRU  8800-EXIT.                  
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------*           
            OPEN  INPUT  BP13F800  BP13K820  BP13K830                           
                         BP13K022  BP13K024                                     
                 OUTPUT  BP13P037  P13P037A  BP13L037.                          
                                                                                
           IF BP13K820-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'BP13K820 OPEN ERROR, STATUS ' BP13K820-STATUS            
              MOVE     BP13K820-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE                                       
           END-IF.                                                              
                                                                                
           IF BP13K830-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'BP13K830 OPEN ERROR, STATUS ' BP13K830-STATUS            
              MOVE     BP13K830-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE                                       
           END-IF.                                                              
                                                                                
           IF BP13K022-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'BP13K022 OPEN ERROR, STATUS ' BP13K022-STATUS            
              MOVE     BP13K022-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE                                       
           END-IF.                                                              
                                                                                
           IF BP13K024-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'BP13K024 OPEN ERROR, STATUS ' BP13K024-STATUS            
              MOVE     BP13K024-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE                                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING  WS-DATE(7:2)       '/'                                       
                   WS-DATE(5:2)       '/'                                       
                   WS-DATE(1:4)                                                 
                   DELIMITED BY SIZE INTO L037-DATE.                            
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------*           
           READ BP13F800  AT END                                                
                           MOVE 'Y' TO BP13F800-STATUS                          
                          NOT AT END                                            
                           ADD  1   TO WS-F800-READ-CNT                         
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       3000-PROCESS-PARA.                                                       
      *-------------------------------------------------------------*           
           MOVE SPACES TO BP13P037-REC                                          
                          P13P037A-REC.                                         
                                                                                
           INITIALIZE   P037-LETTER-PAGE-1                                      
                        L037-DETAIL-LINES.                                      
                                                                                
           MOVE   'Y'    TO WS-LETTER-TAG.                                      
           MOVE L037-DATE TO P037-DATE.                                         
                                                                                
           PERFORM 4000-GET-OCCP-NAME     THRU 4000-EXIT.                       
           PERFORM 5000-GET-MAIL-ADDR     THRU 5000-EXIT.                       
           PERFORM 6000-GET-FLAT-ADDR     THRU 6000-EXIT.                       
           PERFORM 6500-GET-OIC-DETAILS   THRU 6500-EXIT.                       
                                                                                
           IF PRINT-LETTER                                                      
              IF F800-NUM-PROV-TAG = 'Y'                                        
                 PERFORM 7500-PRINT-LETTER-PROV THRU 7500-EXIT                  
              ELSE                                                              
                 PERFORM 7000-PRINT-LETTER      THRU 7000-EXIT                  
              END-IF                                                            
              PERFORM 8000-PRINT-LISTING  THRU 8000-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F800        THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       4000-GET-OCCP-NAME.                                                      
      *-------------------------------------------------------------*           
           STRING  F800-NUM-NEW-TOWN  '/'                                       
                   F800-NUM-ROOM      '/'                                       
                   F800-NUM-REGN                                                
                   DELIMITED BY SIZE INTO P037-REF-NO.                          
           STRING  F800-NUM-NEW-TOWN  '/'                                       
                   F800-NUM-ROOM                                                
                   DELIMITED BY SIZE INTO L037-NTFT.                            
                                                                                
      ** NAME OF OCCUPIER 1 **                                                  
      ** ------------------ **                                                  
           MOVE F800-NUM-REGN           TO K820-NUM-REGN                        
                                           L037-NUM-REGN.                       
                                                                                
           MOVE F800-NUM-NRIC1          TO K820-NUM-NRIC                        
                                           L037-NUM-NRIC1.                      
           PERFORM 4100-READ-BP13K820   THRU 4100-EXIT.                         
           MOVE WS-NAME                 TO P037-NME-OCCP1                       
                                           L037-NME-OCCP1.                      
                                                                                
      ** NAME OF OCCUPIER 2 **                                                  
      ** ------------------ **                                                  
           IF F800-NUM-NRIC2  NOT =  SPACES AND LOW-VALUES                      
              MOVE     F800-NUM-NRIC2       TO K820-NUM-NRIC                    
                                               L037-NUM-NRIC2                   
              PERFORM  4100-READ-BP13K820   THRU 4100-EXIT                      
              MOVE     WS-NAME              TO P037-NME-OCCP2                   
                                               L037-NME-OCCP2                   
           END-IF.                                                              
                                                                                
      ** NAME OF OCCUPIER 3 **                                                  
      ** ------------------ **                                                  
           IF F800-NUM-NRIC3  NOT =  SPACES AND LOW-VALUES                      
              MOVE     F800-NUM-NRIC3       TO K820-NUM-NRIC                    
                                               L037-NUM-NRIC3                   
              PERFORM  4100-READ-BP13K820   THRU 4100-EXIT                      
              MOVE     WS-NAME              TO L037-NME-OCCP3                   
           END-IF.                                                              
                                                                                
      ** NAME OF OCCUPIER 4 **                                                  
      ** ------------------ **                                                  
           IF F800-NUM-NRIC4  NOT =  SPACES AND LOW-VALUES                      
              MOVE     F800-NUM-NRIC4       TO K820-NUM-NRIC                    
                                               L037-NUM-NRIC4                   
              PERFORM  4100-READ-BP13K820   THRU 4100-EXIT                      
              MOVE     WS-NAME              TO L037-NME-OCCP4                   
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       4100-READ-BP13K820.                                                      
      *-------------------------------------------------------------*           
           READ  BP13K820.                                                      
                                                                                
           EVALUATE BP13K820-STATUS                                             
             WHEN 00                                                            
                MOVE    K820-NME-OCCP       TO WS-NAME                          
             WHEN 23                                                            
                MOVE    SPACES              TO WS-NAME                          
                DISPLAY 'REC NOT FOUND IN BP13K820'                             
                      ', K820-KEY-FLD : ' K820-KEY-FLD                          
             WHEN OTHER                                                         
                DISPLAY 'BP13K820 READ ERROR,STATUS : ' BP13K820-STATUS         
                      ', K820-KEY-FLD : ' K820-KEY-FLD                          
                MOVE BP13K820-STATUS   TO RETURN-CODE                           
                PERFORM 9000-CLOSE-ROUTINE                                      
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       5000-GET-MAIL-ADDR.                                                      
      *-------------------------------------------------------------*           
           MOVE    SPACES                TO WS-POSTAL-CODE.                     
           MOVE F800-NUM-REGN            TO K830-NUM-REGN.                      
           PERFORM 5100-READ-BP13K830    THRU 5100-EXIT.                        
                                                                                
           IF BP13K830-STATUS = 00                                              
              IF K830-NUM-MAIL-ADDR-TYPE  NOT =  SPACES AND LOW-VALUES          
                 PERFORM 5200-GET-MAILING-ADDR   THRU 5200-EXIT                 
              ELSE                                                              
                 PERFORM 5400-GET-HOUSE-ADDR     THRU 5400-EXIT                 
              END-IF                                                            
              IF WS-POSTAL-CODE  NOT =  SPACES AND LOW-VALUES                   
                 PERFORM 5800-GET-POSTAL-BARCODE    THRU 5800-EXIT              
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       5100-READ-BP13K830.                                                      
      *-------------------------------------------------------------*           
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           EVALUATE BP13K830-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
                CONTINUE                                                        
             WHEN 23                                                            
                MOVE   SPACES    TO WS-LETTER-TAG                               
                ADD      1       TO WS-NO-CONTACT-ADDR                          
                DISPLAY 'REC NOT FOUND IN BP13K830'                             
                      ', K830-NUM-REGN: ' K830-NUM-REGN                         
             WHEN OTHER                                                         
                DISPLAY 'BP13K830 READ ERROR,STATUS : ' BP13K830-STATUS         
                      ', K830-KEY-FLD : ' K830-NUM-REGN                         
                MOVE BP13K830-STATUS   TO RETURN-CODE                           
                PERFORM 9000-CLOSE-ROUTINE                                      
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       5200-GET-MAILING-ADDR.                                                   
      *-------------------------------------------------------------*           
           MOVE K830-NUM-MAIL-BLK(1:11) TO P037-NUM-BLOCK.                      
           MOVE K830-NUM-MAIL-FLAT      TO P037-HOUSE-LEVEL.                    
           MOVE K830-NME-MAIL-STR-BLDG  TO P037-STREET-NAME.                    
           IF K830-NUM-MAIL-POSTAL  =  SPACES OR ZEROS                          
              MOVE   SPACES   TO P037-NUM-POSTAL                                
           ELSE                                                                 
              MOVE   K830-NUM-MAIL-POSTAL  TO WS-POSTAL-CODE                    
              STRING 'SINGAPORE('  K830-NUM-MAIL-POSTAL  ')'                    
                      DELIMITED BY SIZE  INTO P037-NUM-POSTAL                   
           END-IF.                                                              
                                                                                
           STRING  K830-NUM-MAIL-BLK(1:11)  ' '                                 
                   K830-NUM-MAIL-FLAT       ' ' DELIMITED BY SIZE               
                   K830-NME-MAIL-STR-BLDG       DELIMITED BY '   '              
                   '  SINGAPORE('  K830-NUM-MAIL-POSTAL  ')'                    
                   DELIMITED BY SIZE  INTO L037-MAIL-ADDRESS.                   
                                                                                
       5200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       5400-GET-HOUSE-ADDR.                                                     
      *-------------------------------------------------------------*           
           MOVE K830-NUM-BLK(1:11)      TO P037-NUM-BLOCK.                      
           MOVE K830-NUM-FLAT           TO P037-HOUSE-LEVEL.                    
           MOVE K830-NME-STREET-BLDG    TO P037-STREET-NAME.                    
           IF K830-NUM-POSTAL  =  SPACES OR ZEROS                               
              MOVE   SPACES   TO P037-NUM-POSTAL                                
           ELSE                                                                 
              MOVE   K830-NUM-POSTAL       TO WS-POSTAL-CODE                    
              STRING 'SINGAPORE('  K830-NUM-POSTAL  ')'                         
                      DELIMITED BY SIZE  INTO P037-NUM-POSTAL                   
           END-IF.                                                              
                                                                                
           STRING  K830-NUM-BLK(1:11)  ' '                                      
                   K830-NUM-FLAT       ' ' DELIMITED BY SIZE                    
                   K830-NME-STREET-BLDG    DELIMITED BY '   '                   
                   '  SINGAPORE('  K830-NUM-POSTAL  ')'                         
                   DELIMITED BY SIZE  INTO L037-MAIL-ADDRESS.                   
                                                                                
       5400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       5800-GET-POSTAL-BARCODE.                                                 
      *-------------------------------------------------------------*           
           MOVE SPACES                 TO WS-LINK-AV02C015.                     
           MOVE WS-POSTAL-CODE         TO AV02C015-IN-POSTCODE.                 
           CALL 'AV02C015'   USING                                              
                             AV02C015-IN-POSTCODE                               
                             AV02C015-OUT-POSTCODE-CHKSUM.                      
           MOVE AV02C015-OUT-POSTCODE-CHKSUM   TO P037-BAR-CODE.                
                                                                                
       5800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       6000-GET-FLAT-ADDR.                                                      
      *-------------------------------------------------------------*           
           MOVE    SPACES             TO BP13COMM8-REC.                         
           MOVE F800-NUM-SCH-ACC(1:9) TO WS-LINK-REC.                           
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
             WHEN 000                                                           
                STRING 'BLK '  COMM8-NUM-BLK  '  '                              
                       '#'  COMM8-NUM-LEVEL   '-'                               
                       COMM8-NUM-UNIT-MAIN    '  ' DELIMITED BY SIZE            
                       COMM8-NME-STREET       DELIMITED BY '   '                
                       INTO P037-UNIT-ADDRESS                                   
                STRING COMM8-NUM-BLK  '  '                                      
                       '#'  COMM8-NUM-LEVEL   '-'                               
                       COMM8-NUM-UNIT-MAIN    '  ' DELIMITED BY SIZE            
                       COMM8-NME-STREET       DELIMITED BY '   '                
                       INTO L037-FLAT-ADDRESS                                   
             WHEN 100                                                           
                MOVE  SPACES    TO WS-LETTER-TAG                                
                ADD     1       TO WS-PIDB-NOT-CASES                            
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                     
                         F800-NUM-SCH-ACC ', REGN : ' F800-NUM-REGN             
             WHEN OTHER                                                         
                MOVE  SPACES    TO WS-LETTER-TAG                                
                ADD     1       TO WS-PIDB-NOT-CASES                            
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       6500-GET-OIC-DETAILS.                                                    
      *-------------------------------------------------------------*           
           MOVE F800-NUM-REGN         TO K022-NUM-REGN.                         
           PERFORM 6600-READ-BP13K022       THRU 6600-EXIT.                     
                                                                                
           IF BP13K022-STATUS  =  00                                            
              MOVE K022-NUM-OIC       TO K024-NUM-OIC                           
                                         L037-NUM-OIC                           
              PERFORM 6700-READ-BP13K024    THRU 6700-EXIT                      
                                                                                
              IF BP13K024-STATUS  =  00                                         
                 MOVE K024-NME-OIC     TO P037-NME-CSM                          
                 MOVE K024-NUM-DID     TO P037-TEL-CSM                          
                 MOVE K024-NUM-HP      TO P037-MOBILE-CSM                       
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       6600-READ-BP13K022.                                                      
      *-------------------------------------------------------------*           
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE BP13K022-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
                CONTINUE                                                        
             WHEN 23                                                            
                DISPLAY 'REC NOT FOUND IN BP13K022'                             
                      ', K022-NUM-REGN: ' K022-NUM-REGN                         
             WHEN OTHER                                                         
                DISPLAY 'BP13K022 READ ERROR,STATUS : ' BP13K022-STATUS         
                      ', K022-NUM-REGN: ' K022-NUM-REGN                         
                MOVE BP13K022-STATUS   TO RETURN-CODE                           
                PERFORM 9000-CLOSE-ROUTINE                                      
           END-EVALUATE.                                                        
                                                                                
       6600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       6700-READ-BP13K024.                                                      
      *-------------------------------------------------------------*           
           READ BP13K024.                                                       
                                                                                
           EVALUATE BP13K024-STATUS                                             
             WHEN 00                                                            
                CONTINUE                                                        
             WHEN 23                                                            
                DISPLAY 'REC NOT FOUND IN BP13K024'                             
                      ', K024-NUM-OIC: ' K024-NUM-OIC                           
             WHEN OTHER                                                         
                DISPLAY 'BP13K024 READ ERROR,STATUS : ' BP13K024-STATUS         
                      ', K024-NUM-OIC: ' K024-NUM-OIC                           
                MOVE BP13K024-STATUS   TO RETURN-CODE                           
                PERFORM 9000-CLOSE-ROUTINE                                      
           END-EVALUATE.                                                        
                                                                                
       6700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       7000-PRINT-LETTER.                                                       
      *-------------------------------------------------------------*           
      *----------------------*                                                  
      *  COVER LETTER        *                                                  
      *----------------------*                                                  
           MOVE      SPACES         TO BP13P037-REC.                            
           WRITE BP13P037-REC       AFTER PAGE.                                 
           WRITE BP13P037-REC       FROM P037-LINE01.                           
           WRITE BP13P037-REC       FROM P037-LINE02.                           
                                                                                
           WRITE BP13P037-REC       FROM P037-LINE03 AFTER 5.                   
           WRITE BP13P037-REC       FROM P037-LINE03B.                          
           WRITE BP13P037-REC       FROM P037-LINE04.                           
           WRITE BP13P037-REC       FROM P037-LINE04B.                          
           WRITE BP13P037-REC       FROM P037-LINE04C.                          
           WRITE BP13P037-REC       FROM P037-LINE05 AFTER 3.                   
                                                                                
           WRITE BP13P037-REC       FROM P037-LINE06 AFTER 5.                   
           WRITE BP13P037-REC       FROM P037-LINE07 AFTER 12.                  
           WRITE BP13P037-REC       FROM P037-LINE08.                           
           WRITE BP13P037-REC       FROM P037-LINE09 AFTER 10.                  
                                                                                
           ADD 1 TO WS-LETTER-PRINTED.                                          
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       7500-PRINT-LETTER-PROV.                                                  
      *-------------------------------------------------------------*           
      *----------------------*                                                  
      *  COVER LETTER        *                                                  
      *----------------------*                                                  
           MOVE      SPACES         TO P13P037A-REC.                            
           WRITE P13P037A-REC       AFTER PAGE.                                 
           WRITE P13P037A-REC       FROM P037-LINE01.                           
           WRITE P13P037A-REC       FROM P037-LINE02.                           
                                                                                
           WRITE P13P037A-REC       FROM P037-LINE03 AFTER 5.                   
           WRITE P13P037A-REC       FROM P037-LINE03B.                          
           WRITE P13P037A-REC       FROM P037-LINE04.                           
           WRITE P13P037A-REC       FROM P037-LINE04B.                          
           WRITE P13P037A-REC       FROM P037-LINE04C.                          
           WRITE P13P037A-REC       FROM P037-LINE05 AFTER 3.                   
                                                                                
           WRITE P13P037A-REC       FROM P037-LINE06 AFTER 5.                   
           WRITE P13P037A-REC       FROM P037-LINE07 AFTER 12.                  
           WRITE P13P037A-REC       FROM P037-LINE08.                           
           WRITE P13P037A-REC       FROM P037-LINE09 AFTER 10.                  
                                                                                
           ADD 1 TO WS-LETTER-PROV-PRINTED.                                     
                                                                                
       7500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       8000-PRINT-LISTING.                                                      
      *-------------------------------------------------------------*           
           IF WS-LINE-CNT  >  WS-MAX-LINES                                      
              PERFORM 8500-PRINT-HEADER  THRU 8500-EXIT                         
           END-IF.                                                              
                                                                                
           ADD     1        TO WS-SLNO.                                         
           MOVE  WS-SLNO    TO L037-SLNO.                                       
                                                                                
           MOVE F800-NUM-LOAN-TAG     TO L037-NUM-LOAN-TAG.                     
           MOVE F800-AMT-BK-PAID      TO L037-AMT-BK-PAID.                      
           MOVE F800-NUM-PROV-TAG     TO L037-NUM-PROV-TAG.                     
           MOVE F800-NUM-PROV-REASON  TO L037-NUM-PROV-REASON.                  
                                                                                
           WRITE BP13L037-REC       FROM L037-DETAIL-1 AFTER 2.                 
           ADD   2          TO WS-LINE-CNT.                                     
                                                                                
           IF L037-NUM-NRIC2  NOT =  SPACES                                     
              WRITE BP13L037-REC    FROM L037-DETAIL-2                          
              ADD  1  TO WS-LINE-CNT                                            
           END-IF.                                                              
                                                                                
           IF L037-NUM-NRIC3  NOT =  SPACES                                     
              WRITE BP13L037-REC    FROM L037-DETAIL-3                          
              ADD  1  TO WS-LINE-CNT                                            
           END-IF.                                                              
                                                                                
           IF L037-NUM-NRIC4  NOT =  SPACES                                     
              WRITE BP13L037-REC    FROM L037-DETAIL-4                          
              ADD  1  TO WS-LINE-CNT                                            
           END-IF.                                                              
                                                                                
           WRITE BP13L037-REC       FROM L037-DETAIL-5.                         
           WRITE BP13L037-REC       FROM L037-DETAIL-6.                         
           ADD   2    TO WS-LINE-CNT.                                           
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       8500-PRINT-HEADER.                                                       
      *-------------------------------------------------------------*           
           ADD      1                 TO WS-PAGE-CNT.                           
           MOVE    WS-PAGE-CNT        TO L037-PAGE.                             
                                                                                
           WRITE BP13L037-REC       FROM L037-HEADER-1 AFTER PAGE.              
           WRITE BP13L037-REC       FROM L037-HEADER-2.                         
           WRITE BP13L037-REC       FROM L037-HEADER-3 AFTER 2.                 
           WRITE BP13L037-REC       FROM L037-HEADER-4.                         
                                                                                
           MOVE  5          TO WS-LINE-CNT.                                     
                                                                                
       8500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       8800-PRINT-CONTROL.                                                      
      *-------------------------------------------------------------*           
           IF WS-LINE-CNT  >  (WS-MAX-LINES - 9)                                
              ADD      1                 TO WS-PAGE-CNT                         
              MOVE    WS-PAGE-CNT        TO L037-PAGE                           
              WRITE BP13L037-REC       FROM L037-HEADER-1 AFTER PAGE            
              WRITE BP13L037-REC       FROM L037-HEADER-2                       
              WRITE BP13L037-REC       FROM L037-HEADER-4 AFTER 2               
           END-IF.                                                              
                                                                                
           MOVE WS-F800-READ-CNT      TO L037-TOTAL-CASES.                      
           MOVE WS-PIDB-NOT-CASES     TO L037-PIDB-NOT-CASES.                   
           MOVE WS-NO-CONTACT-ADDR    TO L037-NO-CONTACT-ADDR.                  
           COMPUTE L037-TOTAL-PRINTED =                                         
                   WS-LETTER-PRINTED + WS-LETTER-PROV-PRINTED.                  
           WRITE BP13L037-REC       FROM L037-FOOTER-1 AFTER 3.                 
           WRITE BP13L037-REC       FROM L037-FOOTER-2.                         
           WRITE BP13L037-REC       FROM L037-FOOTER-3.                         
           WRITE BP13L037-REC       FROM L037-FOOTER-4.                         
                                                                                
       8800-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------*           
            CLOSE  BP13F800  BP13K820  BP13K830                                 
                   BP13K022  BP13K024                                           
                   BP13P037  P13P037A  BP13L037.                                
                                                                                
           IF BP13K820-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'BP13K820 CLOSE ERROR, STATUS ' BP13K820-STATUS           
              MOVE     BP13K820-STATUS  TO RETURN-CODE                          
           END-IF.                                                              
                                                                                
           IF BP13K830-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'BP13K830 CLOSE ERROR, STATUS ' BP13K830-STATUS           
              MOVE     BP13K830-STATUS  TO RETURN-CODE                          
           END-IF.                                                              
                                                                                
           IF BP13K022-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'BP13K022 CLOSE ERROR, STATUS ' BP13K022-STATUS           
              MOVE     BP13K022-STATUS  TO RETURN-CODE                          
           END-IF.                                                              
                                                                                
           IF BP13K024-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'BP13K024 CLOSE ERROR, STATUS ' BP13K024-STATUS           
              MOVE     BP13K024-STATUS  TO RETURN-CODE                          
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
