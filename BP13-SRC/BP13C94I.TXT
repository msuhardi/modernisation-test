       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C94I.                                                  
      *DATE-WRITTEN. 17 OCT 2017.                                               
      *AUTHOR.       KRISTINE LANTIN.                                           
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) TO CREATE INTRAY RECORD FOR TEMP LOAN CASES *            
      *                ALREADY KEY ISSUED                          *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP136986 20171017 KVL1  NEW PROGRAM                        *            
      * BP138427 20200831 KVC1  TO CREATE INTRAY TO CRM POINTING TO*            
      *                         RESALE PORTAL                      *            
      * BP139497 20230329 KVC1  TO DISPLAY SALES REGN AND RESALE   *            
      *                         REFERENCE NUMBER                   *            
      * BP139497 20230330 KAC1  TO DISPLAY INPUT PASS TO BP13CCNT  *            
      *                         CHANGE BP13CCNT TO DYNAMIC CALL    *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F48K  ASSIGN        TO BP13F48K.                          
           SELECT BB24K010  ASSIGN        TO BB24K010                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K010-KEY-FLD                       
                            FILE STATUS   IS BB24K010-STATUS.                   
            SELECT BB24K050 ASSIGN        TO BB24K050                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K050-KEY-FLD                       
                            ALTERNATE KEY IS K050-ALT-KEY                       
                            FILE STATUS   IS BB24K050-STATUS.                   
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F48K                                                             
           RECORD CONTAINS 1200 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BP13F48K.                                                           
                                                                                
       FD  BB24K010                                                             
           RECORD CONTAINS 5000 CHARACTERS.                                     
       COPY BB24K010.                                                           
                                                                                
       FD  BB24K050                                                             
           RECORD CONTAINS 250  CHARACTERS.                                     
       COPY BB24K050.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-READ-CNT                 PIC 9(06)  VALUE ZEROES.                 
       01  WS-WRITE-CNT                PIC 9(06)  VALUE ZEROES.                 
       01  BB24K010-STATUS             PIC 9(02)  VALUE ZEROES.                 
       01  BB24K050-STATUS             PIC 9(02)  VALUE ZEROES.                 
       01  WS-OIC-EXIST                PIC X      VALUE 'N'.                    
       01  WS-BP13F48K-EOF             PIC X      VALUE 'N'.                    
       01  WS-BB24K010-FND             PIC X      VALUE 'N'.                    
       01  WS-BB24K050-FND             PIC X      VALUE 'N'.                    
       01  WS-07-DAYS                  PIC 9(01)  VALUE 7.                      
       01  WS-STAFF                    PIC X(06)  VALUE SPACES.                 
       01  WS-BP13CCNT                 PIC X(08)  VALUE 'BP13CCNT'.             
       01  WS-CURRENT-DTE-TME.                                                  
           05 WS-CURRENT-DTE           PIC 9(08)  VALUE ZEROES.                 
           05 WS-CURRENT-TME           PIC 9(06)  VALUE ZEROES.                 
       01  WS-INT-CURR                 PIC 9(08)  VALUE ZEROES.                 
       01  WS-INT-DUE                  PIC 9(08)  VALUE ZEROES.                 
       01  WS-DUE-DTE                  PIC 9(08)  VALUE ZEROES.                 
       01  WS-DTE-CURRENT.                                                      
           05 WS-DTE-CURR-CCYY         PIC X(04).                               
           05 FILLER                   PIC X(01)  VALUE '-'.                    
           05 WS-DTE-CURR-MM           PIC X(02).                               
           05 FILLER                   PIC X(01)  VALUE '-'.                    
           05 WS-DTE-CURR-DD           PIC X(02).                               
       01  WS-TME-CURRENT.                                                      
           05 WS-TME-CURR-HH           PIC X(02).                               
           05 FILLER                   PIC X(01)  VALUE '.'.                    
           05 WS-TME-CURR-MM           PIC X(02).                               
           05 FILLER                   PIC X(01)  VALUE '.'.                    
           05 WS-TME-CURR-SS           PIC X(02).                               
       01  WS-DTE-DUE.                                                          
           05 WS-DTE-CCYY              PIC X(04).                               
           05 FILLER                   PIC X(01)  VALUE '-'.                    
           05 WS-DTE-MM                PIC X(02).                               
           05 FILLER                   PIC X(01)  VALUE '-'.                    
           05 WS-DTE-DD                PIC X(02).                               
       01  WS-INTRAY-CONSTANTS.                                                 
           05  WS-TXN-CREATE           PIC X(02)  VALUE 'C'.                    
           05  WS-SYSTEM-ID            PIC X(04)  VALUE 'BP13'.                 
           05  WS-NUM-PRCS-CTGRY       PIC X(04)  VALUE '0076'.                 
           05  WS-NUM-BRNCH            PIC X(06)  VALUE '000001'.               
           05  WS-DEFAULT-DTE          PIC X(10)  VALUE '0001-01-01'.           
           05  WS-PROGRAM-ID           PIC X(08)  VALUE 'BP13C94I'.             
                                                                                
      ******************************************************************        
      *  TEXT VARIABLES                                                         
      ******************************************************************        
       01  WS-INBOX-TXT-TARGET-URL.                                             
           05  FILLER              PIC X(64) VALUE                              
             '/be10/home/resale/registration/newFlatContra/'.                   
      *      '/be10/home/Newsales/adminAgt/agtContraTempLoan/resaleAck?r        
      *      'egnNo='.                                                          
           05  WS-CASE-REF-URL     PIC X(08).                                   
                                                                                
       01  WS-INBOX-TXT-REMARK.                                                 
           05  FILLER              PIC X(09) VALUE                              
             'Case Num '.                                                       
           05  WS-CASE-NUM-OUT     PIC X(08) VALUE SPACES.                      
           05  FILLER              PIC X(40) VALUE                              
             ' Cash New Flat Contra/Temp Loan Updated.'.                        
                                                                                
      ******************************************************************        
      *        LINKAGE FOR SUB-PROGRAM BP13CCNT                        *        
      ******************************************************************        
       01  WS-BP13CCNT-LINK.                                                    
           05  LINK-CNTCT-TYPE         PIC X(3).                                
           05  LINK-STAFF-NICK         PIC X(6).                                
           05  LINK-STAFF-NUM          PIC X(6).                                
           05  LINK-STAFF-CNTCT        PIC X(50).                               
           05  LINK-SYSERR             PIC 9(4).                                
                                                                                
                                                                                
       COPY BE08W008.                                                           
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F48K       THRU 2000-EXIT.                     
           IF WS-BP13F48K-EOF = 'Y'                                             
              DISPLAY 'FILE BP13F48K IS  EMPTY'                                 
           ELSE                                                                 
              PERFORM 1100-GET-DATE-TME     THRU 1100-EXIT                      
              PERFORM 3000-PROCESS-DATA     THRU 3000-EXIT                      
                UNTIL WS-BP13F48K-EOF = 'Y'                                     
           END-IF                                                               
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F48K                                                 
                       BB24K010                                                 
                       BB24K050.                                                
                                                                                
           IF BB24K050-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BB24K050 OPEN ERROR, STATUS : ' BB24K050-STATUS          
              MOVE     BB24K050-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE   THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF BB24K010-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BB24K010 OPEN ERROR, STATUS : ' BB24K010-STATUS          
              MOVE     BB24K010-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE   THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1100-GET-DATE-TME.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:14) TO WS-CURRENT-DTE-TME.              
           MOVE WS-CURRENT-DTE(1:4)         TO WS-DTE-CURR-CCYY.                
           MOVE WS-CURRENT-DTE(5:2)         TO WS-DTE-CURR-MM.                  
           MOVE WS-CURRENT-DTE(7:2)         TO WS-DTE-CURR-DD.                  
           MOVE WS-CURRENT-TME(1:2)         TO WS-TME-CURR-HH.                  
           MOVE WS-CURRENT-TME(3:2)         TO WS-TME-CURR-MM.                  
           MOVE WS-CURRENT-TME(5:2)         TO WS-TME-CURR-SS.                  
                                                                                
           COMPUTE WS-INT-CURR =                                                
                   FUNCTION INTEGER-OF-DATE (WS-CURRENT-DTE)                    
           COMPUTE WS-INT-DUE  =  WS-INT-CURR + WS-07-DAYS.                     
           COMPUTE WS-DUE-DTE  =                                                
                   FUNCTION DATE-OF-INTEGER (WS-INT-DUE).                       
                                                                                
           MOVE WS-DUE-DTE(1:4)    TO  WS-DTE-CCYY.                             
           MOVE WS-DUE-DTE(5:2)    TO  WS-DTE-MM.                               
           MOVE WS-DUE-DTE(7:2)    TO  WS-DTE-DD.                               
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F48K.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F48K AT END                                                 
                         MOVE 'Y'  TO WS-BP13F48K-EOF                           
                         NOT AT END                                             
                         ADD  1    TO WS-READ-CNT                               
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 3200-INSERT-INBOX-INTRY THRU 3200-EXIT.                      
           PERFORM 2000-READ-BP13F48K      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-READ-BB24K010.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BB24K010.                                                       
                                                                                
           EVALUATE BB24K010-STATUS                                             
              WHEN 00                                                           
                 MOVE 'Y'                TO WS-BB24K010-FND                     
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 MOVE 'N'                TO WS-BB24K010-FND                     
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BB24K010. STATUS IS '                   
                            BB24K010-STATUS                                     
                 MOVE BB24K010-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3150-READ-BB24K050.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BB24K050 KEY IS K050-ALT-KEY.                                   
                                                                                
           EVALUATE BB24K050-STATUS                                             
              WHEN 00                                                           
                 MOVE 'Y'                TO WS-BB24K050-FND                     
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 MOVE 'N'                TO WS-BB24K050-FND                     
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BB24K050. STATUS IS '                   
                            BB24K050-STATUS                                     
                 MOVE BB24K050-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-INSERT-INBOX-INTRY.                                                 
      *-------------------------------------------------------------            
           MOVE SPACES                      TO  BE08W008-COMMAREA.              
           INITIALIZE                           BE08W008-COMMAREA.              
           MOVE F48K-NUM-REGN               TO  WS-CASE-REF-URL.                
           PERFORM 3210-GET-OIC           THRU  3210-EXIT.                      
                                                                                
           MOVE WS-TXN-CREATE               TO  W008-IN-TXN-TYPE.               
           MOVE WS-SYSTEM-ID                TO  W008-IN-SYSTEM-ID.              
           MOVE WS-NUM-PRCS-CTGRY           TO  W008-IN-NUM-PRCS-CTGRY.         
           MOVE WS-NUM-BRNCH                TO  W008-IN-NUM-BRNCH.              
           MOVE F48K-NUM-REGN               TO  W008-IN-NUM-CASE-REF.           
           MOVE F48K-NUM-HDB-REF            TO  W008-IN-HDBREF.                 
           MOVE F48K-NUM-CASE               TO  WS-CASE-NUM-OUT.                
           MOVE WS-INBOX-TXT-REMARK         TO  W008-IN-TXT-REMARK.             
           MOVE WS-STAFF                    TO  W008-IN-NUM-STAFF.              
           MOVE WS-INBOX-TXT-TARGET-URL     TO  W008-IN-TXT-TARGET-URL.         
           MOVE WS-DTE-CURRENT              TO  W008-IN-DTE-INBOX.              
           MOVE WS-TME-CURRENT              TO  W008-IN-TME-INBOX.              
           MOVE WS-DTE-DUE                  TO  W008-IN-DUE-DATE.               
                                                                                
           CALL 'BE08C008'     USING BE08W008-COMMAREA.                         
           IF W008-OUT-RTN-MSGCDE = '0' OR 'W'                                  
              ADD 1                         TO  WS-WRITE-CNT                    
           ELSE                                                                 
              DISPLAY 'ERROR RETURNED CALLING BE08C008 SUBR (C) : '             
                                          W008-IN-NUM-CASE-REF                  
              DISPLAY '{ ' W008-OUT-RTN-MSGCDE ' }'                             
              DISPLAY '{ ' W008-OUT-RTN-MSGNO  ' }'                             
              DISPLAY '{ ' W008-OUT-RTN-DESC   ' }'                             
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3210-GET-OIC.                                                            
      *-------------------------------------------------------------            
                                                                                
            MOVE SPACES                        TO K010-KEY-FLD.                 
            MOVE F48K-NUM-CASE                 TO K010-NUM-CASE.                
            PERFORM 3100-READ-BB24K010       THRU 3100-EXIT.                    
            IF WS-BB24K010-FND = 'Y'                                            
               MOVE SPACES                     TO K050-ALT-KEY                  
               MOVE K010-CDE-INIT-OIC1         TO K050-ALT-KEY                  
               PERFORM 3150-READ-BB24K050    THRU 3150-EXIT                     
               IF WS-BB24K050-FND  = 'Y'                                        
                  MOVE SPACES                  TO WS-BP13CCNT-LINK              
                  MOVE 'EML'                   TO LINK-CNTCT-TYPE               
                  MOVE K050-NME-LOTUS-NOTES    TO LINK-STAFF-NICK               
                  MOVE SPACES                  TO LINK-STAFF-NUM                
                  PERFORM 3220-CALL-BP13CCNT THRU 3220-EXIT                     
                  MOVE LINK-STAFF-NUM          TO WS-STAFF                      
               ELSE                                                             
                  DISPLAY 'BB24K050 OIC NOT FOUND FOR CASE NUM '                
                           F48K-NUM-REGN                                        
                           F48K-NUM-CASE                                        
                  MOVE     99                  TO RETURN-CODE                   
                  PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                     
               END-IF                                                           
            ELSE                                                                
               DISPLAY 'CASE NOT FOUND FOUND IN BB24K010'                       
                           F48K-NUM-REGN                                        
                           F48K-NUM-CASE                                        
               MOVE     99                     TO RETURN-CODE                   
               PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                     
            END-IF.                                                             
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3220-CALL-BP13CCNT.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY 'WS-BP13CCNT-LINK = ' WS-BP13CCNT-LINK.                      
                                                                                
           CALL WS-BP13CCNT  USING WS-BP13CCNT-LINK.                            
                                                                                
           EVALUATE LINK-SYSERR                                                 
           WHEN ZEROS                                                           
              CONTINUE                                                          
           WHEN 100                                                             
              DISPLAY 'OIC NOT FOUND IN OFFICE_CNTCT TABLE - '                  
                  K050-NME-LOTUS-NOTES                                          
                  ', CASE NUM : ' F48K-NUM-CASE                                 
              MOVE     23                  TO  RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING OFFICE_CNTCT TABLE = '                     
                       LINK-SYSERR                                              
              MOVE     99                  TO  RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       3220-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '---- BP13C94I CONTROL TOTAL -----'.                         
           DISPLAY ' NO OF RECS READ FROM BP13F48K   = '                        
                     WS-READ-CNT.                                               
           DISPLAY ' NO OF CREATED INTRAY RECS       = '                        
                     WS-WRITE-CNT.                                              
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13F48K                                                 
                       BB24K010                                                 
                       BB24K050.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
