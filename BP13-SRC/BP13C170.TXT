       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C170.                                                    
       AUTHOR. YCH2                                                             
       DATE-WRITTEN : 07/11/97                                                  
                                                                                
      *----------------------------------------------------------------*        
      *                                                                *        
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                            *        
      *                                                                *        
      *  SYSTEM ID   : BP13                                            *        
      *                                                                *        
      *  OBJECTIVE   : THIS PROGRAM OUTPUT DIRECT PURCHANSE FLAT       *        
      *                                                                *        
      * CHGE ID  OIC  DATE   DESCRIPTION                               *        
      * -------  ---  ----   -----------                               *        
      *          YCH2        PARENT AND CHILD                          *        
      *BP130445  LSB  980216 - EXPNAD F558 TO X(400)                   *        
      *BP130630  NOR  990204 - CHGE F715 F716 EXPAND TO 350            *        
      *BP130897  AKT  000731 - EXPNAD F558 TO X(600)                   *        
      *----------------------------------------------------------------*        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716 ASSIGN TO BP13F716.                                  
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT P13F716A ASSIGN TO P13F716A.                                  
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F558                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F558.                                                           
                                                                                
       FD   BP13F716                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 350 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F716.                                                           
                                                                                
       FD   P13F716A                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 350 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01   P13F716A-REC           PIC X(350).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-F558-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F716-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-MATCH                PIC 9(8)    VALUE ZERO.                      
       01  WS-F716-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-F558-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-F716-STORED          PIC 9(8)    VALUE ZERO.                      
                                                                                
       77  WS-EOF-F716             PIC X(3).                                    
           88  EOF-OF-F716                     VALUE 'YES'.                     
       77  WS-EOF-F558             PIC X(3).                                    
           88  EOF-OF-F558                     VALUE 'YES'.                     
                                                                                
       01  WS-F558-KEY-FLD.                                                     
           05  WS-F558-REGN        PIC X(8)    VALUE SPACES.                    
           05  WS-F558-SCHACC      PIC X(9)    VALUE SPACES.                    
                                                                                
       01  WS-F716-KEY-FLD.                                                     
           05  WS-F716-REGN        PIC X(8)    VALUE SPACES.                    
           05  WS-F716-SCHACC      PIC X(9)    VALUE SPACES.                    
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *--------------*                                                          
       000-MAIN-LOGIC.                                                          
      *--------------*                                                          
                                                                                
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
                                                                                
           PERFORM 200-READ-F716      THRU 200-EXIT.                            
                                                                                
           PERFORM 300-READ-F558      THRU 300-EXIT.                            
                                                                                
           PERFORM 400-PROCESS        THRU 400-EXIT                             
                   UNTIL WS-F716-KEY-FLD = HIGH-VALUE                           
                    AND  WS-F558-KEY-FLD = HIGH-VALUE.                          
                                                                                
           PERFORM 900-FINALIZATION   THRU 900-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       100-INITIALIZATION.                                                      
      *------------------*                                                      
                                                                                
           OPEN INPUT  BP13F716                                                 
                       BP13F558                                                 
                OUTPUT P13F716A.                                                
                                                                                
           MOVE 'NO' TO WS-EOF-F716.                                            
           MOVE 'NO' TO WS-EOF-F558.                                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------*                                                           
       200-READ-F716.                                                           
      *-------------*                                                           
                                                                                
           READ BP13F716 AT END                                                 
                MOVE 'YES'      TO WS-EOF-F716                                  
                MOVE HIGH-VALUES TO WS-F716-KEY-FLD                             
                GO TO 200-EXIT                                                  
           END-READ.                                                            
                                                                                
            ADD 1 TO WS-F716-READ.                                              
            MOVE F716-NUM-REGN  TO WS-F716-REGN.                                
            MOVE F716-NUM-SCH-ACCNT  TO WS-F716-SCHACC.                         
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------*                                                           
       300-READ-F558.                                                           
      *-------------*                                                           
                                                                                
           READ BP13F558 AT END                                                 
                MOVE 'YES'      TO WS-EOF-F558                                  
                MOVE HIGH-VALUES TO WS-F558-KEY-FLD                             
                GO TO 300-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F558-READ.                                               
            MOVE F558-NUM-REGN  TO WS-F558-REGN.                                
            MOVE F558-NUM-HDB-REF-SMS (1:9) TO WS-F558-SCHACC.                  
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *  MATCH  BY REGNNO                                             *         
      *****************************************************************         
                                                                                
      *-----------*                                                             
       400-PROCESS.                                                             
      *-----------*                                                             
                                                                                
           IF WS-F716-KEY-FLD = WS-F558-KEY-FLD                                 
              ADD 1                    TO WS-MATCH                              
              PERFORM 450-CHECK-FILE-STATUS THRU 450-EXIT                       
                UNTIL WS-F716-KEY-FLD NOT = WS-F558-KEY-FLD                     
                 OR  WS-F716-KEY-FLD = HIGH-VALUES                              
              PERFORM 300-READ-F558 THRU 300-EXIT                               
           ELSE                                                                 
               IF WS-F716-KEY-FLD  < WS-F558-KEY-FLD                            
                   ADD 1 TO WS-F716-NOTMATCH                                    
                   PERFORM 200-READ-F716 THRU 200-EXIT                          
               ELSE                                                             
                   ADD 1 TO WS-F558-NOTMATCH                                    
                   PERFORM 300-READ-F558 THRU 300-EXIT                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       450-CHECK-FILE-STATUS.                                                   
           IF F558-CDE-FILE-STATUS-SMS  NOT = 'OM'                              
              WRITE P13F716A-REC FROM BP13F716-REC                              
              ADD  1                   TO WS-F716-STORED                        
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F716 THRU 200-EXIT.                                 
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       900-FINALIZATION.                                                        
      *----------------*                                                        
                                                                                
             COMPUTE WS-F716-NOTMATCH = WS-F716-READ - WS-MATCH.                
                                                                                
             DISPLAY '*--------BP13C626  -----------*'.                         
             DISPLAY '                               '.                         
             DISPLAY '* MATCH BP13F716 WITH BP13F558,   '.                      
             DISPLAY '* TO GET MTH-RESIDE BASED ON SMS'.                        
             DISPLAY '                               '.                         
             DISPLAY ' BP13F716-READ      ' WS-F716-READ.                       
             DISPLAY ' BP13F558-READ      ' WS-F558-READ.                       
             DISPLAY ' NO OF F716 MATCHED ' WS-MATCH.                           
             DISPLAY ' F716-NOT-MATCH     ' WS-F716-NOTMATCH.                   
             DISPLAY ' F558-NOT-MATCH     ' WS-F558-NOTMATCH.                   
             DISPLAY ' RECORDS WRITTEN    ' WS-F716-STORED.                     
                                                                                
           CLOSE      BP13F716                                                  
                      BP13F558                                                  
                      P13F716A.                                                 
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
